---
layout: default
title : Introduction to Univalent Foundations of Mathematics with Agda
date : 2019-03-04
---
## <a name="lecturenotes">Introduction to Univalent Foundations of Mathematics with Agda</a>

<!--
\begin{code}
{-# OPTIONS --without-K --exact-split --safe #-}

module Inhabitation where

open import Universes
open import MLTT-Agda
open import HoTT-UF-Agda
open import FunExt
\end{code}
-->

[<sub>Table of contents â‡‘</sub>](toc.html#contents)
### <a name="truncation"></a> Subsingleton truncation

The following is Voevosky's approach to saying that a type is
inhabited in such a way that the statement of inhabitation is a
subsingleton, using funext.

\begin{code}
is-inhabited : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
is-inhabited {ğ“¤} X = (P : ğ“¤ Ì‡ ) â†’ is-subsingleton P â†’ (X â†’ P) â†’ P
\end{code}

This says that if we have a function from `X` to a subsingleton `P`, then
`P` must have a point. So this fails when `X=ğŸ˜`. Considering `P=ğŸ˜`, we conclude
that `Â¬Â¬ X` if `X` is inhabited, which says that `X` is non-empty. However,
in the absence of excluded middle, [inhabitation is stronger than
double negation](https://lmcs.episciences.org/3217).

For simplicity in the formulation of the theorems, we assume global
`dfunext`.

\begin{code}
global-dfunext : ğ“¤Ï‰
global-dfunext = âˆ€ ğ“¤ ğ“¥ â†’ dfunext ğ“¤ ğ“¥

inhabitation-is-a-subsingleton : global-dfunext â†’ (X : ğ“¤ Ì‡ ) â†’ is-subsingleton (is-inhabited X)
inhabitation-is-a-subsingleton {ğ“¤} fe X =
  Î -is-subsingleton (fe (ğ“¤ âº) ğ“¤)
    Î» P â†’ Î -is-subsingleton (fe ğ“¤ ğ“¤)
           (Î» (s : is-subsingleton P)
                 â†’ Î -is-subsingleton (fe ğ“¤ ğ“¤) (Î» (f : X â†’ P) â†’ s))

pointed-is-inhabited : {X : ğ“¤ Ì‡ } â†’ X â†’ is-inhabited X
pointed-is-inhabited x = Î» P s f â†’ f x

inhabited-recursion : (X P : ğ“¤ Ì‡ ) â†’ is-subsingleton P â†’ (X â†’ P) â†’ is-inhabited X â†’ P
inhabited-recursion X P s f Ï† = Ï† P s f
\end{code}

Although we [don't necessarily have](Appendix.html#moreexercices) that
`Â¬Â¬ P â†’ P`, we do have that `is-inhabited P â†’ P`:

\begin{code}
inhabited-gives-pointed-for-subsingletons : (P : ğ“¤ Ì‡ ) â†’ is-subsingleton P â†’ is-inhabited P â†’ P
inhabited-gives-pointed-for-subsingletons P s = inhabited-recursion P P s id

inhabited-functorial : global-dfunext â†’ (X : ğ“¤ âº Ì‡ ) (Y : ğ“¤ Ì‡ )
                     â†’ (X â†’ Y) â†’ is-inhabited X â†’ is-inhabited Y
inhabited-functorial fe X Y f = inhabited-recursion
                                  X
                                  (is-inhabited Y)
                                  (inhabitation-is-a-subsingleton fe Y)
                                  (pointed-is-inhabited âˆ˜ f)

\end{code}

This universe assignment for functoriality is fairly restrictive, but is the only possible one.

With this notion, we can define the image of a function as follows:

\begin{code}
âˆƒ : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
âˆƒ {ğ“¤} {ğ“¥} {X} A = is-inhabited (Î£ \(x : X) â†’ A x)

image : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
image f = Î£ \(y : codomain f) â†’ âˆƒ \(x : domain f) â†’ f x â‰¡ y
\end{code}

*Exercise.* An attempt to define the image of `f` without the
inhabitation predicate would be to take it to be
`Î£ \(y : codomain f) â†’ Î£ \(x : domain f) â†’ f x â‰¡ y`. Show that this
type is equivalent to `X`. This is similar to what happens in set
theory: the graph of any function is isomorphic to its domain.


We can define the restriction and corestriction of a function to its
image as follows:

\begin{code}
restriction : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
            â†’ image f â†’ Y
restriction f (y , _) = y

corestriction : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
              â†’ X â†’ image f
corestriction f x = f x , pointed-is-inhabited (x , refl (f x))
\end{code}

And we can define the notion of surjection as follows:
\begin{code}
is-surjection : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
is-surjection f = (y : codomain f) â†’ âˆƒ \(x : domain f) â†’ f x â‰¡ y
\end{code}

*Exercise.* The type `(y : codomain f) â†’ Î£ \(x : domain f) â†’ f x â‰¡ y`
 is equivalent to the type `has-section f`, which is stronger than
 saying that `f` is a surjection.

There are two problems with this definition of inhabitation:

  * Inhabitation has values in the next universe.

  * We can eliminate into propositions of the same universe only.

In particular, it is not possible to show that the map `X â†’
is-inhabited X` is a surjection, or that `X â†’ Y` gives `is-inhabited X
â†’ is-inhabited Y`.

There are two proposed ways to solve this:

  * Voevodsky works with certain [resizing
    rules](http://www.math.ias.edu/vladimir/files/2011_Bergen.pdf) for
    subsingletons. At the time of writing, the (relative) consistency
    of the system with such rules is an open question.

  * The HoTT book works with certain higher inductive types (HIT's).
    This is the same approach of taken by cubical Agda.

A third possibility is to work with propositional truncations
[axiomatially](https://lmcs.episciences.org/3217), which is compatible
with the above two proposals. We write this axiom as a record type
rather than as an iterated `Î£-type` for simplicity, where we use the
HoTT-book notation `âˆ¥ X âˆ¥` for the inhabitation of `X`,
called the propositional truncation of `X`:

\begin{code}
record propositional-truncations-exist : ğ“¤Ï‰ where
 field
  âˆ¥_âˆ¥ : {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
  âˆ¥âˆ¥-is-a-prop : {ğ“¤ : Universe} {X : ğ“¤ Ì‡ } â†’ is-prop âˆ¥ X âˆ¥
  âˆ£_âˆ£ : {ğ“¤ : Universe} {X : ğ“¤ Ì‡ } â†’ X â†’ âˆ¥ X âˆ¥
  âˆ¥âˆ¥-rec : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } {P : ğ“¥ Ì‡ } â†’ is-prop P â†’ (X â†’ P) â†’ âˆ¥ X âˆ¥ â†’ P
\end{code}

This is the approach we adopt in our [personal Agda
development](http://www.cs.bham.ac.uk/~mhe/agda-new/).

*Exercise*. If `X` and `Y` are types obtained by summing `x-` and
  `y`-many copies of the type `ğŸ™`, respectively, as in `ğŸ™ + ğŸ™ + ... + ğŸ™` , where `x`
  and `y` are natural numbers, then `âˆ¥ X = Y âˆ¥ = (x â‰¡ y)` and the type
  `X â‰¡ X` has `x!` elements.

[<sub>Table of contents â‡‘</sub>](toc.html#contents)
### <a name="choice"></a> The univalent axiom of choice

For the moment see [this](http://www.cs.bham.ac.uk/~mhe/agda-new/UF-Choice.html).

[<sub>Table of contents â‡‘</sub>](toc.html#contents)
### <a name="sip"></a> Structure of identity principle

For the moment, see [this](http://www.cs.bham.ac.uk/~mhe/agda-new/UF-StructureIdentityPrinciple.html).

[<sub>Table of contents â‡‘</sub>](toc.html#contents) [<sub>Appendix â‡“ </sub>](Appendix.html)
