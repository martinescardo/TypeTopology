--------------------------------------------------------------------------------
Ettore Aldrovandi, ealdrovandi@fsu.edu

Started: January 2023
Revision: June 2023
--------------------------------------------------------------------------------

Port of [HoTT-Agda](https://github.com/HoTT/HoTT-Agda) `PathSeq`
library to TypeTopology.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import MLTT.Spartan
open import UF.Base
open import UF.Equiv
open import PathSequences.Type
open import PathSequences.Concat
open import PathSequences.Split

module PathSequences.Reasoning where

\end{code}


\begin{code}

infix 30 _ï¼â†“_
_ï¼â†“_ : {X : ğ“¤ Ì‡ } {x y : X} â†’ x â‰¡ y â†’ x â‰¡ y â†’ ğ“¤ Ì‡
s ï¼â†“ t = [ s â†“] ï¼ [ t â†“]


module _ {X : ğ“¤ Ì‡ } {x y : X} where

 ï¼-ï¼â‚›-equiv : (s t : x â‰¡ y) â†’ (s ï¼â†“ t) â‰ƒ (s ï¼â‚› t)
 ï¼-ï¼â‚›-equiv s t = ï¼â‚›-in , (ï¼â‚›-out , Î» _ â†’ refl) , (ï¼â‚›-out , Î» _ â†’ refl)

\end{code}

  TODO: there is a function

  ï¼â‚›-level : {s t : x â‰¡ y} {n : â„•} â†’ X is-of-level n â†’ (s ï¼â‚› t) is-of-level (n - 2)

  which awaits implementation once I figure out how to deal with the
  global univalence in UF.HLlevels.

\begin{code}

 _â»Â¹â‚› : {s t : x â‰¡ y} â†’ s ï¼â‚› t â†’ t ï¼â‚› s
 ï¼â‚›-in p â»Â¹â‚› = ï¼â‚›-in (p â»Â¹)

 _âˆ™â‚›_ : {s t u : x â‰¡ y} â†’ s ï¼â‚› t â†’ t ï¼â‚› u â†’ s ï¼â‚› u
 ï¼â‚›-in p âˆ™â‚› ï¼â‚›-in q = ï¼â‚›-in (p âˆ™ q)

 expand : (s : x â‰¡ y) â†’ [ s â†“] â—ƒâˆ ï¼â‚› s
 expand s = ï¼â‚›-in refl

 contract : {s : x â‰¡ y} â†’ s ï¼â‚› [ s â†“] â—ƒâˆ
 contract = ï¼â‚›-in refl

\end{code}

The first is a utility only used in the latter reasoning items.

\begin{code}

 opaque
  private
   infixr 10 _ï¼â†“ï¼âŸ¨_&_&_&_âŸ©_
   _ï¼â†“ï¼âŸ¨_&_&_&_âŸ©_ : {q : x ï¼ y}
                  â†’ (s : x â‰¡ y)
                  â†’ (n : â„•) (m : â„•)
                  â†’ (t : point-from-start n s â‰¡ point-from-start m (drop n s))
                  â†’ take m (drop n s) ï¼â†“ t
                  â†’ [ take n s âˆ™â‰¡ t âˆ™â‰¡ drop m (drop n s) â†“] ï¼ q
                  â†’ [ s â†“] ï¼ q
   _ï¼â†“ï¼âŸ¨_&_&_&_âŸ©_ {q} s n m t p p' =
                 [ s â†“]
                   ï¼âŸ¨ ï¼â‚›-out (take-drop-split n s) âŸ©
                 [ take n s â†“] âˆ™ [ drop n s â†“]
                   ï¼âŸ¨ ap ([ take n s â†“] âˆ™_) (ï¼â‚›-out (take-drop-split m (drop n s))) âŸ©
                 [ take n s â†“] âˆ™ ([ take m (drop n s) â†“] âˆ™ [ drop m (drop n s) â†“])
                   ï¼âŸ¨ ap (Î» v â†’ [ take n s â†“] âˆ™ (v âˆ™ [ drop m (drop n s) â†“])) p âŸ©
                 [ take n s â†“] âˆ™ ([ t â†“] âˆ™ [ drop m (drop n s) â†“])
                   ï¼âŸ¨ ap ([ take n s â†“] âˆ™_) ([â†“]-hom t (drop m (drop n s))) âŸ©
                 [ take n s â†“] âˆ™ [ t âˆ™â‰¡ drop m (drop n s) â†“]
                   ï¼âŸ¨ [â†“]-hom (take n s) (t âˆ™â‰¡ drop m (drop n s)) âŸ©
                 [ take n s âˆ™â‰¡ t âˆ™â‰¡ drop m (drop n s) â†“]
                   ï¼âŸ¨ p' âŸ©
                 q âˆ
\end{code}

The following makes definitional equalities visible, for example:

 p â—ƒâˆ™ ap f refl â—ƒâˆ™ q â—ƒâˆ ï¼â‚›âŸ¨idâŸ© 
 p â—ƒâˆ™ refl â—ƒâˆ™ q â—ƒâˆ      âˆâ‚›

\begin{code}

 _ï¼â‚›âŸ¨idâŸ©_ : (s : x â‰¡ y) {u : x â‰¡ y}
         â†’ s ï¼â‚› u
         â†’ s ï¼â‚› u
 s ï¼â‚›âŸ¨idâŸ© e = e  -- ï¼â‚›-in (ï¼â‚›-out e)

\end{code}

 The following rewrites an entire expression using a _ï¼â‚›_ path
 between path sequences. The usage is like:

 s ï¼â‚›âŸ¨ p âŸ©
 q âˆâ‚›

 or something like:

 s ï¼â‚›âŸ¨ p âŸ©
 t ï¼â‚›âŸ¨ q âŸ©
 r âˆâ‚›

 where s, t, u, v (the latter three implicit) are in x â‰¡ y, and p, q,
 r are paths like:

 p : s ï¼â‚› t, q : t ï¼â‚› u, r : u ï¼â‚› v

\begin{code}

 _ï¼â‚›âŸ¨_âŸ©_ : (s : x â‰¡ y) {t u : x â‰¡ y}
        â†’ s ï¼â‚› t
        â†’ t ï¼â‚› u
        â†’ s ï¼â‚› u
 s ï¼â‚›âŸ¨ p âŸ© q = p âˆ™â‚› q

\end{code}

 The following rewrites a segment of a path sequences using a _ï¼â‚›_
 path. For example, if we have path sequences

 p â—ƒâˆ™ (q âˆ™ r) â»Â¹ â—ƒâˆ™ s â—ƒâˆ

 and

 p â—ƒâˆ™ r â»Â¹ â—ƒâˆ™ q â»Â¹ â—ƒâˆ™ s â—ƒâˆ

 and let's say 

 Î± : (q âˆ™ r) â»Â¹ â—ƒâˆ ï¼â‚› r â»Â¹ â—ƒâˆ™ q â»Â¹ â—ƒâˆ

 then we write

 p â—ƒâˆ™ (q âˆ™ r) â»Â¹ â—ƒâˆ™ s â—ƒâˆ     ï¼â‚›âŸ¨ 1 & 1 & Î± âŸ©
 p â—ƒâˆ™ r â»Â¹ â—ƒâˆ™ q â»Â¹ â—ƒâˆ™ s â—ƒâˆ   âˆâ‚›

\begin{code}

 _ï¼â‚›âŸ¨_&_&_âŸ©_ : (s : x â‰¡ y) {u : x â‰¡ y}
             â†’ (m n : â„•)
             â†’ {r : point-from-start m s â‰¡ point-from-start n (drop m s)}
             â†’ take n (drop m s) ï¼â‚› r
             â†’ take m s âˆ™â‰¡ r âˆ™â‰¡ drop n (drop m s) ï¼â‚› u
             â†’ s ï¼â‚› u
 _ï¼â‚›âŸ¨_&_&_âŸ©_ s m n {r} p q = ï¼â‚›-in (s ï¼â†“ï¼âŸ¨ m & n & r & ï¼â‚›-out p âŸ© ï¼â‚›-out q)

\end{code}

 The following rewrites and entire expression using an equality (the
 usual ï¼) 

 We write:

 s ï¼â†“âŸ¨ p âŸ©
 q âˆâ‚›

\begin{code}

 _ï¼â†“âŸ¨_âŸ©_ : (s : x â‰¡ y) {u : x â‰¡ y}
           {r : x ï¼ y }
         â†’ [ s â†“] ï¼ r
         â†’ r â—ƒâˆ ï¼â‚› u
         â†’ s ï¼â‚› u
 s ï¼â†“âŸ¨ p âŸ© q = ï¼â‚›-in p âˆ™â‚› q

\end{code}

 The following rewrites a segment of a path sequence using a standard
 equality ï¼

 For example, if we have something like

 p â—ƒâˆ™ (ap f q) â»Â¹ â—ƒâˆ™ s â—ƒâˆ

 and

 p â—ƒâˆ™ (ap f q â»Â¹) â—ƒâˆ™ s â—ƒâˆ

 and let's say 

 Î± : (ap f q) â»Â¹ â—ƒâˆ ï¼ ap f q â»Â¹ 

 then we write

 p â—ƒâˆ™ (ap f q) â»Â¹ â—ƒâˆ™ s â—ƒâˆ  ï¼â†“âŸ¨ Î± âŸ©
 p â—ƒâˆ™ ap f q â»Â¹ â—ƒâˆ™ s â—ƒâˆ    âˆâ‚›

\begin{code}

 _ï¼â†“âŸ¨_&_&_âŸ©_ : (s : x â‰¡ y) {u : x â‰¡ y}
             â†’ (m n : â„•)
             â†’ {r : point-from-start m s ï¼ point-from-start n (drop m s)}
             â†’ [ take n (drop m s) â†“] ï¼ r
             â†’ take m s âˆ™â‰¡ r â—ƒâˆ™ drop n (drop m s) ï¼â‚› u
             â†’ s ï¼â‚› u
 _ï¼â†“âŸ¨_&_&_âŸ©_ s m n {r} p q = s ï¼â‚›âŸ¨ m &  n &  ï¼â‚›-in {t = r â—ƒâˆ} p âŸ© q

\end{code}

 This closes.

\begin{code}
  
 _âˆâ‚› : (s : x â‰¡ y) â†’ s ï¼â‚› s
 _âˆâ‚› _ = ï¼â‚›-in refl

\end{code}

Fixities

\begin{code}

 infixr 10 _ï¼â‚›âŸ¨idâŸ©_
 infixr 10 _ï¼â‚›âŸ¨_âŸ©_
 infixr 10 _ï¼â‚›âŸ¨_&_&_âŸ©_
 infixr 10 _ï¼â†“âŸ¨_âŸ©_
 infixr 10 _ï¼â†“âŸ¨_&_&_âŸ©_
 infix 15 _âˆâ‚›

\end{code}
