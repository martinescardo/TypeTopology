\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module UF-ImageAndSurjection where

open import SpartanMLTT
open import UF-Base public
open import UF-Equiv
open import UF-Embedding
open import UF-PropTrunc
open import UF-Retracts

\end{code}

A main application of propositional truncations is to be able to
define images and surjections:

\begin{code}

module ImageAndSurjection (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 image : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 image f = Î£ \y â†’ âˆƒ \x â†’ f x â‰¡ y

 restriction : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
            â†’ image f â†’ Y
 restriction f (y , _) = y

 restriction-embedding : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
                      â†’ is-embedding(restriction f)
 restriction-embedding f = prâ‚-embedding (Î» y â†’ âˆ¥âˆ¥-is-a-prop)


 corestriction : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
             â†’ X â†’ image f
 corestriction f x = f x , âˆ£ x , refl âˆ£

\end{code}

TODO: a map is an embedding iff its corestriction is an equivalence.

\begin{code}

 is-surjection : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-surjection f = âˆ€ y â†’ âˆƒ \x â†’ f x â‰¡ y

 c-es  :  {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
          â†’ is-vv-equiv f â‡” is-embedding f Ã— is-surjection f
 c-es f = g , h
  where
   g : is-vv-equiv f â†’ is-embedding f Ã— is-surjection f
   g i = (Î» y â†’ prâ‚(prâ‚ c-esâ‚ (i y))) , (Î» y â†’ prâ‚‚(prâ‚ c-esâ‚ (i y)))

   h : is-embedding f Ã— is-surjection f â†’ is-vv-equiv f
   h (e , s) = Î» y â†’ prâ‚‚ c-esâ‚ (e y , s y)

 corestriction-surjection : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
                         â†’ is-surjection (corestriction f)
 corestriction-surjection f (y , s) = âˆ¥âˆ¥-funct g s
  where
   g : (Î£ \x â†’ f x â‰¡ y) â†’ Î£ \x â†’ corestriction f x â‰¡ y , s
   g (x , p) = x , to-Î£-â‰¡ (p , âˆ¥âˆ¥-is-a-prop _ _)

 pt-is-surjection : {X : ğ“¤ Ì‡} â†’ is-surjection(Î»(x : X) â†’ âˆ£ x âˆ£)
 pt-is-surjection t = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-a-prop (Î» x â†’ âˆ£ x , âˆ¥âˆ¥-is-a-prop (âˆ£ x âˆ£) t âˆ£) t

\end{code}

Surjections can be characterized as follows, modulo size:

\begin{code}

 imageInduction : âˆ€ {ğ“¦ ğ“¤ ğ“¥} {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦  âº Ì‡
 imageInduction {ğ“¦} {ğ“¤} {ğ“¥} {X} {Y} f =
                (P : Y â†’ ğ“¦ Ì‡) â†’ ((y : Y) â†’ is-prop(P y)) â†’ ((x : X) â†’ P(f x)) â†’ (y : Y) â†’ P y

 surjection-induction : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
                      â†’ is-surjection f â†’ imageInduction {ğ“¦} f
 surjection-induction f is P isp a y = âˆ¥âˆ¥-rec (isp y)
                                             (Î» Ïƒ â†’ transport P (prâ‚‚ Ïƒ) (a (prâ‚ Ïƒ)))
                                             (is y)

 image-surjection-converse : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
                           â†’ imageInduction f â†’ is-surjection f
 image-surjection-converse f is' = is' (Î» y â†’ âˆ¥ Î£ (Î» x â†’ f x â‰¡ y) âˆ¥)
                                       (Î» y â†’ âˆ¥âˆ¥-is-a-prop)
                                       (Î» x â†’ âˆ£ x , refl âˆ£)

 image-induction : âˆ€ {ğ“¦} {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡}
                 (f : X â†’ Y) (P : image f â†’ ğ“¦ Ì‡)
               â†’ (âˆ€ y' â†’ is-prop(P y'))
               â†’ (âˆ€ x â†’ P(corestriction f x))
               â†’ âˆ€ y' â†’ P y'
 image-induction f = surjection-induction (corestriction f)
                                          (corestriction-surjection f)

 retraction-surjection : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y)
                       â†’ has-section f â†’ is-surjection f
 retraction-surjection {ğ“¤} {ğ“¥} {X} f Ï† y = âˆ£ prâ‚ Ï† y , prâ‚‚ Ï† y âˆ£

\end{code}
