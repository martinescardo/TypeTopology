This is an adaptation of WithoutOracle where we're using SystemT instead of CombinatryT.

Alternatively, it can be seen as adaptation of LambdaCalculusVersionOfMFPS, where we use
a slighlty different relation instead of using T'

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness --auto-inline #-}

module EffectfulForcing.WithoutOracleLambda where

open import MLTT.Spartan hiding (rec ; _^_) renaming (â‹† to âŒ©âŒª)
open import MLTT.Athenian using (Fin)
open import UF.Base
open import EffectfulForcing.Combinators
open import EffectfulForcing.Continuity
open import EffectfulForcing.Dialogue
open import EffectfulForcing.SystemT

open Fin

Bã€–_ã€— : type â†’ ğ“¤â‚€ Ì‡
Bã€– Î¹ ã€—     = B â„•
Bã€– Ïƒ â‡’ Ï„ ã€— = Bã€– Ïƒ ã€— â†’ Bã€– Ï„ ã€—

Kleisli-extension : {X : ğ“¤â‚€ Ì‡ } {Ïƒ : type} â†’ (X â†’ Bã€– Ïƒ ã€—) â†’ B X â†’ Bã€– Ïƒ ã€—
Kleisli-extension {X} {Î¹}     = kleisli-extension
Kleisli-extension {X} {Ïƒ â‡’ Ï„} = Î» g d s â†’ Kleisli-extension {X} {Ï„} (Î» x â†’ g x s) d

zero' : B â„•
zero' = Î· zero

succ' : B â„• â†’ B â„•
succ' = B-functor succ

rec' : {Ïƒ : type} â†’ (B â„• â†’ Bã€– Ïƒ ã€— â†’ Bã€– Ïƒ ã€—) â†’ Bã€– Ïƒ ã€— â†’ B â„• â†’ Bã€– Ïƒ ã€—
rec' f x = Kleisli-extension(rec (f âˆ˜ Î·) x)


Bã€_ã€‘ : {n : â„•} (Î“ : Cxt n) â†’ Type
Bã€ Î“ ã€‘ = (i : Fin _) â†’ Bã€– (Î“ [ i ]) ã€—

âŸªâŸ« : Bã€ âŒ©âŒª ã€‘
âŸªâŸ« ()

_â€šâ€š_ : {n : â„•} {Î“ : Cxt n} {Ïƒ : type} â†’ Bã€ Î“ ã€‘ â†’ Bã€– Ïƒ ã€— â†’ Bã€ Î“ , Ïƒ ã€‘
(xs â€šâ€š x) ğŸ       = x
(xs â€šâ€š x) (suc i) = xs i

infixl 6 _â€šâ€š_

BâŸ¦_âŸ§ : {n : â„•} {Î“ : Cxt n} {Ïƒ : type} â†’ T Î“ Ïƒ â†’ Bã€ Î“ ã€‘ â†’ Bã€– Ïƒ ã€—
BâŸ¦ Zero  âŸ§  _ = zero'
BâŸ¦ Succ  âŸ§  _ = succ'
BâŸ¦ Rec   âŸ§  _ = rec'
BâŸ¦ Î½ i   âŸ§ xs = xs i
BâŸ¦ Æ› t   âŸ§ xs = Î» x â†’ BâŸ¦ t âŸ§ (xs â€šâ€š x)
BâŸ¦ t Â· u âŸ§ xs = (BâŸ¦ t âŸ§ xs) (BâŸ¦ u âŸ§ xs)

BâŸ¦_âŸ§â‚€ : {Ïƒ : type} â†’ Tâ‚€ Ïƒ â†’ Bã€– Ïƒ ã€—
BâŸ¦ t âŸ§â‚€ = BâŸ¦ t âŸ§ âŸªâŸ«

dialogue-tree : Tâ‚€((Î¹ â‡’ Î¹) â‡’ Î¹) â†’ B â„•
dialogue-tree t = BâŸ¦ t âŸ§â‚€ generic

R : {Ïƒ : type} â†’ Baire â†’ ã€– Ïƒ ã€— â†’ Bã€– Ïƒ ã€— â†’ Set
R {Î¹}     Î± n d  = n ï¼ dialogue d Î±
R {Ïƒ â‡’ Ï„} Î± f f' = (x  : ã€– Ïƒ ã€—)
                   (x' : Bã€– Ïƒ ã€—)
                 â†’ R {Ïƒ} Î± x x'
                 â†’ R {Ï„} Î± (f x) (f' x')

R-kleisli-lemma : (Ïƒ : type)
                  (Î± : Baire)
                  (g  : â„• â†’ ã€– Ïƒ ã€—)
                  (g' : â„• â†’ Bã€– Ïƒ ã€—)
                â†’ ((k : â„•) â†’ R Î± (g k) (g' k))
                â†’ (n  : â„•)
                  (n' : B â„•)
                â†’ R Î± n n'
                â†’ R Î± (g n) (Kleisli-extension g' n')

R-kleisli-lemma Î¹ Î± g g' rg n n' rn =
 g n                                   ï¼âŸ¨ rg n âŸ©
 dialogue (g' n) Î±                     ï¼âŸ¨ ap (Î» - â†’ dialogue (g' -) Î±) rn âŸ©
 dialogue (g' (dialogue n' Î±)) Î±       ï¼âŸ¨ decode-kleisli-extension g' n' Î± âŸ©
 dialogue (Kleisli-extension g' n') Î±  âˆ

R-kleisli-lemma (Ïƒ â‡’ Ï„) Î± g g' rg n n' rn
  = Î» y y' ry â†’ R-kleisli-lemma
                 Ï„
                 Î±
                 (Î» k â†’ g k y)
                 (Î» k â†’ g' k y')
                 (Î» k â†’ rg k y y' ry)
                 n
                 n'
                 rn

Rs : {n : â„•} {Î“ : Cxt n} â†’ Baire â†’ ã€ Î“ ã€‘ â†’ Bã€ Î“ ã€‘ â†’ Type
Rs {n} {Î“} Î± xs ys = (i : Fin n) â†’ R {Î“ [ i ]} Î± (xs i) (ys i)

main-lemma : {n : â„•} {Î“ : Cxt n} {Ïƒ : type} (t : T Î“ Ïƒ) (Î± : Baire)
             (xs : ã€ Î“ ã€‘)
             (ys : Bã€ Î“ ã€‘)
             â†’ Rs Î± xs ys
             â†’ R Î± (âŸ¦ t âŸ§ xs) (BâŸ¦ t âŸ§ ys)

main-lemma Zero Î± xs ys cr = refl

main-lemma Succ Î± xs ys cr = Î» n n' rn â†’
 succ n               ï¼âŸ¨ ap succ rn âŸ©
 succ (dialogue n' Î±) ï¼âŸ¨ decode-Î±-is-natural succ n' Î± âŸ©
 decode Î± (succ' n')  âˆ

main-lemma (Rec {_} {_} {Ïƒ}) Î± xs ys cr = lemma
 where
  lemma :  (f  : â„• â†’ ã€– Ïƒ ã€— â†’ ã€– Ïƒ ã€—) (f' : B â„• â†’ Bã€– Ïƒ ã€— â†’ Bã€– Ïƒ ã€—)
        â†’ R {Î¹ â‡’ Ïƒ â‡’ Ïƒ} Î± f f'
        â†’ (x  : ã€– Ïƒ ã€—) (y : Bã€– Ïƒ ã€—)
        â†’ R {Ïƒ} Î± x y
        â†’ (n  : â„•) (n' : B â„•)
        â†’ R {Î¹} Î± n n'
        â†’ R {Ïƒ} Î± (rec f x n) (Kleisli-extension (rec (f' âˆ˜ Î·) y) n')
  lemma f f' rf x y rx = R-kleisli-lemma Ïƒ Î± g g' rg
    where
      g : â„• â†’ ã€– Ïƒ ã€—
      g k = rec f x k

      g' : â„• â†’ Bã€– Ïƒ ã€—
      g' k = rec (f' âˆ˜ Î·) y k

      rg : (k : â„•) â†’ R Î± (g k) (g' k)
      rg zero     = rx
      rg (succ k) = rf k (Î· k) refl (g k) (g' k) (rg k)

main-lemma (Î½ i) Î± xs ys cr = cr i

main-lemma {n} {Î“} {Ïƒ â‡’ Ï„} (Æ› t) Î± xs ys cr = lemma
  where
    lemma : (x : ã€– Ïƒ ã€—) (y : Bã€– Ïƒ ã€—)
            â†’ R Î± x y
            â†’ R Î± (âŸ¦ t âŸ§ (xs â€š x)) (BâŸ¦ t âŸ§ (ys â€šâ€š y))
    lemma x y r = main-lemma t Î± (xs â€š x) (ys â€šâ€š y) h
      where
        h : (i : Fin (succ n)) â†’ R Î± ((xs â€š x) i) ((ys â€šâ€š y) i)
        h ğŸ       = r
        h (suc i) = cr i

main-lemma (t Â· u) Î± xs ys cr = IH-t (âŸ¦ u âŸ§ xs) (BâŸ¦ u âŸ§ ys) IH-u
 where
  IH-t : (x  : ã€– _ ã€—)
         (y : Bã€– _ ã€—)
       â†’ R Î± x y
       â†’ R Î± (âŸ¦ t âŸ§ xs x) (BâŸ¦ t âŸ§ ys y)
  IH-t = main-lemma t Î± xs ys cr

  IH-u : R Î± (âŸ¦ u âŸ§ xs) (BâŸ¦ u âŸ§ ys)
  IH-u = main-lemma u Î± xs ys cr

main-lemma-closed : {Ïƒ : type} (t : Tâ‚€ Ïƒ) (Î± : Baire)
                    â†’ R Î± âŸ¦ t âŸ§â‚€ (BâŸ¦ t âŸ§â‚€)
main-lemma-closed {Ïƒ} t Î± = main-lemma t Î± âŸ¨âŸ© âŸªâŸ« (Î»())

dialogue-tree-correct : (t : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹))
                        (Î± : Baire)
                      â†’ âŸ¦ t âŸ§â‚€ Î± ï¼ dialogue (dialogue-tree t) Î±
dialogue-tree-correct t Î± =
 âŸ¦ t âŸ§â‚€ Î±                      ï¼âŸ¨ main-lemma-closed t Î± Î± generic lemma âŸ©
 dialogue (BâŸ¦ t âŸ§â‚€ generic) Î±  ï¼âŸ¨ refl âŸ©
 dialogue (dialogue-tree t) Î± âˆ
  where
   lemma : (n  : â„•)
           (n' : B â„•)
         â†’ n ï¼ dialogue n' Î±
         â†’ Î± n ï¼ dialogue (generic n') Î±
   lemma n n' rn = Î± n                     ï¼âŸ¨ ap Î± rn âŸ©
                   Î± (dialogue n' Î±)       ï¼âŸ¨ generic-diagram Î± n' âŸ©
                   decode Î± (generic n')   ï¼âŸ¨ refl âŸ©
                   dialogue (generic n') Î± âˆ

eloquence-theorem : (f : Baire â†’ â„•)
                  â†’ T-definable f
                  â†’ eloquent f
eloquence-theorem f (t , r) =
 (dialogue-tree t ,
  (Î» Î± â†’ dialogue (dialogue-tree t) Î± ï¼âŸ¨ (dialogue-tree-correct t Î±)â»Â¹ âŸ©
         âŸ¦ t âŸ§â‚€ Î±                      ï¼âŸ¨ ap (Î» - â†’ - Î±) r âŸ©
         f Î±                          âˆ))

eloquence-corollaryâ‚€ : (f : Baire â†’ â„•)
                     â†’ T-definable f
                     â†’ is-continuous f
eloquence-corollaryâ‚€ f d = eloquent-functions-are-continuous
                            f
                            (eloquence-theorem f d)

eloquence-corollaryâ‚ : (f : Baire â†’ â„•)
                     â†’ T-definable f
                     â†’ is-uniformly-continuous (C-restriction f)
eloquence-corollaryâ‚ f d = eloquent-functions-are-UC
                            (C-restriction f)
                            (restriction-is-eloquent f (eloquence-theorem f d))
