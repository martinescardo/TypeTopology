Ayberk Tosun

Based on some previous work by MartÃ­n EscardÃ³.

In this module, we prove the correctness of the internal modulus of continuity
operator.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module EffectfulForcing.Internal.InternalModCont (fe : Fun-Ext) where

open import MLTT.Spartan hiding (rec; _^_)
open import MLTT.List
open import Naturals.Order using (max)
open import EffectfulForcing.Internal.Internal
open import EffectfulForcing.MFPSAndVariations.Church
open import EffectfulForcing.Internal.SystemT
open import EffectfulForcing.MFPSAndVariations.Combinators
open import EffectfulForcing.MFPSAndVariations.Dialogue
 using (eloquent; D; dialogue; eloquent-functions-are-continuous;
        dialogue-continuity; generic)
open import EffectfulForcing.MFPSAndVariations.Continuity
 using (is-continuous; is-continuousâ‚€; continuity-implies-continuityâ‚€;
        _ï¼â¦…_â¦†_; _ï¼âŸª_âŸ«_; modulus-atâ‚€; maximum)
open import EffectfulForcing.Internal.Correctness
 using (Rnorm-generic; is-dialogue-for; extÎ²; Rnorm-lemmaâ‚€; Rnorm)
open import EffectfulForcing.Internal.External
 using (eloquence-theorem; dialogue-tree; âŸªâŸ«; BâŸ¦_âŸ§; BâŸ¦_âŸ§â‚€)
open import EffectfulForcing.Internal.Subst
open import EffectfulForcing.MFPSAndVariations.SystemT
 using (type; Î¹; _â‡’_;ã€–_ã€—)

\end{code}

First, we define some nicer syntax for inherently typed System T terms.

\begin{code}

_âŠ¢_ : Cxt â†’ type â†’ ğ“¤â‚€  Ì‡
_âŠ¢_ Î“ Ï„ = T Î“ Ï„

infix 4 _âŠ¢_

baire : type
baire = Î¹ â‡’ Î¹

\end{code}

Some examples

\begin{code}

lam-exampleâ‚ : (n : â„•) â†’ âŸ¦ Æ› Î½â‚€ âŸ§â‚€ n ï¼ n
lam-exampleâ‚ n = refl

lam-exampleâ‚‚ : (m n : â„•) â†’ âŸ¦ Æ› (Æ› Î½â‚) âŸ§â‚€ m n ï¼ m
lam-exampleâ‚‚ m n = refl

\end{code}

The `ifz` operator in Agda and in System T respectively. We adopt the convention
of using the superscript `áµ€` to denote internal version of an operator that we
have defined in Agda.

\begin{code}

ifz : â„• â†’ â„• â†’ â„• â†’ â„•
ifz zero     nâ‚ nâ‚‚ = nâ‚
ifz (succ _) nâ‚ nâ‚‚ = nâ‚‚

ifzáµ€ : {Î“ : Cxt} â†’ Î“ âŠ¢ Î¹ â‡’ Î¹ â‡’ Î¹ â‡’ Î¹
ifzáµ€ = Æ› (Æ› (Æ› (Rec (Æ› (Æ› Î½â‚‚)) Î½â‚ Î½â‚‚)))

ifzáµ€-correct : (m nâ‚ nâ‚‚ : â„•) â†’ âŸ¦ ifzáµ€ âŸ§â‚€ m nâ‚ nâ‚‚ ï¼ ifz m nâ‚ nâ‚‚
ifzáµ€-correct zero     nâ‚ nâ‚‚ = refl
ifzáµ€-correct (succ m) nâ‚ nâ‚‚ = refl

\end{code}

The predecessor operator.

\begin{code}

pred : â„• â†’ â„•
pred zero     = zero
pred (succ n) = n

predáµ€ : {Î“ : Cxt} â†’ Î“ âŠ¢ Î¹ â‡’ Î¹
predáµ€ = Rec' {Ïƒ = Î¹} Â· (Æ› (Æ› Î½â‚)) Â· Zero

predáµ€-correct : (n : â„•) â†’ âŸ¦ predáµ€ âŸ§â‚€ n ï¼ pred n
predáµ€-correct zero     = refl
predáµ€-correct (succ n) = refl

\end{code}

The identity function on the naturals in System T.

\begin{code}

idáµ€ : {Î“ : Cxt} â†’ Î“ âŠ¢ Î¹ â‡’ Î¹
idáµ€ {Î“} = Æ› Î½â‚€

\end{code}

We now define the System T version of the `max` operator computing the maximum
of two given natural numbers.

\begin{code}

maxáµ€ : {Î“ : Cxt} â†’ Î“ âŠ¢ Î¹ â‡’ (Î¹ â‡’ Î¹)
maxáµ€ =
 Æ› (Rec (Æ› (Æ› (Æ› (ifzáµ€ Â· Î½â‚€ Â· Succ Î½â‚‚ Â· Succ (Î½â‚ Â· (predáµ€ Â· Î½â‚€)))))) idáµ€ Î½â‚€)

maxáµ€-correct : (m n : â„•) â†’ âŸ¦ maxáµ€ âŸ§â‚€ m n ï¼ max m n
maxáµ€-correct zero     n        = refl
maxáµ€-correct (succ m) zero     = refl
maxáµ€-correct (succ m) (succ n) =
 âŸ¦ maxáµ€ âŸ§â‚€ (succ m) (succ n)                                            ï¼âŸ¨ refl âŸ©
 âŸ¦ ifzáµ€ âŸ§â‚€ (succ n) (succ m) (succ (âŸ¦ maxáµ€ âŸ§â‚€ m (âŸ¦ predáµ€ âŸ§â‚€ (succ n)))) ï¼âŸ¨ â…     âŸ©
 ifz (succ n) (succ m) (succ (âŸ¦ maxáµ€ âŸ§â‚€ m (âŸ¦ predáµ€ âŸ§â‚€ (succ n))))       ï¼âŸ¨ refl âŸ©
 succ (âŸ¦ maxáµ€ âŸ§â‚€ m (âŸ¦ predáµ€ âŸ§â‚€ (succ n)))                               ï¼âŸ¨ refl âŸ©
 succ (âŸ¦ maxáµ€ âŸ§â‚€ m (pred (succ n)))                                     ï¼âŸ¨ â…¡    âŸ©
 succ (max m n)                                                         ï¼âŸ¨ refl âŸ©
 max (succ m) (succ n)                                                  âˆ
  where
   â…  = ifzáµ€-correct (succ n) (succ m) (succ (âŸ¦ maxáµ€ âŸ§â‚€ m (âŸ¦ predáµ€ âŸ§â‚€ (succ n))))
   â…¡ = ap succ (maxáµ€-correct m n)

\end{code}

We will use the `maxáµ€` operator to define the internal modulus of continuity
operator. To work towards this, we define the external version of the operator
that we call `max-question`.

There will be three different versions of this operator:

  1. `max-question`, that works on the external inductive type encoding of
     dialogue trees in Agda,
  2. `max-questionâ‹†`, that works on the external Church encoding of dialogue
     trees in Agda, and
  3. `max-questionáµ€`, that is a System T function working on the Church encoding
     of dialogue trees in System T.

There is also `max-questionâ‚€` which is an alternative definition of
`max-question` that uses `dialogue-continuity`. This is used to facilitate a
proof.

\begin{code}

max-question : D â„• â„• â„• â†’ (â„• â†’ â„•) â†’ â„•
max-question (D.Î· n)   Î± = 0
max-question (D.Î² Ï† n) Î± = max n (max-question (Ï† (Î± n)) Î±)

max-questionâ‚€ : D â„• â„• â„• â†’ (â„• â†’ â„•) â†’ â„•
max-questionâ‚€ d Î± = maximum (prâ‚ (dialogue-continuity d Î±))

max-questionâ‚€-agreement : (d : D â„• â„• â„•) (Î± : â„• â†’ â„•)
                        â†’ max-question d Î± ï¼ max-questionâ‚€ d Î±
max-questionâ‚€-agreement (D.Î· n)   Î± = refl
max-questionâ‚€-agreement (D.Î² Ï† n) Î± =
 ap (max n) (max-questionâ‚€-agreement (Ï† (Î± n)) Î±)

max-questionâ‹† : Dâ‹† â„• â„• â„• â„• â†’ (â„• â†’ â„•) â†’ â„•
max-questionâ‹† d Î± = d (Î» _ â†’ 0) (Î» g x â†’ max x (g (Î± x)))

max-questionáµ€ : {Î“ : Cxt} â†’ Î“ âŠ¢ (âŒœBâŒ Î¹ Î¹) â‡’ baire â‡’ Î¹
max-questionáµ€ = Æ› (Æ› (Î½â‚ Â· Æ› Zero Â· Æ› (Æ› (maxáµ€ Â· Î½â‚€ Â· (Î½â‚ Â· (Î½â‚‚ Â· Î½â‚€))))))

max-questionâ‹†-agreement : (d : D â„• â„• â„•) (Î± : â„• â†’ â„•)
                        â†’ max-question d Î± ï¼ max-questionâ‹† (church-encode d) Î±
max-questionâ‹†-agreement (D.Î· n)   Î± = refl
max-questionâ‹†-agreement (D.Î² Ï† n) Î± = â€ 
 where
  IH : max-question (Ï† (Î± n)) Î±
     ï¼ max-questionâ‹† (church-encode (Ï† (Î± n))) Î±
  IH = max-questionâ‹†-agreement (Ï† (Î± n)) Î±

  â€  : max n (max-question (Ï† (Î± n)) Î±)
    ï¼ church-encode (D.Î² Ï† n) (Î» _ â†’ 0) (Î» g x â†’ max x (g (Î± x)))
  â€  = ap (max n) IH

max-questionáµ€-agreement-with-max-questionâ‹† : (d : âŒ©âŒª âŠ¢ âŒœDâ‹†âŒ Î¹ Î¹ Î¹ Î¹) (Î± : â„• â†’ â„•)
                                           â†’ âŸ¦ max-questionáµ€ Â· d âŸ§â‚€ Î±
                                             ï¼ max-questionâ‹† âŸ¦ d âŸ§â‚€ Î±
max-questionáµ€-agreement-with-max-questionâ‹† d Î± =
 ap
  (âŸ¦ d âŸ§â‚€ (Î» _ â†’ 0))
  (dfunext fe Î» g â†’ dfunext fe Î» x â†’ maxáµ€-correct x (g (Î± x)))


\end{code}

The modulus of continuity given by a dialogue tree is the successor of the
maximum question in it. Again, we define three different versions of the modulus
of continuity operation following the same conventions.

  1. `modulus` following `max-question`,
  2. `modulusâ‚€` following `max-questionâ‚€`, and
  3. `modulusáµ€` following `max-questionáµ€`.

\begin{code}

modulus : D â„• â„• â„• â†’ (â„• â†’ â„•) â†’ â„•
modulus d Î± = succ (max-question d Î±)

modulusâ‚€ : (d : D â„• â„• â„•) â†’ (â„• â†’ â„•) â†’ â„•
modulusâ‚€ d Î± = succ (max-questionâ‚€ d Î±)

modulusáµ€ : {Î“ : Cxt}
                   â†’ Î“ âŠ¢ baire â‡’ Î¹
                   â†’ B-contextã€ Î“ ã€‘ Î¹ âŠ¢ (Î¹ â‡’ Î¹) â‡’ Î¹
modulusáµ€ t = comp Â· Succ' Â· (max-questionáµ€ Â· âŒœdialogue-treeâŒ t)

\end{code}

The correctness of `modulusáµ€` is given in `internal-mod-cont-correct` below. To
prove this, we use the lemma `main-lemma`, which contains the main content of
the proof.

\begin{code}


main-lemma : (t : âŒ©âŒª âŠ¢ (baire â‡’ Î¹)) (Î± : â„• â†’ â„•)
           â†’ âŸ¦ max-questionáµ€ Â· (âŒœdialogue-treeâŒ t) âŸ§â‚€ Î±
             ï¼ max-questionâ‚€ (dialogue-tree t) Î±
main-lemma t Î± =
 âŸ¦ max-questionáµ€ Â· âŒœdialogue-treeâŒ t âŸ§â‚€ Î±           ï¼âŸ¨ â…  âŸ©
 max-questionâ‹† âŸ¦ âŒœdialogue-treeâŒ t âŸ§â‚€ Î±             ï¼âŸ¨ â…¡ âŸ©
 max-questionâ‹† (church-encode (dialogue-tree t)) Î±  ï¼âŸ¨ â…¢ âŸ©
 max-question  (dialogue-tree t) Î±                  ï¼âŸ¨ â…£ âŸ©
 max-questionâ‚€ (dialogue-tree t) Î±                  âˆ
  where
   â€  : Rnorm (BâŸ¦ t âŸ§â‚€ generic) (âŒœ t âŒ Â· âŒœgenericâŒ)
   â€  = Rnorm-lemmaâ‚€ t generic âŒœgenericâŒ Rnorm-generic

   ext : extÎ² (Î» g x â†’ max x (g (Î± x)))
   ext f g m n p Ï† =
    max m (f (Î± m)) ï¼âŸ¨ ap (Î» - â†’ max - (f (Î± -))) p âŸ©
    max n (f (Î± n)) ï¼âŸ¨ ap (max n) (Ï† (Î± n))         âŸ©
    max n (g (Î± n)) âˆ

   â…  = max-questionáµ€-agreement-with-max-questionâ‹† (âŒœdialogue-treeâŒ t) Î±
   â…¡ = â€  Î¹ (Î» _ â†’ 0) (Î» g x â†’ max x (g (Î± x))) (Î» _ â†’ refl) ext
   â…¢ = max-questionâ‹†-agreement (dialogue-tree t) Î± â»Â¹
   â…£ = max-questionâ‚€-agreement (dialogue-tree t) Î±

internal-mod-cont-correct : (t : âŒ©âŒª âŠ¢ (baire â‡’ Î¹)) (Î± Î² : âŒ©âŒª âŠ¢ baire)
                          â†’ âŸ¦ Î± âŸ§â‚€ ï¼â¦… âŸ¦ modulusáµ€ t Â· Î± âŸ§â‚€ â¦† âŸ¦ Î² âŸ§â‚€
                          â†’ âŸ¦ t Â· Î± âŸ§â‚€ ï¼ âŸ¦ t Â·  Î² âŸ§â‚€
internal-mod-cont-correct t Î± Î² p = â€ 
 where
  Îµ : eloquent âŸ¦ t âŸ§â‚€
  Îµ = eloquence-theorem âŸ¦ t âŸ§â‚€ (t , refl)

  c : is-continuous âŸ¦ t âŸ§â‚€
  c = eloquent-functions-are-continuous âŸ¦ t âŸ§â‚€ Îµ

  câ‚€ : is-continuousâ‚€ âŸ¦ t âŸ§â‚€
  câ‚€ = continuity-implies-continuityâ‚€ âŸ¦ t âŸ§â‚€ c

  mâ‚€ : â„•
  mâ‚€ = succ (max-questionâ‚€ (dialogue-tree t) âŸ¦ Î± âŸ§â‚€)

  q : âŸ¦ modulusáµ€ t Â· Î± âŸ§â‚€ ï¼ mâ‚€
  q = ap succ (main-lemma t âŸ¦ Î± âŸ§â‚€)

  â€¡ : âŸ¦ Î± âŸ§â‚€ ï¼â¦… mâ‚€ â¦† âŸ¦ Î² âŸ§â‚€
  â€¡ = transport (Î» - â†’ âŸ¦ Î± âŸ§â‚€ ï¼â¦… - â¦† âŸ¦ Î² âŸ§â‚€) q p

  â€  : âŸ¦ t âŸ§â‚€ âŸ¦ Î± âŸ§â‚€ ï¼ âŸ¦ t âŸ§â‚€ âŸ¦ Î² âŸ§â‚€
  â€  = prâ‚‚ (câ‚€ âŸ¦ Î± âŸ§â‚€) âŸ¦ Î² âŸ§â‚€ â€¡

\end{code}

While I was working on the proof, I wrote down the following fact, which turned
out not to be necessary for the proof. However, I am not taking it out of this
file as it might be useful in the future.

\begin{code}

church-encode-to-D-rec : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } {A : ğ“£  Ì‡}
                     â†’ (d : D X Y Z)
                     â†’ (Î·â€² : Z â†’ A)
                     â†’ (Î²â€² : (Y â†’ A) â†’ X â†’ A)
                     â†’ church-encode d Î·â€² Î²â€² ï¼ D-rec Î·â€² Î²â€² d
church-encode-to-D-rec (D.Î· _)   Î·â€² Î²â€² = refl
church-encode-to-D-rec {Y = Y} (D.Î² Ï† x) Î·â€² Î²â€² = ap (Î» - â†’ Î²â€² - x) (dfunext fe â€ )
 where
  â€  : (y : Y) â†’ church-encode (Ï† y) Î·â€² Î²â€² ï¼ D-rec Î·â€² Î²â€² (Ï† y)
  â€  y = church-encode-to-D-rec (Ï† y) Î·â€² Î²â€²

\end{code}
