Martin Escardo 2012

Combinatory version of GÃ¶del's System T and its standard set-theoretic
semantics.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module EffectfulForcing.MFPSAndVariations.CombinatoryT where

open import MLTT.Spartan
open import MLTT.Athenian
open import EffectfulForcing.MFPSAndVariations.Continuity
open import EffectfulForcing.MFPSAndVariations.Combinators
open import UF.Base

data type : ğ“¤â‚€  Ì‡  where
 Î¹   : type
 _â‡’_ : type â†’ type â†’ type

data T : (Ïƒ : type) â†’ ğ“¤â‚€  Ì‡  where
 Zero  : T Î¹
 Succ  : T (Î¹ â‡’ Î¹)
 Iter  : {Ïƒ : type}     â†’ T ((Ïƒ â‡’ Ïƒ) â‡’ Ïƒ â‡’ Î¹ â‡’ Ïƒ)
 K     : {Ïƒ Ï„ : type}   â†’ T (Ïƒ â‡’ Ï„ â‡’ Ïƒ)
 S     : {Ï Ïƒ Ï„ : type} â†’ T ((Ï â‡’ Ïƒ â‡’ Ï„) â‡’ (Ï â‡’ Ïƒ) â‡’ Ï â‡’ Ï„)
 _Â·_   : {Ïƒ Ï„ : type}   â†’ T (Ïƒ â‡’ Ï„) â†’ T Ïƒ â†’ T Ï„

infixr 1 _â‡’_
infixl 1 _Â·_

SetâŸ¦_âŸ§ : type â†’ ğ“¤â‚€  Ì‡
SetâŸ¦ Î¹ âŸ§     = â„•
SetâŸ¦ Ïƒ â‡’ Ï„ âŸ§ = SetâŸ¦ Ïƒ âŸ§ â†’ SetâŸ¦ Ï„ âŸ§

âŸ¦_âŸ§ : {Ïƒ : type} â†’ T Ïƒ â†’ SetâŸ¦ Ïƒ âŸ§
âŸ¦ Zero  âŸ§ = zero
âŸ¦ Succ  âŸ§ = succ
âŸ¦ Iter  âŸ§ = iter
âŸ¦ K     âŸ§ = Ä¶
âŸ¦ S     âŸ§ = Å
âŸ¦ t Â· u âŸ§ = âŸ¦ t âŸ§ âŸ¦ u âŸ§

is-T-definable : {Ïƒ : type} â†’ SetâŸ¦ Ïƒ âŸ§ â†’ ğ“¤â‚€ Ì‡
is-T-definable {Ïƒ} x = Î£ t ê‰ T Ïƒ , âŸ¦ t âŸ§ ï¼ x

\end{code}

System T extended with oracles.

\begin{code}

data TÎ© : (Ïƒ : type) â†’ ğ“¤â‚€ Ì‡  where
 Î©     : TÎ© (Î¹ â‡’ Î¹)
 Zero  : TÎ© Î¹
 Succ  : TÎ© (Î¹ â‡’ Î¹)
 Iter  : {Ïƒ : type}     â†’ TÎ© ((Ïƒ â‡’ Ïƒ) â‡’ Ïƒ â‡’ Î¹ â‡’ Ïƒ)
 K     : {Ïƒ Ï„ : type}   â†’ TÎ© (Ïƒ â‡’ Ï„ â‡’ Ïƒ)
 S     : {Ï Ïƒ Ï„ : type} â†’ TÎ© ((Ï â‡’ Ïƒ â‡’ Ï„) â‡’ (Ï â‡’ Ïƒ) â‡’ Ï â‡’ Ï„)
 _Â·_   : {Ïƒ Ï„ : type}   â†’ TÎ© (Ïƒ â‡’ Ï„) â†’ TÎ© Ïƒ â†’ TÎ© Ï„

âŸ¦_âŸ§' : {Ïƒ : type} â†’ TÎ© Ïƒ â†’ Baire â†’ SetâŸ¦ Ïƒ âŸ§
âŸ¦ Î©     âŸ§' Î± = Î±
âŸ¦ Zero  âŸ§' Î± = zero
âŸ¦ Succ  âŸ§' Î± = succ
âŸ¦ Iter  âŸ§' Î± = iter
âŸ¦ K     âŸ§' Î± = Ä¶
âŸ¦ S     âŸ§' Î± = Å
âŸ¦ t Â· u âŸ§' Î± = âŸ¦ t âŸ§' Î± (âŸ¦ u âŸ§' Î±)

embedding : {Ïƒ : type} â†’ T Ïƒ â†’ TÎ© Ïƒ
embedding Zero    = Zero
embedding Succ    = Succ
embedding Iter    = Iter
embedding K       = K
embedding S       = S
embedding (t Â· u) = embedding t Â· embedding u

preservation : {Ïƒ : type} â†’ (t : T Ïƒ) â†’ (Î± : Baire) â†’ âŸ¦ t âŸ§ ï¼ âŸ¦ embedding t âŸ§' Î±
preservation Zero    Î± = refl
preservation Succ    Î± = refl
preservation Iter    Î± = refl
preservation K       Î± = refl
preservation S       Î± = refl
preservation (t Â· u) Î± = apâ‚‚ id (preservation t Î±) (preservation u Î±)

\end{code}
