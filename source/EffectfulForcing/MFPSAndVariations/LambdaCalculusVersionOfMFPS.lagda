Martin Escardo 22-24 May 2013

The MFPS paper https://doi.org/10.1016/j.entcs.2013.09.010 worked with
the combinatory version of system T. Here we work with the
lambda-calculus version. Moreover, the original version has the
iteration combinator, while here we work with the primitive recursion
combinator for system T.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module EffectfulForcing.MFPSAndVariations.LambdaCalculusVersionOfMFPS where

open import MLTT.Spartan hiding (rec ; _^_) renaming (â‹† to âŒ©âŒª)
open import MLTT.Fin
open import UF.Base
open import EffectfulForcing.MFPSAndVariations.Combinators
open import EffectfulForcing.MFPSAndVariations.Continuity
open import EffectfulForcing.MFPSAndVariations.Dialogue
open import EffectfulForcing.MFPSAndVariations.SystemT

\end{code}

Auxiliary interpretation of types:

\begin{code}

Bã€–_ã€— : type â†’ Type
Bã€– Î¹ ã€—     = B â„•
Bã€– Ïƒ â‡’ Ï„ ã€— = Bã€– Ïƒ ã€— â†’ Bã€– Ï„ ã€—

\end{code}

Generalized Kleisli extension (as in the original treatment):

\begin{code}

Kleisli-extension : {X : Type} {Ïƒ : type} â†’ (X â†’ Bã€– Ïƒ ã€—) â†’ B X â†’ Bã€– Ïƒ ã€—
Kleisli-extension {X} {Î¹}     = kleisli-extension
Kleisli-extension {X} {Ïƒ â‡’ Ï„} = Î» g d s â†’ Kleisli-extension {X} {Ï„} (Î» x â†’ g x s) d

\end{code}

The interpretation of the system T constants (again as in the original
development):

\begin{code}

zero' : B â„•
zero' = Î· 0

succ' : B â„• â†’ B â„•
succ' = B-functor succ

rec' : {Ïƒ : type} â†’ (B â„• â†’ Bã€– Ïƒ ã€— â†’ Bã€– Ïƒ ã€—) â†’ Bã€– Ïƒ ã€— â†’ B â„• â†’ Bã€– Ïƒ ã€—
rec' f x = Kleisli-extension(rec (f âˆ˜ Î·) x)

\end{code}

The auxiliary interpretation of contexts (which were not present in
the original development):

\begin{code}

Bã€_ã€‘ : {n : â„•} (Î“ : Cxt n) â†’ Type
Bã€ Î“ ã€‘ = (i : Fin _) â†’ Bã€– (Î“ [ i ]) ã€—

âŸªâŸ« : Bã€ âŒ©âŒª ã€‘
âŸªâŸ« ()

_â€šâ€š_ : {n : â„•} {Î“ : Cxt n} {Ïƒ : type} â†’ Bã€ Î“ ã€‘ â†’ Bã€– Ïƒ ã€— â†’ Bã€ Î“ , Ïƒ ã€‘
(xs â€šâ€š x) ğŸ       = x
(xs â€šâ€š x) (suc i) = xs i

infixl 6 _â€šâ€š_

\end{code}

The auxiliary interpretation of system TÎ© terms:

\begin{code}

BâŸ¦_âŸ§ : {n : â„•} {Î“ : Cxt n} {Ïƒ : type} â†’ T' Î“ Ïƒ â†’ Bã€ Î“ ã€‘ â†’ Bã€– Ïƒ ã€—
BâŸ¦ Î©     âŸ§  _ = generic
BâŸ¦ Zero  âŸ§  _ = zero'
BâŸ¦ Succ  âŸ§  _ = succ'
BâŸ¦ Rec   âŸ§  _ = rec'
BâŸ¦ Î½ i   âŸ§ xs = xs i
BâŸ¦ Æ› t   âŸ§ xs = Î» x â†’ BâŸ¦ t âŸ§ (xs â€šâ€š x)
BâŸ¦ t Â· u âŸ§ xs = (BâŸ¦ t âŸ§ xs) (BâŸ¦ u âŸ§ xs)

\end{code}

The dialogue tree of a closed term of type ((Î¹ â‡’ Î¹) â‡’ Î¹):

\begin{code}

dialogue-tree : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹) â†’ B â„•
dialogue-tree t = BâŸ¦ embed t Â· Î© âŸ§ âŸªâŸ«

\end{code}

The logical relation is the same as in the original development:

\begin{code}

R : {Ïƒ : type} â†’ (Baire â†’ ã€– Ïƒ ã€—) â†’ Bã€– Ïƒ ã€— â†’ Type
R {Î¹}     n n' = (Î± : Baire) â†’ n Î± ï¼ decode Î± n'
R {Ïƒ â‡’ Ï„} f f' = (x  : Baire â†’ ã€– Ïƒ ã€—)
                 (x' : Bã€– Ïƒ ã€—)
               â†’ R {Ïƒ} x x'
               â†’ R {Ï„} (Î» Î± â†’ f Î± (x Î±)) (f' x')
\end{code}

The following lemma is again the same as in the original development,
by induction on types:

\begin{code}

R-kleisli-lemma : (Ïƒ : type)
                  (g  : â„• â†’ Baire â†’ ã€– Ïƒ ã€—)
                  (g' : â„• â†’ Bã€– Ïƒ ã€—)
                â†’ ((k : â„•) â†’ R (g k) (g' k))
                â†’ (n  : Baire â†’ â„•)
                  (n' : B â„•)
                â†’ R n n'
                â†’ R (Î» Î± â†’ g (n Î±) Î±) (Kleisli-extension g' n')

R-kleisli-lemma Î¹ g g' rg n n' rn = Î» Î± â†’
 g (n Î±) Î±                          ï¼âŸ¨ rg (n Î±) Î± âŸ©
 decode Î± (g' (n Î±))                ï¼âŸ¨ ap (Î» k â†’ decode Î± (g' k)) (rn Î±) âŸ©
 decode Î± (g' (decode Î± n'))        ï¼âŸ¨ decode-kleisli-extension g' n' Î± âŸ©
 decode Î± (kleisli-extension g' n') âˆ

R-kleisli-lemma (Ïƒ â‡’ Ï„) g g' rg n n' rn =
 Î» y y' ry â†’ R-kleisli-lemma
              Ï„
              (Î» k Î± â†’ g k Î± (y Î±))
              (Î» k â†’ g' k y')
              (Î» k â†’ rg k y y' ry)
              n
              n'
              rn
\end{code}

The main lemma is a modification of the main lemma in the original
development, still by induction on terms. We don't have cases for the
combinators K and S anymore, but we need to add two cases for Î½ and Æ›,
and we need to modify the case for application _Â·_, which becomes more
difficult (in a routine way).  Additionally, we first need to extend R
to contexts (in the obvious way):

\begin{code}

Rs : {n : â„•} {Î“ : Cxt n} â†’ (Baire â†’ ã€ Î“ ã€‘) â†’ Bã€ Î“ ã€‘ â†’ Type
Rs {n} {Î“} xs ys = (i : Fin n) â†’ R {Î“ [ i ]} (Î» Î± â†’ xs Î± i) (ys i)

main-lemma : {n : â„•} {Î“ : Cxt n}
             {Ïƒ : type}
             (t : T' Î“ Ïƒ)
             (xs : Baire â†’ ã€ Î“ ã€‘)
             (ys : Bã€ Î“ ã€‘)
           â†’ Rs xs ys
           â†’ R (Î» Î± â†’ âŸ¦ t âŸ§' Î± (xs Î±)) (BâŸ¦ t âŸ§ ys)

main-lemma Î© xs ys cr = Î» n n' rn Î± â†’
 Î± (n Î±)               ï¼âŸ¨ ap Î± (rn Î±) âŸ©
 Î± (decode Î± n')       ï¼âŸ¨ generic-diagram Î± n' âŸ©
 decode Î± (generic n') âˆ

main-lemma Zero xs ys cr = Î» Î± â†’ refl

main-lemma Succ xs ys cr = Î» n n' rn Î±  â†’
 succ (n Î±)          ï¼âŸ¨ ap succ (rn Î±) âŸ©
 succ (decode Î± n')  ï¼âŸ¨ decode-Î±-is-natural succ n' Î± âŸ©
 decode Î± (succ' n') âˆ

main-lemma (Rec {_} {_} {Ïƒ}) _ _ _ = lemma
 where
  lemma : (f  : Baire â†’ â„• â†’ ã€– Ïƒ ã€— â†’ ã€– Ïƒ ã€—)
          (f' : B â„• â†’ Bã€– Ïƒ ã€— â†’ Bã€– Ïƒ ã€—)
        â†’ R {Î¹ â‡’ Ïƒ â‡’ Ïƒ} f f'
        â†’ (x  : Baire â†’ ã€– Ïƒ ã€—)
          (x' : Bã€– Ïƒ ã€—)
        â†’ R {Ïƒ} x x'
        â†’ (n  : Baire â†’ â„•)
          (n' : B â„•) â†’ R {Î¹} n n'
        â†’ R {Ïƒ} (Î» Î± â†’ rec (f Î±) (x Î±) (n Î±))
                (Kleisli-extension(rec (f' âˆ˜ Î·) x') n')
  lemma f f' rf x x' rx = R-kleisli-lemma Ïƒ g g' rg
   where
    g : â„• â†’ Baire â†’ ã€– Ïƒ ã€—
    g k Î± = rec (f Î±) (x Î±) k

    g' : â„• â†’ Bã€– Ïƒ ã€—
    g' k = rec (f' âˆ˜ Î·) x' k

    rg : (k : â„•) â†’ R (g k) (g' k)
    rg 0        = rx
    rg (succ k) = rf (Î» Î± â†’ k) (Î· k) (Î» Î± â†’ refl) (g k) (g' k) (rg k)

main-lemma (Î½ i) xs ys cr = cr i

main-lemma {n} {Î“} {Ïƒ â‡’ Ï„} (Æ› t) xs ys cr = IH
 where
  IH : (x : Baire â†’ ã€– Ïƒ ã€—)
       (y : Bã€– Ïƒ ã€—)
      â†’ R x y
      â†’ R (Î» Î± â†’ âŸ¦ t âŸ§' Î± (xs Î± â€š x Î±)) (BâŸ¦ t âŸ§ (ys â€šâ€š y))
  IH x y r = main-lemma t (Î» Î± â†’ xs Î± â€š x Î±) (ys â€šâ€š y) h
    where
     h : (i : Fin (succ n)) â†’ R (Î» Î± â†’ (xs Î± â€š x Î±) i) ((ys â€šâ€š y) i)
     h ğŸ       = r
     h (suc i) = cr i

main-lemma (t Â· u) xs ys cr = IH-t (Î» Î± â†’ âŸ¦ u âŸ§' Î± (xs Î±)) (BâŸ¦ u âŸ§ ys) IH-u
 where
  IH-t : (x  : Baire â†’ ã€– _ ã€—)
         (x' : Bã€– _ ã€—)
       â†’ R x x'
       â†’ R (Î» Î± â†’ âŸ¦ t âŸ§' Î± (xs Î±) (x Î±))
           (BâŸ¦ t âŸ§ ys x')
  IH-t = main-lemma t xs ys cr

  IH-u : R (Î» Î± â†’ âŸ¦ u âŸ§' Î± (xs Î±)) (BâŸ¦ u âŸ§ ys)
  IH-u = main-lemma u xs ys cr

\end{code}

Of course, all we are interested in is the ground case of the
main-lemma for closed terms, but we needed to prove the general case
to get that, using a logical relation to have a suitable induction
hypothesis, as usual.

\begin{code}

main-closed-ground : (t : T' âŒ©âŒª Î¹) (Î± : Baire)
                   â†’ âŸ¦ t âŸ§' Î± âŸ¨âŸ© ï¼ decode Î± (BâŸ¦ t âŸ§ âŸªâŸ«)
main-closed-ground t = main-lemma t (Î» Î± â†’ âŸ¨âŸ©) âŸªâŸ« (Î»())

\end{code}

Then the correctness of the dialogue-tree function follows directly:

\begin{code}

dialogue-tree-correct : (t : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹))
                        (Î± : Baire)
                      â†’ âŸ¦ t âŸ§â‚€ Î± ï¼ decode Î± (dialogue-tree t)
dialogue-tree-correct t Î± =
 âŸ¦ t âŸ§â‚€ Î±                       ï¼âŸ¨ ap (Î» f â†’ f âŸ¨âŸ© Î±) (preservation t Î±) âŸ©
 âŸ¦ embed t âŸ§' Î± âŸ¨âŸ© Î±            ï¼âŸ¨ main-closed-ground (embed t Â· Î©) Î± âŸ©
 decode Î± (BâŸ¦ embed t Â· Î© âŸ§ âŸªâŸ«) ï¼âŸ¨ refl âŸ©
 decode Î± (dialogue-tree t)     âˆ

\end{code}

And the main theorem follows directly from this:

\begin{code}

eloquence-theorem : (f : Baire â†’ â„•)
                  â†’ T-definable f
                  â†’ eloquent f
eloquence-theorem f (t , r) =
 (dialogue-tree t ,
  Î» Î± â†’ dialogue (dialogue-tree t) Î± ï¼âŸ¨ (dialogue-tree-correct t Î±)â»Â¹ âŸ©
        âŸ¦ t âŸ§â‚€ Î±                     ï¼âŸ¨ ap (Î» - â†’ - Î±) r âŸ©
        f Î±                          âˆ)

eloquence-corollaryâ‚€ : (f : Baire â†’ â„•)
                     â†’ T-definable f
                     â†’ is-continuous f
eloquence-corollaryâ‚€ f d = eloquent-functions-are-continuous
                            f
                            (eloquence-theorem f d)

eloquence-corollaryâ‚ : (f : Baire â†’ â„•)
                     â†’ T-definable f
                     â†’ is-uniformly-continuous (C-restriction f)
eloquence-corollaryâ‚ f d = eloquent-functions-are-UC
                            (C-restriction f)
                            (restriction-is-eloquent f
                            (eloquence-theorem f d))
\end{code}

Examples:

\begin{code}

module examples where

 open import MLTT.Athenian using (List)
 open List

 max : â„• â†’ â„• â†’ â„•
 max 0        y        = y
 max (succ x) 0        = succ x
 max (succ x) (succ y) = succ (max x y)

 mod : List â„• â†’ â„•
 mod []      = 0
 mod (x âˆ· s) = max (succ x) (mod s)

 mod-cont : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹) â†’ Baire â†’ â„•
 mod-cont t Î± = mod (prâ‚ (eloquence-corollaryâ‚€ âŸ¦ t âŸ§â‚€ (t , refl) Î±))

 mâ‚ : (â„• â†’ â„•) â†’ â„•
 mâ‚ = mod-cont (Æ› (Î½â‚€ Â· numeral 17))

 mâ‚-explicitly : mâ‚ ï¼ Î» x â†’ 18
 mâ‚-explicitly = refl

 exampleâ‚ : mâ‚ id ï¼ 18
 exampleâ‚ = refl

 exampleâ‚' : mâ‚ (Î» i â†’ 0) ï¼ 18
 exampleâ‚' = refl

 mâ‚‚ : (â„• â†’ â„•) â†’ â„•
 mâ‚‚ = mod-cont (Æ› (Î½â‚€ Â· (Î½â‚€ Â· numeral 17)))

 mâ‚‚-explicitly : mâ‚‚ ï¼ Î» Î± â†’ succ (max 17 (Î± 17))
 mâ‚‚-explicitly = refl

 exampleâ‚‚ : mâ‚‚ succ ï¼ 19
 exampleâ‚‚ = refl

 exampleâ‚‚' : mâ‚‚ (Î» i â†’ 0) ï¼ 18
 exampleâ‚‚' = refl

 exampleâ‚‚'' : mâ‚‚ id ï¼ 18
 exampleâ‚‚'' = refl

 exampleâ‚‚''' : mâ‚‚ (succ âˆ˜ succ) ï¼ 20
 exampleâ‚‚''' = refl


 Add : {n : â„•} {Î“ : Cxt n} â†’ T Î“ (Î¹ â‡’ Î¹ â‡’ Î¹)
 Add = Rec Â· (Æ› Succ)

 tâ‚ƒ : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹)
 tâ‚ƒ = Æ› (Î½â‚€ Â· (Î½â‚€ Â· (Add Â· (Î½â‚€ Â· numeral 17) Â· (Î½â‚€ Â· numeral 34))))

 add : â„• â†’ â„• â†’ â„•
 add = rec (Î» _ â†’ succ)

 tâ‚ƒ-meaning : âŸ¦ tâ‚ƒ âŸ§â‚€ ï¼ Î» Î± â†’ Î± (Î± (add (Î± 17) (Î± 34)))
 tâ‚ƒ-meaning = refl

 mâ‚ƒ : (â„• â†’ â„•) â†’ â„•
 mâ‚ƒ = mod-cont tâ‚ƒ

 exampleâ‚ƒ : mâ‚ƒ succ ï¼ 55
 exampleâ‚ƒ = refl

 exampleâ‚ƒ' : mâ‚ƒ id ï¼ 52
 exampleâ‚ƒ' = refl

 exampleâ‚ƒ'' : mâ‚ƒ (Î» i â†’ 0) ï¼ 35
 exampleâ‚ƒ'' = refl

 exampleâ‚ƒ''' : mâ‚ƒ (Î» i â†’ 300) ï¼ 601
 exampleâ‚ƒ''' = refl

 exampleâ‚ƒ'''' : mâ‚ƒ (Î» i â†’ add i i) ï¼ 205
 exampleâ‚ƒ'''' = refl

 f : Tâ‚€ ((Î¹ â‡’ Î¹) â‡’ Î¹)
 f = Æ› (Î½â‚€ Â· (Î½â‚€ Â· (Î½â‚€ Â· numeral 17)))

 mâ‚„ : (â„• â†’ â„•) â†’ â„•
 mâ‚„ = mod-cont f

 exampleâ‚„ : mâ‚„ id ï¼ 18
 exampleâ‚„ = refl

 exampleâ‚„' : mâ‚„ (Î» i â†’ 0) ï¼ 18
 exampleâ‚„' = refl

 exampleâ‚„'' : mâ‚„ succ ï¼ 20
 exampleâ‚„'' = refl

 exampleâ‚„''' : mâ‚„ (Î» i â†’ add i i) ï¼ 69
 exampleâ‚„''' = refl

 exampleâ‚„'''' : âŸ¦ f âŸ§â‚€ (Î» i â†’ add i i) ï¼ 136
 exampleâ‚„'''' = refl

\end{code}
