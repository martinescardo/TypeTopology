Martin Escardo, started 5th May 2018

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module NaturalsOrder where

open import SpartanMLTT hiding (_+'_)
open import UF-Subsingletons
open import OrdinalNotions hiding (_â‰¤_ ; <-coarser-than-â‰¤ ; â‰¤-refl)
open import NaturalsAddition renaming (_+_ to _+'_)

_â‰¤_ : â„• â†’ â„• â†’ Uâ‚€ Ì‡
zero â‰¤ n        = ðŸ™
succ m â‰¤ zero   = ðŸ˜
succ m â‰¤ succ n = m â‰¤ n

â‰¤-is-prop-valued : (m n : â„•) â†’ is-prop (m â‰¤ n)
â‰¤-is-prop-valued zero n = ðŸ™-is-prop
â‰¤-is-prop-valued (succ m) zero = ðŸ˜-is-prop
â‰¤-is-prop-valued (succ m) (succ n) = â‰¤-is-prop-valued m n

open import UF-Base
open import UF-SetExamples

subtraction-is-prop : (m n : â„•) â†’ is-prop (Î£ \(k : â„•) â†’ k +' m â‰¡ n)
subtraction-is-prop zero n (.n , refl) (.n , refl) = refl
subtraction-is-prop (succ m) zero (k , ()) (k' , p')
subtraction-is-prop (succ m) (succ n) (k , p) (k' , p') = to-Î£-â‰¡ (ap prâ‚ IH , â„•-is-set _ _)
 where
  IH : k , succ-lc p â‰¡ k' , succ-lc p'
  IH = subtraction-is-prop m n (k , succ-lc p) (k' , succ-lc p')

subtraction : (m n : â„•) â†’ m â‰¤ n â†’ Î£ \(k : â„•) â†’ k +' m â‰¡ n
subtraction zero n l = n , refl
subtraction (succ m) zero ()
subtraction (succ m) (succ n) l = prâ‚ IH , ap succ (prâ‚‚ IH)
 where
  IH : Î£ \(k : â„•) â†’ k +' m â‰¡ n
  IH = subtraction m n l

cosubtraction : (m n : â„•) â†’ (Î£ \(k : â„•) â†’ k +' m â‰¡ n) â†’ m â‰¤ n
cosubtraction zero n (.n , refl) = *
cosubtraction (succ m) zero (k , ())
cosubtraction (succ m) (succ .(k +' m)) (k , refl) = cosubtraction m (k +' m) (k , refl)

zero-minimal : (n : â„•) â†’ zero â‰¤ n
zero-minimal n = *

succ-monotone : (m n : â„•) â†’ m â‰¤ n â†’ succ m â‰¤ succ n
succ-monotone m n l = l

succ-order-injective : (m n : â„•) â†’ succ m â‰¤ succ n â†’ m â‰¤ n
succ-order-injective m n l = l

â‰¤-induction : {U : Universe} (P : (m n : â„•) (l : m â‰¤ n) â†’ U Ì‡)
            â†’ ((n : â„•) â†’ P zero n (zero-minimal n))
            â†’ ((m n : â„•) (l : m â‰¤ n) â†’ P m n l â†’ P (succ m) (succ n) (succ-monotone m n l))
            â†’ (m n : â„•) (l : m â‰¤ n) â†’ P m n l
â‰¤-induction P base step zero n *            = base n
â‰¤-induction P base step (succ m) zero ()
â‰¤-induction P base step (succ m) (succ n) l = step m n l (â‰¤-induction P base step m n l)

succâ‰¤â‰¡ : (m n : â„•) â†’ (succ m â‰¤ succ n) â‰¡ (m â‰¤ n)
succâ‰¤â‰¡ m n = refl

â‰¤-refl : (n : â„•) â†’ n â‰¤ n
â‰¤-refl zero     = *
â‰¤-refl (succ n) = â‰¤-refl n

â‰¤-trans : (l m n : â„•) â†’ l â‰¤ m â†’ m â‰¤ n â†’ l â‰¤ n
â‰¤-trans zero m n p q = *
â‰¤-trans (succ l) zero n () q
â‰¤-trans (succ l) (succ m) zero p ()
â‰¤-trans (succ l) (succ m) (succ n) p q = â‰¤-trans l m n p q

â‰¤-anti : (m n : â„•) â†’ m â‰¤ n â†’ n â‰¤ m â†’ m â‰¡ n
â‰¤-anti zero zero p q = refl
â‰¤-anti zero (succ n) p ()
â‰¤-anti (succ m) zero () q
â‰¤-anti (succ m) (succ n) p q = ap succ (â‰¤-anti m n p q)

â‰¤-succ : (n : â„•) â†’ n â‰¤ succ n
â‰¤-succ zero     = *
â‰¤-succ (succ n) = â‰¤-succ n

unique-minimal : (n : â„•) â†’ n â‰¤ zero â†’ n â‰¡ zero
unique-minimal zero l = refl
unique-minimal (succ n) ()

â‰¤-split : (m n : â„•) â†’ m â‰¤ succ n â†’ (m â‰¤ n) + (m â‰¡ succ n)
â‰¤-split zero n l = inl l
â‰¤-split (succ m) zero l = inr (ap succ (unique-minimal m l))
â‰¤-split (succ m) (succ n) l = cases inl (inr âˆ˜ (ap succ)) (â‰¤-split m n l)

â‰¤-join : (m n : â„•) â†’ (m â‰¤ n) + (m â‰¡ succ n) â†’ m â‰¤ succ n
â‰¤-join m n (inl l) = â‰¤-trans m n (succ n) l (â‰¤-succ n)
â‰¤-join .(succ n) n (inr refl) = â‰¤-refl n

_<_ : â„• â†’ â„• â†’ Uâ‚€ Ì‡
m < n = succ m â‰¤ n

not-less-bigger-or-equal : (m n : â„•) â†’ Â¬(n < m) â†’ m â‰¤ n
not-less-bigger-or-equal zero n u = zero-minimal n
not-less-bigger-or-equal (succ m) zero = double-negation-intro (zero-minimal m)
not-less-bigger-or-equal (succ m) (succ n) = not-less-bigger-or-equal m n

bounded-âˆ€-next : âˆ€ {U} (A : â„• â†’ U Ì‡) (k : â„•)
               â†’ A k
               â†’ ((n : â„•) â†’ n < k â†’ A n)
               â†’ (n : â„•) â†’ n < succ k â†’ A n
bounded-âˆ€-next A k a Ï† n l = cases f g s
 where
  s : (n < k) + (succ n â‰¡ succ k)
  s = â‰¤-split (succ n) k l
  f : n < k â†’ A n
  f = Ï† n
  g : succ n â‰¡ succ k â†’ A n
  g p = back-transport A (succ-lc p) a

\end{code}

Added 20th June 2018:

\begin{code}

<-is-prop-valued : (m n : â„•) â†’ is-prop(m < n)
<-is-prop-valued m n = â‰¤-is-prop-valued (succ m) n

<-coarser-than-â‰¤ : (m n : â„•) â†’ m < n â†’ m â‰¤ n
<-coarser-than-â‰¤ m n = â‰¤-trans m (succ m) n (â‰¤-succ m)

<-trans : (l m n : â„•) â†’ l < m â†’ m < n â†’ l < n
<-trans l m n u v = â‰¤-trans (succ l) m n u (<-coarser-than-â‰¤ m n v)

<-split : (m n : â„•) â†’ m < succ n â†’ (m < n) + (m â‰¡ n)
<-split m zero     l = inr (unique-minimal m l)
<-split m (succ n) l = â‰¤-split m n l

regress : âˆ€ {U} (P : â„• â†’ U Ì‡)
        â†’ ((n : â„•) â†’ P (succ n) â†’ P n)
        â†’ (n m : â„•) â†’ m â‰¤ n â†’ P n â†’ P m
regress P Ï zero m l p = back-transport P (unique-minimal m l) p
regress P Ï (succ n) m l p = cases (Î» (l' : m â‰¤ n) â†’ IH m l' (Ï n p))
                                    (Î» (r : m â‰¡ succ n) â†’ back-transport P r p)
                                    (â‰¤-split m n l)
 where
  IH : (m : â„•) â†’ m â‰¤ n â†’ P n â†’ P m
  IH = regress P Ï n

<-is-well-founded : (m : â„•) â†’ is-accessible _<_ m
<-is-well-founded zero     = next zero     (Î» y l â†’ unique-from-ðŸ˜ l)
<-is-well-founded (succ m) = next (succ m) (Ï„ (<-is-well-founded m))
 where
  Ï„ : is-accessible _<_ m â†’ (n : â„•) â†’ n < succ m â†’ is-accessible _<_ n
  Ï„ a n u = cases (Î» (v : n < m) â†’ prev _<_ m a n v)
                  (Î» (p : n â‰¡ m) â†’ back-transport (is-accessible _<_) p a)
                  (<-split n m u)

course-of-values-induction : âˆ€ {U} (P : â„• â†’ U Ì‡)
                           â†’ ((n : â„•) â†’ ((m : â„•) â†’ m < n â†’ P m) â†’ P n)
                           â†’ (n : â„•) â†’ P n
course-of-values-induction = transfinite-induction _<_ <-is-well-founded

<-is-extensional : is-extensional _<_
<-is-extensional zero     zero     f g = refl
<-is-extensional zero     (succ n) f g = unique-from-ðŸ˜ (g zero (zero-minimal n))
<-is-extensional (succ m) (zero)   f g = unique-from-ðŸ˜ (f zero (zero-minimal m))
<-is-extensional (succ m) (succ n) f g = ap succ (â‰¤-anti m n (f m (â‰¤-refl m)) (g n (â‰¤-refl n)))

â„•-ordinal : is-well-order _<_
â„•-ordinal = <-is-prop-valued , <-is-well-founded , <-is-extensional , <-trans

\end{code}

Induction on z, then y, then x:

\begin{code}

â„•-cotransitive : cotransitive _<_
â„•-cotransitive zero y zero l = inr l
â„•-cotransitive (succ x) y zero l = inr (â‰¤-trans 1 (succ(succ x)) y * l)
â„•-cotransitive x zero (succ z) ()
â„•-cotransitive zero (succ y) (succ z) l = inl (zero-minimal y)
â„•-cotransitive (succ x) (succ y) (succ z) l = Î³ IH
 where
  IH : (x < z) + (z < y)
  IH = â„•-cotransitive x y z l
  Î³ : (x < z) + (z < y) â†’ (succ x < succ z) + (succ z < succ y)
  Î³ (inl l) = inl (succ-monotone (succ x) z l)
  Î³ (inr r) = inr (succ-monotone (succ z) y r)

\end{code}
