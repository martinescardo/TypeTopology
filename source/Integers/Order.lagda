Andrew Sneap, 26th November 2021

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan renaming (_+_ to _âˆ”_)

open import Naturals.AbsoluteDifference using (âˆ£_-_âˆ£)
open import Naturals.Order
open import Notation.Order
open import UF.Base
open import UF.Subsingletons

open import Integers.Abs
open import Integers.Type
open import Integers.Addition
open import Integers.Multiplication
open import Integers.Negation
open import Naturals.Addition renaming (_+_ to _â„•+_)

module Integers.Order where

\end{code}

First, the definition of < and â‰¤ for the integers. See the NaturalsOrder import
to see how < is defined similarly to < for the natural numbers.  Following the
definitions are the proofs that the relations are propositions, and some simple
proofs for each.

\begin{code}

_â‰¤â„¤_ _â‰¥â„¤_ : (x y : â„¤) â†’ ğ“¤â‚€ Ì‡
x â‰¤â„¤ y = Î£ n ê‰ â„• , x + pos n ï¼ y
x â‰¥â„¤ y = y â‰¤â„¤ x

_<â„¤_ _>â„¤_ : (x y : â„¤) â†’ ğ“¤â‚€ Ì‡
x <â„¤ y = succâ„¤ x â‰¤â„¤ y
x >â„¤ y = y <â„¤ x

instance
 Order-â„¤-â„¤ : Order â„¤ â„¤
 _â‰¤_ {{Order-â„¤-â„¤}} = _â‰¤â„¤_

 Strict-Order-â„¤-â„¤ : Strict-Order â„¤ â„¤
 _<_ {{Strict-Order-â„¤-â„¤}} = _<â„¤_

 Strict-Order-Chain-â„¤-â„¤-â„¤ : Strict-Order-Chain â„¤ â„¤ â„¤ _<_ _<_
 _<_<_ {{Strict-Order-Chain-â„¤-â„¤-â„¤}} p q r = (p < q) Ã— (q < r)

 Order-Chain-â„¤-â„¤-â„¤ : Order-Chain â„¤ â„¤ â„¤ _â‰¤_ _â‰¤_
 _â‰¤_â‰¤_ {{Order-Chain-â„¤-â„¤-â„¤}} p q r = (p â‰¤ q) Ã— (q â‰¤ r)

â„¤â‰¤-is-prop : (x y : â„¤) â†’ is-prop (x â‰¤ y)
â„¤â‰¤-is-prop x y (n , p) (m , q) = to-subtype-ï¼ I II
 where
  I : (k : â„•) â†’ (Î± Î² : x + pos k ï¼ y) â†’ Î± ï¼ Î²
  I _ = â„¤-is-set

  II : n ï¼ m
  II = pos-lc (â„¤+-lc (pos n) (pos m) x Î³)
   where
    Î³ : x + pos n ï¼ x + pos m
    Î³ = x + pos n ï¼âŸ¨ p    âŸ©
        y         ï¼âŸ¨ q â»Â¹ âŸ©
        x + pos m âˆ

â„¤<-is-prop : (x y : â„¤) â†’ is-prop (x < y)
â„¤<-is-prop x = â„¤â‰¤-is-prop (succâ„¤ x)

â‰¤-incrâ„¤ : (x : â„¤) â†’ x â‰¤ succâ„¤ x
â‰¤-incrâ„¤ x = 1 , refl

<-incrâ„¤ : (x : â„¤) â†’ x < succâ„¤ x
<-incrâ„¤ x = 0 , refl

â‰¤-predâ„¤ : (x : â„¤) â†’ predâ„¤ x â‰¤ x
â‰¤-predâ„¤ x = 1 , succpredâ„¤ x

â‰¤-predâ„¤' : (x y : â„¤) â†’ x â‰¤ y â†’ predâ„¤ x â‰¤ predâ„¤ y
â‰¤-predâ„¤' x y (n , e) = n , Î³
 where
  Î³ : predâ„¤ x + pos n ï¼ predâ„¤ y
  Î³ = predâ„¤ x + pos n   ï¼âŸ¨ â„¤-left-pred x (pos n) âŸ©
      predâ„¤ (x + pos n) ï¼âŸ¨ ap predâ„¤ e            âŸ©
      predâ„¤ y âˆ

<-predâ„¤ : (x : â„¤) â†’ predâ„¤ x < x
<-predâ„¤ x = 0 , succpredâ„¤ x

<-is-â‰¤ : (x y : â„¤) â†’ x < y â†’ x â‰¤ y
<-is-â‰¤ x y (a , p) = succ a , Î³
 where
  Î³ : succâ„¤ (x + pos a) ï¼ y
  Î³ = succâ„¤ (x + pos a) ï¼âŸ¨ â„¤-left-succ x (pos a) â»Â¹ âŸ©
      succâ„¤ x + pos a   ï¼âŸ¨ p                        âŸ©
      y                 âˆ

â„•-order-respects-â„¤-order : (m n : â„•) â†’ m < n â†’ pos m < pos n
â„•-order-respects-â„¤-order m n l = I (subtraction'' m n l)
 where
  I : (Î£ k ê‰ â„• , succ k â„•+ m ï¼ n) â†’ pos m < pos n
  I (k , e) = k , II
   where
    II : succâ„¤ (pos m) + pos k ï¼ pos n
    II = succâ„¤ (pos m) + pos k ï¼âŸ¨ distributivity-pos-addition (succ m) k     âŸ©
         pos (succ m â„•+ k)     ï¼âŸ¨ ap pos (addition-commutativity (succ m) k) âŸ©
         pos (k â„•+ succ m)     ï¼âŸ¨ ap pos (succ-left k m â»Â¹)                  âŸ©
         pos (succ k â„•+ m)     ï¼âŸ¨ ap pos e                                   âŸ©
         pos n                 âˆ

â„•-order-respects-â„¤-order' : (m n : â„•) â†’ m < n â†’ negsucc n < negsucc m
â„•-order-respects-â„¤-order' m n l = I (subtraction'' m n l)
 where
  I : (Î£ k ê‰ â„• , succ k â„•+ m ï¼ n) â†’ negsucc n < negsucc m
  I (k , e) = k , Î³
   where
    k' = pos (succ k)
    m' = pos (succ m)
    n' = pos (succ n)

    II : k' + m' ï¼ n'
    II = k' + m'                ï¼âŸ¨ i  âŸ©
         pos (succ k â„•+ succ m) ï¼âŸ¨ ii âŸ©
         n'                     âˆ
     where
      i  = distributivity-pos-addition (succ k) (succ m)
      ii = ap (pos âˆ˜ succ) e

    Î³ : succâ„¤ (negsucc n) + pos k ï¼ negsucc m
    Î³ = succâ„¤ (negsucc n) + pos k         ï¼âŸ¨ i    âŸ©
        negsucc n + k'                    ï¼âŸ¨ ii   âŸ©
        k' + negsucc n                    ï¼âŸ¨ iii  âŸ©
        k' + negsucc n + pos 0            ï¼âŸ¨ iv   âŸ©
        k' + negsucc n + (m' + negsucc m) ï¼âŸ¨ v    âŸ©
        k' + negsucc n + m' + negsucc m   ï¼âŸ¨ vi   âŸ©
        k' + (negsucc n + m') + negsucc m ï¼âŸ¨ vii  âŸ©
        k' + (m' + negsucc n) + negsucc m ï¼âŸ¨ viii âŸ©
        k' + m' + negsucc n + negsucc m   ï¼âŸ¨ ix   âŸ©
        k' + m' + (negsucc n + negsucc m) ï¼âŸ¨ x    âŸ©
        n' + (negsucc n + negsucc m)      ï¼âŸ¨ xi   âŸ©
        n' + negsucc n + negsucc m        ï¼âŸ¨ xii  âŸ©
        pos 0 + negsucc m                 ï¼âŸ¨ xiii âŸ©
        negsucc m                         âˆ
      where
       ivâ‚â‚š : pos 0 ï¼ m' - m'
       ivâ‚â‚š = â„¤-sum-of-inverse-is-zero m' â»Â¹

       i    = â„¤-left-succ (negsucc n) (pos k)
       ii   = â„¤+-comm (negsucc n) k'
       iii  = â„¤-zero-right-neutral (k' + negsucc n)
       iv   = ap ((k' + negsucc n) +_) ivâ‚â‚š
       v    = â„¤+-assoc (k' + negsucc n) m' (negsucc m) â»Â¹
       vi   = ap (_+ negsucc m) (â„¤+-assoc k' (negsucc n) m')
       vii  = ap (Î» p â†’ k' + p + negsucc m) (â„¤+-comm (negsucc n) m')
       viii = ap (_+ negsucc m) (â„¤+-assoc k' m' (negsucc n) â»Â¹)
       ix   = â„¤+-assoc (k' + m') (negsucc n) (negsucc m)
       x    = ap (Î» p â†’ p + (negsucc n + negsucc m)) II
       xi   = â„¤+-assoc n' (negsucc n) (negsucc m) â»Â¹
       xii  = ap (_+ negsucc m) (â„¤-sum-of-inverse-is-zero n')
       xiii = â„¤-zero-left-neutral (negsucc m)

â„¤-bigger-or-equal-not-less : (x y : â„¤) â†’ x â‰¤ y â†’ Â¬ (y < x)
â„¤-bigger-or-equal-not-less x y (Î± , p) (Î² , q) = ğŸ˜-elim Î³
 where
  I : x + succâ„¤ (pos (Î± â„•+ Î²)) ï¼ x + pos 0
  I = x + succâ„¤ (pos (Î± â„•+ Î²))    ï¼âŸ¨ i             âŸ©
      x + succâ„¤ (pos Î± + pos Î²)   ï¼âŸ¨ ii            âŸ©
      succâ„¤ (x + (pos Î± + pos Î²)) ï¼âŸ¨ iii           âŸ©
      succâ„¤ (x + pos Î± + pos Î²)   ï¼âŸ¨ iv            âŸ©
      succâ„¤ (x + pos Î±) + pos Î²   ï¼âŸ¨ v             âŸ©
      x                           ï¼âŸ¨ by-definition âŸ©
      x + pos 0                   âˆ
   where
    i   = ap (Î» - â†’ x + succâ„¤ -) (distributivity-pos-addition Î± Î² â»Â¹)
    ii  = â„¤-right-succ x (pos Î± + pos Î²)
    iii = ap succâ„¤ (â„¤+-assoc x (pos Î±) (pos Î²) â»Â¹)
    iv  = â„¤-left-succ (x + pos Î±) (pos Î²) â»Â¹
    v   = transport (Î» - â†’ succâ„¤ - + (pos Î²) ï¼ x) (p â»Â¹) q

  II : succâ„¤ (pos (Î± â„•+ Î²)) ï¼ pos 0
  II = â„¤+-lc (succâ„¤ (pos (Î± â„•+ Î²))) (pos 0) x I

  Î³ : ğŸ˜
  Î³ = pos-succ-not-zero (Î± â„•+ Î²) II

â„¤â‰¤-split : (x y : â„¤) â†’ x â‰¤ y â†’ (x < y) âˆ” (x ï¼ y)
â„¤â‰¤-split x y (zero , p)   = inr p
â„¤â‰¤-split x y (succ a , p) = inl (a , (â„¤-left-succ x (pos a)  âˆ™ p))

â„¤â‰¤-trans : (x y z : â„¤) â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
â„¤â‰¤-trans x y z (a , p) (b , q) = a â„•+ b , I
 where
  I : x + pos (a â„•+ b) ï¼ z
  I = x + pos (a â„•+ b)    ï¼âŸ¨ ap (x +_) (distributivity-pos-addition a b â»Â¹) âŸ©
      x + (pos a + pos b) ï¼âŸ¨ â„¤+-assoc x (pos a) (pos b) â»Â¹                  âŸ©
      x + pos a + pos b   ï¼âŸ¨ ap (_+ (pos b)) p                              âŸ©
      y + (pos b)         ï¼âŸ¨ q                                              âŸ©
      z                   âˆ

â„¤<-trans : (x y z : â„¤) â†’ x < y â†’ y < z â†’ x < z
â„¤<-trans x y z lâ‚ lâ‚‚ = â„¤â‰¤-trans (succâ„¤ x) (succâ„¤ y) z I lâ‚‚
 where
  I : succâ„¤ x â‰¤ succâ„¤ y
  I = â„¤â‰¤-trans (succâ„¤ x) y (succâ„¤ y) lâ‚ (â‰¤-incrâ„¤ y)

â„¤<-â‰¤-trans : (x y z : â„¤) â†’ x < y â†’ y â‰¤ z â†’ x < z
â„¤<-â‰¤-trans x y z lâ‚ lâ‚‚ = cases Î³â‚ Î³â‚‚ I
 where
  I : (y < z) âˆ” (y ï¼ z)
  I = â„¤â‰¤-split y z lâ‚‚

  Î³â‚ : y < z â†’ x < z
  Î³â‚ lâ‚ƒ = â„¤<-trans x y z lâ‚ lâ‚ƒ

  Î³â‚‚ : y ï¼ z â†’ x < z
  Î³â‚‚ e = transport (x <_) e lâ‚

â„¤â‰¤-<-trans : (x y z : â„¤) â†’ x â‰¤ y â†’ y < z â†’ x < z
â„¤â‰¤-<-trans x y z lâ‚ lâ‚‚ = cases Î³â‚ Î³â‚‚ I
 where
  I : (x < y) âˆ” (x ï¼ y)
  I = â„¤â‰¤-split x y lâ‚

  Î³â‚ : x < y â†’ x < z
  Î³â‚ lâ‚ƒ = â„¤<-trans x y z lâ‚ƒ lâ‚‚

  Î³â‚‚ : x ï¼ y â†’ x < z
  Î³â‚‚ e = transport (_< z) (e â»Â¹) lâ‚‚

â„¤â‰¤-refl : (x : â„¤) â†’ x â‰¤ x
â„¤â‰¤-refl x = 0 , refl

â„¤-equal-not-less-than : (x : â„¤) â†’ Â¬ (x < x)
â„¤-equal-not-less-than x (0 , Î±)      = succâ„¤-no-fp x (Î± â»Â¹)
â„¤-equal-not-less-than x (succ n , Î±) = pos-succ-not-zero (n â„•+ 1) Î³
 where
  I : x + succâ„¤ (succâ„¤ (pos n)) ï¼ x + pos 0
  I = x + succâ„¤ (succâ„¤ (pos n))  ï¼âŸ¨ â„¤-right-succ x (succâ„¤ (pos n))   âŸ©
      succâ„¤ (x + succâ„¤ (pos n))  ï¼âŸ¨ â„¤-left-succ x (succâ„¤ (pos n)) â»Â¹ âŸ©
      succâ„¤ x + succâ„¤ (pos n)    ï¼âŸ¨ by-definition                    âŸ©
      succâ„¤ x + pos (succ n)     ï¼âŸ¨ Î±                                âŸ©
      x                          ï¼âŸ¨ â„¤-zero-right-neutral x           âŸ©
      x + pos 0                  âˆ

  Î³ : succâ„¤ (succâ„¤ (pos n)) ï¼ pos 0
  Î³ = â„¤+-lc (succâ„¤ (succâ„¤ (pos n))) (pos 0) x I

â„¤-zero-less-than-pos : (n : â„•) â†’ pos 0 < pos (succ n)
â„¤-zero-less-than-pos n = â„•-order-respects-â„¤-order 0 (succ n) (zero-least n)

â„¤-zero-least-pos : (n : â„•) â†’ pos 0 â‰¤ pos n
â„¤-zero-least-pos 0 = â„¤â‰¤-refl (pos 0)
â„¤-zero-least-pos (succ n) = Î³
 where
  I : pos 0 â‰¤ pos n
  I = â„¤-zero-least-pos n

  II : pos n â‰¤ pos (succ n)
  II = â‰¤-incrâ„¤ (pos n)

  Î³ : pos 0 â‰¤ pos (succ n)
  Î³ = â„¤â‰¤-trans (pos 0) (pos n) (pos (succ n)) I II

negative-less-than-positive : (x y : â„•) â†’ negsucc x < pos y
negative-less-than-positive x y = (x â„•+ y) , I
 where
  I : succâ„¤ (negsucc x) + pos (x â„•+ y) ï¼ pos y
  I = succâ„¤ (negsucc x) + pos (x â„•+ y)        ï¼âŸ¨ i    âŸ©
      succâ„¤ (negsucc x) + (pos x + pos y)     ï¼âŸ¨ ii   âŸ©
      succâ„¤ (negsucc x) + pos x + pos y       ï¼âŸ¨ iii  âŸ©
      negsucc x + pos (succ x) + pos y        ï¼âŸ¨ refl âŸ©
      (- pos (succ x)) + pos (succ x) + pos y ï¼âŸ¨ iv   âŸ©
      pos 0 + pos y                           ï¼âŸ¨ v    âŸ©
      pos y                                   âˆ
   where
    i   = ap (succâ„¤ (negsucc x) +_) (distributivity-pos-addition x y â»Â¹)
    ii  = â„¤+-assoc (succâ„¤ (negsucc x)) (pos x) (pos y) â»Â¹
    iii = ap (_+ pos y) (â„¤-left-succ (negsucc x) (pos x))
    iv  = ap (_+ pos y) (â„¤-sum-of-inverse-is-zero' (pos (succ x)))
    v   = â„¤-zero-left-neutral (pos y)

â„¤â‰¤-swap : (x y : â„¤) â†’ x â‰¤ y â†’ - y â‰¤ - x
â„¤â‰¤-swap x y (k , e) = k , â„¤+-lc ((- y) + pos k) (- x) (y + x) I
 where
  I : y + x + ((- y) + pos k) ï¼ y + x - x
  I = y + x + ((- y) + pos k) ï¼âŸ¨ i             âŸ©
      x + y + ((- y) + pos k) ï¼âŸ¨ ii            âŸ©
      x + y - y + pos k       ï¼âŸ¨ iii           âŸ©
      x + (y - y) + pos k     ï¼âŸ¨ iv            âŸ©
      x + pos 0 + pos k       ï¼âŸ¨ by-definition âŸ©
      x + pos k               ï¼âŸ¨ e             âŸ©
      y                       ï¼âŸ¨ by-definition âŸ©
      y + pos 0               ï¼âŸ¨ v             âŸ©
      y + (x - x)             ï¼âŸ¨ vi            âŸ©
      y + x - x               âˆ
   where
    i   = ap (_+ ((- y) + pos k)) (â„¤+-comm y x)
    ii  = â„¤+-assoc (x + y) (- y) (pos k) â»Â¹
    iii = ap (_+ pos k) (â„¤+-assoc x y (- y))
    iv  = ap (Î» Î± â†’ x + Î± + (pos k)) (â„¤-sum-of-inverse-is-zero y)
    v   = ap (y +_) (â„¤-sum-of-inverse-is-zero x â»Â¹)
    vi  = â„¤+-assoc y x (- x) â»Â¹

â„¤â‰¤-swapâ‚‚ : (x y z : â„¤) â†’ x â‰¤ y â‰¤ z â†’ (- y â‰¤ - x) Ã— (- z â‰¤ - y)
â„¤â‰¤-swapâ‚‚ x y z (lâ‚ , lâ‚‚) = (â„¤â‰¤-swap x y lâ‚) , (â„¤â‰¤-swap y z lâ‚‚)

â„•â‰¤-to-â„¤â‰¤ : (x y : â„•) â†’ x â‰¤ y â†’ pos x â‰¤ pos y
â„•â‰¤-to-â„¤â‰¤ x y l = I (subtraction x y l)
 where
  I : (Î£ k ê‰ â„• , k â„•+ x ï¼ y) â†’ pos x â‰¤ pos y
  I (k , e) = k , II
   where
    II : pos x + pos k ï¼ pos y
    II = pos x + pos k ï¼âŸ¨ distributivity-pos-addition x k     âŸ©
         pos (x â„•+ k)  ï¼âŸ¨ ap pos (addition-commutativity x k) âŸ©
         pos (k â„•+ x)  ï¼âŸ¨ ap pos e                            âŸ©
         pos y         âˆ

â„¤-dichotomous : (x y : â„¤) â†’ (x â‰¤ y) âˆ” (y â‰¤ x)
â„¤-dichotomous (pos x) (pos y) = I (â‰¤-dichotomous x y)
 where
  I : (x â‰¤ y) âˆ” (y â‰¤ x) â†’ (pos x â‰¤ pos y) âˆ” (pos y â‰¤ pos x)
  I (inl l) = inl (â„•â‰¤-to-â„¤â‰¤ x y l)
  I (inr r) = inr (â„•â‰¤-to-â„¤â‰¤ y x r)
â„¤-dichotomous (pos x) (negsucc y) = inr (negative-less-than-positive (succ y) x)
â„¤-dichotomous (negsucc x) (pos y) = inl (negative-less-than-positive (succ x) y)
â„¤-dichotomous (negsucc x) (negsucc y) = Cases (â‰¤-dichotomous x y) Î³â‚ Î³â‚‚
 where
  I : (a b : â„•) â†’ a â‰¤ b â†’ negsucc b â‰¤ negsucc a
  I a b l = â„¤â‰¤-swap (pos (succ a)) (pos (succ b)) II
   where
    II : pos (succ a) â‰¤ pos (succ b)
    II = â„•â‰¤-to-â„¤â‰¤ (succ a) (succ b) l

  Î³â‚ : x â‰¤ y â†’ (negsucc x â‰¤ negsucc y) âˆ” (negsucc y â‰¤ negsucc x)
  Î³â‚ l = inr (I x y l)

  Î³â‚‚ : y â‰¤ x â†’ (negsucc x â‰¤ negsucc y) âˆ” (negsucc y â‰¤ negsucc x)
  Î³â‚‚ l = inl (I y x l)

trich-locate : (x y : â„¤) â†’ ğ“¤â‚€ Ì‡
trich-locate x y = (x < y) âˆ” (x ï¼ y) âˆ” (y < x)

â„¤-trichotomous : (x y : â„¤) â†’ trich-locate x y
â„¤-trichotomous x y = I (â„¤-dichotomous x y)
 where
  I : (x â‰¤ y) âˆ” (y â‰¤ x) â†’ (x < y) âˆ” (x ï¼ y) âˆ” (y < x)
  I (inl l) = II (â„¤â‰¤-split x y l)
   where
    II : (x < y) âˆ” (x ï¼ y) â†’ (x < y) âˆ” (x ï¼ y) âˆ” (y < x)
    II (inl l) = inl l
    II (inr r) = inr (inl r)
  I (inr r) = II (â„¤â‰¤-split y x r)
   where
    II : (y < x) âˆ” (y ï¼ x) â†’ (x < y) âˆ” (x ï¼ y) âˆ” (y < x)
    II (inl l) = inr (inr l)
    II (inr r) = inr (inl (r â»Â¹))

â„¤-dichotomous' : (x y : â„¤) â†’ x < y âˆ” y â‰¤ x
â„¤-dichotomous' x y = I (â„¤-trichotomous x y)
 where
  I : (x < y) âˆ” (x ï¼ y) âˆ” (y < x) â†’ x < y âˆ” y â‰¤ x
  I (inl x<y) = inl x<y
  I (inr (inl xï¼y)) = inr (transport (_â‰¤ x) xï¼y (â„¤â‰¤-refl x))
  I (inr (inr y<x)) = inr (<-is-â‰¤ y x y<x)

â„¤-trichotomous-is-prop : (x y : â„¤) â†’ is-prop (trich-locate x y)
â„¤-trichotomous-is-prop x y = +-is-prop I II Î³
 where
  I : is-prop (x < y)
  I = â„¤<-is-prop x y

  II : is-prop ((x ï¼ y) âˆ” y < x)
  II = +-is-prop â„¤-is-set (â„¤<-is-prop y x) III
   where
    III : x ï¼ y â†’ Â¬ (y < x)
    III e l = â„¤-equal-not-less-than y (transport (y <_) e l)

  Î³ : x < y â†’ Â¬ ((x ï¼ y) âˆ” y < x)
  Î³ l (inl e ) = â„¤-equal-not-less-than x (transport (x <_) (e â»Â¹) l)
  Î³ l (inr l') = â„¤-bigger-or-equal-not-less x y (<-is-â‰¤ x y l) l'

â„¤â‰¤-adding : (a b c d : â„¤) â†’ a â‰¤ b â†’ c â‰¤ d â†’ a + c â‰¤ b + d
â„¤â‰¤-adding a b c d (p , Î²) (q , Î²') = (p â„•+ q) , I
 where
  I : a + c + pos (p â„•+ q) ï¼ b + d
  I = a + c + pos (p â„•+ q)    ï¼âŸ¨ i    âŸ©
      a + c + (pos p + pos q) ï¼âŸ¨ ii   âŸ©
      a + c + pos p + pos q   ï¼âŸ¨ iii  âŸ©
      c + a + pos p + pos q   ï¼âŸ¨ iv   âŸ©
      c + (a + pos p) + pos q ï¼âŸ¨ v    âŸ©
      c + b + pos q           ï¼âŸ¨ vi   âŸ©
      b + c + pos q           ï¼âŸ¨ vii  âŸ©
      b + (c + pos q)         ï¼âŸ¨ viii âŸ©
      b + d                   âˆ
   where
    i    = ap (a + c +_) (distributivity-pos-addition p q â»Â¹)
    ii   = â„¤+-assoc (a + c) (pos p) (pos q) â»Â¹
    iii  = ap (Î» z â†’ z + pos p + pos q) (â„¤+-comm a c)
    iv   = ap (_+ pos q) (â„¤+-assoc c a (pos p))
    v    = ap (Î» z â†’ c + z + pos q) Î²
    vi   = ap (_+ pos q) (â„¤+-comm c b)
    vii  = â„¤+-assoc b c (pos q)
    viii = ap (b +_) Î²'

â„¤<-adding : (a b c d : â„¤) â†’ a < b â†’ c < d â†’ a + c < b + d
â„¤<-adding a b c d lâ‚ lâ‚‚ = â„¤<-trans (a + c) (a + succâ„¤ c) (b + d) II III
 where
  I : succâ„¤ a + succâ„¤ c â‰¤ b + d
  I = â„¤â‰¤-adding (succâ„¤ a) b (succâ„¤ c) d lâ‚ lâ‚‚

  II : a + c < a + succâ„¤ c
  II = 0 , (â„¤-right-succ a c â»Â¹)

  III : a + succâ„¤ c < b + d
  III = transport (_â‰¤ b + d) (â„¤-left-succ a (succâ„¤ c)) I

â„¤â‰¤-adding' :  (a b c : â„¤) â†’ a â‰¤ b â†’ a + c â‰¤ b + c
â„¤â‰¤-adding' a b c (k , p) = k , I
 where
  I : a + c + pos k ï¼ b + c
  I = a + c + pos k   ï¼âŸ¨ â„¤+-assoc a c (pos k)          âŸ©
      a + (c + pos k) ï¼âŸ¨ ap (a +_) (â„¤+-comm c (pos k)) âŸ©
      a + (pos k + c) ï¼âŸ¨ â„¤+-assoc a (pos k) c â»Â¹       âŸ©
      a + pos k + c   ï¼âŸ¨ ap (_+ c) p                   âŸ©
      b + c           âˆ

â„¤â‰¤-adding-left : (a b c : â„¤) â†’ a â‰¤ b â†’ c + a â‰¤ c + b
â„¤â‰¤-adding-left a b c l = transportâ‚‚ _â‰¤_ I II III
 where
  I : a + c ï¼ c + a
  I = â„¤+-comm a c

  II : b + c ï¼ c + b
  II = â„¤+-comm b c

  III : a + c â‰¤ b + c
  III = â„¤â‰¤-adding' a b c l

â„¤â‰¤-addingâ‚‚ : (a b c d : â„¤) â†’ a â‰¤ b â‰¤ c â†’ (a + d â‰¤ b + d â‰¤ c + d)
â„¤â‰¤-addingâ‚‚ a b c d (lâ‚ , lâ‚‚) = (â„¤â‰¤-adding' a b d lâ‚) , (â„¤â‰¤-adding' b c d lâ‚‚)

â„¤<-adding' : (a b c : â„¤) â†’ a < b â†’ a + c < b + c
â„¤<-adding' a b c (k , p) = I (â„¤â‰¤-adding' (succâ„¤ a) b c (k , p))
 where
  I : succâ„¤ a + c â‰¤ b + c â†’ a + c < b + c
  I (h , q) = h , II
   where
    II : succâ„¤ (a + c) + pos h ï¼ b + c
    II = succâ„¤ (a + c) + pos h ï¼âŸ¨ ap (_+ (pos h)) (â„¤-left-succ a c â»Â¹) âŸ©
         succâ„¤ a + c + pos h   ï¼âŸ¨ q                                    âŸ©
         b + c                 âˆ

â„¤<-adding-left : (a b c : â„¤) â†’ a < b â†’ c + a < c + b
â„¤<-adding-left a b c l = transportâ‚‚ _<_ I II III
 where
  I : a + c ï¼ c + a
  I = â„¤+-comm a c

  II : b + c ï¼ c + b
  II = â„¤+-comm b c

  III : a + c < b + c
  III = â„¤<-adding' a b c l

â„¤<-adding'' : (a b c : â„¤) â†’ a < b â†’ c + a < c + b
â„¤<-adding'' a b c l = transportâ‚‚ _<_ (â„¤+-comm a c) (â„¤+-comm b c) I
 where
  I : a + c < b + c
  I = â„¤<-adding' a b c l

pmpo-lemma : (a b : â„¤) â†’ (n : â„•) â†’ a < b â†’  a * pos (succ n) < b * pos (succ n)
pmpo-lemma a b = induction base step
 where
  base : a < b
       â†’ a * pos 1 < b * pos 1
  base z = z

  step : (k : â„•)
       â†’ (a < b â†’ a * pos (succ k) < b * pos (succ k))
       â†’ a < b
       â†’ a * pos (succ (succ k)) < b * pos (succ (succ k))
  step k IH l = â„¤<-adding a b (a + (a * pos k)) (b + (b * pos k)) l (IH l)

positive-multiplication-preserves-order : (a b c : â„¤)
                                        â†’ is-pos-succ c
                                        â†’ a < b
                                        â†’ a * c < b * c
positive-multiplication-preserves-order a b (negsucc x)    p l = ğŸ˜-elim p
positive-multiplication-preserves-order a b (pos 0)        p l = ğŸ˜-elim p
positive-multiplication-preserves-order a b (pos (succ x)) p l = pmpo-lemma a b x l

positive-multiplication-preserves-order' : (a b c : â„¤)
                                         â†’ is-pos-succ c
                                         â†’ a â‰¤ b
                                         â†’ a * c â‰¤ b * c
positive-multiplication-preserves-order' a b c p l = I (â„¤â‰¤-split a b l)
 where
  I : (a < b) âˆ” (a ï¼ b) â†’ a * c â‰¤ b * c
  I (inl l) = <-is-â‰¤ (a * c) (b * c) Î³
   where
    Î³ :  a * c < b * c
    Î³ = positive-multiplication-preserves-order a b c p l
  I (inr e) = transport (_â‰¤ b * c) Î³ (â„¤â‰¤-refl (b * c))
   where
    Î³ : b * c ï¼ a * c
    Î³ = ap (_* c) (e â»Â¹)

â„¤*-multiplication-order : (a b c : â„¤)
                        â†’ pos 0 â‰¤ c
                        â†’ a â‰¤ b
                        â†’ a * c â‰¤ b * c
â„¤*-multiplication-order a b (pos 0) p l = â„¤â‰¤-refl (pos 0)
â„¤*-multiplication-order a b (pos (succ c)) p l
 = positive-multiplication-preserves-order' a b (pos (succ c)) â‹† l
â„¤*-multiplication-order a b (negsucc c) p l = ğŸ˜-elim Î³
 where
  I : negsucc c < pos 0
  I = negative-less-than-positive c 0

  Î³ : ğŸ˜
  Î³ = â„¤-bigger-or-equal-not-less (pos 0) (negsucc c) p I

nmco-lemma : (a b : â„¤) â†’ (c : â„•) â†’ a < b â†’ b * (negsucc c) < a * (negsucc c)
nmco-lemma a b = induction base step
 where
  base : a < b â†’ b * negsucc 0 < a * negsucc 0
  base (k , Î³) = k , I
   where
    II : (- b) + pos k + (a - a) ï¼ a + pos k + ((- b) - a)
    II = (- b) + pos k + (a - a)    ï¼âŸ¨ i   âŸ©
          pos k - b + (a - a)       ï¼âŸ¨ ii  âŸ©
          pos k - b + a - a         ï¼âŸ¨ iii âŸ©
          a + (pos k - b) - a       ï¼âŸ¨ iv  âŸ©
          a + pos k - b - a         ï¼âŸ¨ v   âŸ©
          a + pos k + ((- b) - a)   âˆ
     where
      i   = ap (_+ (a - a)) (â„¤+-comm (- b) (pos k))
      ii  = â„¤+-assoc (pos k - b) a (- a) â»Â¹
      iii = ap (_+ (- a)) (â„¤+-comm (pos k - b) a)
      iv  = ap (_+ (- a)) (â„¤+-assoc a (pos k) (- b) â»Â¹)
      v   = â„¤+-assoc (a + pos k) (- b) (- a)

    I : succâ„¤ (b * negsucc 0) + pos k ï¼ a * negsucc 0
    I = succâ„¤ (b * negsucc 0) + pos k    ï¼âŸ¨ by-definition âŸ©
        succâ„¤ (- b) + pos k              ï¼âŸ¨ i             âŸ©
        succâ„¤ ((- b) + pos k)            ï¼âŸ¨ ii            âŸ©
        succâ„¤ ((- b) + pos k) + pos 0    ï¼âŸ¨ iii           âŸ©
        succâ„¤ ((- b) + pos k) + (a - a)  ï¼âŸ¨ iv            âŸ©
        succâ„¤ ((- b) + pos k + (a - a))  ï¼âŸ¨ v             âŸ©
        succâ„¤ (a + pos k + ((- b) - a))  ï¼âŸ¨ vi            âŸ©
        succâ„¤ (a + pos k) + ((- b) - a)  ï¼âŸ¨ vii           âŸ©
        succâ„¤ a + pos k + ((- b) - a)    ï¼âŸ¨ viii          âŸ©
        b + ((- b) - a)                  ï¼âŸ¨ ix            âŸ©
        b - b - a                        ï¼âŸ¨ x             âŸ©
        pos 0 - a                        ï¼âŸ¨ xi            âŸ©
        - a                              ï¼âŸ¨ by-definition âŸ©
        a * negsucc 0                    âˆ
     where
       i    = â„¤-left-succ (- b) (pos k)
       ii   = â„¤-zero-right-neutral (succâ„¤ ((- b) +pos k))
       iii  = ap (succâ„¤ ((- b) + pos k) +_) (â„¤-sum-of-inverse-is-zero a â»Â¹)
       iv   = â„¤-left-succ ((- b) + pos k) (a - a)
       v    = ap succâ„¤ II
       vi   = â„¤-left-succ (a + pos k) ((- b) - a) â»Â¹
       vii  = ap (_+ ((- b) - a)) (â„¤-left-succ a (pos k) â»Â¹)
       viii = ap (_+ ((- b) - a)) Î³
       ix   = â„¤+-assoc b (- b) (- a) â»Â¹
       x    = ap (_+ (- a)) (â„¤-sum-of-inverse-is-zero b)
       xi   = â„¤-zero-left-neutral (- a)

  step : (k : â„•)
       â†’ (a < b â†’ b * negsucc k < a * negsucc k)
       â†’  a < b â†’ b * negsucc (succ k) < a * negsucc (succ k)
  step k IH l = â„¤<-adding (- b) (- a) (b * negsucc k) (a * negsucc k) I II
   where
    I : - b < - a
    I = base l
    II :  b * negsucc k < a * negsucc k
    II = IH l

negative-multiplication-changes-order : (a b c : â„¤)
                                      â†’ negative c
                                      â†’ a < b
                                      â†’ b * c < a * c
negative-multiplication-changes-order a b (pos c)     g l = ğŸ˜-elim g
negative-multiplication-changes-order a b (negsucc c) g l = nmco-lemma a b c l

negative-multiplication-changes-order' : (a b c : â„¤)
                                       â†’ negative c
                                       â†’ a â‰¤ b
                                       â†’ b * c â‰¤ a * c
negative-multiplication-changes-order' a b (pos x) g l = ğŸ˜-elim g
negative-multiplication-changes-order' a b (negsucc x) g l = I (â„¤â‰¤-split a b l)
 where
  I : a < b âˆ” (a ï¼ b) â†’ b * negsucc x â‰¤ a * negsucc x
  I (inl a<b) = <-is-â‰¤ (b * negsucc x) (a * negsucc x) Î³
   where
    Î³ : b * negsucc x < a * negsucc x
    Î³ = negative-multiplication-changes-order a b (negsucc x) â‹† a<b
  I (inr aï¼b) = transport (b * negsucc x â‰¤â„¤_) Î³â‚ Î³â‚‚
   where
    Î³â‚ : b * negsucc x ï¼ a * negsucc x
    Î³â‚ = ap (_* negsucc x) (aï¼b â»Â¹)

    Î³â‚‚ : b * negsucc x â‰¤ b * negsucc x
    Î³â‚‚ = â„¤â‰¤-refl (b * negsucc x)

â„¤-mult-right-cancellable : (x y z : â„¤) â†’ not-zero z â†’ x * z ï¼ y * z â†’ x ï¼ y
â„¤-mult-right-cancellable x y (pos 0)        nz e = ğŸ˜-elim (nz â‹†)
â„¤-mult-right-cancellable x y (pos (succ z)) nz e = tri-split (â„¤-trichotomous x y)
 where
  tri-split : (x < y) âˆ” (x ï¼ y) âˆ” (y < x) â†’ x ï¼ y
  tri-split (inl l) = ğŸ˜-elim (â„¤-equal-not-less-than (x * pos (succ z)) II)
   where
    I : x * pos (succ z) < y * pos (succ z)
    I = positive-multiplication-preserves-order x y (pos (succ z)) â‹† l

    II : x * pos (succ z) < x * pos (succ z)
    II = transport (x * pos (succ z) <_) (e â»Â¹) I

  tri-split (inr (inl m)) = m
  tri-split (inr (inr r)) = ğŸ˜-elim (â„¤-equal-not-less-than (y * pos (succ z)) II)
   where
    I : y * pos (succ z) < x * pos (succ z)
    I = positive-multiplication-preserves-order y x (pos (succ z)) â‹† r

    II : y * pos (succ z) < y * pos (succ z)
    II = transport (y * pos (succ z) <_) e I
â„¤-mult-right-cancellable x y (negsucc z)    nz e = tri-split (â„¤-trichotomous x y)
 where
  tri-split : (x < y) âˆ” (x ï¼ y) âˆ” (y < x) â†’ x ï¼ y
  tri-split (inl l) = ğŸ˜-elim (â„¤-equal-not-less-than (y * negsucc z) II)
   where
    I : y * negsucc z < x * negsucc z
    I = nmco-lemma x y z l

    II : y * negsucc z < y * negsucc z
    II = transport (y * negsucc z <_) e I

  tri-split (inr (inl r)) = r
  tri-split (inr (inr r)) = ğŸ˜-elim (â„¤-equal-not-less-than (x * negsucc z) II)
   where
    I : (x * negsucc z) < (y * negsucc z)
    I = nmco-lemma y x z r

    II : x * negsucc z < x * negsucc z
    II = transport (x * negsucc z <_) (e â»Â¹) I

â„¤-mult-left-cancellable : (x y z : â„¤) â†’ not-zero z
                                      â†’ z * x ï¼ z * y
                                      â†’ x ï¼ y
â„¤-mult-left-cancellable x y z nz e = â„¤-mult-right-cancellable x y z nz I
 where
  I : x * z ï¼ y * z
  I = x * z   ï¼âŸ¨ â„¤*-comm x z âŸ©
      z * x   ï¼âŸ¨ e           âŸ©
      z * y   ï¼âŸ¨ â„¤*-comm z y âŸ©
      y * z   âˆ

non-zero-multiplication : (x y : â„¤) â†’ Â¬ (x ï¼ pos 0) â†’ Â¬ (y ï¼ pos 0) â†’ Â¬ (x * y ï¼ pos 0)
non-zero-multiplication (pos 0)        y xnz ynz e = xnz refl
non-zero-multiplication (pos (succ x)) y xnz ynz e = ynz Î³
 where
  Î³ : y ï¼ pos 0
  Î³ = â„¤-mult-left-cancellable y (pos 0) (pos (succ x)) id e
non-zero-multiplication (negsucc x)    y xnz ynz e = ynz Î³
 where
  Î³ : y ï¼ pos 0
  Î³ = â„¤-mult-left-cancellable y (pos 0) (negsucc x) id e

orcl : (a b : â„¤) â†’ (n : â„•) â†’ a * (pos (succ n)) â‰¤ b * (pos (succ n)) â†’ a â‰¤ b
orcl a b = induction base step
 where
  base : a * pos 1 â‰¤ b * pos 1 â†’ a â‰¤ b
  base = id

  step : (k : â„•)
       â†’ (a * pos (succ k) â‰¤ b * pos (succ k) â†’ a â‰¤ b)
       â†’ a * pos (succ (succ k)) â‰¤ b * pos (succ (succ k))
       â†’ a â‰¤ b
  step k IH (Î± , Î²) = casesâ‚ƒ Î³â‚ Î³â‚‚ Î³â‚ƒ (â„¤-trichotomous a b)
   where
    k' = pos (succ (succ k))

    Î³â‚ : a < b â†’ a â‰¤ b
    Î³â‚ = <-is-â‰¤ a b

    Î³â‚‚ : a ï¼ b â†’ a â‰¤ b
    Î³â‚‚ e = transport (a â‰¤_) e (â„¤â‰¤-refl a)

    Î³â‚ƒ : b < a â†’ a â‰¤ b
    Î³â‚ƒ l = ğŸ˜-elim III
     where
      I : a * k' â‰¤ b * k'
      I = Î± , Î²

      II : b * k' < a * k'
      II = positive-multiplication-preserves-order b a k' â‹† l

      III : ğŸ˜
      III = â„¤-bigger-or-equal-not-less (a * k') (b * k') I II

orcl' : (a b : â„¤) â†’ (n : â„•) â†’ a * (pos (succ n)) < b * (pos (succ n)) â†’ a < b
orcl' a b n l = II (â„¤â‰¤-split a b I)
 where
  I : a â‰¤ b
  I = orcl a b n (<-is-â‰¤ (a * pos (succ n)) (b * pos (succ n)) l)
  II : a < b âˆ” (a ï¼ b) â†’ a < b
  II (inl l) = l
  II (inr e) = ğŸ˜-elim (â„¤-equal-not-less-than (a * pos (succ n)) III)
   where
    III : a * pos (succ n) < a * pos (succ n)
    III = transport (Î» - â†’ (a * pos (succ n)) < (- * pos (succ n))) (e â»Â¹) l

ordering-right-cancellable : (a b c : â„¤) â†’ is-pos-succ c â†’ a * c < b * c â†’ a < b
ordering-right-cancellable a b (negsucc x) p l    = ğŸ˜-elim p
ordering-right-cancellable a b (pos 0) p l        = ğŸ˜-elim p
ordering-right-cancellable a b (pos (succ x)) p l = orcl' a b x l

â„¤â‰¤-ordering-right-cancellable : (a b c : â„¤)
                              â†’ is-pos-succ c â†’ a * c â‰¤ b * c â†’ a â‰¤ b
â„¤â‰¤-ordering-right-cancellable a b (pos zero) p l     = ğŸ˜-elim p
â„¤â‰¤-ordering-right-cancellable a b (pos (succ x)) p l = orcl a b x l
â„¤â‰¤-ordering-right-cancellable a b (negsucc x) p l    = ğŸ˜-elim p

â„¤â‰¤-anti : (x y : â„¤) â†’ x â‰¤ y â†’ y â‰¤ x â†’ x ï¼ y
â„¤â‰¤-anti x y lâ‚ lâ‚‚ = I (â„¤â‰¤-split x y lâ‚) (â„¤â‰¤-split y x lâ‚‚)
 where
  I : x < y âˆ” (x ï¼ y) â†’ y < x âˆ” (y ï¼ x)
    â†’ x ï¼ y
  I (inl x<y) (inr e)   = e â»Â¹
  I (inr e)   (inl y<x) = e
  I (inr e)   (inr e')  = e
  I (inl x<y) (inl y<x) = ğŸ˜-elim III
   where
    II : x < x
    II = â„¤<-trans x y x x<y y<x

    III : ğŸ˜
    III = â„¤-equal-not-less-than x II

maxâ„¤ : â„¤ â†’ â„¤ â†’ â„¤
maxâ„¤ x y with â„¤-dichotomous x y
... | inl xâ‰¤y = y
... | inr yâ‰¤x = x

maxâ‚‚ : â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤
maxâ‚‚ x y z = maxâ„¤ (maxâ„¤ x y) z

maxâ‚ƒ : â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤
maxâ‚ƒ w x y z = maxâ„¤ (maxâ‚‚ w x y) z

minâ„¤ : â„¤ â†’ â„¤ â†’ â„¤
minâ„¤ x y with â„¤-dichotomous x y
... | inl xâ‰¤y = x
... | inr yâ‰¤x = y

minâ‚‚ : â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤
minâ‚‚ x y z = minâ„¤ (minâ„¤ x y) z

minâ‚ƒ : â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤ â†’ â„¤
minâ‚ƒ w x y z = minâ„¤ (minâ‚‚ w x y) z

\end{code}

Added by Todd

\begin{code}

â„¤â‰¤-attach : (x y : â„¤) â†’ (y ï¼ x) âˆ” (x < y) â†’ x â‰¤ y
â„¤â‰¤-attach x x (inl refl) = 0 , refl
â„¤â‰¤-attach x y (inr (a , p)) = succ a , (â„¤-left-succ-pos x a â»Â¹ âˆ™ p)

â„¤â‰¤-same-witness : (x y : â„¤) â†’ ((n , _) (m , _) : x â‰¤ y) â†’ n ï¼ m
â„¤â‰¤-same-witness x y p q = ap prâ‚ (â„¤â‰¤-is-prop x y p q)

â„¤â‰¤-add-witness : (x y z : â„¤) â†’ ((n , p) : x â‰¤ y) ((m , q) : y â‰¤ z)
               â†’ ((o , r) : x â‰¤ z)
               â†’ o ï¼ n â„•+ m
â„¤â‰¤-add-witness x y z xâ‰¤y yâ‰¤z xâ‰¤z
 = â„¤â‰¤-same-witness x z xâ‰¤z (â„¤â‰¤-trans x y z xâ‰¤y yâ‰¤z)

â„¤-less-not-bigger-or-equal : (x y : â„¤) â†’ x < y â†’ Â¬ (y â‰¤ x)
â„¤-less-not-bigger-or-equal x y x<y yâ‰¤x
 = â„¤-bigger-or-equal-not-less y x yâ‰¤x x<y

\end{code}

Lane Biocini, 07 September 2023

A proof of the triangle inequality in the Integers using the Absolute
Difference operation defined in the Naturals. We first prove a convenience
lemma.

\begin{code}

â„•-order-respects-â„¤-order'' : (m n : â„•) â†’ m â‰¤ n â†’ pos m â‰¤ pos n
â„•-order-respects-â„¤-order'' zero n l = â„¤-zero-least-pos n
â„•-order-respects-â„¤-order'' (succ m) n l = â„•-order-respects-â„¤-order m n l

triangle-inequalityâ‚€ : (x y : â„•) â†’ abs (pos x +pos y) â‰¤â„• abs (pos x) â„•+ abs (pos y)
triangle-inequalityâ‚€ x y = transport (_â‰¤â„• x â„•+ y) Î“ (â‰¤-refl (x â„•+ y))
 where
  Î“ : x â„•+ y ï¼ abs (pos x +pos y)
  Î“ = x â„•+ y ï¼âŸ¨ ap abs (distributivity-pos-addition x y) â»Â¹ âŸ©
    abs (pos x +pos y) âˆ

triangle-inequalityâ‚ : (x y : â„•) â†’ abs (pos x +negsucc y) â‰¤â„• succ (x â„•+ y)
triangle-inequalityâ‚ x y = transport (_â‰¤â„• succ (x â„•+ y)) Î“ Î³
 where
  Î“ : âˆ£ x - succ y âˆ£ ï¼ abs (pos x +negsucc y)
  Î“ = abs-pos-plus-negsucc x y â»Â¹

  Î³ : âˆ£ x - succ y âˆ£ â‰¤â„• succ (x â„•+ y)
  Î³ = triangle-inequality x 0 (succ y)

triangle-inequalityâ‚‚ : (x y : â„•) â†’ abs (negsucc x +pos y) â‰¤â„• (succ x â„•+ y)
triangle-inequalityâ‚‚ x y = transportâ‚‚ _â‰¤â„•_ I II (triangle-inequalityâ‚ y x)
 where
  I : abs (pos y +negsucc x) ï¼ abs (negsucc x +pos y)
  I = ap abs (â„¤+-comm (pos y) (negsucc x))

  II : succ (y â„•+ x) ï¼ succ x â„•+ y
  II = ap succ (addition-commutativity y x) âˆ™ succ-left x y â»Â¹

triangle-inequalityâ‚ƒ : (x y : â„•) â†’ abs (negsucc x +negsucc y) â‰¤â„• succ (succ x â„•+ y)
triangle-inequalityâ‚ƒ x y = transport (_â‰¤â„• succ (succ x â„•+ y)) Î“ Î³
 where
  Î“ : abs (pos (succ x) + pos (succ y)) ï¼ abs (negsucc x +negsucc y)
  Î“ = abs (succâ„¤ (pos (succ x) +pos y))   ï¼âŸ¨ i âŸ©
      abs (- succâ„¤ (pos (succ x) +pos y)) ï¼âŸ¨ ii âŸ©
      abs (negsucc x +negsucc y)          âˆ
   where
    i = abs-removes-neg-sign (succâ„¤ (pos (succ x) +pos y))
    ii = ap abs (negation-dist (pos (succ x)) (pos (succ y))) â»Â¹

  Î³ : abs (pos (succ x) + pos (succ y)) â‰¤â„• succ (succ x â„•+ y)
  Î³ = triangle-inequalityâ‚€ (succ x) (succ y)

â„¤-triangle-inequality : (x y : â„¤) â†’ abs (x + y) â‰¤â„• abs x â„•+ abs y
â„¤-triangle-inequality (pos x) (pos y) = triangle-inequalityâ‚€ x y
â„¤-triangle-inequality (pos x) (negsucc y) = triangle-inequalityâ‚ x y
â„¤-triangle-inequality (negsucc x) (pos y) = triangle-inequalityâ‚‚ x y
â„¤-triangle-inequality (negsucc x) (negsucc y) = triangle-inequalityâ‚ƒ x y

â„¤-triangle-inequality' : (x y : â„¤) â†’ absâ„¤ (x + y) â‰¤â„¤ absâ„¤ x + absâ„¤ y
â„¤-triangle-inequality' x y = transportâ‚‚ _â‰¤â„¤_ I II Î³
 where
  I : pos (abs (x + y)) ï¼ absâ„¤ (x + y)
  I = pos-abs-is-absâ„¤ (x + y)

  II : pos (abs x â„•+ abs y) ï¼ absâ„¤ x + absâ„¤ y
  II = pos (abs x â„•+ abs y)   ï¼âŸ¨ i âŸ©
     (pos (abs x) +pos abs y) ï¼âŸ¨ ii âŸ©
     absâ„¤ x + absâ„¤ y          âˆ
   where
    i = distributivity-pos-addition (abs x) (abs y) â»Â¹
    ii : (pos (abs x) +pos abs y) ï¼ absâ„¤ x + absâ„¤ y
    ii = apâ‚‚ (Î» x y â†’ x + y) (pos-abs-is-absâ„¤ x) (pos-abs-is-absâ„¤ y)

  Î³ : pos (abs (x + y)) â‰¤â„¤ pos (abs x â„•+ abs y)
  Î³ = â„•-order-respects-â„¤-order'' (abs (x + y)) (abs x â„•+ abs y) (â„¤-triangle-inequality x y)
