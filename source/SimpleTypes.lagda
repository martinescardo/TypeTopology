Martin Escardo, January 2018

The simple types are the smallest collection of types containing â„• and
closed under exponentials (function types).  All simple types are
totally separated and retracts of ğŸš. This is used to show that no
simple type is ğŸš-compact, unless WLPO holds. If ğŸš is included as a
base simple type, then for (X â†’ Y) to be compact it is necessary that
X is discrete and Y is compact. (It is consistent that the converse
holds (Tychonoff Theorem).)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import DisconnectedTypes

open import UF-FunExt
open import UF-PropTrunc

module SimpleTypes
        (fe : FunExt)
        (pt : propositional-truncations-exist)
       where

open import UF-Retracts
open import UF-Retracts-FunExt

data simple-type : ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚ Ì‡ where
 base : simple-type â„•
 step : {X Y : ğ“¤â‚€ Ì‡ } â†’ simple-type X â†’ simple-type Y â†’ simple-type (X â†’ Y)

open import TotallySeparated
open import WeaklyCompactTypes fe pt renaming (Î -compact to compact)
open import DiscreteAndSeparated

simple-types-are-totally-separated : {X : ğ“¤â‚€ Ì‡ }
                                   â†’ simple-type X
                                   â†’ is-totally-separated X
simple-types-are-totally-separated base       = â„•-is-totally-separated
simple-types-are-totally-separated (step s t) = Î -is-totally-separated (fe ğ“¤â‚€ ğ“¤â‚€)
                                                 Î» _ â†’ simple-types-are-totally-separated t

simple-types-pointed : {X : ğ“¤â‚€ Ì‡ } â†’ simple-type X â†’ X
simple-types-pointed base       = zero
simple-types-pointed (step s t) = Î» x â†’ simple-types-pointed t

simple-types-r : {X A : ğ“¤â‚€ Ì‡ } â†’ retract A of â„• â†’ simple-type X â†’ retract A of X
simple-types-r rn base       = rn
simple-types-r rn (step s t) = retracts-of-closed-under-exponentials
                                 (fe ğ“¤â‚€ ğ“¤â‚€)
                                 (simple-types-pointed s)
                                 (simple-types-r rn s)
                                 (simple-types-r rn t)

cfdbce : {X Y : ğ“¤â‚€ Ì‡ }
       â†’ simple-type X
       â†’ simple-type Y
       â†’ compact (X â†’ Y)
       â†’ is-discrete X Ã— compact Y
cfdbce s t c = tscdâ‚€ (simple-types-are-totally-separated s) (simple-types-r â„•-disconnected t) c ,
               Î -compact-exponential-with-pointed-domain-has-Î -compact-domain (simple-types-pointed s) c

\end{code}

TODO: prove that WLPO' is equivalent to WLPO. But notice that WLPO' is
the original formalution of WLPO by Bishop (written in type theory).

We have that simple types are "not" compact:

\begin{code}

WLPO' : ğ“¤â‚€ Ì‡
WLPO' = compact â„•

stcwlpo : {X : ğ“¤â‚€ Ì‡ } â†’ simple-type X â†’ compact X â†’ WLPO'
stcwlpo base c = c
stcwlpo (step s t) c = stcwlpo t (prâ‚‚ (cfdbce s t c))

\end{code}

But, of course, the last consequence can be proved more directly by
simply showing that â„• is a retract of every simple type, using the
fact that compactness is inherited by retracts, which doesn't rely
on the notion of total separatedness:

\begin{code}

simple-types-râ„• : {X : ğ“¤â‚€ Ì‡ } â†’ simple-type X â†’ retract â„• of X
simple-types-râ„• = simple-types-r identity-retraction

stcwlpo' : {X : ğ“¤â‚€ Ì‡ } â†’ simple-type X â†’ compact X â†’ WLPO'
stcwlpo' s = retract-Î -compact (simple-types-râ„• s)

\end{code}

To make this less trivial, we include ğŸš as a base type in the
definition of simple types:

\begin{code}

data simple-typeâ‚‚ : ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚ Ì‡ where
 baseâ‚‚ : simple-typeâ‚‚ ğŸš
 base  : simple-typeâ‚‚ â„•
 step  : {X Y : ğ“¤â‚€ Ì‡ } â†’ simple-typeâ‚‚ X â†’ simple-typeâ‚‚ Y â†’ simple-typeâ‚‚ (X â†’ Y)

\end{code}

Then now, potentially, there are compact types such as the Cantor
space (â„• â†’ ğŸš), and more generally (X â†’ Y) with X discrete and Y
compact, if Tychonoff holds (which is consistent but not
provable). The following then says that this is the only possibility:
If X and Y simple types in this generalized sense, for (X â†’ Y) to be
compact, it is necessary that X is discrete and Y is compact.

\begin{code}

simple-typesâ‚‚-totally-separated : {X : ğ“¤â‚€ Ì‡ } â†’ simple-typeâ‚‚ X â†’ is-totally-separated X
simple-typesâ‚‚-totally-separated baseâ‚‚       = ğŸš-is-totally-separated
simple-typesâ‚‚-totally-separated base        = â„•-is-totally-separated
simple-typesâ‚‚-totally-separated (step s t)  = Î -is-totally-separated (fe ğ“¤â‚€ ğ“¤â‚€)
                                               Î» _ â†’ simple-typesâ‚‚-totally-separated t

simple-typesâ‚‚-pointed : {X : ğ“¤â‚€ Ì‡ } â†’ simple-typeâ‚‚ X â†’ X
simple-typesâ‚‚-pointed baseâ‚‚      = â‚€
simple-typesâ‚‚-pointed base       = zero
simple-typesâ‚‚-pointed (step s t) = Î» x â†’ simple-typesâ‚‚-pointed t

simple-typesâ‚‚-disconnected : {X : ğ“¤â‚€ Ì‡ } â†’ simple-typeâ‚‚ X â†’ disconnected X
simple-typesâ‚‚-disconnected baseâ‚‚      = identity-retraction
simple-typesâ‚‚-disconnected base       = â„•-disconnected
simple-typesâ‚‚-disconnected (step s t) = retracts-of-closed-under-exponentials
                                         (fe ğ“¤â‚€ ğ“¤â‚€)
                                         (simple-typesâ‚‚-pointed s)
                                         (simple-typesâ‚‚-disconnected s)
                                         (simple-typesâ‚‚-disconnected t)

cfdbceâ‚‚ : {X Y : ğ“¤â‚€ Ì‡ }
        â†’ simple-typeâ‚‚ X
        â†’ simple-typeâ‚‚ Y
        â†’ compact (X â†’ Y)
        â†’ is-discrete X Ã— compact Y
cfdbceâ‚‚ s t c = tscdâ‚€ (simple-typesâ‚‚-totally-separated s) (simple-typesâ‚‚-disconnected t) c ,
                Î -compact-exponential-with-pointed-domain-has-Î -compact-domain (simple-typesâ‚‚-pointed s) c

\end{code}
