Martin Escardo, January 2018

The simple types are the smallest collection of types containing â„• and
closed under exponentials (function types).  All simple types are
totally separated and retracts of ğŸš. This is used to show that no
simple type is ğŸš-compact, unless WLPO holds. If ğŸš is included as a
base simple type, then for (X â†’ Y) to be compact it is necessary that
X is discrete and Y is compact. (It is consistent that the converse
holds (Tychonoff Theorem).)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UF-FunExt
open import UF-PropTrunc

module SimpleTypes (fe : âˆ€ U V â†’ FunExt U V) (pt : PropTrunc) where

open import UF-Retracts
open import UF-Retracts-FunExt

data simple-type : Uâ‚€ Ì‡ â†’ Uâ‚ Ì‡ where
 base : simple-type â„•
 step : {X Y : Uâ‚€ Ì‡} â†’ simple-type X â†’ simple-type Y â†’ simple-type (X â†’ Y)

open import TotallySeparated
open import 2CompactTypes (fe) (pt)
open import DiscreteAndSeparated

ğŸš-retract-of-â„• : retract ğŸš of â„•
ğŸš-retract-of-â„• = (r , (s , rs))
 where
  r : â„• â†’ ğŸš
  r zero = â‚€
  r (succ n) = â‚
  
  s : ğŸš â†’ â„•
  s â‚€ = zero
  s â‚ = succ zero
  
  rs : (n : ğŸš) â†’ r (s n) â‰¡ n
  rs â‚€ = refl
  rs â‚ = refl

â„•-totally-separated : totally-separated â„•
â„•-totally-separated = discrete-totally-separated (â„•-discrete)

simple-types-totally-separated : {X : Uâ‚€ Ì‡} â†’ simple-type X â†’ totally-separated X
simple-types-totally-separated base       = â„•-totally-separated
simple-types-totally-separated (step s t) = totally-separated-ideal (fe Uâ‚€ Uâ‚€)
                                              Î» _ â†’ simple-types-totally-separated t

simple-types-pointed : {X : Uâ‚€ Ì‡} â†’ simple-type X â†’ X
simple-types-pointed base       = zero
simple-types-pointed (step s t) = Î» x â†’ simple-types-pointed t

simple-types-r : {X A : Uâ‚€ Ì‡} â†’ retract A of â„• â†’ simple-type X â†’ retract A of X
simple-types-r rn base       = rn
simple-types-r rn (step s t) = retracts-of-closed-under-exponentials
                                 (fe Uâ‚€ Uâ‚€)
                                 (simple-types-pointed s)
                                 (simple-types-r rn s)
                                 (simple-types-r rn t)

cfdbce : {X Y : Uâ‚€ Ì‡} â†’ simple-type X â†’ simple-type Y
       â†’ ğŸš-compact (X â†’ Y) â†’ discrete X Ã— ğŸš-compact Y
cfdbce s t c = (tscdâ‚€ (simple-types-totally-separated s) (simple-types-r ğŸš-retract-of-â„• t) c ,
               i2c2c (simple-types-pointed s) c)

\end{code}

TODO: prove that WLPO' is equivalent to WLPO. But notice that WLPO' is
the original formalution of WLPO by Bishop (written in type theory).

\begin{code}

WLPO' : Uâ‚€ Ì‡
WLPO' = ğŸš-compact â„•

stcwlpo : {X : Uâ‚€ Ì‡} â†’ simple-type X â†’ ğŸš-compact X â†’ WLPO'
stcwlpo base c = c
stcwlpo (step s t) c = stcwlpo t (prâ‚‚ (cfdbce s t c))

\end{code}

But, of course, the last consequence can be proved more directly by
simply showing that â„• is a retract of every simple type, using the
fact that ğŸš-compactness is inherited by retracts, which doesn't rely
on the notion of total separatedness:

\begin{code}

simple-types-râ„• : {X : Uâ‚€ Ì‡} â†’ simple-type X â†’ retract â„• of X
simple-types-râ„• = simple-types-r identity-retraction

stcwlpo' : {X : Uâ‚€ Ì‡} â†’ simple-type X â†’ ğŸš-compact X â†’ WLPO'
stcwlpo' s = retract-ğŸš-compact (simple-types-râ„• s)

\end{code}

To make this less trivial, we include ğŸš as a base type in the
definition of simple types:

\begin{code}

data simple-typeâ‚‚ : Uâ‚€ Ì‡ â†’ Uâ‚ Ì‡ where
 baseâ‚‚ : simple-typeâ‚‚ ğŸš
 base : simple-typeâ‚‚ â„•
 step : {X Y : Uâ‚€ Ì‡} â†’ simple-typeâ‚‚ X â†’ simple-typeâ‚‚ Y â†’ simple-typeâ‚‚ (X â†’ Y)

\end{code}

Then now, potentially, there are compact types such as the Cantor
space (â„• â†’ ğŸš), and more generally (X â†’ Y) with X discrete and Y
compact, if Tychonoff holds (which is consistent but not
provable). The following then says that this is the only possibility:
If X and Y simple types in this generalized sense, for (X â†’ Y) to be
compact, it is necessary that X is discrete and Y is compact.

\begin{code}

simple-typesâ‚‚-totally-separated : {X : Uâ‚€ Ì‡} â†’ simple-typeâ‚‚ X â†’ totally-separated X
simple-typesâ‚‚-totally-separated baseâ‚‚       = ğŸš-totally-separated
simple-typesâ‚‚-totally-separated base        = â„•-totally-separated
simple-typesâ‚‚-totally-separated (step s t)  = totally-separated-ideal (fe Uâ‚€ Uâ‚€)
                                               Î» _ â†’ simple-typesâ‚‚-totally-separated t

simple-typesâ‚‚-pointed : {X : Uâ‚€ Ì‡} â†’ simple-typeâ‚‚ X â†’ X
simple-typesâ‚‚-pointed baseâ‚‚      = â‚€
simple-typesâ‚‚-pointed base       = zero
simple-typesâ‚‚-pointed (step s t) = Î» x â†’ simple-typesâ‚‚-pointed t

simple-typesâ‚‚-rğŸš : {X : Uâ‚€ Ì‡} â†’ simple-typeâ‚‚ X â†’ retract ğŸš of X
simple-typesâ‚‚-rğŸš baseâ‚‚      = identity-retraction
simple-typesâ‚‚-rğŸš base       = ğŸš-retract-of-â„•
simple-typesâ‚‚-rğŸš (step s t) = retracts-of-closed-under-exponentials
                                 (fe Uâ‚€ Uâ‚€)
                                 (simple-typesâ‚‚-pointed s)
                                 (simple-typesâ‚‚-rğŸš s)
                                 (simple-typesâ‚‚-rğŸš t)

cfdbceâ‚‚ : {X Y : Uâ‚€ Ì‡} â†’ simple-typeâ‚‚ X â†’ simple-typeâ‚‚ Y
       â†’ ğŸš-compact (X â†’ Y) â†’ discrete X Ã— ğŸš-compact Y
cfdbceâ‚‚ s t c = (tscdâ‚€ (simple-typesâ‚‚-totally-separated s) (simple-typesâ‚‚-rğŸš t) c ,
                 i2c2c (simple-typesâ‚‚-pointed s) c)

\end{code}
