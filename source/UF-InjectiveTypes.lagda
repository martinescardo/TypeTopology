Martin Escardo, 27 April 2014, with later additions, completed in December 2017

We show that the injective types are the retracts of the exponential
powers of universes, where an exponential power of a type D is a type
of the form A â†’ D for some type A. Injectivity is defined as
(functional) data rather than property.


Injectivity of the universe (2014)
----------------------------

Here we have definitions and proofs in Agda notation, which assume a
univalent mathematics background (e.g. given by the HoTT book),
preceded by informal (rigorous) discussion.

We show that the universe is (right-Kan) injective wrt embeddings. An
embedding is a map j:Xâ†’Y whose fibers are all univalent propositions.

In the remote past, I looked at injectivity in categories of spaces
and locales, with respect to various kinds of maps, and I wrote
several papers about that.

At present, I am looking at searchable sets in type theory
(corresponding to compact sets in topology), and I accidentally landed
in the same injectivity territory. This file is about
injectivity. Other files make use of this for searchability purposes,
which is not discussed here.

Abstractly, the general situation is

                   j
              X ------> Y
               \       /
                \     /
             f   \   / f/j
                  \ /
                   v
                   D

Given f and j, we want to find f/j making the diagram commute (that
is, f = f/j âˆ˜ j). Of course, this "extension property" is not always
possible, and it depends on properties of f, j and D. Usually, one
requires j to be an embedding (of some sort).

Here I consider the case that D=U, a universe, in which case, if one
doesn't want to assume univalence, then it makes sense to consider
commutation up to equivalence:

                   j
              X ------> Y
               \       /
                \  â‰ƒ  /
             f   \   / f/j
                  \ /
                   v
                   U

But this can be the case only if j is an embedding in a suitable
sense. Otherwise, we only have a right-Kan extension

                   j
              X ------> Y
               \       /
                \  <- /
             f   \   / f/j
                  \ /
                   v
                   U

in the sense that the natural transformations from "presheaves"
g : Y â†’ U to the "presheaf" f/j are naturally equivalent to the
natural transformations from g âˆ˜ j to f:

     Nat g f/j â‰ƒ Nat (g âˆ˜ j) f

If j is an embedding (in the sense of univalent mathematics), then,
for any f, we can find f/j which is both a right-Kan extension and a
(proper) extension (up to equivalence).

All this dualizes with Î  replaced by Î£ and right replaced by left.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UF-FunExt

module UF-InjectiveTypes (fe : âˆ€ U V â†’ funext U V) where

open import SpartanMLTT
open import UF-Base
open import UF-Equiv
open import UF-Embedding
open import UF-Retracts
open import UF-EquivalenceExamples
open import UF-Univalence

\end{code}

Here is how we define f/j given f and j.

                   j
              X ------> Y
               \       /
                \  <- /
             f   \   / f' := f/j
                  \ /
                   v
                   U

We have to apply the following constructions for U=V=W for the above
triangles to make sense.

\begin{code}

module _ {U V W : Universe} {X : U Ì‡} {Y : V Ì‡} (f : X â†’ W Ì‡) (j : X â†’ Y) where

  Î -extension Î£-extension : Y â†’ U âŠ” V âŠ” W Ì‡
  Î -extension y = Î  \(w : fiber j y) â†’ f(prâ‚ w)
  Î£-extension y = Î£ \(w : fiber j y) â†’ f(prâ‚ w)

  private f/j fâˆ–j : Y â†’ U âŠ” V âŠ” W Ì‡
  f/j = Î -extension
  fâˆ–j = Î£-extension

  Î£â†’Î  : is-embedding j â†’ Nat fâˆ–j f/j
  Î£â†’Î  e y ((x , p) , B) (x' , p') = transport f (embedding-lc j e (p âˆ™ p' â»Â¹)) B

\end{code}

  The natural transformation Î£â†’Î  j itself should be a natural
  embedding (conjectural conjecture).

\begin{code}

  Î·Î  : Nat (f/j âˆ˜ j) f
  Î·Î  x C = C(x , refl)

  Î·Î£ : Nat f (fâˆ–j âˆ˜ j)
  Î·Î£ x B = (x , refl) , B

\end{code}

  For arbitrary j:Xâ†’Y, this gives Kan extensions in the following
  sense:

\begin{code}

  Î -extension-right-Kan : (g : Y â†’ U Ì‡) â†’ Nat g f/j â‰ƒ Nat (g âˆ˜ j) f
  Î -extension-right-Kan g = e
   where
    Ï† : (g : Y â†’ U Ì‡) â†’ Nat (g âˆ˜ j) f â†’ Nat g f/j
    Ï† g Î· y C (x , p) = Î· x (transport g (p â»Â¹) C)

    Ïˆ : (g : Y â†’ U Ì‡) â†’ Nat g f/j â†’ Nat (g âˆ˜ j) f
    Ïˆ g Î¸ x C = Î¸ (j x) C (x , refl)

    ÏˆÏ† : (g : Y â†’ U Ì‡) (Î· : Nat (g âˆ˜ j) f) (x : X) (C : g (j x)) â†’ Ïˆ g (Ï† g Î·) x C â‰¡ Î· x C
    ÏˆÏ† g Î· x C = refl

    Ï†Ïˆ : (g : Y â†’ U Ì‡) (Î¸ : Nat g f/j) (y : Y) (C : g y) (w : fiber j y) â†’ Ï† g (Ïˆ g Î¸) y C w â‰¡ Î¸ y C w
    Ï†Ïˆ g Î¸ y C (x , refl) = refl

    e : Nat g f/j â‰ƒ Nat (g âˆ˜ j) f
    e = Ïˆ g , (Ï† g , Î» Î· â†’ dfunext (fe U (W âŠ” U)) (Î» x â†’ dfunext (fe U W) (ÏˆÏ† g Î· x )))
            , (Ï† g , Î» Î¸ â†’ dfunext (fe V (U âŠ” V âŠ” W)) (Î» y â†’ dfunext (fe U (U âŠ” V âŠ” W)) (Î» C â†’ dfunext (fe (U âŠ” V) W) (Ï†Ïˆ g Î¸ y C))))

  Î£-extension-left-Kan : (g : Y â†’ U Ì‡) â†’ Nat fâˆ–j g â‰ƒ Nat f (g âˆ˜ j)
  Î£-extension-left-Kan g = e
   where
    Ï† : (g : Y â†’ U Ì‡) â†’ Nat f (g âˆ˜ j) â†’ Nat fâˆ–j g
    Ï† g Î· y ((x , p) , C) = transport g p (Î· x C)

    Ïˆ : (g : Y â†’ U Ì‡) â†’ Nat fâˆ–j g â†’ Nat f (g âˆ˜ j)
    Ïˆ g Î¸ x B = Î¸ (j x) ((x , refl) , B)

    Ï†Ïˆ : (g : Y â†’ U Ì‡) (Î¸ : Nat fâˆ–j g) (y : Y) (B : fâˆ–j y) â†’ Ï† g (Ïˆ g Î¸) y B â‰¡ Î¸ y B
    Ï†Ïˆ g Î¸ y ((x , refl) , B) = refl

    ÏˆÏ† : (g : Y â†’ U Ì‡) (Î· : Nat f (g âˆ˜ j)) (x : X) (B : f x) â†’ Ïˆ g (Ï† g Î·) x B â‰¡ Î· x B
    ÏˆÏ† g Î· x B = refl

    e : Nat fâˆ–j g â‰ƒ Nat f (g âˆ˜ j)
    e = Ïˆ g , (Ï† g , Î» Î· â†’ dfunext (fe U (U âŠ” W)) (Î» x â†’ dfunext (fe W U) (Î» B â†’ ÏˆÏ† g Î· x B)))
            , (Ï† g , Î» Î¸ â†’ dfunext (fe V (U âŠ” V âŠ” W)) (Î» y â†’ dfunext (fe (U âŠ” V âŠ” W) U) (Î» C â†’ Ï†Ïˆ g Î¸ y C)))

\end{code}

  Conjectural conjecture: the type

    Î£(f' : Y â†’ U), Î (g : Y â†’ U), Nat g f' = Nat (gâˆ˜f) f

  should be contractible assuming univalence. Similarly for left can
  extensions as discussed below.

  The above formula actually give extensions up to pointwise
  equivalence if j:Xâ†’Y is an embedding in the sense of univalent
  mathematics.

\begin{code}

  open import UF-PropIndexedPiSigma

  Î -extension-in-range : is-embedding j â†’ (x : X) â†’ f/j(j x) â‰ƒ f x
  Î -extension-in-range e x = prop-indexed-product (fe (U âŠ” V) W) {fiber j (j x)} {Î» (z : fiber j (j x)) â†’ f (prâ‚ z)} (e (j x)) (x , refl)

  Î -extension-out-of-range : âˆ€ {W} (y : Y) â†’ ((x : X) â†’ j x â‰¢ y) â†’ f/j(y) â‰ƒ ðŸ™ {W}
  Î -extension-out-of-range y Ï† = prop-indexed-product-one (fe (U âŠ” V) W) (uncurry Ï†)

  Î£-extension-in-range : is-embedding j â†’ (x : X) â†’ fâˆ–j(j x) â‰ƒ f x
  Î£-extension-in-range e x = prop-indexed-sum (e(j x)) (x , refl)

  Î£-extension-out-of-range : (y : Y) â†’ ((x : X) â†’ j x â‰¢ y) â†’ fâˆ–j(y) â‰ƒ ðŸ˜
  Î£-extension-out-of-range y Ï† = prop-indexed-sum-zero (uncurry Ï†)

\end{code}

  We now rewrite the Î -extension formula in an equivalent way:

\begin{code}

  2nd-Î -extension-formula : (y : Y) â†’ f/j(y) â‰ƒ Î  \(x : X) â†’ j x â‰¡ y â†’ f x
  2nd-Î -extension-formula y = Curry-Uncurry fe

  2nd-Î£-extension-formula : (y : Y) â†’ fâˆ–j(y) â‰ƒ Î£ \(x : X) â†’ (j x â‰¡ y) Ã— f x
  2nd-Î£-extension-formula y = Î£-assoc


\end{code}

  This is the Î -extension formula we actually used for (1) showing that
  the universe is indiscrete, and (2) defining the squashed sum and
  proving that it preserves searchability.

\begin{code}

  Î -observation : is-embedding j â†’ (a : X) â†’ f a â‰ƒ (Î  \(x : X) â†’ j x â‰¡ j a â†’ f x)
  Î -observation e a = â‰ƒ-sym (â‰ƒ-trans (â‰ƒ-sym (2nd-Î -extension-formula (j a)))
                                      (Î -extension-in-range e a))

  Î£-observation : is-embedding j â†’ (a : X) â†’ f a â‰ƒ (Î£ \(x : X) â†’ (j x â‰¡ j a) Ã— f x)
  Î£-observation e a = â‰ƒ-sym (â‰ƒ-trans (â‰ƒ-sym (2nd-Î£-extension-formula (j a)))
                                      (Î£-extension-in-range e a))

\end{code}

Added December 2017:

The extensions f/j and fâˆ–j have the same product and sum as f
respectively:

\begin{code}

  same-Î  : Î  f â‰ƒ Î  f/j
  same-Î  = F , (G , FG) , (G , GF)
    where
      F : Î  f â†’ Î  f/j
      F Ï† y (x , p) = Ï† x

      G : Î  f/j â†’ Î  f
      G Ïˆ x = Ïˆ (j x) (x , refl)

      FG' : (Ïˆ : Î  f/j) (y : Y) (Ïƒ : fiber j y) â†’ F(G Ïˆ) y Ïƒ â‰¡ Ïˆ y Ïƒ
      FG' Ïˆ x (_ , refl) = refl

      FG : (Ïˆ : Î  f/j) â†’ F(G Ïˆ) â‰¡ Ïˆ
      FG Ïˆ = dfunext (fe V (U âŠ” V âŠ” W)) (Î» y â†’ dfunext (fe (U âŠ” V) W) (FG' Ïˆ y))

      GF : (Ï† : Î  f) â†’ G(F Ï†) â‰¡ Ï†
      GF Ï† = refl

  same-Î£ : Î£ f â‰ƒ Î£ fâˆ–j
  same-Î£ = F , (G , FG) , (G , GF)
    where
      F : Î£ f â†’ Î£ fâˆ–j
      F (x , y) = (j x , (x , refl) , y)

      G : Î£ fâˆ–j â†’ Î£ f
      G (y , (x , p) , y') = (x , y')

      FG : (Ïƒ : Î£ fâˆ–j) â†’ F(G Ïƒ) â‰¡ Ïƒ
      FG (x , (_ , refl) , y') = refl

      GF : (Ïƒ : Î£ f) â†’ G(F Ïƒ) â‰¡ Ïƒ
      GF (x , y) = refl

\end{code}

(Conjectural conjecture (2nd July 2018): if j is an embedding, then we have an embedding Î£ f â†’ Î£ f/j.)

We now introduce the notations f / j and f âˆ– j for the Î - and
Î£-extensions, outside the above anonymous module.

\begin{code}

_/_ _âˆ–_ :  âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡}
        â†’ (X â†’ W Ì‡) â†’ (X â†’ Y) â†’ (Y â†’ U âŠ” V âŠ” W Ì‡)
f / j = Î -extension f j
f âˆ– j = Î£-extension f j

\end{code}

Also added December 2017.

A different notation reflects a different view of these processes:

\begin{code}

inverse-image :  âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡}
              â†’ (X â†’ Y) â†’ (Y â†’ W Ì‡) â†’ (X â†’ W Ì‡)

inverse-image f v = v âˆ˜ f


Î -image Î£-image :  âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡}
                â†’ (X â†’ Y) â†’ ((X â†’ W Ì‡) â†’ (Y â†’ U âŠ” V âŠ” W Ì‡))

Î -image j = Î» f â†’ Î -extension f j

Î£-image j = Î» f â†’ Î£-extension f j

\end{code}

Î£-images of singletons. Another way to see the following is with the
function same-Î£ defined above. This and univalence give

 Î£ (Id x) â‰¡ Î£ (Id x âˆ– j) = Î£-image j (Id x)

Hence

 is-singleton(Î£ (Id x)) â‰¡ is-singleton(Î£-image j (Id x))

But the lhs holds, and hence is-singleton(Î£-image j (Id x)).

\begin{code}

Î£-image-of-singleton-lemma : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ (j : X â†’ Y) (x : X) (y : Y)
                           â†’ Î£-image j (Id x) y â‰ƒ Id (j x) y
Î£-image-of-singleton-lemma {U} {V} {X} {Y} j x y = (f , (g , fg) , (g , gf))
 where
  f : Î£-image j (Id x) y â†’ Id (j x) y
  f ((x , refl) , refl) = refl

  g : Id (j x) y â†’ Î£-image j (Id x) y
  g refl = (x , refl) , refl

  gf : (i : Î£-image j (Id x) y) â†’ g(f i) â‰¡ i
  gf ((x , refl) , refl) = refl

  fg : (p : Id (j x) y) â†’ f(g p) â‰¡ p
  fg refl = refl

Î£-image-of-singleton : âˆ€ {U} {X Y : U Ì‡}
                     â†’ is-univalent U
                     â†’ (j : X â†’ Y) (x : X) â†’ Î£-image j (Id x) â‰¡ Id (j x)
Î£-image-of-singleton {U} {X} {Y} ua j x = b
  where
   a : (y : Y) â†’ Î£-image j (Id x) y â‰¡ Id (j x) y
   a y = eqtoid ua (Î£-image j (Id x) y) (Id (j x) y) (Î£-image-of-singleton-lemma j x y)

   b : Î£-image j (Id x) â‰¡ Id (j x)
   b = dfunext (fe U (U â€²)) a

\end{code}

There is more to do about this.

We now consider injectivity, defined with Î£ rather than âˆƒ (that is, as
data rather than property):

\begin{code}

injectiveType : âˆ€ {U V W} â†’ W Ì‡ â†’ U â€² âŠ” V â€² âŠ” W Ì‡
injectiveType {U} {V} D = {X : U Ì‡} {Y : V Ì‡} (j : X â†’ Y) â†’ is-embedding j
                       â†’ (f : X â†’ D) â†’ Î£ \(f' : Y â†’ D) â†’ f' âˆ˜ j âˆ¼ f

universes-are-injective-Î  : âˆ€ {U} â†’ is-univalent U â†’ injectiveType {U} {U} (U Ì‡)
universes-are-injective-Î  ua j e f = f / j , Î» x â†’ eqtoid ua _ _ (Î -extension-in-range f j e x)

universes-are-injective-Î£ : âˆ€ {U} â†’ is-univalent U â†’ injectiveType {U} {U} (U Ì‡)
universes-are-injective-Î£ ua j e f = f âˆ– j , Î» x â†’ eqtoid ua _ _ (Î£-extension-in-range f j e x)

retracts-of-injectives : âˆ€ {U V W T} {D : U Ì‡} {D' : V Ì‡}
                       â†’ injectiveType {W} {T} D â†’ retract D' Of D â†’ injectiveType D'
retracts-of-injectives {U} {V} {W} {T} {D} {D'} i (r , Ï) {X} {Y} j e f = r âˆ˜ g , go
  where
    s : D' â†’ D
    s d' = prâ‚ (Ï d')

    rs : r âˆ˜ s âˆ¼ id
    rs d' = prâ‚‚(Ï d')

    g : Y â†’ D
    g = prâ‚(i j e (s âˆ˜ f))

    h : g âˆ˜ j âˆ¼ s âˆ˜ f
    h = prâ‚‚(i j e (s âˆ˜ f))

    go : r âˆ˜ g âˆ˜ j âˆ¼ f
    go x = ap r (h x) âˆ™ rs (f x)

open import UF-IdEmbedding

injective-retract-of-power-of-universe : âˆ€ {U} {D : U Ì‡} â†’ is-univalent U
                                       â†’ injectiveType D â†’ retract D Of (D â†’ U Ì‡)
injective-retract-of-power-of-universe ua i = prâ‚ a , Î» y â†’ Id y , prâ‚‚ a y
  where
    a : Î£ \r  â†’ r âˆ˜ Id âˆ¼ id
    a = i Id (UA-Id-embedding-Theorem ua fe) id

power-of-injective : âˆ€ {U V W T} {D : U Ì‡} {A : V Ì‡}
                   â†’ injectiveType {W} {T} D â†’ injectiveType (A â†’ D)
power-of-injective {U} {V} {W} {T} {D} {A} i {X} {Y} j e f = f' , g
  where
    l : (a : A) â†’ Î£ \(h : Y â†’ D) â†’ h âˆ˜ j âˆ¼ (Î» x â†’ f x a)
    l a = i j e (Î» x â†’ f x a)

    f' : Y â†’ A â†’ D
    f' y a = prâ‚ (l a) y

    g : f' âˆ˜ j âˆ¼ f
    g x = dfunext (fe V U) (Î» a â†’ prâ‚‚ (l a) x)

\end{code}

Added 18th July 2018. Notice that the function e : X â†’ Y doesn't need
to be an embedding and that the proof is completely routine.

\begin{code}

retract-extension : âˆ€ {U V W T} {X : U Ì‡} {Y : V Ì‡} (A : X â†’ W Ì‡) (B : X â†’ T Ì‡) (e : X â†’ Y)
               â†’ ((x : X) â†’ retract (A x) of (B x))
               â†’ ((y : Y) â†’ retract ((A / e) y) of ((B / e) y))
retract-extension {U} {V} {W} {T} {X} {Y} A B e Ï y = r , s , rs
 where
  R : (x : X) â†’ B x â†’ A x
  R x = prâ‚(Ï x)
  S : (x : X) â†’ A x â†’ B x
  S x = prâ‚(prâ‚‚(Ï x))
  RS : (x : X) (a : A x) â†’ R x (S x a) â‰¡ a
  RS x = prâ‚‚(prâ‚‚(Ï x))
  r : (B / e) y â†’ (A / e) y
  r v (x , p) = R x (v (x , p))
  s : (A / e) y â†’ (B / e) y
  s u (x , p) = S x (u (x , p))
  h : (u : (A / e) y) (Ïƒ : fiber e y) â†’ r (s u) Ïƒ â‰¡ u Ïƒ
  h u (x , p) = RS x (u (x , p))
  rs : (u : (A / e) y) â†’ r (s u) â‰¡ u
  rs u = dfunext (fe (U âŠ” V) W) (h u)

\end{code}

Added 25th July 2018.

\begin{code}

iterated-extension : âˆ€ {U V W T} {X : U Ì‡} {Y : V Ì‡} {Z : W Ì‡} {A : X â†’ T Ì‡}
                     (j : X â†’ Y) (k : Y â†’ Z)
                  â†’ (z : Z) â†’ ((A / j) / k) z â‰ƒ (A / (k âˆ˜ j)) z
iterated-extension {U} {V} {W} {T} {X} {Y} {Z} {A} j k z = Î³
 where
  f : ((A / j) / k) z â†’ (A / (k âˆ˜ j)) z
  f u (x , p) = u (j x , p) (x , refl)
  g : (A / (k âˆ˜ j)) z â†’ ((A / j) / k) z
  g v (.(j x) , q) (x , refl) = v (x , q)
  fg : (v : (A / (k âˆ˜ j)) z) â†’ f (g v) â‰¡ v
  fg v = refl
  gf' : (u : ((A / j) / k) z) (w : fiber k z) (t : fiber j (prâ‚ w))
      â†’ g (f u) w t â‰¡ u w t
  gf' u (.(j x) , q) (x , refl) = refl
  gf : (u : ((A / j) / k) z) â†’ g (f u) â‰¡ u
  gf u = dfunext (fe (V âŠ” W) (U âŠ” V âŠ” T))
          (Î» w â†’ dfunext (fe (U âŠ” V) T) (gf' u w))
  Î³ : ((A / j) / k) z â‰ƒ (A / (k âˆ˜ j)) z
  Î³ = f , ((g , fg) , (g , gf))

\end{code}
