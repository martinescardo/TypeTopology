Martin Escardo 2012.

See my JSL paper "Infinite sets that satisfy the principle of
omniscience" for a discussion of the type â„•âˆ defined here.
Essentially, â„•âˆ is â„• with an added point âˆ.

Added December 2017. What we knew for a long time: The â„•âˆ is a retract
of the Cantor type â„• â†’ ğŸš. This required adding a number of
lemmas. More additions after that date.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module GenericConvergentSequence where

open import SpartanMLTT
open import Two-Properties
open import NaturalsAddition renaming (_+_ to _â§¾_)
open import NaturalsOrder hiding (max)
open import DiscreteAndSeparated
open import OrderNotation
open import CanonicalMapNotation
open import UF-Base
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-FunExt
open import UF-Embeddings
open import UF-Equiv
open import UF-Retracts
open import UF-Miscelanea

funextâ‚€ : ğ“¤â‚ Ì‡
funextâ‚€ = funext ğ“¤â‚€ ğ“¤â‚€

\end{code}

Definition (The generic convergent sequence).  We use u,v,x to range
over â„•âˆ and Î±,Î² to range over (â„• â†’ ğŸš):

\begin{code}

is-decreasing : (â„• â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
is-decreasing Î± = (i : â„•) â†’ Î± i â‰¥ Î± (i â§¾ 1)

being-decreasing-is-prop : funextâ‚€ â†’ (Î± : â„• â†’ ğŸš) â†’ is-prop (is-decreasing Î±)
being-decreasing-is-prop fe Î± = Î -is-prop fe (Î» _ â†’ â‰¤â‚‚-is-prop-valued)

â„•âˆ : ğ“¤â‚€ Ì‡
â„•âˆ = Î£ Î± ê‰ (â„• â†’ ğŸš) , is-decreasing Î±

incl : â„•âˆ â†’ (â„• â†’ ğŸš)
incl = prâ‚

incl-lc : funextâ‚€ â†’ left-cancellable incl
incl-lc fe = prâ‚-lc (being-decreasing-is-prop fe _)

force-decreasing : (â„• â†’ ğŸš) â†’ (â„• â†’ ğŸš)
force-decreasing Î² 0        = Î² 0
force-decreasing Î² (succ i) = minğŸš (Î² (i â§¾ 1)) (force-decreasing Î² i)

force-decreasing-is-decreasing : (Î² : â„• â†’ ğŸš) â†’ is-decreasing (force-decreasing Î²)
force-decreasing-is-decreasing Î² zero     = Lemma[minabâ‰¤â‚‚b]
force-decreasing-is-decreasing Î² (succ i) = Lemma[minabâ‰¤â‚‚b] {Î² (i â§¾ 2)}
                                                            {force-decreasing Î² (i â§¾ 1)}

force-decreasing-unchanged : (Î± : â„• â†’ ğŸš) â†’ is-decreasing Î± â†’ force-decreasing Î± âˆ¼ Î±
force-decreasing-unchanged Î± d zero     = refl
force-decreasing-unchanged Î± d (succ i) = g
  where
    IH : force-decreasing Î± i â‰¡ Î± i
    IH = force-decreasing-unchanged Î± d i

    p : Î± (i â§¾ 1) â‰¤ Î± i
    p = d i

    h : minğŸš (Î± (i â§¾ 1)) (Î± i) â‰¡ Î± (i â§¾ 1)
    h = Lemma[aâ‰¤â‚‚bâ†’minğŸšabâ‰¡a] p

    g' : minğŸš (Î± (i â§¾ 1)) (force-decreasing Î± i) â‰¡ Î± (i â§¾ 1)
    g' = transport (Î» b â†’ minğŸš (Î± (i â§¾ 1)) b â‰¡ Î± (i â§¾ 1)) (IH â»Â¹) h

    g : force-decreasing Î± (i â§¾ 1) â‰¡ Î± (i â§¾ 1)
    g = g'

lcni : (â„•  â†’ ğŸš) â†’ â„•âˆ
lcni Î² = force-decreasing Î² , force-decreasing-is-decreasing Î²

lcni-incl : funextâ‚€ â†’ (x : â„•âˆ) â†’ lcni (incl x) â‰¡ x
lcni-incl fe (Î± , d) = to-Î£-â‰¡ (dfunext fe (force-decreasing-unchanged Î± d) ,
                               being-decreasing-is-prop fe Î± _ _)

â„•âˆ-retract-of-Cantor : funextâ‚€ â†’ retract â„•âˆ of (â„• â†’ ğŸš)
â„•âˆ-retract-of-Cantor fe = lcni , incl , lcni-incl fe

force-decreasing-is-smaller : (Î² : â„• â†’ ğŸš) (i : â„•) â†’ force-decreasing Î² i â‰¤ Î² i
force-decreasing-is-smaller Î² zero     = â‰¤â‚‚-refl
force-decreasing-is-smaller Î² (succ i) = Lemma[minabâ‰¤â‚‚a]

force-decreasing-is-not-much-smaller : (Î² : â„• â†’ ğŸš) (n : â„•)
                                     â†’ force-decreasing Î² n â‰¡ â‚€
                                     â†’ Î£ m ê‰ â„• , Î² m â‰¡ â‚€
force-decreasing-is-not-much-smaller Î² zero  p    = zero , p
force-decreasing-is-not-much-smaller Î² (succ n) p = f c
  where
    A = Î£ m ê‰ â„• , Î² m â‰¡ â‚€

    c : (Î² (n â§¾ 1) â‰¡ â‚€) + (force-decreasing Î² n â‰¡ â‚€)
    c = lemma[minğŸšabâ‰¡â‚€] {Î² (n â§¾ 1)} {force-decreasing Î² n} p

    f : (Î² (n â§¾ 1) â‰¡ â‚€) + (force-decreasing Î² n â‰¡ â‚€) â†’ A
    f (inl q) = n â§¾ 1 , q
    f (inr r) = force-decreasing-is-not-much-smaller Î² n r

Cantor-is-Â¬Â¬-separated : funextâ‚€ â†’ is-Â¬Â¬-separated (â„• â†’ ğŸš)
Cantor-is-Â¬Â¬-separated fe = Î -is-Â¬Â¬-separated fe (Î» _ â†’ ğŸš-is-Â¬Â¬-separated)

â„•âˆ-is-Â¬Â¬-separated : funextâ‚€ â†’ is-Â¬Â¬-separated â„•âˆ
â„•âˆ-is-Â¬Â¬-separated fe = subtype-is-Â¬Â¬-separated
                         prâ‚
                         (incl-lc fe)
                         (Cantor-is-Â¬Â¬-separated fe)

â„•âˆ-is-set : funextâ‚€ â†’ is-set â„•âˆ
â„•âˆ-is-set fe = Â¬Â¬-separated-types-are-sets fe (â„•âˆ-is-Â¬Â¬-separated fe)

open import TotallySeparated

â„•âˆ-is-totally-separated : funextâ‚€ â†’ is-totally-separated â„•âˆ
â„•âˆ-is-totally-separated fe = retract-of-totally-separated
                              (â„•âˆ-retract-of-Cantor fe)
                              (Cantor-is-totally-separated fe)

Zero : â„•âˆ
Zero = (Î» i â†’ â‚€) , (Î» i â†’ â‰¤â‚‚-refl {â‚€})

Succ : â„•âˆ â†’ â„•âˆ
Succ (Î± , d) = (Î±' , d')
 where
  Î±' : â„• â†’ ğŸš
  Î±' 0 = â‚
  Î±'(succ n) = Î± n

  d' : is-decreasing Î±'
  d' 0 = â‚-top
  d' (succ i) = d i

instance
 Square-Order-â„•âˆ-â„• : Square-Order â„•âˆ â„•
 _âŠ‘_ {{Square-Order-â„•âˆ-â„•}} u n = incl u n â‰¡ â‚€

 Strict-Square-Order-â„•-â„•âˆ : Strict-Square-Order â„• â„•âˆ
 _âŠ_ {{Strict-Square-Order-â„•-â„•âˆ}} n u = incl u n â‰¡ â‚

not-âŠ-is-âŠ’ : {m : â„•} {u : â„•âˆ} â†’ Â¬ (m âŠ u) â†’ u âŠ‘ m
not-âŠ-is-âŠ’ f = different-from-â‚-equal-â‚€ f

not-âŠ‘-is-âŠ : {m : â„•} {u : â„•âˆ} â†’ Â¬ (u âŠ‘ m) â†’ m âŠ u
not-âŠ‘-is-âŠ f = different-from-â‚€-equal-â‚ f

is-Zero : â„•âˆ â†’ ğ“¤â‚€ Ì‡
is-Zero u = u âŠ‘ 0

is-positive : â„•âˆ â†’ ğ“¤â‚€ Ì‡
is-positive u = 0 âŠ u

positivity : â„•âˆ â†’ ğŸš
positivity u = incl u 0

is-Zero-Zero : is-Zero Zero
is-Zero-Zero = refl

is-positive-Succ : (Î± : â„•âˆ) â†’ is-positive (Succ Î±)
is-positive-Succ Î± = refl

Zero-not-Succ : {u : â„•âˆ} â†’ Zero â‰¢ Succ u
Zero-not-Succ {u} r = zero-is-not-one (ap positivity r)

Succ-not-Zero : {u : â„•âˆ} â†’ Succ u â‰¢ Zero
Succ-not-Zero = â‰¢-sym Zero-not-Succ

âˆ : â„•âˆ
âˆ = (Î» i â†’ â‚) , (Î» i â†’ â‰¤â‚‚-refl {â‚})

Succ-âˆ-is-âˆ : funextâ‚€ â†’ Succ âˆ â‰¡ âˆ
Succ-âˆ-is-âˆ fe = incl-lc fe (dfunext fe lemma)
 where
   lemma : (i : â„•) â†’ incl (Succ âˆ) i â‰¡ incl âˆ i
   lemma 0        = refl
   lemma (succ i) = refl

unique-fixed-point-of-Succ : funextâ‚€ â†’ (u : â„•âˆ) â†’ u â‰¡ Succ u â†’ u â‰¡ âˆ
unique-fixed-point-of-Succ fe u r = incl-lc fe claim
 where
  fact : (i : â„•) â†’ incl u i â‰¡ incl (Succ u) i
  fact i = ap (Î» - â†’ incl - i) r

  lemma : (i : â„•) â†’ incl u i â‰¡ â‚
  lemma 0        = fact 0
  lemma (succ i) = incl u (i â§¾ 1)        â‰¡âŸ¨ fact (i â§¾ 1) âŸ©
                   incl (Succ u) (i â§¾ 1) â‰¡âŸ¨ lemma i âŸ©
                   â‚                     âˆ

  claim : incl u â‰¡ incl âˆ
  claim = dfunext fe lemma

Pred : â„•âˆ â†’ â„•âˆ
Pred (Î± , d) = (Î± âˆ˜ succ , d âˆ˜ succ)

Pred-Zero-is-Zero : Pred Zero â‰¡ Zero
Pred-Zero-is-Zero = refl

Pred-Zero-is-Zero' : (u : â„•âˆ) â†’ u â‰¡ Zero â†’ Pred u â‰¡ u
Pred-Zero-is-Zero' u p = transport (Î» - â†’ Pred - â‰¡ -) (p â»Â¹) Pred-Zero-is-Zero

Pred-Succ : {u : â„•âˆ} â†’ Pred (Succ u) â‰¡ u
Pred-Succ {u} = refl

Pred-âˆ-is-âˆ : Pred âˆ â‰¡ âˆ
Pred-âˆ-is-âˆ = refl

Succ-lc : left-cancellable Succ
Succ-lc = ap Pred

â„•-to-â„•âˆ : â„• â†’ â„•âˆ
â„•-to-â„•âˆ 0        = Zero
â„•-to-â„•âˆ (succ n) = Succ (â„•-to-â„•âˆ n)

instance
 Canonical-Map-â„•-â„•âˆ : Canonical-Map â„• â„•âˆ
 Î¹ {{Canonical-Map-â„•-â„•âˆ}} = â„•-to-â„•âˆ

_â‰£_ : â„•âˆ â†’ â„• â†’ ğ“¤â‚€ Ì‡
u â‰£ n = u â‰¡ Î¹ n

â„•-to-â„•âˆ-lc : left-cancellable Î¹
â„•-to-â„•âˆ-lc {0}      {0}      r = refl
â„•-to-â„•âˆ-lc {0}      {succ n} r = ğŸ˜-elim (Zero-not-Succ r)
â„•-to-â„•âˆ-lc {succ m} {0}      r = ğŸ˜-elim (Zero-not-Succ (r â»Â¹))
â„•-to-â„•âˆ-lc {succ m} {succ n} r = ap succ (â„•-to-â„•âˆ-lc {m} {n} (Succ-lc r))

Î¹-embedding : funextâ‚€ â†’ is-embedding Î¹
Î¹-embedding fe = lc-maps-into-sets-are-embeddings Î¹ â„•-to-â„•âˆ-lc (â„•âˆ-is-set fe)

â„•-to-â„•âˆ-lc-refl : (k : â„•) â†’ â„•-to-â„•âˆ-lc refl â‰¡ refl {_} {â„•} {k}
â„•-to-â„•âˆ-lc-refl 0        = refl
â„•-to-â„•âˆ-lc-refl (succ k) = ap (ap succ) (â„•-to-â„•âˆ-lc-refl k)

Î¹-diagonalâ‚€ : (n : â„•) â†’ Î¹ n âŠ‘ n
Î¹-diagonalâ‚€ 0        = refl
Î¹-diagonalâ‚€ (succ n) = Î¹-diagonalâ‚€ n

Î¹-diagonalâ‚ : (n : â„•) â†’ n âŠ Î¹ (n â§¾ 1)
Î¹-diagonalâ‚ 0        = refl
Î¹-diagonalâ‚ (succ n) = Î¹-diagonalâ‚ n

is-Zero-equal-Zero : funextâ‚€ â†’ {u : â„•âˆ} â†’ is-Zero u â†’ u â‰¡ Zero
is-Zero-equal-Zero fe {u} base = incl-lc fe (dfunext fe lemma)
 where
  lemma : (i : â„•) â†’ incl u i â‰¡ incl Zero i
  lemma 0        = base
  lemma (succ i) = [aâ‰¡â‚â†’bâ‰¡â‚]-gives-[bâ‰¡â‚€â†’aâ‰¡â‚€] (â‰¤â‚‚-criterion-converse (prâ‚‚ u i)) (lemma i)

same-positivity : funextâ‚€ â†’ (u v : â„•âˆ)
                â†’ (u â‰¡ Zero â†’ v â‰¡ Zero)
                â†’ (v â‰¡ Zero â†’ u â‰¡ Zero)
                â†’ positivity u â‰¡ positivity v
same-positivity feâ‚€ u v f g = â‰¤â‚‚-anti (â‰¤â‚‚'-gives-â‰¤â‚‚ a)
                                      (â‰¤â‚‚'-gives-â‰¤â‚‚ b)
 where
  a : is-Zero v â†’ is-Zero u
  a p = back-transport is-Zero (g (is-Zero-equal-Zero feâ‚€ p)) refl

  b : is-Zero u â†’ is-Zero v
  b p = back-transport is-Zero (f (is-Zero-equal-Zero feâ‚€ p)) refl

successors-same-positivity : {u u' v v' : â„•âˆ}
                           â†’ u â‰¡ Succ u'
                           â†’ v â‰¡ Succ v'
                           â†’ positivity u â‰¡ positivity v
successors-same-positivity refl refl = refl

not-Zero-is-Succ : funextâ‚€ â†’ {u : â„•âˆ} â†’ u â‰¢ Zero â†’ u â‰¡ Succ (Pred u)
not-Zero-is-Succ fe {u} f = incl-lc fe (dfunext fe lemma)
 where
  lemma : (i : â„•) â†’ incl u i â‰¡ incl (Succ (Pred u)) i
  lemma 0        = different-from-â‚€-equal-â‚ (f âˆ˜ is-Zero-equal-Zero fe)
  lemma (succ i) = refl

positive-is-not-Zero : {u : â„•âˆ} â†’ is-positive u â†’ u â‰¢ Zero
positive-is-not-Zero {u} r s = lemma r
 where
  lemma : Â¬ (is-positive u)
  lemma = equal-â‚€-different-from-â‚ (ap positivity s)

positive-equal-Succ : funextâ‚€ â†’ {u : â„•âˆ} â†’ is-positive u â†’ u â‰¡ Succ (Pred u)
positive-equal-Succ fe r = not-Zero-is-Succ fe (positive-is-not-Zero r)

Zero-or-Succ : funextâ‚€ â†’ (u : â„•âˆ) â†’ (u â‰¡ Zero) + (u â‰¡ Succ (Pred u))
Zero-or-Succ feâ‚€ u = ğŸš-equality-cases
                      (Î» (z : is-Zero u) â†’ inl (is-Zero-equal-Zero feâ‚€ z))
                      (Î» (p : is-positive u) â†’ inr (positive-equal-Succ feâ‚€ p))

is-Succ : â„•âˆ â†’ ğ“¤â‚€ Ì‡
is-Succ u = Î£ w ê‰ â„•âˆ , u â‰¡ Succ w

Zero+Succ : funextâ‚€ â†’ (u : â„•âˆ) â†’ (u â‰¡ Zero) + is-Succ u
Zero+Succ feâ‚€ u = Cases (Zero-or-Succ feâ‚€ u) inl (Î» p â†’ inr (Pred u , p))

Succ-criterion : funextâ‚€ â†’ {u : â„•âˆ} {n : â„•} â†’ n âŠ u â†’ u âŠ‘ n â§¾ 1 â†’ u â‰¡ Succ (Î¹ n)
Succ-criterion fe {u} {n} r s = incl-lc fe claim
 where
  lemma : (u : â„•âˆ) (n : â„•) â†’ n âŠ u â†’ u âŠ‘ n â§¾ 1
        â†’ (i : â„•) â†’ incl u i â‰¡ incl (Succ (Î¹ n)) i
  lemma u 0 r s 0        = r
  lemma u 0 r s (succ i) = lemmaâ‚€ i
     where
      lemmaâ‚€ : (i : â„•) â†’ u âŠ‘ i â§¾ 1
      lemmaâ‚€ 0        = s
      lemmaâ‚€ (succ i) = [aâ‰¡â‚â†’bâ‰¡â‚]-gives-[bâ‰¡â‚€â†’aâ‰¡â‚€] (â‰¤â‚‚-criterion-converse (prâ‚‚ u (i â§¾ 1))) (lemmaâ‚€ i)
  lemma u (succ n) r s 0 = lemmaâ‚ (n â§¾ 1) r
     where
      lemmaâ‚ : (n : â„•) â†’ n âŠ u â†’ is-positive u
      lemmaâ‚ 0        t = t
      lemmaâ‚ (succ n) t = lemmaâ‚ n (â‰¤â‚‚-criterion-converse (prâ‚‚ u n) t)
  lemma u (succ n) r s (succ i) = lemma (Pred u) n r s i

  claim : incl u â‰¡ incl (Succ (Î¹ n))
  claim = dfunext fe (lemma u n r s)

âˆ-is-not-finite : (n : â„•) â†’ âˆ â‰¢ Î¹ n
âˆ-is-not-finite n s = one-is-not-zero (â‚            â‰¡âŸ¨ ap (Î» - â†’ incl - n) s âŸ©
                                       incl (Î¹ n) n â‰¡âŸ¨ Î¹-diagonalâ‚€ n âŸ©
                                       â‚€            âˆ)

not-finite-is-âˆ : funextâ‚€ â†’ {u : â„•âˆ} â†’ ((n : â„•) â†’ u â‰¢ Î¹ n) â†’ u â‰¡ âˆ
not-finite-is-âˆ fe {u} f = incl-lc fe (dfunext fe lemma)
 where
  lemma : (n : â„•) â†’ n âŠ u
  lemma 0        = different-from-â‚€-equal-â‚ (Î» r â†’ f 0 (is-Zero-equal-Zero fe r))
  lemma (succ n) = different-from-â‚€-equal-â‚ (Î» r â†’ f (n â§¾ 1) (Succ-criterion fe (lemma n) r))

â„•âˆ-ddensity : funextâ‚€ â†’ {Y : â„•âˆ â†’ ğ“¤ Ì‡ }
            â†’ ({u : â„•âˆ} â†’ is-Â¬Â¬-separated (Y u))
            â†’ {f g : Î  Y}
            â†’ ((n : â„•) â†’ f (Î¹ n) â‰¡ g (Î¹ n))
            â†’ f âˆ â‰¡ g âˆ
            â†’ (u : â„•âˆ) â†’ f u â‰¡ g u
â„•âˆ-ddensity fe {Y} s {f} {g} h hâˆ u = s (f u) (g u) c
 where
  a : f u â‰¢ g u â†’ (n : â„•) â†’ u â‰¢ Î¹ n
  a t n = contrapositive (Î» (r : u â‰¡ Î¹ n) â†’ back-transport (Î» - â†’ f - â‰¡ g -) r (h n)) t

  b : f u â‰¢ g u â†’ u â‰¢ âˆ
  b = contrapositive (Î» (r : u â‰¡ âˆ) â†’ back-transport (Î» - â†’ f - â‰¡ g -) r hâˆ)

  c : Â¬Â¬ (f u â‰¡ g u)
  c = Î» t â†’ b t (not-finite-is-âˆ fe (a t))

â„•âˆ-density : funextâ‚€
           â†’ {Y : ğ“¤ Ì‡ }
           â†’ is-Â¬Â¬-separated Y
           â†’ {f g : â„•âˆ â†’ Y}
           â†’ ((n : â„•) â†’ f (Î¹ n) â‰¡ g (Î¹ n))
           â†’ f âˆ â‰¡ g âˆ
           â†’ (u : â„•âˆ) â†’ f u â‰¡ g u
â„•âˆ-density fe s = â„•âˆ-ddensity fe (Î» {_} â†’ s)

â„•âˆ-ğŸš-density : funextâ‚€
             â†’ {p : â„•âˆ â†’ ğŸš}
             â†’ ((n : â„•) â†’ p (Î¹ n) â‰¡ â‚)
             â†’ p âˆ â‰¡ â‚
             â†’ (u : â„•âˆ) â†’ p u â‰¡ â‚
â„•âˆ-ğŸš-density fe = â„•âˆ-density fe ğŸš-is-Â¬Â¬-separated

Î¹ğŸ™ : â„• + ğŸ™ â†’ â„•âˆ
Î¹ğŸ™ = cases {ğ“¤â‚€} {ğ“¤â‚€} Î¹ (Î» _ â†’ âˆ)

Î¹ğŸ™-embedding : funextâ‚€ â†’ is-embedding Î¹ğŸ™
Î¹ğŸ™-embedding fe = disjoint-cases-embedding Î¹ (Î» _ â†’ âˆ) (Î¹-embedding fe) g d
 where
  g : is-embedding (Î» _ â†’ âˆ)
  g x (* , p) (â‹† , q) = ap (Î» - â†’ â‹† , -) (â„•âˆ-is-set fe p q)

  d : (n : â„•) (y : ğŸ™) â†’ Î¹ n â‰¢ âˆ
  d n _ p = âˆ-is-not-finite n (p â»Â¹)

Î¹ğŸ™-dense : funextâ‚€ â†’ is-dense Î¹ğŸ™
Î¹ğŸ™-dense fe (u , f) = g (not-finite-is-âˆ fe h)
 where
  g : Â¬ (u â‰¡ âˆ)
  g p = f ((inr â‹†) , (p â»Â¹))

  h : (n : â„•) â†’ Â¬ (u â‰¡ Î¹ n)
  h n p = f (inl n , (p â»Â¹))

\end{code}

There should be a better proof of the following. The idea is simple:
by the above development, u = Î¹ 0 if and only if incl u 0 â‰¡ 0, and
u â‰¡ Î¹ (n+1) if and only if n âŠ u âŠ‘ n+1.

\begin{code}

finite-isolated : funextâ‚€ â†’ (n : â„•) â†’ is-isolated (Î¹ n)
finite-isolated fe n u = decidable-eq-sym u (Î¹ n) (f u n)
 where
  f : (u : â„•âˆ) (n : â„•) â†’ decidable (u â‰¡ Î¹ n)
  f u 0 = ğŸš-equality-cases gâ‚€ gâ‚
   where
    gâ‚€ : is-Zero u â†’ decidable (u â‰¡ Zero)
    gâ‚€ r = inl (is-Zero-equal-Zero fe r)

    h : u â‰¡ Zero â†’ is-Zero u
    h = ap (Î» - â†’ incl - 0)

    gâ‚ : is-positive u â†’ decidable (u â‰¡ Zero)
    gâ‚ r = inr (contrapositive h (equal-â‚-different-from-â‚€ r))

  f u (succ n) = ğŸš-equality-cases gâ‚€ gâ‚
   where
    g : u â‰¡ Î¹ (n â§¾ 1) â†’ n âŠ u
    g r = ap (Î» - â†’ incl - n) r âˆ™ Î¹-diagonalâ‚ n

    gâ‚€ :  u âŠ‘ n â†’ decidable (u â‰¡ Î¹ (n â§¾ 1))
    gâ‚€ r = inr (contrapositive g (equal-â‚€-different-from-â‚ r))

    h : u â‰¡ Î¹ (n â§¾ 1) â†’ u âŠ‘ n â§¾ 1
    h r = ap (Î» - â†’ incl - (n â§¾ 1)) r âˆ™ Î¹-diagonalâ‚€ (n â§¾ 1)

    gâ‚ :  n âŠ u â†’ decidable (u â‰¡ Î¹ (n â§¾ 1))
    gâ‚ r = ğŸš-equality-cases gâ‚â‚€ gâ‚â‚
     where
      gâ‚â‚€ : u âŠ‘ n â§¾ 1 â†’ decidable (u â‰¡ Î¹ (n â§¾ 1))
      gâ‚â‚€ s = inl (Succ-criterion fe r s)

      gâ‚â‚ : n â§¾ 1 âŠ u â†’ decidable (u â‰¡ Î¹ (n â§¾ 1))
      gâ‚â‚ s = inr (contrapositive h (equal-â‚-different-from-â‚€ s))


is-finite : â„•âˆ â†’ ğ“¤â‚€ Ì‡
is-finite u = Î£ n ê‰ â„• , Î¹ n â‰¡ u

size : {u : â„•âˆ} â†’ is-finite u â†’ â„•
size (n , r) = n

being-finite-is-prop : funextâ‚€ â†’ (u : â„•âˆ) â†’ is-prop (is-finite u)
being-finite-is-prop = Î¹-embedding

Î¹-is-finite : (n : â„•) â†’ is-finite (Î¹ n)
Î¹-is-finite n = (n , refl)

Zero-is-finite : is-finite Zero
Zero-is-finite = Î¹-is-finite zero

Zero-is-finite' : funextâ‚€ â†’ (u : â„•âˆ) â†’ is-Zero u â†’ is-finite u
Zero-is-finite' fe u z = back-transport
                           is-finite
                           (is-Zero-equal-Zero fe z)
                           Zero-is-finite

is-finite-down : (u : â„•âˆ) â†’ is-finite (Succ u) â†’ is-finite u
is-finite-down u (zero , r)   = ğŸ˜-elim (Zero-not-Succ r)
is-finite-down u (succ n , r) = n , Succ-lc r

is-finite-up : (u : â„•âˆ) â†’ is-finite u â†’ is-finite (Succ u)
is-finite-up u (n , r) = (n â§¾ 1 , ap Succ r)

is-finite-up' : funextâ‚€ â†’ (u : â„•âˆ) â†’ is-finite (Pred u) â†’ is-finite u
is-finite-up' fe u i = ğŸš-equality-cases
                         (Î» (z : is-Zero u)
                            â†’ Zero-is-finite' fe u z)
                         (Î» (p : is-positive u)
                            â†’ back-transport
                               is-finite
                               (positive-equal-Succ fe p)
                               (is-finite-up (Pred u) i))

is-infinite-âˆ : Â¬ is-finite âˆ
is-infinite-âˆ (n , r) = ğŸ˜-elim (âˆ-is-not-finite n (r â»Â¹))

\end{code}

Order on â„•âˆ:

\begin{code}

_â‰¼â„•âˆ_ : â„•âˆ â†’ â„•âˆ â†’ ğ“¤â‚€ Ì‡
u â‰¼â„•âˆ v = (n : â„•) â†’ n âŠ u â†’ n âŠ v

instance
 Curly-Order-â„•âˆ-â„•âˆ : Curly-Order â„•âˆ â„•âˆ
 _â‰¼_ {{Curly-Order-â„•âˆ-â„•âˆ}} = _â‰¼â„•âˆ_

â‰¼-anti : funextâ‚€ â†’ (u v : â„•âˆ) â†’ u â‰¼ v â†’ v â‰¼ u â†’ u â‰¡ v
â‰¼-anti fe u v l m = incl-lc fe Î³
 where
  Î³ : incl u â‰¡ incl v
  Î³ = dfunext fe (Î» i â†’ â‰¤â‚‚-anti (â‰¤â‚‚-criterion (l i)) (â‰¤â‚‚-criterion (m i)))

âˆ-maximal : (u : â„•âˆ) â†’ u â‰¼ âˆ
âˆ-maximal u = Î» n _ â†’ refl

Zero-minimal : (u : â„•âˆ) â†’ Zero â‰¼ u
Zero-minimal u n = Î» (p : â‚€ â‰¡ â‚) â†’ ğŸ˜-elim (zero-is-not-one p)

Succ-not-â‰¼-Zero : (u : â„•âˆ) â†’ Â¬ (Succ u â‰¼ Zero)
Succ-not-â‰¼-Zero u l = zero-is-not-one (l zero refl)

Succ-monotone : (u v : â„•âˆ) â†’ u â‰¼ v â†’ Succ u â‰¼ Succ v
Succ-monotone u v l zero p = p
Succ-monotone u v l (succ n) p = l n p

Succ-loc : (u v : â„•âˆ) â†’ Succ u â‰¼ Succ v â†’ u â‰¼ v
Succ-loc u v l n = l (n â§¾ 1)

above-Succ-is-positive : (u v : â„•âˆ) â†’ Succ u â‰¼ v â†’ is-positive v
above-Succ-is-positive u v l = l zero refl

â‰¼-unfold-Succ : funextâ‚€ â†’ (u v : â„•âˆ) â†’ Succ u â‰¼ v â†’ Succ u â‰¼ Succ (Pred v)
â‰¼-unfold-Succ fe u v l = transport (Î» - â†’ Succ u â‰¼ -)
                          (positive-equal-Succ fe {v}
                            (above-Succ-is-positive u v l)) l

â‰¼-unfold : funextâ‚€ â†’ (u v : â„•âˆ)
         â†’ u â‰¼ v
         â†’ (u â‰¡ Zero) + (Î£ w ê‰ â„•âˆ , Î£ t ê‰ â„•âˆ , (u â‰¡ Succ w) Ã— (v â‰¡ Succ t) Ã— (w â‰¼ t))
â‰¼-unfold fe u v l = Ï† (Zero+Succ fe u) (Zero+Succ fe v)
 where
  Ï† : (u â‰¡ Zero) + is-Succ u â†’ (v â‰¡ Zero) + is-Succ v â†’ _
  Ï† (inl p)          _                = inl p
  Ï† (inr (w , refl)) (inl refl)       = ğŸ˜-elim (Succ-not-â‰¼-Zero w l)
  Ï† (inr (w , refl)) (inr (t , refl)) = inr (w , t , refl , refl , Succ-loc w t l)

â‰¼-fold : (u v : â„•âˆ)
       â†’ ((u â‰¡ Zero) + (Î£ w ê‰ â„•âˆ , Î£ t ê‰ â„•âˆ , (u â‰¡ Succ w) Ã— (v â‰¡ Succ t) Ã— (w â‰¼ t)))
       â†’ u â‰¼ v
â‰¼-fold Zero      v         (inl refl)                      = Zero-minimal v
â‰¼-fold .(Succ w) .(Succ t) (inr (w , t , refl , refl , l)) = Succ-monotone w t l

max : â„•âˆ â†’ â„•âˆ â†’ â„•âˆ
max (Î± , r) (Î² , s) = (Î» i â†’ maxğŸš (Î± i) (Î² i)) , t
 where
  t : is-decreasing (Î» i â†’ maxğŸš (Î± i) (Î² i))
  t i = maxğŸš-preserves-â‰¤ (r i) (s i)

min : â„•âˆ â†’ â„•âˆ â†’ â„•âˆ
min (Î± , r) (Î² , s) = (Î» i â†’ minğŸš (Î± i) (Î² i)) , t
 where
  t : is-decreasing (Î» i â†’ minğŸš (Î± i) (Î² i))
  t i = minğŸš-preserves-â‰¤ (r i) (s i)

\end{code}

More lemmas about order should be added, but I will do this on demand
as the need arises.

\begin{code}

âˆ-âŠ-maximal : (n : â„•) â†’ n âŠ âˆ
âˆ-âŠ-maximal n = refl

_â‰ºâ„•âˆ_ : â„•âˆ â†’ â„•âˆ â†’ ğ“¤â‚€ Ì‡
u â‰ºâ„•âˆ v = Î£ n ê‰ â„• , (u â‰¡ Î¹ n) Ã— n âŠ v

instance
 Strict-Curly-Order-â„•âˆ-â„•âˆ : Strict-Curly-Order â„•âˆ â„•âˆ
 _â‰º_ {{Strict-Curly-Order-â„•âˆ-â„•âˆ}} = _â‰ºâ„•âˆ_

âˆ-top : (u : â„•âˆ) â†’ Â¬ (âˆ â‰º u)
âˆ-top u (n , r , l) = âˆ-is-not-finite n r

below-isolated : funextâ‚€ â†’ (u v : â„•âˆ) â†’ u â‰º v â†’ is-isolated u
below-isolated fe u v (n , r , l) = back-transport is-isolated r (finite-isolated fe n)

â‰º-prop-valued : funextâ‚€ â†’ (u v : â„•âˆ) â†’ is-prop (u â‰º v)
â‰º-prop-valued fe u v (n , r , a) (m , s , b) = to-Î£-â‰¡ (â„•-to-â„•âˆ-lc (r â»Â¹ âˆ™ s) ,
                                                       to-Î£-â‰¡ (â„•âˆ-is-set fe _ _ ,
                                                               ğŸš-is-set _ _))

âŠ-gives-â‰º : (n : â„•) (u : â„•âˆ) â†’ n âŠ u â†’ Î¹ n â‰º u
âŠ-gives-â‰º n u a = n , refl , a

â‰º-gives-âŠ : (n : â„•) (u : â„•âˆ) â†’ Î¹ n â‰º u â†’ n âŠ u
â‰º-gives-âŠ n u (m , r , a) = back-transport (Î» k â†’ k âŠ u) (â„•-to-â„•âˆ-lc r) a

âˆ-â‰º-maximal : (n : â„•) â†’ Î¹ n â‰º âˆ
âˆ-â‰º-maximal n = n , refl , âˆ-âŠ-maximal n

â‰º-implies-finite : (a b : â„•âˆ) â†’ a â‰º b â†’ is-finite a
â‰º-implies-finite a b (n , p , _) = n , (p â»Â¹)

Î¹-â‰º-diagonal : (n : â„•) â†’ Î¹ n â‰º Î¹ (n â§¾ 1)
Î¹-â‰º-diagonal n = n , refl , Î¹-diagonalâ‚ n

finite-â‰º-Succ : (a : â„•âˆ) â†’ is-finite a â†’ a â‰º Succ a
finite-â‰º-Succ a (n , p) = transport (_â‰º Succ a) p
                            (transport (Î¹ n â‰º_) (ap Succ p)
                              (Î¹-â‰º-diagonal n))

â‰º-Succ : (a b : â„•âˆ) â†’ a â‰º b â†’ Succ a â‰º Succ b
â‰º-Succ a b (n , p , q) = n â§¾ 1 , ap Succ p , q

open import NaturalsOrder

<-gives-âŠ : (m n : â„•) â†’ m < n â†’  m âŠ Î¹ n
<-gives-âŠ zero     zero     l = ğŸ˜-elim l
<-gives-âŠ zero     (succ n) l = refl
<-gives-âŠ (succ m) zero     l = ğŸ˜-elim l
<-gives-âŠ (succ m) (succ n) l = <-gives-âŠ m n l

âŠ-gives-< : (m n : â„•) â†’  m âŠ Î¹ n â†’ m < n
âŠ-gives-< zero     zero     l = ğŸ˜-elim (zero-is-not-one l)
âŠ-gives-< zero     (succ n) l = zero-minimal n
âŠ-gives-< (succ m) zero     l = ğŸ˜-elim (zero-is-not-one l)
âŠ-gives-< (succ m) (succ n) l = âŠ-gives-< m n l

âŠ-back : (u : â„•âˆ) (n : â„•) â†’ n â§¾ 1 âŠ u â†’ n âŠ u
âŠ-back u n = â‰¤â‚‚-criterion-converse (prâ‚‚ u n)

âŠ-trans'' : (u : â„•âˆ) (n : â„•) â†’ (m : â„•) â†’ m â‰¤ n â†’ n âŠ u â†’ m âŠ u
âŠ-trans'' u = regress (Î» n â†’ n âŠ u) (âŠ-back u)

âŠ-trans' : (m : â„•) (n : â„•) (u : â„•âˆ)  â†’ m < n â†’ n âŠ u â†’ m âŠ u
âŠ-trans' m n u l = âŠ-trans'' u n m (â‰¤-trans m (m â§¾ 1) n (â‰¤-succ m) l)

âŠ-trans : (m n : â„•) (u : â„•âˆ) â†’ m âŠ Î¹ n â†’ n âŠ u â†’ m âŠ u
âŠ-trans m n u a = âŠ-trans' m n u (âŠ-gives-< m n a)

open import OrdinalNotions

â‰º-trans : is-transitive _â‰º_
â‰º-trans u v w (m , r , a) (n , s , b) = m , r , c
 where
  c : m âŠ w
  c = âŠ-trans m n w (transport (Î» t â†’ m âŠ t) s a) b

â‰º-Succ-r : (a b : â„•âˆ) â†’ a â‰º b â†’ a â‰º Succ b
â‰º-Succ-r a b l = â‰º-trans a (Succ a) (Succ b)
                     (finite-â‰º-Succ a (â‰º-implies-finite a b l))
                     (â‰º-Succ a b l)

â‰ºâ‰¼-gives-â‰º : (x y z : â„•âˆ) â†’ x â‰º y â†’ y â‰¼ z â†’ x â‰º z
â‰ºâ‰¼-gives-â‰º x y z (n , p , q) le = n , p , le n q

finite-accessible : (n : â„•) â†’ is-accessible _â‰º_ (Î¹ n)
finite-accessible = course-of-values-induction (Î» n â†’ is-accessible _â‰º_ (Î¹ n)) Ï†
 where
  Ï† : (n : â„•)
    â†’ ((m : â„•) â†’ m < n â†’ is-accessible _â‰º_ (Î¹ m))
    â†’ is-accessible _â‰º_ (Î¹ n)
  Ï† n Ïƒ = next (Î¹ n) Ï„
   where
    Ï„ : (u : â„•âˆ) â†’ u â‰º Î¹ n â†’ is-accessible _â‰º_ u
    Ï„ u (m , r , l) = back-transport (is-accessible _â‰º_) r (Ïƒ m (âŠ-gives-< m n l))

â‰º-well-founded : is-well-founded _â‰º_
â‰º-well-founded v = next v Ïƒ
 where
  Ïƒ : (u : â„•âˆ) â†’ u â‰º v â†’ is-accessible _â‰º_ u
  Ïƒ u (n , r , l) = back-transport (is-accessible _â‰º_) r (finite-accessible n)

â‰º-extensional : funextâ‚€ â†’ is-extensional _â‰º_
â‰º-extensional fe u v l m = Î³
 where
  f : (i : â„•) â†’ i âŠ u â†’ i âŠ v
  f i a = â‰º-gives-âŠ i v (l (Î¹ i) (âŠ-gives-â‰º i u a))

  g : (i : â„•) â†’ i âŠ v â†’ i âŠ u
  g i a = â‰º-gives-âŠ i u (m (Î¹ i) (âŠ-gives-â‰º i v a))

  h : (i : â„•) â†’ incl u i â‰¡ incl v i
  h i = â‰¤â‚‚-anti (â‰¤â‚‚-criterion (f i)) (â‰¤â‚‚-criterion (g i))

  Î³ : u â‰¡ v
  Î³ = incl-lc fe (dfunext fe h)

â„•âˆ-ordinal : funextâ‚€ â†’ is-well-order _â‰º_
â„•âˆ-ordinal fe = (â‰º-prop-valued fe) , â‰º-well-founded , â‰º-extensional fe , â‰º-trans

\end{code}

The following is not needed anymore, as we have the stronger fact,
proved above, that â‰º is well founded:

\begin{code}

â‰º-well-foundedâ‚‚ : funextâ‚€ â†’ is-well-foundedâ‚‚ _â‰º_
â‰º-well-foundedâ‚‚ fe p Ï† = â„•âˆ-ğŸš-density fe a b
 where
  Î³ : (n : â„•) â†’ ((m : â„•) â†’ m < n â†’ p (Î¹ m) â‰¡ â‚) â†’ p (Î¹ n) â‰¡ â‚
  Î³ n g = Ï† (Î¹ n) h
   where
    h : (u : â„•âˆ) â†’ u â‰º Î¹ n â†’ p u â‰¡ â‚
    h u (m , r , l) = back-transport (Î» v â†’ p v â‰¡ â‚) r (g m (âŠ-gives-< m n l))

  a : (n : â„•) â†’ p (Î¹ n) â‰¡ â‚
  a = course-of-values-induction (Î» n â†’ p (Î¹ n) â‰¡ â‚) Î³

  f : (u : â„•âˆ) â†’ u â‰º âˆ â†’ p u â‰¡ â‚
  f u (n , r , l) = back-transport (Î» v â†’ p v â‰¡ â‚) r (a n)

  b : p âˆ â‰¡ â‚
  b = Ï† âˆ f

â„•âˆ-ordinalâ‚‚ : funextâ‚€ â†’ is-well-orderâ‚‚ _â‰º_
â„•âˆ-ordinalâ‚‚ fe = â‰º-prop-valued fe ,
                 â‰º-well-foundedâ‚‚ fe ,
                 â‰º-extensional fe ,
                 â‰º-trans

â„•-to-â„•âˆ-lemma : funextâ‚€ â†’ (u : â„•âˆ) (n : â„•) â†’ u âŠ‘ n â†’ Î£ m ê‰ â„• , (m â‰¤ n) Ã— (u â‰¡ Î¹ m)
â„•-to-â„•âˆ-lemma fe u zero p     = zero , â‰¤-refl zero , is-Zero-equal-Zero fe p
â„•-to-â„•âˆ-lemma fe u (succ n) p = g (ğŸš-is-discrete (incl u n) â‚€)
 where
  IH : u âŠ‘ n â†’ Î£ m ê‰ â„• , (m â‰¤ n) Ã— (u â‰¡ Î¹ m)
  IH = â„•-to-â„•âˆ-lemma fe u n

  g : decidable(u âŠ‘ n) â†’ Î£ m ê‰ â„• , (m â‰¤ n â§¾ 1) Ã— (u â‰¡ Î¹ m)
  g (inl q) = prâ‚(IH q) , â‰¤-trans (prâ‚(IH q)) n (n â§¾ 1) (prâ‚(prâ‚‚(IH q))) (â‰¤-succ n) , prâ‚‚(prâ‚‚(IH q))
  g (inr Ï†) = n â§¾ 1 , â‰¤-refl n , s
    where
     q : n âŠ u
     q = different-from-â‚€-equal-â‚ Ï†

     s : u â‰¡ Succ (Î¹ n)
     s = Succ-criterion fe {u} {n} q p

â‰º-cotransitive : funextâ‚€ â†’ cotransitive _â‰º_
â‰º-cotransitive fe u v w (n , r , a) = g (ğŸš-is-discrete (incl w n) â‚)
 where
  g : decidable(n âŠ w) â†’ (u â‰º w) + (w â‰º v)
  g (inl a) = inl (n , r , a)
  g (inr f) = inr (m , s , âŠ-trans'' v n m l a)
   where
    b : w âŠ‘ n
    b = not-âŠ-is-âŠ’ {n} {w} f

    Ïƒ : Î£ m ê‰ â„• , (m â‰¤ n) Ã— (w â‰¡ Î¹ m)
    Ïƒ = â„•-to-â„•âˆ-lemma fe w n b

    m : â„•
    m = prâ‚ Ïƒ

    l : m â‰¤ n
    l = prâ‚ (prâ‚‚ Ïƒ)

    s : w â‰¡ Î¹ m
    s = prâ‚‚ (prâ‚‚ Ïƒ)

â„•âˆ-ğŸš-order-separated : funextâ‚€ â†’ ğŸš-order-separated _â‰º_
â„•âˆ-ğŸš-order-separated fe x y (n , r , l) =  p , t , h
 where
  p : â„•âˆ â†’ ğŸš
  p z = incl z n

  e : incl x n â‰¡ â‚€
  e = back-transport (Î» z â†’ p z â‰¡ â‚€) r (Î¹-diagonalâ‚€ n)

  t : incl x n <â‚‚ incl y n
  t = <â‚‚-criterion e l

  f : (u v : â„•âˆ) â†’ u â‰º v â†’ p u â‰¤ p v
  f u v (n' , r' , l') = â‰¤â‚‚-criterion Ï•
   where
    Ï• : incl u n â‰¡ â‚ â†’ p v â‰¡ â‚
    Ï• s = âŠ-trans' n n' v b l'
     where
      a : p (Î¹ n') â‰¡ â‚
      a = transport (Î» z â†’ p z â‰¡ â‚) r' s

      b : n < n'
      b = âŠ-gives-< n n' a

  g : (u v : â„•âˆ) â†’ p u <â‚‚ p v â†’ u â‰º v
  g u v l = Î³ (<â‚‚-criterion-converse l)
   where
    Î³ : (p u â‰¡ â‚€) Ã— (p v â‰¡ â‚) â†’ u â‰º v
    Î³ (a , b) = prâ‚ c , prâ‚‚ (prâ‚‚ c) , (âŠ-trans'' v n (prâ‚ c) (prâ‚ (prâ‚‚ c)) b)
     where
      c : Î£ m ê‰ â„• , (m â‰¤ n) Ã— (u â‰¡ Î¹ m)
      c = â„•-to-â„•âˆ-lemma fe u n a

  h : (u v : â„•âˆ) â†’ (u â‰º v â†’ p u â‰¤ p v) Ã— (p u <â‚‚ p v â†’ u â‰º v)
  h u v = f u v , g u v

Î¹-order-preserving : (m n : â„•) â†’ m < n â†’ Î¹ m â‰º Î¹ n
Î¹-order-preserving m n l = m , refl , <-gives-âŠ m n l

Î¹-order-reflecting : (m n : â„•) â†’ Î¹ m â‰º Î¹ n â†’ m < n
Î¹-order-reflecting m n (m' , p , l') = âŠ-gives-< m n l
 where
  l : m âŠ Î¹ n
  l = back-transport (Î» - â†’ - âŠ Î¹ n) (â„•-to-â„•âˆ-lc p) l'

{- TODO

<-gives-â‰º : (m n : â„•) â†’ Î¹ m â‰º Î¹ n â†’ m < n
<-gives-â‰º = ?

âŠ-gives-â‰º : (m : â„•) (u : â„•âˆ) â†’ m âŠ u â†’ Î¹ m â‰º u
âŠ-gives-â‰º = ?
-}

\end{code}

Added 25 June 2018. This may be placed somewhere else in the future.
Another version of Nâˆ, to be investigated.

\begin{code}

Îâˆ : ğ“¤â‚ Ì‡
Îâˆ = Î£ A ê‰ (â„• â†’ Î© ğ“¤â‚€), ((n : â„•) â†’ A (n â§¾ 1) holds â†’ A n holds)

\end{code}

Needed 28 July 2018:

\begin{code}

â‰¼-is-prop-valued : funextâ‚€ â†’ (u v : â„•âˆ) â†’ is-prop (u â‰¼ v)
â‰¼-is-prop-valued fe u v = Î -is-prop fe (Î» n â†’ Î -is-prop fe (Î» l â†’ ğŸš-is-set))

â‰¼-not-â‰º : (u v : â„•âˆ) â†’ u â‰¼ v â†’ Â¬ (v â‰º u)
â‰¼-not-â‰º u v l (n , (p , m)) = zero-is-not-one (e â»Â¹ âˆ™ d)
 where
  a : v â‰º u
  a = transport (Î» - â†’ - â‰º u) (p â»Â¹) (âŠ-gives-â‰º n u m)

  k : â„•
  k = prâ‚ a

  b : v â‰¡ Î¹ k
  b = prâ‚ (prâ‚‚ a)

  c : k âŠ v
  c = l k (prâ‚‚ (prâ‚‚ a))

  d : incl (Î¹ k) k â‰¡ â‚
  d = transport (Î» - â†’ k âŠ -) b c

  e : incl (Î¹ k) k â‰¡ â‚€
  e = Î¹-diagonalâ‚€ k

not-â‰º-â‰¼ : funextâ‚€ â†’ (u v : â„•âˆ) â†’ Â¬ (v â‰º u) â†’ u â‰¼ v
not-â‰º-â‰¼ fe u v Ï† n l = ğŸš-equality-cases f g
 where
  f : v âŠ‘ n â†’ n âŠ v
  f m = ğŸ˜-elim (Ï† (k , (p , b)))
   where
    k : â„•
    k = prâ‚ (â„•-to-â„•âˆ-lemma fe v n m)

    a : k â‰¤ n
    a = prâ‚ (prâ‚‚ (â„•-to-â„•âˆ-lemma fe v n m))

    p : v â‰¡ Î¹ k
    p = prâ‚‚ (prâ‚‚ (â„•-to-â„•âˆ-lemma fe v n m))

    b : k âŠ u
    b = âŠ-trans'' u n k a l

  g : n âŠ v â†’ n âŠ v
  g = id

\end{code}

Characterization of âŠ.

\begin{code}

âŠ-positive : (n : â„•) (u : â„•âˆ) â†’ n âŠ u â†’ is-positive u
âŠ-positive n u = âŠ-trans'' u n 0 (zero-minimal n)

âŠ-characâ†’ : funextâ‚€
          â†’ (n : â„•) (u : â„•âˆ)
          â†’ n âŠ u
          â†’ Î£ v ê‰ â„•âˆ , u â‰¡ (Succ ^ (n â§¾ 1)) v
âŠ-characâ†’ feâ‚€ zero u l = Pred u , (positive-equal-Succ feâ‚€ l)
âŠ-characâ†’ feâ‚€ (succ n) u l = Î³
 where
  IH : Î£ v ê‰ â„•âˆ , Pred u â‰¡ (Succ ^ (n â§¾ 1)) v
  IH = âŠ-characâ†’ feâ‚€ n (Pred u) l

  v : â„•âˆ
  v = prâ‚ IH

  p : u â‰¡ (Succ ^ (n â§¾ 2)) v
  p = u                   â‰¡âŸ¨ positive-equal-Succ feâ‚€ (âŠ-positive (n â§¾ 1) u l) âŸ©
      Succ (Pred u)       â‰¡âŸ¨ ap Succ (prâ‚‚ IH) âŸ©
      (Succ ^ (n â§¾ 2)) v  âˆ

  Î³ : Î£ v ê‰ â„•âˆ , u â‰¡ (Succ ^ (n â§¾ 2)) v
  Î³ = v , p

âŠ-characâ† : funextâ‚€ â†’ (n : â„•) (u : â„•âˆ)
           â†’ (Î£ v ê‰ â„•âˆ , u â‰¡ (Succ ^ (n â§¾ 1)) v) â†’ n âŠ u
âŠ-characâ† feâ‚€ zero u (v , refl) = refl
âŠ-characâ† feâ‚€ (succ n) u (v , refl) = Î³
 where
  IH : n âŠ Pred u
  IH = âŠ-characâ† feâ‚€ n (Pred u) (v , refl)

  Î³ : n â§¾ 1 âŠ u
  Î³ = IH

\end{code}

\begin{code}
{-
â„•âˆ-charac : â„•âˆ â‰ƒ (Î£ Î² ê‰ (â„• â†’ ğŸš), is-prop (Î£ n ê‰ â„• , Î² n â‰¡ â‚€))
â„•âˆ-charac = qinveq f (g , Î· , Îµ)
 where
  f : â„•âˆ â†’ Î£ Î² ê‰ (â„• â†’ ğŸš), is-prop (Î£ n ê‰ â„• , Î² n â‰¡ â‚€)
  f (Î± , Î´) = {!!}
   where
    Î² : â„• â†’ ğŸš
    Î² n = sub (Î± n) (Î± (n â§¾ 1)) (Î´ n)
    i : is-prop (Î£ n ê‰ â„• , Î² n â‰¡ â‚€)
    i (n , p) (m , q) = {!!}
     where
      h : (a b u v : ğŸš)
        â†’ (Ï• : a â‰¥ b)
        â†’ (Ïˆ : u â‰¥ v)
        â†’ sub a b Ï• â‰¡ sub u v Ïˆ
        â†’ a â‰¡ u
      h a b u v Ï• Ïˆ e = {!!}
      I = Î² n â‰¡âŸ¨ {!!} âŸ©
          {!!} â‰¡âŸ¨ {!!} âŸ©
          {!!} â‰¡âŸ¨ {!!} âŸ©
          {!!} â‰¡âŸ¨ {!!} âŸ©
          {!!} âˆ
  g : (Î£ Î± ê‰ (â„• â†’ ğŸš), is-prop (Î£ n ê‰ â„• , Î± n â‰¡ â‚€)) â†’ â„•âˆ
  g = {!!}
  Î· : g âˆ˜ f âˆ¼ id
  Î· = {!!}
  Îµ : f âˆ˜ g âˆ¼ id
  Îµ = {!!}
-}
\end{code}
