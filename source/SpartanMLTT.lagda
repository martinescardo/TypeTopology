Martin Escardo 2011.

Our Spartan (intensional) Martin-LÃ¶f type theory has a countable tower
of universes, the empty type âˆ…, the unit type ğŸ™, product types Î , sum
types Î£ (and hence binary product types _Ã—_), sum types _+_.
identity types _â‰¡_.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module SpartanMLTT where

open import Universes public

\end{code}

The module Universes allows us to write e.g. the following instead of

    Î  : âˆ€ {i j} {X : Set i} (Y : X â†’ Set j) â†’ Set (i âŠ” j)
    Î  Y = (x : _) â†’ Y x

\begin{code}

Î  : âˆ€ {U V} {X : U Ì‡} (Y : X â†’ V Ì‡) â†’ U âŠ” V Ì‡
Î  Y = (x : _) â†’ Y x

syntax Î  {A} (Î» x â†’ B) = Î ï¼ˆ x âˆ¶ A ï¼‰, B

\end{code}

Identity and dependent function composition:

\begin{code}

id : âˆ€ {U} {X : U Ì‡} â†’ X â†’ X
id x = x

_âˆ˜_ : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {Z : Y â†’ W Ì‡}
    â†’ ((y : Y) â†’ Z y) â†’ (f : X â†’ Y) â†’ (x : X) â†’ Z (f x)
g âˆ˜ f = Î» x â†’ g(f x)

\end{code}

xsEmpty type.

\begin{code}

data ğŸ˜ {U} : U Ì‡ where

unique-from-ğŸ˜ : âˆ€ {U V} {A : U Ì‡} â†’ ğŸ˜ {V} â†’ A
unique-from-ğŸ˜ = Î» ()

ğŸ˜-elim = unique-from-ğŸ˜

\end{code}

The one-element type is defined by induction with one case:

\begin{code}

data ğŸ™ {U} : U Ì‡ where
 * : ğŸ™

unique-to-ğŸ™ : âˆ€ {U V} {A : U Ì‡} â†’ A â†’ ğŸ™ {V}
unique-to-ğŸ™ {U} {V} a = * {V}

\end{code}

Using our conventions below, a sum can be written Î£ {X} Y or as
Î£ \(x : X) â†’ Y x, or even Î£ \x â†’ Y x when Agda can infer the type of
the element x from the context. I prefer to use \ rather than Î» in
such cases.

\begin{code}

record Î£ {U V} {X : U Ì‡} (Y : X â†’ V Ì‡) : U âŠ” V Ì‡ where
  constructor _,_
  field
   prâ‚ : X
   prâ‚‚ : Y prâ‚

open Î£ public

syntax Î£ {A} (Î» x â†’ B) = Î£ï¼ˆ x âˆ¶ A ï¼‰, B

Î£-elim : âˆ€ {U V} {X : U Ì‡} {Y : X â†’ V Ì‡} {A : Î£ Y â†’ U âŠ” V Ì‡}
       â†’ ((x : X) (y : Y x) â†’ A (x , y)) â†’ Î  A
Î£-elim f (x , y) = f x y

uncurry : âˆ€ {U V W} {X : U Ì‡} {Y : X â†’ V Ì‡} {Z : W Ì‡}
        â†’ ((x : X) â†’ Y x â†’ Z) â†’ Î£ Y â†’ Z
uncurry f (x , y) = f x y

curry :  âˆ€ {U V W} {X : U Ì‡} {Y : X â†’ V Ì‡} {Z : W Ì‡}
      â†’ (Î£ Y â†’ Z) â†’ ((x : X) â†’ Y x â†’ Z)
curry f x y = f (x , y)

\end{code}

Equivalently, Î£-elim f t = f (prâ‚ t) (prâ‚‚ t).

As usual in type theory, binary products are particular cases of
dependent sums.

\begin{code}

_Ã—_ : âˆ€ {U V} â†’ U Ì‡ â†’ V Ì‡ â†’ U âŠ” V Ì‡
X Ã— Y = Î£ \(x : X) â†’ Y

\end{code}

Binary sums

\begin{code}

data _+_ {U V} (X : U Ì‡) (Y : V Ì‡) : U âŠ” V Ì‡ where
  inl : X â†’ X + Y
  inr : Y â†’ X + Y

dep-cases : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : X + Y â†’ W Ì‡}
          â†’ ((x : X) â†’ A(inl x))
          â†’ ((y : Y) â†’ A(inr y))
          â†’ ((z : X + Y) â†’ A z)
dep-cases f g (inl x) = f x
dep-cases f g (inr y) = g y

cases : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : W Ì‡} â†’ (X â†’ A) â†’ (Y â†’ A) â†’ X + Y â†’ A
cases = dep-cases

Cases : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : W Ì‡} â†’ X + Y â†’ (X â†’ A) â†’ (Y â†’ A) â†’ A
Cases z f g = cases f g z

+-commutative : âˆ€ {U V} {A : U Ì‡} {B : V Ì‡} â†’ A + B â†’ B + A
+-commutative = cases inr inl

\end{code}

Some basic Curry--Howard logic.

\begin{code}

Â¬_ : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
Â¬ A = A â†’ ğŸ˜ {Uâ‚€}

is-empty : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
is-empty = Â¬_

decidable : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
decidable A = A + Â¬ A

_â‡”_ : âˆ€ {U V} â†’ U Ì‡ â†’ V Ì‡ â†’ U âŠ” V Ì‡
A â‡” B = (A â†’ B) Ã— (B â†’ A)

dual : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} (R : W Ì‡) â†’ (X â†’ Y) â†’ (Y â†’ R) â†’ (X â†’ R)
dual R f p = p âˆ˜ f

contrapositive : âˆ€ {U V} {A : U Ì‡} {B : V Ì‡} â†’ (A â†’ B) â†’ Â¬ B â†’ Â¬ A
contrapositive = dual _

Â¬Â¬ : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
Â¬Â¬ A = Â¬(Â¬ A)

Â¬Â¬-functor : âˆ€ {U V} {A : U Ì‡} {B : V Ì‡} â†’ (A â†’ B) â†’ Â¬Â¬ A â†’ Â¬Â¬ B
Â¬Â¬-functor = contrapositive âˆ˜ contrapositive

double-negation-intro : âˆ€ {U} {A : U Ì‡} â†’ A â†’ Â¬Â¬ A
double-negation-intro x u = u x

three-negations-imply-one : âˆ€ {U} {A : U Ì‡} â†’ Â¬(Â¬Â¬ A) â†’ Â¬ A
three-negations-imply-one = contrapositive double-negation-intro

double-negation-unshift : âˆ€ {U V} {X : U Ì‡} {A : X â†’ V Ì‡} â†’ Â¬Â¬((x : X) â†’ A x) â†’ (x : X) â†’ Â¬Â¬(A x)
double-negation-unshift f x g = f (Î» h â†’ g (h x))

dnu : âˆ€ {U} {V} {A : U Ì‡} {B : V Ì‡} â†’ Â¬Â¬(A Ã— B) â†’ Â¬Â¬ A Ã— Â¬Â¬ B
dnu Ï† = (Â¬Â¬-functor prâ‚ Ï†) , (Â¬Â¬-functor prâ‚‚ Ï†)

und : âˆ€ {U} {V} {A : U Ì‡} {B : V Ì‡} â†’ Â¬Â¬ A Ã— Â¬Â¬ B â†’ Â¬Â¬(A Ã— B)
und (Ï† , Î³) w = Î³ (Î» y â†’ Ï† (Î» x â†’ w (x , y)))

not-Î£-implies-Î -not : âˆ€ {U V} {X : U Ì‡} {A : X â†’ V Ì‡}
                    â†’ Â¬(Î£ \(x : X) â†’ A x) â†’ (x : X) â†’ Â¬(A x)
not-Î£-implies-Î -not = curry

Î -not-implies-not-Î£ : âˆ€ {U} {X : U Ì‡} {A : X â†’ U Ì‡}
                    â†’ ((x : X) â†’ Â¬(A x)) â†’ Â¬(Î£ \(x : X) â†’ A x)
Î -not-implies-not-Î£ = uncurry

Left-fails-then-right-holds : âˆ€ {U} {V} {P : U Ì‡} {Q : V Ì‡} â†’ P + Q â†’ Â¬ P â†’ Q
Left-fails-then-right-holds (inl p) u = ğŸ˜-elim (u p)
Left-fails-then-right-holds (inr q) u = q

Right-fails-then-left-holds : âˆ€ {U} {V} {P : U Ì‡} {Q : V Ì‡} â†’ P + Q â†’ Â¬ Q â†’ P
Right-fails-then-left-holds (inl p) u = p
Right-fails-then-left-holds (inr q) u = ğŸ˜-elim (u q)

\end{code}

Equality (more in the module UF).

\begin{code}

data _â‰¡_ {U} {X : U Ì‡} : X â†’ X â†’ U Ì‡ where
  refl : {x : X} â†’ x â‰¡ x

lhs : âˆ€ {U} {X : U Ì‡} {x y : X} â†’ x â‰¡ y â†’ X
lhs {U} {X} {x} {y} p = x

rhs : âˆ€ {U} {X : U Ì‡} {x y : X} â†’ x â‰¡ y â†’ X
rhs {U} {X} {x} {y} p = y

Id : âˆ€ {U} {X : U Ì‡} â†’ X â†’ X â†’ U Ì‡
Id = _â‰¡_

_â‰¢_ : âˆ€ {U} {X : U Ì‡} â†’ (x y : X) â†’ U Ì‡
x â‰¢ y = Â¬(x â‰¡ y)

Jbased : âˆ€ {U V} {X : U Ì‡} (x : X) (A : (y : X) â†’ x â‰¡ y â†’ V Ì‡)
       â†’ A x refl â†’ (y : X) (r : x â‰¡ y) â†’ A y r
Jbased x A b .x refl = b

J : âˆ€ {U V} {X : U Ì‡} (A : (x y : X) â†’ x â‰¡ y â†’ V Ì‡)
  â†’ ((x : X) â†’ A x x refl) â†’ {x y : X} (r : x â‰¡ y) â†’ A x y r
J A f {x} {y} = Jbased x (Î» y p â†’ A x y p) (f x) y

transport' : âˆ€ {U V} {X : U Ì‡} (A : X â†’ V Ì‡) {x y : X}
          â†’ x â‰¡ y â†’ A x â†’ A y
transport' A {x} {y} p a = Jbased x (Î» y p â†’ A y) a y p

transport : âˆ€ {U V} {X : U Ì‡} (A : X â†’ V Ì‡) {x y : X}
          â†’ x â‰¡ y â†’ A x â†’ A y
transport A refl = id

_âˆ™_ : âˆ€ {U} {X : U Ì‡} â†’ {x y z : X} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
p âˆ™ q = transport (Id (lhs p)) q p

_â»Â¹ : âˆ€ {U} {X : U Ì‡} â†’ {x y : X} â†’ x â‰¡ y â†’ y â‰¡ x
p â»Â¹ = transport (Î» - â†’ - â‰¡ lhs p) p refl

ap : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) {x x' : X} â†’ x â‰¡ x' â†’ f x â‰¡ f x'
ap f p = transport (Î» - â†’ f (lhs p) â‰¡ f -) p refl

back-transport : âˆ€ {U V} {X : U Ì‡} (A : X â†’ V Ì‡) {x y : X} â†’ x â‰¡ y â†’ A y â†’ A x
back-transport B p = transport B (p â»Â¹)

â‰¢-sym : âˆ€ {U} {X : U Ì‡} â†’ {x y : X} â†’ x â‰¢ y â†’ y â‰¢ x
â‰¢-sym u r = u(r â»Â¹)

\end{code}

Standard syntax for equality chain reasoning:

\begin{code}

_â‰¡âŸ¨_âŸ©_ : âˆ€ {U} {X : U Ì‡} (x : X) {y z : X} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
_ â‰¡âŸ¨ p âŸ© q = p âˆ™ q

_âˆ : âˆ€ {U} {X : U Ì‡} (x : X) â†’ x â‰¡ x
_âˆ _ = refl

equality-cases : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : W Ì‡} (z : X + Y)
              â†’ ((x : X) â†’ z â‰¡ inl x â†’ A) â†’ ((y : Y) â†’ z â‰¡ inr y â†’ A) â†’ A
equality-cases (inl x) f g = f x refl
equality-cases (inr y) f g = g y refl

Cases-equality-l : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : W Ì‡} (f : X â†’ A) (g : Y â†’ A)
                 â†’ (z : X + Y) (x : X) â†’ z â‰¡ inl x â†’ Cases z f g â‰¡ f x
Cases-equality-l f g .(inl x) x refl = refl

Cases-equality-r : âˆ€ {U V W} {X : U Ì‡} {Y : V Ì‡} {A : W Ì‡} (f : X â†’ A) (g : Y â†’ A)
                 â†’ (z : X + Y) (y : Y) â†’ z â‰¡ inr y â†’ Cases z f g â‰¡ g y
Cases-equality-r f g .(inr y) y refl = refl

\end{code}

Some general definitions (perhaps we need to find a better place for
this):

\begin{code}

Nat : âˆ€ {U V W} {X : U Ì‡} â†’ (X â†’ V Ì‡) â†’ (X â†’ W Ì‡) â†’ U âŠ” V âŠ” W Ì‡
Nat A B = Î  \x â†’ A x â†’ B x

_âˆ¼_ : âˆ€ {U V} {X : U Ì‡} {A : X â†’ V Ì‡} â†’ Î  A â†’ Î  A â†’ U âŠ” V Ì‡
f âˆ¼ g = âˆ€ x â†’ f x â‰¡ g x

_â‰ˆ_ : âˆ€ {U V} {X : U Ì‡} {x : X} {A : X â†’ V Ì‡} â†’ Nat (Id x) A â†’ Nat (Id x) A â†’ U âŠ” V Ì‡
Î· â‰ˆ Î¸ = âˆ€ y â†’ Î· y âˆ¼ Î¸ y

NatÎ£ : âˆ€ {U V W} {X : U Ì‡} {A : X â†’ V Ì‡} {B : X â†’ W Ì‡} â†’ Nat A B â†’ Î£ A â†’ Î£ B
NatÎ£ Î¶ (x , a) = (x , Î¶ x a)

NatÎ  : âˆ€ {U V W} {X : U Ì‡} {A : X â†’ V Ì‡} {B : X â†’ W Ì‡} â†’ Nat A B â†’ Î  A â†’ Î  B
NatÎ  f g x = f x (g x) -- (S combinator from combinatory logic!)

left-cancellable : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
left-cancellable f = âˆ€ {x x'} â†’ f x â‰¡ f x' â†’ x â‰¡ x'

left-cancellable' : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
left-cancellable' f = âˆ€ x x' â†’ f x â‰¡ f x' â†’ x â‰¡ x'

Î£! : âˆ€ {U V} {X : U Ì‡} (A : X â†’ V Ì‡) â†’ U âŠ” V Ì‡
Î£! {U} {V} {X} A = (Î£ \(x : X) â†’ A x) Ã— ((x x' : X) â†’ A x â†’ A x' â†’ x â‰¡ x')

\end{code}

Note: Î£! is to be avoided, in favour of the contractibility of Î£, following univalent mathematics.

The following is properly proved using universes, but we don't bother
for the moment:

\begin{code}

+disjoint : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} {x : X} {y : Y} â†’ Â¬(inl x â‰¡ inr y)
+disjoint ()

+disjoint' : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} {x : X} {y : Y} â†’ Â¬(inr y â‰¡ inl x)
+disjoint' ()

inl-lc : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} {x x' : X} â†’ inl {U} {V} {X} {Y} x â‰¡ inl x' â†’ x â‰¡ x'
inl-lc refl = refl

inr-lc : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} {y y' : Y} â†’ inr {U} {V} {X} {Y} y â‰¡ inr y' â†’ y â‰¡ y'
inr-lc refl = refl

ğŸ™-all-* : âˆ€ {U} (x : ğŸ™) â†’ x â‰¡ *
ğŸ™-all-* {U} * = refl {U}

\end{code}

General utilities to avoid (sometimes) mentioning implicit arguments
in function definitions.

\begin{code}

type-of : âˆ€ {U} {X : U Ì‡} â†’ X â†’ U Ì‡
type-of {U} {X} x = X

universe-of : âˆ€ {U} (X : U Ì‡) â†’ Universe
universe-of {U} X = U

domain dom : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U Ì‡
domain {U} {V} {X} {Y} f = X
dom = domain

codomain cod : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ V Ì‡
codomain {U} {V} {X} {Y} f = Y
cod = codomain

\end{code}

The two-point type (or booleans)

\begin{code}

data ğŸš : Uâ‚€ Ì‡ where
 â‚€ : ğŸš
 â‚ : ğŸš

ğŸš-induction : âˆ€ {U} {A : ğŸš â†’ U Ì‡} â†’ A â‚€ â†’ A â‚ â†’ (x : ğŸš) â†’ A x
ğŸš-induction r s â‚€ = r
ğŸš-induction r s â‚ = s

ğŸš-cases : âˆ€ {U} {A : U Ì‡} â†’ A â†’ A â†’ ğŸš â†’ A
ğŸš-cases = ğŸš-induction

\end{code}

Alternative coproduct:

\begin{code}

_+'_ : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡ â†’ U Ì‡
Xâ‚€ +' Xâ‚ = Î£ \(i : ğŸš) â†’ ğŸš-cases Xâ‚€ Xâ‚ i

\end{code}

The natural numbers:

\begin{code}

data â„• : Set where
  zero : â„•
  succ : â„• â†’ â„•

{-# BUILTIN NATURAL â„• #-}

rec : âˆ€ {U} {X : U Ì‡} â†’ X â†’ (X â†’ X) â†’ (â„• â†’ X)
rec x f zero = x
rec x f (succ n) = f(rec x f n)

induction : âˆ€ {U} {A : â„• â†’ U Ì‡} â†’ A 0 â†’ ((k : â„•) â†’ A k â†’ A(succ k)) â†’ (n : â„•) â†’ A n
induction base step 0 = base
induction base step (succ n) = step n (induction base step n)

a-peano-axiom : {n : â„•} â†’ succ n â‰¢ 0
a-peano-axiom ()

pred : â„• â†’ â„•
pred 0 = 0
pred (succ n) = n

succ-lc : {i j : â„•} â†’ succ i â‰¡ succ j â†’ i â‰¡ j
succ-lc = ap pred

succ-no-fp : âˆ€ {U} (n : â„•) â†’ n â‰¡ succ n â†’ ğŸ˜ {U}
succ-no-fp zero ()
succ-no-fp (succ n) p = succ-no-fp n (succ-lc p)

\end{code}

We use the following to indicate the type of a subterm:

\begin{code}

-id : âˆ€ {U} (X : U Ì‡) â†’ X â†’ X
-id X x = x

syntax -id X x = x âˆ¶ X

\end{code}

And the following to make explicit the type of hypotheses:

\begin{code}

have : âˆ€ {U V} {A : U Ì‡} {B : V Ì‡} â†’ A â†’ B â†’ B
have _ y = y

\end{code}

Operator fixity and precedences.

\begin{code}

infixr 4 _,_
infixr 2 _Ã—_
infixr 1 _+_
infixl 5 _âˆ˜_
infix  50 Â¬_
infix  -1 _â‡”_
infix  0 _â‰¡_
infix  0 _â‰¢_
infix  3  _â»Â¹
infix  1 _âˆ
infixr 0 _â‰¡âŸ¨_âŸ©_
infixl 2 _âˆ™_
infix  4  _âˆ¼_
infixl 0 -id

\end{code}
