Martin Escardo 2011.

Our Spartan (intensional) Martin-LÃ¶f type theory has a countable tower
of universes, the empty type âˆ…, the unit type ğŸ™, product types Î , sum
types Î£ (and hence binary product types _Ã—_), sum types _+_.
identity types _â‰¡_.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module SpartanMLTT where

open import Universes public

\end{code}

The module Universes allows us to write e.g. the following instead of

    Î  : âˆ€ {i j} {X : Set i} (Y : X â†’ Set j) â†’ Set (i âŠ” j)
    Î  Y = (x : _) â†’ Y x

\begin{code}

Î  : {X : ğ“¤ Ì‡} (Y : X â†’ ğ“¥ Ì‡) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î  Y = (x : _) â†’ Y x

syntax Î  {A} (Î» x â†’ B) = Î ï¼ˆ x âˆ¶ A ï¼‰ B

\end{code}

Identity and dependent function composition:

\begin{code}

id : {X : ğ“¤ Ì‡} â†’ X â†’ X
id x = x

_âˆ˜_ : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {Z : Y â†’ ğ“¦ Ì‡}
    â†’ ((y : Y) â†’ Z y)
    â†’ (f : X â†’ Y) (x : X) â†’ Z (f x)
g âˆ˜ f = Î» x â†’ g(f x)

\end{code}

Empty type.

\begin{code}

data ğŸ˜ {ğ“¤} : ğ“¤ Ì‡ where

unique-from-ğŸ˜ : {A : ğ“¤ Ì‡} â†’ ğŸ˜ {ğ“¥} â†’ A
unique-from-ğŸ˜ = Î» ()

ğŸ˜-elim = unique-from-ğŸ˜

\end{code}

The one-element type is defined by induction with one case:

\begin{code}

data ğŸ™ {ğ“¤} : ğ“¤ Ì‡ where
 * : ğŸ™

unique-to-ğŸ™ : {A : ğ“¤ Ì‡} â†’ A â†’ ğŸ™ {ğ“¥}
unique-to-ğŸ™ {ğ“¤} {ğ“¥} a = * {ğ“¥}

open import Sigma public

\end{code}

Binary sums

\begin{code}

data _+_ {ğ“¤ ğ“¥} (X : ğ“¤ Ì‡) (Y : ğ“¥ Ì‡) : ğ“¤ âŠ” ğ“¥ Ì‡ where
  inl : X â†’ X + Y
  inr : Y â†’ X + Y

dep-cases : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : X + Y â†’ ğ“¦ Ì‡}
          â†’ ((x : X) â†’ A(inl x))
          â†’ ((y : Y) â†’ A(inr y))
          â†’ ((z : X + Y) â†’ A z)
dep-cases f g (inl x) = f x
dep-cases f g (inr y) = g y

cases : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} â†’ (X â†’ A) â†’ (Y â†’ A) â†’ X + Y â†’ A
cases = dep-cases

Cases : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} â†’ X + Y â†’ (X â†’ A) â†’ (Y â†’ A) â†’ A
Cases z f g = cases f g z

+-commutative : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ A + B â†’ B + A
+-commutative = cases inr inl

\end{code}

Some basic Curry--Howard logic.

\begin{code}

Â¬_ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬ A = A â†’ ğŸ˜ {ğ“¤â‚€}

is-empty : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-empty = Â¬_

decidable : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
decidable A = A + Â¬ A

_â‡”_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
A â‡” B = (A â†’ B) Ã— (B â†’ A)

dual : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (R : ğ“¦ Ì‡) â†’ (X â†’ Y) â†’ (Y â†’ R) â†’ (X â†’ R)
dual R f p = p âˆ˜ f

contrapositive : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ (A â†’ B) â†’ Â¬ B â†’ Â¬ A
contrapositive = dual _

Â¬Â¬ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬Â¬ A = Â¬(Â¬ A)

Â¬Â¬-functor : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ (A â†’ B) â†’ Â¬Â¬ A â†’ Â¬Â¬ B
Â¬Â¬-functor = contrapositive âˆ˜ contrapositive

double-negation-intro : {A : ğ“¤ Ì‡} â†’ A â†’ Â¬Â¬ A
double-negation-intro x u = u x

three-negations-imply-one : {A : ğ“¤ Ì‡} â†’ Â¬(Â¬Â¬ A) â†’ Â¬ A
three-negations-imply-one = contrapositive double-negation-intro

double-negation-unshift : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} â†’ Â¬Â¬((x : X) â†’ A x) â†’ (x : X) â†’ Â¬Â¬(A x)
double-negation-unshift f x g = f (Î» h â†’ g (h x))

dnu : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ Â¬Â¬(A Ã— B) â†’ Â¬Â¬ A Ã— Â¬Â¬ B
dnu Ï† = (Â¬Â¬-functor prâ‚ Ï†) , (Â¬Â¬-functor prâ‚‚ Ï†)

und : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ Â¬Â¬ A Ã— Â¬Â¬ B â†’ Â¬Â¬(A Ã— B)
und (Ï† , Î³) w = Î³ (Î» y â†’ Ï† (Î» x â†’ w (x , y)))

not-Î£-implies-Î -not : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡}
                    â†’ Â¬(Î£ \(x : X) â†’ A x) â†’ (x : X) â†’ Â¬(A x)
not-Î£-implies-Î -not = curry

Î -not-implies-not-Î£ : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¤ Ì‡}
                    â†’ ((x : X) â†’ Â¬(A x)) â†’ Â¬(Î£ \(x : X) â†’ A x)
Î -not-implies-not-Î£ = uncurry

Left-fails-then-right-holds : {P : ğ“¤ Ì‡} {Q : ğ“¥ Ì‡} â†’ P + Q â†’ Â¬ P â†’ Q
Left-fails-then-right-holds (inl p) u = ğŸ˜-elim (u p)
Left-fails-then-right-holds (inr q) u = q

Right-fails-then-left-holds : {P : ğ“¤ Ì‡} {Q : ğ“¥ Ì‡} â†’ P + Q â†’ Â¬ Q â†’ P
Right-fails-then-left-holds (inl p) u = p
Right-fails-then-left-holds (inr q) u = ğŸ˜-elim (u q)

\end{code}

Equality (more in the module UF).

\begin{code}

data _â‰¡_ {ğ“¤} {X : ğ“¤ Ì‡} : X â†’ X â†’ ğ“¤ Ì‡ where
  refl : {x : X} â†’ x â‰¡ x

lhs : {X : ğ“¤ Ì‡} {x y : X} â†’ x â‰¡ y â†’ X
lhs {ğ“¤} {X} {x} {y} p = x

rhs : {X : ğ“¤ Ì‡} {x y : X} â†’ x â‰¡ y â†’ X
rhs {ğ“¤} {X} {x} {y} p = y

Id : {X : ğ“¤ Ì‡} â†’ X â†’ X â†’ ğ“¤ Ì‡
Id = _â‰¡_

_â‰¢_ : {X : ğ“¤ Ì‡} â†’ (x y : X) â†’ ğ“¤ Ì‡
x â‰¢ y = Â¬(x â‰¡ y)

Jbased : {X : ğ“¤ Ì‡} (x : X) (A : (y : X) â†’ x â‰¡ y â†’ ğ“¥ Ì‡)
       â†’ A x refl â†’ (y : X) (r : x â‰¡ y) â†’ A y r
Jbased x A b .x refl = b

J : {X : ğ“¤ Ì‡} (A : (x y : X) â†’ x â‰¡ y â†’ ğ“¥ Ì‡)
  â†’ ((x : X) â†’ A x x refl) â†’ {x y : X} (r : x â‰¡ y) â†’ A x y r
J A f {x} {y} = Jbased x (Î» y p â†’ A x y p) (f x) y

transport' : {X : ğ“¤ Ì‡} (A : X â†’ ğ“¥ Ì‡) {x y : X}
          â†’ x â‰¡ y â†’ A x â†’ A y
transport' A {x} {y} p a = Jbased x (Î» y p â†’ A y) a y p

transport : {X : ğ“¤ Ì‡} (A : X â†’ ğ“¥ Ì‡) {x y : X}
          â†’ x â‰¡ y â†’ A x â†’ A y
transport A refl = id

_âˆ™_ : {X : ğ“¤ Ì‡} {x y z : X} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
p âˆ™ q = transport (Id (lhs p)) q p

_â»Â¹ : {X : ğ“¤ Ì‡} â†’ {x y : X} â†’ x â‰¡ y â†’ y â‰¡ x
p â»Â¹ = transport (Î» - â†’ - â‰¡ lhs p) p refl

ap : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y) {x x' : X} â†’ x â‰¡ x' â†’ f x â‰¡ f x'
ap f p = transport (Î» - â†’ f (lhs p) â‰¡ f -) p refl

back-transport : {X : ğ“¤ Ì‡} (A : X â†’ ğ“¥ Ì‡) {x y : X} â†’ x â‰¡ y â†’ A y â†’ A x
back-transport B p = transport B (p â»Â¹)

â‰¢-sym : {X : ğ“¤ Ì‡} â†’ {x y : X} â†’ x â‰¢ y â†’ y â‰¢ x
â‰¢-sym u r = u(r â»Â¹)

\end{code}

Standard syntax for equality chain reasoning:

\begin{code}

_â‰¡âŸ¨_âŸ©_ : {X : ğ“¤ Ì‡} (x : X) {y z : X} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
_ â‰¡âŸ¨ p âŸ© q = p âˆ™ q

_âˆ : {X : ğ“¤ Ì‡} (x : X) â†’ x â‰¡ x
_âˆ _ = refl

equality-cases : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} (z : X + Y)
              â†’ ((x : X) â†’ z â‰¡ inl x â†’ A) â†’ ((y : Y) â†’ z â‰¡ inr y â†’ A) â†’ A
equality-cases (inl x) f g = f x refl
equality-cases (inr y) f g = g y refl

Cases-equality-l : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} (f : X â†’ A) (g : Y â†’ A)
                 â†’ (z : X + Y) (x : X) â†’ z â‰¡ inl x â†’ Cases z f g â‰¡ f x
Cases-equality-l f g .(inl x) x refl = refl

Cases-equality-r : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} (f : X â†’ A) (g : Y â†’ A)
                 â†’ (z : X + Y) (y : Y) â†’ z â‰¡ inr y â†’ Cases z f g â‰¡ g y
Cases-equality-r f g .(inr y) y refl = refl

\end{code}

Some general definitions (perhaps we need to find a better place for
this):

\begin{code}

Nat : {X : ğ“¤ Ì‡} â†’ (X â†’ ğ“¥ Ì‡) â†’ (X â†’ ğ“¦ Ì‡) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ Ì‡
Nat A B = Î  \x â†’ A x â†’ B x

_âˆ¼_ : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} â†’ Î  A â†’ Î  A â†’ ğ“¤ âŠ” ğ“¥ Ì‡
f âˆ¼ g = âˆ€ x â†’ f x â‰¡ g x

_â‰ˆ_ : {X : ğ“¤ Ì‡} {x : X} {A : X â†’ ğ“¥ Ì‡} â†’ Nat (Id x) A â†’ Nat (Id x) A â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î· â‰ˆ Î¸ = âˆ€ y â†’ Î· y âˆ¼ Î¸ y

NatÎ£ : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} {B : X â†’ ğ“¦ Ì‡} â†’ Nat A B â†’ Î£ A â†’ Î£ B
NatÎ£ Î¶ (x , a) = (x , Î¶ x a)

NatÎ  : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} {B : X â†’ ğ“¦ Ì‡} â†’ Nat A B â†’ Î  A â†’ Î  B
NatÎ  f g x = f x (g x) -- (S combinator from combinatory logic!)

Î Î£-distr : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} {P : (x : X) â†’ A x â†’ ğ“¦ Ì‡}
         â†’ (Î  \(x : X) â†’ Î£ \(a : A x) â†’ P x a) â†’ Î£ \(f : Î  A) â†’ Î  \(x : X) â†’ P x (f x)
Î Î£-distr Ï† = (Î» x â†’ prâ‚ (Ï† x)) , Î» x â†’ prâ‚‚ (Ï† x)

Î Î£-distr-back : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} {P : (x : X) â†’ A x â†’ ğ“¦ Ì‡}
              â†’ (Î£ \(f : Î  A) â†’ Î  \(x : X) â†’ P x (f x)) â†’ Î  \(x : X) â†’ Î£ \(a : A x) â†’ P x a
Î Î£-distr-back (f , Ï†) x = f x , Ï† x

left-cancellable : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
left-cancellable f = âˆ€ {x x'} â†’ f x â‰¡ f x' â†’ x â‰¡ x'

left-cancellable' : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
left-cancellable' f = âˆ€ x x' â†’ f x â‰¡ f x' â†’ x â‰¡ x'

Î£! : {X : ğ“¤ Ì‡} (A : X â†’ ğ“¥ Ì‡) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î£! {ğ“¤} {ğ“¥} {X} A = (Î£ \(x : X) â†’ A x) Ã— ((x x' : X) â†’ A x â†’ A x' â†’ x â‰¡ x')

\end{code}

Note: Î£! is to be avoided, in favour of the contractibility of Î£, following univalent mathematics.

The following is properly proved using universes, but we don't bother
for the moment:

\begin{code}

+disjoint : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {x : X} {y : Y} â†’ Â¬(inl x â‰¡ inr y)
+disjoint ()

+disjoint' : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {x : X} {y : Y} â†’ Â¬(inr y â‰¡ inl x)
+disjoint' ()

inl-lc : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {x x' : X} â†’ inl {ğ“¤} {ğ“¥} {X} {Y} x â‰¡ inl x' â†’ x â‰¡ x'
inl-lc refl = refl

inr-lc : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {y y' : Y} â†’ inr {ğ“¤} {ğ“¥} {X} {Y} y â‰¡ inr y' â†’ y â‰¡ y'
inr-lc refl = refl

ğŸ™-all-* : (x : ğŸ™ {ğ“¤}) â†’ x â‰¡ *
ğŸ™-all-* {ğ“¤} * = refl {ğ“¤}

\end{code}

General utilities to avoid (sometimes) mentioning implicit arguments
in function definitions.

\begin{code}

type-of : {X : ğ“¤ Ì‡} â†’ X â†’ ğ“¤ Ì‡
type-of {ğ“¤} {X} x = X

universe-of : (X : ğ“¤ Ì‡) â†’ Universe
universe-of {ğ“¤} X = ğ“¤

domain : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ Ì‡
domain {ğ“¤} {ğ“¥} {X} {Y} f = X

codomain : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¥ Ì‡
codomain {ğ“¤} {ğ“¥} {X} {Y} f = Y

\end{code}

The two-point type (or booleans)

\begin{code}

data ğŸš : ğ“¤â‚€ Ì‡ where
 â‚€ : ğŸš
 â‚ : ğŸš

ğŸš-induction : {A : ğŸš â†’ ğ“¤ Ì‡} â†’ A â‚€ â†’ A â‚ â†’ (x : ğŸš) â†’ A x
ğŸš-induction r s â‚€ = r
ğŸš-induction r s â‚ = s

ğŸš-cases : {A : ğ“¤ Ì‡} â†’ A â†’ A â†’ ğŸš â†’ A
ğŸš-cases = ğŸš-induction

\end{code}

Alternative coproduct:

\begin{code}

_+'_ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Xâ‚€ +' Xâ‚ = Î£ \(i : ğŸš) â†’ ğŸš-cases Xâ‚€ Xâ‚ i

\end{code}

The natural numbers:

\begin{code}

data â„• : Set where
  zero : â„•
  succ : â„• â†’ â„•

{-# BUILTIN NATURAL â„• #-}

rec : {X : ğ“¤ Ì‡} â†’ X â†’ (X â†’ X) â†’ (â„• â†’ X)
rec x f zero = x
rec x f (succ n) = f(rec x f n)

_^_ : {X : ğ“¤ Ì‡} â†’ (X â†’ X) â†’ â„• â†’ (X â†’ X)
(f ^ n) x = rec x f n

induction : {A : â„• â†’ ğ“¤ Ì‡} â†’ A 0 â†’ ((k : â„•) â†’ A k â†’ A(succ k)) â†’ (n : â„•) â†’ A n
induction base step 0 = base
induction base step (succ n) = step n (induction base step n)

a-peano-axiom : {n : â„•} â†’ succ n â‰¢ 0
a-peano-axiom ()

pred : â„• â†’ â„•
pred 0 = 0
pred (succ n) = n

succ-lc : {i j : â„•} â†’ succ i â‰¡ succ j â†’ i â‰¡ j
succ-lc = ap pred

succ-no-fp : (n : â„•) â†’ n â‰¡ succ n â†’ ğŸ˜ {ğ“¤}
succ-no-fp zero ()
succ-no-fp (succ n) p = succ-no-fp n (succ-lc p)

\end{code}

We use the following to indicate the type of a subterm:

\begin{code}

-id : (X : ğ“¤ Ì‡) â†’ X â†’ X
-id X x = x

syntax -id X x = x âˆ¶ X

\end{code}

And the following to make explicit the type of hypotheses:

\begin{code}

have : {A : ğ“¤ Ì‡} {B : ğ“¥ Ì‡} â†’ A â†’ B â†’ B
have _ y = y

\end{code}

Operator fixity and precedences.

\begin{code}

infixr 1 _+_
infixl 5 _âˆ˜_
infix  50 Â¬_
infix  -1 _â‡”_
infix  0 _â‰¡_
infix  0 _â‰¢_
infix  3  _â»Â¹
infix  1 _âˆ
infixr 0 _â‰¡âŸ¨_âŸ©_
infixl 2 _âˆ™_
infix  4  _âˆ¼_
infix 0 -id

\end{code}
