Martin Escardo, January 2018, May 2020

Based on joint work with Cory Knapp.
http://www.cs.bham.ac.uk/~mhe/papers/partial-elements-and-recursion.pdf

Convention:

  * ğ“£ is the universe where the dominant truth values live.

  * ğ“š is the universe where the knowledge they are dominant lives.

  * A dominance is given by a function d : ğ“£ Ì‡ â†’ ğ“š Ì‡ subject to suitable
    properties.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-Equiv
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-FunExt

module Dominance where

module _ {ğ“£ ğ“š : Universe} where

 D2 : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š Ì‡
 D2 d = (X : ğ“£ Ì‡ ) â†’ is-prop (d X)

 D3 : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š Ì‡
 D3 d = (X : ğ“£ Ì‡ ) â†’ d X â†’ is-prop X

 D4 : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“š Ì‡
 D4 d = d ğŸ™

 D5 : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š Ì‡
 D5 d = (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ ) â†’ d P â†’ ((p : P) â†’ d (Q p)) â†’ d (Î£ Q)

\end{code}

condition D5 is more conceptual and often what we need in practice,
and condition D5' below is easier to check:

\begin{code}

 D5' : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š Ì‡
 D5' d = (P Q' : ğ“£ Ì‡ ) â†’ d P â†’ (P â†’ d Q') â†’ d (P Ã— Q')

 D5-gives-D5' : (d : ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ D5 d â†’ D5' d
 D5-gives-D5' d d5 P Q' i j = d5 P (Î» p â†’ Q') i j

 D3-and-D5'-give-D5 : propext ğ“£
                    â†’ (d : ğ“£ Ì‡ â†’ ğ“š Ì‡ )
                    â†’ D3 d
                    â†’ D5' d
                    â†’ D5 d
 D3-and-D5'-give-D5 pe d d3 d5' P Q i j = w
  where
   Q' : ğ“£ Ì‡
   Q' = Î£ Q

   k : is-prop P
   k = d3 P i

   l : (p : P) â†’ is-prop (Q p)
   l p = d3 (Q p) (j p)

   m : is-prop Q'
   m = Î£-is-prop k l

   n : (p : P) â†’ Q p â‰¡ Q'
   n p = pe (l p) m (Î» q        â†’ (p , q))
                    (Î» (p' , q) â†’ transport Q (k p' p) q)

   j' : P â†’ d Q'
   j' p = transport d (n p) (j p)

   u : d (P Ã— Q')
   u = d5' P Q' i j'

   v : P Ã— Q' â‰¡ Î£ Q
   v = pe (Ã—-is-prop k m) m (Î» (p , p' , q) â†’ (p' , q))
                            (Î» (p' , q)     â†’ (p' , p' , q))
   w : d (Î£ Q)
   w = transport d v u

 is-dominance : (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š Ì‡
 is-dominance d = D2 d Ã— D3 d Ã— D4 d Ã— D5 d

 Dominance : (ğ“£ âŠ” ğ“š)âº Ì‡
 Dominance = Î£ d ê‰ (ğ“£ Ì‡ â†’ ğ“š Ì‡ ) , is-dominance d

 is-dominant : (D : Dominance) â†’ ğ“£ Ì‡ â†’ ğ“š Ì‡
 is-dominant (d , _) = d

 being-dominant-is-prop : (D : Dominance) â†’ (X : ğ“£ Ì‡ ) â†’ is-prop (is-dominant D X)
 being-dominant-is-prop (_ , (isp , _)) = isp

 dominant-types-are-props : (D : Dominance) â†’ (X : ğ“£ Ì‡ ) â†’ is-dominant D X â†’ is-prop X
 dominant-types-are-props (_ , (_ , (disp , _))) = disp

 ğŸ™-is-dominant : (D : Dominance) â†’ is-dominant D ğŸ™
 ğŸ™-is-dominant (_ , (_ , (_ , (oisd , _)))) = oisd

 dominant-closed-under-Î£ : (D : Dominance) â†’ (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ )
                         â†’ is-dominant D P â†’ ((p : P) â†’ is-dominant D (Q p)) â†’ is-dominant D (Î£ Q)
 dominant-closed-under-Î£ (_ , (_ , (_ , (_ , cus)))) = cus

 being-dominance-is-prop : Fun-Ext â†’ (d : ğ“£ Ì‡ â†’ ğ“š Ì‡ ) â†’ is-prop (is-dominance d)
 being-dominance-is-prop fe d = prop-criterion lemma
  where
   lemma : is-dominance d â†’ is-prop (is-dominance d)
   lemma i = Î£-is-prop
               (Î -is-prop fe (Î» _ â†’ being-prop-is-prop fe))
                (Î» _ â†’ Ã—â‚ƒ-is-prop
                         (Î â‚‚-is-prop fe (Î» _ _ â†’ being-prop-is-prop fe))
                         (being-dominant-is-prop (d , i) ğŸ™)
                         (Î â‚„-is-prop fe Î» _ Q _ _ â†’ being-dominant-is-prop (d , i) (Î£ Q)))

\end{code}

Example: the decidable propositions form a dominance.

\begin{code}

module DecidableDominance where

 open import DecidableAndDetachable

 decidable-dominance : Fun-Ext â†’ Dominance {ğ“¤} {ğ“¤}
 decidable-dominance fe = (Î» P â†’ is-prop P Ã— decidable P) ,
                          (Î» P â†’ Î£-is-prop
                                    (being-prop-is-prop fe)
                                    (decidability-of-prop-is-prop fe)) ,
                          (Î» X â†’ prâ‚) ,
                          (ğŸ™-is-prop , inl â‹†) ,
                          Î» P Q dP dQ â†’ Î£-is-prop (prâ‚ dP) (Î» p â†’ prâ‚ (dQ p)) ,
                                         decidable-closed-under-Î£ (prâ‚ dP) (prâ‚‚ dP) Î» p â†’ prâ‚‚ (dQ p)

module lift
        {ğ“£ ğ“š : Universe}
        (d : ğ“£ Ì‡ â†’ ğ“š Ì‡ )
        (isd : is-dominance d)
       where

 D : Dominance
 D = (d , isd)

 L : âˆ€ {ğ“¥} (X : ğ“¥ Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š âŠ” ğ“¥ Ì‡
 L X = Î£ P ê‰ ğ“£ Ì‡ , d P Ã— (P â†’ X)

 LL : âˆ€ {ğ“¥} (X : ğ“¥ Ì‡ ) â†’ ğ“£ âº âŠ” ğ“š âŠ” ğ“¥ Ì‡
 LL X = L (L X)

 _â‡€_ : âˆ€ {ğ“¥ ğ“¦} â†’ ğ“¥ Ì‡ â†’ ğ“¦ Ì‡ â†’ ğ“£ âº âŠ” ğ“š âŠ” ğ“¥ âŠ” ğ“¦ Ì‡
 X â‡€ Y = X â†’ L Y

 isDefined : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ L X â†’ ğ“£ Ì‡
 isDefined (P , (isdp , Ï†)) = P

 is-dominantisDefined : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ (xÌƒ : L X) â†’ is-dominant D (isDefined xÌƒ)
 is-dominantisDefined (P , (isdp , Ï†)) = isdp

 value : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ (xÌƒ : L X) â†’ isDefined xÌƒ â†’ X
 value (P , (isdp , Ï†)) = Ï†

 Î· : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ X â†’ L X
 Î· x = ğŸ™ , ğŸ™-is-dominant D , Î» _ â†’ x

 extension : âˆ€ {ğ“¥ ğ“¦} {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ } â†’ (X â‡€ Y) â†’ (L X â†’ L Y)
 extension {ğ“¥} {ğ“¦} {X} {Y} f (P , (isdp , Ï†)) = (Q , (isdq , Î³))
  where
   Q : ğ“£ Ì‡
   Q = Î£ p ê‰ P , isDefined (f (Ï† p))

   isdq : is-dominant D Q
   isdq = dominant-closed-under-Î£ D
            P
            (Î» p â†’ isDefined (f (Ï† p)))
            isdp
            (Î» p â†’ is-dominantisDefined (f (Ï† p)))

   Î³ : Q â†’ Y
   Î³ (p , def) = value (f (Ï† p)) def

 _â™¯ : âˆ€ {ğ“¥ ğ“¦} {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ } â†’ (X â‡€ Y) â†’ (L X â†’ L Y)
 f â™¯ = extension f

 _â—Œ_ : âˆ€ {ğ“¥ ğ“¦ ğ“£} {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ } {Z : ğ“£ Ì‡ }
     â†’ (Y â‡€ Z) â†’ (X â‡€ Y) â†’ (X â‡€ Z)
 g â—Œ f = g â™¯ âˆ˜ f

 Î¼ : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ L (L X) â†’ L X
 Î¼ = extension id

 {- TODO:
 kleisli-lawâ‚€ : âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ } â†’ extension (Î· {ğ“¥} {X}) âˆ¼ id
 kleisli-lawâ‚€ {ğ“¥} {X} (P , (isdp , Ï†)) = {!!}

 kleisli-lawâ‚ : âˆ€ {ğ“¥ ğ“¦)} {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ } (f : X â‡€ Y) â†’ extension f âˆ˜ Î· âˆ¼ f
 kleisli-lawâ‚ {ğ“¥} {ğ“¦} {X} {Y} f x = {!!}


 kleisli-lawâ‚‚ : âˆ€ {ğ“¥ ğ“¦) T} {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ } {Z : ğ“£ Ì‡ } (f : X â‡€ Y) (g : Y â‡€ Z)
              â†’ (g â™¯ âˆ˜ f)â™¯ âˆ¼ g â™¯ âˆ˜ f â™¯
 kleisli-lawâ‚‚ {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {Z} f g (P , (isdp , Ï†)) = {!!}
 -}

\end{code}
