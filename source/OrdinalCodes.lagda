
    Ordinals in GÃ¶del's system T and in Martin-LÃ¶f Type Theory
    ------------------------------------------------------------

    Martin Escardo, 2010, coded in Agda November 2011.

    This is based on work of Coquand, Setzer and Hancock, in particular:

    (i) Coquand, Hancock and Setzer (1997)
    http://www.cse.chalmers.se/~coquand/ordinal.ps

    (ii) Hancock (Russell'08 Proof Theory meets Type Theory, Swansea)
    http://www.cs.swan.ac.uk/~csetzer/russell08/slides/hancock.pdf

    An interesting and powerful idea is their use of "lenses", which
    allows to define rather large ordinals, in particular in the
    presence of dependent types and universes. Another idea is to use
    Church encodings of ordinals.

    Here I do something more modest, without lenses, but still with
    Church encodings. I explicitly define addition, multiplication and
    exponentiation of ordinals, and there may be a small contribution
    here.

    In the Goedel system T fragment of Agda, these arithmetic
    operations cannot be uniformly typed, but they still have neat
    definitions. In particular, because of the non-uniform typing, we
    can only dominate ordinals strictly below Îµâ‚€ - this is not a
    limitation of our approach, but rather of system T.

    Using dependent types (products in fact will be enough) and a
    universe, we can get a uniform typing of the arithmetic
    operations, and hence the ordinal Îµâ‚€ and much higher indeed. But I
    will content myself with defining Îµâ‚€, which is not definable in
    system T, as an illustration of what dependent types and universes
    add in terms of expressivity. But it is easy to get higher using
    what is defined here. If you want to get really high, then you
    should study lenses: http://personal.cis.strath.ac.uk/~ph/

    We proceed in three steps to define addition, multiplication and
    exponentiation, and hence Îµâ‚€ and much higher.

    (1): We essentially use Goedel's system T and work with a type

            O X = X â†’ (X â†’ X) â†’ ((â„• â†’ X) â†’ X) â†’ X

         of Church encodings of ordinal trees, where X is a parameter,
         and define the basic arithmetic operations on ordinals with
         the non-uniform types

            add : O X â†’ O X â†’ O X
            mul : O X â†’ O (O X) â†’ O X
            exp : O (O X) â†’ O (O X) â†’ O X

         These types are the best one can do in system T. With this we
         can define ordinals abitrarily close to, and strictly below,
         the ordinal Îµâ‚€.

    (2): We use the first universe and dependent products to define

            O' X = Î  (n : â„•) â†’ Oâ¿âºÂ¹ X

         and hence the arithmetic operations with uniform types

            add', mul', exp' : O' X â†’ O' X â†’ O' X

         from add, mul, exp defined in step (1). With this we can now
         define Îµâ‚€, not only in O' X, but also in O X.

         So you can see the type O' X as an auxiliary construction to
         get more in O X.

    (3): We inductively define a (standard) W-type of ordinal trees
    (e.g. studied by Brouwer, by Howard in an extension of system T,
    and mentioned by Martin-Loef in some of his papers), and show how
    to define complex Brouwer ordinal trees *without* using
    (structural) recursion on ordinals trees, using step (2).

    All (primitive) recursions in the development of (1)-(3) are on
    the set â„•. This is followed by exercises, now using recursion and
    induction on Brouwer ordinal trees.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module OrdinalCodes where

open import SpartanMLTT

\end{code}

Step (1). Church ordinal trees:

\begin{code}

O : ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚€ Ì‡
O X = X â†’ (X â†’ X) â†’ ((â„• â†’ X) â†’ X) â†’ X

zer : {X : ğ“¤â‚€ Ì‡ } â†’ O X
zer = Î» z â†’ Î» s â†’ Î» l â†’ z

suc : {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O X
suc a = Î» z â†’ Î» s â†’ Î» l â†’ s (a z s l)

lim : {X : ğ“¤â‚€ Ì‡ } â†’ (â„• â†’ O X) â†’ O X
lim as = Î» z â†’ Î» s â†’ Î» l â†’ l (Î» i â†’ as i z s l)

O-rec : {X : ğ“¤â‚€ Ì‡ } â†’ X â†’ (X â†’ X) â†’ ((â„• â†’ X) â†’ X) â†’ (O X â†’ X)
O-rec z s l a = a z s l

\end{code}

Notice that by uncurrying, flipping and currying the type of O-rec is
isomorphic to {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O X, but the above form is more
convenient for recursive definitions.

In this first step, we have natural definitions but the types are not
uniform:

\begin{code}

add : {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O X â†’ O X
add a b = Î» z â†’ Î» s â†’ Î» l â†’ a (b z s l) s l

mul : {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O (O X) â†’ O X
mul a = O-rec zer (Î» r â†’ add r a) lim

exp : {X : ğ“¤â‚€ Ì‡ } â†’ O (O X) â†’ O (O X) â†’ O X
exp a = O-rec (suc zer) (Î» r â†’ mul r a) lim

\end{code}

Remark: if we had used O-rec to define add, it would instead have
the type {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O (O X) â†’ O X, and then mul would have
the type {X : ğ“¤â‚€ Ì‡ } â†’ O (O X) â†’ O (O X) â†’ O X, with the same
definition, but the same definition of exp then cannot be typed
using iterations of O. In step (2) we will consider all finite
iterations of O to define a type O', and give a uniform type
{X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O' X â†’ O' X to add, mul, and exp.

We will not use the following:

\begin{code}

down : {X : ğ“¤â‚€ Ì‡ } â†’ O (O X) â†’ O X
down = O-rec zer suc lim

\end{code}

There is a term up : {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O (O X), but no such term has
the desired behaviour of being a (left or right) inverse of down.

Before using the first universe, we can dominate any ordinal below Îµâ‚€.

The sequence of finite ordinals first:

\begin{code}

finite : {X : ğ“¤â‚€ Ì‡ } â†’ â„• â†’ O X
finite = rec zer suc

\end{code}

Its limit:

\begin{code}

Ï‰ : {X : ğ“¤â‚€ Ì‡ } â†’ O X
Ï‰ = lim finite

\end{code}

Now the iterated powers of Ï‰, which can't be defined uniformly
without universes or W-types or impredicativity etc.

\begin{code}

Ï‰â‚ : {X : ğ“¤â‚€ Ì‡ } â†’ O X
Ï‰â‚ = exp Ï‰ Ï‰

Ï‰â‚‚ : {X : ğ“¤â‚€ Ì‡ } â†’ O X
Ï‰â‚‚ = exp Ï‰ Ï‰â‚

Ï‰â‚ƒ : {X : ğ“¤â‚€ Ì‡ } â†’ O X
Ï‰â‚ƒ = exp Ï‰ Ï‰â‚‚

\end{code}

And so on. Although the definitions look uniform, they are not. In
fact, the candidate for the recursion step doesn't have type
O X â†’ O X, but rather:

\begin{code}

step :  {X : ğ“¤â‚€ Ì‡ } â†’ O (O X) â†’ O X
step = exp Ï‰

\end{code}

If you try to define

  Ï‰-tower : {X : ğ“¤â‚€ Ì‡ } â†’ â„• â†’ O X
  Ï‰-tower = rec Ï‰ (exp Ï‰)

then Agda rightfully complains that this would need X = O X, which
is impossible.

Moreover, e.g. in the definition of Ï‰â‚ƒ the use of Ï‰ has its type X
instantiated to O (O (O (O X))), if I counted properly. Thus, although we
always write Ï‰ in the definitions of Ï‰â‚, Ï‰â‚‚, Ï‰â‚ƒ, ..., if we are
strictly working in system T we need a different definition of Ï‰ in
each case (with the same raw term but with a different type).


Step (2).

We now use the first universe to reach Îµâ‚€ and beyond.  We
build a type O' X of ordinals based on O X. It is the definition of
recâ‚, used to construct O', that uses the first universe. So we
move from higher-type primitive recursion (system T) to even higher
primitive recursion using a universe.

\begin{code}

recâ‚ : ğ“¤â‚€ Ì‡ â†’ (ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚€ Ì‡ ) â†’ â„• â†’ ğ“¤â‚€ Ì‡
recâ‚ X F zero = X
recâ‚ X F (succ n) = F (recâ‚ X F n)

\end{code}

We define O' X = Î  (n : â„•) â†’ Oâ¿âºÂ¹ X as follows in Agda notation:

\begin{code}

O' : ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚€ Ì‡
O' X = (n : â„•) â†’ O (recâ‚ X O n)

zer' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X
zer' = Î» n â†’ zer

suc' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O' X
suc' a = Î» n â†’ suc (a n)

lim' : {X : ğ“¤â‚€ Ì‡ } â†’ (â„• â†’ O' X) â†’ O' X
lim' as = Î» n â†’ lim (Î» i â†’ as i n)

add' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O' X â†’ O' X
add' a b = Î» n â†’ add (a n) (b n)

mul' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O' X â†’ O' X
mul' a b = Î» n â†’ mul (a n) (b (succ n))

exp' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O' X â†’ O' X
exp' a b = Î» n â†’ exp (a (succ n)) (b (succ n))

Ï‰' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X
Ï‰' = Î» n â†’ Ï‰

Ï‰-tower' : {X : ğ“¤â‚€ Ì‡ } â†’ â„• â†’ O' X
Ï‰-tower' = rec Ï‰' (exp' Ï‰')

\end{code}

The ordinal Îµâ‚€ can now be defined in O' X (and hence in O X - see
below).

\begin{code}

Îµâ‚€' : {X : ğ“¤â‚€ Ì‡ } â†’ O' X
Îµâ‚€' = lim' Ï‰-tower'

\end{code}

Because we now have addition, multiplication, exponentiation and
limits in a uniform way, we can of course get much higher than Îµâ‚€.
For example, Îµâ‚, Îµâ‚‚, ... can be defined uniformly and hence we can
define ÎµÏ‰. Then proceeding in the same way we can get ÎµÎ± for Î± =
Îµâ‚€, and much higher indeed.


Now, using this last step (2), we can project to step (1) and
define Îµâ‚€ as an element of O X using the following coersion:

\begin{code}

O'â†¦O : {X : ğ“¤â‚€ Ì‡ } â†’ O' X â†’ O X
O'â†¦O a = a zero

Îµâ‚€ : {X : ğ“¤â‚€ Ì‡ } â†’ O X
Îµâ‚€ = O'â†¦O Îµâ‚€'

\end{code}

Notice that the following doesn't type check:

  Oâ†¦O' : {X : ğ“¤â‚€ Ì‡ } â†’ O X â†’ O' X
  Oâ†¦O' a = Î» n â†’ a

But it does type check for some particular a, such as Ï‰ in the
above definition of Ï‰'.


Step (3). Brouwer's ordinal trees.

I will use the letters u,v to range over B, and us,vs to range over
forests, that is, sequences â„• â†’ B.

\begin{code}

data B : ğ“¤â‚€ Ì‡ where
 Z : B
 S : B â†’ B
 L : (â„• â†’ B) â†’ B

\end{code}

Firstly we can go from O X to B when X=B:

\begin{code}

Oâ†¦B : O B â†’ B
Oâ†¦B u = u Z S L

\end{code}

We can now define a very tall ordinal tree without recursion on B:

\begin{code}

B-Îµâ‚€ : B
B-Îµâ‚€ = Oâ†¦B Îµâ‚€

\end{code}

Step (4): But the above is not the end of the story. This step, not
mentioned above, is started but not completed. We leave the
completion as an exercise at the time of writing.

We can define the tree B-Îµâ‚€ by recursion on B and we should show,
in Agda, that this produces the same result as the above
recursion-free definition.

\begin{code}
B-rec : {X : ğ“¤â‚€ Ì‡ } â†’ X â†’ (X â†’ X) â†’ ((â„• â†’ X) â†’ X) â†’ B â†’ X
B-rec {X} z s l = h
 where
  h : B â†’ X
  h Z = z
  h (S u) = s (h u)
  h (L us) = l (Î» i â†’ h (us i))
\end{code}

By suitable uncurrying, flipping and currying, the type of B-rec is
isomorphic to {X : ğ“¤â‚€ Ì‡ } â†’ B â†’ O X, but the above form is more
convenient for recursive definitions:

\begin{code}

Bâ†¦O : {X : ğ“¤â‚€ Ì‡ } â†’ B â†’ O X
Bâ†¦O u s z l = B-rec s z l u

\end{code}

Ordinal tree arithmetic:

\begin{code}

B-add : B â†’ B â†’ B
B-add u = B-rec u S L

B-mul : B â†’ B â†’ B
B-mul u = B-rec Z (Î» r â†’ B-add r u) L

B-exp : B â†’ B â†’ B
B-exp u = B-rec (S Z) (Î» r â†’ B-mul r u) L

B-finite : â„• â†’ B
B-finite = rec Z S

B-Ï‰ : B
B-Ï‰ = L B-finite

B-Ï‰-tower : â„• â†’ B
B-Ï‰-tower = rec B-Ï‰ (B-exp B-Ï‰)

B-Îµâ‚€-alternative : B
B-Îµâ‚€-alternative = L B-Ï‰-tower

\end{code}

We are almost ready to formulate the exercise. We need to define
extensional equality on B.

\begin{code}

data _â‰£_ : B â†’ B â†’ ğ“¤â‚€ Ì‡ where
 â‰£-Z : Z â‰£ Z
 â‰£-S : (u v : B) â†’ u â‰£ v â†’ S u â‰£ S v
 â‰£-L : (us vs : â„• â†’ B) â†’ ((i : â„•) â†’ us i â‰£ vs i) â†’ L us â‰£ L vs

\end{code}

Exercises: Construct elements of the following Exercise* types:

\begin{code}

Exerciseâ‚€ = B-Îµâ‚€ â‰£ B-Îµâ‚€-alternative

\end{code}

Here is a sketch of how this can be approached:

\begin{code}

Exerciseâ‚ = (u v : B) â†’ B-add u v â‰£ Oâ†¦B (add (Bâ†¦O u) (Bâ†¦O v))
Exerciseâ‚‚ = (u v : B) â†’ B-mul u v â‰£ Oâ†¦B (mul (Bâ†¦O u) (Bâ†¦O v))
Exerciseâ‚ƒ = (u v : B) â†’ B-exp u v â‰£ Oâ†¦B (exp (Bâ†¦O u) (Bâ†¦O v))

\end{code}

We need more coersions:

\begin{code}

Bâ†¦O' : {X : ğ“¤â‚€ Ì‡ } â†’ B â†’ O' X
Bâ†¦O' u = Î» n â†’ Bâ†¦O u

O'â†¦B : O' B â†’ B
O'â†¦B a =  Oâ†¦B (O'â†¦O a)

Exerciseâ‚' = (u v : B) â†’ B-add u v â‰£ O'â†¦B (add' (Bâ†¦O' u) (Bâ†¦O' v))
Exerciseâ‚‚' = (u v : B) â†’ B-mul u v â‰£ O'â†¦B (mul' (Bâ†¦O' u) (Bâ†¦O' v))
Exerciseâ‚ƒ' = (u v : B) â†’ B-exp u v â‰£ O'â†¦B (exp' (Bâ†¦O' u) (Bâ†¦O' v))

\end{code}

And, to solve the above exercises, you will need induction on B
(which amounts to "primitive recursion" on B, rather than simple
recursion or iteration B-rec on B):

\begin{code}

B-induction : {A : B â†’ ğ“¤â‚€ Ì‡ } â†’
   A Z â†’
  ((u : B) â†’ A u â†’ A (S u)) â†’
  ((us : â„• â†’ B) â†’ ((i : â„•) â†’ A (us i)) â†’ A (L us)) â†’
-----------------------------------------------------------
  ((u : B) â†’ A u)

B-induction {A} z s l = h
 where
  h : (u : B) â†’ A u
  h Z = z
  h (S u) = s u (h u)
  h (L us) = l us (Î» i â†’ h (us i))

\end{code}
