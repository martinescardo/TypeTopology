Martin Escardo, December 2020

A notion of disconnectedness. This is different from homotopy
disconnectedness in the sense of the HoTT book. It is based on the
topological, rather than homotopical, interpretation of types.

A discussion of such models is in

  M.H. Escardo and Chuangjie Xu. A constructive manifestation of the
  Kleene-Kreisel continuous functionals. Annals of Pure and Applied
  Logic, 2016.

and

  M.H. Escardo and Thomas Streicher. Annals of Pure and Applied Logic,
  2016.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module TypeTopology.DisconnectedTypes where

open import MLTT.Spartan
open import MLTT.Two-Properties
open import Naturals.Properties
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Hedberg
open import UF.Retracts

\end{code}

The following notions of disconnectedness are data rather than
property.

\begin{code}

is-disconnectedâ‚€ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-disconnectedâ‚€ X = retract ğŸš of X

is-disconnectedâ‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-disconnectedâ‚ X = Î£ p ê‰ (X â†’ ğŸš) , fiber p â‚€ Ã— fiber p â‚

is-disconnectedâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
is-disconnectedâ‚‚ {ğ“¤} X = Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚)

is-disconnectedâ‚ƒ : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
is-disconnectedâ‚ƒ {ğ“¤} X = Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X)

is-disconnected-eq : (X : ğ“¤ Ì‡ )
                â†’ (is-disconnectedâ‚€ X â†’ is-disconnectedâ‚ X)
                Ã— (is-disconnectedâ‚ X â†’ is-disconnectedâ‚‚ X)
                Ã— (is-disconnectedâ‚‚ X â†’ is-disconnectedâ‚ƒ X)
                Ã— (is-disconnectedâ‚ƒ X â†’ is-disconnectedâ‚€ X)

is-disconnected-eq {ğ“¤} X = (f , g , h , k)
 where
  f : (Î£ p ê‰ (X â†’ ğŸš) , Î£ s ê‰ (ğŸš â†’ X) , p âˆ˜ s âˆ¼ id)
    â†’ Î£ p ê‰ (X â†’ ğŸš) , (Î£ x ê‰ X , p x ï¼ â‚€) Ã— (Î£ x ê‰ X , p x ï¼ â‚)
  f (p , s , e) = p , (s â‚€ , e â‚€) , (s â‚ , e â‚)

  g : (Î£ p ê‰ (X â†’ ğŸš) , (Î£ x ê‰ X , p x ï¼ â‚€) Ã— (Î£ x ê‰ X , p x ï¼ â‚))
    â†’ Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚)
  g (p , (xâ‚€ , eâ‚€) , (xâ‚ , eâ‚)) = (Î£ x ê‰ X , p x ï¼ â‚€) ,
                                  (Î£ x ê‰ X , p x ï¼ â‚) ,
                                  (xâ‚€ , eâ‚€) ,
                                  (xâ‚ , eâ‚) ,
                                  qinveq Ï• (Î³ , Î³Ï• , Ï•Î³)
   where
    Ï• : X â†’ (Î£ x ê‰ X , p x ï¼ â‚€) + (Î£ x ê‰ X , p x ï¼ â‚)
    Ï• x = ğŸš-equality-cases
           (Î» (râ‚€ : p x ï¼ â‚€) â†’ inl (x , râ‚€))
           (Î» (râ‚ : p x ï¼ â‚) â†’ inr (x , râ‚))

    Î³ : (Î£ x ê‰ X , p x ï¼ â‚€) + (Î£ x ê‰ X , p x ï¼ â‚) â†’ X
    Î³ (inl (x , râ‚€)) = x
    Î³ (inr (x , râ‚)) = x

    Ï•Î³ : Ï• âˆ˜ Î³ âˆ¼ id
    Ï•Î³ (inl (x , râ‚€)) = ğŸš-equality-casesâ‚€ râ‚€
    Ï•Î³ (inr (x , râ‚)) = ğŸš-equality-casesâ‚ râ‚

    Î³Ï• : Î³ âˆ˜ Ï• âˆ¼ id
    Î³Ï• x = ğŸš-equality-cases
           (Î» (râ‚€ : p x ï¼ â‚€) â†’ ap Î³ (ğŸš-equality-casesâ‚€ râ‚€))
           (Î» (râ‚ : p x ï¼ â‚) â†’ ap Î³ (ğŸš-equality-casesâ‚ râ‚))

  h : (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚))
    â†’ (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X))
  h (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , (Ï• , Î³Ï•) , _)) = (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , Ï• , Î³Ï•))

  k : (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X))
    â†’ Î£ p ê‰ (X â†’ ğŸš) , Î£ s ê‰ (ğŸš â†’ X) , p âˆ˜ s âˆ¼ id
  k (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , Ï• , Î³Ï•)) = p , s , ps
   where
    p : X â†’ ğŸš
    p x = Cases (Î³ x) (Î» _ â†’ â‚€) (Î» _ â†’ â‚)

    s : ğŸš â†’ X
    s â‚€ = Ï• (inl xâ‚€)
    s â‚ = Ï• (inr xâ‚)

    ps : p âˆ˜ s âˆ¼ id
    ps â‚€ = ap (cases (Î» _ â†’ â‚€) (Î» _ â†’ â‚)) (Î³Ï• (inl xâ‚€))
    ps â‚ = ap (cases (Î» _ â†’ â‚€) (Î» _ â†’ â‚)) (Î³Ï• (inr xâ‚))

\end{code}

The following is our official notion of disconnectedness (logically
equivalent to the other ones):

\begin{code}

is-disconnected : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-disconnected = is-disconnectedâ‚€

\end{code}

Some examples:

\begin{code}

ğŸš-is-disconnected : is-disconnected ğŸš
ğŸš-is-disconnected = identity-retraction

â„•-is-disconnected : is-disconnected â„•
â„•-is-disconnected = (r , s , rs)
 where
  r : â„• â†’ ğŸš
  r zero     = â‚€
  r (succ n) = â‚

  s : ğŸš â†’ â„•
  s â‚€ = zero
  s â‚ = succ zero

  rs : (n : ğŸš) â†’ r (s n) ï¼ n
  rs â‚€ = refl
  rs â‚ = refl

types-with-isolated-point-different-from-another-point-are-disconnected :

    {Y : ğ“¥ Ì‡ }
  â†’ (Î£ yâ‚€ ê‰ Y , Î£ yâ‚ ê‰ Y , (yâ‚€ â‰  yâ‚) Ã— is-isolated yâ‚€ )
  â†’ is-disconnected Y

types-with-isolated-point-different-from-another-point-are-disconnected (yâ‚€ , yâ‚ , ne , i) =
  ğŸš-retract-of-non-trivial-type-with-isolated-point ne i

discrete-types-with-two-different-points-are-disconnected :

    {Y : ğ“¥ Ì‡ }
  â†’ (Î£ yâ‚€ ê‰ Y , Î£ yâ‚ ê‰ Y , yâ‚€ â‰  yâ‚)
  â†’ is-discrete Y
  â†’ is-disconnected Y

discrete-types-with-two-different-points-are-disconnected (yâ‚€ , yâ‚ , ne) d =
  types-with-isolated-point-different-from-another-point-are-disconnected
   (yâ‚€ , yâ‚ , ne , d yâ‚€)


â„•-is-disconnected' : is-disconnected â„•
â„•-is-disconnected' = discrete-types-with-two-different-points-are-disconnected
                     (0 , 1 , succ-no-fp 0)
                     â„•-is-discrete

retract-is-disconnected : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                        â†’ retract X of Y
                        â†’ is-disconnected X
                        â†’ is-disconnected Y
retract-is-disconnected = retracts-compose

\end{code}

TODO. Define totally disconnected. Then maybe for compact types the
notions of total disconnectedness and total separatedness agree.

The negation of disconnectedness can be expressed positively in
various equivalent ways.

\begin{code}

open import TypeTopology.TotallySeparated
open import UF.Base
open import UF.FunExt
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

is-connectedâ‚€ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚€ X = (f : X â†’ ğŸš) â†’ wconstant f

is-connectedâ‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚ X = (x y : X) â†’ x ï¼â‚‚ y

is-connectedâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚‚ X = Â¬ is-disconnected X


connectedâ‚€-types-are-connectedâ‚ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚€ X â†’ is-connectedâ‚ X
connectedâ‚€-types-are-connectedâ‚ i x y p = i p x y

connectedâ‚-types-are-connectedâ‚€ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚ X â†’ is-connectedâ‚€ X
connectedâ‚-types-are-connectedâ‚€ Ï• f x y = Ï• x y f

connectedâ‚€-types-are-connectedâ‚‚ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚€ X â†’ is-connectedâ‚‚ X
connectedâ‚€-types-are-connectedâ‚‚ c (r , s , rs) = n (c r)
 where
  n : Â¬ wconstant r
  n Îº = zero-is-not-one (â‚€       ï¼âŸ¨ (rs â‚€)â»Â¹ âŸ©
                         r (s â‚€) ï¼âŸ¨ Îº (s â‚€) (s â‚) âŸ©
                         r (s â‚) ï¼âŸ¨ rs â‚ âŸ©
                         â‚       âˆ)

disconnected-types-are-not-connected : {X : ğ“¤ Ì‡ }
                                     â†’ is-disconnected X
                                     â†’ Â¬ is-connectedâ‚€ X
disconnected-types-are-not-connected c d = connectedâ‚€-types-are-connectedâ‚‚ d c

connectedâ‚‚-types-are-is-connectedâ‚€ : {X : ğ“¤ Ì‡ }
                                   â†’ is-connectedâ‚‚ X
                                   â†’ is-connectedâ‚€ X
connectedâ‚‚-types-are-is-connectedâ‚€ {ğ“¤} {X} n f x y =
 ğŸš-is-Â¬Â¬-separated (f x) (f y) Ï•
 where
  Ï• : Â¬Â¬ (f x ï¼ f y)
  Ï• u = n (f , s , fs)
   where
    s : ğŸš â†’ X
    s â‚€ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x ï¼ â‚€) â†’ x)
           (Î» (pâ‚ : f x ï¼ â‚) â†’ y)
    s â‚ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x ï¼ â‚€) â†’ y)
           (Î» (pâ‚ : f x ï¼ â‚) â†’ x)

    a : f x ï¼ â‚ â†’ f y ï¼ â‚€
    a p = different-from-â‚-equal-â‚€ (Î» (q : f y ï¼ â‚) â†’ u (p âˆ™ (q â»Â¹)))

    b : f x ï¼ â‚€ â†’ f y ï¼ â‚
    b p = different-from-â‚€-equal-â‚ (Î» (q : f y ï¼ â‚€) â†’ u (p âˆ™ q â»Â¹))

    fs : f âˆ˜ s âˆ¼ id
    fs â‚€ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x ï¼ â‚€) â†’ f (s â‚€) ï¼âŸ¨ ap f (ğŸš-equality-casesâ‚€ pâ‚€) âŸ©
                               f x      ï¼âŸ¨ pâ‚€ âŸ©
                               â‚€        âˆ)
           (Î» (pâ‚ : f x ï¼ â‚) â†’ f (s â‚€) ï¼âŸ¨ ap f (ğŸš-equality-casesâ‚ pâ‚) âŸ©
                               f y      ï¼âŸ¨ a pâ‚ âŸ©
                               â‚€        âˆ)
    fs â‚ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x ï¼ â‚€) â†’ f (s â‚) ï¼âŸ¨ ap f (ğŸš-equality-casesâ‚€ pâ‚€) âŸ©
                               f y      ï¼âŸ¨ b pâ‚€ âŸ©
                               â‚        âˆ)
           (Î» (pâ‚ : f x ï¼ â‚) â†’ f (s â‚) ï¼âŸ¨ ap f (ğŸš-equality-casesâ‚ pâ‚) âŸ©
                               f x      ï¼âŸ¨ pâ‚ âŸ©
                               â‚        âˆ)

is-connected : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connected = is-connectedâ‚€

being-connected-is-prop : {X : ğ“¤ Ì‡ } â†’ Fun-Ext â†’ is-prop (is-connected X)
being-connected-is-prop fe = Î â‚ƒ-is-prop fe (Î» f x y â†’ ğŸš-is-set)

\end{code}
