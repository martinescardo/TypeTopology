Andrew Swan, February 7th 2024

This is a generalisation of some of the results by MartÃ­n EscardÃ³ in
TypeTopology.PropTychonoff, based on the observation that for
propositions P, the functor sending A to P â†’ A is a
modality. Modalities of this form are an important special case and
they have a name; they are *open modalities* (Example 1.7 in
[1]). However, we will show a version of the theorem is not only true
for open modalities, but for all modalities.

For another example, let âˆ‡ be the modality of double negation sheaves
(Example 3.41 of [1]). The internal logic in this reflective universe
is boolean. It follows that âˆ‡ (is-compactâˆ™ A) holds for all types A,
and so we can deduce that âˆ‡ A is always compact.

TODO: add double negation sheaves to the library

We can also see as a special case that truncation preserves
compactness, although it seems unlikely there are any good examples of
compact higher types where it isn't already clear that the
0-truncation is compact.

When formulated in terms of modalities, the result is best thought of
as an "absoluteness result." When working in models of some theory, a
logical formula might make sense stated both internally in the model
and for the same object viewed externally from outside the
model. Absoluteness says that these two statements are equivalent.

In this case we are thinking of reflective subuniverses as models of
type theory sitting inside some larger "external" model of type
theory. We will show that compactness is an upwards absolute
notion. That is, if a type inside the reflective subuniverse is
compact with respect to the internal logic of the subuniverse then it
is compact viewed outside the subuniverse as just a type. The converse
does not quite hold, so potentially there could be compact types where
the internal statement of compactness is not true, and we don't get
full absoluteness.

We note that the result holds for all modalities with no further
conditions and in particular the modality is not required to be lex,
or to preserve ğŸ˜. For the main theorem, we don't even need a full
modality, and the weaker notion of reflective subuniverse suffices.

[1] Rijke, Shulman, Spitters, Modalities in homotopy type theory,
https://doi.org/10.23638/LMCS-16(1:2)2020

\begin{code}
{-# OPTIONS --safe --without-K --exact-split --auto-inline #-}
open import MLTT.Spartan
open import MLTT.Two-Properties

open import UF.Base
open import UF.Univalence
open import UF.UA-FunExt
open import UF.UniverseEmbedding
open import UF.Equiv

open import Modal.Subuniverse
import Modal.SigmaClosedReflectiveSubuniverse

open import TypeTopology.CompactTypes
\end{code}

Throughout we are going to assume that we are given a reflective
subuniverse. We import some notation and lemmas from
Modal.ReflectiveSubuniverse. In particular, we write â—‹ for the
modality corresponding to the reflective subuniverse.

\begin{code}
module TypeTopology.AbsolutenessOfCompactness
 (P : subuniverse ğ“¤ ğ“¥)
 (P-is-reflective : subuniverse-is-reflective P)
 where

open import Modal.ReflectiveSubuniverse P P-is-reflective
\end{code}

We now give some statements related to compactness. We first consider
what it means for a type in the reflective subuniverse to be compact
according to the internal logic. We recall that when we interpret type
theory in a reflective subuniverse, this is done by induction on the
structure of types. Dependent functions, Î£-types and identity types
are the same as externally, whereas whenever we see an inductive type
(such as ğŸš) we need to apply the modality.

Unwinding all this, gives the following internal definition of
compactness for the reflective subuniverse.

\begin{code}
is-internal-compactâˆ™ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-internal-compactâˆ™ A =
 (F : A â†’ â—‹ (Lift _ ğŸš))
 â†’ Î£ aâ‚€ ê‰ A , (F aâ‚€ ï¼ Î· _ (lift ğ“¤ â‚)
 â†’ (a : A)
 â†’ F a ï¼ Î· _ (lift ğ“¤ â‚))
\end{code}

It turns out that in addition to internal compactness, it's also
useful to consider the weaker notion below. The reason for this is
that although we can show internal compact implies compact, we don't
have the converse direction. However, we will be able to show that
compact implies weak internal compact.

This weaker notion will also be useful for making the connection with
the results of TypeTopology.PropTychonoff clear. To do this, we will
also look at the type obtained by simply applying the modality to the
statement that A is compact. We will be able to show â—‹ (is-compactâˆ™ A)
â†’ is-weak-internal-compactâˆ™, but not the same implication for just
internal compact.

\begin{code}
is-weak-internal-compactâˆ™ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-weak-internal-compactâˆ™ A =
 (F : A â†’ ğŸš)
   â†’ Î£ aâ‚€ ê‰ A , (F aâ‚€ ï¼ â‚ â†’ (a : A) â†’ Î· _ (lift ğ“¤ (F a)) ï¼ Î· _ (lift ğ“¤ â‚))
\end{code}

We check that weak internal compactness actually is weaker.

\begin{code}
internal-compact-implies-weak-internal-compact
 : (A : ğ“¤ Ì‡ )
 â†’ is-internal-compactâˆ™ A
 â†’ is-weak-internal-compactâˆ™ A

internal-compact-implies-weak-internal-compact A c F =
 (prâ‚ weak-internal-instance) ,
 (Î» p â†’ prâ‚‚ weak-internal-instance (ap (Î· _ âˆ˜ lift _) p))

 where
  F' : A â†’ â—‹ (Lift _ ğŸš)
  F' = Î· _ âˆ˜ (lift _) âˆ˜ F

  weak-internal-instance
   : Î£ aâ‚€ ê‰ A , (F' aâ‚€ ï¼ Î· _ (lift ğ“¤ â‚) â†’ (a : A) â†’ F' a ï¼ Î· _ (lift ğ“¤ â‚))

  weak-internal-instance = c F'
\end{code}

Note that we defined weak internal compactness so that it is also
implied by compactness.

\begin{code}
compact-implies-weak-internal-compact
 : (A : ğ“¤ Ì‡ )
 â†’ is-compactâˆ™ A
 â†’ is-weak-internal-compactâˆ™ A

compact-implies-weak-internal-compact A c F =
 (prâ‚ (c F)) , (Î» p a â†’ ap (Î· _ âˆ˜ lift _) (prâ‚‚ (c F) p a))

\end{code}

We can now prove the main theorem: whenever a modal type is weak
internal compact, it is (externally) compact.

Although it looks a bit different, this is the argument that most
closely follows the original theorem prop-tychonoff.

\begin{code}
weak-internal-compact-implies-compact
 : (A : ğ“¤ Ì‡ )
 â†’ (A-modal : is-modal A)
 â†’ is-weak-internal-compactâˆ™ A
 â†’ is-compactâˆ™ A
weak-internal-compact-implies-compact A A-modal c F = aâ‚€ , aâ‚€-works
 where
\end{code}

Constructing a candidate universal witness is very easy. We just use
the same one given by weak internal compactness.

\begin{code}
  internal-compactness-instance :
   Î£ aâ‚€ ê‰ A , (F aâ‚€ ï¼ â‚ â†’ (a : A) â†’ Î· _ (lift ğ“¤ (F a)) ï¼ Î· _ (lift ğ“¤ â‚))
  internal-compactness-instance = c F

  aâ‚€ = prâ‚ internal-compactness-instance
\end{code}

To show that the candidate universal witness actually works, we need
to check that the boolean F a is 1, whenever F aâ‚€ is. We will split
into two cases depending on the value of F a. If F a = 1, then we are
already done. The tricky case, which we deal with in the lemma below
is getting a proof F a = 1 out of a proof of F a = 0. We would like to
argue by contradiction from the fact that F aâ‚€ = 1, but F a =
0. However, all that weak internal compactness tells us is that Î·(F a)
= Î·(1) as elements of â—‹ ğŸš.  This is actually consistant with F a = 0:
consider the open modality on the empty type.

The idea of the lemma is as follows: given Î·(F a) = Î·(1), we can
derive a proof that Î·(0) = Î·(1). We define a map ğŸš â†’ A sending 0 to a
and 1 to aâ‚€. Since A is modal, this map must factor through â—‹ ğŸš, and
so we can apply ap to our path to get the required path a = aâ‚€.

\begin{code}
  lemma
   : (F aâ‚€ ï¼ â‚)
   â†’ (a : A)
   â†’ (F a ï¼ â‚€)
   â†’ (a ï¼ aâ‚€)
  lemma p a q =
   a ï¼âŸ¨ by-construction âŸ©
   t (lift _ â‚€) ï¼âŸ¨ â—‹-rec-compute _ _ _ _ _ â»Â¹ âŸ©
   t' (Î· _ (lift _ â‚€)) ï¼âŸ¨ ap t' modal-zero-is-modal-one âŸ©
   t' (Î· _ (lift _ â‚)) ï¼âŸ¨ â—‹-rec-compute _ _ _ _ _ âŸ©
   t (lift _ â‚) ï¼âŸ¨ by-construction âŸ©
   aâ‚€ âˆ

   where
    t : Lift _ ğŸš â†’ A
    t = ğŸš-cases a aâ‚€ âˆ˜ lower

    t' : â—‹ (Lift _ ğŸš) â†’ A
    t' = â—‹-rec _ _ A-modal t

    modal-zero-is-modal-one : Î· _ (lift _ â‚€) ï¼ Î· _ (lift _ â‚)
    modal-zero-is-modal-one =
     Î· _ (lift _ â‚€) ï¼âŸ¨ ap (Î· _ âˆ˜ lift _) (q â»Â¹) âŸ©
     Î· _ (lift _ (F a)) ï¼âŸ¨ prâ‚‚ internal-compactness-instance p a âŸ©
     Î· _ (lift _ â‚) âˆ

  aâ‚€-works : F aâ‚€ ï¼ â‚ â†’ (a : A) â†’ F a ï¼ â‚
  aâ‚€-works p a = ğŸš-equality-cases (Î» q â†’ ap F (lemma p a q) âˆ™ p) id
\end{code}

As a corollary we can combine the main theorem with our proposition
that internal compact implies weak internal compact, to show that if
a type is compact according to the internal logic of a reflective
subuniverse, then it is compact externally. That is, compactness is
upwards absolute for reflective subuniverses.

\begin{code}
internal-compact-implies-compact
 : (A : ğ“¤ Ì‡ )
 â†’ (A-modal : is-modal A)
 â†’ is-internal-compactâˆ™ A
 â†’ is-compactâˆ™ A
internal-compact-implies-compact A A-modal c =
 weak-internal-compact-implies-compact
  _
  A-modal
  (internal-compact-implies-weak-internal-compact _ c)
\end{code}

The remaining theorems in this module all require a couple of extra
assumptions: the universe needs to be univalent, and the subuniverse
needs to be Î£-closed, making it an actual modality.

\begin{code}
module WithUnivalenceAndSigmaClosedness
 (ua : is-univalent ğ“¤)
 (P-is-sigma-closed : subuniverse-is-sigma-closed P)
 where
\end{code}

We import some theorems about Î£-closed reflective subuniverses, and
recall proofs of the two ways that we will use univalence: function
extensionality and repleteness of the subuniverse.

\begin{code}
 module S =
  Modal.SigmaClosedReflectiveSubuniverse
   P P-is-reflective P-is-sigma-closed

 fe = univalence-gives-funext ua
 repleteness = univalence-implies-subuniverse-is-replete ua P
\end{code}

The next two lemmas get quite technical. In both cases the ideas are
simple, but we require constructing terms by by â—‹-induction or
recursion. This requires proving that certain types are â—‹-modal, which
requires some care with universe levels, as well as the application of
several lemmas using function extensionality and repleteness of P.

We first show that if A is weak internal compact, then so is â—‹ A.
\begin{code}
 â—‹-preserves-wi-compact
  : (A : ğ“¤ Ì‡ )
  â†’ is-weak-internal-compactâˆ™ A
  â†’ is-weak-internal-compactâˆ™ (â—‹ A)
 â—‹-preserves-wi-compact A c F = Î±â‚€ , Î±â‚€-works
  where
   F' : A â†’ ğŸš
   F' = F âˆ˜ Î· _

   compactness-instance
    : Î£ aâ‚€ ê‰ A ,
    (F' aâ‚€ ï¼ â‚ â†’ (a : A) â†’ Î· _ (lift _ (F' a)) ï¼ Î· _ (lift _ â‚))
   compactness-instance = c F'

   Î±â‚€ = Î· _ (prâ‚ compactness-instance)

   Î±â‚€-works
    : (p : F Î±â‚€ ï¼ â‚)
    â†’ (Î± : â—‹ A)
    â†’ Î· _ (lift _ (F Î±)) ï¼ Î· _ (lift _ â‚)
   Î±â‚€-works p =
    S.â—‹-induction
     fe
     _ _
     (Î» _ â†’
      id-types-of-modal-types-are-modal
       fe
       repleteness
       _ _
       _
       (â—‹-is-modal _))
     (prâ‚‚ compactness-instance p)
\end{code}

In the second technical lemma we strengthen the above result. We
derive the same conclusion as before, but we weaken the assumption by
putting it inside the modality.

\begin{code}
 â—‹-compact-implies-weak-internal-compact
  : (A : ğ“¤ Ì‡ )
  â†’ â—‹ (is-weak-internal-compactâˆ™ A)
  â†’ is-weak-internal-compactâˆ™ (â—‹ A)

 â—‹-compact-implies-weak-internal-compact A c F =
  demodify-wic-instance
   (â—‹-rec
    _
    _
    modified-wic-is-modal
    (Î» c' â†’ modify-wic-instance (â—‹-preserves-wi-compact A c' F))
    c)

  where
   modified-wic-instance : ğ“¤ Ì‡
   modified-wic-instance =
    Î£ Î±â‚€ ê‰ â—‹ A ,
    (lift ğ“¤ (F Î±â‚€) ï¼ lift ğ“¤ â‚ â†’
     (Î± : â—‹ A) â†’ Î· _ (lift ğ“¤ (F Î±)) ï¼ Î· _ (lift ğ“¤ â‚))

   demodify-wic-instance
    : modified-wic-instance
    â†’ Î£ Î±â‚€ ê‰ â—‹ A ,
    (F Î±â‚€ ï¼ â‚ â†’ (Î± : â—‹ A) â†’ Î· _ (lift ğ“¤ (F Î±)) ï¼ Î· _ (lift ğ“¤ â‚))
   demodify-wic-instance (Î±â‚€ , f) = Î±â‚€ , (Î» p Î± â†’ f (ap (lift _) p) Î±)

   modify-wic-instance
    : Î£ Î±â‚€ ê‰ â—‹ A ,
    (F Î±â‚€ ï¼ â‚ â†’ (Î± : â—‹ A) â†’ Î· _ (lift ğ“¤ (F Î±)) ï¼ Î· _ (lift ğ“¤ â‚))
    â†’ modified-wic-instance
   modify-wic-instance (Î±â‚€ , f) =
    Î±â‚€ , (Î» p Î± â†’ f (equivs-are-lc _ lift-is-equiv p) Î±)

   modified-wic-is-modal : is-modal modified-wic-instance
   modified-wic-is-modal =
    P-is-sigma-closed
     _ _
     (â—‹-is-modal A)
     (Î» _ â†’
      products-of-modal-types-are-modal
       fe
       repleteness
       _
       _
       Î» _ â†’
        products-of-modal-types-are-modal
         fe
         repleteness
         _ _
         (Î» _ â†’
           id-types-of-modal-types-are-modal
           fe
           repleteness
           _ _ _
           (â—‹-is-modal _)))
\end{code}

Finally, we can use the lemmas together with the main theorem to get a
result which is closer to the statement of prop-tychonoff. This says â—‹
"preserves compactness" in the sense that if â—‹ (A is compact), then
(â—‹ A) is compact.

We note that the arguments in prop-tychonoff show that we can think of
the family of types Y : X â†’ ğ“¥ Ì‡  without loss of generality as a
constant family. Aside from that and the assumption here of
univalence, we can view the remainder of theorem prop-tychonoff as a
special case of the one below. To see this, note that when X is a
proposition the functor X â†’ - is a modality, and so can derive
is-compactâˆ™ (X â†’ A) from X â†’ is-compactâˆ™ A.

\begin{code}
 modalities-preserve-compact
  : (A : ğ“¤ Ì‡  )
  â†’ â—‹ (is-compactâˆ™ A)
  â†’ is-compactâˆ™ (â—‹ A)
 modalities-preserve-compact A c =
  weak-internal-compact-implies-compact
   _
   (â—‹-is-modal _)
   (â—‹-compact-implies-weak-internal-compact A
    (â—‹-rec
     _ _
     (â—‹-is-modal _)
     (Î» c' â†’ Î· _ (compact-implies-weak-internal-compact _ c'))
     c))
\end{code}
