Martin Escardo 29 April 2014.

A proposition-indexed product of pointed compact sets is itself
compact. But the assumption that a proposition-indexed product of
compact sets is compact gives weak excluded middle (negative
propositions are decidable).

The definition of compactness (or exhaustive searchability) is

    compactâˆ™ A = (p : A â†’ ğŸš) â†’ Î£ aâ‚€ ê‰ A , p aâ‚€ ï¼ â‚ â†’ (a : A) â†’ p a ï¼ â‚

With excluded middle for propositions, the above claim is not
surprising, because

    (ğŸ˜ â†’ Y) = Y^ğŸ˜ â‰ƒ ğŸ™ (which is always compact),
    (ğŸ™ â†’ Y) = Y^ğŸ™ â‰ƒ Y (which is compact if Y is),

and excluded middle for a proposition X amounts to X = ğŸ˜ or X = ğŸ™, so
that

    Y^X is compact if Y is compact and X is a proposition.

The point is that

    (1) We can reach this conclusion without excluded middle.

    (2) This also holds for dependent products:

        Î  x : X , Y x is compact if X is a proposition and Y x is
        compact for every x : X.

        (This product is also written (x : X) â†’ Y x or Î  Y in Agda.)

Our Agda proof below can be written rather concisely by expanding the
definitions. We deliberately don't do that, so that we have a rigorous
informal proof side-by-side with the formal proof. We proceed in a
series of trivial steps, hopefully in the most natural way (although
we had a convoluted path to this supposedly natural way).

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.FunExt

module TypeTopology.PropTychonoff (fe : FunExt) where

open import MLTT.Two-Properties
open import TypeTopology.CompactTypes
open import UF.Base
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.PropIndexedPiSigma
open import UF.Subsingletons

\end{code}

A crucial lemma is

    prop-indexed-product : is-prop X â†’ (a : X) â†’ Î  Y â‰… Y a

This is proved in the module Prop-indexed-product. Although it has a
subtle proof, it should be intuitively clear, as X has at most one
element by hypothesis, and if the element is a:X then the product Î  Y
should be isomorphic to its only factor Y a.

With this observation, the following proof should be self-contained,
if we recall again the definition of compact set from the module
CompacTypes:

    compactâˆ™ A = (p : A â†’ ğŸš) â†’ Î£ aâ‚€ ê‰ A , p aâ‚€ ï¼ â‚ â†’ (a : A) â†’ p a ï¼ â‚

Recall also that such an aâ‚€ is called a universal witness for the predicate p.

\begin{code}

prop-tychonoff : {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ }
               â†’ is-prop X
               â†’ ((x : X) â†’ is-compactâˆ™ (Y x))
               â†’ is-compactâˆ™ (Î  Y)
prop-tychonoff {ğ“¤} {ğ“¥} {X} {Y} X-is-prop Îµ p = Î³
 where
  have : (type-of Îµ ï¼ ((x : X) â†’ is-compactâˆ™(Y x)))
       Ã— (type-of p ï¼ (Î  Y â†’ ğŸš))
  have = refl , refl

  hip : (x : X) â†’ Î  Y â‰ƒ Y x
  hip = prop-indexed-product (fe ğ“¤ ğ“¥) X-is-prop

\end{code}

The essence of the first part of the proof is this:

\begin{code}

  crude : X â†’ is-compactâˆ™ (Î  Y)
  crude x = compactâˆ™-types-are-closed-under-equiv(â‰ƒ-sym(hip x)) (Îµ x)

\end{code}

But this is very crude for our purposes (or so it seems).  So we
instead proceed as follows.

The following is what we get from prop-indexed-product, abstractly:

\begin{code}

  f : (x : X) â†’ Î  Y â†’ Y x
  f x = prâ‚ (hip x)

  hrf : (x : X) â†’ Î£ r ê‰ (Y x â†’ Î  Y), r âˆ˜ f x âˆ¼ id
  hrf x = prâ‚‚ (prâ‚‚ (hip x))

  h : (x : X) â†’ Y x â†’ Î  Y
  h x = prâ‚ (hrf x)

  hf : (x : X) (Ï† : Î  Y) â†’ h x (f x Ï†) ï¼ Ï†
  hf x = prâ‚‚ (hrf x)

\end{code}

We define a predicate q x : Y x â†’ ğŸš, for each x : X, from the
predicate p : Î  Y â†’ ğŸš via (part of) the above equivalence:

\begin{code}

  q : (x : X) â†’ Y x â†’ ğŸš
  q x y = p (h x y)

\end{code}

We argue that the following is a universal witness for the
searchability of the type Î  Y wrt the predicate p:

\begin{code}

  Ï†â‚€ : Î  Y
  Ï†â‚€ x = prâ‚ (Îµ x (q x))

\end{code}

By hypothesis, it satisfies:

\begin{code}

  Ï†â‚€-spec : (x : X) â†’ q x (Ï†â‚€ x) ï¼ â‚ â†’ (y : Y x) â†’ q x y ï¼ â‚
  Ï†â‚€-spec x = prâ‚‚ (Îµ x (q x))

\end{code}

By expanding the definitions, this amounts to:

\begin{code}

  Ï†â‚€-specâ‚€ : (x : X) â†’ p (h x (Ï†â‚€ x)) ï¼ â‚ â†’ (y : Y x) â†’ p (h x y) ï¼ â‚
  Ï†â‚€-specâ‚€ = Ï†â‚€-spec

\end{code}

By the definition of f in prop-indexed-product (namely f x Ï† = Ï† x):

\begin{code}

  Ï†â‚€-specâ‚ : (x : X) â†’ p (h x (f x Ï†â‚€)) ï¼ â‚ â†’ (y : Y x) â†’ p (h x y) ï¼ â‚
  Ï†â‚€-specâ‚ = Ï†â‚€-specâ‚€

\end{code}

(So we can't abstract away the construction of prop-indexed-product.)

In particular, with y = f x Ï†, we get:

\begin{code}

  Ï†â‚€-specâ‚-particular-case : (x : X)
                           â†’ p (h x (f x Ï†â‚€)) ï¼ â‚
                           â†’ (Ï† : Î  Y) â†’ p (h x (f x Ï†)) ï¼ â‚
  Ï†â‚€-specâ‚-particular-case x r Ï† = Ï†â‚€-specâ‚ x r (f x Ï†)

\end{code}

Using the fact that g x (f x Ï†) = Ï† for any x:X, we get:

\begin{code}

  Ï†â‚€-is-universal-witness-assuming-X : X â†’ p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚
  Ï†â‚€-is-universal-witness-assuming-X x r Ï† =
     p Ï†             ï¼âŸ¨ ap p ((hf x Ï†)â»Â¹) âŸ©
     p (h x (f x Ï†)) ï¼âŸ¨ Ï†â‚€-specâ‚-particular-case x s Ï† âŸ©
     â‚               âˆ
   where
    s = p (h x (f x Ï†â‚€)) ï¼âŸ¨ ap p (hf x Ï†â‚€) âŸ©
        p Ï†â‚€             ï¼âŸ¨ r âŸ©
        â‚                âˆ

\end{code}

Notice that the point x : X vanishes from the conclusion, and so we
are able to omit it from the hypothesis, which is crucial for what
follows.

We get the same conclusion if X is empty:

\begin{code}

  Ï†â‚€-is-universal-witness-assuming-Xâ†’ğŸ˜ : (X â†’ ğŸ˜) â†’ p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚
  Ï†â‚€-is-universal-witness-assuming-Xâ†’ğŸ˜ u r Ï† = p Ï†  ï¼âŸ¨ ap p claim âŸ©
                                               p Ï†â‚€ ï¼âŸ¨ r âŸ©
                                               â‚    âˆ

   where
    claim : Ï† ï¼ Ï†â‚€
    claim = dfunext (fe ğ“¤ ğ“¥) (Î» x â†’ unique-from-ğŸ˜ (u x))

\end{code}

So we would get what we want if we had excluded middle, because X is a
proposition and the above shows that both X and X â†’ ğŸ˜ give the desired
conclusion that Ï†â‚€ is a universal witness. But excluded middle is not
needed.

We shuffle the arguments of Ï†â‚€-is-universal-witness-assuming-X:

\begin{code}
  claimâ‚€ : p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ X â†’ p Ï† ï¼ â‚
  claimâ‚€ r Ï† x = Ï†â‚€-is-universal-witness-assuming-X x r Ï†

\end{code}

We then take the contrapositive of the conclusion X â†’ p Ï† ï¼ â‚, and
use the fact that if a point of the two-point type ğŸš is â‚€, then it is
not â‚:

\begin{code}

  Claimâ‚ : p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚€ â†’ (X â†’ ğŸ˜)
  Claimâ‚ r Ï† = contrapositive(claimâ‚€ r Ï†) âˆ˜ equal-â‚€-different-from-â‚

\end{code}

This concludes the first part of the argument.

We now shuffle the arguments of Ï†â‚€-is-universal-witness-assuming-Xâ†’ğŸ˜:

\begin{code}

  Claimâ‚‚ : p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ (X â†’ ğŸ˜) â†’ p Ï† ï¼ â‚
  Claimâ‚‚ r Ï† u = Ï†â‚€-is-universal-witness-assuming-Xâ†’ğŸ˜ u r Ï†

\end{code}

Combining the two last claims, we get:

\begin{code}

  Claimâ‚ƒ : p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚€ â†’ p Ï† ï¼ â‚
  Claimâ‚ƒ r Ï† = Claimâ‚‚ r Ï† âˆ˜ Claimâ‚ r Ï†

\end{code}

Finally, we do case analysis on the value of p Ï†:

\begin{code}

  Ï†â‚€-is-universal-witness : p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚
  Ï†â‚€-is-universal-witness r Ï† = ğŸš-equality-cases (Claimâ‚ƒ r Ï†) id

\end{code}

And we are done:

\begin{code}

  Î³ : Î£ Ï†â‚€ ê‰ Î  Y , (p Ï†â‚€ ï¼ â‚ â†’ (Ï† : Î  Y) â†’ p Ï† ï¼ â‚)
  Î³ = Ï†â‚€ , Ï†â‚€-is-universal-witness

\end{code}


TODO. 9 Sep 2015. We can generalize from X being a subsingleton (or
proposition) to X being subfinite (embedded into a finite type).

A particular case is the following:

\begin{code}

prop-tychonoff-corollary : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                         â†’ is-prop X
                         â†’ is-compactâˆ™ Y
                         â†’ is-compactâˆ™ (X â†’ Y)
prop-tychonoff-corollary X-is-prop Îµ = prop-tychonoff X-is-prop (Î» x â†’ Îµ)

\end{code}

This holds even for undecided X (such as X = LPO), or when we have no
idea whether X or (X â†’ ğŸ˜), and hence whether (X â†’ Y) is ğŸ™ or Y (or
none, if this is undecided)!

Better (9 Sep 2015):

\begin{code}

prop-tychonoff-corollary' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                          â†’ is-prop X
                          â†’ (X â†’ is-compactâˆ™ Y)
                          â†’ is-compactâˆ™ (X â†’ Y)
prop-tychonoff-corollary' = prop-tychonoff

\end{code}

So the function type (LPO â†’ â„•) is compact! (See the module LPO for a
proof.)

The Tychonoff theorem for prop-indexed products of compact types
doesn't hold. To see this, first notice that a proposition is
compact iff it is decidable. Now, the empty type ğŸ˜ is compact
(but not compactâ€Œâˆ™), and if ğŸ˜^P, that is, Â¬P, where compact for a
proposition P, this would imply that Â¬P is decidable for every
proposition P, which is weak excluded middle, which is not provable.

\begin{code}

open import UF.ExcludedMiddle

compact-prop-tychonoff-gives-WEM : ((X : ğ“¤ Ì‡ ) (Y : X â†’ ğ“¥ Ì‡ )
                                       â†’ is-prop X
                                       â†’ ((x : X) â†’ is-compact (Y x))
                                       â†’ is-compact (Î  Y))
                                 â†’ WEM ğ“¤
compact-prop-tychonoff-gives-WEM {ğ“¤} {ğ“¥} Ï„ X X-is-prop = Î´ Î³
 where
  Y : X â†’ ğ“¥ Ì‡
  Y x = ğŸ˜

  negation-compact : is-compact (X â†’ ğŸ˜ {ğ“¥})
  negation-compact = Ï„ X Y X-is-prop (Î» p â†’ ğŸ˜-compact)

  Î³ : is-decidable (X â†’ ğŸ˜ {ğ“¥})
  Î³ = compact-types-are-decidable (X â†’ ğŸ˜) negation-compact

  Î´ : is-decidable (X â†’ ğŸ˜ {ğ“¥}) â†’ is-decidable (Â¬ X)
  Î´ (inl f) = inl (ğŸ˜-elim âˆ˜ f)
  Î´ (inr Ï•) = inr (contrapositive (Î» f â†’ ğŸ˜-elim âˆ˜ f) Ï•)

\end{code}
