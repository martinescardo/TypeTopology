Martin Escardo, 20th June 2019 onwards.

The Cantor type of infinite binary sequences.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import MLTT.Two-Properties
open import Naturals.Order
open import Notation.Order
open import UF.Base
open import UF.DiscreteAndSeparated hiding (_â™¯_)
open import UF.Equiv
open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

module TypeTopology.Cantor where

Cantor = â„• â†’ ğŸš

\end{code}

We let  Î±,Î²,Î³ range  over the  Cantor type.

The constant sequences:

\begin{code}

ğŸ : Cantor
ğŸ = (i â†¦ â‚€)

ğŸ : Cantor
ğŸ = (i â†¦ â‚)

\end{code}

Cons, head and tail.

\begin{code}

head : Cantor â†’ ğŸš
head Î± = Î± 0

tail : Cantor â†’ Cantor
tail Î± = Î± âˆ˜ succ

cons : ğŸš â†’ Cantor â†’ Cantor
cons n Î± 0        = n
cons n Î± (succ i) = Î± i

_âˆ·_ : ğŸš â†’ Cantor â†’ Cantor
_âˆ·_ = cons

cons-âˆ¼ : {x : ğŸš} {Î± Î² : Cantor} â†’ Î± âˆ¼ Î² â†’ x âˆ· Î± âˆ¼ x âˆ· Î²
cons-âˆ¼ h 0        = refl
cons-âˆ¼ h (succ i) = h i

âˆ¼-cons : {x y : ğŸš} {Î± : Cantor} â†’ x ï¼ y â†’ x âˆ· Î± âˆ¼ y âˆ· Î±
âˆ¼-cons refl = âˆ¼-refl

head-cons : (n : ğŸš) (Î± : Cantor) â†’ head (cons n Î±) ï¼ n
head-cons n Î± = refl

tail-cons : (n : ğŸš) (Î± : Cantor) â†’ tail (cons n Î±) ï¼ Î±
tail-cons n Î± = refl

tail-cons' : (n : ğŸš) (Î± : Cantor) â†’ tail (cons n Î±) âˆ¼ Î±
tail-cons' n Î± i = refl

cons-head-tail : (Î± : Cantor) â†’ Î± âˆ¼ cons (head Î±) (tail Î±)
cons-head-tail Î± 0        = refl
cons-head-tail Î± (succ i) = refl

\end{code}

Agreement of two binary sequences Î± and Î² at the first n positions,
written Î± ï¼âŸ¦ n âŸ§ Î².

\begin{code}

_ï¼âŸ¦_âŸ§_ : Cantor â†’ â„• â†’ Cantor â†’ ğ“¤â‚€ Ì‡
Î± ï¼âŸ¦ 0      âŸ§ Î² = ğŸ™
Î± ï¼âŸ¦ succ n âŸ§ Î² = (head Î± ï¼ head Î²) Ã— (tail Î± ï¼âŸ¦ n âŸ§ tail Î²)

\end{code}

We have that (Î± ï¼âŸ¦ n âŸ§ Î²) iff Î± k ï¼ Î² k for all k < n:

\begin{code}

agreementâ†’ : (Î± Î² : Cantor)
             (n : â„•)
           â†’ (Î± ï¼âŸ¦ n âŸ§ Î²)
           â†’ ((k : â„•) â†’ k < n â†’ Î± k ï¼ Î² k)
agreementâ†’ Î± Î² 0        *       k        l = ğŸ˜-elim l
agreementâ†’ Î± Î² (succ n) (p , e) 0        l = p
agreementâ†’ Î± Î² (succ n) (p , e) (succ k) l = IH k l
 where
  IH : (k : â„•) â†’ k < n â†’ Î± (succ k) ï¼ Î² (succ k)
  IH = agreementâ†’ (tail Î±) (tail Î²) n e

agreementâ† : (Î± Î² : Cantor)
             (n : â„•)
           â†’ ((k : â„•) â†’ k < n â†’ Î± k ï¼ Î² k)
           â†’ (Î± ï¼âŸ¦ n âŸ§ Î²)
agreementâ† Î± Î² 0        Ï• = â‹†
agreementâ† Î± Î² (succ n) Ï• = Ï• 0 â‹† , agreementâ† (tail Î±) (tail Î²) n (Ï• âˆ˜ succ)

\end{code}

A function Cantor â†’ ğŸš is uniformly continuous if it has a modulus
of continuity:

\begin{code}

_is-a-modulus-of-uniform-continuity-of_ : â„• â†’ (Cantor â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
m is-a-modulus-of-uniform-continuity-of p = âˆ€ Î± Î² â†’ Î± ï¼âŸ¦ m âŸ§ Î² â†’ p Î± ï¼ p Î²

uniformly-continuous : (Cantor â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
uniformly-continuous p = Î£ m ê‰ â„• , m is-a-modulus-of-uniform-continuity-of p

\end{code}

Uniform continuity as defined above is data rather than property. This
is because any number bigger than a modulus of uniform continuity is
also a modulus.

TODO. Show that

 (Î£ p ê‰ (Cantor  â†’ ğŸš) , uniformly-continuous p) â‰ƒ (Î£ n ê‰ â„• , Fin (2 ^ n) â†’ ğŸš)

If we define uniform continuity with âˆƒ rather than Î£, this is no
longer the case.

\begin{code}

continuous : (Cantor â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
continuous p = âˆ€ Î± â†’ Î£ m ê‰ â„• , (âˆ€ Î² â†’ Î± ï¼âŸ¦ m âŸ§ Î² â†’ p Î± ï¼ p Î²)

\end{code}

\begin{code}

module notions-of-continuity (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 is-uniformly-continuous : (Cantor â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
 is-uniformly-continuous p = âˆƒ m ê‰ â„• , m is-a-modulus-of-uniform-continuity-of p

 is-continuous : (Cantor â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
 is-continuous p = âˆ€ Î± â†’ âˆƒ m ê‰ â„• , (âˆ€ Î² â†’ Î± ï¼âŸ¦ m âŸ§ Î² â†’ p Î± ï¼ p Î²)

\end{code}

We now define the canonical apartness relation _â™¯_ for points of the
Cantor type. Two sequences are apart if they differ at some index.

To make apartness into a proposition, which is crucial for our
purposes, we consider the minimal index at which they differ. This
allows us to avoid the assumption that propositional truncations
exist. But we still need function extensionality, so that the proof is
not in the realm of pure Martin-LÃ¶f type theory.

\begin{code}

_â™¯_ : Cantor â†’ Cantor â†’ ğ“¤â‚€ Ì‡
Î± â™¯ Î² = Î£ n ê‰ â„• , (Î± n â‰  Î² n)
                Ã— ((i : â„•) â†’ Î± i â‰  Î² i â†’ n â‰¤ i)

\end{code}

TODO. It is easy to see that this is a tight apartness relation. Maybe
implement this here. But this is not needed for our purposes.

We use Î´ to range over the type Î± n â‰  Î² n, and Î¼ to range over the
minimality condition (i : â„•) â†’ Î± i â‰  Î² i â†’ n â‰¤ i, for Î±, Î² and n
suitably specialized according to the situation we are considering.
We also use the letter "a" to range over the apartness type Î± â™¯ Î².

As claimed above, the apartness relation is proposition-valued.

\begin{code}

â™¯-is-prop-valued : Fun-Ext â†’ (Î± Î² : Cantor) â†’ is-prop (Î± â™¯ Î²)
â™¯-is-prop-valued fe Î± Î² (n , Î´ , Î¼) (n' , Î´' , Î¼') = III
 where
  I : (n : â„•) â†’ is-prop ((Î± n â‰  Î² n) Ã— ((i : â„•) â†’ Î± i â‰  Î² i â†’ n â‰¤ i))
  I n = Ã—-is-prop
         (negations-are-props fe)
         (Î â‚‚-is-prop fe Î» i _ â†’ â‰¤-is-prop-valued n i)

  II : n ï¼ n'
  II = â‰¤-anti n n' (Î¼ n' Î´') (Î¼' n Î´)

  III : (n , Î´ , Î¼) ï¼[ Î± â™¯ Î² ] (n' , Î´' , Î¼')
  III = to-subtype-ï¼ I II

\end{code}

If two sequences Î± and Î² are apart, they agree before the apartness index n.

\begin{code}

â™¯-agreement : (Î± Î² : Cantor)
              ((n , Î´ , Î¼) : Î± â™¯ Î²)
              (i : â„•)
            â†’ i < n â†’ Î± i ï¼ Î² i
â™¯-agreement Î± Î² (n , _ , Î¼) i â„“ = IV
 where
  I : Î± i â‰  Î² i â†’ n â‰¤ i
  I = Î¼ i

  II : Â¬ (n â‰¤ i) â†’ Â¬ (Î± i â‰  Î² i)
  II = contrapositive I

  III : Â¬ (n â‰¤ i)
  III = less-not-bigger-or-equal i n â„“

  IV : Î± i ï¼ Î² i
  IV = ğŸš-is-Â¬Â¬-separated (Î± i) (Î² i) (II III)

\end{code}

The Cantor type is homogeneous.

\begin{code}

module _ (fe : Fun-Ext) (Î± Î² : Cantor) where

 Cantor-swap : Cantor â†’ Cantor
 Cantor-swap Î³ i = (Î² i âŠ• Î± i) âŠ• Î³ i

 Cantor-swap-involutive : Cantor-swap âˆ˜ Cantor-swap âˆ¼ id
 Cantor-swap-involutive Î³ = dfunext fe (Î» i â†’ âŠ•-involutive {Î² i âŠ• Î± i} {Î³ i})

 Cantor-swap-swapsâˆ¼ : Cantor-swap Î± âˆ¼ Î²
 Cantor-swap-swapsâˆ¼ i =
  Cantor-swap Î± i   ï¼âŸ¨ refl âŸ©
  (Î² i âŠ• Î± i) âŠ• Î± i ï¼âŸ¨ âŠ•-assoc {Î² i} {Î± i} {Î± i} âŸ©
  Î² i âŠ• (Î± i âŠ• Î± i) ï¼âŸ¨ ap (Î² i âŠ•_) (Lemma[bâŠ•bï¼â‚€] {Î± i}) âŸ©
  Î² i âŠ• â‚€           ï¼âŸ¨ âŠ•-â‚€-right-neutral  âŸ©
  Î² i               âˆ

 Cantor-swap-swaps : Cantor-swap Î± ï¼ Î²
 Cantor-swap-swaps = dfunext fe Cantor-swap-swapsâˆ¼

 Cantor-swap-swaps' : Cantor-swap Î² ï¼ Î±
 Cantor-swap-swaps' = involution-swap
                       Cantor-swap
                       Cantor-swap-involutive
                       Cantor-swap-swaps

 Cantor-swap-â‰ƒ : Cantor â‰ƒ Cantor
 Cantor-swap-â‰ƒ = Cantor-swap ,
                 involutions-are-equivs Cantor-swap Cantor-swap-involutive

Cantor-homogeneous : Fun-Ext
                   â†’ (Î± Î² : Cantor)
                   â†’ Î£ f ê‰ Cantor â‰ƒ Cantor , (âŒœ f âŒ Î± ï¼ Î²)
Cantor-homogeneous fe Î± Î² = Cantor-swap-â‰ƒ fe Î± Î² , Cantor-swap-swaps fe Î± Î²

\end{code}
