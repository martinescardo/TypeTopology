Martin Escardo, 7 May 2014.

For any function f : â„•âˆ â†’ â„•, it is decidable whether f is non-continuous.

  Î  (f : â„•âˆ â†’ â„•). Â¬ (continuous f) + Â¬Â¬ (continuous f).

Based on the paper

    Mathematical Structures in Computer Science , Volume 25,
    October 2015 , pp. 1578 - 1589
    DOI: https://doi.org/10.1017/S096012951300042X

The title of this paper is a bit misleading. It should have been
called "Decidability of non-continuity".

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt

module TypeTopology.DecidabilityOfNonContinuity (fe : funext ğ“¤â‚€ ğ“¤â‚€) where

open import CoNaturals.Type
open import MLTT.Two-Properties
open import Notation.CanonicalMap
open import NotionsOfDecidability.Decidable
open import TypeTopology.ADecidableQuantificationOverTheNaturals fe
open import UF.DiscreteAndSeparated

Lemma-3Â·1 : (q : â„•âˆ â†’ â„•âˆ â†’ ğŸš)
          â†’ is-decidable ((m : â„•) â†’ Â¬ ((n : â„•) â†’ q (Î¹ m) (Î¹ n) ï¼ â‚))
Lemma-3Â·1 q = claimâ‚„
 where
  A : â„•âˆ â†’ ğ“¤â‚€ Ì‡
  A u = (n : â„•) â†’ q u (Î¹ n) ï¼ â‚

  claimâ‚€ :  (u : â„•âˆ) â†’ is-decidable (A u)
  claimâ‚€ u = Theorem-8Â·2 (q u)

  p : â„•âˆ â†’ ğŸš
  p = indicator-map claimâ‚€

  claimâ‚ : is-decidable ((n : â„•) â†’ p (Î¹ n) ï¼ â‚)
  claimâ‚ = Theorem-8Â·2 p

  claimâ‚‚ : ((n : â„•) â†’ Â¬ A (Î¹ n)) â†’ (n : â„•) â†’ p (Î¹ n) ï¼ â‚
  claimâ‚‚ Ï† n = different-from-â‚€-equal-â‚
                (Î» v â†’ Ï† n (indicator-propertyâ‚€ claimâ‚€ (Î¹ n) v))

  claimâ‚ƒ : ((n : â„•) â†’ p (Î¹ n) ï¼ â‚) â†’ (n : â„•) â†’ Â¬ A (Î¹ n)
  claimâ‚ƒ f n = indicator-propertyâ‚ claimâ‚€ (Î¹ n) (f n)

  claimâ‚„ : is-decidable ((n : â„•) â†’ Â¬ (A (Î¹ n)))
  claimâ‚„ = map-decidable claimâ‚ƒ claimâ‚‚ claimâ‚

\end{code}

Omitting the inclusion function, or coercion,

   Î¹ : â„• â†’ â„•âˆ,

a map f : â„•âˆ â†’ â„• is called continuous iff

   âˆƒ m. âˆ€ n â‰¥ m. f n ï¼ âˆ,

where m and n range over the natural numbers.

The negation of this statement is equivalent to

   âˆ€ m. Â¬ âˆ€ n â‰¥ m. f n ï¼ âˆ.

We can implement âˆ€ y â‰¥ x. A y as âˆ€ x. A (max x y), so that the
continuity of f amounts to

   âˆƒ m. âˆ€ n. f (max m n) ï¼ âˆ,

and its negation to

   âˆ€ m. Â¬ âˆ€ n. f (max m n) ï¼ âˆ.

\begin{code}

continuous : (â„•âˆ â†’ â„•) â†’ ğ“¤â‚€ Ì‡
continuous f = Î£ m ê‰ â„• , ((n : â„•) â†’ f (max (Î¹ m) (Î¹ n)) ï¼ f âˆ)

Theorem-3Â·2 : (f : â„•âˆ â†’ â„•) â†’ is-decidable (Â¬ continuous f)
Theorem-3Â·2 f = V
 where
  ncf : ğ“¤â‚€ Ì‡
  ncf = (m : â„•) â†’ Â¬ ((n : â„•) â†’ f (max (Î¹ m) (Î¹ n)) ï¼[â„•] f âˆ)

  I : ncf â†’ Â¬ continuous f
  I Î½ (m , a) = Î½ m (Î» n â†’ lr-implication
                            (ï¼-agrees-with-ï¼[â„•]
                              (f (max (Î¹ m) (Î¹ n)))
                              (f âˆ))
                            (a n))

  II : Â¬ continuous f â†’ ncf
  II Î½ m a = Î½ (m , (Î» n â†’ rl-implication
                            (ï¼-agrees-with-ï¼[â„•]
                               (f (max (Î¹ m) (Î¹ n)))
                               (f âˆ))
                            (a n)))

  III : is-decidable ncf
  III = Lemma-3Â·1 (Î» x y â†’ Ï‡ï¼ (f (max x y)) (f âˆ))

  V : is-decidable (Â¬ continuous f)
  V = map-decidable I II III

\end{code}

(Maybe) to be continued (see the paper for the moment).

 * MP gives that continuity and doubly negated continuity agree.

 * WLPO is equivalent to the existence of a noncontinuous function â„•âˆ â†’ â„•.

 * Â¬WLPO is equivalent to the doubly negated continuity of all functions â„•âˆ â†’ â„•.

 * If MP and Â¬WLPO then all functions â„•âˆ â†’ â„• are continuous.
