Martin Escardo, 17th July 2024.

Sequentially Hausdorff types.

Motivated by https://grossack.site/2024/07/03/life-in-johnstones-topological-topos

The idea in this file, and many files in TypeTopology, is that we are
working in an arbitrary topos, which may or may not be a topological
topos, and we are interested in proving things synthetically in
topological toposes.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module TypeTopology.SequentiallyHausdorff
        (feâ‚€ : funextâ‚€)
       where

open import CoNaturals.Type
open import MLTT.Spartan
open import Notation.CanonicalMap
open import Taboos.BasicDiscontinuity feâ‚€
open import Taboos.WLPO

\end{code}

A topological space is sequentially Hausdorff if every sequence
converges to at most one point. In our synthetic setting, this can be
formulated as follows, following the above blog post by Chris
Grossack.

\begin{code}

is-sequentially-Hausdorff : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-sequentially-Hausdorff X = (f g : â„•âˆ â†’ X)
                            â†’ ((n : â„•) â†’ f (Î¹ n) ï¼ g (Î¹ n))
                            â†’ f âˆ ï¼ g âˆ

\end{code}

If WLPO holds in our topos, then our topos is not topological, in any
conceivable sense, and no type with two distinct points is
sequentially Hausdorff.

\begin{code}

no-non-trivial-sequentially-Hausdorff-types-under-WLPO
 : WLPO
 â†’ (X : ğ“¤ Ì‡ )
 â†’ has-two-distinct-points X
 â†’ Â¬ is-sequentially-Hausdorff X
no-non-trivial-sequentially-Hausdorff-types-under-WLPO
 wlpo X ((xâ‚€ , xâ‚), d) X-is-seq-Haus = III
 where
  f : â„•âˆ â†’ X
  f u = xâ‚€

  g' : (u : â„•âˆ) â†’ (u ï¼ âˆ) + (u â‰  âˆ) â†’ X
  g' u (inl _) = xâ‚
  g' u (inr _) = xâ‚€

  g : â„•âˆ â†’ X
  g u = g' u (wlpo u)

  I : (n : â„•) (c : (Î¹ n ï¼ âˆ) + (Î¹ n â‰  âˆ)) â†’ g' (Î¹ n) c ï¼ xâ‚€
  I n (inl e) = ğŸ˜-elim (âˆ-is-not-finite n (e â»Â¹))
  I n (inr _) = refl

  a : (n : â„•) â†’  f (Î¹ n) ï¼ g (Î¹ n)
  a n =  f (Î¹ n) ï¼âŸ¨ refl âŸ©
         xâ‚€      ï¼âŸ¨ (I n (wlpo (Î¹ n)))â»Â¹ âŸ©
         g (Î¹ n) âˆ

  II : (c : (âˆ ï¼ âˆ) + (âˆ â‰  âˆ)) â†’ g' âˆ c ï¼ xâ‚
  II (inl _) = refl
  II (inr Î½) = ğŸ˜-elim (Î½ refl)

  III : ğŸ˜
  III = d (xâ‚€  ï¼âŸ¨ refl âŸ©
           f âˆ ï¼âŸ¨ X-is-seq-Haus f g a âŸ©
           g âˆ ï¼âŸ¨ II (wlpo âˆ) âŸ©
           xâ‚  âˆ)

\end{code}

The reason WLPO doesn't hold topological in toposes is that the
negation of WLPO is a weak continuity principle [1], which holds in
topological toposes. So it makes sense to investigate which sets are
sequentially Hausdorff assuming Â¬ WLPO or stronger continuity
principles.

[1] https://doi.org/10.1017/S096012951300042X

To begin with, we show that all totally separated types are
sequentially Hausdorff in topological toposes.

\begin{code}

open import TypeTopology.TotallySeparated
open import UF.DiscreteAndSeparated

totally-separated-types-are-sequentially-Hausdorff
 : Â¬ WLPO
 â†’ (X : ğ“¤ Ì‡ )
 â†’ is-totally-separated X
 â†’ is-sequentially-Hausdorff X
totally-separated-types-are-sequentially-Hausdorff nwlpo X X-is-ts f g a = II
 where
  I : (p : X â†’ ğŸš) â†’ p (f âˆ) ï¼ p (g âˆ)
  I p = Iâ‚ƒ
   where
    Iâ‚€ : (n : â„•) â†’ p (f (Î¹ n)) ï¼ p (g (Î¹ n))
    Iâ‚€ n = ap p (a n)

    Iâ‚ : p (f âˆ) â‰  p (g âˆ) â†’ WLPO
    Iâ‚ = disagreement-taboo (p âˆ˜ f) (p âˆ˜ g) Iâ‚€

    Iâ‚‚ : Â¬ (p (f âˆ) â‰  p (g âˆ))
    Iâ‚‚ = contrapositive Iâ‚ nwlpo

    Iâ‚ƒ : p (f âˆ) ï¼ p (g âˆ)
    Iâ‚ƒ = ğŸš-is-Â¬Â¬-separated (p (f âˆ)) (p (g âˆ)) Iâ‚‚

  II : f âˆ ï¼ g âˆ
  II = X-is-ts I

\end{code}

There are plenty of totally separated types. For example, the types ğŸš,
â„• and â„•âˆ are totally separated, and the totally separated types are
closed under products (and hence function spaces and more generally
form an exponential ideal) and under retracts, as proved in the above
import TypeTopology.TotallySeparated.

And here is an example of a non-sequentially Hausdorff space, which
was originally offered in the following imported module as an example
of a type which is not totally separated in general. This is the type

    â„•âˆâ‚‚ = Î£ u ê‰ â„•âˆ , (u ï¼ âˆ â†’ ğŸš),

which amounts to â„•âˆ with the point âˆ split into two copies

    âˆâ‚€ = (âˆ , Î» _ â†’ â‚€),
    âˆâ‚ = (âˆ , Î» _ â†’ â‚).

\begin{code}

open import TypeTopology.FailureOfTotalSeparatedness feâ‚€

â„•âˆâ‚‚-is-not-sequentially-Hausdorff : Â¬ is-sequentially-Hausdorff â„•âˆâ‚‚
â„•âˆâ‚‚-is-not-sequentially-Hausdorff h = III
 where
  f g : â„•âˆ â†’ â„•âˆâ‚‚
  f u = u , Î» (e : u ï¼ âˆ) â†’ â‚€
  g u = u , Î» (e : u ï¼ âˆ) â†’ â‚

  I : (n : â„•) â†’ (Î» (e : Î¹ n ï¼ âˆ) â†’ â‚€) ï¼ (Î» (e : Î¹ n ï¼ âˆ) â†’ â‚)
  I n = dfunext feâ‚€ (Î» (e : Î¹ n ï¼ âˆ) â†’ ğŸ˜-elim (âˆ-is-not-finite n (e â»Â¹)))

  a : (n : â„•) â†’ f (Î¹ n) ï¼ g (Î¹ n)
  a n = ap (Î¹ n ,_) (I n)

  II : âˆâ‚€ ï¼ âˆâ‚
  II = h f g a

  III : ğŸ˜
  III = âˆâ‚€-and-âˆâ‚-different II

\end{code}

The following was already proved in TypeTopology.FailureOfTotalSeparatedness.

\begin{code}

â„•âˆâ‚‚-is-not-totally-separated-in-topological-toposes
 : Â¬ WLPO
 â†’ Â¬ is-totally-separated â„•âˆâ‚‚
â„•âˆâ‚‚-is-not-totally-separated-in-topological-toposes nwlpo ts =
 â„•âˆâ‚‚-is-not-sequentially-Hausdorff
  (totally-separated-types-are-sequentially-Hausdorff nwlpo â„•âˆâ‚‚ ts)

\end{code}

The proof given here is the same, but factored in two steps, by
considering sequentially Hausdorff spaces as an intermediate step.
