Martin Escardo, 20 Feb 2012.

We give a negative answer to a question posed by Altenkirch, AnberrÃ©e
and Li.

They asked whether for every definable type X in Martin-LÃ¶f type
theory, it is the case that for any two provably distinct elements
xâ‚€,xâ‚:X, there is a function p:Xâ†’ğŸš and a proof d: p xâ‚€ â‰  p xâ‚. Here ğŸš
is the type of binary numerals, or booleans if you like, but I am not
telling you which of â‚€ and â‚ is to be regarded as true or false.

If one thinks of ğŸš-valued maps as characteristic functions of clopen
sets in a topological view of types, then their question amounts to
asking whether the definable types are totally separated, that is,
whether the clopens separate the points. See Johnstone's book "Stone
Spaces" for some information about this notion - e.g. for compact
spaces, it agrees with total disconnectedness (the connected
components are the singletons) and zero-dimensionality (the clopens
form a base of the topology), but in general the three notions don't
agree.

We give an example of a type X whose total separatedness implies a
constructive taboo. The proof works by constructing two elements xâ‚€
and xâ‚ of X, and a discontinuous function â„•âˆâ†’ğŸš from any hypothetical
p:Xâ†’ğŸš with p xâ‚€ â‰  p xâ‚, and then reducing discontinuity to WLPO.

Our proof assumes function extensionality. Without the assumption
there are fewer closed terms of type Xâ†’ğŸš, and their question was for
closed terms X, xâ‚€,xâ‚:X, and d:xâ‚€â‰ xâ‚, and so the negative answer also
works in the absence of function extensionality. But assuming function
extensionality we get a stronger result, which is not restricted to
closed terms, and which is a theorem rather than a metatheorem.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module TypeTopology.FailureOfTotalSeparatedness (fe : FunExt) where

open import MLTT.Spartan

open import MLTT.Two-Properties
open import CoNaturals.GenericConvergentSequence
open import Taboos.BasicDiscontinuity (fe ğ“¤â‚€ ğ“¤â‚€)
open import Taboos.WLPO
open import UF.Base
open import Notation.CanonicalMap

\end{code}

The idea of the following construction is to replace âˆ in â„•âˆ by two
copies âˆâ‚€ and âˆâ‚, which are different but not distinguishable by maps
into ğŸš, unless WLPO holds. (We can use the Cantor space (â„•â†’ğŸš) or the
Baire space (â„•â†’â„•), or many other types instead of â„•âˆ, with âˆ replaced
by any fixed element. But I think the proposed construction gives a
more transparent and conceptual argument.)

\begin{code}

module concrete-example where

 X : ğ“¤â‚€ Ì‡
 X = Î£ u ê‰ â„•âˆ , (u ï¼ âˆ â†’ ğŸš)

 âˆâ‚€ : X
 âˆâ‚€ = (âˆ , Î» r â†’ â‚€)

 âˆâ‚ : X
 âˆâ‚ = (âˆ , Î» r â†’ â‚)

\end{code}

 The elements âˆâ‚€ and âˆâ‚ look different:

\begin{code}

 naive : (prâ‚‚ âˆâ‚€ refl ï¼ â‚€)  Ã—  (prâ‚‚ âˆâ‚ refl ï¼ â‚)
 naive = refl , refl

\end{code}

 But there is no function p : X â†’ ğŸš such that p x = prâ‚‚ x refl, because
 prâ‚ x may be different from âˆ, in which case prâ‚‚ x is the function with
 empty graph, and so it can't be applied to anything, and certainly
 not to refl. In fact, the definition

    p : X â†’ ğŸš
    p x = prâ‚‚ x refl

 doesn't type check (Agda says: " (prâ‚ (prâ‚ x) x) != â‚ of type ğŸš when
 checking that the expression refl has type prâ‚ x ï¼ âˆ"), and hence we
 haven't distinguished âˆâ‚€ and âˆâ‚ by applying the same function to
 them. This is clearly seen when enough implicit arguments are made
 explicit.

 No matter how hard we try to find such a function, we won't succeed,
 because we know that WLPO is not provable:

\begin{code}

 failure : (p : X â†’ ğŸš) â†’ p âˆâ‚€ â‰  p âˆâ‚ â†’ WLPO
 failure p = disagreement-taboo pâ‚€ pâ‚ lemma
  where
   pâ‚€ : â„•âˆ â†’ ğŸš
   pâ‚€ u = p (u , Î» r â†’ â‚€)

   pâ‚ : â„•âˆ â†’ ğŸš
   pâ‚ u = p (u , Î» r â†’ â‚)

   lemma : (n : â„•) â†’ pâ‚€ (Î¹ n) ï¼ pâ‚ (Î¹ n)
   lemma n = ap (Î» - â†’ p (Î¹ n , -)) (dfunext (fe ğ“¤â‚€ ğ“¤â‚€) claim)
    where
     claim : (r : Î¹ n ï¼ âˆ) â†’ (Î» r â†’ â‚€) r ï¼ (Î» r â†’ â‚) r
     claim s = ğŸ˜-elim (âˆ-is-not-finite n (s â»Â¹))

 open import UF.DiscreteAndSeparated

 ğŸš-indistinguishability : Â¬ WLPO â†’ (p : X â†’ ğŸš) â†’ p âˆâ‚€ ï¼ p âˆâ‚
 ğŸš-indistinguishability nwlpo p = ğŸš-is-Â¬Â¬-separated (p âˆâ‚€) (p âˆâ‚)
                                   (not-Î£-implies-Î -not
                                   (contrapositive (Î» Ïƒ â†’ failure (prâ‚ Ïƒ) (prâ‚‚ Ïƒ)) nwlpo) p)
\end{code}

 Precisely because one cannot construct maps from X into ğŸš that
 distinguish âˆâ‚€ and âˆâ‚, it is a bit tricky to prove that they are
 indeed different:

\begin{code}

 âˆâ‚€-and-âˆâ‚-different : âˆâ‚€ â‰  âˆâ‚
 âˆâ‚€-and-âˆâ‚-different r = zero-is-not-one claimâ‚‚
  where
   p : âˆ ï¼ âˆ
   p = ap prâ‚ r

   t : {x x' : â„•âˆ} â†’ x ï¼ x' â†’ (x ï¼ âˆ â†’ ğŸš) â†’ (x' ï¼ âˆ â†’ ğŸš)
   t = transport (Î» - â†’ - ï¼ âˆ â†’ ğŸš)

   claimâ‚€ : refl ï¼ p
   claimâ‚€ = â„•âˆ-is-set (fe ğ“¤â‚€ ğ“¤â‚€) refl p

   claimâ‚ : t p (Î» p â†’ â‚€) ï¼ (Î» p â†’ â‚)
   claimâ‚ = from-Î£-ï¼' r

   claimâ‚‚ : â‚€ ï¼ â‚
   claimâ‚‚ =  â‚€                  ï¼âŸ¨ ap (Î» - â†’ t - (Î» _ â†’ â‚€) refl) claimâ‚€ âŸ©
             t p (Î» _ â†’ â‚€) refl ï¼âŸ¨ ap (Î» - â†’ - refl) claimâ‚ âŸ©
             â‚                  âˆ

\end{code}

 Finally, the total separatedness of X is a taboo. In particular, it
 can't be proved, because Â¬WLPO is consistent.

\begin{code}

 open import TypeTopology.TotallySeparated

 Failure : is-totally-separated X â†’ Â¬Â¬ WLPO
 Failure ts nwlpo = g (ğŸš-indistinguishability nwlpo)
  where
   g : Â¬ ((p : X â†’ ğŸš) â†’ p âˆâ‚€ ï¼ p âˆâ‚)
   g = contrapositive ts âˆâ‚€-and-âˆâ‚-different

\end{code}

We can generalize this as follows, without using â„•âˆ.

From an arbitrary given type X and distinguished element a : X, we
construct a new type Y, which will fail to be totally separated unless
the point a is weakly isolated. The idea is to "explode" the point a
into two different copies, which cannot be distinguished unless point
a is weakly isolated, and keep all the other original points
unchanged.

\begin{code}

module general-example (ğ“¤ : Universe) (X : ğ“¤ Ì‡ ) (a : X) where

 Y : ğ“¤ Ì‡
 Y = Î£ x ê‰ X , (x ï¼ a â†’ ğŸš)

 e : ğŸš â†’ X â†’ Y
 e n x = (x , Î» p â†’ n)

 aâ‚€ : Y
 aâ‚€ = e â‚€ a

 aâ‚ : Y
 aâ‚ = e â‚ a

 Proposition : aâ‚€ â‰  aâ‚
 Proposition r = zero-is-not-one zero-is-one
  where
   P : Y â†’ ğ“¤ Ì‡
   P (x , f) = Î£ q ê‰ x ï¼ a , f q ï¼ â‚

   observationâ‚€ : P aâ‚€ ï¼ (a ï¼ a) Ã— (â‚€ ï¼ â‚)
   observationâ‚€ = refl

   observationâ‚ : P aâ‚ ï¼ (a ï¼ a) Ã— (â‚ ï¼ â‚)
   observationâ‚ = refl

   t : P aâ‚ â†’ P aâ‚€
   t = transport P (r â»Â¹)

   pâ‚ : P aâ‚
   pâ‚ = refl , refl

   pâ‚€ : P aâ‚€
   pâ‚€ = t pâ‚

   zero-is-one : â‚€ ï¼ â‚
   zero-is-one = prâ‚‚ pâ‚€

\end{code}

Points different from the point a are mapped to the same point by the
two embeddings eâ‚€ and eâ‚:

\begin{code}

 Lemma : (x : X) â†’ x â‰  a â†’ e â‚€ x ï¼ e â‚ x
 Lemma x Ï† = ap (Î» - â†’ (x , -)) claim
  where
   claim : (Î» p â†’ â‚€) ï¼ (Î» p â†’ â‚)
   claim = dfunext (fe ğ“¤ ğ“¤â‚€) (Î» p â†’ ğŸ˜-elim (Ï† p))

\end{code}

The following theorem shows that, because not every type X has
decidable equality, the points aâ‚€,aâ‚ of Y cannot necessarily be
distinguished by maps into the discrete set ğŸš. To get the desired
conclusion, it is enough to consider X = (â„• â†’ ğŸš), which is
Â¬Â¬-separated, in the sense that Â¬Â¬ (x ï¼ y) â†’ x ï¼ y, assuming
extensionality. (Cf. the module DiscreteAndSeparated.)

\begin{code}

 weakly-isolated : {X : ğ“¤ Ì‡ } (x : X) â†’ ğ“¤ Ì‡
 weakly-isolated x = âˆ€ x' â†’ is-decidable (x' â‰  x)

 Theorem : (Î£ g ê‰ (Y â†’ ğŸš), g aâ‚€ â‰  g aâ‚) â†’ weakly-isolated a
 Theorem (g , d) x = ğŸš-equality-cases' (claimâ‚€' x) (claimâ‚' x)
  where
   f : X â†’ ğŸš
   f x = g (e â‚€ x) âŠ• g (e â‚ x)

   claimâ‚€ : f a ï¼ â‚
   claimâ‚€ = Lemma[bâ‰ câ†’bâŠ•cï¼â‚] d

   claimâ‚ : (x : X) â†’ x â‰  a â†’ f x ï¼ â‚€
   claimâ‚ x Ï† = Lemma[bï¼câ†’bâŠ•cï¼â‚€] (ap g (Lemma x Ï†))

   claimâ‚€' : (x : X) â†’ f x ï¼ â‚€ â†’ x â‰  a
   claimâ‚€' x p r = ğŸ˜-elim (equal-â‚€-different-from-â‚ fact claimâ‚€)
    where
     fact : f a ï¼ â‚€
     fact = ap f (r â»Â¹) âˆ™ p

   claimâ‚' : (x : X) â†’ f x ï¼ â‚ â†’ Â¬ (x â‰  a)
   claimâ‚' x p Ï† = ğŸ˜-elim (equal-â‚€-different-from-â‚ fact p)
    where
     fact : f x ï¼ â‚€
     fact = claimâ‚ x Ï†

\end{code}
