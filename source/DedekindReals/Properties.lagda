Andrew Sneap, April 2021 - January 2022

In this file, I prove that the Reals are arithmetically located.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan renaming (_+_ to _âˆ”_)

open import Notation.Order
open import UF.Base
open import UF.PropTrunc
open import UF.FunExt
open import UF.Powerset
open import UF.Subsingletons
open import Naturals.Properties
open import Naturals.Order
open import Rationals.Type
open import Rationals.Abs
open import Rationals.Addition
open import Rationals.Multiplication
open import Rationals.Negation
open import Rationals.Order
open import Rationals.Positive hiding (_*_)

module DedekindReals.Properties
  (fe : Fun-Ext)
  (pe : Prop-Ext)
  (pt : propositional-truncations-exist)
 where
open import DedekindReals.Type fe pe pt
open import MetricSpaces.Rationals fe pe pt
open import Rationals.Limits fe pe pt

open PropositionalTruncation pt

transâ†’disjoint : (L R : ğ“Ÿ â„š) â†’ disjoint L R â†’ (q : â„š) â†’ Â¬ (q âˆˆ L Ã— q âˆˆ R)
transâ†’disjoint L R d q (qâˆˆL , qâˆˆR) = â„š<-irrefl q Î³
 where
  Î³ : q < q
  Î³ = d q q (qâˆˆL , qâˆˆR)

disjointâ†’trans : (L R : ğ“Ÿ â„š)
               â†’ located L R â†’ ((q : â„š)
               â†’ Â¬ (q âˆˆ L Ã— q âˆˆ R))
               â†’ disjoint L R
disjointâ†’trans L R loc dis p q (pâˆˆL , qâˆˆR) = casesâ‚ƒ Î³â‚ Î³â‚‚ Î³â‚ƒ I
 where
  I : (p < q) âˆ” (p ï¼ q) âˆ” (q < p)
  I = â„š-trichotomous p q

  Î³â‚ : p < q â†’ p < q
  Î³â‚ = id

  Î³â‚‚ : p ï¼ q â†’ p < q
  Î³â‚‚ e = ğŸ˜-elim (dis p (pâˆˆL , pâˆˆR))
   where
    pâˆˆR : p âˆˆ R
    pâˆˆR = transport (_âˆˆ R) (e â»Â¹) qâˆˆR

  Î³â‚ƒ : q < p â†’ p < q
  Î³â‚ƒ l = ğŸ˜-elim (âˆ¥âˆ¥-rec ğŸ˜-is-prop Î³ II)
   where
    II : (q âˆˆ L) âˆ¨ (p âˆˆ R)
    II = loc q p l

    Î³ : Â¬ ((q âˆˆ L) âˆ” (p âˆˆ R))
    Î³ (inl qâˆˆL) = dis q (qâˆˆL , qâˆˆR)
    Î³ (inr pâˆˆR) = dis p (pâˆˆL , pâˆˆR)

â„-arithmetically-located : (x : â„)
                         â†’ (Îµ : â„šâ‚Š)
                         â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
â„-arithmetically-located x Îµâ‚Š@(Îµ , 0<Îµ) = âˆ¥âˆ¥-rec âˆƒ-is-prop Î³ I
 where
  I : âˆ¥ (Î£ p ê‰ â„š , p < x) Ã— (Î£ q ê‰ â„š , x < q) âˆ¥
  I = binary-choice (inhabited-from-real-L x) (inhabited-from-real-R x)

  Î³ : (Î£ p ê‰ â„š , p < x) Ã— (Î£ q ê‰ â„š , x < q)
    â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
  Î³ ((p , p<x) , (q , x<q)) = Î³â‚ n p q p<x x<q II lâ‚‚
   where
    p<q : p < q
    p<q = disjoint-from-real x p q (p<x , x<q)

    lâ‚ : 0â„š < q - p
    lâ‚ = â„š<-difference-positive p q p<q

    II : _
    II = trisect p q p<q

    III : Î£ n ê‰ â„• , (âŸ¨2/3âŸ©^ n) * (q - p) < Îµâ‚Š
    III = âŸ¨2/3âŸ©^n<Îµ-consequence (Îµ , 0<Îµ) (q - p , lâ‚)
    n = prâ‚ III
    lâ‚‚ = prâ‚‚ III

    Î³â‚ : (n : â„•) â†’ (p q : â„š) â†’ p < x â†’ x < q
       â†’ Î£ (u , v) ê‰ â„š Ã— â„š , (p < u) Ã— (u < v) Ã— (v < q)
                           Ã— (q - u ï¼ 2/3 * (q - p))
                           Ã— (v - p ï¼ 2/3 * (q - p))
       â†’ (âŸ¨2/3âŸ©^ n) * (q - p) < Îµ
       â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
    Î³â‚ 0 p q p<x x<q ((u , v) , p<u , u<v , v<q , eâ‚ , eâ‚‚) l
     = âˆ£ (p , q) , (p<x , x<q) , (transport (_< Îµ) eâ‚ƒ l) âˆ£
     where
      eâ‚ƒ : 1â„š * (q - p) ï¼ q - p
      eâ‚ƒ = â„š-mult-left-id (q - p)
    Î³â‚ (succ n) p q p<x x<q ((u , v) , p<u , u<v , v<q , eâ‚ , eâ‚‚) l
     = âˆ¨-elim âˆƒ-is-prop Î³â‚‚ Î³â‚ƒ IV
     where
      IV : (u < x) âˆ¨ (x < v)
      IV = â„-locatedness x u v u<v

      Î³â‚‚ : (u < x) â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
      Î³â‚‚ u<x = Î³â‚ n u q u<x x<q V (transport (_< Îµ) Î± l)
       where
        u<q : u < q
        u<q = disjoint-from-real x u q (u<x , x<q)

        V : _
        V = trisect u q u<q

        Î± : (âŸ¨2/3âŸ©^ succ n) * (q - p) ï¼ (âŸ¨2/3âŸ©^ n) * (q - u)
        Î± = (âŸ¨2/3âŸ©^ succ n) * (q - p)    ï¼âŸ¨ ap (_* (q - p)) (âŸ¨2/3âŸ©-to-mult n) âŸ©
            (âŸ¨2/3âŸ©^ n) * 2/3 * (q - p)   ï¼âŸ¨ â„š*-assoc (âŸ¨2/3âŸ©^ n) 2/3 (q - p)   âŸ©
            (âŸ¨2/3âŸ©^ n) * (2/3 * (q - p)) ï¼âŸ¨ ap ((âŸ¨2/3âŸ©^ n) *_) (eâ‚ â»Â¹)        âŸ©
            (âŸ¨2/3âŸ©^ n) * (q - u)         âˆ

      Î³â‚ƒ : (x < v) â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
      Î³â‚ƒ x<v = Î³â‚ n p v p<x x<v V (transport (_< Îµ) Î± l)
       where
        p<v : p < v
        p<v = disjoint-from-real x p v (p<x , x<v)

        V : _
        V = trisect p v p<v

        Î± : (âŸ¨2/3âŸ©^ succ n) * (q - p) ï¼ (âŸ¨2/3âŸ©^ n) * (v - p)
        Î± = (âŸ¨2/3âŸ©^ succ n) * (q - p)    ï¼âŸ¨ ap (_* (q - p)) (âŸ¨2/3âŸ©-to-mult n) âŸ©
            (âŸ¨2/3âŸ©^ n) * 2/3 * (q - p)   ï¼âŸ¨ â„š*-assoc (âŸ¨2/3âŸ©^ n) 2/3 (q - p)   âŸ©
            (âŸ¨2/3âŸ©^ n) * (2/3 * (q - p)) ï¼âŸ¨ ap ((âŸ¨2/3âŸ©^ n) *_) (eâ‚‚ â»Â¹)        âŸ©
            (âŸ¨2/3âŸ©^ n) * (v - p)         âˆ

â„-arithmetically-located' : (x : â„)
                          â†’ ((Îµ , _) : â„šâ‚Š)
                          â†’ âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (0â„š < (v - u) < Îµ)
â„-arithmetically-located' x (Îµ , 0<Îµ) = âˆ¥âˆ¥-functor Î³â‚‚ Î³â‚

 where
  Î³â‚ : âˆƒ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
  Î³â‚ = â„-arithmetically-located x (Îµ , 0<Îµ)

  Î³â‚‚ : Î£ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (v - u < Îµ)
     â†’ Î£ (u , v) ê‰ â„š Ã— â„š , (u < x < v) Ã— (0â„š < (v - u) < Îµ)
  Î³â‚‚ ((u , v) , u<x<v , l) = (u , v) , u<x<v , l' , l
   where
    u<v : u < v
    u<v = disjoint-from-real x u v u<x<v

    l' : 0â„š < v - u
    l' = â„š<-difference-positive u v u<v

\end{code}
