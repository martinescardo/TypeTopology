Martin Escardo, 21th April 2023

Based on Section 8.1 of the paper https://doi.org/10.2168/LMCS-4(3:3)2008

Let ğŸš be the two-point set with elements 0 and 1.

Consider a given boolean function f : ğŸšâ¿ â†’ ğŸš.

Definition. A *root* of f is some x in ğŸšâ¿ such that f x = 0.

Definition. A *putative root* of f is any x in ğŸšâ¿ such that if f has
some root, then x is a root.

Example. If f doesn't have any root, then any x in ğŸšâ¿ is putative root.

Example. If x is a root, then x is a putative root.

Theorem. For any n, there is a formula that mentions only the variable
ğ•— and the constant 0 such that for any given function f: ğŸš^n â†’ ğŸš, the
formula gives a putative root of f when the variable is substituted
for f.

Example. For n = 3, we have the putative root x := (xâ‚€,xâ‚,xâ‚‚) where

  y  := f(0,0,f(0,0,0))
  xâ‚€ := f(0,y,f(0,y,0))
  xâ‚ := f(xâ‚€,0,f(xâ‚€,0,0))
  xâ‚‚ := f(xâ‚€,xâ‚,0)

The purpose of this Agda file is to construct such a formula for any
given n, and prove that it indeed gives a putative root.

Because this file is intended for a general public of mathematicians
and computer scientists, we include some remarks that are expected to
be obvious to Agda practioners.

Agda is a computer language based on Martin-LÃ¶f Type Theory, which in
turn is the core of HoTT/UF. In this development we adopt the UF
approach to mathematics.

\begin{code}

{-# OPTIONS --safe --without-K --exact-split --no-sized-types --no-guardedness --auto-inline #-}

open import MLTT.Spartan hiding (_^_)
open import UF.FunExt

module Various.RootsOfBooleanFunctions (fe : Fun-Ext) where

open import MLTT.Athenian
open import MLTT.Two-Properties

\end{code}

A version of the type of n-tuples of elements of a type X, where Fin n
is the type of numbers 0, ..., n-1 (and is empty when n is 0):

\begin{code}

_^_ : ğ“¤ Ì‡ â†’ â„• â†’ ğ“¤ Ì‡
X ^ n = Fin n â†’ X

\end{code}

The unique element of the empty tuple:

\begin{code}

!0 : {X : ğ“¤ Ì‡ } â†’ X ^ 0
!0 ()

\end{code}

Any two elements of this type are equal:

\begin{code}

!0-uniqueness' : {X : ğ“¤ Ì‡ } {x y : X ^ 0} (i : Fin 0) â†’ x i ï¼ y i
!0-uniqueness' ()

!0-uniqueness : {X : ğ“¤ Ì‡ } {x y : X ^ 0} â†’ x ï¼ y
!0-uniqueness = dfunext fe !0-uniqueness'

\end{code}

For any f : ğŸš â†’ ğŸš, we can check whether it is constantly 1 by checking
whether f (f 0) is 1, which is easily proved by case analysis on the
value of f 0:

\begin{code}

motivating-fact : (f : ğŸš â†’ ğŸš) â†’  f (f â‚€) ï¼ â‚ â†’ (n : ğŸš) â†’ f n ï¼ â‚
motivating-fact f r = Î³ (f â‚€) refl r
 where
  Î³ : (nâ‚€ : ğŸš) â†’ f â‚€ ï¼ nâ‚€ â†’ f nâ‚€ ï¼ â‚ â†’ (n : ğŸš) â†’ f n ï¼ â‚
  Î³ â‚€ s r â‚€ = r
  Î³ â‚€ s r â‚ = ğŸ˜-elim (zero-is-not-one (s â»Â¹ âˆ™ r))
  Î³ â‚ s r â‚€ = s
  Î³ â‚ s r â‚ = r

\end{code}

Motivated by this, we define:

\begin{code}

ÎµğŸš : (ğŸš â†’ ğŸš) â†’ ğŸš
ÎµğŸš f = f â‚€

AğŸš : (ğŸš â†’ ğŸš) â†’ ğŸš
AğŸš f = f (ÎµğŸš f)

\end{code}

The desired property of AğŸš is the following, which follows from the
motivating fact, in one direction, and directly in the other
direction:

\begin{code}

AğŸš-propertyâ†’ : (f : ğŸš â†’ ğŸš) â†’ AğŸš f ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚
AğŸš-propertyâ†’ = motivating-fact

AğŸš-propertyâ† : (f : ğŸš â†’ ğŸš) â†’ ((b : ğŸš) â†’ f b ï¼ â‚) â†’ AğŸš f ï¼ â‚
AğŸš-propertyâ† f Î± = Î± (ÎµğŸš f)

\end{code}

From this it follows directly that for any f : ğŸš â†’ ğŸš we can find a
boolean bâ‚€ such that if f bâ‚€ ï¼ â‚ then f n ï¼ â‚ for every boolean b:

\begin{code}

ÏƒğŸš : (f : ğŸš â†’ ğŸš) â†’ Î£ bâ‚€ ê‰ ğŸš , (f bâ‚€ ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚)
ÏƒğŸš f = ÎµğŸš f , AğŸš-propertyâ†’ f

\end{code}

The functional ÎµğŸš computes a putative root:

\begin{code}

ÎµğŸš-gives-putative-root : {n : â„•} (f : ğŸš â†’ ğŸš)
                       â†’ (Î£ b ê‰ ğŸš , f b ï¼ â‚€)
                       â†’ f (ÎµğŸš f) ï¼ â‚€
ÎµğŸš-gives-putative-root {n} f (b , r) =
 different-from-â‚-equal-â‚€
  (Î» (s : AğŸš f ï¼ â‚) â†’ zero-is-not-one
                       (â‚€   ï¼âŸ¨ r â»Â¹ âŸ©
                        f b ï¼âŸ¨ AğŸš-propertyâ†’ f s b âŸ©
                        â‚   âˆ))

\end{code}

We now pause briefly to define some standard functions on tuples.

The head, or first element of a tuple:

\begin{code}

hd : {X : ğ“¤ Ì‡ } {n : â„•} â†’ X ^ (succ n) â†’ X
hd xs = xs ğŸ

\end{code}

Removing the head of a tuple, we get its tail:

\begin{code}

tl : {X : ğ“¤ Ì‡ } {n : â„•} â†’ X ^ (succ n) â†’ X ^ n
tl xs i = xs (suc i)

\end{code}

Prepending a new head to a tuple:

\begin{code}

cons : {X : ğ“¤ Ì‡ } {n : â„•} â†’ X â†’ X ^ n â†’ X ^ (succ n)
cons x xs ğŸ       = x
cons x xs (suc i) = xs i

\end{code}

The fundamental properties of the above three functions:

\begin{code}

head-cons : {X : ğ“¤ Ì‡ } {n : â„•} (x : X) (xs : X ^ n)
          â†’ hd (cons x xs) ï¼ x
head-cons x xs = refl

tail-cons : {X : ğ“¤ Ì‡ } {n : â„•} (x : X) (xs : X ^ n)
          â†’ tl (cons x xs) ï¼ xs
tail-cons x xs = refl

cons-head-tail : {X : ğ“¤ Ì‡ } {n : â„•} (xs : X ^ (succ n))
               â†’ cons (hd xs) (tl xs) ï¼ xs
cons-head-tail xs = dfunext fe Î³
 where
  Î³ : âˆ€ i â†’ cons (hd xs) (tl xs) i ï¼ xs i
  Î³  ğŸ      = refl
  Î³ (suc i) = refl

\end{code}

We are now ready to compute putative roots of boolean functions. We
will later adapt this argument to give a formula for the putative
root.

We define two functions A and Îµ by simulateous induction on n as
follows:

\begin{code}

A : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš
Îµ : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš ^ n

A f = f (Îµ f)

Îµ {0}      f = Î» (_ : Fin 0) â†’ f !0
Îµ {succ n} f = cons bâ‚€ (Îµ (f âˆ˜ cons bâ‚€))
  where
   bâ‚€ : ğŸš
   bâ‚€ = ÎµğŸš (Î» b â†’ A (f âˆ˜ cons b))

\end{code}

It is of course possible to first define Îµ, by induction on n, and
then A directly from Îµ as follows:

\begin{code}

Îµ' : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš ^ n
Îµ' {0}      f = Î» (_ : Fin 0) â†’ f !0
Îµ' {succ n} f = cons bâ‚€ (Îµ (f âˆ˜ cons bâ‚€))
  where
   bâ‚€ : ğŸš
   bâ‚€ = ÎµğŸš (Î» b â†’ (f âˆ˜ cons b) (Îµ' (f âˆ˜ cons b)))

A' : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš
A' f = f (Îµ' f)

\end{code}

However, we want to highlight the role of A in our definition of Îµ.

We have that A f ï¼ â‚ if and only if f x ï¼ â‚ for all x in ğŸš ^ n:

\begin{code}

A-propertyâ† : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
            â†’ ((x : ğŸš ^ n) â†’ f x ï¼ â‚)
            â†’ A f ï¼ â‚
A-propertyâ† f Î± = Î± (Îµ f)

A-propertyâ†’ : {n : â„•}
              (f : ğŸš ^ n â†’ ğŸš)
            â†’ A f ï¼ â‚
            â†’ (x : ğŸš ^ n) â†’ f x ï¼ â‚
A-propertyâ†’ {0} f r x = f x         ï¼âŸ¨ ap f !0-uniqueness âŸ©
                        f (Îµ {0} f) ï¼âŸ¨ r âŸ©
                        â‚           âˆ
A-propertyâ†’ {succ n} f r x = IV
 where
  IH : (b : ğŸš) â†’ A (f âˆ˜ cons b) ï¼ â‚ â†’ (Î² : ğŸš ^ n) â†’ f (cons b Î²) ï¼ â‚
  IH b = A-propertyâ†’ {n} (f âˆ˜ cons b)

  bâ‚€ : ğŸš
  bâ‚€ = ÎµğŸš (Î» b â†’ A (f âˆ˜ cons b))

  I : A (f âˆ˜ cons bâ‚€) ï¼ â‚ â†’ (b : ğŸš) â†’ A (f âˆ˜ cons b) ï¼ â‚
  I = AğŸš-propertyâ†’ (Î» b â†’ A (f âˆ˜ cons b))

  observationâ‚€ : A f ï¼ â‚
  observationâ‚€ = r

  observationâ‚ : A f ï¼ A (f âˆ˜ cons bâ‚€)
  observationâ‚ = refl

  II : (b : ğŸš) (Î² : ğŸš ^ n) â†’ f (cons b Î²) ï¼ â‚
  II b = IH b (I r b)

  III : f (cons (hd x) (tl x)) ï¼ â‚
  III = II (hd x) (tl x)

  IV : f x ï¼ â‚
  IV = transport (Î» - â†’ f - ï¼ â‚) (cons-head-tail x) III

Ïƒ : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
  â†’ Î£ xâ‚€ ê‰ ğŸš ^ n , (f xâ‚€ ï¼ â‚ â†’ (x : ğŸš ^ n) â†’ f x ï¼ â‚)
Ïƒ f = Îµ f , A-propertyâ†’ f

\end{code}

From this it follows that Îµ f computes a putative root of f.
That is, if f has a root, then Îµ f is a root of f:

\begin{code}

Îµ-gives-putative-root : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
                      â†’ (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
                      â†’ f (Îµ f) ï¼ â‚€
Îµ-gives-putative-root {n} f (x , r) =
 different-from-â‚-equal-â‚€
  (Î» (s : A f ï¼ â‚) â†’ zero-is-not-one
                       (â‚€   ï¼âŸ¨ r â»Â¹ âŸ©
                        f x ï¼âŸ¨ A-propertyâ†’ f s x âŸ©
                        â‚   âˆ))
\end{code}

The above computes a putative root. But what we want to do in this
file is to give a formula for computing putative roots.

So we now introduce a type of formulas, using only the symbol O and a
"variable" ğ•—, defined by induction as follows for any n fixed in
advance:

\begin{code}

data F (n : â„•) : ğ“¤â‚€ Ì‡ where
 O : F n
 ğ•— : F n ^ n â†’ F n

\end{code}

Given any function f : ğŸš ^ n â†’ ğŸš, any formula Ï• in the type F n can be
evaluated to a boolean by replacing the symbol O by the boolean â‚€ and
the variable ğ•— by the function f, by induction on formulas, where we
use the letter Ï•s to range over tuples of formulas:

\begin{code}

eval : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ F n â†’ ğŸš
eval f O      = â‚€
eval f (ğ•— Ï•s) = f (Î» i â†’ eval f (Ï•s i))

\end{code}

Now, for any k, we think of the type F k as that of "formulas for
defining booleans", and we repeat the above definitions of the above
functions ÎµğŸš, A and Îµ, replacing booleans by formulas for booleans, in
order to compute them symbolically (indicated by the superscript s).

\begin{code}

ÎµğŸšË¢ : {k : â„•} â†’ (F k â†’ F k) â†’ F k
ÎµğŸšË¢ f = f O

AË¢ : {n k : â„•} â†’ (F k ^ n â†’ F k) â†’ F k
ÎµË¢ : {n k : â„•} â†’ (F k ^ n â†’ F k) â†’ F k ^ n

AË¢ f = f (ÎµË¢ f)

ÎµË¢ {0}      f = Î» (_ : Fin 0) â†’ f !0
ÎµË¢ {succ n} f = cons bâ‚€ (ÎµË¢ (f âˆ˜ cons bâ‚€))
 where
  bâ‚€ = ÎµğŸšË¢ (Î» b â†’ AË¢ (f âˆ˜ cons b))

\end{code}

Notice now the definitions look exactly the same as those given above,
even if the types of the functions are diffent.

\begin{code}

putative-root-formula : {n : â„•} â†’ F n ^ n
putative-root-formula = ÎµË¢ ğ•—

\end{code}

The intended properties of these functions are, of course:

\begin{code}

AË¢-desired-property = {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
                    â†’ eval f (AË¢ ğ•—) ï¼ A f

ÎµË¢-desired-property = {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
                    â†’ (i : Fin n) â†’ eval f (ÎµË¢ ğ•— i) ï¼ Îµ f i
\end{code}

We will prove them here on another occasion. But we emphasize, for
now, that we need to prove something stronger, involving not only n
but also k.

In any case, notice that the desired property of for AË¢ follows
directly rom the desired property for ÎµË¢:

\begin{code}

AË¢-observation : ÎµË¢-desired-property â†’ AË¢-desired-property
AË¢-observation d {0} f      = ap f !0-uniqueness
AË¢-observation d {succ n} f =
 eval f (AË¢ ğ•—)             ï¼âŸ¨ refl âŸ©
 f (Î» i â†’ eval f (ÎµË¢ ğ•— i)) ï¼âŸ¨ ap f (dfunext fe (d f)) âŸ©
 f (Îµ f)                   ï¼âŸ¨ refl âŸ©
 A f                       âˆ

\end{code}

Before we prove the desired property for ÎµË¢, we can give an example.

First notice that Agda can't display functions in a nice, visualizable
way. In order to do that, we consider a type of n-tuples of elements
of a type X, which is in bijection with the type X ^ n, and which can
be nicely displayed. This copy of X ^ n can be inductively defined as
follows:

\begin{code}

data tuple (X : ğ“¤ Ì‡ ) : â„• â†’ ğ“¤ Ì‡ where
 â‹†   : tuple X 0
 _,_ : {n : â„•} â†’ X â†’ tuple X n â†’ tuple X (succ n)

\end{code}

With this, we can define an equivalent copy F' of F as follows:

\begin{code}

data F' (n : â„•) : ğ“¤â‚€ Ì‡ where
 O : F' n
 ğ•— : tuple (F' n) n â†’ F' n

tuplify : {n : â„•} {X : ğ“¤ Ì‡ } â†’ X ^ n â†’ tuple X n
tuplify {ğ“¤} {0}      f = â‹†
tuplify {ğ“¤} {succ n} f = f ğŸ , tuplify (f âˆ˜ suc)

translate : {n : â„•} â†’ F n â†’ F' n
translate O      = O
translate (ğ•— Ï•s) = ğ•— (tuplify (Î» i â†’ translate (Ï•s i)))

\end{code}

With this, we can visualize our formula for e.g. the putative root
when n = 2 as follows:

\begin{code}

exampleâ‚‚ : tuple (F' 2) 2
exampleâ‚‚ = tuplify (Î» i â†’ translate (putative-root-formula i))

exampleâ‚‚-explicitly : exampleâ‚‚ ï¼
                      (ğ•— (O , (ğ•— (O , (O , â‹†)) , â‹†)) ,
                       ğ•— (ğ•— (O , (ğ•— (O , (O , â‹†)) , â‹†)) , (O , â‹†)) , â‹†)
exampleâ‚‚-explicitly = refl

\end{code}

This says that for any f : ğŸš ^ 2 â†’ ğŸš, our putative root is
(xâ‚€ , xâ‚) where

  xâ‚€ = ğ•— (O , ğ•— (O , O))
  xâ‚ = ğ•— (ğ•— (O , ğ•— (O , O)) , O)

TODO. Prove the above desired properties and use them to show that the
formula for putative roots indeed gives putative roots.

Appendix. Things that are not needed for the above discussion, but
that we may need for other purposes in the future.

\begin{code}

Î´Î£ : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
   â†’ is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
Î´Î£ {n} f = Î³ (f x) refl
 where
  x : ğŸš ^ n
  x = Îµ f

  Î³ : (k : ğŸš) â†’ f x ï¼ k â†’ is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
  Î³ â‚€ r = inl (x  , r)
  Î³ â‚ r = inr (Î» ((Î² , s) : Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€) â†’ zero-is-not-one
                                                      (â‚€   ï¼âŸ¨ s â»Â¹ âŸ©
                                                       f Î² ï¼âŸ¨ A-propertyâ†’ f r Î² âŸ©
                                                       â‚   âˆ))

Î´Î  : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
   â†’ is-decidable (Î  x ê‰ ğŸš ^ n , f x ï¼ â‚)
Î´Î  {n} f = Î³ (Î´Î£ f)
 where
  Î³ : is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€) â†’ is-decidable ((x : ğŸš ^ n) â†’ f x ï¼ â‚)
  Î³ (inl (x , r)) = inr (Î» Ï• â†’ zero-is-not-one (r â»Â¹ âˆ™ Ï• x))
  Î³ (inr Î½)       = inl (Î» x â†’ different-from-â‚€-equal-â‚ (Î» r â†’ Î½ (x , r)))

F-rec : {n : â„•} {X : ğ“¤ Ì‡ } â†’ X â†’ (X ^ n â†’ X) â†’ F n â†’ X
F-rec {ğ“¤} {n} {X} xâ‚€ p = h
 where
  h : F n â†’ X
  h O     = xâ‚€
  h (ğ•— t) = p (Î» i â†’ h (t i))

F-ind : {n : â„•} {X : F n â†’ ğ“¤ Ì‡ }
      â†’ X O
      â†’ ((ts : F n ^ n) â†’ ((i : Fin n) â†’ X (ts i)) â†’ X (ğ•— ts))
      â†’ (t : F n) â†’ X t
F-ind {ğ“¤} {n} {X} xâ‚€ p = h
 where
  h : (t : F n) â†’ X t
  h O      = xâ‚€
  h (ğ•— ts) = p ts (Î» i â†’ h (ts i))

\end{code}
