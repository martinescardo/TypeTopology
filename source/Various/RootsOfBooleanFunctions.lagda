Martin Escardo, 21th April 2023

Based on Section 8.1 of the paper https://doi.org/10.2168/LMCS-4(3:3)2008

Let ğŸš be the two-point set with elements 0 and 1.

Consider a given boolean function f: ğŸšâ¿ â†’ ğŸš.

Definition. A *root* of f is some x in ğŸšâ¿ such that f x = 0.

Definition. A *putative root* of f is any x in ğŸšâ¿ such that if f has
some root, then x is a root.

Example. If f doesn't have any root, then any x in ğŸšâ¿ is putative root.

Example. If x is a root, then x is a putative root.

Theorem. For any n, there is a formula that mentions only the variable
ğ•— and the constant 0 such that for any given function f: ğŸšâ¿ â†’ ğŸš, the
formula gives a putative root of f when the variable is substituted
for f.

Example. For n = 3, we have the putative root x := (xâ‚€,xâ‚,xâ‚‚) where

  y  := f(0,0,f(0,0,0))
  xâ‚€ := f(0,y,f(0,y,0))
  xâ‚ := f(xâ‚€,0,f(xâ‚€,0,0))
  xâ‚‚ := f(xâ‚€,xâ‚,0)

The purpose of this Agda file is to construct such a formula for any
given n, and prove that it indeed gives a putative root.

Because this file is intended for a general public of mathematicians
and computer scientists, we include some remarks that are expected to
be obvious to Agda practioners, but not necessarily for everybody.
Agda is a computer language based on Martin-LÃ¶f Type Theory.

\begin{code}

{-# OPTIONS --safe --without-K --exact-split --no-sized-types --no-guardedness --auto-inline #-}

module Various.RootsOfBooleanFunctions where

open import MLTT.Spartan hiding (_^_)
open import MLTT.Athenian
open import MLTT.Two-Properties

\end{code}

For any f : ğŸš â†’ ğŸš, we can check whether it is constantly 1 by checking
whether f (f 0) is 1, which is easily proved by case analysis on the
value of f 0:

\begin{code}

motivating-fact : (f : ğŸš â†’ ğŸš) â†’ f (f â‚€) ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚
motivating-fact f = Î³ (f â‚€) refl
 where
  Î³ : (bâ‚€ : ğŸš) â†’ f â‚€ ï¼ bâ‚€ â†’ f bâ‚€ ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚
  Î³ â‚€ s r â‚€ = r
  Î³ â‚€ s r â‚ = ğŸ˜-elim
               (zero-is-not-one
                 (â‚€   ï¼âŸ¨ s â»Â¹ âŸ©
                  f â‚€ ï¼âŸ¨ r âŸ©
                  â‚   âˆ))
  Î³ â‚ s r â‚€ = s
  Î³ â‚ s r â‚ = r

\end{code}

Motivated by this, we define:

\begin{code}

ÎµğŸš : (ğŸš â†’ ğŸš) â†’ ğŸš
ÎµğŸš f = f â‚€

AğŸš : (ğŸš â†’ ğŸš) â†’ ğŸš
AğŸš f = f (ÎµğŸš f)

\end{code}

The desired property of AğŸš is the following, which follows from the
motivating fact, in one direction, and directly in the other
direction:

\begin{code}

AğŸš-propertyâ†’ : (f : ğŸš â†’ ğŸš) â†’ AğŸš f ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚
AğŸš-propertyâ†’ = motivating-fact

AğŸš-propertyâ† : (f : ğŸš â†’ ğŸš) â†’ ((b : ğŸš) â†’ f b ï¼ â‚) â†’ AğŸš f ï¼ â‚
AğŸš-propertyâ† f Î± = Î± (ÎµğŸš f)

\end{code}

From this it follows directly that for any f : ğŸš â†’ ğŸš we can find a
boolean bâ‚€ such that if f bâ‚€ ï¼ â‚ then f n ï¼ â‚ for every boolean b:

\begin{code}

ÏƒğŸš : (f : ğŸš â†’ ğŸš) â†’ Î£ bâ‚€ ê‰ ğŸš , (f bâ‚€ ï¼ â‚ â†’ (b : ğŸš) â†’ f b ï¼ â‚)
ÏƒğŸš f = ÎµğŸš f , AğŸš-propertyâ†’ f

\end{code}

The functional ÎµğŸš computes the putative root Îµ f for any f: ğŸš â†’ ğŸš:

\begin{code}

ÎµğŸš-gives-putative-root : {n : â„•} (f : ğŸš â†’ ğŸš)
                       â†’ (Î£ b ê‰ ğŸš , f b ï¼ â‚€)
                       â†’ f (ÎµğŸš f) ï¼ â‚€
ÎµğŸš-gives-putative-root {n} f (b , r) =
 different-from-â‚-equal-â‚€
  (Î» (s : AğŸš f ï¼ â‚) â†’ zero-is-not-one
                       (â‚€   ï¼âŸ¨ r â»Â¹ âŸ©
                        f b ï¼âŸ¨ AğŸš-propertyâ†’ f s b âŸ©
                        â‚   âˆ))
\end{code}

We define the type X ^ n of n-tuples of elements of a type X by
induction as follows.

\begin{code}

data _^_ (X : ğ“¤ Ì‡ ) : â„• â†’ ğ“¤ Ì‡ where
 â‹†   : X ^ 0
 _,_ : {n : â„•} â†’ X â†’ X ^ n â†’ X ^ (succ n)

prepend : {X : ğ“¤ Ì‡ } {n : â„•} â†’ X â†’ X ^ n â†’ X ^ (succ n)
prepend x = (xs â†¦ (x , xs))

\end{code}

We are now ready to compute putative roots of boolean functions. We
will later adapt this argument to give a formula for the putative
root.

We define two functions A and Îµ by simulateous induction on n as
follows:

\begin{code}

A : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš
Îµ : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš ^ n

A f = f (Îµ f)

Îµ {0}      f = â‹†
Îµ {succ n} f = prepend bâ‚€ (Îµ (f âˆ˜ prepend bâ‚€))
  where
   bâ‚€ : ğŸš
   bâ‚€ = ÎµğŸš (b â†¦ A (f âˆ˜ prepend b))

\end{code}

It is of course possible to first define Îµ, by induction on n, and
then A directly from Îµ as follows:

\begin{code}

private

 Îµ' : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš ^ n
 Îµ' {0}      f = â‹†
 Îµ' {succ n} f = prepend bâ‚€ (Îµ (f âˆ˜ prepend bâ‚€))
   where
    bâ‚€ : ğŸš
    bâ‚€ = ÎµğŸš (b â†¦ (f âˆ˜ prepend b) (Îµ' (f âˆ˜ prepend b)))

 A' : {n : â„•} â†’ (ğŸš ^ n â†’ ğŸš) â†’ ğŸš
 A' f = f (Îµ' f)

\end{code}

However, we want to highlight the role of A in our definition of Îµ.

We have that A f ï¼ â‚ if and only if f x ï¼ â‚ for all x in ğŸš ^ n:

\begin{code}

A-propertyâ† : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
            â†’ ((x : ğŸš ^ n) â†’ f x ï¼ â‚)
            â†’ A f ï¼ â‚
A-propertyâ† f Î± = Î± (Îµ f)

A-propertyâ†’ : {n : â„•}
              (f : ğŸš ^ n â†’ ğŸš)
            â†’ A f ï¼ â‚
            â†’ (x : ğŸš ^ n) â†’ f x ï¼ â‚
A-propertyâ†’ {0}      f r â‹† = f â‹†         ï¼âŸ¨ refl âŸ©
                             f (Îµ {0} f) ï¼âŸ¨ r âŸ©
                             â‚           âˆ
A-propertyâ†’ {succ n} f r ( x , xs) = II
 where
  IH : (b : ğŸš) â†’ A (f âˆ˜ prepend b) ï¼ â‚ â†’ (xs : ğŸš ^ n) â†’ f (prepend b xs) ï¼ â‚
  IH b = A-propertyâ†’ {n} (f âˆ˜ prepend b)

  bâ‚€ : ğŸš
  bâ‚€ = ÎµğŸš (b â†¦ A (f âˆ˜ prepend b))

  I : A (f âˆ˜ prepend bâ‚€) ï¼ â‚ â†’ (b : ğŸš) â†’ A (f âˆ˜ prepend b) ï¼ â‚
  I = AğŸš-propertyâ†’ (b â†¦ A (f âˆ˜ prepend b))

  II : f (x , xs) ï¼ â‚
  II = IH x (I r x) xs

Ïƒ : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
  â†’ Î£ xâ‚€ ê‰ ğŸš ^ n , (f xâ‚€ ï¼ â‚ â†’ (x : ğŸš ^ n) â†’ f x ï¼ â‚)
Ïƒ f = Îµ f , A-propertyâ†’ f

\end{code}

From this it follows that Îµ f computes a putative root of f.
That is, if f has a root, then Îµ f is a root of f:

\begin{code}

Îµ-gives-putative-root : {n : â„•}  (f : ğŸš ^ n â†’ ğŸš)
                      â†’ (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
                      â†’ f (Îµ f) ï¼ â‚€
Îµ-gives-putative-root {n} f (x , p) =
 different-from-â‚-equal-â‚€
  (Î» (q : A f ï¼ â‚) â†’ zero-is-not-one
                       (â‚€   ï¼âŸ¨ p â»Â¹ âŸ©
                        f x ï¼âŸ¨ A-propertyâ†’ f q x âŸ©
                        â‚   âˆ))
\end{code}

The above computes a putative root. But what we want to do in this
file is to give a formula for computing putative roots using only 0
and f, as discussed above.

So we now introduce a type of formulas, using only the symbol O and a
"variable" ğ•—, defined by induction as follows for any n fixed in
advance:

\begin{code}

data F (n : â„•) : ğ“¤â‚€ Ì‡ where
 O : F n
 ğ•— : F n ^ n â†’ F n

\end{code}

Given any function f : ğŸš ^ n â†’ ğŸš, any formula Ï• in the type F n can be
evaluated to a boolean by replacing the symbol O by the boolean â‚€ and
the variable ğ•— by the function f, by induction on formulas, where we
use the letter Ï•s to range over tuples of formulas:

\begin{code}

module _ {n : â„•} (f : ğŸš ^ n â†’ ğŸš) where

 eval       : F n â†’ ğŸš
 eval-tuple : {k : â„•} â†’ F n ^ k â†’ ğŸš ^ k

 eval O     = â‚€
 eval (ğ•— Ï•) = f (eval-tuple Ï•)

 eval-tuple â‹†        = â‹†
 eval-tuple (Ï• , Ï•s) = eval Ï• , eval-tuple Ï•s

\end{code}

Now, for any n, we think of the type F n as that of "formulas for
defining booleans", and we repeat the above definitions of the above
functions ÎµğŸš, A and Îµ, replacing booleans by formulas for booleans, in
order to compute them symbolically (indicated by the superscript s).

\begin{code}

ÎµğŸšË¢ : {n : â„•} â†’ (F n â†’ F n) â†’ F n
ÎµğŸšË¢ f = f O

AË¢ : {k n : â„•} â†’ (F n ^ k â†’ F n) â†’ F n
ÎµË¢ : {k n : â„•} â†’ (F n ^ k â†’ F n) â†’ F n ^ k

AË¢ f = f (ÎµË¢ f)

ÎµË¢ {0}      {n} f = â‹†
ÎµË¢ {succ k} {n} f = prepend bâ‚€ (ÎµË¢ (f âˆ˜ prepend bâ‚€))
 where
  bâ‚€ : F n
  bâ‚€ = ÎµğŸšË¢ (b â†¦ AË¢ (f âˆ˜ prepend b))

\end{code}

Notice how the definitions look exactly the same as those given above,
even if the types of the functions are diffent.

\begin{code}

putative-root-formula : {n : â„•} â†’ F n ^ n
putative-root-formula = ÎµË¢ ğ•—

\end{code}

The intended properties of these functions are, of course:

\begin{code}

AË¢-desired-property = {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
                    â†’ eval f (AË¢ ğ•—) ï¼ A f

ÎµË¢-desired-property = {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
                    â†’ eval-tuple f (ÎµË¢ ğ•—) ï¼ Îµ f
\end{code}

Before we prove these desired properties, we can give some
examples.

\begin{code}

putative-root-formulaâ‚‚-works : (f : ğŸš ^ 2 â†’ ğŸš)
                             â†’ (Î£ x ê‰ ğŸš ^ 2 , f x ï¼ â‚€)
                             â†’ f (eval-tuple f putative-root-formula) ï¼ â‚€
putative-root-formulaâ‚‚-works = Îµ-gives-putative-root

putative-root-formulaâ‚‚-explicitly :

  putative-root-formula {2}
  ï¼ (ğ•— (O , ğ•— (O , O , â‹†) , â‹†) , ğ•— (ğ•— (O , ğ•— (O , O , â‹†) , â‹†) , O , â‹†) , â‹†)

putative-root-formulaâ‚‚-explicitly = refl

putative-root-formulaâ‚ƒ-works : (f : ğŸš ^ 3 â†’ ğŸš)
                             â†’ (Î£ x ê‰ ğŸš ^ 3 , f x ï¼ â‚€)
                             â†’ f (eval-tuple f putative-root-formula) ï¼ â‚€
putative-root-formulaâ‚ƒ-works = Îµ-gives-putative-root

putative-root-formulaâ‚ƒ-explicitly :
 let
  y  = ğ•— (O , O , ğ•— (O , O , O , â‹†) , â‹†)
  xâ‚€ = ğ•— (O , y , ğ•— (O , y , O , â‹†) , â‹†)
  xâ‚ = ğ•— (xâ‚€ , O , ğ•— (xâ‚€ , O , O , â‹†) , â‹†)
  xâ‚‚ = ğ•— (xâ‚€ , xâ‚ , O , â‹†)
 in
  putative-root-formula {3} ï¼ (xâ‚€ , xâ‚ , xâ‚‚ , â‹†)
putative-root-formulaâ‚ƒ-explicitly = refl

\end{code}

TODO. Prove the above desired properties and use them to show that the
formula for putative roots indeed gives putative roots.

In any case, notice that the desired property of AË¢ follows
directly from the desired property for ÎµË¢:

\begin{code}

AË¢-observation : ÎµË¢-desired-property â†’ AË¢-desired-property
AË¢-observation d {0} f      = refl
AË¢-observation d {succ n} f =
 eval f (AË¢ ğ•—)           ï¼âŸ¨ refl âŸ©
 f (eval-tuple f (ÎµË¢ ğ•—)) ï¼âŸ¨ ap f (d f) âŸ©
 f (Îµ f)                 ï¼âŸ¨ refl âŸ©
 A f                     âˆ

\end{code}


Appendix. Things that are not needed for the above discussion, but
that we may need for other purposes in the future.

\begin{code}

Î´Î£ : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
   â†’ is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
Î´Î£ {n} f = Î³ (f x) refl
 where
  x : ğŸš ^ n
  x = Îµ f

  Î³ : (k : ğŸš) â†’ f x ï¼ k â†’ is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
  Î³ â‚€ p = inl (x  , p)
  Î³ â‚ p = inr (Î» ((x , q) : Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
                 â†’ zero-is-not-one
                    (â‚€   ï¼âŸ¨ q â»Â¹ âŸ©
                     f x ï¼âŸ¨ A-propertyâ†’ f p x âŸ©
                     â‚   âˆ))

Î´Î  : {n : â„•} (f : ğŸš ^ n â†’ ğŸš)
   â†’ is-decidable (Î  x ê‰ ğŸš ^ n , f x ï¼ â‚)
Î´Î  {n} f = Î³ (Î´Î£ f)
 where
  Î³ : is-decidable (Î£ x ê‰ ğŸš ^ n , f x ï¼ â‚€)
    â†’ is-decidable ((x : ğŸš ^ n) â†’ f x ï¼ â‚)
  Î³ (inl (x , p)) = inr (Î» Ï• â†’ zero-is-not-one (p â»Â¹ âˆ™ Ï• x))
  Î³ (inr Î½)       = inl (Î» x â†’ different-from-â‚€-equal-â‚ (Î» p â†’ Î½ (x , p)))

\end{code}
