Martin Escardo, Tom de Jong, 16 & 22 Feb 2023

We implement a predicative version of Pataraia's fixed point theorem
that every monotone endomap of a dcpo (directed complete poset) with a
least element has a least fixed point.

Pataraia [1] was the first to give a constructive proof of this in
topos logic. A version of his proof is published in [2] by Escardo,
with Pataraia's permission. Pataraia himself didn't publish the
result. An earlier, less general, theorem was proved by Coquand [6]
for *bounded complete* dcpos.

Pataraia's proof has two steps, the first of which is directly
predicative and coded in the module stepâ‚ below.

The second step is impredicative, because it considers the
intersection of all subsets of the dcpo that contain the least
element, are closed under the monotone map, and are closed under
directed suprema. This is impredicative in the sense that it requires
propositional resizing axioms so that we can form this intersection.

We instead consider a direct, explicit, elegant, predicative
construction of this subset, due to Paul Taylor [3], in our second
step, coded in the module stepâ‚‚ below.

This version of the theorem probably deserves to be called the
Pataraia-Taylor fixed-point theorem, not only because the proof
involves a new ingredient, but also because it holds in a more general
predicative setting (here MLTT with function extensionality and
existence of propositional truncations). Therefore we decided to name
this module Pataraia-Taylor.

There is a catch, though. In a predicative setting, there is no
non-trivial dcpo to apply the theorem [4]. More precisely, dcpos are
parametrized by three universes (ğ“¤,ğ“¥,ğ“¦) where the carrier lives in ğ“¤,
the truth values of the order relation live in ğ“¥, and the index types
for directed families live in ğ“¦. In practice, for instance for the
Scott model of PCF, or Scott's Dâˆ-model of the untyped
lambda-calculus, the parameter is of the form (ğ“¤âº,ğ“¤,ğ“¤), and we refer
to such dcpos as "large, locally small, small directed-complete", and
if the parameter is (ğ“¤,ğ“¤,ğ“¤), we could refer to the dcpo as "small and
small directed-complete". The Pataraia-Taylor fixed point theorem
presented here applies to small, small directed-complete dcpos, and
the trouble is that there are no non-trivial examples of such dcpos in
our predicative world [4]. The only way to produce nontrivial such
dcpos to apply the theorem is to assume propositional resizing axioms
(which e.g. the UniMath project [5] does).

[1] Dito Pataraia. A constructive proof of Tarskiâ€™s fixed-point
    theorem for dcpoâ€™s. Presented at the 65th Peripatetic Seminar on
    Sheaves and Logic, Aarhus, Denmark, November 1997.

[2] Martin Escardo. Joins in the frame of nuclei.
    Applied Categorical Structures 11: 117â€“124, 2003.
    https://doi.org/10.1023/A:1023555514029

[3] Paul Taylor. Two recent talks at Birmingham.
    Slides and papers available at
    https://paultaylor.eu/ordinals/
    https://web.archive.org/web/20240222103315/https://paultaylor.eu/ordinals/
    (22 Feb 2024 snapshot)

[4] Tom de Jong. Domain theory in constructive and predicative
    univalent foundations.
    PhD thesis at the University of Birmingham, UK, 2023.
    https://arxiv.org/abs/2301.12405

[5] Vladimir Voevodky, Benedikt Ahrens, Dan Grayson and others.
    Unimath --- a computer-checked library of univalent mathematics.
    https://unimath.github.io/UniMath/
    https://doi.org/10.5281/zenodo.8427604

[6] Thierry Coquand. A topos theoretic fix point theorem.
    Unpublished manuscript, June 1995.
    https://web.archive.org/web/20110822085930/https://www.cse.chalmers.se/~coquand/fix.pdf

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc

module Various.Pataraia-Taylor
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¤ : Universe)
       where

open PropositionalTruncation pt

open import DomainTheory.Basics.Dcpo pt fe ğ“¤
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

\end{code}

We prove the following theorem, which says that every monotone endomap
of a dcpo with a least element has a least fixed point. As discussed
above, dcpos require three universes to be fully specified. For the
formulation of the theorem, we need the three universes to be the
same, namely ğ“¤. (Notice that we mention ğ“¤ only twice in the statement
of the theorem. This is because when we opened the domain theory
modules above, we already passed the universe ğ“¤ once as a parameter.)

\begin{code}

Theorem : (ğ““ : DCPO {ğ“¤} {ğ“¤})
        â†’ has-bottom ğ““
        â†’ (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
        â†’ is-monotone ğ““ ğ““ f
        â†’ Î£ x ê‰ âŸ¨ ğ““ âŸ©
              , (f x ï¼ x)
              Ã— ((y : âŸ¨ ğ““ âŸ©) â†’ f y ï¼ y â†’ x âŠ‘âŸ¨ ğ““ âŸ© y)
\end{code}

Before proving this theorem, we first need to prove a number of
lemmas, in two modules, stepâ‚ and stepâ‚‚.

\begin{code}

module stepâ‚ (ğ“” : DCPO {ğ“¤} {ğ“¤}) where

 private
  E   = âŸ¨ ğ“” âŸ©
  _âŠ‘_ = underlying-order ğ“”

\end{code}

We now define the pointed type MI of monotone inflationary endomaps of
the underlying set E of the dcpo ğ“”. Notice that E is allowed to be
empty.

\begin{code}

 MI : ğ“¤ Ì‡
 MI = Î£ f ê‰ (E â†’ E) , is-monotone ğ“” ğ“” f Ã— is-inflationary ğ“” f

 ğ•šğ•• : MI
 ğ•šğ•• = id , id-is-monotone ğ“” , id-is-inflationary ğ“”

\end{code}

We use the following auxiliary function Î“ : E â†’ MI â†’ E to define a
function Î³ : E â†’ E with suitable properties. For each x : E we get a
directed family Î“ x : MI â†’ E, and we define Î³ x to be the supremum of
this family.

Notice that it is at this point that we need that our dcpo is small
and small complete, because the index set MI lives in the universe ğ“¤,
which is also where the carrier E of ğ“” lives.

\begin{code}

 Î“ : E â†’ MI â†’ E
 Î“ x (f , _) = f x

 Î“-is-semidirected : (x : E) â†’ is-Semidirected ğ“” (Î“ x)
 Î“-is-semidirected x ğ•—@(f , fm , fi) ğ•˜@(g , gm , gi) = âˆ£ ğ•™ , f-le-h , g-le-h âˆ£
  where
   h = g âˆ˜ f

   ğ•™ : MI
   ğ•™ = (h , âˆ˜-is-monotone ğ“” ğ“” ğ“” f g fm gm , âˆ˜-is-inflationary ğ“” f g fi gi)

   f-le-h : f x âŠ‘ h x
   f-le-h = gi (f x)

   g-le-h : g x âŠ‘ h x
   g-le-h = gm x (f x) (fi x)

 Î“-is-directed : (x : E) â†’ is-Directed ğ“” (Î“ x)
 Î“-is-directed x = âˆ£ ğ•šğ•• âˆ£ , Î“-is-semidirected x

 Î³ : E â†’ E
 Î³ x = âˆ ğ“” (Î“-is-directed x)

\end{code}

So the function Î³ is the pointwise supremum of the monotone
inflationary endomaps of E.

\begin{code}

 Î³-is-monotone : is-monotone ğ“” ğ“” Î³
 Î³-is-monotone x y l = âˆ-is-monotone ğ“” (Î“-is-directed x) (Î“-is-directed y) l'
  where
   l' : (ğ•— : MI) â†’ Î“ x ğ•— âŠ‘ Î“ y ğ•—
   l' (f , fm , fi) = fm x y l

\end{code}

From this it is easy to conclude that Î³ is the greatest monotone
inflationary map for any x : E.

\begin{code}

 Î³-is-greatest-mi-map : ((f , fm , fi) : MI) (x : E) â†’ f x âŠ‘ Î³ x
 Î³-is-greatest-mi-map ğ•— x = âˆ-is-upperbound ğ“” (Î“-is-directed x) ğ•—

 Î³-is-inflationary : is-inflationary ğ“” Î³
 Î³-is-inflationary = Î³-is-greatest-mi-map ğ•šğ••

\end{code}

And, in turn, from this we conclude that Î³ x is a fixed point of any
monotone inflationary map f : E â†’ E.

\begin{code}

 Î³-is-fixed-point : ((f , fm , fi) : MI) (x : E) â†’ f (Î³ x) ï¼ Î³ x
 Î³-is-fixed-point (f , fm , fi) x = antisymmetry ğ“” _ _ I II
  where
   I : f (Î³ x) âŠ‘ Î³ x
   I = Î³-is-greatest-mi-map
        (f âˆ˜ Î³ ,
         âˆ˜-is-monotone ğ“” ğ“” ğ“” Î³ f Î³-is-monotone fm ,
         âˆ˜-is-inflationary ğ“” Î³ f Î³-is-inflationary fi)
       x

   II : Î³ x âŠ‘ f (Î³ x)
   II = fi (Î³ x)

\end{code}

This concludes the first step of Pataraia's proof.

The second part of Pataraia's proof considers the intersection of all
subsets of ğ““ that have âŠ¥ as a member, are closed under f, and are
closed under directed suprema. This is impredicative in the sense that
it requires propositional resizing axioms to compute the
intersection. We instead consider the subset of ğ““ consisting of the
elements that satisfy Taylor's condition (TC) below, which is defined
predicatively.

\begin{code}

module stepâ‚‚
        (ğ““ : DCPO {ğ“¤} {ğ“¤})
        ((âŠ¥ , âŠ¥-is-least) : has-bottom ğ““)
        (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
        (fm : is-monotone ğ““ ğ““ f)
       where

 private
  D   = âŸ¨ ğ““ âŸ©
  _âŠ‘_ = underlying-order ğ““

 TC : D â†’ ğ“¤ Ì‡
 TC x = (x âŠ‘ f x) Ã— ((u : D) â†’ f u âŠ‘ u â†’ x âŠ‘ u)

 TCâ‚ : (x : D) â†’ TC x â†’ x âŠ‘ f x
 TCâ‚ x = prâ‚

 TCâ‚‚ : (x : D) â†’ TC x â†’ (u : D) â†’ f u âŠ‘ u â†’ x âŠ‘ u
 TCâ‚‚ x = prâ‚‚

 TC-is-prop-valued : (x : D) â†’ is-prop (TC x)
 TC-is-prop-valued x =  Ã—-is-prop
                         (prop-valuedness ğ““ _ _)
                         (Î â‚‚-is-prop fe Î» _ _ â†’ prop-valuedness ğ““ _ _)

 TC-is-closed-under-directed-sups : is-closed-under-directed-sups ğ““ TC
 TC-is-closed-under-directed-sups {A} Î± Î´ TC-preservation = câ‚ , câ‚‚
  where
   TC-preservationâ‚ : (a : A) â†’ Î± a âŠ‘ f (Î± a)
   TC-preservationâ‚ a = TCâ‚ (Î± a) (TC-preservation a)

   TC-preservationâ‚‚ : (a : A) (u : D) â†’ f u âŠ‘ u â†’ Î± a âŠ‘ u
   TC-preservationâ‚‚ a = TCâ‚‚ (Î± a) (TC-preservation a)

   I : (a : A) â†’ Î± a âŠ‘ f (âˆ ğ““ Î´)
   I a = Î± a        âŠ‘âŸ¨ ğ““ âŸ©[ TC-preservationâ‚ a ]
         f (Î± a)    âŠ‘âŸ¨ ğ““ âŸ©[ fm (Î± a) (âˆ ğ““ Î´) (âˆ-is-upperbound ğ““ Î´ a) ]
         f (âˆ ğ““ Î´) âˆâŸ¨ ğ““ âŸ©

   câ‚ : âˆ ğ““ Î´ âŠ‘ f (âˆ ğ““ Î´)
   câ‚ = âˆ-is-lowerbound-of-upperbounds ğ““ Î´ (f (âˆ ğ““ Î´)) I

   câ‚‚ : (u : D) â†’ f u âŠ‘ u â†’ âˆ ğ““ Î´ âŠ‘ u
   câ‚‚ u l = âˆ-is-lowerbound-of-upperbounds ğ““ Î´ u II
    where
     II : (a : A) â†’ Î± a âŠ‘ u
     II a = TC-preservationâ‚‚ a u l

 TC-holds-at-âŠ¥ : TC âŠ¥
 TC-holds-at-âŠ¥ = âŠ¥-is-least (f âŠ¥) , (Î» (u : D) _ â†’ âŠ¥-is-least u)

\end{code}

Now the rest of stepâ‚‚ is essentially the original one by Pataraia. We
apply stepâ‚ to the subdcpo ğ“” of ğ““ consisting of the elements that
satisfy Taylor's condition.

\begin{code}

 ğ“” : DCPO
 ğ“” = subdcpo ğ““ TC TC-is-prop-valued TC-is-closed-under-directed-sups

 private
  E = âŸ¨ ğ“” âŸ©
  _â‰¤_ : E â†’ E â†’ ğ“¤ Ì‡
  s â‰¤ t = s âŠ‘âŸ¨ ğ“” âŸ© t

  NB-E : E ï¼ (Î£ x ê‰ D , TC x)
  NB-E = by-definition

  NB-â‰¤ : (x x' : D) (c : TC x) (c' : TC x')
       â†’ ((x , c) â‰¤ (x' , c')) ï¼ (x âŠ‘ x')
  NB-â‰¤ x x' c c' = by-definition

 Î¹ : E â†’ D
 Î¹ (x , c) = x

 Ï„ : (t : E) â†’ TC (Î¹ t)
 Ï„ (x , c) = c

 âŠ¥ğ“” : E
 âŠ¥ğ“” =  âŠ¥ , TC-holds-at-âŠ¥

\end{code}

The monotone function f : D â†’ D restricts to a monotone inflationary
function ğ“¯ : E â†’ E.

\begin{code}

 ğ“¯ : E â†’ E
 ğ“¯ (x , câ‚ , câ‚‚) = f x ,
                   fm x (f x) câ‚ ,
                   (Î» u (l : f u âŠ‘ u) â†’ f x âŠ‘âŸ¨ ğ““ âŸ©[ fm x u (câ‚‚ u l) ]
                                        f u âŠ‘âŸ¨ ğ““ âŸ©[ l ]
                                        u   âˆâŸ¨ ğ““ âŸ©)

 ğ“¯-is-monotone : (s t : E) â†’ s â‰¤ t â†’ ğ“¯ s â‰¤ ğ“¯ t
 ğ“¯-is-monotone (x , _) (y , _) = fm x y

 ğ“¯-is-inflationary : (t : E) â†’ t â‰¤ ğ“¯ t
 ğ“¯-is-inflationary (x , câ‚ , câ‚‚) = câ‚

 TC-ğ“¯ : (s : E) â†’ TC (f (Î¹ s))
 TC-ğ“¯ s = prâ‚‚ (ğ“¯ s)

\end{code}

So now we can apply the development of stepâ‚.

\begin{code}

 open stepâ‚ ğ“”

 ğ•— : MI
 ğ•— = (ğ“¯ , ğ“¯-is-monotone , ğ“¯-is-inflationary)

 tâ‚€ : E
 tâ‚€ = Î³ âŠ¥ğ“”

 tâ‚€-is-fp : ğ“¯ tâ‚€ ï¼ tâ‚€
 tâ‚€-is-fp = Î³-is-fixed-point ğ•— âŠ¥ğ“”

 xâ‚€ : D
 xâ‚€ = Î¹ tâ‚€

 xâ‚€-is-fp : f xâ‚€ ï¼ xâ‚€
 xâ‚€-is-fp = ap Î¹ tâ‚€-is-fp

\end{code}

xâ‚€ is the least pre-fixed point.

\begin{code}

 xâ‚€-is-lpfp : (x : D) â†’ f x âŠ‘ x â†’ xâ‚€ âŠ‘ x
 xâ‚€-is-lpfp = TCâ‚‚ xâ‚€ (Ï„ tâ‚€)

\end{code}

And so it is the least fixed point.

\begin{code}

 xâ‚€-is-lfp : (x : D) â†’ f x ï¼ x â†’ xâ‚€ âŠ‘ x
 xâ‚€-is-lfp x p = xâ‚€-is-lpfp x (ï¼-to-âŠ‘ ğ““ p)

\end{code}

This concludes the proof of the theorem.

\begin{code}

Theorem ğ““ hb f fm = xâ‚€ , xâ‚€-is-fp , xâ‚€-is-lfp
 where
  open stepâ‚‚ ğ““ hb f fm

\end{code}

This theorem can be strengthened as follows, which says that any
endofunctor f has an initial algebra, when the dcpo is viewed as a
category.

\begin{code}

initial-algebra : (ğ““ : DCPO {ğ“¤} {ğ“¤})
                â†’ has-bottom ğ““
                â†’ (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
                â†’ is-monotone ğ““ ğ““ f
                â†’ Î£ x ê‰ âŸ¨ ğ““ âŸ©
                      , (f x ï¼ x)
                      Ã— ((y : âŸ¨ ğ““ âŸ©) â†’ f y âŠ‘âŸ¨ ğ““ âŸ© y â†’ x âŠ‘âŸ¨ ğ““ âŸ© y)
initial-algebra ğ““ hb f fm = xâ‚€ , xâ‚€-is-fp , xâ‚€-is-lpfp
 where
  open stepâ‚‚ ğ““ hb f fm

\end{code}

NB. We could have formulated and proved this more categorically as

  (ğ““ : DCPO {ğ“¤} {ğ“¤})
 â†’ has-bottom ğ““
 â†’ (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
 â†’ is-monotone ğ““ ğ““ f
 â†’ Î£ x ê‰ âŸ¨ ğ““ âŸ©
       , (f x âŠ‘âŸ¨ ğ““ âŸ© x)
       Ã— ((y : âŸ¨ ğ““ âŸ©) â†’ f y âŠ‘âŸ¨ ğ““ âŸ© y â†’ x âŠ‘âŸ¨ ğ““ âŸ© y)

and then conclude that actually f x ï¼ x by Lambek's Lemma. But we
already know that the initial algebra is a fixed point in our case,
and so there is no point in doing this.

For later reference we repackage the theorem as follows:

\begin{code}

module _
        (ğ““ : DCPO {ğ“¤} {ğ“¤})
        ((âŠ¥ , âŠ¥-is-least) : has-bottom ğ““)
        (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
        (fm : is-monotone ğ““ ğ““ f)
       where

 lfp : âŸ¨ ğ““ âŸ©
 lfp = prâ‚ (Theorem ğ““ (âŠ¥ , âŠ¥-is-least) f fm)

 lfp-is-fixed-point : f lfp ï¼ lfp
 lfp-is-fixed-point = prâ‚ (prâ‚‚ (Theorem ğ““ (âŠ¥ , âŠ¥-is-least) f fm))

 lfp-is-least : (y : âŸ¨ ğ““ âŸ©) â†’ f y ï¼ y â†’ lfp âŠ‘âŸ¨ ğ““ âŸ© y
 lfp-is-least = prâ‚‚ (prâ‚‚ (Theorem ğ““ (âŠ¥ , âŠ¥-is-least) f fm))

\end{code}

Added 22 February 2024.

It follows directly from Pataraia's original proof [2] that if P is a property
that holds for âŠ¥, is closed under directed suprema, and is closed under f, then
P holds for the least fixed point of f. We refer to this as the fixed-point
induction principle. Although this principle doesn't follow directly from the
above argument, we can prove it as follows, using stepâ‚‚ again.

\begin{code}

 lfp-induction :
    (P : âŸ¨ ğ““ âŸ© â†’ ğ“¤ Ì‡  )
  â†’ ((x : âŸ¨ ğ““ âŸ©) â†’ is-prop (P x))
  â†’ P âŠ¥
  â†’ is-closed-under-directed-sups ğ““ P
  â†’ ((x : âŸ¨ ğ““ âŸ©) â†’ P x â†’ P (f x))
  â†’ P lfp

 module fixed-point-induction
         (P : âŸ¨ ğ““ âŸ© â†’ ğ“¤ Ì‡  )
         (P-is-prop-valued : (x : âŸ¨ ğ““ âŸ©) â†’ is-prop (P x))
         (P-holds-at-âŠ¥ : P âŠ¥)
         (P-is-closed-under-directed-sups : is-closed-under-directed-sups ğ““ P)
         (P-is-closed-under-f : (x : âŸ¨ ğ““ âŸ©) â†’ P x â†’ P (f x))
        where

  private
   D = âŸ¨ ğ““ âŸ©
   _âŠ‘_ = underlying-order ğ““

  open stepâ‚‚ ğ““ (âŠ¥ , âŠ¥-is-least) f fm
   using (TC ;
          TCâ‚‚ ;
          TC-ğ“¯ ;
          TC-is-prop-valued ;
          TC-is-closed-under-directed-sups ;
          TC-holds-at-âŠ¥)
   renaming (ğ“¯ to ğ“¯')

  TC' : D â†’ ğ“¤ Ì‡
  TC' x = TC x Ã— P x

  TC'-âŠ†-TC : (x : âŸ¨ ğ““ âŸ©) â†’ TC' x â†’ TC x
  TC'-âŠ†-TC x = prâ‚

  TC'-âŠ†-P : (x : âŸ¨ ğ““ âŸ©) â†’ TC' x â†’ P x
  TC'-âŠ†-P x = prâ‚‚

  TC'-is-prop-valued : (x : D) â†’ is-prop (TC' x)
  TC'-is-prop-valued x = Ã—-is-prop
                          (TC-is-prop-valued x)
                          (P-is-prop-valued x)

  TC'-is-closed-under-directed-sups : is-closed-under-directed-sups ğ““ TC'
  TC'-is-closed-under-directed-sups Î± Î´ TC'-preservation = câ‚ , câ‚‚
   where
    câ‚ : TC (âˆ ğ““ Î´)
    câ‚ = TC-is-closed-under-directed-sups Î± Î´
          (Î» a â†’ TC'-âŠ†-TC (Î± a) (TC'-preservation a))

    câ‚‚ : P (âˆ ğ““ Î´)
    câ‚‚ = P-is-closed-under-directed-sups Î± Î´
          (Î» a â†’ TC'-âŠ†-P (Î± a) (TC'-preservation a))

  ğ“” : DCPO
  ğ“” = subdcpo ğ““ TC' TC'-is-prop-valued TC'-is-closed-under-directed-sups

  private
   E = âŸ¨ ğ“” âŸ©
   _â‰¤_ : E â†’ E â†’ ğ“¤ Ì‡
   s â‰¤ t = s âŠ‘âŸ¨ ğ“” âŸ© t

  Î¹ : E â†’ D
  Î¹ (x , c) = x

  Ï„' : (t : E) â†’ TC' (Î¹ t)
  Ï„' (x , c) = c

  Ï„ : (t : E) â†’ TC (Î¹ t)
  Ï„ t = TC'-âŠ†-TC (Î¹ t) (Ï„' t)

  âŠ¥ğ“” : E
  âŠ¥ğ“” = âŠ¥ , TC-holds-at-âŠ¥ , P-holds-at-âŠ¥

  ğ“¯ : E â†’ E
  ğ“¯ (x , tc , p) = f x , TC-ğ“¯ (x , tc) , P-is-closed-under-f x p

  ğ“¯-is-monotone : (s t : E) â†’ s â‰¤ t â†’ ğ“¯ s â‰¤ ğ“¯ t
  ğ“¯-is-monotone (x , _) (y , _) = fm x y

  ğ“¯-is-inflationary : (t : E) â†’ t â‰¤ ğ“¯ t
  ğ“¯-is-inflationary (x , (câ‚ , câ‚‚) , _) = câ‚

  open stepâ‚ ğ“”

  ğ•— : MI
  ğ•— = (ğ“¯ , ğ“¯-is-monotone , ğ“¯-is-inflationary)

  tâ‚€ : E
  tâ‚€ = Î³ âŠ¥ğ“”

  tâ‚€-is-fp : ğ“¯ tâ‚€ ï¼ tâ‚€
  tâ‚€-is-fp = Î³-is-fixed-point ğ•— âŠ¥ğ“”

  xâ‚€ : D
  xâ‚€ = Î¹ tâ‚€

  xâ‚€-is-fp : f xâ‚€ ï¼ xâ‚€
  xâ‚€-is-fp = ap Î¹ tâ‚€-is-fp

  xâ‚€-is-lpfp : (x : D) â†’ f x âŠ‘ x â†’ xâ‚€ âŠ‘ x
  xâ‚€-is-lpfp = TCâ‚‚ xâ‚€ (Ï„ tâ‚€)

  xâ‚€-is-lfp : (x : D) â†’ f x ï¼ x â†’ xâ‚€ âŠ‘ x
  xâ‚€-is-lfp x p = xâ‚€-is-lpfp x (ï¼-to-âŠ‘ ğ““ p)

  xâ‚€-satisfies-P : P xâ‚€
  xâ‚€-satisfies-P = TC'-âŠ†-P (Î¹ tâ‚€) (Ï„' tâ‚€)

\end{code}

Now we are ready to prove the least fixed point induction theorem.

\begin{code}

 lfp-induction
  P
  P-is-prop-valued
  P-holds-at-âŠ¥
  P-is-closed-under-directed-sups
  P-is-closed-under-f = transport P e xâ‚€-satisfies-P
   where
    open fixed-point-induction
          P
          P-is-prop-valued
          P-holds-at-âŠ¥
          P-is-closed-under-directed-sups
          P-is-closed-under-f
    e : xâ‚€ ï¼ lfp
    e = antisymmetry ğ““
         xâ‚€ lfp
         (xâ‚€-is-lfp lfp lfp-is-fixed-point)
         (lfp-is-least xâ‚€ xâ‚€-is-fp)

\end{code}
