MartÃ­n EscardÃ³, 31 March 2016, updated 1st April 2016.

Dummett disjunction in Martin-LÓ§f Type Theory.

Last updated 6 Sep 2016 with the addition of weak Dummet disjunction.

We consider the behaviour of what we call "Dummett disjunction" in
intuitionistic logic in its MLTT rendering in Agda notation. (A
motivation coming from univalent type theory is also discussed.)

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Various.DummettDisjunction where

open import MLTT.Spartan

_âŠ_ : Type â†’ Type â†’ Type
P âŠ Q = ((P â†’ Q) â†’ Q) Ã— ((Q â†’ P) â†’ P)

inL : (P Q : Type) â†’ P â†’ P âŠ Q
inL P Q p = (Î» u â†’ u p) , (Î» _ â†’ p)

inR : (P Q : Type) â†’ Q â†’ P âŠ Q
inR P Q q = (Î» _ â†’ q) , (Î» v â†’ v q)

\end{code}

Dummett disjunction _âŠ_ weakens intuitionistic disjunction _+_:

\begin{code}

weaker-than-intuitionistic : (P Q : Type) â†’ P + Q â†’ P âŠ Q
weaker-than-intuitionistic P Q (inl p) = inL P Q p
weaker-than-intuitionistic P Q (inr q) = inR P Q q

\end{code}

and strengthens classical disjunction:

\begin{code}

stronger-than-classical : (P Q : Type) â†’ P âŠ Q â†’ Â¬ (Â¬ P Ã— Â¬ Q)
stronger-than-classical P Q = more-generally ğŸ˜ ğŸ˜-elim
  where
    more-generally : (R : Type) â†’ (R â†’ P) â†’ P âŠ Q â†’ ((P â†’ R) Ã— (Q â†’ R)) â†’ R
    more-generally R e (_ , Î³) (u , v) = u (Î³ (Î» q â†’ e (v q)))

\end{code}

Dummett's linearity axiom for implication,

\begin{code}

linearity-axiom : Type â†’ Type â†’ Type
linearity-axiom P Q = (P â†’ Q) + (Q â†’ P)

\end{code}

makes Dummett disjunction logically equivalent to _+_:

\begin{code}

equivalent-to-intuitionistic : (P Q : Type) â†’ linearity-axiom P Q â†’ P âŠ Q â†’ P + Q
equivalent-to-intuitionistic P Q (inl u) (Ï† , _) = inr (Ï† u)
equivalent-to-intuitionistic P Q (inr v) (_ , Î³) = inl (Î³ v)

\end{code}

We may wish to reformulate the above as follows:

\begin{code}

LA : Typeâ‚
LA = (P Q : Type) â†’ linearity-axiom P Q

LA-gives-agreement : LA â†’ (P Q : Type) â†’ P âŠ Q â†’ P + Q
LA-gives-agreement la P Q = equivalent-to-intuitionistic P Q (la P Q)

\end{code}

In other words, in GÃ¶del-Dummett logic, disjunction is definable from
conjunction and implication as _âŠ_ (M. Dummett, A propositional
calculus with denumerable matrix, Journal of Symbolic Logic, Vol. 24,
No. 2, pp. 97-106, 1959).

It is also well known, and obvious, that linearity just holds if one
of the propositions is decidable:

\begin{code}

dl : (P Q : Type) â†’ is-decidable P â†’ linearity-axiom P Q
dl P Q (inl p) = inr (Î» _ â†’ p)
dl P Q (inr u) = inl (Î» p â†’ ğŸ˜-elim (u p))

\end{code}

Hence if we assume excluded middle (all propositions are decidable),
Dummett disjunction agrees with (intuitionistic and classical)
disjunction.

More generally, without assuming excluded middle, if one of the
propositions is decidable, then P âŠ Q and P + Q are equivalent:

\begin{code}

classical-logic-gives-agreement : (P Q : Type) â†’ is-decidable P â†’ P âŠ Q â†’ P + Q
classical-logic-gives-agreement P Q dp = equivalent-to-intuitionistic P Q (dl P Q dp)

\end{code}

The linearity axiom just holds, in intuitionistic logic, for all
propositions, if we formulate it with Dummett disjunction rather than
intuitionistic disjunction:

\begin{code}

âŠ-linearity : (P Q : Type) â†’ (P â†’ Q) âŠ (Q â†’ P)
âŠ-linearity P Q = (Î» Ï† q â†’ Ï† (Î» _ â†’ q) q) , (Î» Î³ p â†’ Î³ (Î» _ â†’ p) p)

\end{code}

I don't know whether this has already been observed. It must have
been.

Therefore we also get that if Dummett disjunction _âŠ_ is equivalent to
_+_ in intuitionistic logic, then the linearity axiom must hold:

\begin{code}

agreement-gives-LA : ((P Q : Type) â†’ P âŠ Q â†’ P + Q) â†’ LA
agreement-gives-LA f P Q = f (P â†’ Q) (Q â†’ P) (âŠ-linearity P Q)

\end{code}

And I don't know either whether this is known. It probably is.

The following is adapted from
http://www.jstor.org/stable/20016490?seq=2#page_scan_tab_contents page
154, replacing disjunction by Dummett disjunction.

Jan von Plato, Skolem's Discovery of GÃ¶del-Dummett Logic.  Studia
Logica: An International Journal for Symbolic Logic Vol. 73, No. 1,
Constructivism in Non-Classical Logics and Computer Science (Feb.,
2003), pp. 153-157

\begin{code}

skolem : (A B C : Type) â†’ (A â†’ B âŠ C) â†’ (A â†’ B) âŠ (A â†’ C)
skolem A B C h = (Î» f a â†’ prâ‚ (h a) (Î» b â†’ f (Î» _ â†’ b) a)) ,
                 (Î» g a â†’ prâ‚‚ (h a) (Î» c â†’ g (Î» _ â†’ c) a))

dummett : (A B C : Type) â†’ (A Ã— B â†’ C) â†’ (A â†’ C) âŠ (B â†’ C)
dummett A B C h = (Î» f b â†’ f (Î» a â†’ h (a , b)) b) ,
                  (Î» g a â†’ g (Î» b â†’ h (a , b)) a)

\end{code}

The point is that we don't use the linearity axiom for implication to
prove the above, by weakening disjunction to Dummett disjunction.

Dummett also has weak excluded middle Â¬P âˆ¨ Â¬Â¬P in his logic. This
again holds in intuitionistic logic if we replace disjunction by
Dummett disjunction. Because the proof doesn't use efq, as noted by
Dummett, we have, more generally, with ğŸ˜ generalized to any
proposition Q:

\begin{code}

âŠ-wem' : (P Q : Type) â†’ (P â†’ Q) âŠ ((P â†’ Q) â†’ Q)
âŠ-wem' P Q = (Î» f g â†’ f g g) , (Î» f p â†’ f (Î» g â†’ g p) p)

âŠ-wem : (P Q : Type) â†’ Â¬ P âŠ Â¬Â¬ P
âŠ-wem P Q = âŠ-wem' P ğŸ˜

\end{code}

What about excluded middle? Peirce's Law arises directly.

\begin{code}

Peirce's-Law : Type â†’ Type â†’ Type
Peirce's-Law P Q = ((P â†’ Q) â†’ P) â†’ P

PLâ‚€ : Typeâ‚
PLâ‚€ = (P : Type) â†’ Peirce's-Law P ğŸ˜

âŠ-EM : Typeâ‚
âŠ-EM = (P : Type) â†’ P âŠ Â¬ P

âŠ-EM-gives-PLâ‚€ : âŠ-EM â†’ PLâ‚€
âŠ-EM-gives-PLâ‚€ dem P = more-generally ğŸ˜ (dem P)
 where
  more-generally : (Q : Type) â†’ P âŠ (P â†’ Q) â†’ Peirce's-Law P Q
  more-generally Q (_ , Î³)= Î³

\end{code}

The converse holds, but we don't need it:

\begin{code}

PLâ‚€-gives-âŠ-EM : PLâ‚€ â†’ âŠ-EM
PLâ‚€-gives-âŠ-EM plâ‚€ P = more-generally ğŸ˜ (plâ‚€ P)
 where
  more-generally : (Q : Type) â†’ Peirce's-Law P Q â†’ P âŠ (P â†’ Q)
  more-generally Q pl = (Î» f p â†’ f p p) , pl

PL : Typeâ‚
PL = (P Q : Type) â†’ Peirce's-Law P Q

PL-gives-PLâ‚€ : PL â†’ PLâ‚€
PL-gives-PLâ‚€ pl P = pl P ğŸ˜

PLâ‚€-gives-PL : PLâ‚€ â†’ PL
PLâ‚€-gives-PL plâ‚€ P Q Îµ = plâ‚€ P (Î» u â†’ Îµ (Î» p â†’ plâ‚€ Q (Î» _ â†’ ğŸ˜-elim (u p))))

Curry-Howard-EM : Typeâ‚
Curry-Howard-EM = (P : Type) â†’ P + Â¬ P

PL-gives-Curry-Howard-EM : PL â†’ Curry-Howard-EM
PL-gives-Curry-Howard-EM pl P = pl (P + Â¬ P) P (Î» f â†’ inl (pl P ğŸ˜ (Î» g â†’ f (inr g))))

\end{code}

The converse of course holds, but again we don't need it:

\begin{code}

Curry-Howard-EM-gives-PL : Curry-Howard-EM â†’ PL
Curry-Howard-EM-gives-PL em P Q Îµ = f (em P)
  where
    f : P + Â¬ P â†’ P
    f (inl p) = p
    f (inr u) = ğŸ˜-elim (u (Îµ (Î» p â†’ ğŸ˜-elim (u p))))

\end{code}

Hence although weak excluded middle formulated with Dummett
disjunction just holds, we have that excluded middle formulated with
Dummett or Curry-Howard disjunction are logically equivalent:

\begin{code}

âŠ-EM-gives-Curry-Howard-EM : âŠ-EM â†’ Curry-Howard-EM
âŠ-EM-gives-Curry-Howard-EM dem = PL-gives-Curry-Howard-EM (PLâ‚€-gives-PL (âŠ-EM-gives-PLâ‚€ dem))

\end{code}

Also, the above shows that Peirce's Law is equivalent to
(P Q : Type) â†’ P âŠ (P â†’ Q).

I hadn't looked at GÓ§del-Dummett logic before.

I came across this as follows.

In univalent foundations, the propositional of a type X can be defined as

   âˆ¥ X âˆ¥ = (P : ğ“¤) â†’ is-prop P â†’ (X â†’ P) â†’ P.

A proposition (or -1-type) is a type whose elements are all equal
in the sense of the identity type.

(If X lives in a universe ğ“¤ then âˆ¥ X âˆ¥ lives in the next universe.)

Then disjunction, of -1-types, is defined by

   P âˆ¨ Q = âˆ¥ P + Q âˆ¥.

One can consider a variation of the definition of propositional
truncation that preserves the universe level, by working with a family
of propositions P : I â†’ U with I : ğ“¤:

  J X = (i : I) â†’ (X â†’ P i) â†’ P i.

(Cf. Aczel, "The Russellâ€“Prawitz modality", Mathematical Structures in
Computer Science archive Volume 11 Issue 4, August 2001 Pages 541-554.)

Now, Dummett disjunction arises as

  P âŠ Q = J (P + Q)

for the choice of I as the two-point type (the booleans) with one
point mapped to P and the other to Q.

I wondered what we get in this case, and the answer was Dummett
disjunction. But notice that you won't find "Dummett disjunction" in
the literature. What Dummett proved was that in his (intermediate)
logic, disjunction Pâ€Œâ€Œ âˆ¨ Q agrees with ((P â†’ Q) â†’ Q) âˆ§ ((Q â†’ P) â†’ P),
and hence is definable from implication and conjunction.

Here, instead, we have considered the behaviour of what we call
Dummett disjunction in intuitionistic logic.

Added 6 Sep 2016. Weak Dummet disjunction.

We can decompose P âŠ Q as (P âŠ• Q) Ã— (Q âŠ• P) in the obvious way.

Weak Dummet disjunction:

\begin{code}

_âŠ•_ : Type â†’ Type â†’ Type
P âŠ• Q = (P â†’ Q) â†’ Q

âŠ•-inL : (P Q : Type) â†’ P â†’ P âŠ• Q
âŠ•-inL P Q p = Î» u â†’ u p

âŠ•-inR : (P Q : Type) â†’ Q â†’ P âŠ• Q
âŠ•-inR P Q q = Î» _ â†’ q

âŠ•-weaker-than-intuitionistic : (P Q : Type) â†’ P + Q â†’ P âŠ• Q
âŠ•-weaker-than-intuitionistic P Q (inl p) = âŠ•-inL P Q p
âŠ•-weaker-than-intuitionistic P Q (inr q) = âŠ•-inR P Q q

âŠ•-stronger-than-classical : (P Q : Type) â†’ P âŠ• Q â†’ Â¬ (Â¬ P Ã— Â¬ Q)
âŠ•-stronger-than-classical P Q = more-generally ğŸ˜ ğŸ˜-elim
  where
    more-generally : (R : Type) â†’ (R â†’ Q) â†’ P âŠ• Q â†’ ((P â†’ R) Ã— (Q â†’ R)) â†’ R
    more-generally R e Î³ (u , v) = v (Î³ (Î» p â†’ e (u p)))

\end{code}

Right excluded middle just holds for this notion of disjunction:

\begin{code}

âŠ•-em-right : (P : Type) â†’ P âŠ• Â¬ P
âŠ•-em-right P = Î» u p â†’ u p p

\end{code}

But the lack of commutativity shows here: left âŠ•-excluded middle is
equivalent to excluded middle.

Notice that this doesn't use ğŸ˜-elim:

\begin{code}

âŠ•-Curry-Howard-EM-left-gives-Curry-Howard-EM : ((P : Type) â†’ Â¬ P âŠ• P) â†’ Curry-Howard-EM
âŠ•-Curry-Howard-EM-left-gives-Curry-Howard-EM e P = e (P + Â¬ P) (Î» Ï† â†’ inr (Î» p â†’ Ï† (inl p)))

\end{code}

Notice also that Â¬ P âŠ• P is (Â¬ P â†’ P) â†’ P, which is a particular
case of Peirce's Law with an empty type.

\begin{code}

Curry-Howard-EM-gives-âŠ•-Curry-Howard-EM-left : Curry-Howard-EM â†’ (P : Type) â†’ Â¬ P âŠ• P
Curry-Howard-EM-gives-âŠ•-Curry-Howard-EM-left em P = more-generally P (em P)
 where
  more-generally : (P : Type) â†’ is-decidable P â†’ Â¬ P âŠ• P
  more-generally P (inl p) = Î» Ï† â†’ p
  more-generally P (inr u) = Î» Ï† â†’ Ï† u

\end{code}

Notice, however, that we do have weak excluded middle for our
asymmetric notion of disjunction, on both sides, but we already know
the right case:

\begin{code}

âŠ•-wem-left : (P : Type) â†’ Â¬ (Â¬ P) âŠ• Â¬ P
âŠ•-wem-left P = Î» Ï† p â†’ Ï† (Î» u â†’ u p) p

\end{code}

Curry-Howard disjunction agrees with weak Dummet disjunction iff
excluded middle holds:

\begin{code}

agreement-gives-Curry-Howard-EM : ((P Q : Type) â†’ P âŠ• Q â†’ P + Q) â†’ Curry-Howard-EM
agreement-gives-Curry-Howard-EM f P = f P (Â¬ P) (âŠ•-em-right P)

Curry-Howard-EM-gives-agreement : Curry-Howard-EM â†’ (P Q : Type) â†’ P âŠ• Q â†’ P + Q
Curry-Howard-EM-gives-agreement em P Q = more-generally P Q (em P)
 where
  more-generally : (P Q : Type) â†’ is-decidable P â†’ P âŠ• Q â†’ P + Q
  more-generally P Q (inl p) Ï† = inl p
  more-generally P Q (inr u) Ï† = inr (Ï† (Î» p â†’ ğŸ˜-elim (u p)))

\end{code}

Interestingly, also the commutativity of âŠ• is equivalent to excluded middle:

\begin{code}

âŠ•-commutative : Typeâ‚
âŠ•-commutative = (P Q : Type) â†’ P âŠ• Q â†’ Q âŠ• P

âŠ•-commutative-gives-Curry-Howard-EM : âŠ•-commutative â†’ Curry-Howard-EM
âŠ•-commutative-gives-Curry-Howard-EM c P = c (P + Â¬ P) (Â¬ P) (Î» Ï† p â†’ Ï† (inl p) p) inr

\end{code}

We also have, of course:

\begin{code}

equivalent-to-classical : Curry-Howard-EM â†’ (P Q : Type) â†’ Â¬ (Â¬ P Ã— Â¬ Q) â†’ P âŠ• Q
equivalent-to-classical em P Q = more-generally P Q (em P) (em Q)
 where
  more-generally : (P Q : Type) â†’ is-decidable P â†’ is-decidable Q â†’ Â¬ (Â¬ P Ã— Â¬ Q) â†’ P âŠ• Q
  more-generally P Q (inl p) e v w = w p
  more-generally P Q (inr p) (inl q) v w = q
  more-generally P Q (inr p) (inr q) v w = ğŸ˜-elim (v ((Î» p â†’ q (w p)) , q))

\end{code}

The Skolem and Dummet properties also hold for our weakened notion
of Dummet disjunction:

\begin{code}

âŠ•-skolem : (A B C : Type) â†’ (A â†’ B âŠ• C) â†’ (A â†’ B) âŠ• (A â†’ C)
âŠ•-skolem A B C h = Î» Ï† a â†’ h a (Î» b â†’ Ï† (Î» _ â†’ b) a)

âŠ•-dummett : (A B C : Type) â†’ (A Ã— B â†’ C) â†’ (A â†’ C) âŠ• (B â†’ C)
âŠ•-dummett A B C h = Î» Ï† b â†’ Ï† (Î» a â†’ h (a , b)) b

\end{code}

Added April 2016:

We can apply the same idea to the existential quantifier.

Given a family of propositions P : X â†’ Type, we consider

   (i : X) â†’ ((Î£ x ê‰ X , P x) â†’ P i) â†’ P i

which is equivalent to

   (i : X) â†’ ((x : X) â†’ P x â†’ P i) â†’ P i

A Dummet existential quantifier:

\begin{code}

D : {X : Type} (P : X â†’ Type) â†’ Type
D P = âˆ€ i â†’ (âˆ€ x â†’ P x â†’ P i) â†’ P i

\end{code}

If X is empty then D P holds, but Â¬ (âˆ€ x â†’ Â¬ (P x)) fails because
âˆ€ x â†’ Â¬ (P x) holds vacuously.  However D strengthens the classical
existential quantifier for X non-empty:

\begin{code}

D-stronger-than-classical : {X : Type} (P : X â†’ Type) â†’ Â¬Â¬ X â†’ D P â†’ Â¬ (âˆ€ x â†’ Â¬ P x)
D-stronger-than-classical P ne d u = ne (Î» i â†’ u i (d i (Î» x p â†’ ğŸ˜-elim (u x p))))

\end{code}

More slowly:

\begin{code}
 where
  a : âˆ€ i x â†’ P x â†’ P i
  a i x p = ğŸ˜-elim (u x p)

  b : âˆ€ i â†’ P i
  b i = d i (a i)

  c : âˆ€ i â†’ ğŸ˜
  c i = u i (b i)

  g : ğŸ˜
  g = ne c

D-lin : {X : Type} (P : X â†’ Type) â†’ D (Î» i â†’ D (Î» x â†’ P i â†’ P x))
D-lin P i _ _ f = f i (Î» p â†’ p)

\end{code}
