Martin Escardo, 15 August 2014, with additions 23 January 2021.

Higgs' Involution Theorem. In any topos, if f : Î© â†’ Î© is a
monomorphism, then it is an involution.

We adapt and prove the result in univalent mathematics, using
propositional and functional extensionality. (We don't rely on
propositional resizing (or impredicativity).)

There is a proof by diagram chasing with iterated pullbacks, in page
65 of Johnstone's Sketches of an Elephant, volume 1.

The proof given here is based on an exercise in page 160 of Lambek and
Scott's Introduction to Higher-Order Categorical Logic, attributed to
Scedrov. Thanks to Phil Scott for bringing my attention to this proof
during a visit to Birmingham.

Added 23 Jan 2021. From a group structure on Î© we get excluded middle,
as an application of Higgs Theorem. This doesn't seems to be known in
the topos theory community.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness --auto-inline #-}

open import MLTT.Spartan
open import UF.Base
open import UF.Subsingletons renaming (Î© to Î©' ; âŠ¤Î© to âŠ¤ ; âŠ¥Î© to âŠ¥)
open import UF.FunExt
open import UF.Subsingletons-FunExt

\end{code}

We work with a universe ğ“¤ and assume functional and propositional
extensionality:

\begin{code}

module Various.HiggsInvolutionTheorem
        {ğ“¤ : Universe}
        (fe : Fun-Ext)
        (pe : propext ğ“¤)
       where

\end{code}

We work with Î© of universe ğ“¤:

\begin{code}

Î© = Î©' ğ“¤

\end{code}

Recall that a map f is left-cancellable if f p ï¼ f q â†’ p ï¼ q, and
involutive if f (f p) ï¼ p.

\begin{code}

higgs : (f : Î© â†’ Î©) â†’ left-cancellable f â†’ involutive f
higgs f lc = VIII
  where
   I : (p : Î©) â†’ f p ï¼ âŠ¤ â†’ p ï¼ âŠ¤ â†’ f âŠ¤ ï¼ âŠ¤
   I p r s = transport (Î» - â†’ f - ï¼ âŠ¤) s r

   II : (p : Î©) â†’ f p ï¼ âŠ¤ â†’ f âŠ¤ ï¼ âŠ¤ â†’ p ï¼ âŠ¤
   II p r s = lc (f p ï¼âŸ¨ r âŸ©
                  âŠ¤   ï¼âŸ¨ s â»Â¹ âŸ©
                  f âŠ¤ âˆ)

   III : (p : Î©) â†’ f p ï¼ âŠ¤ â†’ p ï¼ f âŠ¤
   III p r = Î©-ext pe fe (I p r) (II p r)

   IV : (p : Î©) â†’ f (f p) ï¼ âŠ¤ â†’ p ï¼ âŠ¤
   IV p r = lc (III (f p) r)

   V : (p : Î©) â†’ f (f (f p)) ï¼ âŠ¤ â†’ f p ï¼ âŠ¤
   V p = IV (f p)

   VI : (p : Î©) â†’ f p ï¼ âŠ¤ â†’ f (f (f p)) ï¼ âŠ¤
   VI p r = iv âˆ™ r
    where
     i : f (f p) ï¼ f âŠ¤
     i = ap f r

     ii : f âŠ¤ ï¼ p
     ii = (III p r)â»Â¹

     iii : f (f p) ï¼ p
     iii = i âˆ™ ii

     iv : f (f (f p)) ï¼ f p
     iv = ap f iii

   VII : (p : Î©) â†’ f (f (f p)) ï¼ f p
   VII p = Î©-ext pe fe (V p) (VI p)

   VIII : (p : Î©) â†’ f (f p) ï¼ p
   VIII p = lc (VII p)

\end{code}

Added 23 Jan 2021. From a group structure on Î© we get excluded middle,
as an application of Higgs Theorem. This doesn't seems to be known in
the topos theory community. I've written a blog post about this here:

https://homotopytypetheory.org/2021/01/23/can-the-type-of-truth-values-be-given-the-structure-of-a-group/

Such a group structure is necessarily abelian.

Moreover, any left-cancellable monoid structure (_âŠ•_ , O) on Î© is an
abelian group structure with p âŠ• p = O for all p : Î©, that is, such
that every element is its own inverse.

To define negation on Î© we need function extensionality, which we are
assuming in this module. We introduce friendlier notation for it:

\begin{code}

â‡_ : Î© â†’ Î©
â‡_ = not fe

â‡â‡_ : Î© â†’ Î©
â‡â‡ p = â‡(â‡ p)

open import UF.ExcludedMiddle

lc-monoid-structure-on-Î©-gives-EM : (O : Î©)
                                    (_âŠ•_ : Î© â†’ Î© â†’ Î©)
                                  â†’ left-neutral O _âŠ•_
                                  â†’ right-neutral O _âŠ•_
                                  â†’ associative _âŠ•_
                                  â†’ ((p : Î©) â†’ left-cancellable (p âŠ•_))
                                  â†’ excluded-middle ğ“¤
lc-monoid-structure-on-Î©-gives-EM O _âŠ•_ left-neutral right-neutral assoc lc = Î³
 where
  invol : (p : Î©) â†’ involutive (p âŠ•_)
  invol p = higgs (p âŠ•_) (lc p)

  own-inv : (p : Î©) â†’ p âŠ• p ï¼ O
  own-inv p = p âŠ• p       ï¼âŸ¨ (right-neutral (p âŠ• p))â»Â¹ âŸ©
              (p âŠ• p) âŠ• O ï¼âŸ¨ assoc p p O âŸ©
              p âŠ• (p âŠ• O) ï¼âŸ¨ invol p O âŸ©
              O           âˆ

  to-ï¼ : {p q : Î©} â†’ p âŠ• q ï¼ O â†’ p ï¼ q
  to-ï¼ {p} {q} e = p           ï¼âŸ¨ (right-neutral p)â»Â¹ âŸ©
                   p âŠ• O       ï¼âŸ¨ ap (p âŠ•_) (e â»Â¹) âŸ©
                   p âŠ• (p âŠ• q) ï¼âŸ¨ (assoc p p q)â»Â¹ âŸ©
                   (p âŠ• p) âŠ• q ï¼âŸ¨ ap (_âŠ• q) (own-inv p) âŸ©
                   O âŠ• q       ï¼âŸ¨ left-neutral q âŸ©
                   q           âˆ

  f : Î© â†’ Î©
  f p = p âŠ• (âŠ¥ âŠ• âŠ¤)

  f-invol : involutive f
  f-invol p = f (f p)                 ï¼âŸ¨ refl âŸ©
              (p âŠ• (âŠ¥ âŠ• âŠ¤)) âŠ• (âŠ¥ âŠ• âŠ¤) ï¼âŸ¨ assoc p (âŠ¥ âŠ• âŠ¤) (âŠ¥ âŠ• âŠ¤) âŸ©
              p âŠ• ((âŠ¥ âŠ• âŠ¤) âŠ• (âŠ¥ âŠ• âŠ¤)) ï¼âŸ¨ ap (p âŠ•_) (own-inv (âŠ¥ âŠ• âŠ¤)) âŸ©
              p âŠ• O                   ï¼âŸ¨ right-neutral p âŸ©
              p                       âˆ

  Î± : (p : Î©) â†’ f p ï¼ âŠ¤ â†’ p ï¼ âŠ¥
  Î± p e = to-ï¼ (p âŠ• âŠ¥             ï¼âŸ¨ (right-neutral (p âŠ• âŠ¥))â»Â¹ âŸ©
                (p âŠ• âŠ¥) âŠ• O       ï¼âŸ¨ ap ((p âŠ• âŠ¥) âŠ•_) ((own-inv âŠ¤)â»Â¹) âŸ©
                (p âŠ• âŠ¥) âŠ• (âŠ¤ âŠ• âŠ¤) ï¼âŸ¨ (assoc (p âŠ• âŠ¥) âŠ¤ âŠ¤)â»Â¹ âŸ©
                ((p âŠ• âŠ¥) âŠ• âŠ¤) âŠ• âŠ¤ ï¼âŸ¨ ap (_âŠ• âŠ¤) (assoc p âŠ¥ âŠ¤) âŸ©
                (p âŠ• (âŠ¥ âŠ• âŠ¤)) âŠ• âŠ¤ ï¼âŸ¨ refl âŸ©
                f p âŠ• âŠ¤           ï¼âŸ¨ ap (_âŠ• âŠ¤) e âŸ©
                âŠ¤ âŠ• âŠ¤             ï¼âŸ¨ own-inv âŠ¤ âŸ©
                O                 âˆ)

  Î² : (p : Î©) â†’ p ï¼ âŠ¥ â†’ f p ï¼ âŠ¤
  Î² p e = f p         ï¼âŸ¨ refl âŸ©
          p âŠ• (âŠ¥ âŠ• âŠ¤) ï¼âŸ¨ (assoc p âŠ¥ âŠ¤)â»Â¹ âŸ©
          (p âŠ• âŠ¥) âŠ• âŠ¤ ï¼âŸ¨ ap (Î» - â†’ (- âŠ• âŠ¥) âŠ• âŠ¤) e âŸ©
          (âŠ¥ âŠ• âŠ¥) âŠ• âŠ¤ ï¼âŸ¨ ap (_âŠ• âŠ¤) (own-inv âŠ¥) âŸ©
          O âŠ• âŠ¤       ï¼âŸ¨ left-neutral âŠ¤ âŸ©
          âŠ¤           âˆ

  characterization-of-f : (p : Î©) â†’ f p ï¼ â‡ p
  characterization-of-f p = Î©-ext pe fe a b
   where
    a : f p ï¼ âŠ¤ â†’ (â‡ p) ï¼ âŠ¤
    a e = equal-âŠ¥-gives-not-equal-âŠ¤ fe pe p (Î± p e)

    b : (â‡ p) ï¼ âŠ¤ â†’ f p ï¼ âŠ¤
    b e = Î² p (not-equal-âŠ¤-gives-equal-âŠ¥ fe pe p e)

  Î½ : (p : Î©) â†’ (â‡â‡ p) ï¼ p
  Î½ p = â‡â‡ p      ï¼âŸ¨ ap â‡_ ((characterization-of-f p)â»Â¹) âŸ©
        (â‡ (f p)) ï¼âŸ¨ (characterization-of-f (f p))â»Â¹ âŸ©
        f (f p)   ï¼âŸ¨ f-invol p âŸ©
        p         âˆ

  Î´ : (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ P
  Î´ P i = Idtofun (ap _holds (Î½ (P , i)))

  Î³ : excluded-middle ğ“¤
  Î³ = DNE-gives-EM fe Î´

\end{code}

Additional facts that are not needed to conclude excluded middle:

\begin{code}

  from-ï¼ : (p q : Î©) â†’ p ï¼ q â†’ p âŠ• q ï¼ O
  from-ï¼ p q e = p âŠ• q ï¼âŸ¨ ap (_âŠ• q) e âŸ©
                 q âŠ• q ï¼âŸ¨ own-inv q âŸ©
                 O     âˆ

  abelian : (p q : Î©) â†’ p âŠ• q ï¼ q âŠ• p
  abelian p q = to-ï¼ ((p âŠ• q) âŠ• (q âŠ• p) ï¼âŸ¨ assoc p q (q âŠ• p) âŸ©
                      p âŠ• (q âŠ• (q âŠ• p)) ï¼âŸ¨ ap (p âŠ•_) ((assoc q q p)â»Â¹) âŸ©
                      p âŠ• ((q âŠ• q) âŠ• p) ï¼âŸ¨ ap (Î» - â†’ p âŠ• (- âŠ• p)) (own-inv q) âŸ©
                      p âŠ• (O âŠ• p)       ï¼âŸ¨ ap (p âŠ•_) (left-neutral p) âŸ©
                      p âŠ• p             ï¼âŸ¨ own-inv p âŸ©
                      O                 âˆ)

  characâ‚‚-of-f : (p : Î©) â†’ f p ï¼ (âŠ¥ âŠ• âŠ¤) âŠ• p
  characâ‚‚-of-f p = abelian p (âŠ¥ âŠ• âŠ¤)

  f-invol' : involutive f
  f-invol' p = f (f p)                   ï¼âŸ¨ characâ‚‚-of-f (f p) âŸ©
               ((âŠ¥ âŠ• âŠ¤) âŠ• f p)           ï¼âŸ¨ ap ((âŠ¥ âŠ• âŠ¤) âŠ•_) (characâ‚‚-of-f p) âŸ©
               ((âŠ¥ âŠ• âŠ¤) âŠ• ((âŠ¥ âŠ• âŠ¤) âŠ• p)) ï¼âŸ¨ higgs ((âŠ¥ âŠ• âŠ¤) âŠ•_) (lc (âŠ¥ âŠ• âŠ¤)) p âŸ©
               p âˆ

\end{code}

This shows that any cancellative monoid structure on Î© is
automatically an abelian group structure (which is not very surprising
given that we have already established excluded middle, but justifies
our additive notation).
