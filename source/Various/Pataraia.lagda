Martin Escardo, 26 Feb 2023

We implement Pataraia's result that every monotone endomap of a dcpo
(directed complete poset) with a least element has a least fixed point
in topos logic. This is Corollary-2Â·1 below.

Pataraia [1] was the first to give a constructive proof of this in
topos logic. A version of his proof is published in [2] by Escardo,
with Pataraia's permission. Pataraia himself didn't publish the
result. An earlier, less general, theorem was proved by Coquand [3]
for *bounded complete* dcpos, with an easier proof.

The proof given here follows [2] closely and is impredicative (as is
the one given in [3]). For a predicative version, see the module
Various.Pataraia-Taylor.

[1] Dito Pataraia. A constructive proof of Tarskiâ€™s fixed-point
    theorem for dcpoâ€™s. Presented at the 65th Peripatetic Seminar on
    Sheaves and Logic, Aarhus, Denmark, November 1997.

[2] Martin Escardo. Joins in the frame of nuclei.
    Applied Categorical Structures 11: 117â€“124, 2003.
    https://doi.org/10.1023/A:1023555514029

[3] Thierry Coquand. A topos theoretic fix point theorem.
    Unpublished manuscript, June 1995.
    https://web.archive.org/web/20110822085930/
    https://www.cse.chalmers.se/~coquand/fix.pdf

We prove the following from [2].

  Lemma 2Â·1.     The set of all monotone inflationary maps on any dcpo
                 has a common fixed point.

  Theorem 2Â·2.   Any set F of monotone inflationary maps on a dcpo ğ““
                 with a least element âŠ¥ has a least common fixed
                 point.

                 Moreover, any subset of D that has âŠ¥ as a member, is
                 closed under every f in F, and is closed under
                 directed suprema has the least common fixed point as
                 a member.

  Corollary 2Â·1. Any monotone endomap of a dcpo with âŠ¥ has a least
                 fixed point.

Notice that Corollary 2.1 doesn't generalize from single monotone maps
to sets of monotone maps, as exemplified by any poset with a least
element and two maximal elements and by the two constant endomaps that
fix each of these two maximal elements.

I am not sure why the publisher gave the number 2.1 to the corollary
that follows Theorem 2.2.

One minor difference here is that we work with families of functions
rather than sets of functions, which is more natural in dependent type
theory.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc

module Various.Pataraia
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¤ : Universe)
       where

open PropositionalTruncation pt

open import DomainTheory.Basics.Dcpo pt fe ğ“¤
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import UF.Sets
open import UF.Sets-Properties
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

\end{code}

The following is Lemma 2.1 of [2].

\begin{code}

module lemmaâ‚‚Â·â‚ (ğ“” : DCPO {ğ“¤} {ğ“¤}) where

 private
  E   = âŸ¨ ğ“” âŸ©
  _âŠ‘_ = underlying-order ğ“”

\end{code}

We now define the pointed type MI of monotone inflationary endomaps of
the underlying set E of the dcpo ğ“”. Notice that E is allowed to be
empty.

\begin{code}

 MI : ğ“¤ Ì‡
 MI = Î£ f ê‰ (E â†’ E) , is-monotone ğ“” ğ“” f Ã— is-inflationary ğ“” f

 ğ•šğ•• : MI
 ğ•šğ•• = id , id-is-monotone ğ“” , id-is-inflationary ğ“”

\end{code}

We use the following auxiliary function Î“ : E â†’ MI â†’ E to define a
function Î³ : E â†’ E with suitable properties. For each x : E we get a
directed family Î“ x : MI â†’ E, and we define Î³ x to be the supremum of
this family.

\begin{code}

 Î“ : E â†’ MI â†’ E
 Î“ x (f , _) = f x

 Î“-is-semidirected : (x : E) â†’ is-Semidirected ğ“” (Î“ x)
 Î“-is-semidirected x ğ•—@(f , fm , fi) ğ•˜@(g , gm , gi) = âˆ£ ğ•™ , f-le-h , g-le-h âˆ£
  where
   h = g âˆ˜ f

   ğ•™ : MI
   ğ•™ = (h , âˆ˜-is-monotone ğ“” ğ“” ğ“” f g fm gm , âˆ˜-is-inflationary ğ“” f g fi gi)

   f-le-h : f x âŠ‘ h x
   f-le-h = gi (f x)

   g-le-h : g x âŠ‘ h x
   g-le-h = gm x (f x) (fi x)

 Î“-is-directed : (x : E) â†’ is-Directed ğ“” (Î“ x)
 Î“-is-directed x = âˆ£ ğ•šğ•• âˆ£ , Î“-is-semidirected x

 Î³ : E â†’ E
 Î³ x = âˆ ğ“” (Î“-is-directed x)

\end{code}

So the function Î³ is the pointwise supremum of the monotone
inflationary endomaps of E.

\begin{code}

 Î³-is-monotone : is-monotone ğ“” ğ“” Î³
 Î³-is-monotone x y l = âˆ-is-monotone ğ“” (Î“-is-directed x) (Î“-is-directed y) l'
  where
   l' : (ğ•— : MI) â†’ Î“ x ğ•— âŠ‘ Î“ y ğ•—
   l' (f , fm , fi) = fm x y l

\end{code}

From this it is easy to conclude that Î³ is the greatest monotone
inflationary map for any x : E.

\begin{code}

 Î³-is-greatest-mi-map : ((f , fm , fi) : MI) (x : E) â†’ f x âŠ‘ Î³ x
 Î³-is-greatest-mi-map ğ•— x = âˆ-is-upperbound ğ“” (Î“-is-directed x) ğ•—

 Î³-is-inflationary : is-inflationary ğ“” Î³
 Î³-is-inflationary = Î³-is-greatest-mi-map ğ•šğ••

\end{code}

And, in turn, from this we conclude that Î³ x is a fixed point of any
monotone inflationary map f : E â†’ E.

\begin{code}

 Î³-is-fixed-point : ((f , fm , fi) : MI) (x : E) â†’ f (Î³ x) ï¼ Î³ x
 Î³-is-fixed-point (f , fm , fi) x = antisymmetry ğ“” _ _ I II
  where
   I : f (Î³ x) âŠ‘ Î³ x
   I = Î³-is-greatest-mi-map
        (f âˆ˜ Î³ ,
         âˆ˜-is-monotone ğ“” ğ“” ğ“” Î³ f Î³-is-monotone fm ,
         âˆ˜-is-inflationary ğ“” Î³ f Î³-is-inflationary fi)
       x

   II : Î³ x âŠ‘ f (Î³ x)
   II = fi (Î³ x)

\end{code}

This concludes the proof of Lemma 2Â·1, which we use to
prove Theorem 2Â·2.

\begin{code}

module theoremâ‚‚Â·â‚‚
        (Ï : Propositional-Resizing)
        (ğ““ : DCPO {ğ“¤} {ğ“¤})
        ((âŠ¥ , âŠ¥-is-least) : has-bottom ğ““)
        (I : ğ“¤ Ì‡ )
        (f : I â†’ âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©)
        (fm : (i : I) â†’ is-monotone ğ““ ğ““ (f i))
        (fi : (i : I) â†’ is-inflationary ğ““ (f i))
       where

 private
  D   = âŸ¨ ğ““ âŸ©
  _âŠ‘_ = underlying-order ğ““

 open import UF.Logic
 open import UF.Powerset
 open import UF.Powerset-Resizing fe Ï
 open import UF.SubtypeClassifier hiding (âŠ¥)

 open AllCombinators pt fe

 Câ‚ : (D â†’ Î© ğ“¤) â†’ Î© (ğ“¤ âº)
 Câ‚ = is-closed-under-directed-supsâ‚š ğ““

 Câ‚‚ : (D â†’ Î© ğ“¤) â†’ Î© ğ“¤
 Câ‚‚ = Î» A â†’ âŠ¥ âˆˆâ‚š A

 Câ‚ƒ : (D â†’ Î© ğ“¤) â†’ Î© ğ“¤
 Câ‚ƒ = Î» A â†’ â±¯ i ê‰ I , â±¯ x ê‰ D , x âˆˆâ‚š A â‡’ f i x âˆˆâ‚š A

 C : (D â†’ Î© ğ“¤) â†’ Î© (ğ“¤ âº)
 C = Î» A â†’ A âˆˆâ‚š Câ‚ âˆ§ A âˆˆâ‚š Câ‚‚ âˆ§ A âˆˆâ‚š Câ‚ƒ

 Bâ‚ : â‹‚ C âˆˆ Câ‚
 Bâ‚ {I} Î± Î´ Îº =
  to-â‹‚ C (âˆ ğ““ Î´)
  (Î» A c@(câ‚ , câ‚‚ , câ‚ƒ) â†’ câ‚ Î± Î´ (Î» (i : I) â†’ from-â‹‚ C (Î± i) (Îº i) A c))

 Bâ‚‚ : â‹‚ C âˆˆ Câ‚‚
 Bâ‚‚ = to-â‹‚ C âŠ¥ (Î» A (câ‚ , câ‚‚ , câ‚ƒ) â†’ câ‚‚)

 Bâ‚ƒ : â‹‚ C âˆˆ Câ‚ƒ
 Bâ‚ƒ i x x-in-â‹‚ =
  to-â‹‚ C
   (f i x)
   (Î» A c@(câ‚ , câ‚‚ , câ‚ƒ) â†’ câ‚ƒ i x (from-â‹‚ C x x-in-â‹‚ A c))

 B : â‹‚ C âˆˆ C
 B = Bâ‚ , Bâ‚‚ , Bâ‚ƒ

 private
  ğ“” : DCPO
  ğ“” = subdcpoâ‚š ğ““ (â‹‚ C) Bâ‚

  E = âŸ¨ ğ“” âŸ©
  _â‰¤_ : E â†’ E â†’ ğ“¤ Ì‡
  s â‰¤ t = s âŠ‘âŸ¨ ğ“” âŸ© t

  Î¹ : E â†’ D
  Î¹ (x , c) = x

  Ï„ : (t : E) â†’ Î¹ t âˆˆ â‹‚ C
  Ï„ (x , c) = c

  âŠ¥ğ“” : E
  âŠ¥ğ“” =  âŠ¥ , Bâ‚‚

\end{code}

The monotone inflationary functions fáµ¢ : D â†’ D restrict to monotone
inflationary functions ğ“¯áµ¢ : E â†’ E.

\begin{code}

 private
  ğ“¯ : I â†’ E â†’ E
  ğ“¯ i (x , c) = f i x , Bâ‚ƒ i x c

  ğ“¯-is-monotone : (i : I) (s t : E) â†’ s â‰¤ t â†’ ğ“¯ i s â‰¤ ğ“¯ i t
  ğ“¯-is-monotone i (x , _) (y , _) = fm i x y

  ğ“¯-is-inflationary : (i : I) (t : E) â†’ t â‰¤ ğ“¯ i t
  ğ“¯-is-inflationary i (x , c) = fi i x

\end{code}

So now we can apply lemmaâ‚‚Â·â‚.

\begin{code}

 open lemmaâ‚‚Â·â‚ ğ“”

 ğ•— : I â†’ MI
 ğ•— i = (ğ“¯ i , ğ“¯-is-monotone i , ğ“¯-is-inflationary i)

 eâ‚€ : E
 eâ‚€ = Î³ âŠ¥ğ“”

 eâ‚€-is-fp : (i : I) â†’ ğ“¯ i eâ‚€ ï¼ eâ‚€
 eâ‚€-is-fp i = Î³-is-fixed-point (ğ•— i) âŠ¥ğ“”

 dâ‚€ : D
 dâ‚€ = Î¹ eâ‚€

\end{code}

dâ‚€ is a common fixed point of the family f.

\begin{code}

 dâ‚€-is-fp : (i : I) â†’ f i dâ‚€ ï¼ dâ‚€
 dâ‚€-is-fp i = ap Î¹ (eâ‚€-is-fp i)

 fp-induction : (S : D â†’ Î© ğ“¤) â†’ S âˆˆ C â†’ dâ‚€ âˆˆ S
 fp-induction = from-â‹‚ C dâ‚€ (Ï„ eâ‚€)

\end{code}

dâ‚€ is the least common pre-fixed point of the family f.

\begin{code}

 dâ‚€-is-lpfp : (x : D) â†’ ((i : I) â†’ f i x âŠ‘ x) â†’ dâ‚€ âŠ‘ x
 dâ‚€-is-lpfp x le = fp-induction S S-in-C
  where
   S : D â†’ Î© ğ“¤
   S = Î» (d : D) â†’ (d âŠ‘ x , prop-valuedness ğ““ d x)

   S-in-C : S âˆˆ C
   S-in-C =
    (Î» Î± Î´ Î±-in-S â†’ âˆ-is-lowerbound-of-upperbounds ğ““ Î´ x Î±-in-S) ,
    âŠ¥-is-least x ,
    (Î» i d d-in-S â†’ f i d âŠ‘âŸ¨ ğ““ âŸ©[ fm i d x d-in-S ]
                    f i x âŠ‘âŸ¨ ğ““ âŸ©[ le i ]
                    x     âˆâŸ¨ ğ““ âŸ©)

\end{code}

And so it is the least common fixed point.

\begin{code}

 dâ‚€-is-lfp : (x : D) â†’ ((i : I) â†’ f i x ï¼ x) â†’ dâ‚€ âŠ‘ x
 dâ‚€-is-lfp x e = dâ‚€-is-lpfp x (Î» i â†’ ï¼-to-âŠ‘ ğ““ (e i))

\end{code}

This concludes the proof of Theorem 2Â·2, which has the following
corollary.

\begin{code}

module corollaryâ‚‚Â·â‚
        (Ï : Propositional-Resizing)
        (ğ“’ : DCPO {ğ“¤} {ğ“¤})
        ((âŠ¥ , âŠ¥-is-least) : has-bottom ğ“’)
        (f : âŸ¨ ğ“’ âŸ© â†’ âŸ¨ ğ“’ âŸ©)
        (fm : is-monotone ğ“’ ğ“’ f)
       where

 ğ““ : DCPO
 ğ““ = subdcpo ğ“’
      (Î» x â†’ x âŠ‘âŸ¨ ğ“’ âŸ© f x)
      (Î» x â†’ prop-valuedness ğ“’ x (f x))
      (Î» {I} Î± Î´ le â†’
        âˆ-is-lowerbound-of-upperbounds ğ“’ Î´
         (f (âˆ ğ“’ Î´))
         (Î» i â†’ Î± i       âŠ‘âŸ¨ ğ“’ âŸ©[ le i ]
                f (Î± i)   âŠ‘âŸ¨ ğ“’ âŸ©[ fm (Î± i) (âˆ ğ“’ Î´) (âˆ-is-upperbound ğ“’ Î´ i) ]
                f (âˆ ğ“’ Î´) âˆâŸ¨ ğ“’ âŸ©))

 ğ“¯ : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©
 ğ“¯ (x , le) = f x , fm x (f x) le

 ğ“¯-is-monotone : (s t : âŸ¨ ğ““ âŸ©) â†’ s âŠ‘âŸ¨ ğ““ âŸ© t â†’ ğ“¯ s âŠ‘âŸ¨ ğ““ âŸ© ğ“¯ t
 ğ“¯-is-monotone (x , _) (y , _) = fm x y

 ğ“¯-is-inflationary : (t : âŸ¨ ğ““ âŸ©) â†’ t âŠ‘âŸ¨ ğ““ âŸ© ğ“¯ t
 ğ“¯-is-inflationary (x , c) = c

 ğ““-has-bottom : has-bottom ğ““
 ğ““-has-bottom = (âŠ¥ , âŠ¥-is-least (f âŠ¥)) , (Î» (x , _) â†’ âŠ¥-is-least x)

 open theoremâ‚‚Â·â‚‚ Ï ğ““ ğ““-has-bottom ğŸ™
       (Î» _ â†’ ğ“¯)
       (Î» _ â†’ ğ“¯-is-monotone)
       (Î» _ â†’ ğ“¯-is-inflationary)

 Î¹ : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“’ âŸ©
 Î¹ (x , le) = x

 Ï„ : (d : âŸ¨ ğ““ âŸ©) â†’ d âŠ‘âŸ¨ ğ““ âŸ© ğ“¯ d
 Ï„ (x , le) = le

 câ‚€ : âŸ¨ ğ“’ âŸ©
 câ‚€ = Î¹ dâ‚€

 câ‚€-is-fp : f câ‚€ ï¼ câ‚€
 câ‚€-is-fp = ap Î¹ (dâ‚€-is-fp â‹†)

 câ‚€-is-lfp : (c : âŸ¨ ğ“’ âŸ©) â†’ f c ï¼ c â†’ câ‚€ âŠ‘âŸ¨ ğ“’ âŸ© c
 câ‚€-is-lfp c e = dâ‚€-is-lfp
                  (c , ï¼-to-âŠ‘ ğ“’ (e â»Â¹))
                  (Î» _ â†’ to-subtype-ï¼ (Î» x â†’ prop-valuedness ğ“’ x (f x)) e)
\end{code}

This concludes the proof of Corollary 2Â·1, which we repackage as
follows.

\begin{code}

Pataraia : Propositional-Resizing
         â†’ (ğ“’ : DCPO {ğ“¤} {ğ“¤})
         â†’ has-bottom ğ“’
         â†’ (f : âŸ¨ ğ“’ âŸ© â†’ âŸ¨ ğ“’ âŸ©)
         â†’ is-monotone ğ“’ ğ“’ f
         â†’ Î£ câ‚€ ê‰ âŸ¨ ğ“’ âŸ©
                , (f câ‚€ ï¼ câ‚€)
                Ã— ((c : âŸ¨ ğ“’ âŸ©) â†’ f c ï¼ c â†’ câ‚€ âŠ‘âŸ¨ ğ“’ âŸ© c)
Pataraia Ï ğ“’ hb f fm = câ‚€ , câ‚€-is-fp , câ‚€-is-lfp
 where
  open corollaryâ‚‚Â·â‚ Ï ğ“’ hb f fm

\end{code}

See the module Various.Pataraia-Taylor for a proof that doesn't assume
propositional resizing.
