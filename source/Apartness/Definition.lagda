Martin Escardo, 26 January 2018.

Moved from the file TotallySeparated 22 August 2024.

Definition of apartness relation and basic general facts.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Apartness.Definition where

open import MLTT.Spartan
open import UF.DiscreteAndSeparated hiding (tight)
open import UF.FunExt
open import UF.Lower-FunExt
open import UF.NotNotStablePropositions
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

is-prop-valued
 is-irreflexive
 is-symmetric
 is-strongly-cotransitive
 is-tight
 is-strong-apartness
  : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡

is-prop-valued           _â™¯_ = âˆ€ x y â†’ is-prop (x â™¯ y)
is-irreflexive           _â™¯_ = âˆ€ x â†’ Â¬ (x â™¯ x)
is-symmetric             _â™¯_ = âˆ€ x y â†’ x â™¯ y â†’ y â™¯ x
is-strongly-cotransitive _â™¯_ = âˆ€ x y z â†’ x â™¯ y â†’ (x â™¯ z) + (y â™¯ z)
is-tight                 _â™¯_ = âˆ€ x y â†’ Â¬ (x â™¯ y) â†’ x ï¼ y
is-strong-apartness      _â™¯_ = is-prop-valued _â™¯_
                             Ã— is-irreflexive _â™¯_
                             Ã— is-symmetric _â™¯_
                             Ã— is-strongly-cotransitive _â™¯_

Strong-Apartness : ğ“¤ Ì‡ â†’ (ğ“¥ : Universe) â†’ ğ“¥ âº âŠ” ğ“¤ Ì‡
Strong-Apartness X ğ“¥ = Î£ _â™¯_ ê‰ (X â†’ X â†’ ğ“¥ Ì‡) , is-strong-apartness _â™¯_

\end{code}

Not-not equal elements are not apart, and hence, in the presence of
tightness, they are equal. It follows that tight apartness types are
sets.

\begin{code}

double-negation-of-equality-gives-negation-of-apartness
 : {X : ğ“¤ Ì‡ } (x y : X) (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
 â†’ is-irreflexive _â™¯_
 â†’ Â¬Â¬ (x ï¼ y)
 â†’ Â¬ (x â™¯ y)
double-negation-of-equality-gives-negation-of-apartness x y _â™¯_ i
 = contrapositive f
 where
  f : x â™¯ y â†’ Â¬ (x ï¼ y)
  f a refl = i y a

tight-types-are-Â¬Â¬-separated' : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                              â†’ is-irreflexive _â™¯_
                              â†’ is-tight _â™¯_
                              â†’ is-Â¬Â¬-separated X
tight-types-are-Â¬Â¬-separated' _â™¯_ i t = f
 where
  f : âˆ€ x y â†’ Â¬Â¬ (x ï¼ y) â†’ x ï¼ y
  f x y Ï† = t x y (double-negation-of-equality-gives-negation-of-apartness
                    x y _â™¯_ i Ï†)

tight-types-are-sets' : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                      â†’ funext ğ“¤ ğ“¤â‚€
                      â†’ is-irreflexive _â™¯_
                      â†’ is-tight _â™¯_
                      â†’ is-set X
tight-types-are-sets' _â™¯_ fe i t =
 Â¬Â¬-separated-types-are-sets fe (tight-types-are-Â¬Â¬-separated' _â™¯_ i t)

\end{code}

To define apartness we need to define (weak) cotransitivity, and for
this we need to assume the existence of propositional truncations.

\begin{code}

module Apartness (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 is-cotransitive is-apartness : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡

 is-cotransitive _â™¯_ = âˆ€ x y z â†’ x â™¯ y â†’ x â™¯ z âˆ¨ y â™¯ z
 is-apartness    _â™¯_ = is-prop-valued _â™¯_
                     Ã— is-irreflexive _â™¯_
                     Ã— is-symmetric _â™¯_
                     Ã— is-cotransitive _â™¯_

 Apartness : ğ“¤ Ì‡ â†’ (ğ“¥ : Universe) â†’ ğ“¥ âº âŠ” ğ“¤ Ì‡
 Apartness X ğ“¥ = Î£ _â™¯_ ê‰ (X â†’ X â†’ ğ“¥ Ì‡) , is-apartness _â™¯_

 apartness-is-prop-valued : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                          â†’ is-apartness _â™¯_
                          â†’ is-prop-valued _â™¯_
 apartness-is-prop-valued _â™¯_ (p , i , s , c) = p

 apartness-is-irreflexive : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                          â†’ is-apartness _â™¯_
                          â†’ is-irreflexive _â™¯_
 apartness-is-irreflexive _â™¯_ (p , i , s , c) = i

 apartness-is-symmetric : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                        â†’ is-apartness _â™¯_
                        â†’ is-symmetric _â™¯_
 apartness-is-symmetric _â™¯_ (p , i , s , c) = s

 apartness-is-cotransitive : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                           â†’ is-apartness _â™¯_
                           â†’ is-cotransitive _â™¯_
 apartness-is-cotransitive _â™¯_ (p , i , s , c) = c

 not-not-equal-not-apart : {X : ğ“¤ Ì‡ } (x y : X) (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                         â†’ is-apartness _â™¯_
                         â†’ Â¬Â¬ (x ï¼ y)
                         â†’ Â¬ (x â™¯ y)
 not-not-equal-not-apart x y _â™¯_ (_ , i , _ , _) =
  double-negation-of-equality-gives-negation-of-apartness x y _â™¯_ i

 tight-types-are-Â¬Â¬-separated : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                              â†’ is-apartness _â™¯_
                              â†’ is-tight _â™¯_
                              â†’ is-Â¬Â¬-separated X
 tight-types-are-Â¬Â¬-separated _â™¯_ (_ , i , _ , _) =
  tight-types-are-Â¬Â¬-separated' _â™¯_ i

 tight-types-are-sets : {X : ğ“¤ Ì‡ } (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
                      â†’ funext ğ“¤ ğ“¤â‚€
                      â†’ is-apartness _â™¯_
                      â†’ is-tight _â™¯_
                      â†’ is-set X
 tight-types-are-sets _â™¯_ fe (_ , i , _ , _) = tight-types-are-sets' _â™¯_ fe i

\end{code}

The above use apartness data, but its existence is enough, because
being a Â¬Â¬-separated type and being a set are propositions.

\begin{code}

 tight-separated' : funext ğ“¤ ğ“¤
                  â†’ {X : ğ“¤ Ì‡ }
                  â†’ (âˆƒ _â™¯_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), is-apartness _â™¯_ Ã— is-tight _â™¯_)
                  â†’ is-Â¬Â¬-separated X
 tight-separated' {ğ“¤} fe {X} = âˆ¥âˆ¥-rec (being-Â¬Â¬-separated-is-prop fe) f
   where
    f : (Î£ _â™¯_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), is-apartness _â™¯_ Ã— is-tight _â™¯_)
      â†’ is-Â¬Â¬-separated X
    f (_â™¯_ , a , t) = tight-types-are-Â¬Â¬-separated _â™¯_ a t

 tight-types-are-sets'' : funext ğ“¤ ğ“¤
                        â†’ {X : ğ“¤ Ì‡ }
                        â†’ (âˆƒ _â™¯_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), is-apartness _â™¯_ Ã— is-tight _â™¯_)
                        â†’ is-set X
 tight-types-are-sets'' {ğ“¤} fe {X} = âˆ¥âˆ¥-rec (being-set-is-prop fe) f
   where
    f : (Î£ _â™¯_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), is-apartness _â™¯_ Ã— is-tight _â™¯_) â†’ is-set X
    f (_â™¯_ , a , t) = tight-types-are-sets _â™¯_ (lower-funext ğ“¤ ğ“¤ fe) a t

\end{code}

The following is the standard equivalence relation induced by an
apartness relation. The tightness axiom defined above says that this
equivalence relation is equality.

\begin{code}

 is-equiv-rel : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-equiv-rel _â‰ˆ_ = is-prop-valued _â‰ˆ_
                  Ã— reflexive _â‰ˆ_
                  Ã— symmetric _â‰ˆ_
                  Ã— transitive _â‰ˆ_

 negation-of-apartness-is-equiv-rel : {X : ğ“¤ Ì‡ }
                                    â†’ funext ğ“¤ ğ“¤â‚€
                                    â†’ (_â™¯_ : X â†’ X â†’ ğ“¤ Ì‡ )
                                    â†’ is-apartness _â™¯_
                                    â†’ is-equiv-rel (Î» x y â†’ Â¬ (x â™¯ y))
 negation-of-apartness-is-equiv-rel {ğ“¤} {X} fe _â™¯_ (â™¯p , â™¯i , â™¯s , â™¯c)
  = p , â™¯i , s , t
  where
   p : (x y : X) â†’ is-prop (Â¬ (x â™¯ y))
   p x y = negations-are-props fe

   s : (x y : X) â†’ Â¬ (x â™¯ y) â†’ Â¬ (y â™¯ x)
   s x y u a = u (â™¯s y x a)

   t : (x y z : X) â†’ Â¬ (x â™¯ y) â†’ Â¬ (y â™¯ z) â†’ Â¬ (x â™¯ z)
   t x y z u v a = v (â™¯s z y (left-fails-gives-right-holds (â™¯p z y) b u))
    where
     b : (x â™¯ y) âˆ¨ (z â™¯ y)
     b = â™¯c x z y a

\end{code}
