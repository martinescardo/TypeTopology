Martin Escardo, 26 January 2018.

Moved from the file TotallySeparated 22 August 2024.

Every apartness relation has a tight reflection, in the categorical
sense of reflection, where the morphisms are strongly extensional
functions.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.PropTrunc

module Apartness.TightReflection
        (pt : propositional-truncations-exist)
       where

open import Apartness.Definition
open import Apartness.Morphisms
open import Apartness.NegationOfApartness pt
open import MLTT.Spartan
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.FunExt
open import UF.ImageAndSurjection pt
open import UF.Powerset hiding (ğ•‹)
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open PropositionalTruncation pt
open Apartness pt

module tight-reflection
        (fe : Fun-Ext)
        (pe : propext ğ“¥)
        (X : ğ“¤ Ì‡ )
        (_â™¯_ : X â†’ X â†’ ğ“¥ Ì‡ )
        (â™¯p : is-prop-valued _â™¯_)
        (â™¯i : is-irreflexive _â™¯_)
        (â™¯s : is-symmetric _â™¯_)
        (â™¯c : is-cotransitive _â™¯_)
      where

\end{code}

We now name the standard equivalence relation induced by _â™¯_.

\begin{code}

 _~_ : X â†’ X â†’ ğ“¥ Ì‡
 x ~ y = Â¬ (x â™¯ y)

\end{code}

 For certain purposes we need the apartness axioms packed into a
 single axiom.

\begin{code}

 â™¯a : is-apartness _â™¯_
 â™¯a = (â™¯p , â™¯i , â™¯s , â™¯c)

\end{code}

Initially we tried to work with the function apart : X â†’ (X â†’ ğ“¥ Ì‡ )
defined by apart = _â™¯_. However, at some point in the development
below it was difficult to proceed, when we need that the identity
type apart x = apart y is a proposition. This should be the case
because _â™¯_ is is-prop-valued. The most convenient way to achieve this
is to restrict the codomain of apart from ğ“¥ to Î©, so that the
codomain of apart is a set.

\begin{code}

 Î± : X â†’ (X â†’ Î© ğ“¥)
 Î± x y = x â™¯ y , â™¯p x y

\end{code}

The following is an immediate consequence of the fact that two
equivalent elements have the same apartness class, using functional
and propositional extensionality.

\begin{code}

 Î±-lemma : (x y : X) â†’ x ~ y â†’ Î± x ï¼ Î± y
 Î±-lemma x y na = dfunext fe h
  where
   f : (z : X) â†’ x â™¯ z â†” y â™¯ z
   f = elements-that-are-not-apart-have-the-same-apartness-class x y _â™¯_ â™¯a na

   g : (z : X) â†’ x â™¯ z ï¼ y â™¯ z
   g z = pe (â™¯p x z) (â™¯p y z) (prâ‚ (f z)) (prâ‚‚ (f z))

   h : (z : X) â†’ Î± x z ï¼ Î± y z
   h z = to-subtype-ï¼ (Î» _ â†’ being-prop-is-prop fe) (g z)

\end{code}

We now construct the tight reflection of (X,â™¯) to get (X',â™¯')
together with a universal strongly extensional map from X into tight
apartness types. We take X' to be the image of the map Î±.

\begin{code}

 X' : ğ“¤ âŠ” ğ“¥ âº Ì‡
 X' = image Î±

\end{code}

The type X may or may not be a set, but its tight reflection is
necessarily a set, and we can see this before we define a tight
apartness on it.

\begin{code}

 X'-is-set : is-set X'
 X'-is-set = subsets-of-sets-are-sets (X â†’ Î© ğ“¥) _
              (powersets-are-sets'' fe fe pe) âˆ¥âˆ¥-is-prop

 Î· : X â†’ X'
 Î· = corestriction Î±

\end{code}

The following induction principle is our main tool. Its uses look
convoluted at times by the need to show that the property one is
doing induction over is proposition valued. Typically this involves
the use of the fact the propositions form an exponential ideal, and,
 more generally, are closed under products.

\begin{code}

 Î·-is-surjection : is-surjection Î·
 Î·-is-surjection = corestrictions-are-surjections Î±

 Î·-induction : (P : X' â†’ ğ“¦ Ì‡ )
             â†’ ((x' : X') â†’ is-prop (P x'))
             â†’ ((x : X) â†’ P (Î· x))
             â†’ (x' : X') â†’ P x'
 Î·-induction = surjection-induction Î· Î·-is-surjection

\end{code}

The apartness relation _â™¯'_ on X' is defined as follows.

\begin{code}

 _â™¯'_ : X' â†’ X' â†’ ğ“¤ âŠ” ğ“¥ âº Ì‡
 (u , _) â™¯' (v , _) = âˆƒ x ê‰ X , Î£ y ê‰ X , (x â™¯ y) Ã— (Î± x ï¼ u) Ã— (Î± y ï¼ v)

\end{code}

Then Î· preserves and reflects apartness.

\begin{code}

 Î·-preserves-apartness : preserves _â™¯_ _â™¯'_ Î·
 Î·-preserves-apartness {x} {y} a = âˆ£ x , y , a , refl , refl âˆ£

 Î·-is-strongly-extensional : is-strongly-extensional _â™¯_ _â™¯'_ Î·
 Î·-is-strongly-extensional x y = âˆ¥âˆ¥-rec (â™¯p x y) g
  where
   g : (Î£ x' ê‰ X , Î£ y' ê‰ X , (x' â™¯ y') Ã— (Î± x' ï¼ Î± x) Ã— (Î± y' ï¼ Î± y))
     â†’ x â™¯ y
   g (x' , y' , a , p , q) = â™¯s _ _ (j (â™¯s _ _ (i a)))
    where
     i : x' â™¯ y' â†’ x â™¯ y'
     i = idtofun _ _ (ap prâ‚ (happly p y'))

     j : y' â™¯ x â†’ y â™¯ x
     j = idtofun _ _ (ap prâ‚ (happly q x))

\end{code}

Of course, we must check that _â™¯'_ is indeed an apartness
relation. We do this by Î·-induction. These proofs by induction need
routine proofs that some things are propositions.

\begin{code}

 â™¯'p : is-prop-valued _â™¯'_
 â™¯'p _ _ = âˆ¥âˆ¥-is-prop

 â™¯'i : is-irreflexive _â™¯'_
 â™¯'i = by-induction
  where
   induction-step : âˆ€ x â†’ Â¬ (Î· x â™¯' Î· x)
   induction-step x a = â™¯i x (Î·-is-strongly-extensional x x a)

   by-induction = Î·-induction (Î» x' â†’ Â¬ (x' â™¯' x'))
                   (Î» _ â†’ Î -is-prop fe (Î» _ â†’ ğŸ˜-is-prop))
                   induction-step

 â™¯'s : is-symmetric _â™¯'_
 â™¯'s = by-nested-induction
  where
   induction-step : âˆ€ x y â†’ Î· x â™¯' Î· y â†’ Î· y â™¯' Î· x
   induction-step x y a = Î·-preserves-apartness
                           (â™¯s x y (Î·-is-strongly-extensional x y a))

   by-nested-induction =
     Î·-induction (Î» x' â†’ âˆ€ y' â†’ x' â™¯' y' â†’ y' â™¯' x')
      (Î» x' â†’ Î â‚‚-is-prop fe (Î» y' _ â†’ â™¯'p y' x'))
      (Î» x â†’ Î·-induction (Î» y' â†’ Î· x â™¯' y' â†’ y' â™¯' Î· x)
               (Î» y' â†’ Î -is-prop fe (Î» _ â†’ â™¯'p y' (Î· x)))
               (induction-step x))

 â™¯'c : is-cotransitive _â™¯'_
 â™¯'c = by-nested-induction
  where
   induction-step : âˆ€ x y z â†’ Î· x â™¯' Î· y â†’ Î· x â™¯' Î· z âˆ¨ Î· y â™¯' Î· z
   induction-step x y z a = âˆ¥âˆ¥-functor c b
    where
     a' : x â™¯ y
     a' = Î·-is-strongly-extensional x y a

     b : x â™¯ z âˆ¨ y â™¯ z
     b = â™¯c x y z a'

     c : (x â™¯ z) + (y â™¯ z) â†’ (Î· x â™¯' Î· z) + (Î· y â™¯' Î· z)
     c (inl e) = inl (Î·-preserves-apartness e)
     c (inr f) = inr (Î·-preserves-apartness f)

   by-nested-induction =
     Î·-induction (Î» x' â†’ âˆ€ y' z' â†’ x' â™¯' y' â†’ (x' â™¯' z') âˆ¨ (y' â™¯' z'))
      (Î» _ â†’ Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¥âˆ¥-is-prop))
      (Î» x â†’ Î·-induction (Î» y' â†’ âˆ€ z' â†’ Î· x â™¯' y' â†’ (Î· x â™¯' z') âˆ¨ (y' â™¯' z'))
               (Î» _ â†’ Î â‚‚-is-prop fe (Î» _ _ â†’ âˆ¥âˆ¥-is-prop))
               (Î» y â†’ Î·-induction (Î» z' â†’ Î· x â™¯' Î· y â†’ (Î· x â™¯' z') âˆ¨ (Î· y â™¯' z'))
                        (Î» _ â†’ Î -is-prop fe (Î» _ â†’ âˆ¥âˆ¥-is-prop))
                        (induction-step x y)))

 â™¯'a : is-apartness _â™¯'_
 â™¯'a = (â™¯'p , â™¯'i , â™¯'s , â™¯'c)

\end{code}

The tightness of _â™¯'_ cannot by proved by induction by reduction to
properties of _â™¯_, as above, because _â™¯_ is not (necessarily)
tight. We need to work with the definitions of X' and _â™¯'_ directly.

\begin{code}

 â™¯'t : is-tight _â™¯'_
 â™¯'t (u , e) (v , f) n = âˆ¥âˆ¥-rec X'-is-set (Î» Ïƒ â†’ âˆ¥âˆ¥-rec X'-is-set (h Ïƒ) f) e
  where
   h : (Î£ x ê‰ X , Î± x ï¼ u) â†’ (Î£ y ê‰ X , Î± y ï¼ v) â†’ (u , e) ï¼ (v , f)
   h (x , p) (y , q) = to-Î£-ï¼ (t , âˆ¥âˆ¥-is-prop _ _)
    where
     remark : Â¬âˆƒ x ê‰ X , Î£ y ê‰ X , (x â™¯ y) Ã— (Î± x ï¼ u) Ã— (Î± y ï¼ v)
     remark = n

     r : Â¬ (x â™¯ y)
     r a = n âˆ£ x , y , a , p , q âˆ£

     t : u ï¼ v
     t = u   ï¼âŸ¨ p â»Â¹ âŸ©
         Î± x ï¼âŸ¨ Î±-lemma x y r âŸ©
         Î± y ï¼âŸ¨ q âŸ©
         v   âˆ

\end{code}

The tightness of _â™¯'_ gives that Î· maps equivalent elements to equal
elements, and its irreflexity gives that elements with the same Î·
image are equivalent.

\begin{code}

 Î·-equiv-gives-equal : {x y : X} â†’ x ~ y â†’ Î· x ï¼ Î· y
 Î·-equiv-gives-equal = â™¯'t _ _ âˆ˜ contrapositive (Î·-is-strongly-extensional _ _)

 Î·-equal-gives-equiv : {x y : X} â†’ Î· x ï¼ Î· y â†’ x ~ y
 Î·-equal-gives-equiv {x} {y} p a = â™¯'i
                                    (Î· y)
                                    (transport (Î» - â†’ - â™¯' Î· y)
                                    p
                                    (Î·-preserves-apartness a))

\end{code}

We now show that the above data provide the tight reflection, or
universal strongly extensional map from X to tight apartness types,
where unique existence is expressed by saying that a Î£ type is a
singleton, as usual in univalent mathematics and homotopy type
theory. Notice the use of Î·-induction to avoid dealing directly with
the details of the constructions performed above.

\begin{code}

 module _
         {ğ“¦ ğ“£ : Universe}
         (A : ğ“¦ Ì‡ )
         (_â™¯á´¬_ : A â†’ A â†’ ğ“£ Ì‡ )
         (â™¯á´¬a : is-apartness _â™¯á´¬_)
         (â™¯á´¬t : is-tight _â™¯á´¬_)
         (f : X â†’ A)
         (f-is-strongly-extensional : is-strongly-extensional _â™¯_ _â™¯á´¬_ f)
        where

  private
   A-is-set : is-set A
   A-is-set = tight-types-are-sets _â™¯á´¬_ fe â™¯á´¬a â™¯á´¬t

   f-transforms-~-into-= : {x y : X} â†’ x ~ y â†’ f x ï¼ f y
   f-transforms-~-into-= = â™¯á´¬t _ _ âˆ˜ contrapositive (f-is-strongly-extensional _ _)

  tr-lemma : (x' : X') â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ x') Ã— (f x ï¼ a))
  tr-lemma = Î·-induction _ p induction-step
    where
     p : (x' : X')
       â†’ is-prop (is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ x') Ã— (f x ï¼ a)))
     p x' = being-prop-is-prop fe

     induction-step : (y : X)
                    â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ a))
     induction-step x (a , d) (b , e) = to-Î£-ï¼ (IV , âˆ¥âˆ¥-is-prop _ _)
      where
       I : (Î£ x' ê‰ X , (Î· x' ï¼ Î· x) Ã— (f x' ï¼ a))
         â†’ (Î£ y' ê‰ X , (Î· y' ï¼ Î· x) Ã— (f y' ï¼ b))
         â†’ a ï¼ b
       I (x' , r , s) (y' , t , u) =
        a    ï¼âŸ¨ s â»Â¹ âŸ©
        f x' ï¼âŸ¨ f-transforms-~-into-= III âŸ©
        f y' ï¼âŸ¨ u âŸ©
        b    âˆ
         where
           II : Î· x' ï¼ Î· y'
           II = Î· x' ï¼âŸ¨ r âŸ©
                Î· x  ï¼âŸ¨ t â»Â¹ âŸ©
                Î· y' âˆ

           III : x' ~ y'
           III = Î·-equal-gives-equiv II

       IV : a ï¼ b
       IV = âˆ¥âˆ¥-rec A-is-set (Î» Ïƒ â†’ âˆ¥âˆ¥-rec A-is-set (I Ïƒ) e) d

  tr-construction : (x' : X') â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ x') Ã— (f x ï¼ a)
  tr-construction = Î·-induction _ tr-lemma induction-step
   where
    induction-step : (y : X) â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ a)
    induction-step x = f x , âˆ£ x , refl , refl âˆ£

  mediating-map : X' â†’ A
  mediating-map x' = prâ‚ (tr-construction x')

  private
   fâ» = mediating-map

  mediating-map-property : (y : X) â†’ âˆƒ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ fâ» (Î· y))
  mediating-map-property y = prâ‚‚ (tr-construction (Î· y))

  mediating-triangle : fâ» âˆ˜ Î· ï¼ f
  mediating-triangle = dfunext fe II
   where
    I : (y : X) â†’ (Î£ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ fâ» (Î· y))) â†’ fâ» (Î· y) ï¼ f y
    I y (x , p , q) =
     fâ» (Î· y) ï¼âŸ¨ q â»Â¹ âŸ©
     f x      ï¼âŸ¨ f-transforms-~-into-= (Î·-equal-gives-equiv p) âŸ©
     f y      âˆ

    II : (y : X) â†’ fâ» (Î· y) ï¼ f y
    II y = âˆ¥âˆ¥-rec A-is-set (I y) (mediating-map-property y)

  private
   c' : is-central
          (Î£ fâ» ê‰ (X' â†’ A) , (fâ» âˆ˜ Î· ï¼ f))
          (fâ» , mediating-triangle)
   c' (fâº , fâº-triangle) = IV
     where
      I : fâ» âˆ˜ Î· âˆ¼ fâº âˆ˜ Î·
      I = happly (fâ» âˆ˜ Î·  ï¼âŸ¨ mediating-triangle âŸ©
                  f       ï¼âŸ¨ fâº-triangle â»Â¹ âŸ©
                  fâº âˆ˜ Î·  âˆ)

      II : fâ» ï¼ fâº
      II = dfunext fe (Î·-induction _ (Î» _ â†’ A-is-set) I)

      triangle : fâº âˆ˜ Î· ï¼ f
      triangle = transport (Î» - â†’ - âˆ˜ Î· ï¼ f) II mediating-triangle

      III : triangle ï¼ fâº-triangle
      III = Î -is-set fe (Î» _ â†’ A-is-set) triangle fâº-triangle

      IV : (fâ» , mediating-triangle) ï¼ (fâº , fâº-triangle)
      IV = to-subtype-ï¼ (Î» h â†’ Î -is-set fe (Î» _ â†’ A-is-set)) II

  pre-tight-reflection : âˆƒ! fâ» ê‰ (X' â†’ A) , (fâ» âˆ˜ Î· ï¼ f)
  pre-tight-reflection = (fâ» , mediating-triangle) , c'

  mediating-map-is-strongly-extensional : is-strongly-extensional _â™¯'_ _â™¯á´¬_ fâ»
  mediating-map-is-strongly-extensional = V
   where
    I : (x y : X) â†’ fâ» (Î· x) â™¯á´¬ fâ» (Î· y) â†’ Î· x â™¯' Î· y
    I x y a = IV
     where
      II : f x â™¯á´¬ f y
      II = transportâ‚‚ (_â™¯á´¬_)
            (happly mediating-triangle x)
            (happly mediating-triangle y) a

      III : x â™¯ y
      III = f-is-strongly-extensional x y II

      IV : Î· x â™¯' Î· y
      IV = Î·-preserves-apartness III

    V : âˆ€ x' y' â†’ fâ» x' â™¯á´¬ fâ» y' â†’ x' â™¯' y'
    V = Î·-induction (Î» x' â†’ (y' : X') â†’ fâ» x' â™¯á´¬ fâ» y' â†’ x' â™¯' y')
          (Î» x' â†’ Î â‚‚-is-prop fe (Î» y' _ â†’ â™¯'p x' y'))
          (Î» x â†’ Î·-induction (Î» y' â†’ fâ» (Î· x) â™¯á´¬ fâ» y' â†’ Î· x â™¯' y')
                  (Î» y' â†’ Î -is-prop fe (Î» _ â†’ â™¯'p (Î· x) y'))
                  (I x))

  private
   c : is-central
        (Î£ fâ» ê‰ (X' â†’ A) , (is-strongly-extensional _â™¯'_ _â™¯á´¬_ fâ») Ã— (fâ» âˆ˜ Î· ï¼ f))
        (fâ» , mediating-map-is-strongly-extensional , mediating-triangle)
   c (fâº , fâº-is-strongly-extensional , fâº-triangle) =
    to-subtype-ï¼
      (Î» h â†’ Ã—-is-prop
               (being-strongly-extensional-is-prop fe _â™¯'_ _â™¯á´¬_ â™¯'p h)
               (Î -is-set fe (Î» _ â†’ A-is-set)))
      (ap prâ‚ (c' (fâº , fâº-triangle)))


  tight-reflection : âˆƒ! fâ» ê‰ (X' â†’ A)
                           , (is-strongly-extensional _â™¯'_ _â™¯á´¬_ fâ»)
                           Ã— (fâ» âˆ˜ Î· ï¼ f)
  tight-reflection = (fâ» , mediating-map-is-strongly-extensional ,
                     mediating-triangle) ,
                     c

\end{code}

The following is an immediate consequence of the tight reflection,
by the usual categorical argument, using the fact that the identity
map is strongly extensional (with the identity function as the
proof). Notice that our construction of the reflection produces a
result in a universe higher than those where the starting data are,
to avoid impredicativity (aka propositional resizing). Nevertheless,
the usual categorical argument is applicable.

A direct proof that doesn't rely on the tight reflection is equally
short in this case, and is also included.

What the following construction says is that if _â™¯_ is tight, then
any element of X is uniquely determined by the set of elements apart
from it.

\begin{code}

 tight-Î·-equiv-abstract-nonsense : is-tight _â™¯_ â†’ X â‰ƒ X'
 tight-Î·-equiv-abstract-nonsense â™¯t = Î· , (Î¸ , happly pâ‚„) , (Î¸ , happly pâ‚€)
  where
   u : âˆƒ! Î¸ ê‰ (X' â†’ X), Î¸ âˆ˜ Î· ï¼ id
   u = pre-tight-reflection X _â™¯_ â™¯a â™¯t id (Î» _ _ a â†’ a)

   v : âˆƒ! Î¶ ê‰ (X' â†’ X'), Î¶ âˆ˜ Î· ï¼ Î·
   v = pre-tight-reflection X' _â™¯'_ â™¯'a â™¯'t Î· Î·-is-strongly-extensional

   Î¸ : X' â†’ X
   Î¸ = âˆƒ!-witness u

   Î¶ : X' â†’ X'
   Î¶ = âˆƒ!-witness v

   Ï† : (Î¶' : X' â†’ X') â†’ Î¶' âˆ˜ Î· ï¼ Î· â†’ Î¶ ï¼ Î¶'
   Ï† Î¶' p = ap prâ‚ (âˆƒ!-uniqueness' v (Î¶' , p))

   pâ‚€ : Î¸ âˆ˜ Î· ï¼ id
   pâ‚€ = âˆƒ!-is-witness u

   pâ‚ : Î· âˆ˜ Î¸ âˆ˜ Î· ï¼ Î·
   pâ‚ = ap (Î· âˆ˜_) pâ‚€

   pâ‚‚ : Î¶ ï¼ Î· âˆ˜ Î¸
   pâ‚‚ = Ï† (Î· âˆ˜ Î¸) pâ‚

   pâ‚ƒ : Î¶ ï¼ id
   pâ‚ƒ = Ï† id refl

   pâ‚„ = Î· âˆ˜ Î¸ ï¼âŸ¨ pâ‚‚ â»Â¹ âŸ©
        Î¶     ï¼âŸ¨ pâ‚ƒ âŸ©
        id    âˆ

 tight-Î·-equiv-direct : is-tight _â™¯_ â†’ X â‰ƒ X'
 tight-Î·-equiv-direct t = (Î· , vv-equivs-are-equivs Î· cm)
  where
   lc : left-cancellable Î·
   lc {x} {y} p = j h
    where
     j : Â¬ (Î· x â™¯' Î· y) â†’ x ï¼ y
     j = t x y âˆ˜ contrapositive (Î·-preserves-apartness {x} {y})

     h : Â¬ (Î· x â™¯' Î· y)
     h a = â™¯'i (Î· y) (transport (Î» - â†’ - â™¯' Î· y) p a)

   e : is-embedding Î·
   e = lc-maps-into-sets-are-embeddings Î· lc X'-is-set

   cm : is-vv-equiv Î·
   cm = surjective-embeddings-are-vv-equivs Î· e Î·-is-surjection

\end{code}

TODO.

* The tight reflection has the universal property of the quotient by
  _~_. Conversely, the quotient by _~_ gives the tight reflection.

* The tight reflection of â™¯â‚‚ has the universal property of the totally
  separated reflection.

* If a type Y has an apartness with yâ‚€ â™¯ yâ‚, then
  the function type (X â†’ Y) has an apartness

    f â™¯ g := âˆƒ x ê‰ X , f x â™¯ g x

  that tells apart the constant functions with values yâ‚€ and yâ‚
  respectively.
