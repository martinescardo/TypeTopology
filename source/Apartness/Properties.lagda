Martin Escardo and Tom de Jong, August 2024

Moved from the file InjectiveTypes.CounterExamples on 12 September 2024.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.PropTrunc

module Apartness.Properties
        (pt : propositional-truncations-exist)
       where

open import MLTT.Spartan
open import Apartness.Definition
open import UF.ClassicalLogic
open import UF.FunExt
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open Apartness pt
open PropositionalTruncation pt

\end{code}

We define an apartness relation to be nontrivial if it tells two points apart.

\begin{code}

has-two-points-apart : {X : ğ“¤ Ì‡ } â†’ Apartness X ğ“¥ â†’ ğ“¥ âŠ” ğ“¤ Ì‡
has-two-points-apart {ğ“¤} {ğ“¥} {X} (_â™¯_ , Î±) = Î£ (x , y) ê‰ X Ã— X , (x â™¯ y)

Nontrivial-Apartness : ğ“¤ Ì‡ â†’ (ğ“¥ : Universe) â†’ ğ“¥ âº âŠ” ğ“¤ Ì‡
Nontrivial-Apartness X ğ“¥ = Î£ a ê‰ Apartness X ğ“¥ , has-two-points-apart a

\end{code}

Assuming weak excluded middle, every type with two distinct points can be
equipped with a nontrivial apartness relation.

\begin{code}

WEM-gives-that-type-with-two-distinct-points-has-nontrivial-apartness
 : funext ğ“¤ ğ“¤â‚€
 â†’ {X : ğ“¤ Ì‡ }
 â†’ has-two-distinct-points X
 â†’ typal-WEM ğ“¤
 â†’ Nontrivial-Apartness X ğ“¤
WEM-gives-that-type-with-two-distinct-points-has-nontrivial-apartness
 {ğ“¤} fe {X} htdp wem = Î³
 where
  s : (x y z : X) â†’ x â‰  y â†’ (x â‰  z) + (y â‰  z)
  s x y z d =
   Cases (wem (x â‰  z))
    (Î» (a : Â¬ (x â‰  z))  â†’ inr (Î» {refl â†’ a d}))
    (Î» (b : Â¬Â¬ (x â‰  z)) â†’ inl (three-negations-imply-one b))

  c : is-cotransitive _â‰ _
  c x y z d = âˆ£ s x y z d âˆ£

  Î³ : Nontrivial-Apartness X ğ“¤
  Î³ = (_â‰ _ ,
      ((Î» x y â†’ negations-are-props fe) ,
       â‰ -is-irrefl ,
       (Î» x y â†’ â‰ -sym) , c)) ,
      htdp

WEM-gives-that-type-with-two-distinct-points-has-nontrivial-apartnessâº
 : funext ğ“¤ ğ“¤â‚€
 â†’ {X : ğ“¤ âº Ì‡ }
 â†’ is-locally-small X
 â†’ has-two-distinct-points X
 â†’ typal-WEM ğ“¤
 â†’ Nontrivial-Apartness X ğ“¤
WEM-gives-that-type-with-two-distinct-points-has-nontrivial-apartnessâº
 {ğ“¤} fe {X} ls ((xâ‚€ , xâ‚) , d) wem = Î³
 where
  _â™¯_ : X â†’ X â†’ ğ“¤ Ì‡
  x â™¯ y = x â‰ âŸ¦ ls âŸ§ y

  s : (x y z : X) â†’ x â™¯ y â†’ (x â™¯ z) + (y â™¯ z)
  s x y z a = Cases (wem (x â™¯ z)) (inr âˆ˜ f) (inl âˆ˜ g)
   where
    f : Â¬ (x â™¯ z) â†’ y â™¯ z
    f = contrapositive
         (Î» (e : y ï¼âŸ¦ ls âŸ§ z) â†’ transport (x â™¯_) (ï¼âŸ¦ ls âŸ§-gives-ï¼ e) a)

    g : Â¬Â¬ (x â™¯ z) â†’ x â™¯ z
    g = three-negations-imply-one

  c : is-cotransitive _â™¯_
  c x y z d = âˆ£ s x y z d âˆ£

  Î³ : Nontrivial-Apartness X ğ“¤
  Î³ = (_â™¯_ ,
       (Î» x y â†’ negations-are-props fe) ,
       (Î» x â†’ â‰ âŸ¦ ls âŸ§-irrefl) ,
       (Î» x y â†’ â‰ âŸ¦ ls âŸ§-sym) ,
       c) ,
      (xâ‚€ , xâ‚) , â‰ -gives-â‰ âŸ¦ ls âŸ§ d

\end{code}

In particular, weak excluded middle yields a nontrivial apartness relation on
any universe.

\begin{code}

WEM-gives-non-trivial-apartness-on-universe
 : funext (ğ“¤ âº) ğ“¤â‚€
 â†’ typal-WEM (ğ“¤ âº)
 â†’ Nontrivial-Apartness (ğ“¤ Ì‡ ) (ğ“¤ âº)
WEM-gives-non-trivial-apartness-on-universe fe =
 WEM-gives-that-type-with-two-distinct-points-has-nontrivial-apartness
  fe
  universe-has-two-distinct-points

\end{code}

Further properties of apartness relations can be found in the following file
InjectiveTypes.CounterExamples. In particular, it is shown that the universe
can't have any nontrivial apartness unless weak excluded middle holds.

Added 31 January 2025 by Tom de Jong and Martin Escardo.

\begin{code}

EM-gives-tight-apartness-is-â‰  : DNE ğ“¥
                              â†’ (X : ğ“¤ Ì‡  )
                              â†’ ((_â™¯_ , _ , _) : Tight-Apartness X ğ“¥)
                              â†’ ((x y : X) â†’ x â™¯ y â†” x â‰  y)
EM-gives-tight-apartness-is-â‰  dne X (_â™¯_ , â™¯-is-apartness , â™¯-is-tight) x y = III
 where
  I : x â™¯ y â†’ x â‰  y
  I = not-equal-if-apart _â™¯_ â™¯-is-apartness
  II : x â‰  y â†’ x â™¯ y
  II Î½ = dne (x â™¯ y)
             (apartness-is-prop-valued _â™¯_ â™¯-is-apartness x y)
             (contrapositive (â™¯-is-tight x y) Î½)
  III : x â™¯ y â†” x â‰  y
  III = I , II

\end{code}
