Martin Escardo, early 2013, typed 5th May 2018

We show that the type of roots of a function Î± : â„• â†’ ğŸš has a
propositional truncation, in pure spartan Martin-LÃ¶f theory (without
using function extensionality). We also show that if we already have
truncations, we can "exit" the truncation of the set of roots.

\begin{code}

module  RootsTruncation where

open import NaturalsOrder
open import UF-Base hiding (_â‰¤_) hiding (â‰¤-anti)
open import UF-Subsingletons
open import DiscreteAndSeparated
open import UF-SetExamples
open import UF-KrausLemma

\end{code}

We now consider whether there is or there isn't a minimal root
(strictly) bounded by a number k:

\begin{code}

there-is-a-minimal-root : â„• â†’ (â„• â†’ ğŸš) â†’ Uâ‚€ Ì‡
there-is-a-minimal-root k Î± = Î£ \(m : â„•) â†’ (Î± m â‰¡ â‚€) Ã— (m < k) Ã— ((n : â„•) â†’ n < m â†’ Î± n â‰¢ â‚€)

there-is-no-root : â„• â†’ (â„• â†’ ğŸš) â†’ Uâ‚€ Ì‡
there-is-no-root k Î± = (n : â„•) â†’ n < k â†’ Î± n â‰¢ â‚€

FPO : â„• â†’ (â„• â†’ ğŸš) â†’ Uâ‚€ Ì‡
FPO k Î± = there-is-a-minimal-root k Î± + there-is-no-root k Î±

\end{code}

The above "finite principle of omniscience" is a proposition using
functional extensionality. However, we want to avoid function
extensionality here.

\begin{code}

fpo : (k : â„•) (Î± : â„• â†’ ğŸš) â†’ FPO k Î±
fpo zero Î± = inr (Î» n ())
fpo (succ k) Î± = cases f g (fpo k Î±)
 where
  f : there-is-a-minimal-root k Î± â†’ FPO (succ k) Î±
  f (m , p , l , Ï†) = inl (m , p , â‰¤-trans (succ m) k (succ k) l (â‰¤-succ k) , Ï†)
  
  g : there-is-no-root k Î± â†’ FPO (succ k) Î±
  g Ï† = cases gâ‚€ gâ‚ (ğŸš-discrete (Î± k) â‚€)
   where
    gâ‚€ : Î± k â‰¡ â‚€ â†’ FPO (succ k) Î±
    gâ‚€ p = inl (k , p , â‰¤-refl k , Ï†)
    
    gâ‚ : Î± k â‰¢ â‚€ â†’ FPO (succ k) Î±
    gâ‚ u = inr (bounded-âˆ€-next (Î» n â†’ Î± n â‰¢ â‚€) k u Ï†)

\end{code}

Given any root, we can find a minimal root.

\begin{code}

minimal-root : (Î± : â„• â†’ ğŸš) â†’ (n : â„•) â†’ Î± n â‰¡ â‚€ â†’ there-is-a-minimal-root (succ n) Î±
minimal-root Î± n p = Right-fails-then-left-holds (fpo (succ n) Î±) g
 where
  g : Â¬(there-is-no-root (succ n) Î±)
  g Ï† = Ï† n (â‰¤-refl n) p

\end{code}

With this we can define a constant endomap on the type of roots:

\begin{code}

roots : (â„• â†’ ğŸš) â†’ Uâ‚€ Ì‡
roots Î± = Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€

Î¼Ï : (Î± : â„• â†’ ğŸš) â†’ roots Î± â†’ roots Î±
Î¼Ï Î± (n , p) = prâ‚ (minimal-root Î± n p) , prâ‚ (prâ‚‚ (minimal-root Î± n p))

Î¼Ï-root : (Î± : â„• â†’ ğŸš) â†’ roots Î± â†’ â„•
Î¼Ï-root Î± r = prâ‚ (Î¼Ï Î± r)

Î¼Ï-root-is-root : (Î± : â„• â†’ ğŸš) (r : roots Î±) â†’ Î± (Î¼Ï-root Î± r) â‰¡ â‚€
Î¼Ï-root-is-root Î± r = prâ‚‚ (Î¼Ï Î± r)

Î¼Ï-root-minimal : (Î± : â„• â†’ ğŸš) (m : â„•) (p : Î± m â‰¡ â‚€)
                â†’ (n : â„•) â†’ Î± n â‰¡ â‚€ â†’ Î¼Ï-root Î± (m , p) â‰¤ n
Î¼Ï-root-minimal Î± m p n q = not-less-bigger-or-equal (Î¼Ï-root Î± (m , p)) n (f (double-negation-intro q))
 where
  f : Â¬(Î± n â‰¢ â‚€) â†’ Â¬(n < Î¼Ï-root Î± (m , p))
  f = contrapositive (prâ‚‚(prâ‚‚(prâ‚‚ (minimal-root Î± m p))) n)

Î¼Ï-constant : (Î± : â„• â†’ ğŸš) â†’ constant (Î¼Ï Î±)
Î¼Ï-constant Î± (n , p) (n' , p') = r
 where
  u : Î¼Ï-root Î± (n , p) â‰¤ Î¼Ï-root Î± (n' , p')
  u = Î¼Ï-root-minimal Î± n p (Î¼Ï-root Î± (n' , p')) (Î¼Ï-root-is-root Î± (n' , p'))
  
  v : Î¼Ï-root Î± (n' , p') â‰¤ Î¼Ï-root Î± (n , p)
  v = Î¼Ï-root-minimal Î± n' p' (Î¼Ï-root Î± (n , p)) (Î¼Ï-root-is-root Î± (n , p))
  
  q : Î¼Ï-root Î± (n , p) â‰¡ Î¼Ï-root Î± (n' , p')
  q = â‰¤-anti _ _ u v

  r : Î¼Ï Î± (n , p) â‰¡ Î¼Ï Î± (n' , p')
  r = to-Î£-â‰¡'' (q , ğŸš-isSet _ _)
 
roots-hasPropTruncation : (Î± : â„• â†’ ğŸš) â†’ âˆ€ U â†’ hasPropTruncation U (roots Î±)
roots-hasPropTruncation Î± = collapsible-hasPropTruncation (Î¼Ï Î± , Î¼Ï-constant Î±)

\end{code}

Explicitly (and repeating the construction of roots-hasPropTruncation):

\begin{code}

roots-truncation : (â„• â†’ ğŸš) â†’ Uâ‚€ Ì‡
roots-truncation Î± = Î£ \(r : roots Î±) â†’ r â‰¡ Î¼Ï Î± r

roots-truncation-isProp : (Î± : â„• â†’ ğŸš) â†’ isProp (roots-truncation Î±)
roots-truncation-isProp Î± = Kraus-Lemma (Î¼Ï Î±) (Î¼Ï-constant Î±)

roots-Î· : (Î± : â„• â†’ ğŸš) â†’ roots Î± â†’ roots-truncation Î±
roots-Î· Î± = to-fix (Î¼Ï Î±) (Î¼Ï-constant Î±)

roots-universal : (Î± : â„• â†’ ğŸš) â†’ âˆ€ {U} (P : U Ì‡)
                â†’ isProp P â†’ (roots Î± â†’ P) â†’ roots-truncation Î± â†’ P
roots-universal Î± {U} P _ f t = f (from-fix (Î¼Ï Î±) t)

\end{code}

We can't normally "exit a truncation", but in this special case we can:

\begin{code}

roots-exit-truncation : (Î± : â„• â†’ ğŸš) â†’ roots-truncation Î± â†’ roots Î±
roots-exit-truncation Î± = from-fix (Î¼Ï Î±)

\end{code}

Of course, if we already have propositional truncations, we can exit
root truncations using the above technique.

\begin{code}

open import UF-PropTrunc

module ExitRootTruncations (pt : PropTrunc) where

 open PropositionalTruncation pt

 exit-roots-truncation : (Î± : â„• â†’ ğŸš) â†’ âˆ¥(Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€)âˆ¥ â†’ Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€
 exit-roots-truncation Î± = h âˆ˜ g
  where
   f : (Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€) â†’ fix (Î¼Ï Î±)
   f = to-fix (Î¼Ï Î±) (Î¼Ï-constant Î±)
   
   g : âˆ¥(Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€)âˆ¥ â†’ fix (Î¼Ï Î±)
   g = ptrec (Kraus-Lemma (Î¼Ï Î±) (Î¼Ï-constant Î±)) f
   
   h : fix (Î¼Ï Î±) â†’ Î£ \(n : â„•) â†’ Î± n â‰¡ â‚€
   h = from-fix (Î¼Ï Î±)

\end{code}

This says that if there is a root, then we can find one.
