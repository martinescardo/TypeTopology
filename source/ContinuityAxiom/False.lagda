Martin Escardo 27th Nov 2013, last updated 4th Dec 2013, ported to
this repository 2nd June 2023

In intensional Martin-L√∂f type theory, if all functions (‚Ñï ‚Üí ‚Ñï) ‚Üí ‚Ñï
are continuous, then 0 = 1

1. Introduction
2. Informal proof of the theorem
3. Formal proof in Agda notation
4. Discussion
   References

Introduction
------------

In Brouwer's intuitionistic mathematics, it can be proved that all
functions f : (‚Ñï ‚Üí ‚Ñï) ‚Üí ‚Ñï are continuous [5]. This means that the
value f(Œ±) depends only on a finite prefix of the infinite sequence
Œ± : ‚Ñï ‚Üí ‚Ñï. Then, of course, Brouwer's mathematics is incompatible with
excluded middle, as excluded middle easily defines non-continuous
functions.

Intensional Martin-L√∂f type theory (MLTT) [2] is an alternative
foundation for constructive mathematics [5]. Like Bishop's
constructive mathematics [5], it is compatible with classical
mathematics. It doesn't prove or disprove excluded middle, which can
be consistently postulated, albeit with loss of computational content,
for the purposes of developing classical mathematics.

In the version of (intensional) MLTT considered here, we have identity
types Id X x y (written x = y and called propositional equality), some
basic types such as that of natural numbers (written ‚Ñï) and that of
finite sequences of natural numbers of length n (written Vec ‚Ñï n), and
dependent type constructors Œ† (written ‚àÄ) and Œ£.  We have the
eliminator J for identity types, but not Streicher's eliminator K, and
we don't have the equality reflection rule. We don't have the axiom of
function extensionality (any two pointwise equal functions are equal).

A possible formulation of continuity in MLTT under the so-called BHK
(Brouwer-Heyting-Kolmogorov) interpretation Œ£ of the quantifier ‚àÉ is

  ‚àÄ(Œ± : ‚Ñï ‚Üí ‚Ñï), Œ£ n : ‚Ñï , ‚àÄ(Œ≤ : ‚Ñï ‚Üí ‚Ñï) , Œ± =[n] Œ≤ ‚Üí f(Œ±) = f(Œ≤),

where Œ± =[n] Œ≤ means that the sequences Œ± and Œ≤ agree at the first n
positions.

We show in MLTT that, with this formulation of continuity, if all
functions f : (‚Ñï ‚Üí ‚Ñï) ‚Üí ‚Ñï are continuous, then 0 = 1.

We write the proof both informally and formally in Agda notation [3].
See Bauer [1] for a related well-known phenomenon with the same proof
ingredients used here.

We emphasize that our proof doesn't use the axiom of function
extensionality (any two pointwise equal functions are equal), which
anyway is not available in MLTT (but is in Homotopy Type Theory
(HoTT)). It also doesn't require universes.

In HoTT [2], there is an alternative constructive existential
quantifier, written ‚àÉ, corresponding to the topos theoretic
existential quantifier, in which witnesses are hidden, but can be
disclosed via unique choice, formulated as the axiom of description by
Coquand [6].

Brouwer's continuity axiom with Œ£ replaced by ‚àÉ is consistent with
MLTT, even when MLTT is extended with the axiom of function
extensionality. Hopefully it is compatible with the HoTT extensions of
MLTT too, but this is an open problem.


2. Informal proof of the theorem
--------------------------------

The proof in this section is the informal rendering of the formal
proof given in Section 3 below. It is based on a proof attributed to
van Dalen and Troelstra by Bauer [1] for a different theorem.

Write

  Baire = ‚Ñï ‚Üí ‚Ñï

to denote the type of sequences, and let

  0^œâ = the infinite sequence of zeros,

and

  0^n k^œâ = the sequence of n many zeros followed by infinitely many k's.

Then

  (0^n k^œâ) =[n] 0^œâ     and    (0^n k^œâ)(n) = k.

Assume that all functions are continuous, with the Œ£-formulation of
continuity as in the introduction:

 ‚àÄ(f : Baire ‚Üí ‚Ñï). ‚àÄ(Œ± : Baire). Œ£(n : ‚Ñï). ‚àÄ(Œ≤ : Baire). Œ± =[n] Œ≤ ‚Üí f(Œ±) = f(Œ≤).

By projection, with Œ± = 0^œâ, this gives a modulus-of-continuity
function

 M : (Baire ‚Üí ‚Ñï) ‚Üí ‚Ñï

such that

 ‚àÄ (f : Baire ‚Üí ‚Ñï) , ‚àÄ (Œ≤ : Baire) , 0^œâ =[M f] Œ≤ ‚Üí f(0^œâ) = f(Œ≤).    (0)

We use M to define a non-continuous function f : Baire ‚Üí ‚Ñï and hence
get a contradiction. What this really says is that no such function M
can be continuous. See the discussion in Section 4 below.  Notice also
that if we were using ‚àÉ rather than Œ£ to define continuity, as
discussed in the introduction, we would need choice to get M.

Let

  m = M (Œª Œ±. 0),

and define f : Baire ‚Üí ‚Ñï by

  f Œ≤ = M (Œª Œ±. Œ≤ (Œ± m)).

Observe that, by simply expanding the definitions, with the
understanding that 0^œâ defined above is Œª i. 0,

  f(0^œâ) = m.

By the defining property (0) of M,

  ‚àÄ(Œ≤ : Baire). 0^œâ =[ M f ] Œ≤ ‚Üí m = f Œ≤.    (1)


Case 0. M f = 0.
------

By (1),

  ‚àÄ(Œ≤ : Baire). m = f Œ≤.

The choice Œ≤ i = i gives

  m = f(Œª i. i) = M (Œª Œ±. Œ± m).

By the defining property (0) of M, this means that

  ‚àÄ(Œ± : Baire). 0^œâ =[ m ] Œ± ‚Üí 0 = Œ± m.

But this gives 0 = 1 if we choose e.g. the sequence Œ± = 0^m 1^œâ.


Case 1. M f > 0.
------

For any Œ≤ : Baire, by the continuity of Œª Œ±. Œ≤ (Œ± m), by the definition
of f, and by the defining property (0) of M, we have that

  ‚àÄ(Œ± : Baire). 0^œâ =[ f Œ≤ ] Œ± ‚Üí Œ≤ 0 = Œ≤ (Œ± m).

Considering

  Œ≤ = 0^(M f) 1^œâ,

this gives

  ‚àÄ(Œ± : Baire). 0^œâ =[ m ] Œ± ‚Üí Œ≤ 0 = Œ≤ (Œ± m),

because f Œ≤ = m as 0^œâ =[ M f ] Œ≤ and f(0^œâ) = m. Considering

  Œ± = 0^m (M f)^œâ,

this in turn gives 0 = Œ≤ 0 = Œ≤ (Œ± m) = Œ≤ (M f) = 1. Q.E.D.


3. Formal proof in Agda notation
--------------------------------

The main reason for giving a formal proof is to be certain that the
above argument doesn't use function extensionality (any two pointwise
equal functions are equal) inadvertently.

3.1. Standard preliminary definitions
-------------------------------------

The option without-K is used to disable the UIP (uniqueness of
identity proofs) principle, also known as Streicher's Axiom K, which
is not part of MLTT:

\begin{code}

{-# OPTIONS --safe --without-K #-}

module ContinuityAxiom.False where

open import MLTT.Spartan
open import MLTT.Vector

Baire : ùì§‚ÇÄ Ãá
Baire = ‚Ñï ‚Üí ‚Ñï

_‚ü¶_‚üß : {X : ùì§ Ãá} ‚Üí (‚Ñï ‚Üí X) ‚Üí (n : ‚Ñï) ‚Üí Vector X n
Œ± ‚ü¶ 0      ‚üß = []
Œ± ‚ü¶ succ n ‚üß = Œ± 0 ‚à∑ ((Œª i ‚Üí Œ± (succ i)) ‚ü¶ n ‚üß)

_Ôºù‚ü¶_‚üß_ : {X : ùì§‚ÇÄ Ãá} ‚Üí (‚Ñï ‚Üí X) ‚Üí ‚Ñï ‚Üí (‚Ñï ‚Üí X) ‚Üí ùì§‚ÇÄ Ãá
Œ± Ôºù‚ü¶ n ‚üß Œ≤ = Œ± ‚ü¶ n ‚üß Ôºù Œ≤ ‚ü¶ n ‚üß

\end{code}

The above definitions are standard in type theory/constructive
mathematics. The second last one takes a finite prefix of a given
length of a given infinite sequence, and hence the last one defines
when two given sequences agree at the first n positions.


3.2 Formulation of the theorem
------------------------------

The following is Brouwer's definition of continuity, formulated with Œ£
rather than ‚àÉ:

\begin{code}

continuous : (Baire ‚Üí ‚Ñï) ‚Üí ùì§‚ÇÄ Ãá
continuous f = (Œ± : Baire) ‚Üí Œ£ n Íûâ ‚Ñï , ((Œ≤ : Baire) ‚Üí Œ± Ôºù‚ü¶ n ‚üß Œ≤ ‚Üí f Œ± Ôºù f Œ≤)

\end{code}

In Brouwer's mathematics, all functions are continuous, but this can't
be true if continuity is (incorrectly) formulated with Œ£ as above:

\begin{code}

theorem : ((f : Baire ‚Üí ‚Ñï) ‚Üí continuous f) ‚Üí 0 Ôºù 1

\end{code}

We need some preparation to prove that.

3.3 Preliminary proofs and constructions
----------------------------------------

The following is standard in MLTT:

For future reference, notice that all uses of ap below are for
(first-order) functions of types

  ‚Ñï ‚Üí ‚Ñï
  vec ‚Ñï (succ n) ‚Üí ‚Ñï
  vec ‚Ñï n ‚Üí vec ‚Ñï (succ n)

only.

The following definition of (n zeros-and-then k) gives a sequence of n
many zeros followed by k and then by something irrelevant (infinitely
many k's, in fact, but we don't need to know that):

\begin{code}

_zeros-and-then_ : ‚Ñï ‚Üí ‚Ñï ‚Üí Baire
( 0       zeros-and-then k)  i       = k
((succ n) zeros-and-then k)  0       = 0
((succ n) zeros-and-then k) (succ i) = (n zeros-and-then k) i

o : Baire
o = Œª i ‚Üí 0

zeros-and-then-spec‚ÇÄ : (n k : ‚Ñï) ‚Üí (n zeros-and-then k) n Ôºù k
zeros-and-then-spec‚ÇÄ  0       k = refl
zeros-and-then-spec‚ÇÄ (succ n) k = zeros-and-then-spec‚ÇÄ n k

zeros-and-then-spec‚ÇÅ : (n k : ‚Ñï) ‚Üí o Ôºù‚ü¶ n ‚üß (n zeros-and-then k)
zeros-and-then-spec‚ÇÅ n k = zeros‚ÇÄ n ‚àô zeros‚ÇÅ n k
 where
  zeros : (n : ‚Ñï) ‚Üí Vector ‚Ñï n
  zeros  0       = []
  zeros (succ n) = 0 ‚à∑ zeros n

  zeros‚ÇÄ : (n : ‚Ñï) ‚Üí o ‚ü¶ n ‚üß Ôºù zeros n
  zeros‚ÇÄ  0       = refl
  zeros‚ÇÄ (succ n) = ap (Œª ns ‚Üí 0 ‚à∑ ns) (zeros‚ÇÄ n)

  zeros‚ÇÅ : (n k : ‚Ñï) ‚Üí zeros n Ôºù (n zeros-and-then k) ‚ü¶ n ‚üß
  zeros‚ÇÅ  0       k = refl
  zeros‚ÇÅ (succ n) k = ap (Œª ns ‚Üí 0 ‚à∑ ns) (zeros‚ÇÅ n k)

\end{code}


3.4 Proof of the theorem
------------------------

Finally, here is the proof of the claim. It uses the modulus of
continuity functional M such that M f is a modulus of continuity at
the constant zero sequence o (defined above), which exists because we
are using the existential quantifier Œ£ rather than ‚àÉ in our
(unsatisfactory) definition of continuity, in order to define a
non-continuous function f and hence get a contradiction.

Recall that our goal is to prove

  theorem : ((f : Baire ‚Üí ‚Ñï) ‚Üí continuous f) ‚Üí 0 Ôºù 1

formulated above in Agda.

\begin{code}

theorem continuity = Œ≥
 where
  M : (Baire ‚Üí ‚Ñï) ‚Üí ‚Ñï
  M f = pr‚ÇÅ (continuity f o)

  continuity‚ÇÄ : (f : Baire ‚Üí ‚Ñï) (Œ≤ : Baire) ‚Üí o Ôºù‚ü¶ M f ‚üß Œ≤ ‚Üí f o Ôºù f Œ≤
  continuity‚ÇÄ f = pr‚ÇÇ (continuity f o)

  m : ‚Ñï
  m = M (Œª Œ± ‚Üí 0)

  f : Baire ‚Üí ‚Ñï
  f Œ≤ = M (Œª Œ± ‚Üí Œ≤ (Œ± m))

  observation : f o Ôºù m
  observation = refl

  fact‚ÇÄ : (Œ≤ : Baire) ‚Üí o Ôºù‚ü¶ M f ‚üß Œ≤ ‚Üí m Ôºù f Œ≤
  fact‚ÇÄ = continuity‚ÇÄ f

  lemma‚ÇÄ : M f Ôºù 0 ‚Üí 0 Ôºù 1
  lemma‚ÇÄ r =
   0   Ôºù‚ü® claim‚ÇÑ Œ± (zeros-and-then-spec‚ÇÅ m 1) ‚ü©
   Œ± m Ôºù‚ü® Œ±-fact‚ÇÄ ‚ü©
   1   ‚àé
   where
    claim‚ÇÄ : (Œ≤ : Baire) ‚Üí m Ôºù f Œ≤
    claim‚ÇÄ Œ≤ = fact‚ÇÄ Œ≤ claim‚ÇÅ
      where
       claim‚ÇÅ : o Ôºù‚ü¶ M f ‚üß Œ≤
       claim‚ÇÅ = transport (Œª - ‚Üí o Ôºù‚ü¶ - ‚üß Œ≤) (r ‚Åª¬π) refl

    claim‚ÇÇ : m Ôºù M (Œª Œ± ‚Üí Œ± m)
    claim‚ÇÇ = claim‚ÇÄ (Œª i ‚Üí i)

    claim‚ÇÉ : (Œ± : Baire) ‚Üí o Ôºù‚ü¶ M (Œª Œ± ‚Üí Œ± m) ‚üß Œ± ‚Üí 0 Ôºù Œ± m
    claim‚ÇÉ = continuity‚ÇÄ (Œª Œ± ‚Üí Œ± m)

    claim‚ÇÑ : (Œ± : Baire) ‚Üí o Ôºù‚ü¶ m ‚üß Œ± ‚Üí 0 Ôºù Œ± m
    claim‚ÇÑ = transport (Œª - ‚Üí (Œ± : Baire) ‚Üí o Ôºù‚ü¶ - ‚üß Œ± ‚Üí 0 Ôºù Œ± m) (claim‚ÇÇ ‚Åª¬π) claim‚ÇÉ

    Œ± : Baire
    Œ± = m zeros-and-then 1

    Œ±-fact‚ÇÄ : Œ± m Ôºù 1
    Œ±-fact‚ÇÄ = zeros-and-then-spec‚ÇÄ m 1

  lemma‚ÇÅ : (Œ£ n Íûâ ‚Ñï , M f Ôºù succ n) ‚Üí 0 Ôºù 1
  lemma‚ÇÅ (n , r) =
    0   Ôºù‚ü® claim (transport (Œª - ‚Üí o Ôºù‚ü¶ - ‚üß Œ≤) r Œ≤-fact‚ÇÅ) ‚ü©
    Œ≤ 0 Ôºù‚ü® fact‚ÇÑ ‚ü©
    1   ‚àé
   where
    Œ≤ : Baire
    Œ≤ = (M f) zeros-and-then 1

    Œ≤-fact‚ÇÄ : Œ≤ (M f) Ôºù 1
    Œ≤-fact‚ÇÄ = zeros-and-then-spec‚ÇÄ (M f) 1

    Œ≤-fact‚ÇÅ : o Ôºù‚ü¶ M f ‚üß Œ≤
    Œ≤-fact‚ÇÅ = zeros-and-then-spec‚ÇÅ (M f) 1

    fact‚ÇÄ' : f Œ≤ Ôºù m
    fact‚ÇÄ' = (fact‚ÇÄ Œ≤ Œ≤-fact‚ÇÅ)‚Åª¬π

    fact‚ÇÅ : (Œ± : Baire) ‚Üí o Ôºù‚ü¶ f Œ≤ ‚üß Œ± ‚Üí Œ≤ 0 Ôºù Œ≤ (Œ± m)
    fact‚ÇÅ Œ± = continuity‚ÇÄ (Œª Œ± ‚Üí Œ≤ (Œ± m)) Œ±

    fact‚ÇÇ : (Œ± : Baire) ‚Üí o Ôºù‚ü¶ m ‚üß Œ± ‚Üí Œ≤ 0 Ôºù Œ≤ (Œ± m)
    fact‚ÇÇ = transport (Œª n ‚Üí (Œ± : Baire) ‚Üí o Ôºù‚ü¶ n ‚üß Œ± ‚Üí Œ≤ 0 Ôºù Œ≤ (Œ± m)) fact‚ÇÄ' fact‚ÇÅ

    Œ± : Baire
    Œ± = m zeros-and-then (M f)

    Œ±-fact‚ÇÄ : Œ± m Ôºù M f
    Œ±-fact‚ÇÄ = zeros-and-then-spec‚ÇÄ m (M f)

    Œ±-fact‚ÇÅ : o Ôºù‚ü¶ m ‚üß Œ±
    Œ±-fact‚ÇÅ = zeros-and-then-spec‚ÇÅ m (M f)

    fact‚ÇÉ : Œ≤ 0 Ôºù Œ≤ (Œ± m)
    fact‚ÇÉ = fact‚ÇÇ Œ± Œ±-fact‚ÇÅ

    fact‚ÇÑ : Œ≤ 0 Ôºù 1
    fact‚ÇÑ =
     Œ≤ 0     Ôºù‚ü® fact‚ÇÉ ‚ü©
     Œ≤ (Œ± m) Ôºù‚ü® ap Œ≤ Œ±-fact‚ÇÄ ‚ü©
     Œ≤ (M f) Ôºù‚ü® Œ≤-fact‚ÇÄ ‚ü©
     1       ‚àé

    claim : o Ôºù‚ü¶ succ n ‚üß Œ≤ ‚Üí 0 Ôºù Œ≤ 0
    claim = ap head

  lemma : (Œ£ n Íûâ ‚Ñï , M f Ôºù n) ‚Üí 0 Ôºù 1
  lemma (0      , r) = lemma‚ÇÄ r
  lemma (succ n , r) = lemma‚ÇÅ (n , r)

  Œ≥ : 0 Ôºù 1
  Œ≥ = lemma (M f , refl)

\end{code}

4. Discussion
-------------

For a large research community, the terminology "extensionality"
usually refers to the rule

    x Ôºù y implies f x Ôºù f y,

which here is called "ap" and defined above. This form of
extensionality is a feature of intensional type theory, and can be
regarded as a property of identity types.

A red herring in connection with Andrej Bauer's post [1] is that the
"extensionality" of M, that is, the availability of the function
ap _ _ M of type f Ôºù g ‚Üí M f Ôºù M g, is not used in the above proof. We
only use the "extensionality" of first-order functions.  However, one
of these functions, namely Œ≤, is defined indirectly from M, and so its
"extensionality" relies on that of M, at least loosely speaking.

What is important is that we don't use the so-called axiom of function
extensionality (any two pointwise equal functions are equal), which is
not available in intensional type theory.

The fundamental reason for the contradiction is a topological
phenomenon: there can't be any continuous modulus-of-continuity
functional M. No finite part of a continuous function f : (‚Ñï ‚Üí ‚Ñï) ‚Üí ‚Ñï
is enough to tell which finite part of Œ± : ‚Ñï ‚Üí ‚Ñï is sufficient to
determine f(Œ±).  In fact, what the above proof does is to use the
hypothetical M to construct a non-continuous function f.

In the topological topos all functions Baire ‚Üí ‚Ñï are continuous, both
externally and internally, but there is no continuous M.

The right notion of continuity, which is validated in some models, is

 ‚àÄ (f : Baire ‚Üí ‚Ñï) , ‚àÄ(Œ± : Baire) , ‚à• Œ£ n : ‚Ñï , ‚àÄ (Œ≤ : Baire), Œ± Ôºù‚ü¶ n ‚üß Œ≤ ‚Üí f Œ± Ôºù f Œ≤ ‚à•,

where we use propositional truncation (or bracket types), which can
be written, using HoTT notation [2], as

 ‚àÄ (f : Baire ‚Üí ‚Ñï) , ‚àÄ (Œ± : Baire) , ‚àÉ n : ‚Ñï) , ‚àÄ (Œ≤ : Baire) , Œ± Ôºù‚ü¶ n ‚üß Œ≤ ‚Üí f Œ± Ôºù f Œ≤.

This implies the weaker statement

 ‚àÄ (f : Baire ‚Üí ‚Ñï) , ‚àÄ (Œ± : Baire) , ¬¨¬¨Œ£ n : ‚Ñï , ‚àÄ (Œ≤ : Baire), Œ± Ôºù‚ü¶ n ‚üß Œ≤ ‚Üí f Œ± Ôºù f Œ≤,

which can be expressed without hpropositional truncation and hence in
MLTT. It is an interesting problem whether the ‚àÉ-formulation of
continuity can be formulated in pure MLTT (with or without universes).

Both the ‚àÉ-version and the ¬¨¬¨Œ£ version are validated in the
topological topos interpretation and hence are consistent.

Notice that a topos has both ‚àÉ (via the subobject classifier) and Œ£
(via its local cartesian closed structure). What the above says is
that continuity formulated with Œ£ is false in any topos, but
continuity formulated with ‚àÉ holds in some toposes.

This is similar to the fact that the axiom of choice formulated with Œ£
holds in any topos, but often fails when formulated with ‚àÉ.

Other continuity axioms that hold in Browerian mathematics do admit a
consistent Œ£ formulation in MLTT, such as uniform continuity

 ‚àÄ(f : Cantor ‚Üí ‚Ñï).Œ£(n : ‚Ñï).‚àÄ(Œ± Œ≤ : Cantor). Œ± Ôºù‚ü¶ n ‚üß Œ≤ ‚Üí f Œ± Ôºù f Œ≤,

where Cantor is the type of infinite binary sequences [4].


References
----------

[0] This file is available in two forms:
    http://www.cs.bham.ac.uk/~mhe/continuity-false/continuity-false.lagda
    http://www.cs.bham.ac.uk/~mhe/continuity-false/continuity-false.html

[1] Andrej Bauer.
    http://math.andrej.com/2011/07/27/definability-and-extensionality-of-the-modulus-of-continuity-functional/

[2] The HoTT Book. http://homotopytypetheory.org/book/

    Chapter 1 introduces intensional MLTT without its homotopical extensions.
    The appendix gives a formal treatment of MLTT with and without the extensions.

[3] The Agda Wiki. http://wiki.portal.chalmers.se/agda/pmwiki.php

[4] Chuangjie Xu and M.H. Escard√≥.
    A constructive model of uniform continuity.
    M. Hasegawa (Ed.): TLCA 2013, LNCS 7941, pp. 236-249. Springer, Heidelberg 2013.

[4'] M.H. Escard√≥ and Chuangjie Xu.
    A constructive manifestation of the Kleene-Kreisel continuous functionals. Submitted.
    http://www.cs.bham.ac.uk/~mhe/papers/kleene-kreisel.pdf

[5] M. Beeson. Foundations of Constructive Mathematics:
    Metamathematical Studies, Springer, Berlin/Heidelberg/New York, 1985.

[6] T. Coquand. Type Theory and Univalent Foundation.
    Royal Society meeting, 26 Nov 2013.
    http://www.cse.chalmers.se/~coquand/rs.pdf
