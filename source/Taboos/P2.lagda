Martin Escardo, 24th March 2022
with minor improvements and additions June 2024.

This file is a apropos the discussion at the end of the file
Ordinals.NotationInterpretation2.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module Taboos.P2 (fe : Fun-Ext) where

private
 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open import MLTT.Spartan
open import MLTT.Two
open import MLTT.Two-Properties
open import UF.Base
open import UF.ClassicalLogic
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

\end{code}

The following map plays a central role in the investigations of this
file.

\begin{code}

Ïƒ : (X : ğ“¤ Ì‡ ) â†’ ğŸš â†’ (X â†’ ğŸš)
Ïƒ X n = Î» _ â†’ n

\end{code}

Abbreviations.

\begin{code}

Ïƒâ‚€ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚€ {ğ“¤} {X} = Ïƒ X â‚€

Ïƒâ‚ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚ {ğ“¤} {X} = Ïƒ X â‚

\end{code}

Recall that we say that a type X is empty to mean Â¬ X, namely X â†’ ğŸ˜,
and nonempty to mean Â¬Â¬ X.

In general, if the type X is a proposition we will write "Â¬ X" rather
than the synonym "is-empty X", and "Â¬Â¬ X" rather than the synonym
"is-nonempty X". Also, we will pronounce "Â¬Â¬ X" as "X is irrefutable"
when X is a proposition.

\begin{code}

emptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-empty X â†” (Ïƒâ‚€ ï¼ Ïƒâ‚)
emptiness-criterion {ğ“¤} X = (f , g)
 where
  f : Â¬ X â†’ Ïƒâ‚€ ï¼ Ïƒâ‚
  f Î½ = dfunext fe (Î» (x : X) â†’ ğŸ˜-elim (Î½ x))

  g : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ X
  g e x = zero-is-not-one
           (â‚€    ï¼âŸ¨ refl âŸ©
            Ïƒâ‚€ x ï¼âŸ¨ happly e x âŸ©
            Ïƒâ‚ x ï¼âŸ¨ refl âŸ©
            â‚    âˆ)

nonemptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-nonempty X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
nonemptiness-criterion {ğ“¤} X = I (emptiness-criterion X)
 where
  I : type-of (emptiness-criterion X) â†’ Â¬Â¬ X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
  I (f , g) = (Î» (Î½ : Â¬Â¬ X) (e : Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Î½ (g e)) ,
              contrapositive f

\end{code}

The main notion studied in this file is the following:

\begin{code}

is-thinly-inhabited : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-thinly-inhabited X = is-equiv (Ïƒ X)

being-thinly-inhabited-is-prop : {X : ğ“¤ Ì‡ } â†’ is-prop (is-thinly-inhabited X)
being-thinly-inhabited-is-prop {ğ“¤} {X} = being-equiv-is-prop fe' (Ïƒ X)

\end{code}

The idea of the terminology "thinly" is that there are very few
elements in the type, but at the same time the type is nonempty. As we
shall see, this is actually a notion stronger than nonemptiness. But
this idea works only for types that have enough functions into the
booleans, called totally separated. We show below that if X is totally
separated and thinly inhabited then X is a proposition.

Exercise. A type X is thinly inhabited if and only if there is *any*
equivalence ğŸš â‰ƒ (X â†’ ğŸš).

For propositions X, the thin inhabitedness of X is equivalent to the
map Ïƒ X having a retraction Ï.

                            Ïƒ X
                          ğŸš  â†ª  (X â†’ ğŸš)
                          ğŸš  â†  (X â†’ ğŸš)
                             Ï

In general there can be many maps Ï with Ï âˆ˜ Ïƒ X âˆ¼ id, but there is at
most one if X is a proposition. We use the following lemma to prove
this:

\begin{code}

retraction-of-Ïƒ-is-section : {P : ğ“¤ Ì‡ }
                           â†’ is-prop P
                           â†’ (Ï : (P â†’ ğŸš) â†’ ğŸš)
                           â†’ Ï âˆ˜ Ïƒ P âˆ¼ id
                           â†’ Ïƒ P âˆ˜ Ï âˆ¼ id
retraction-of-Ïƒ-is-section {ğ“¤} {P} i Ï h f = IV
 where
  I : (p : P) â†’ Ï f ï¼ f p
  I p = Ï f           ï¼âŸ¨ ap Ï III âŸ©
        Ï (Ïƒ P (f p)) ï¼âŸ¨ h (f p) âŸ©
        f p           âˆ
   where
    II : f âˆ¼ Ïƒ P (f p)
    II q = f q         ï¼âŸ¨ ap f (i q p) âŸ©
           f p         ï¼âŸ¨ refl âŸ©
           Ïƒ P (f p) q âˆ

    III : f ï¼ Ïƒ P (f p)
    III = dfunext fe II

  IV : Ïƒ P (Ï f) ï¼ f
  IV = dfunext fe I

Ïƒ-having-retraction-is-prop : {X : ğ“¤ Ì‡ }
                            â†’ is-prop X
                            â†’ is-prop (has-retraction (Ïƒ X))
Ïƒ-having-retraction-is-prop {ğ“¤} {X} i =
 prop-criterion
  (Î» (Ï , ÏÏƒ) â†’ sections-have-at-most-one-retraction fe' (Ïƒ X)
                 (Ï , retraction-of-Ïƒ-is-section i Ï ÏÏƒ))

retraction-of-Ïƒ-gives-thinly-inhabited : {P : ğ“¤ Ì‡ }
                                       â†’ is-prop P
                                       â†’ has-retraction (Ïƒ P)
                                       â†’ is-thinly-inhabited P
retraction-of-Ïƒ-gives-thinly-inhabited {ğ“¤} {P} i (Ï , ÏÏƒ) =
 qinvs-are-equivs (Ïƒ P) (Ï , ÏÏƒ , retraction-of-Ïƒ-is-section i Ï ÏÏƒ)

\end{code}

For the converse we don't need X to be a proposition, of course.

\begin{code}

thinly-inhabited-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                                       â†’ is-thinly-inhabited X
                                       â†’ has-retraction (Ïƒ X)
thinly-inhabited-gives-retraction-of-Ïƒ {ğ“¤} {X} = equivs-are-sections (Ïƒ X)

\end{code}

By construction, every ğŸš-valued function on a thinly inhabited type is
constant.

\begin{code}

constancy : {X : ğ“¤ Ì‡ }
          â†’ is-thinly-inhabited X
          â†’ (f : X â†’ ğŸš)
          â†’ Î£ n ê‰ ğŸš , f ï¼ Î» _ â†’ n
constancy {ğ“¤} {X} ti f = âŒœ e âŒâ»Â¹ f , ((inverses-are-sections' e f)â»Â¹)
 where
  e : ğŸš â‰ƒ (X â†’ ğŸš)
  e = Ïƒ X , ti

\end{code}

Next we want to show that

 P â†’ is-thinly-inhabited P

for any proposition P, and we use the following lemma.

\begin{code}

point-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                            â†’ X
                            â†’ has-retraction (Ïƒ X)
point-gives-retraction-of-Ïƒ {ğ“¤} {X} x = (Î³ , Î³Ïƒ)
 where
  Î³ : (X â†’ ğŸš) â†’ ğŸš
  Î³ f = f x

  Î³Ïƒ : Î³ âˆ˜ Ïƒ X âˆ¼ id
  Î³Ïƒ n = refl

point-gives-is-thinly-inhabited : {P : ğ“¤ Ì‡ }
                                â†’ is-prop P
                                â†’ P
                                â†’ is-thinly-inhabited P
point-gives-is-thinly-inhabited {ğ“¤} {P} i p =
 retraction-of-Ïƒ-gives-thinly-inhabited i (point-gives-retraction-of-Ïƒ p)

\end{code}

Notice, however, that pointed types X other than propositions are not
thinly inhabited in general. An example is the type X := ğŸš, because there
are four maps X â†’ ğŸš in this case, and we need exactly two to have an
equivalence.

We will see later that the following implication can't be reversed,
even for just propositions, unless weak excluded middle holds, so that
the notion of being thinly inhabited is stronger, in general, than
that of being nonempty.

\begin{code}

thinly-inhabited-gives-nonempty : {X : ğ“¤ Ì‡ }
                                â†’ is-thinly-inhabited X
                                â†’ is-nonempty X
thinly-inhabited-gives-nonempty {ğ“¤} {X} ti Î½ = III
 where
  e : ğŸš â‰ƒ (X â†’ ğŸš)
  e = Ïƒ X , ti

  I : âŒœ e âŒâ»Â¹ Ïƒâ‚€ ï¼ âŒœ e âŒâ»Â¹ Ïƒâ‚
  I = ap (âŒœ e âŒâ»Â¹) (Ïƒâ‚€ ï¼âŸ¨ dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)) âŸ©
                    Ïƒâ‚ âˆ)

  II = â‚€          ï¼âŸ¨ (inverses-are-retractions' e â‚€)â»Â¹ âŸ©
       âŒœ e âŒâ»Â¹ Ïƒâ‚€ ï¼âŸ¨ I âŸ©
       âŒœ e âŒâ»Â¹ Ïƒâ‚ ï¼âŸ¨ inverses-are-retractions' e â‚ âŸ©
       â‚          âˆ

  III : ğŸ˜
  III = zero-is-not-one II

\end{code}

In some cases the implication

 P â†’ is-thinly-inhabited P

can be reversed:

\begin{code}

thinly-inhabited-emptiness-gives-emptiness
 : {X : ğ“¤ Ì‡ }
 â†’ is-thinly-inhabited (is-empty X)
 â†’ is-empty X
thinly-inhabited-emptiness-gives-emptiness h =
 three-negations-imply-one (thinly-inhabited-gives-nonempty h)

thinly-inhabited-nonemptiness-gives-nonemptiness
 : {X : ğ“¤ Ì‡ }
 â†’ is-thinly-inhabited (is-nonempty X)
 â†’ is-nonempty X
thinly-inhabited-nonemptiness-gives-nonemptiness {ğ“¤} {X} =
 thinly-inhabited-emptiness-gives-emptiness {ğ“¤} {is-empty X}

\end{code}

The following is a digression from our main aims. Recall that a type
is called discrete if it has decidable equality.

\begin{code}

Xâ†’ğŸš-discreteness-criterion : {X : ğ“¤ Ì‡ }
                           â†’ is-empty X + is-thinly-inhabited X
                           â†’ is-discrete (X â†’ ğŸš)
Xâ†’ğŸš-discreteness-criterion (inl Î½) f g = inl (dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)))
Xâ†’ğŸš-discreteness-criterion (inr h) f g = retract-is-discrete
                                          (â‰ƒ-gives-â–· (Ïƒ _ , h))
                                          ğŸš-is-discrete
                                          f g

Pâ†’ğŸš-discreteness-criterion-necessity : {P : ğ“¤ Ì‡ }
                                     â†’ is-prop P
                                     â†’ is-discrete (P â†’ ğŸš)
                                     â†’ Â¬ P + is-thinly-inhabited P
Pâ†’ğŸš-discreteness-criterion-necessity {ğ“¤} {P} i Î´ = Ï• (Î´ Ïƒâ‚€ Ïƒâ‚)
 where
  Ï• : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Â¬ P + is-thinly-inhabited P
  Ï• (inl e) = inl (fact e)
   where
    fact : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ P
    fact e p = zero-is-not-one (ap (Î» f â†’ f p) e)
  Ï• (inr n) = inr (retraction-of-Ïƒ-gives-thinly-inhabited i (Î³ , Î³Ïƒ))
   where
    h : (f : P â†’ ğŸš) â†’ is-decidable (f ï¼ Ïƒâ‚€) â†’ ğŸš
    h f (inl _) = â‚€
    h f (inr _) = â‚

    Î³ : (P â†’ ğŸš) â†’ ğŸš
    Î³ f = h f (Î´ f Ïƒâ‚€)

    hâ‚€ : (d : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚€ d ï¼ â‚€
    hâ‚€ (inl _) = refl
    hâ‚€ (inr d) = ğŸ˜-elim (d refl)

    hâ‚ : (d : is-decidable (Ïƒâ‚ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚ d ï¼ â‚
    hâ‚ (inl e) = ğŸ˜-elim (n (e â»Â¹))
    hâ‚ (inr _) = refl

    Î³Ïƒ : Î³ âˆ˜ Ïƒ P âˆ¼ id
    Î³Ïƒ â‚€ = hâ‚€ (Î´ Ïƒâ‚€ Ïƒâ‚€)
    Î³Ïƒ â‚ = hâ‚ (Î´ Ïƒâ‚ Ïƒâ‚€)

\end{code}

Added 25th March 2022. If every nonempty proposition is thinly inhabited,
then weak excluded middle holds. We use the following lemma to prove
this. Recall that the principle of weak excluded middle, which is
equivalent to De Morgan's Law, says that for every proposition P,
either Â¬ P or Â¬Â¬ P holds.

\begin{code}

thinly-inhabited-wem-lemma : (X : ğ“¤ Ì‡)
                           â†’ is-thinly-inhabited (X + is-empty X)
                           â†’ is-empty X + is-nonempty X
thinly-inhabited-wem-lemma X ti = II
 where
  Y = X + Â¬ X

  f : Y â†’ ğŸš
  f (inl _) = â‚€
  f (inr _) = â‚

  n : ğŸš
  n = inverse (Ïƒ Y) ti f

  I : (k : ğŸš) â†’ n ï¼ k â†’ Â¬ X + Â¬Â¬ X
  I â‚€ e = inr Ï•
   where
    Iâ‚€ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) ti f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚€) âˆ

    Ï• : Â¬Â¬ X
    Ï• u = zero-is-not-one
           (â‚€         ï¼âŸ¨ (ap (Î» - â†’ - (inr u)) Iâ‚€)â»Â¹ âŸ©
            f (inr u) ï¼âŸ¨ refl âŸ©
            â‚         âˆ)

  I â‚ e = inl u
   where
    Iâ‚ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) ti f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚) âˆ

    u : Â¬ X
    u q = zero-is-not-one
           (â‚€         ï¼âŸ¨ refl âŸ©
            f (inl q) ï¼âŸ¨ ap (Î» - â†’ - (inl q)) Iâ‚ âŸ©
            â‚         âˆ)

  II : Â¬ X + Â¬Â¬ X
  II = I n refl

\end{code}

As promised above, thin inhabitedness is stronger than nonemptiness in
general, because WEM is not provable or disprovable, as it is true in
some models and false in others, and this is the main observation in
this file so far.

\begin{code}

irrefutable-props-are-thinly-inhabited-gives-WEM
 : ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ is-thinly-inhabited P)
 â†’ WEM ğ“¤
irrefutable-props-are-thinly-inhabited-gives-WEM {ğ“¤} Î± = I
 where
  module _ (Q : ğ“¤ Ì‡ ) (i : is-prop Q) where
   P = Q + Â¬ Q

   Î½ : Â¬Â¬ P
   Î½ Ï• = Ï• (inr (Î» q â†’ Ï• (inl q)))

   h : is-thinly-inhabited P
   h = Î± P (decidability-of-prop-is-prop fe i) Î½

  I : WEM ğ“¤
  I = WEM'-gives-WEM fe (Î» Q i â†’ thinly-inhabited-wem-lemma Q (h Q i))

\end{code}

Nathanael Rosnik proved the above independently within a few
hours of difference here:
https://gist.github.com/nrosnick/922250ddcc6bd04272199f59443d7510

A minor observation, giving another instance of an implication
is-thinly-inhabited P â†’ P.

\begin{code}

thinly-inhabited-wem-special : (X : ğ“¤ Ì‡)
                             â†’ is-thinly-inhabited (is-empty X + is-nonempty X)
                             â†’ is-empty X + is-nonempty X
thinly-inhabited-wem-special X h =
 Cases (thinly-inhabited-wem-lemma (Â¬ X) h)
  inr
  (inl âˆ˜ three-negations-imply-one)

\end{code}

Digression. A monad on propositions (or even a wild monad on all types?).

\begin{code}

module retraction-monad where

 Î· : {X : ğ“¤ Ì‡ } â†’ X â†’ has-retraction (Ïƒ X)
 Î· x = (Î» f â†’ f x) , (Î» n â†’ refl)

 _â™¯ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
    â†’ (X â†’ has-retraction (Ïƒ Y))
    â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 _â™¯ {ğ“¤} {ğ“¥} {X} {Y} h (Ï , ÏÏƒ) = q
  where
   a : X â†’ (Y â†’ ğŸš) â†’ ğŸš
   a x = retraction-of (Ïƒ Y) (h x)

   b : (x : X) (n : ğŸš) â†’ a x (Ïƒ Y n) ï¼ n
   b x = retraction-equation (Ïƒ Y) (h x)

   u : (Y â†’ ğŸš) â†’ ğŸš
   u g = Ï (Î» x â†’ a x g)

   v : u âˆ˜ Ïƒ Y âˆ¼ id
   v n = (u âˆ˜ Ïƒ Y) n           ï¼âŸ¨ refl âŸ©
         Ï (Î» x â†’ a x (Ïƒ Y n)) ï¼âŸ¨ ap Ï (dfunext fe (Î» x â†’ b x n)) âŸ©
         Ï (Î» _ â†’ n)           ï¼âŸ¨ refl âŸ©
         Ï (Ïƒ X n)             ï¼âŸ¨ ÏÏƒ n âŸ©
         n                     âˆ

   q : has-retraction (Ïƒ Y)
   q = u , v

 functor : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
         â†’ (X â†’ Y)
         â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 functor f = (Î· âˆ˜ f) â™¯

 Î¼ : (X : ğ“¤ Ì‡ )
   â†’ has-retraction (Ïƒ (has-retraction (Ïƒ X)))
   â†’ has-retraction (Ïƒ X)
 Î¼ X = id â™¯

\end{code}

TODO. It doesn't seem to be possible to give the structure of a monad
to is-thinly-inhabited.

Added 13th June 2024. The homotopy circle SÂ¹ is thinly inhabited
because, as it is a connected 1-type, all functions SÂ¹ â†’ ğŸš are
constant as ğŸš is a set. As another example, the type â„ of Dedekind
reals is a set, but still there may be no function â„ â†’ ğŸš other than
the constant functions, because "all functions â„ â†’ ğŸš are continuous"
is a consistent axiom. But if a totally separated type (which is
necessarily a set) is thinly inhabited, then it must be a proposition.

Recall that x ï¼â‚‚ y is defined to mean that p x = p y for all p : X â†’ ğŸš,
that is, x and y satisfy the same boolean-valued properties. When
x ï¼â‚‚ y holds for all x and y in X, we say that X is connectedâ‚‚. And
recall that, in another extreme, when x ï¼â‚‚ y implies x ï¼ y for all x
and y, we say that X is totally separated.

\begin{code}

open import TypeTopology.TotallySeparated
open import TypeTopology.DisconnectedTypes

thinly-inhabited-types-are-connectedâ‚‚ : {X : ğ“¤ Ì‡ }
                                      â†’ is-thinly-inhabited X
                                      â†’ is-connectedâ‚‚ X
thinly-inhabited-types-are-connectedâ‚‚ {ğ“¤} {X} ti x y = I
 where
  e : ğŸš â‰ƒ (X â†’ ğŸš)
  e = Ïƒ X , ti

  I : (p : X â†’ ğŸš) â†’ p x ï¼ p y
  I p = p x                 ï¼âŸ¨ happly ((inverses-are-sections' e p)â»Â¹) x âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) x ï¼âŸ¨ refl âŸ©
        âŒœ e âŒâ»Â¹ p           ï¼âŸ¨ refl âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) y ï¼âŸ¨ happly (inverses-are-sections' e p) y âŸ©
        p y                 âˆ

totally-separated-thinly-inhabited-types-are-props : {X : ğ“¤ Ì‡ }
                                                   â†’ is-totally-separated X
                                                   â†’ is-thinly-inhabited X
                                                   â†’ is-prop X
totally-separated-thinly-inhabited-types-are-props ts ti x y = I
 where
  I : x ï¼ y
  I = ts (thinly-inhabited-types-are-connectedâ‚‚ ti x y)

\end{code}

If replace the type ğŸš by the type Î© of propositions in the notion of
thin inhabitedness, then we can replace the assumption
"is-totally-separated X" by "is-set X" to get the same conclusion
(exercise). And if we replace ğŸš by some universe ğ“¤, no assumption is
needed to conclude that thinly inhabited types are propositions:

\begin{code}

module universe-discussion where

 Îº : (X : ğ“¤ Ì‡ ) â†’ ğ“¤ Ì‡ â†’ (X â†’ ğ“¤ Ì‡ )
 Îº X Y = Î» (_ : X) â†’ Y

 is-thinly-inhabited' : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
 is-thinly-inhabited' X = is-equiv (Îº X)

 thinly-inhabited'-types-are-props : {X : ğ“¤ Ì‡ }
                                   â†’ is-thinly-inhabited' X
                                   â†’ is-prop X
 thinly-inhabited'-types-are-props {ğ“¤} {X} ti x y = III
  where
   e : ğ“¤ Ì‡ â‰ƒ (X â†’ ğ“¤ Ì‡ )
   e = Îº X , ti

   I : (p : X â†’ ğ“¤ Ì‡ ) â†’ p x ï¼ p y
   I p = p x                 ï¼âŸ¨ happly ((inverses-are-sections' e p)â»Â¹) x âŸ©
         âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) x ï¼âŸ¨ refl âŸ©
         âŒœ e âŒâ»Â¹ p           ï¼âŸ¨ refl âŸ©
         âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) y ï¼âŸ¨ happly (inverses-are-sections' e p) y âŸ©
         p y                 âˆ

   II : (x ï¼ x) ï¼ (x ï¼ y)
   II = I (x ï¼_)

   III : x ï¼ y
   III = idtofun' II refl

 Î· : {X : ğ“¤ Ì‡ } â†’ is-prop X â†’ X â†’ is-thinly-inhabited' X
 Î· {ğ“¤} {X} i xâ‚€ = qinvs-are-equivs (Îº X) (s , sÎº , Îºs)
  where
   s : (X â†’ ğ“¤ Ì‡) â†’ ğ“¤ Ì‡
   s A = A xâ‚€

   sÎº : s âˆ˜ Îº X âˆ¼ id
   sÎº Y = refl

   Îºs : Îº X âˆ˜ s âˆ¼ id
   Îºs A = dfunext fe (Î» x â†’ ap A (i xâ‚€ x))

 thinly-inhabited'-gives-nonempty : {X : ğ“¤ Ì‡ }
                                  â†’ is-thinly-inhabited' X
                                  â†’ is-nonempty X
 thinly-inhabited'-gives-nonempty {ğ“¤} {X} ti Î½ = III
  where
   e : ğ“¤ Ì‡ â‰ƒ (X â†’ ğ“¤ Ì‡ )
   e = Îº X , ti

   I : âŒœ e âŒâ»Â¹  (âŒœ e âŒ ğŸ™) ï¼ âŒœ e âŒâ»Â¹  (âŒœ e âŒ ğŸ˜)
   I = ap (âŒœ e âŒâ»Â¹) (âŒœ e âŒ ğŸ™ ï¼âŸ¨ dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)) âŸ©
                     âŒœ e âŒ ğŸ˜ âˆ)

   II = ğŸ™                 ï¼âŸ¨ (inverses-are-retractions' e ğŸ™)â»Â¹ âŸ©
        âŒœ e âŒâ»Â¹ (âŒœ e âŒ ğŸ™) ï¼âŸ¨ I âŸ©
        âŒœ e âŒâ»Â¹ (âŒœ e âŒ ğŸ˜) ï¼âŸ¨ inverses-are-retractions' e ğŸ˜ âŸ©
        ğŸ˜                 âˆ

   III : ğŸ˜ {ğ“¤â‚€}
   III = ğŸ˜-elim (idtofun' II â‹†)

\end{code}

We now come back to the original notion of thin inhabitedness studied
in this file.

TODO. Derive a constructive taboo from the hypothesis

      (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ is-thinly-inhabited P â†’ P.

The difficulty, of course, is to come up with a type P which we can
prove to be thinly inhabited but (we can't prove to be pointed and)
whose pointedness would give a constructive taboo.
