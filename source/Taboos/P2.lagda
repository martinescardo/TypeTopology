Martin Escardo, 24th March 2022 with minor improvements June 2024.

This file is a apropos the discussion at the end of the file
Ordinals.NotationInterpretation2.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt

module Taboos.P2 (fe : Fun-Ext) where

private
 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open import MLTT.Two
open import MLTT.Two-Properties

open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.ClassicalLogic
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

Ïƒ : (X : ğ“¤ Ì‡ ) â†’ ğŸš â†’ (X â†’ ğŸš)
Ïƒ X n = Î» _ â†’ n

\end{code}

Abbreviations.

\begin{code}

Ïƒâ‚€ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚€ {ğ“¤} {X} = Ïƒ X â‚€

Ïƒâ‚ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚ {ğ“¤} {X} = Ïƒ X â‚

\end{code}

Recall that we say that a type X is empty to mean Â¬ X, that is X â†’ ğŸ˜,
and nonempty to mean Â¬Â¬ X.

\begin{code}

emptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-empty X â†” (Ïƒâ‚€ ï¼ Ïƒâ‚)
emptiness-criterion {ğ“¤} X = (f , g)
 where
  f : Â¬ X â†’ Ïƒâ‚€ ï¼ Ïƒâ‚
  f Î½ = dfunext fe (Î» (x : X) â†’ ğŸ˜-elim (Î½ x))

  g : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ X
  g e x = zero-is-not-one
           (â‚€    ï¼âŸ¨ refl âŸ©
            Ïƒâ‚€ x ï¼âŸ¨ happly e x âŸ©
            Ïƒâ‚ x ï¼âŸ¨ refl âŸ©
            â‚    âˆ)

nonemptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-nonempty X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
nonemptiness-criterion {ğ“¤} X = I (emptiness-criterion X)
 where
  I : type-of (emptiness-criterion X) â†’ Â¬Â¬ X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
  I (f , g) = (Î» (Î½ : Â¬Â¬ X) (e : Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Î½ (g e)) ,
              contrapositive f

\end{code}

The main notion studied in this file is the following.

\begin{code}

is-thinly-populated : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-thinly-populated X = is-equiv (Ïƒ X)

being-thinly-populated-is-prop : {X : ğ“¤ Ì‡ } â†’ is-prop (is-thinly-populated X)
being-thinly-populated-is-prop {ğ“¤} {X} = being-equiv-is-prop fe' (Ïƒ X)

\end{code}

For propositions X, this is equivalent to the map Ïƒ X having a
retraction Ï.

                            Ïƒ X
                          ğŸš  â†ª  (X â†’ ğŸš)
                          ğŸš  â†  (X â†’ ğŸš)
                             Ï

In general there can be many maps Ï with Ï âˆ˜ Ïƒ X âˆ¼ id, but there is at
most one if X is a proposition:

\begin{code}

retraction-of-Ïƒ-is-section : {P : ğ“¤ Ì‡ }
                           â†’ is-prop P
                           â†’ (Ï : (P â†’ ğŸš) â†’ ğŸš)
                           â†’ Ï âˆ˜ Ïƒ P âˆ¼ id
                           â†’ Ïƒ P âˆ˜ Ï âˆ¼ id
retraction-of-Ïƒ-is-section {ğ“¤} {P} i Ï h f = IV
 where
  I : (p : P) â†’ Ï f ï¼ f p
  I p = Ï f           ï¼âŸ¨ ap Ï III âŸ©
        Ï (Ïƒ P (f p)) ï¼âŸ¨ h (f p) âŸ©
        f p           âˆ
   where
    II : f âˆ¼ Ïƒ P (f p)
    II q = f q         ï¼âŸ¨ ap f (i q p) âŸ©
           f p         ï¼âŸ¨ refl âŸ©
           Ïƒ P (f p) q âˆ

    III : f ï¼ Ïƒ P (f p)
    III = dfunext fe II

  IV : Ïƒ P (Ï f) ï¼ f
  IV = dfunext fe I

Ïƒ-having-retraction-is-prop : {X : ğ“¤ Ì‡ }
                            â†’ is-prop X
                            â†’ is-prop (has-retraction (Ïƒ X))
Ïƒ-having-retraction-is-prop {ğ“¤} {X} i =
 prop-criterion
  (Î» (Ï , ÏÏƒ) â†’ sections-have-at-most-one-retraction fe' (Ïƒ X)
                 (Ï , retraction-of-Ïƒ-is-section i Ï ÏÏƒ))

retraction-of-Ïƒ-gives-thin-populatedness : {P : ğ“¤ Ì‡ }
                                         â†’ is-prop P
                                         â†’ has-retraction (Ïƒ P)
                                         â†’ is-thinly-populated P
retraction-of-Ïƒ-gives-thin-populatedness {ğ“¤} {P} i (Ï , ÏÏƒ) =
 qinvs-are-equivs (Ïƒ P) (Ï , ÏÏƒ , retraction-of-Ïƒ-is-section i Ï ÏÏƒ)

\end{code}

For the converse we don't need X to be a proposition, of course.

\begin{code}

thin-populatedness-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                                         â†’ is-thinly-populated X
                                         â†’ has-retraction (Ïƒ X)
thin-populatedness-gives-retraction-of-Ïƒ {ğ“¤} {X} = equivs-are-sections (Ïƒ X)

point-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                            â†’ X
                            â†’ has-retraction (Ïƒ X)
point-gives-retraction-of-Ïƒ {ğ“¤} {X} x = (Î³ , Î³Ïƒ)
 where
  Î³ : (X â†’ ğŸš) â†’ ğŸš
  Î³ f = f x

  Î³Ïƒ : Î³ âˆ˜ Ïƒ X âˆ¼ id
  Î³Ïƒ n = refl

\end{code}

Notice, however, that pointed types X other than propositions are not
thinly-populated in general. An example is the type X := ğŸš, because there
are four maps X â†’ ğŸš in this case, and we need exactly two to have an
equivalence.

\begin{code}

point-gives-thinly-populated : {P : ğ“¤ Ì‡ }
                             â†’ is-prop P
                             â†’ P
                             â†’ is-thinly-populated P
point-gives-thinly-populated {ğ“¤} {P} i p =
 retraction-of-Ïƒ-gives-thin-populatedness i (point-gives-retraction-of-Ïƒ p)

\end{code}

We will later see that the following implication can't be reversed
unless weak excluded middle holds, so that being thinly populated is
stronger, in general, than being nonempty.

\begin{code}

thinly-populated-gives-nonempty : {X : ğ“¤ Ì‡ }
                                â†’ is-thinly-populated X
                                â†’ is-nonempty X
thinly-populated-gives-nonempty {ğ“¤} {X} e Î½ = zero-is-not-one II
 where
  I : inverse (Ïƒ X) e Ïƒâ‚€ ï¼ inverse (Ïƒ X) e Ïƒâ‚
  I = ap (inverse (Ïƒ X) e) (Ïƒâ‚€ ï¼âŸ¨ dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)) âŸ©
                            Ïƒâ‚ âˆ)

  II = â‚€                       ï¼âŸ¨ (inverses-are-retractions (Ïƒ X) e â‚€)â»Â¹ âŸ©
       inverse (Ïƒ X) e (Ïƒ X â‚€) ï¼âŸ¨ I âŸ©
       inverse (Ïƒ X) e (Ïƒ X â‚) ï¼âŸ¨ inverses-are-retractions (Ïƒ X) e â‚ âŸ©
       â‚                       âˆ

\end{code}

In some cases the above implication P â†’ is-thinly-populated P can be
reversed:

\begin{code}

thinly-populated-emptiness-gives-emptiness : {X : ğ“¤ Ì‡ }
                                           â†’ is-thinly-populated (is-empty X)
                                           â†’ is-empty X
thinly-populated-emptiness-gives-emptiness h =
 three-negations-imply-one (thinly-populated-gives-nonempty h)

thinly-populated-nonemptiness-gives-nonemptiness : {X : ğ“¤ Ì‡ }
                                                 â†’ is-thinly-populated (is-nonempty X)
                                                 â†’ is-nonempty X
thinly-populated-nonemptiness-gives-nonemptiness {ğ“¤} {X} =
 thinly-populated-emptiness-gives-emptiness {ğ“¤} {is-empty X}

Xâ†’ğŸš-discreteness-criterion : {X : ğ“¤ Ì‡ }
                           â†’ is-empty X + is-thinly-populated X
                           â†’ is-discrete (X â†’ ğŸš)
Xâ†’ğŸš-discreteness-criterion (inl Î½) f g = inl (dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)))
Xâ†’ğŸš-discreteness-criterion (inr h) f g = retract-is-discrete
                                          (â‰ƒ-gives-â–· (Ïƒ _ , h))
                                          ğŸš-is-discrete
                                          f g

Pâ†’ğŸš-discreteness-criterion-necessity : {P : ğ“¤ Ì‡ }
                                     â†’ is-prop P
                                     â†’ is-discrete (P â†’ ğŸš)
                                     â†’ Â¬ P + is-thinly-populated P
Pâ†’ğŸš-discreteness-criterion-necessity {ğ“¤} {P} i Î´ = Ï• (Î´ Ïƒâ‚€ Ïƒâ‚)
 where
  Ï• : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Â¬ P + is-thinly-populated P
  Ï• (inl e) = inl (fact e)
   where
    fact : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ P
    fact e p = zero-is-not-one (ap (Î» f â†’ f p) e)
  Ï• (inr n) = inr (retraction-of-Ïƒ-gives-thin-populatedness i (Î³ , Î³Ïƒ))
   where
    h : (f : P â†’ ğŸš) â†’ is-decidable (f ï¼ Ïƒâ‚€) â†’ ğŸš
    h f (inl _) = â‚€
    h f (inr _) = â‚

    Î³ : (P â†’ ğŸš) â†’ ğŸš
    Î³ f = h f (Î´ f Ïƒâ‚€)

    hâ‚€ : (d : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚€ d ï¼ â‚€
    hâ‚€ (inl _) = refl
    hâ‚€ (inr d) = ğŸ˜-elim (d refl)

    hâ‚ : (d : is-decidable (Ïƒâ‚ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚ d ï¼ â‚
    hâ‚ (inl e) = ğŸ˜-elim (n (e â»Â¹))
    hâ‚ (inr _) = refl

    Î³Ïƒ : Î³ âˆ˜ Ïƒ P âˆ¼ id
    Î³Ïƒ â‚€ = hâ‚€ (Î´ Ïƒâ‚€ Ïƒâ‚€)
    Î³Ïƒ â‚ = hâ‚ (Î´ Ïƒâ‚ Ïƒâ‚€)

\end{code}

Added 25th March 2022. If every irrefutable proposition is
thinly-populated, then weak excluded middle holds.

\begin{code}

thin-populatedness-wem-lemma : (X : ğ“¤ Ì‡)
                             â†’ is-thinly-populated (X + is-empty X)
                             â†’ is-empty X + is-nonempty X
thin-populatedness-wem-lemma X h = II
 where
  Y = X + Â¬ X

  f : Y â†’ ğŸš
  f (inl _) = â‚€
  f (inr _) = â‚

  n : ğŸš
  n = inverse (Ïƒ Y) h f

  I : (k : ğŸš) â†’ n ï¼ k â†’ Â¬ X + is-nonempty X
  I â‚€ e = inr Ï•
   where
    Iâ‚€ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) h f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚€) âˆ

    Ï• : Â¬Â¬ X
    Ï• u = zero-is-not-one
           (â‚€         ï¼âŸ¨ (ap (Î» - â†’ - (inr u)) Iâ‚€)â»Â¹ âŸ©
            f (inr u) ï¼âŸ¨ refl âŸ©
            â‚         âˆ)

  I â‚ e = inl u
   where
    Iâ‚ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) h f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚) âˆ

    u : Â¬ X
    u q = zero-is-not-one
           (â‚€         ï¼âŸ¨ refl âŸ©
            f (inl q) ï¼âŸ¨ ap (Î» - â†’ - (inl q)) Iâ‚ âŸ©
            â‚         âˆ)

  II : Â¬ X + Â¬Â¬ X
  II = I n refl

\end{code}

As promised above, thin population is stronger than nonemptiness in
general:

\begin{code}

nonempty-props-are-thinly-populated-gives-WEM
 : ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ is-nonempty P â†’ is-thinly-populated P)
 â†’ WEM ğ“¤
nonempty-props-are-thinly-populated-gives-WEM {ğ“¤} Î± Q i =
  thin-populatedness-wem-lemma Q h
 where
  P = Q + Â¬ Q

  Î½ : Â¬Â¬ P
  Î½ Ï• = Ï• (inr (Î» q â†’ Ï• (inl q)))

  h : is-thinly-populated P
  h = Î± P (decidability-of-prop-is-prop fe i) Î½

\end{code}

Nathanael Rosnik proved the above independently within a few
hours of difference here:
https://gist.github.com/nrosnick/922250ddcc6bd04272199f59443d7510

A special case of the lemma:

\begin{code}

thin-populatedness-wem-special : (X : ğ“¤ Ì‡)
                               â†’ is-thinly-populated (is-empty X + is-nonempty X)
                               â†’ is-empty X + is-nonempty X
thin-populatedness-wem-special X h =
 Cases (thin-populatedness-wem-lemma (Â¬ X) h)
  inr
  (inl âˆ˜ three-negations-imply-one)

\end{code}

Digression. A monad on propositions (or even a wild monad on all types?).

\begin{code}

module retraction-monad where

 Î· : {X : ğ“¤ Ì‡ } â†’ X â†’ has-retraction (Ïƒ X)
 Î· x = (Î» f â†’ f x) , (Î» n â†’ refl)

 _â™¯ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
    â†’ (X â†’ has-retraction (Ïƒ Y))
    â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 _â™¯ {ğ“¤} {ğ“¥} {X} {Y} h (Ï , ÏÏƒ) = q
  where
   a : X â†’ (Y â†’ ğŸš) â†’ ğŸš
   a x = retraction-of (Ïƒ Y) (h x)

   b : (x : X) (n : ğŸš) â†’ a x (Ïƒ Y n) ï¼ n
   b x = retraction-equation (Ïƒ Y) (h x)

   u : (Y â†’ ğŸš) â†’ ğŸš
   u g = Ï (Î» x â†’ a x g)

   v : u âˆ˜ Ïƒ Y âˆ¼ id
   v n = (u âˆ˜ Ïƒ Y) n           ï¼âŸ¨ refl âŸ©
         Ï (Î» x â†’ a x (Ïƒ Y n)) ï¼âŸ¨ ap Ï (dfunext fe (Î» x â†’ b x n)) âŸ©
         Ï (Î» _ â†’ n)           ï¼âŸ¨ refl âŸ©
         Ï (Ïƒ X n)             ï¼âŸ¨ ÏÏƒ n âŸ©
         n                     âˆ

   q : has-retraction (Ïƒ Y)
   q = u , v

 functor : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
         â†’ (X â†’ Y)
         â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 functor f = (Î· âˆ˜ f) â™¯

 Î¼ : (X : ğ“¤ Ì‡ )
   â†’ has-retraction (Ïƒ (has-retraction (Ïƒ X)))
   â†’ has-retraction (Ïƒ X)
 Î¼ X = id â™¯

\end{code}

TODO. It doesn't seem to be possible to give the structure of a monad
to is-thinly-inhabited.

Added 13th June 2024. The homotopy circle SÂ¹ is thinly populated
because, being a connected 1-type, all functions SÂ¹ â†’ ğŸš are constant
as ğŸš is a set. As another example, the type â„ of Dedekind reals is a
set, but still there may be no function â„ â†’ ğŸš other than the constant
functions, because "all functions â„ â†’ ğŸš" are continuous is a
consistent axiom. But if a totally separated type (which is
necessarily a set) is thinly populated, then it must be a proposition.

\begin{code}

open import TypeTopology.TotallySeparated

thin-totally-separated-types-are-props : {X : ğ“¤ Ì‡ }
                                       â†’ is-totally-separated X
                                       â†’ is-thinly-populated X
                                       â†’ is-prop X
thin-totally-separated-types-are-props {ğ“¤} {X} ts tp x y = II
 where
  e : ğŸš â‰ƒ (X â†’ ğŸš)
  e = Ïƒ X , tp

  I : (f : X â†’ ğŸš) â†’ f x ï¼ f y
  I f = f x                 ï¼âŸ¨ happly ((inverses-are-sections' e f)â»Â¹) x âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ f) x ï¼âŸ¨ refl âŸ©
        âŒœ e âŒâ»Â¹ f           ï¼âŸ¨ refl âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ f) y ï¼âŸ¨ happly (inverses-are-sections' e f) y âŸ©
        f y                 âˆ

  II : x ï¼ y
  II = ts I

\end{code}

TODO. Derive a constructive taboo from the hypothesis

      (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ is-thinly-populated P â†’ P.
