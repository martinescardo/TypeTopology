Martin Escardo, 24th March 2022
with minor improvements and additions June 2024.

This file is a apropos the discussion at the end of the file
Ordinals.NotationInterpretation2.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module Taboos.P2 (fe : Fun-Ext) where

private
 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open import MLTT.Spartan
open import MLTT.Two
open import MLTT.Two-Properties
open import UF.Base
open import UF.ClassicalLogic
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

\end{code}

The following map plays a central role in the investigations of this
file.

\begin{code}

Ïƒ : (X : ğ“¤ Ì‡ ) â†’ ğŸš â†’ (X â†’ ğŸš)
Ïƒ X n = Î» _ â†’ n

\end{code}

Abbreviations.

\begin{code}

Ïƒâ‚€ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚€ {ğ“¤} {X} = Ïƒ X â‚€

Ïƒâ‚ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸš)
Ïƒâ‚ {ğ“¤} {X} = Ïƒ X â‚

\end{code}

Recall that we say that a type X is empty to mean Â¬ X, namely X â†’ ğŸ˜,
and nonempty to mean Â¬Â¬ X.

In general, if the type X is a proposition we will write "Â¬ X" rather
than the synonym "is-empty X", and "Â¬Â¬ X" rather than the synonym
"is-nonempty X". Also, we will pronounce "Â¬Â¬ X" as "X is irrefutable"
when X is a proposition.

\begin{code}

emptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-empty X â†” (Ïƒâ‚€ ï¼ Ïƒâ‚)
emptiness-criterion {ğ“¤} X = (f , g)
 where
  f : Â¬ X â†’ Ïƒâ‚€ ï¼ Ïƒâ‚
  f Î½ = dfunext fe (Î» (x : X) â†’ ğŸ˜-elim (Î½ x))

  g : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ X
  g e x = zero-is-not-one
           (â‚€    ï¼âŸ¨ refl âŸ©
            Ïƒâ‚€ x ï¼âŸ¨ happly e x âŸ©
            Ïƒâ‚ x ï¼âŸ¨ refl âŸ©
            â‚    âˆ)

nonemptiness-criterion : (X : ğ“¤ Ì‡ ) â†’ is-nonempty X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
nonemptiness-criterion {ğ“¤} X = I (emptiness-criterion X)
 where
  I : type-of (emptiness-criterion X) â†’ Â¬Â¬ X â†” (Ïƒâ‚€ â‰  Ïƒâ‚)
  I (f , g) = (Î» (Î½ : Â¬Â¬ X) (e : Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Î½ (g e)) ,
              contrapositive f

\end{code}

The main notion studied in this file is the following, which we refer
to as "two-inhabitedness". We are mainly interested in this notion
when X is a proposition. (But we prove below that for certain types
called totally separated, if they are two-inhabited then they are
necessarily propositions.)

Another terminology we experimented with for this notion was "thinly
inhabited", but, as suggestive as it may be, there may be other
related notions which equally deserve this alternative
terminology. But the overall idea is that in some sense the type is
inhabited, but we may not be able get hold of any point of the type,
and in some sense there is at most one inhabitant anyway, similarly to
nonempty types

\begin{code}

is-inhabitedâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-inhabitedâ‚‚ X = is-equiv (Ïƒ X)

being-inhabitedâ‚‚-is-prop : {X : ğ“¤ Ì‡ } â†’ is-prop (is-inhabitedâ‚‚ X)
being-inhabitedâ‚‚-is-prop {ğ“¤} {X} = being-equiv-is-prop fe' (Ïƒ X)

\end{code}

For a proposition P, we will use the synonym "holdsâ‚‚ P" for
"is-inhabitedâ‚‚ P".

\begin{code}

holdsâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
holdsâ‚‚ = is-inhabitedâ‚‚

\end{code}

Exercise (easy but tedious, and hence I didn't implement it). A type X
is two-inhabited if and only if there is *any* equivalence ğŸš â‰ƒ (X â†’ ğŸš).
If this gets added here, it should be added towards the end as an
appendix (but before the main open problem), to avoid unnecessary
distractions.

For propositions X, the two-inhabitedness of X is equivalent to the
map Ïƒ X having a retraction Ï.

                            Ïƒ X
                          ğŸš  â†ª  (X â†’ ğŸš)
                          ğŸš  â†  (X â†’ ğŸš)
                             Ï

In general there can be many maps Ï with Ï âˆ˜ Ïƒ X âˆ¼ id, but there is at
most one if X is a proposition. We use the following lemma to prove
this:

\begin{code}

retraction-of-Ïƒ-is-section : {P : ğ“¤ Ì‡ }
                           â†’ is-prop P
                           â†’ (Ï : (P â†’ ğŸš) â†’ ğŸš)
                           â†’ Ï âˆ˜ Ïƒ P âˆ¼ id
                           â†’ Ïƒ P âˆ˜ Ï âˆ¼ id
retraction-of-Ïƒ-is-section {ğ“¤} {P} i Ï h f = IV
 where
  I : (p : P) â†’ Ï f ï¼ f p
  I p = Ï f           ï¼âŸ¨ ap Ï III âŸ©
        Ï (Ïƒ P (f p)) ï¼âŸ¨ h (f p) âŸ©
        f p           âˆ
   where
    II : f âˆ¼ Ïƒ P (f p)
    II q = f q         ï¼âŸ¨ ap f (i q p) âŸ©
           f p         ï¼âŸ¨ refl âŸ©
           Ïƒ P (f p) q âˆ

    III : f ï¼ Ïƒ P (f p)
    III = dfunext fe II

  IV : Ïƒ P (Ï f) ï¼ f
  IV = dfunext fe I

Ïƒ-having-retraction-is-prop : {X : ğ“¤ Ì‡ }
                            â†’ is-prop X
                            â†’ is-prop (has-retraction (Ïƒ X))
Ïƒ-having-retraction-is-prop {ğ“¤} {X} i =
 prop-criterion
  (Î» (Ï , ÏÏƒ) â†’ sections-have-at-most-one-retraction fe' (Ïƒ X)
                 (Ï , retraction-of-Ïƒ-is-section i Ï ÏÏƒ))

retraction-of-Ïƒ-gives-inhabitedâ‚‚ : {P : ğ“¤ Ì‡ }
                                 â†’ is-prop P
                                 â†’ has-retraction (Ïƒ P)
                                 â†’ holdsâ‚‚ P
retraction-of-Ïƒ-gives-inhabitedâ‚‚ {ğ“¤} {P} i (Ï , ÏÏƒ) =
 qinvs-are-equivs (Ïƒ P) (Ï , ÏÏƒ , retraction-of-Ïƒ-is-section i Ï ÏÏƒ)

\end{code}

For the converse we don't need X to be a proposition, of course.

\begin{code}

inhabitedâ‚‚-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                                 â†’ is-inhabitedâ‚‚ X
                                 â†’ has-retraction (Ïƒ X)
inhabitedâ‚‚-gives-retraction-of-Ïƒ {ğ“¤} {X} = equivs-are-sections (Ïƒ X)

\end{code}

Next we want to show that P â†’ holdsâ‚‚ P for any proposition P,
and we use the following lemma.

\begin{code}

point-gives-retraction-of-Ïƒ : {X : ğ“¤ Ì‡ }
                            â†’ X
                            â†’ has-retraction (Ïƒ X)
point-gives-retraction-of-Ïƒ {ğ“¤} {X} x = (Î³ , Î³Ïƒ)
 where
  Î³ : (X â†’ ğŸš) â†’ ğŸš
  Î³ f = f x

  Î³Ïƒ : Î³ âˆ˜ Ïƒ X âˆ¼ id
  Î³Ïƒ n = refl

point-gives-holdsâ‚‚ : {P : ğ“¤ Ì‡ }
                   â†’ is-prop P
                   â†’ P
                   â†’ holdsâ‚‚ P
point-gives-holdsâ‚‚ {ğ“¤} {P} i p =
 retraction-of-Ïƒ-gives-inhabitedâ‚‚ i (point-gives-retraction-of-Ïƒ p)

\end{code}

Notice, however, that pointed types X other than propositions are not
two-inhabited in general. An example is the type X := ğŸš, because there
are four maps X â†’ ğŸš in this case, and we need exactly two to have an
equivalence.

We will see later that the following implication can't be reversed,
even for just propositions, unless weak excluded middle holds, so that
being two-inhabited is stronger, in general, than being nonempty.

\begin{code}

inhabitedâ‚‚-gives-nonempty : {X : ğ“¤ Ì‡ }
                          â†’ is-inhabitedâ‚‚ X
                          â†’ is-nonempty X
inhabitedâ‚‚-gives-nonempty {ğ“¤} {X} e Î½ = zero-is-not-one II
 where
  I : inverse (Ïƒ X) e Ïƒâ‚€ ï¼ inverse (Ïƒ X) e Ïƒâ‚
  I = ap (inverse (Ïƒ X) e) (Ïƒâ‚€ ï¼âŸ¨ dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)) âŸ©
                            Ïƒâ‚ âˆ)

  II = â‚€                       ï¼âŸ¨ (inverses-are-retractions (Ïƒ X) e â‚€)â»Â¹ âŸ©
       inverse (Ïƒ X) e (Ïƒ X â‚€) ï¼âŸ¨ I âŸ©
       inverse (Ïƒ X) e (Ïƒ X â‚) ï¼âŸ¨ inverses-are-retractions (Ïƒ X) e â‚ âŸ©
       â‚                       âˆ

\end{code}

In some cases the implication

 X â†’ is-inhabitedâ‚‚ X

can be reversed:

\begin{code}

inhabitedâ‚‚-emptiness-gives-emptiness : {X : ğ“¤ Ì‡ }
                                     â†’ is-inhabitedâ‚‚ (is-empty X)
                                     â†’ is-empty X
inhabitedâ‚‚-emptiness-gives-emptiness h =
 three-negations-imply-one (inhabitedâ‚‚-gives-nonempty h)

inhabitedâ‚‚-nonemptiness-gives-nonemptiness : {X : ğ“¤ Ì‡ }
                                           â†’ is-inhabitedâ‚‚ (is-nonempty X)
                                           â†’ is-nonempty X
inhabitedâ‚‚-nonemptiness-gives-nonemptiness {ğ“¤} {X} =
 inhabitedâ‚‚-emptiness-gives-emptiness {ğ“¤} {is-empty X}

\end{code}

The following is a digression from our main aims. Recall that a type
is called discrete if it has decidable equality.

\begin{code}

Xâ†’ğŸš-discreteness-criterion : {X : ğ“¤ Ì‡ }
                           â†’ is-empty X + is-inhabitedâ‚‚ X
                           â†’ is-discrete (X â†’ ğŸš)
Xâ†’ğŸš-discreteness-criterion (inl Î½) f g = inl (dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x)))
Xâ†’ğŸš-discreteness-criterion (inr h) f g = retract-is-discrete
                                          (â‰ƒ-gives-â–· (Ïƒ _ , h))
                                          ğŸš-is-discrete
                                          f g

Pâ†’ğŸš-discreteness-criterion-necessity : {P : ğ“¤ Ì‡ }
                                     â†’ is-prop P
                                     â†’ is-discrete (P â†’ ğŸš)
                                     â†’ Â¬ P + holdsâ‚‚ P
Pâ†’ğŸš-discreteness-criterion-necessity {ğ“¤} {P} i Î´ = Ï• (Î´ Ïƒâ‚€ Ïƒâ‚)
 where
  Ï• : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚) â†’ Â¬ P + holdsâ‚‚ P
  Ï• (inl e) = inl (fact e)
   where
    fact : Ïƒâ‚€ ï¼ Ïƒâ‚ â†’ Â¬ P
    fact e p = zero-is-not-one (ap (Î» f â†’ f p) e)
  Ï• (inr n) = inr (retraction-of-Ïƒ-gives-inhabitedâ‚‚ i (Î³ , Î³Ïƒ))
   where
    h : (f : P â†’ ğŸš) â†’ is-decidable (f ï¼ Ïƒâ‚€) â†’ ğŸš
    h f (inl _) = â‚€
    h f (inr _) = â‚

    Î³ : (P â†’ ğŸš) â†’ ğŸš
    Î³ f = h f (Î´ f Ïƒâ‚€)

    hâ‚€ : (d : is-decidable (Ïƒâ‚€ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚€ d ï¼ â‚€
    hâ‚€ (inl _) = refl
    hâ‚€ (inr d) = ğŸ˜-elim (d refl)

    hâ‚ : (d : is-decidable (Ïƒâ‚ ï¼ Ïƒâ‚€)) â†’ h Ïƒâ‚ d ï¼ â‚
    hâ‚ (inl e) = ğŸ˜-elim (n (e â»Â¹))
    hâ‚ (inr _) = refl

    Î³Ïƒ : Î³ âˆ˜ Ïƒ P âˆ¼ id
    Î³Ïƒ â‚€ = hâ‚€ (Î´ Ïƒâ‚€ Ïƒâ‚€)
    Î³Ïƒ â‚ = hâ‚ (Î´ Ïƒâ‚ Ïƒâ‚€)

\end{code}

Added 25th March 2022. If every nonempty proposition is inhabitedâ‚‚,
then weak excluded middle holds. We use the following lemma to prove
this. Recall that the principle of weak excluded middle, which is
equivalent to De Morgan's Law, say that for every proposition P,
either Â¬ P or Â¬Â¬ P holds.

\begin{code}

inhabitedâ‚‚-wem-lemma : (X : ğ“¤ Ì‡)
                     â†’ is-inhabitedâ‚‚ (X + is-empty X)
                     â†’ is-empty X + is-nonempty X
inhabitedâ‚‚-wem-lemma X h = II
 where
  Y = X + Â¬ X

  f : Y â†’ ğŸš
  f (inl _) = â‚€
  f (inr _) = â‚

  n : ğŸš
  n = inverse (Ïƒ Y) h f

  I : (k : ğŸš) â†’ n ï¼ k â†’ Â¬ X + is-nonempty X
  I â‚€ e = inr Ï•
   where
    Iâ‚€ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) h f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚€) âˆ

    Ï• : Â¬Â¬ X
    Ï• u = zero-is-not-one
           (â‚€         ï¼âŸ¨ (ap (Î» - â†’ - (inr u)) Iâ‚€)â»Â¹ âŸ©
            f (inr u) ï¼âŸ¨ refl âŸ©
            â‚         âˆ)

  I â‚ e = inl u
   where
    Iâ‚ = f         ï¼âŸ¨ (inverses-are-sections (Ïƒ Y) h f)â»Â¹ âŸ©
         Ïƒ Y n     ï¼âŸ¨ ap (Ïƒ Y) e âŸ©
         (Î» _ â†’ â‚) âˆ

    u : Â¬ X
    u q = zero-is-not-one
           (â‚€         ï¼âŸ¨ refl âŸ©
            f (inl q) ï¼âŸ¨ ap (Î» - â†’ - (inl q)) Iâ‚ âŸ©
            â‚         âˆ)

  II : Â¬ X + Â¬Â¬ X
  II = I n refl

\end{code}

As promised above, two-inhabitedness is stronger than nonemptiness in
general, because WEM is not provable or disprovable, as it is true in
some models and false in others, and this is the main observation in
this file so far.

\begin{code}

irrefutable-props-holdâ‚‚-gives-WEM
 : ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ holdsâ‚‚ P)
 â†’ WEM ğ“¤
irrefutable-props-holdâ‚‚-gives-WEM {ğ“¤} Î± Q i =
  inhabitedâ‚‚-wem-lemma Q h
 where
  P = Q + Â¬ Q

  Î½ : Â¬Â¬ P
  Î½ Ï• = Ï• (inr (Î» q â†’ Ï• (inl q)))

  h : holdsâ‚‚ P
  h = Î± P (decidability-of-prop-is-prop fe i) Î½

\end{code}

Nathanael Rosnik proved the above independently within a few
hours of difference here:
https://gist.github.com/nrosnick/922250ddcc6bd04272199f59443d7510

A minor observation:

\begin{code}

inhabitedâ‚‚-wem-special : (X : ğ“¤ Ì‡)
                       â†’ holdsâ‚‚ (is-empty X + is-nonempty X)
                       â†’ is-empty X + is-nonempty X
inhabitedâ‚‚-wem-special X h =
 Cases (inhabitedâ‚‚-wem-lemma (Â¬ X) h)
  inr
  (inl âˆ˜ three-negations-imply-one)

\end{code}

Digression. A monad on propositions (or even a wild monad on all types?).

\begin{code}

module retraction-monad where

 Î· : {X : ğ“¤ Ì‡ } â†’ X â†’ has-retraction (Ïƒ X)
 Î· x = (Î» f â†’ f x) , (Î» n â†’ refl)

 _â™¯ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
    â†’ (X â†’ has-retraction (Ïƒ Y))
    â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 _â™¯ {ğ“¤} {ğ“¥} {X} {Y} h (Ï , ÏÏƒ) = q
  where
   a : X â†’ (Y â†’ ğŸš) â†’ ğŸš
   a x = retraction-of (Ïƒ Y) (h x)

   b : (x : X) (n : ğŸš) â†’ a x (Ïƒ Y n) ï¼ n
   b x = retraction-equation (Ïƒ Y) (h x)

   u : (Y â†’ ğŸš) â†’ ğŸš
   u g = Ï (Î» x â†’ a x g)

   v : u âˆ˜ Ïƒ Y âˆ¼ id
   v n = (u âˆ˜ Ïƒ Y) n           ï¼âŸ¨ refl âŸ©
         Ï (Î» x â†’ a x (Ïƒ Y n)) ï¼âŸ¨ ap Ï (dfunext fe (Î» x â†’ b x n)) âŸ©
         Ï (Î» _ â†’ n)           ï¼âŸ¨ refl âŸ©
         Ï (Ïƒ X n)             ï¼âŸ¨ ÏÏƒ n âŸ©
         n                     âˆ

   q : has-retraction (Ïƒ Y)
   q = u , v

 functor : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
         â†’ (X â†’ Y)
         â†’ (has-retraction (Ïƒ X) â†’ has-retraction (Ïƒ Y))
 functor f = (Î· âˆ˜ f) â™¯

 Î¼ : (X : ğ“¤ Ì‡ )
   â†’ has-retraction (Ïƒ (has-retraction (Ïƒ X)))
   â†’ has-retraction (Ïƒ X)
 Î¼ X = id â™¯

\end{code}

TODO. It doesn't seem to be possible to give the structure of a monad
to is-inhabitedâ‚‚.

Added 13th June 2024. The homotopy circle SÂ¹ is two-inhabited because,
as its a connected 1-type, all functions SÂ¹ â†’ ğŸš are constant as ğŸš is a
set. As another example, the type â„ of Dedekind reals is a set, but
still there may be no function â„ â†’ ğŸš other than the constant
functions, because "all functions â„ â†’ ğŸš are continuous" is a
consistent axiom. But if a totally separated type (which is
necessarily a set) is two-inhabited, then it must be a proposition.

Recall that x ï¼â‚‚ y is defined to mean that p x = p y for all p : X â†’ ğŸš,
that is, x and y satisfy the same boolean-valued properties. When
all x ï¼â‚‚ y holds for all x and y in X, we say that X is
connectedâ‚‚. And recall that, in another extreme, when x ï¼â‚‚ y implies
x ï¼ y for all x and y, we say that X is totally separated.w

\begin{code}

open import TypeTopology.TotallySeparated
open import TypeTopology.DisconnectedTypes

inhabitedâ‚‚-types-are-connectedâ‚‚ : {X : ğ“¤ Ì‡ }
                                â†’ is-inhabitedâ‚‚ X
                                â†’ is-connectedâ‚‚ X
inhabitedâ‚‚-types-are-connectedâ‚‚ {ğ“¤} {X} tp x y = I
 where
  e : ğŸš â‰ƒ (X â†’ ğŸš)
  e = Ïƒ X , tp

  I : (p : X â†’ ğŸš) â†’ p x ï¼ p y
  I p = p x                 ï¼âŸ¨ happly ((inverses-are-sections' e p)â»Â¹) x âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) x ï¼âŸ¨ refl âŸ©
        âŒœ e âŒâ»Â¹ p           ï¼âŸ¨ refl âŸ©
        âŒœ e âŒ (âŒœ e âŒâ»Â¹ p) y ï¼âŸ¨ happly (inverses-are-sections' e p) y âŸ©
        p y                 âˆ

totally-separated-inhabitedâ‚‚-types-are-props : {X : ğ“¤ Ì‡ }
                                             â†’ is-totally-separated X
                                             â†’ is-inhabitedâ‚‚ X
                                             â†’ is-prop X
totally-separated-inhabitedâ‚‚-types-are-props ts tp x y = I
 where
  I : x ï¼ y
  I = ts (inhabitedâ‚‚-types-are-connectedâ‚‚ tp x y)

\end{code}

TODO. Derive a constructive taboo from the hypothesis

      (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ holdsâ‚‚ P â†’ P.

The difficulty, of course, is to come up with a type P which we can
prove to be two-inhabited but (we can't prove to be pointed and)
whose pointedness would give a constructive taboo.
