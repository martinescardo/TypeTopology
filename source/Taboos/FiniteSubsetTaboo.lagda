---
title:        Kuratowski-finite Subset Taboo
author:       Ayberk Tosun
date-started: 2023-11-27
---

Based on a proof that I learned from MartÃ­n EscardÃ³ on 2023-11-21, though the
proof I ended up writing here ended up being a bit different.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt
open import UF.PropTrunc

module Taboos.FiniteSubsetTaboo (pt : propositional-truncations-exist)
                                (fe : Fun-Ext) where

open import Fin.Kuratowski pt
open import Fin.Type
open import MLTT.Negation
open import MLTT.Spartan
open import Naturals.Order
open import UF.DiscreteAndSeparated
open import UF.ExcludedMiddle
open import UF.ImageAndSurjection pt
open import UF.Logic
open import UF.Powerset
open import UF.Powerset-Fin pt
open import UF.Sets
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier
open import UF.SubtypeClassifier-Properties using (Î©-is-set)

open AllCombinators pt fe
open PropositionalSubsetInclusionNotation fe
open PropositionalTruncation pt

\end{code}

Let us first define a propositional version of `is-Kuratowski-finite-subset` for
the sake of convenience.

\begin{code}

is-Kuratowski-finite-subsetâ‚š : {X : ğ“¤  Ì‡} â†’ ğ“Ÿ X â†’ Î© ğ“¤
is-Kuratowski-finite-subsetâ‚š P =
 is-Kuratowski-finite-subset P , being-Kuratowski-finite-is-prop

\end{code}

We now define a predicate expressing the taboo we are interested in: given a
type `X`, `has-finite-subset-property X` expresses that for every
Kuratowski-finite subset `F âŠ† X`, any further subset of `S âŠ† F` is also
Kuratowski-finite.

\begin{code}

Kuratowski-finiteness-is-hereditary :
 (X : ğ“¤  Ì‡) â†’ Î© (ğ“¤ âº)
Kuratowski-finiteness-is-hereditary X =
 â±¯ F ê‰ ğ“Ÿ X , â±¯ S ê‰ ğ“Ÿ X ,
  S âŠ†â‚š F â‡’ is-Kuratowski-finite-subsetâ‚š F â‡’ is-Kuratowski-finite-subsetâ‚š S

\end{code}

The result that we prove in this module is the following

```
  finite-subset-property X â†’ is-discrete X
```

We now prove two easy lemmas before we proceed to the proof of the main result
of interest.

Lemma 1:

\begin{code}

having-empty-enumeration-entails-emptiness :
 (X : ğ“¤  Ì‡) â†’ (e : ğŸ˜ {ğ“¤â‚€} â†’ X) â†’ is-surjection e â†’ Â¬ X
having-empty-enumeration-entails-emptiness X e Ïƒ x =
 âˆ¥âˆ¥-rec ğŸ˜-is-prop (ğŸ˜-elim âˆ˜ prâ‚) (Ïƒ x)

\end{code}

\begin{code}

having-nonempty-enumeration-entails-inhabitedness :
 (X : ğ“¤  Ì‡) (n : â„•) â†’ 0 <â„• n â†’ (e : Fin n â†’ X) â†’ is-surjection e â†’ X
having-nonempty-enumeration-entails-inhabitedness X (succ n) p e Ïƒ = e ğŸ

\end{code}

Satisfying the finite subset property gives decidable equality.

\begin{code}

finite-subset-property-gives-discreteness
 : (X : ğ“¤  Ì‡)
 â†’ is-set X
 â†’ Kuratowski-finiteness-is-hereditary X holds
 â†’ is-discrete X
finite-subset-property-gives-discreteness {ğ“¤} X ğ•¤ Ï¡ x y =
 âˆ¥âˆ¥-rec (decidability-of-prop-is-prop fe ğ•¤) â€  (Ï¡ F S Î¹ Ï†)
  where
   _ï¼â‚š_ : X â†’ X â†’ Î© ğ“¤
   _ï¼â‚š_ xâ‚ xâ‚‚ = (xâ‚ ï¼ xâ‚‚) , ğ•¤

   F : ğ“Ÿ X
   F z = âˆ¥ (z ï¼ x) + (z ï¼ y) âˆ¥Î©

   e : Fin 2 â†’ ğ•‹ F
   e ğŸ = x , âˆ£ inl refl âˆ£
   e ğŸ = y , âˆ£ inr refl âˆ£

   Ïƒ : is-surjection e
   Ïƒ (z , p) = âˆ¥âˆ¥-rec âˆƒ-is-prop â€  p
    where
     â€  : (z ï¼ x) + (z ï¼ y) â†’ âˆƒ i ê‰ Fin 2 , e i ï¼ (z , p)
     â€  (inl refl) = âˆ£ ğŸ , to-subtype-ï¼ (holds-is-prop âˆ˜ F) refl âˆ£
     â€  (inr refl) = âˆ£ ğŸ , to-subtype-ï¼ (holds-is-prop âˆ˜ F) refl  âˆ£

   Ï† : is-Kuratowski-finite-subset F
   Ï† = âˆ£ 2 , e , Ïƒ âˆ£

   S : ğ“Ÿ X
   S z = F z âˆ§ ((x ï¼ y) , ğ•¤)

   Î¹ : S âŠ† F
   Î¹ z (p , q) = âˆ¥âˆ¥-rec (holds-is-prop (F z)) â€  p
    where
     â€  : (z ï¼ x) + (z ï¼ y) â†’ F z holds
     â€  (inl refl) = p
     â€  (inr refl) = p

   â€  : Î£ n ê‰ â„• , Fin n â†  ğ•‹ S â†’ (x ï¼ y) + Â¬ (x ï¼ y)
   â€  (zero   , eË¢) = let
                      Î½ : Â¬ ğ•‹ S
                      Î½ = uncurry (having-empty-enumeration-entails-emptiness (ğ•‹ S)) eË¢
                     in
                      inr (Î» p â†’ Î½ (x , (âˆ£ suc refl âˆ£ , p)))
   â€  (succ n , eË¢) = inl p
    where
     Ï„ : ğ•‹ S
     Ï„ = uncurry
          (having-nonempty-enumeration-entails-inhabitedness (ğ•‹ S) (succ n) â‹†)
          eË¢

     p : x ï¼ y
     p = prâ‚‚ (prâ‚‚ Ï„)

\end{code}

From this result, the following corollary follows:

    if every subset of a Kuratowski-finite subset of `Î©` is finite, then
    the law of excluded middle holds.

\begin{code}

finite-subset-property-for-Î©-gives-EM :
   {ğ“¤ : Universe}
 â†’ propext ğ“¤
 â†’ Kuratowski-finiteness-is-hereditary (Î© ğ“¤) holds
 â†’ EM ğ“¤
finite-subset-property-for-Î©-gives-EM {ğ“¤} pe Ï¡ = Î©-discrete-gives-EM fe pe â€ 
 where
  â€  : is-discrete (Î© ğ“¤)
  â€  = finite-subset-property-gives-discreteness (Î© ğ“¤) (Î©-is-set fe pe) Ï¡

\end{code}

Combining the two, we get:

\begin{code}

finite-subset-property-gives-EM :
   (ğ“¤ : Universe)
 â†’ (pe : propext ğ“¤)
 â†’ ((X : ğ“¤ âº  Ì‡) â†’ Kuratowski-finiteness-is-hereditary X holds)
 â†’ EM ğ“¤
finite-subset-property-gives-EM ğ“¤ pe Ï¡ =
 finite-subset-property-for-Î©-gives-EM pe (Ï¡ (Î© ğ“¤))

\end{code}

TODO: prove the converse direction of `is-discrete X` implies
`finite-subset-property X`.
