Martin Escardo, December 2017 (but done much earlier on paper)

As discussed in the module CompactTypes, Bishop's "limited principle
of omniscience" amount to the compactness of the type â„•, that is,

  Î  p ê‰ â„• â†’ ğŸš , (Î£ n ê‰ â„• , p n ï¼ â‚€) + (Î  n ê‰ â„• , p n ï¼ â‚),

which fails in contructive mathematics (here in the sense that it is
independent - it is not provable, and its negation is also not
provable).

This is in general not a univalent proposition, because there may be
many n:â„• with p n ï¼ â‚€. In univalent mathematics, we may get a
proposition by truncating the Î£ to get the existential quantifier âˆƒ
(see the Homotopy Type Theory book). Here instead we construct the
truncation directly, and call it LPO.

Using this and the module Prop-Tychonoff, we show that the function
type LPOâ†’â„• is compact, despite the fact that LPO is undecided in our
type theory.

(We needed to add new helper lemmas in the module
GenericConvergentSequence)

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module Taboos.LPO (fe : FunExt) where

open import CoNaturals.GenericConvergentSequence
open import MLTT.Spartan
open import MLTT.Two-Properties
open import Naturals.Order
open import Notation.CanonicalMap
open import Notation.Order
open import TypeTopology.CompactTypes
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

private
 feâ‚€ = fe ğ“¤â‚€ ğ“¤â‚€

LPO : ğ“¤â‚€ Ì‡
LPO = (x : â„•âˆ) â†’ is-decidable (Î£ n ê‰ â„• , x ï¼ Î¹ n)

LPO-is-prop : is-prop LPO
LPO-is-prop = Î -is-prop feâ‚€ f
 where
  a : (x : â„•âˆ) â†’ is-prop (Î£ n ê‰ â„• , x ï¼ Î¹ n)
  a x (n , p) (m , q) = to-Î£-ï¼ (â„•-to-â„•âˆ-lc (p â»Â¹ âˆ™ q) , â„•âˆ-is-set feâ‚€ _ _)

  f : (x : â„•âˆ) â†’ is-prop (is-decidable (Î£ n ê‰ â„• , x ï¼ Î¹ n))
  f x = decidability-of-prop-is-prop feâ‚€ (a x)

\end{code}

We now show that LPO is logically equivalent to its traditional
formulation by Bishop. However, the traditional formulation is not a
univalent proposition in general, and not type equivalent (in the
sense of UF) to our formulation.

\begin{code}

LPO-gives-compact-â„• : LPO â†’ is-compact â„•
LPO-gives-compact-â„• â„“ Î² = Î³
  where
    A = (Î£ n ê‰ â„• , Î² n ï¼ â‚€) + (Î  n ê‰ â„• , Î² n ï¼ â‚)

    Î± : â„• â†’ ğŸš
    Î± = force-decreasing Î²

    x : â„•âˆ
    x = (Î± , force-decreasing-is-decreasing Î²)

    d : is-decidable (Î£ n ê‰ â„• , x ï¼ Î¹ n)
    d = â„“ x

    a : (Î£ n ê‰ â„• , x ï¼ Î¹ n) â†’ A
    a (n , p) = inl (force-decreasing-is-not-much-smaller Î² n c)
      where
        c : Î± n ï¼ â‚€
        c = Î± n       ï¼âŸ¨ ap (Î» - â†’ Î¹ - n) p âŸ©
            Î¹ (Î¹ n) n ï¼âŸ¨ â„•-to-â„•âˆ-diagonalâ‚€ n âŸ©
            â‚€         âˆ

    b : (Â¬ (Î£ n ê‰ â„• , x ï¼ Î¹ n)) â†’ A
    b u = inr g
      where
        v : (n : â„•) â†’ x ï¼ Î¹ n â†’ ğŸ˜
        v = curry u

        g : (n : â„•) â†’ Î² n ï¼ â‚
        g n = â‰¤â‚‚-criterion-converse (force-decreasing-is-smaller Î² n) e
          where
            c : x ï¼ Î¹ n â†’ ğŸ˜
            c = v n

            l : x ï¼ âˆ
            l = not-finite-is-âˆ feâ‚€ v

            e : Î± n ï¼ â‚
            e = ap (Î» - â†’ Î¹ - n) l

    Î³ : A
    Î³ = cases a b d

compact-â„•-gives-LPO : is-compact â„• â†’ LPO
compact-â„•-gives-LPO Îº x = Î³
  where
    A = is-decidable (Î£ n ê‰ â„• , x ï¼ Î¹ n)

    Î² : â„• â†’ ğŸš
    Î² = Î¹ x

    d : (Î£ n ê‰ â„• , Î² n ï¼ â‚€) + (Î  n ê‰ â„• , Î² n ï¼ â‚)
    d = Îº Î²

    a : (Î£ n ê‰ â„• , Î² n ï¼ â‚€) â†’ A
    a (n , p) = inl (prâ‚ g , prâ‚‚(prâ‚‚ g))
      where
        g : Î£ m ê‰ â„• , (m â‰¤ n) Ã— (x ï¼ Î¹ m)
        g = â„•-to-â„•âˆ-lemma feâ‚€ x n p

    b : (Î  n ê‰ â„• , Î² n ï¼ â‚) â†’ A
    b Ï† = inr g
      where
        Ïˆ : Â¬ (Î£ n ê‰ â„• , Î² n ï¼ â‚€)
        Ïˆ = uncurry (Î» n â†’ equal-â‚-different-from-â‚€(Ï† n))

        f : (Î£ n ê‰ â„• , x ï¼ Î¹ n) â†’ Î£ n ê‰ â„• , Î² n ï¼ â‚€
        f (n , p) = (n , (ap (Î» - â†’ Î¹ - n) p âˆ™ â„•-to-â„•âˆ-diagonalâ‚€ n))
          where
           l : Î¹ x n ï¼ Î¹ (Î¹ n) n
           l = ap (Î» - â†’ Î¹ - n) p

        g : Â¬ (Î£ n ê‰ â„• , x ï¼ Î¹ n)
        g = contrapositive f Ïˆ

    Î³ : is-decidable (Î£ n ê‰ â„• , x ï¼ Î¹ n)
    Î³ = cases a b d

\end{code}

Now, if LPO is false, that is, an empty type, then the function type

  LPO â†’ â„•

is isomorphic to the unit type ğŸ™, and hence is compact. If LPO holds,
that is, LPO is equivalent to ğŸ™ because it is a univalent proposition,
then the function type LPO â†’ â„• is isomorphic to â„•, and hence the type
LPO â†’ â„• is again compact by LPO. So in any case we have that the type
LPO â†’ â„• is compact. However, LPO is an undecided proposition in our
type theory, so that the nature of the function type LPO â†’ â„• is
undecided. Nevertheless, we can show that it is compact, without
knowing whether LPO holds or not!

\begin{code}

open import TypeTopology.PropTychonoff

[LPOâ†’â„•]-is-compactâˆ™ : is-compactâˆ™ (LPO â†’ â„•)
[LPOâ†’â„•]-is-compactâˆ™ = prop-tychonoff-corollary' fe LPO-is-prop f
 where
   f : LPO â†’ is-compactâˆ™ â„•
   f lpo = compact-pointed-types-are-compactâˆ™ (LPO-gives-compact-â„• lpo) 0

[LPOâ†’â„•]-is-compact : is-compact (LPO â†’ â„•)
[LPOâ†’â„•]-is-compact = compactâˆ™-types-are-compact [LPOâ†’â„•]-is-compactâˆ™

[LPOâ†’â„•]-is-Compact : is-Compact (LPO â†’ â„•) {ğ“¤}
[LPOâ†’â„•]-is-Compact = compact-types-are-Compact [LPOâ†’â„•]-is-compact

\end{code}

However, we cannot prove that the function type LPOâ†’â„• is discrete, as
otherwise we would be able to decide the negation of LPO (added 14th
Feb 2020):

\begin{code}

open import Naturals.Properties
open import UF.DiscreteAndSeparated

[LPOâ†’â„•]-discrete-gives-Â¬LPO-decidable : is-discrete (LPO â†’ â„•) â†’ is-decidable (Â¬ LPO)
[LPOâ†’â„•]-discrete-gives-Â¬LPO-decidable =
  discrete-exponential-has-decidable-emptiness-of-exponent
   feâ‚€
   (1 , 0 , positive-not-zero 0)

\end{code}

Another condition equivalent to LPO is that the obvious
embedding Î¹ğŸ™ : â„• + ğŸ™ â†’ â„•âˆ has a section:

\begin{code}

Î¹ğŸ™-has-section-gives-LPO : (Î£ s ê‰ (â„•âˆ â†’ â„• + ğŸ™) , Î¹ğŸ™ âˆ˜ s âˆ¼ id) â†’ LPO
Î¹ğŸ™-has-section-gives-LPO (s , Îµ) u = Ïˆ (s u) refl
 where
  Ïˆ : (z : â„• + ğŸ™) â†’ s u ï¼ z â†’ is-decidable (Î£ n ê‰ â„• , u ï¼ Î¹ n)
  Ïˆ (inl n) p = inl (n , (u        ï¼âŸ¨ (Îµ u) â»Â¹ âŸ©
                          Î¹ğŸ™ (s u) ï¼âŸ¨ ap Î¹ğŸ™ p âŸ©
                          Î¹ n      âˆ))
  Ïˆ (inr *) p = inr Î³
   where
    Î³ : Â¬ (Î£ n ê‰ â„• , u ï¼ Î¹ n)
    Î³ (n , q) = âˆ-is-not-finite n (âˆ        ï¼âŸ¨ (ap Î¹ğŸ™ p)â»Â¹ âŸ©
                                   Î¹ğŸ™ (s u) ï¼âŸ¨ Îµ u âŸ©
                                   u        ï¼âŸ¨ q âŸ©
                                   Î¹ n      âˆ)

Î¹ğŸ™-is-equiv-gives-LPO : is-equiv Î¹ğŸ™ â†’ LPO
Î¹ğŸ™-is-equiv-gives-LPO i = Î¹ğŸ™-has-section-gives-LPO (equivs-have-sections Î¹ğŸ™ i)

Î¹ğŸ™-inverse : (u : â„•âˆ) â†’ is-decidable (Î£ n ê‰ â„• , u ï¼ Î¹ n) â†’ â„• + ğŸ™ {ğ“¤â‚€}
Î¹ğŸ™-inverse .(Î¹ n) (inl (n , refl)) = inl n
Î¹ğŸ™-inverse u (inr g) = inr â‹†

LPO-gives-has-section-Î¹ğŸ™ : LPO â†’ Î£ s ê‰ (â„•âˆ â†’ â„• + ğŸ™) , Î¹ğŸ™ âˆ˜ s âˆ¼ id
LPO-gives-has-section-Î¹ğŸ™ lpo = s , Îµ
 where
  s : â„•âˆ â†’ â„• + ğŸ™
  s u = Î¹ğŸ™-inverse u (lpo u)

  Ï† : (u : â„•âˆ) (d : is-decidable (Î£ n ê‰ â„• , u ï¼ Î¹ n)) â†’ Î¹ğŸ™ (Î¹ğŸ™-inverse u d) ï¼ u
  Ï† .(Î¹ n) (inl (n , refl)) = refl
  Ï† u (inr g) = (not-finite-is-âˆ feâ‚€ (curry g))â»Â¹

  Îµ : Î¹ğŸ™ âˆ˜ s âˆ¼ id
  Îµ u = Ï† u (lpo u)

LPO-gives-Î¹ğŸ™-is-equiv : LPO â†’ is-equiv Î¹ğŸ™
LPO-gives-Î¹ğŸ™-is-equiv lpo = embeddings-with-sections-are-equivs Î¹ğŸ™
                             (Î¹ğŸ™-is-embedding feâ‚€)
                             (LPO-gives-has-section-Î¹ğŸ™ lpo)
\end{code}
