Martin Escardo 2012.

The Weak Limited Principle of Omniscience (only somebody called Bishop
could have called it that), or WLPO for short, says that every
infinity binary sequence is either constantly 1 or it isn't.

This is equivalent to saying that every decreasing infinity binary
sequence os either constantly one or not.

The type â„•âˆ of decreasing binary sequences is defined in the module
GenericConvergentSequence. The constantly 1 sequence is called âˆ.

WLPO is independent of type theory: it holds in the model of classical
sets, and it fails in recursive models, because it amounts to a
solution of the Halting Problem. But we want to keep it undecided, for
the sake of being compatible with classical mathematics, following the
wishes of Bishop, and perhaps upsetting those of Brouwer who was happy
to accept continuity principles that falsify WLPO. In the words of
Aczel, WLPO is a taboo.  More generally, anything that implies a taboo
is a taboo, and any taboo is undecided. Taboos are boundary
propositions: they are classically true, recursively false, and
constructively, well, taboos!

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Taboos.WLPO where

open import MLTT.Spartan
open import CoNaturals.Type
open import UF.DiscreteAndSeparated
open import UF.FunExt
open import NotionsOfDecidability.Decidable

WLPO : ğ“¤â‚€ Ì‡
WLPO = (u : â„•âˆ) â†’ (u ï¼ âˆ) + (u â‰  âˆ)

\end{code}

If â„•âˆ is discrete, i.e. has decidable equality, then WLPO follows:

\begin{code}

â„•âˆ-discrete-gives-WLPO : is-discrete â„•âˆ â†’ WLPO
â„•âˆ-discrete-gives-WLPO d u = d u âˆ

\end{code}

Added 12 September 2018.

Conversely, assuming function extensionality, WLPO implies that â„•âˆ is
discrete. The proof uses a codistance (or closeness) function
c : â„•âˆ â†’ â„•âˆ â†’ â„•âˆ such that c u v ï¼ âˆ â†” u ï¼ v.

\begin{code}

WLPO-gives-â„•âˆ-discrete : FunExt â†’ WLPO â†’ is-discrete â„•âˆ
WLPO-gives-â„•âˆ-discrete fe wlpo u v =
 Cases (wlpo (â„•âˆ-closeness u v))
  (Î» (p : â„•âˆ-closeness u v ï¼ âˆ)
        â†’ inl (â„•âˆ-infinitely-close-are-equal u v p))
  (Î» (n : â„•âˆ-closeness u v â‰  âˆ)
        â†’ inr (contrapositive (Î» (q : u ï¼ v)
                                    â†’ â„•âˆ-equal-are-infinitely-close u v q) n))
 where
  open import TWA.Closeness fe

\end{code}

More discussion about WLPO is included in the modules
TheTopologyOfTheUniverse and FailureOfTotalSeparatedness, among
others.

Notice that weak excluded middle implies WLPO.

\begin{code}

open import UF.ClassicalLogic

WEM-gives-WLPO : funextâ‚€ â†’ typal-WEM ğ“¤â‚€ â†’ WLPO
WEM-gives-WLPO fe wem u = Cases (wem (u ï¼ âˆ))
                           (Î» (p : (u â‰  âˆ))
                                 â†’ inr p)
                           (Î» (Î½ : Â¬ (u â‰  âˆ))
                                 â†’ inl (â„•âˆ-is-Â¬Â¬-separated fe u âˆ Î½))
\end{code}

Added 15th November 2023.

\begin{code}

open import UF.Base

WLPO-traditional : ğ“¤â‚€ Ì‡
WLPO-traditional = (Î± : â„• â†’ ğŸš) â†’ is-decidable ((n : â„•) â†’ Î± n ï¼ â‚)

open import MLTT.Two-Properties

WLPO-gives-WLPO-traditional : funext ğ“¤â‚€ ğ“¤â‚€ â†’ WLPO â†’ WLPO-traditional
WLPO-gives-WLPO-traditional fe wlpo Î± = IV
 where
  I : (â„•â†’ğŸš-to-â„•âˆ Î± ï¼ âˆ) + (â„•â†’ğŸš-to-â„•âˆ Î± â‰  âˆ)
  I = wlpo (â„•â†’ğŸš-to-â„•âˆ Î±)

  II :  â„•â†’ğŸš-to-â„•âˆ Î± ï¼ âˆ â†’ (n : â„•) â†’ Î± n ï¼ â‚
  II p n = IIâ‚‚
   where
    IIâ‚€ : â„•âˆ-to-â„•â†’ğŸš (â„•â†’ğŸš-to-â„•âˆ Î±) ï¼ â„•âˆ-to-â„•â†’ğŸš âˆ
    IIâ‚€ = ap â„•âˆ-to-â„•â†’ğŸš p

    IIâ‚ : force-decreasing Î± n ï¼ â‚
    IIâ‚ = ap (Î» - â†’ - n) IIâ‚€

    IIâ‚‚ : Î± n ï¼ â‚
    IIâ‚‚ = â‰¤â‚‚-criterion-converse (force-decreasing-is-smaller Î± n) IIâ‚

  III : ((n : â„•) â†’ Î± n ï¼ â‚) â†’ â„•â†’ğŸš-to-â„•âˆ Î± ï¼ âˆ
  III Ï• = â„•âˆ-to-â„•â†’ğŸš-lc fe (dfunext fe IIIâ‚)
   where
    IIIâ‚€ : (n : â„•) â†’ force-decreasing Î± n ï¼ Î± n
    IIIâ‚€ = force-decreasing-unchanged Î±
            (Î» i â†’ transportâ‚‚ _â‰¤â‚‚_
                    ((Ï• (succ i))â»Â¹)
                    ((Ï• i)â»Â¹)
                    (â‰¤â‚‚-refl {â‚}))

    IIIâ‚ : â„•âˆ-to-â„•â†’ğŸš (â„•â†’ğŸš-to-â„•âˆ Î±) âˆ¼ â„•âˆ-to-â„•â†’ğŸš âˆ
    IIIâ‚ n = â„•âˆ-to-â„•â†’ğŸš (â„•â†’ğŸš-to-â„•âˆ Î±) n ï¼âŸ¨reflâŸ©
             force-decreasing Î± n      ï¼âŸ¨ IIIâ‚€ n âŸ©
             Î± n                       ï¼âŸ¨ Ï• n âŸ©
             â‚                         ï¼âŸ¨reflâŸ©
             â„•âˆ-to-â„•â†’ğŸš âˆ n             âˆ

  IV : is-decidable ((n : â„•) â†’ Î± n ï¼ â‚)
  IV = map-decidable II III I

WLPO-traditional-gives-WLPO : funextâ‚€ â†’ WLPO-traditional â†’ WLPO
WLPO-traditional-gives-WLPO fe wlpot u = IV
 where
  I : is-decidable ((n : â„•) â†’ â„•âˆ-to-â„•â†’ğŸš u n ï¼ â‚)
  I = wlpot (â„•âˆ-to-â„•â†’ğŸš u)

  II : ((n : â„•) â†’ â„•âˆ-to-â„•â†’ğŸš u n ï¼ â‚) â†’ u ï¼ âˆ
  II Ï• = â„•âˆ-to-â„•â†’ğŸš-lc fe (dfunext fe Ï•)

  III :  u ï¼ âˆ â†’ (n : â„•) â†’ â„•âˆ-to-â„•â†’ğŸš u n ï¼ â‚
  III e n = ap (Î» - â†’ â„•âˆ-to-â„•â†’ğŸš - n) e

  IV : (u ï¼ âˆ) + (u â‰  âˆ)
  IV = map-decidable II III I

\end{code}

Added 9th September 2024. WLPO amounts to saying that the constancy of
a binary sequence is decidable.

\begin{code}

WLPO-variationâ‚ : ğ“¤â‚€ Ì‡
WLPO-variationâ‚ = (Î± : â„• â†’ ğŸš) â†’ is-decidable ((n : â„•) â†’ Î± n ï¼ Î± 0)

WLPO-variationâ‚-gives-WLPO-traditional
 : WLPO-variationâ‚
 â†’ WLPO-traditional
WLPO-variationâ‚-gives-WLPO-traditional wlpov Î±
 = ğŸš-equality-cases I II
 where
  I : Î± 0 ï¼ â‚€ â†’ ((n : â„•) â†’ Î± n ï¼ â‚) + Â¬ ((n : â„•) â†’ Î± n ï¼ â‚)
  I p = inr (Î» (Ï• : (n : â„•) â†’ Î± n ï¼ â‚)
               â†’ zero-is-not-one
                  (â‚€   ï¼âŸ¨ p â»Â¹ âŸ©
                   Î± 0 ï¼âŸ¨ Ï• 0 âŸ©
                   â‚   âˆ))

  II : Î± 0 ï¼ â‚ â†’ ((n : â„•) â†’ Î± n ï¼ â‚) + Â¬ ((n : â„•) â†’ Î± n ï¼ â‚)
  II p = map-decidable
          (Î» (Ï• : (n : â„•) â†’ Î± n ï¼ Î± 0) (n : â„•)
             â†’ Î± n ï¼âŸ¨ Ï• n âŸ©
               Î± 0 ï¼âŸ¨ p âŸ©
               â‚   âˆ)
          (Î» (Î³ : (n : â„•) â†’ Î± n ï¼ â‚) (n : â„•)
             â†’ Î± n ï¼âŸ¨ Î³ n âŸ©
               â‚   ï¼âŸ¨ p â»Â¹ âŸ©
               Î± 0 âˆ)
          (wlpov Î±)

\end{code}

TODO. The converse.

Added 1 February 2025 by Tom de Jong.

\begin{code}

WLPO-variationâ‚‚ : ğ“¤â‚€ Ì‡
WLPO-variationâ‚‚ = (Î± : â„• â†’ ğŸš) â†’ is-decidable (Â¬ (Î£ n ê‰ â„• , Î± n ï¼ â‚€))

WLPO-traditional-gives-WLPO-variationâ‚‚ : WLPO-traditional â†’ WLPO-variationâ‚‚
WLPO-traditional-gives-WLPO-variationâ‚‚ wlpo Î± = Îº (wlpo Î±)
 where
  Îº : is-decidable (Î  n ê‰ â„• , Î± n ï¼ â‚) â†’ is-decidable (Â¬ (Î£ n ê‰ â„• , Î± n ï¼ â‚€))
  Îº (inl p) = inl (Î -not-implies-not-Î£ I)
   where
    I : (n : â„•) â†’ Î± n â‰  â‚€
    I n e = zero-is-not-one (e â»Â¹ âˆ™ p n)
  Îº (inr q) = inr (Â¬Â¬-functor I (not-Î -implies-not-not-Î£ II q))
   where
    I : (Î£ n ê‰ â„• , Î± n â‰  â‚) â†’ (Î£ n ê‰ â„• , Î± n ï¼ â‚€)
    I (n , Î½) = n , ğŸš-equality-cases id (Î» (e : Î± n ï¼ â‚) â†’ ğŸ˜-elim (Î½ e))
    II : (n : â„•) â†’ Â¬Â¬-stable (Î± n ï¼ â‚)
    II n = ğŸš-is-Â¬Â¬-separated (Î± n) â‚

WLPO-variationâ‚‚-gives-traditional-WLPO : WLPO-variationâ‚‚ â†’ WLPO-traditional
WLPO-variationâ‚‚-gives-traditional-WLPO wlpovar Î± = Îº (wlpovar Î±)
 where
  Îº : is-decidable (Â¬ (Î£ n ê‰ â„• , Î± n ï¼ â‚€)) â†’ is-decidable (Î  n ê‰ â„• , Î± n ï¼ â‚)
  Îº (inl p) = inl (Î» n â†’ I n (II n))
   where
    I : (n : â„•) â†’ Â¬ (Î± n ï¼ â‚€) â†’ (Î± n ï¼ â‚)
    I n Î½ = ğŸš-equality-cases (Î» (e : Î± n ï¼ â‚€) â†’ ğŸ˜-elim (Î½ e)) id
    II : (n : â„•) â†’ Â¬ (Î± n ï¼ â‚€)
    II = not-Î£-implies-Î -not p
  Îº (inr q) = inr (contrapositive I q)
   where
    I : (Î  n ê‰ â„• , Î± n ï¼ â‚) â†’ Â¬ (Î£ n ê‰ â„• , Î± n ï¼ â‚€)
    I h (n , e) = zero-is-not-one (e â»Â¹ âˆ™ h n)

\end{code}
