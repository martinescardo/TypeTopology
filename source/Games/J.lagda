Martin Escardo, Paulo Oliva, 2023

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness --auto-inline #-}

open import MLTT.Spartan hiding (J)

module Games.J where

open import UF.FunExt
open import Games.Base
open import Games.Monad

ğ• : Type â†’ Monad
ğ• R = record {
 functor = Î» X â†’ (X â†’ R) â†’ X ;
 Î·       = Î» x p â†’ x ;
 ext     = Î» f Îµ p â†’ f (Îµ (Î» x â†’ p (f x p))) p ;
 ext-Î·   = Î» Îµ â†’ refl ;
 unit    = Î» f x â†’ refl ;
 assoc   = Î» g f x â†’ refl
 }

ğ•-transf : Fun-Ext â†’ Monad â†’ Type â†’ Monad
ğ•-transf fe ğ“£ R = monad J Î·á´¶ extá´¶ extá´¶-Î· unitá´¶ assocá´¶
 where
 T = functor ğ“£

 J : Type â†’ Type
 J X = (X â†’ T R) â†’ T X

 Î·á´¶ : {X : Type} â†’ X â†’ J X
 Î·á´¶ = Î» x p â†’ Î· ğ“£ x

 extá´¶ : {X Y : Type} â†’ (X â†’ J Y) â†’ J X â†’ J Y
 extá´¶ f Îµ p = ext ğ“£ (Î» x â†’ f x p) (Îµ (Î» x â†’ ext ğ“£ p (f x p)))

 extá´¶-Î· : {X : Set} â†’ extá´¶ (Î·á´¶ {X}) âˆ¼ ğ‘–ğ‘‘ (J X)
 extá´¶-Î· Îµ = dfunext fe Î» p â†’
  ext ğ“£ (Î· ğ“£) (Îµ (Î» x â†’ ext ğ“£ p (Î· ğ“£ x))) ï¼âŸ¨ ext-Î· ğ“£ _ âŸ©
  Îµ (Î» x â†’ ext ğ“£ p (Î· ğ“£ x))               ï¼âŸ¨ ap Îµ (dfunext fe (unit ğ“£ _)) âŸ©
  Îµ p                                     âˆ

 unitá´¶ : {X Y : Type} (f : X â†’ J Y) (x : X) â†’ extá´¶ f (Î·á´¶ x) ï¼ f x
 unitá´¶ f x = dfunext fe (Î» p â†’ unit ğ“£ (Î» x â†’ f x p) x)

 assocá´¶ : {X Y Z : Type} (g : Y â†’ J Z) (f : X â†’ J Y) (Îµ : J X)
        â†’ extá´¶ (Î» x â†’ extá´¶ g (f x)) Îµ ï¼ extá´¶ g (extá´¶ f Îµ)
 assocá´¶ g f Îµ = dfunext fe Î³
  where
   Î³ : âˆ€ p â†’ extá´¶ (Î» x â†’ extá´¶ g (f x)) Îµ p ï¼ extá´¶ g (extá´¶ f Îµ) p
   Î³ p =
    extá´¶ (Î» x â†’ extá´¶ g (f x)) Îµ p                  ï¼âŸ¨ refl âŸ©
    ğ•– (Î» x â†’ ğ•– ğ•˜ (ğ•— x)) (Îµ (Î» x â†’ ğ•– p (ğ•– ğ•˜ (ğ•— x)))) ï¼âŸ¨ assoc ğ“£ _ _ _ âŸ©
    ğ•– ğ•˜ (ğ•– ğ•— (Îµ (Î» x â†’ ğ•– p (ğ•– ğ•˜ (ğ•— x)))))           ï¼âŸ¨ again-by-assoc âŸ©
    ğ•– ğ•˜ (ğ•– ğ•— (Îµ (Î» x â†’ ğ•– (Î» y â†’ ğ•– p (ğ•˜ y)) (ğ•— x)))) ï¼âŸ¨ refl âŸ©
    extá´¶ g (extá´¶ f Îµ) p âˆ
     where
      ğ•– = ext ğ“£
      ğ•˜ = Î» y â†’ g y p
      ğ•— = Î» x â†’ f x (Î» y â†’ ğ•– p (ğ•˜ y))
      again-by-assoc = ap (Î» - â†’ ğ•– ğ•˜ (ğ•– ğ•— (Îµ -)))
                          (dfunext fe (Î» x â†’ (assoc ğ“£ _ _ _)â»Â¹))

ğ•' : Fun-Ext â†’ Type â†’ Monad
ğ•' fe = ğ•-transf fe ğ•€ğ••

module J-definitions (R : Type) where

 J : Type â†’ Type
 J = functor (ğ• R)

 _âŠ—á´¶_ : {X : Type} {Y : X â†’ Type}
      â†’ J X
      â†’ ((x : X) â†’ J (Y x))
      â†’ J (Î£ x ê‰ X , Y x)
 _âŠ—á´¶_ = _âŠ—_ (ğ• R)

 âŠ—á´¶-direct-definition : {X : Type} {Y : X â†’ Type}
                        (Îµ : J X)
                        (Î´ : (x : X) â†’ J (Y x))
                      â†’ Îµ âŠ—á´¶ Î´ âˆ¼ (Î» q â†’ let
                                         Î½  = Î» x â†’ Î´ x (sub q x)
                                         xâ‚€ = Îµ (Î» x â†’ sub q x (Î½ x))
                                        in (xâ‚€ , Î½ xâ‚€))
 âŠ—á´¶-direct-definition Îµ Î´ q = refl

 Î·á´¶ : {X : Type} â†’ X â†’ J X
 Î·á´¶ = Î· (ğ• R)

 J-ext : {X Y : Type} â†’ (X â†’ J Y) â†’ J X â†’ J Y
 J-ext = ext (ğ• R)

 J-map : {X Y : Type} â†’ (X â†’ Y) â†’ J X â†’ J Y
 J-map = map (ğ• R)

\end{code}
