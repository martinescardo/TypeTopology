Martin Escardo, 11th September 2018
Completed by Todd Waugh Ambridge, 15th May 2020

We begin by defining a "closeness function"

  c : X â†’ X â†’ â„•âˆ

such that

  c x y ï¼ âˆ â†” x ï¼ y

for some examples of types X, including Baire, Cantor and â„•âˆ.

That is, two points are equal iff they are infinitely close.  If we
have c x y ï¼ under n for n : â„•, the intuition is that x and y can be
distinguished by a finite amount of information of size n.

(An application is to show that WLPO holds iff â„•âˆ is discrete.)

We then discuss further codistance axioms.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt

module TWA.Closeness (fe : FunExt) where

open import CoNaturals.Arithmetic fe
open import CoNaturals.GenericConvergentSequence renaming (min to min')
open import CoNaturals.UniversalProperty fe
open import MLTT.Two-Properties
open import Naturals.Sequence fe
open import Notation.CanonicalMap
open import Notation.Order
open import UF.DiscreteAndSeparated

module sequences
        {ğ“¤ : Universe}
        (D : ğ“¤ Ì‡ )
        (Î´ : is-discrete D)
       where

\end{code}

We denote the type of sequences over D by $, and define a closeness
function $ â†’ $ â†’ â„•âˆ using the fact that â„•âˆ is the final coalgebra of
the functor ğŸ™ + (-), which we refer to as corecursion.

\begin{code}

 private
  ğ“¢ : ğ“¤ Ì‡
  ğ“¢ = â„• â†’ D
  X : ğ“¤ Ì‡
  X = ğ“¢ Ã— ğ“¢
  f : (Î± Î² : ğ“¢) â†’ head Î± ï¼ head Î² â†’ ğŸ™ {ğ“¤â‚€} + X
  f Î± Î² q = inr (tail Î± , tail Î²)
  g : (Î± Î² : ğ“¢) â†’ head Î± â‰  head Î² â†’ ğŸ™ {ğ“¤â‚€} + X
  g Î± Î² n = inl â‹†
  p : X â†’ ğŸ™ {ğ“¤â‚€} + X
  p (Î± , Î²) = cases (f Î± Î²) (g Î± Î²) (Î´ (head Î±) (head Î²))
  c : ğ“¢ â†’ ğ“¢ â†’ â„•âˆ
  c = curry (â„•âˆ-corec p)

\end{code}

We use the private name "c" in this submodule, which is exported as
"closeness":

\begin{code}

 closeness : ğ“¢ â†’ ğ“¢ â†’ â„•âˆ
 closeness = c

\end{code}

The two defining properties of the function c are the following:

\begin{code}

 closeness-eqâ‚€ : (Î± Î² : ğ“¢) â†’ head Î± â‰  head Î²
               â†’ c Î± Î² ï¼ Zero
 closeness-eqâ‚ : (Î± Î² : ğ“¢) â†’ head Î± ï¼ head Î²
               â†’ c Î± Î² ï¼ Succ (c (tail Î±) (tail Î²))

 closeness-eqâ‚€ Î± Î² n = Î³ r
  where
   t : Î´ (head Î±) (head Î²) ï¼ inr n
   t = discrete-inr (fe ğ“¤ ğ“¤â‚€) Î´ (head Î±) (head Î²) n
   r : p (Î± , Î²) ï¼ inl â‹†
   r = ap (cases (f Î± Î²) (g Î± Î²)) t
   Î³ : p (Î± , Î²) ï¼ inl â‹† â†’ c Î± Î² ï¼ Zero
   Î³ = Coalg-morphism-Zero p (Î± , Î²) â‹†

 closeness-eqâ‚ Î± Î² q = Î³ r
  where
   t : Î´ (head Î±) (head Î²) ï¼ inl q
   t = discrete-inl Î´ (head Î±) (head Î²) q
   r : p (Î± , Î²) ï¼ inr (tail Î± , tail Î²)
   r = ap (cases (f Î± Î²) (g Î± Î²)) t
   Î³ : p (Î± , Î²) ï¼ inr (tail Î± , tail Î²)
     â†’ c Î± Î² ï¼ Succ (c (tail Î±) (tail Î²))
   Î³ = Coalg-morphism-Succ p (Î± , Î²) (tail Î± , tail Î²)

\end{code}

That any sequence is infinitely close to itself is proved by
coinduction on â„•âˆ using closeness-eqâ‚:

\begin{code}

 infinitely-close-to-itself : (Î± : ğ“¢) â†’ c Î± Î± ï¼ âˆ
 infinitely-close-to-itself Î± = â„•âˆ-coinduction R b (c Î± Î±) âˆ Î³
  where
   l : âˆ€ Î± â†’ c Î± Î± ï¼ Succ (c (tail Î±) (tail Î±))
   l Î± = closeness-eqâ‚ Î± Î± refl
   R : â„•âˆ â†’ â„•âˆ â†’ ğ“¤ Ì‡
   R u v = (Î£ Î± ê‰ ğ“¢ , u ï¼ c Î± Î±) Ã— (v ï¼ âˆ)
   b : â„•âˆ-bisimulation R
   b .(c Î± Î±) .âˆ ((Î± , refl) , refl) = s , t , Pred-âˆ-is-âˆ
    where
     s : positivity (c Î± Î±) ï¼ positivity âˆ
     s = ap positivity (l Î±)
     t : Î£ Î±' ê‰ ğ“¢ , Pred (c Î± Î±) ï¼ c Î±' Î±'
     t = tail Î± , (ap Pred (l Î±) âˆ™ Pred-Succ)
   Î³ : R (c Î± Î±) âˆ
   Î³ = (Î± , refl) , refl

\end{code}

That any two infinitely close sequences are equal is proved by
coinduction on sequences, using both closeness-eqâ‚€ (to rule out an
impossible case) and closeness-eqâ‚ (to establish the result):

\begin{code}

 infinitely-close-are-equal : (Î± Î² : ğ“¢) â†’ c Î± Î² ï¼ âˆ â†’ Î± ï¼ Î²
 infinitely-close-are-equal = seq-coinduction (Î» Î± Î² â†’ c Î± Î² ï¼ âˆ) b
  where
   b : (Î± Î² : ğ“¢) â†’ c Î± Î² ï¼ âˆ
                 â†’ (head Î± ï¼ head Î²) Ã— (c (tail Î±) (tail Î²) ï¼ âˆ)
   b Î± Î² q = d , e
    where
     l : head Î± â‰  head Î² â†’ c Î± Î² ï¼ Zero
     l = closeness-eqâ‚€ Î± Î²
     d : head Î± ï¼ head Î²
     d = Cases (Î´ (head Î±) (head Î²))
          (Î» (p : head Î± ï¼ head Î²)
            â†’ p)
          (Î» (n : head Î± â‰  head Î²)
            â†’ ğŸ˜-elim (Zero-not-Succ (Zero    ï¼âŸ¨ (l n)â»Â¹ âŸ©
                                     c Î± Î²   ï¼âŸ¨ q âŸ©
                                     âˆ       ï¼âŸ¨ (Succ-âˆ-is-âˆ (fe ğ“¤â‚€ ğ“¤â‚€))â»Â¹ âŸ©
                                     Succ âˆ  âˆ)))
     e : c (tail Î±) (tail Î²) ï¼ âˆ
     e = ap Pred (Succ (c (tail Î±) (tail Î²)) ï¼âŸ¨ (closeness-eqâ‚ Î± Î² d)â»Â¹ âŸ©
                  c Î± Î²                      ï¼âŸ¨ q âŸ©
                  âˆ                          âˆ)

\end{code}

Symmetric property:

\begin{code}

 symmetric-property : (Î± Î² : ğ“¢) â†’ c Î± Î² ï¼ c Î² Î±
 symmetric-property Î± Î² = â„•âˆ-coinduction R b (c Î± Î²) (c Î² Î±) Î³
  where
   R : â„•âˆ â†’ â„•âˆ â†’ ğ“¤ Ì‡
   R u v = Î£ \Î± â†’ Î£ \Î² â†’ (u ï¼ c Î± Î²) Ã— (v ï¼ c Î² Î±)
   b : â„•âˆ-bisimulation R
   b .(c Î± Î²) .(c Î² Î±) (Î± , Î² , refl , refl)
     = s , t (Î´ (head Î±) (head Î²))
    where
     s : positivity (c Î± Î²) ï¼ positivity (c Î² Î±)
     s = Cases (Î´ (head Î±) (head Î²)) sâ‚• sâ‚œ
      where
       sâ‚• : head Î± ï¼ head Î² â†’ positivity (c Î± Î²) ï¼ positivity (c Î² Î±)
       sâ‚• p = successors-same-positivity
                (closeness-eqâ‚ Î± Î² p)
                (closeness-eqâ‚ Î² Î± (p â»Â¹))
       sâ‚œ : head Î± â‰  head Î² â†’ positivity (c Î± Î²) ï¼ positivity (c Î² Î±)
       sâ‚œ d = ap positivity
               (closeness-eqâ‚€ Î± Î² d
               âˆ™ closeness-eqâ‚€ Î² Î± (Î» p â†’ d (p â»Â¹)) â»Â¹)
     t : (head Î± ï¼ head Î²) + (head Î± â‰  head Î²)
       â†’ R (Pred (c Î± Î²)) (Pred (c Î² Î±))
     t (inl p) = tail Î± , tail Î²
               , ap Pred (closeness-eqâ‚ Î± Î² p âˆ™ Pred-Succ)
               , ap Pred (closeness-eqâ‚ Î² Î± (p â»Â¹) âˆ™ Pred-Succ)
     t (inr d) = Î± , Î²
               , Pred-Zero-is-Zero' (c Î± Î²) (closeness-eqâ‚€ Î± Î² d)
               , Pred-Zero-is-Zero' (c Î² Î±) (closeness-eqâ‚€ Î² Î± (Î» p â†’ d (p â»Â¹)))
   Î³ : R (c Î± Î²) (c Î² Î±)
   Î³ = Î± , Î² , refl , refl

\end{code}

Ultra property:

\begin{code}

 closeness-eqâ‚' : (Î± Î² : ğ“¢) â†’ is-positive (c Î± Î²)
                â†’ head Î± ï¼ head Î²
 closeness-eqâ‚' Î± Î² p = Cases (Î´ (head Î±) (head Î²)) id
   (Î» hâ‰  â†’ ğŸ˜-elim (zero-is-not-one
    (is-Zero-Zero â»Â¹ âˆ™ ap (Î» - â†’ Î¹ - 0) (closeness-eqâ‚€ Î± Î² hâ‰  â»Â¹) âˆ™ p)))

 open import Naturals.Order

 closeness-conceptuallyâ‚ : (Î± Î² : ğ“¢) (n : â„•)
                         â†’ ((k : â„•) â†’ k â‰¤ n â†’ Î± k ï¼ Î² k)
                         â†’ n âŠ c Î± Î²
 closeness-conceptuallyâ‚ Î± Î² zero Î±â‰ˆâ‚™Î²
  = transport (0 âŠ_) (closeness-eqâ‚ Î± Î² (Î±â‰ˆâ‚™Î² 0 â‹†) â»Â¹)
    (is-positive-Succ (c (tail Î±) (tail Î²)))
 closeness-conceptuallyâ‚ Î± Î² (succ n) Î±â‰ˆâ‚™Î²
  = transport (succ n âŠ_) (closeness-eqâ‚ Î± Î² (Î±â‰ˆâ‚™Î² 0 â‹†) â»Â¹)
    (closeness-conceptuallyâ‚ (tail Î±) (tail Î²) n (Î» m â†’ Î±â‰ˆâ‚™Î² (succ m)))

 closeness-conceptuallyâ‚‚ : (Î± Î² : ğ“¢) (n : â„•)
                         â†’ n âŠ c Î± Î²
                         â†’ ((k : â„•) â†’ k â‰¤ n â†’ Î± k ï¼ Î² k)
 closeness-conceptuallyâ‚‚ Î± Î² n âŠâ‚™cÎ±Î² zero kâ‰¤n
  = closeness-eqâ‚' Î± Î² (âŠ-trans'' (c Î± Î²) n 0 kâ‰¤n âŠâ‚™cÎ±Î²)
 closeness-conceptuallyâ‚‚ Î± Î² n âŠâ‚™cÎ±Î² (succ k) kâ‰¤n
  = closeness-conceptuallyâ‚‚ (tail Î±) (tail Î²) k (transport (succ k âŠ_)
      (closeness-eqâ‚ Î± Î² (closeness-eqâ‚' Î± Î² (âŠ-trans'' (c Î± Î²) n 0 â‹† âŠâ‚™cÎ±Î²)))
      (âŠ-trans'' (c Î± Î²) n (succ k) kâ‰¤n âŠâ‚™cÎ±Î²))
    k (â‰¤-refl k)

 min-split : (Î± Î² : â„•âˆ) (n : â„•) â†’ n âŠ uncurry min' (Î± , Î²) â†’ n âŠ Î± Ã— n âŠ Î²
 prâ‚ (min-split Î± Î² n minâ‰¼) = different-from-â‚€-equal-â‚
                              (Î» x â†’ zero-is-not-one (Lemma[minğŸšabï¼â‚€] (inl x) â»Â¹ âˆ™ minâ‰¼))
 prâ‚‚ (min-split Î± Î² n minâ‰¼) = different-from-â‚€-equal-â‚
                              (Î» x â†’ zero-is-not-one (Lemma[minğŸšabï¼â‚€] (inr x) â»Â¹ âˆ™ minâ‰¼))

 ultra-property : (Î± Î² Îµ : ğ“¢) â†’ min (c Î± Î² , c Î² Îµ) â‰¼ c Î± Îµ
 ultra-property Î± Î² Îµ n minâ‰¼  = closeness-conceptuallyâ‚ Î± Îµ n
                     (Î» k kâ‰¤n â†’ closeness-conceptuallyâ‚‚ Î± Î² n (prâ‚ min-split') k kâ‰¤n
                              âˆ™ closeness-conceptuallyâ‚‚ Î² Îµ n (prâ‚‚ min-split') k kâ‰¤n)
  where
   min-split' : n âŠ c Î± Î² Ã— n âŠ c Î² Îµ
   min-split' = min-split (c Î± Î²) (c Î² Îµ) n
                (transport (Î» - â†’ n âŠ - (c Î± Î² , c Î² Îµ)) minï¼ minâ‰¼)

\end{code}

We now consider the following two special cases for the Baire and
Cantor types:

\begin{code}

open sequences â„• â„•-is-discrete
 renaming
  (closeness                  to Baire-closeness ;
   infinitely-close-to-itself to Baire-infinitely-close-to-itself ;
   infinitely-close-are-equal to Baire-infinitely-close-are-equal ;
   symmetric-property         to Baire-symmetric-property ;
   ultra-property             to Baire-ultra-property )

open sequences ğŸš ğŸš-is-discrete
 renaming
  (closeness                  to Cantor-closeness ;
   infinitely-close-to-itself to Cantor-infinitely-close-to-itself ;
   infinitely-close-are-equal to Cantor-infinitely-close-are-equal ;
   symmetric-property         to Cantor-symmetric-property ;
   ultra-property             to Cantor-ultra-property )

\end{code}

And now we reduce the closeness of the Cantor type to the generic
convergent sequence:

\begin{code}

â„•âˆ-closeness : â„•âˆ â†’ â„•âˆ â†’ â„•âˆ
â„•âˆ-closeness u v = Cantor-closeness (Î¹ u) (Î¹ v)

â„•âˆ-infinitely-close-to-itself : (u : â„•âˆ) â†’ â„•âˆ-closeness u u ï¼ âˆ
â„•âˆ-infinitely-close-to-itself u = Cantor-infinitely-close-to-itself (Î¹ u)

â„•âˆ-equal-are-infinitely-close : (u v : â„•âˆ) â†’ u ï¼ v â†’ â„•âˆ-closeness u v ï¼ âˆ
â„•âˆ-equal-are-infinitely-close u .u refl = â„•âˆ-infinitely-close-to-itself u

â„•âˆ-infinitely-close-are-equal : (u v : â„•âˆ) â†’ â„•âˆ-closeness u v ï¼ âˆ â†’ u ï¼ v
â„•âˆ-infinitely-close-are-equal u v r = â„•âˆ-to-â„•â†’ğŸš-lc (fe ğ“¤â‚€ ğ“¤â‚€) Î³
 where
  Î³ : Î¹ u ï¼ Î¹ v
  Î³ = Cantor-infinitely-close-are-equal (Î¹ u) (Î¹ v) r

â„•âˆ-symmetric-property : (u v : â„•âˆ) â†’ â„•âˆ-closeness u v ï¼ â„•âˆ-closeness v u
â„•âˆ-symmetric-property u v = Cantor-symmetric-property (Î¹ u) (Î¹ v)

â„•âˆ-ultra-property : (u v w : â„•âˆ)
                  â†’ min (â„•âˆ-closeness u v , â„•âˆ-closeness v w) â‰¼ â„•âˆ-closeness u w
â„•âˆ-ultra-property u v w = Cantor-ultra-property (Î¹ u) (Î¹ v) (Î¹ w)

\end{code}

Axioms for closeness:

\begin{code}

is-closeness
 indistinguishable-are-equal
 self-indistinguishable
 is-symmetric
 is-ultra
  : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ â„•âˆ) â†’ ğ“¤ Ì‡

indistinguishable-are-equal c = âˆ€ x y â†’ c x y ï¼ âˆ â†’ x ï¼ y
self-indistinguishable      c = âˆ€ x â†’ c x x ï¼ âˆ
is-symmetric                c = âˆ€ x y â†’ c x y ï¼ c y x
is-ultra                    c = âˆ€ x y z â†’ min (c x y , c y z) â‰¼ c x z
is-closeness                c = indistinguishable-are-equal c
                              Ã— self-indistinguishable c
                              Ã— is-symmetric c
                              Ã— is-ultra c

\end{code}

The above closenesss are indeed closenesss according
to this definition

\begin{code}

open sequences

â„•â†’D-has-closeness : (X : ğ“¤ Ì‡ ) (Î´ : is-discrete X)
                  â†’ is-closeness (closeness X Î´)
â„•â†’D-has-closeness X Î´
 = infinitely-close-are-equal X Î´
 , infinitely-close-to-itself X Î´
 , symmetric-property X Î´
 , ultra-property X Î´

â„•â†’â„•-has-closeness : is-closeness (Baire-closeness)
â„•â†’â„•-has-closeness = â„•â†’D-has-closeness â„• â„•-is-discrete

â„•â†’ğŸš-has-closeness : is-closeness (Cantor-closeness)
â„•â†’ğŸš-has-closeness = â„•â†’D-has-closeness ğŸš ğŸš-is-discrete

â„•â†’â„•âˆ-has-closeness : is-closeness (â„•âˆ-closeness)
â„•â†’â„•âˆ-has-closeness = â„•âˆ-infinitely-close-are-equal
                   , â„•âˆ-infinitely-close-to-itself
                   , â„•âˆ-symmetric-property
                   , â„•âˆ-ultra-property
