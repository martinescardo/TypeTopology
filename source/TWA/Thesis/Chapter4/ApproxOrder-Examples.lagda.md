[â‡ Index](../html/TWA.Thesis.index.html)

# Examples of approximate orders

```agda

{-# OPTIONS --without-K --exact-split --safe #-}

open import MLTT.Spartan
open import UF.FunExt
open import TypeTopology.DiscreteAndSeparated
open import Notation.Order
open import Naturals.Order
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Quotient
open import UF.Embeddings
open import CoNaturals.GenericConvergentSequence
  renaming (â„•-to-â„•âˆ to _â†‘
         ; Zero-smallest to zero-minimal
         ; âˆ-largest to âˆ-maximal)
open import NotionsOfDecidability.Decidable
open import MLTT.Two-Properties
open import UF.Miscelanea
open import Fin.Type
open import Fin.Bishop
open import UF.PropTrunc

open import TWA.Thesis.Chapter2.Finite
open import TWA.Thesis.Chapter2.Sequences

module TWA.Thesis.Chapter4.ApproxOrder-Examples (fe : FunExt) where

open import TWA.Thesis.Chapter3.ClosenessSpaces fe
open import TWA.Thesis.Chapter3.ClosenessSpaces-Examples fe
open import TWA.Thesis.Chapter3.SearchableTypes fe
open import TWA.Thesis.Chapter4.ApproxOrder fe
```

## Subtype orders

```
inclusion-order
 : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (f : X â†’ Y) (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡) â†’ X â†’ X â†’ ğ“¦ Ì‡
inclusion-order f _â‰¤_ xâ‚ xâ‚‚ = f xâ‚ â‰¤ f xâ‚‚

inclusion-order-is-preorder
 : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
 â†’ (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡)
 â†’ is-preorder _â‰¤_
 â†’ is-preorder (inclusion-order f _â‰¤_)
inclusion-order-is-preorder {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y}
 f _â‰¤_ (r' , t' , p') = r , t , p
 where
  r : reflexive (inclusion-order f _â‰¤_)
  r x     = r' (f x)
  t : transitive (inclusion-order f _â‰¤_)
  t x y z = t' (f x) (f y) (f z)
  p : is-prop-valued (inclusion-order f _â‰¤_)
  p x y   = p' (f x) (f y)

inclusion-order-is-linear-preorder
 : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
 â†’ (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡)
 â†’ is-linear-preorder _â‰¤_
 â†’ is-linear-preorder (inclusion-order f _â‰¤_)
inclusion-order-is-linear-preorder {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y}
 f _â‰¤_ (pre , l') = inclusion-order-is-preorder f _â‰¤_ pre , l
 where
  l : (x y : X) â†’ inclusion-order f _â‰¤_ x y + inclusion-order f _â‰¤_ y x
  l x y   = l' (f x) (f y)

inclusion-order-is-strict-order
 : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
 â†’ (_<_ : Y â†’ Y â†’ ğ“¦ Ì‡)
 â†’ is-strict-order _<_
 â†’ is-strict-order (inclusion-order f _<_)
inclusion-order-is-strict-order {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y}
 f _<_ (i' , t' , a' , p') = i , t , a , p
 where
  i : (x : X) â†’ Â¬ inclusion-order f _<_ x x
  i x     = i' (f x) 
  t : transitive (inclusion-order f _<_)
  t x y z = t' (f x) (f y) (f z)
  a : (x y : X)
    â†’ inclusion-order f _<_ x y
    â†’ Â¬ inclusion-order f _<_ y x
  a x y   = a' (f x) (f y)
  p : is-prop-valued (inclusion-order f _<_)
  p x y   = p' (f x) (f y)

embedding-strict-order-trichotomous
 : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } ((f , _) : X â†ª Y)
 â†’ (_<_ : Y â†’ Y â†’ ğ“¦ Ì‡) â†’ trichotomous _<_
 â†’ trichotomous (inclusion-order f _<_)
embedding-strict-order-trichotomous
 {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y} (f , Î·) _<_ t x y
 = Cases (t (f x) (f y))
     inl (cases (inr âˆ˜ inl âˆ˜ Î» e â†’ ap prâ‚ (f-lc x y e)) (inr âˆ˜ inr))
 where
  f-lc : (x y : X) (e : f x ï¼ f y) â†’ x , e ï¼ y , refl
  f-lc x y e = Î· (f y) (x , e) (y , refl)

inclusion-approx-order
 : {X : ğ“¤ Ì‡ } {Y : ClosenessSpace ğ“¥} (f : X â†’ âŸ¨ Y âŸ©)
 â†’ (_â‰¤â¿_ : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ â„• â†’ ğ“¦  Ì‡)
 â†’ X â†’ X â†’ â„• â†’ ğ“¦  Ì‡ 
inclusion-approx-order f _â‰¤â¿_ x y = f x â‰¤â¿ f y

Î£-order : {X : ğ“¤ Ì‡ } (P : X â†’ ğ“¥ Ì‡ ) (_â‰¤_ : X â†’ X â†’ ğ“¦  Ì‡)
        â†’ Î£ P â†’ Î£ P â†’ ğ“¦  Ì‡
Î£-order P _â‰¤_ (x , _) (y , _) = x â‰¤ y

Î£-order-is-preorder
 : {X : ğ“¤ Ì‡ } (P : X â†’ ğ“¥ Ì‡ ) (_â‰¤_ : X â†’ X â†’ ğ“¦ Ì‡ )
 â†’ is-preorder _â‰¤_
 â†’ is-preorder (Î£-order P _â‰¤_)
Î£-order-is-preorder P _â‰¤_ (r' , t' , p') = r , t , p
 where
  r : reflexive (Î£-order P _â‰¤_)
  r (x , _) = r' x
  t : transitive (Î£-order P _â‰¤_)
  t (x , _) (y , _) (z , _) = t' x y z
  p : is-prop-valued (Î£-order P _â‰¤_)
  p (x , _) (y , _) = p' x y

Î£-approx-order : {X : ğ“¤ Ì‡ } â†’ (P : X â†’ ğ“¥ Ì‡ ) â†’ (_â‰¤â¿_ : X â†’ X â†’ â„• â†’ ğ“¦  Ì‡)
               â†’ Î£ P â†’ Î£ P â†’ â„• â†’ ğ“¦  Ì‡ 
Î£-approx-order P _â‰¤â¿_ (x , _) (y , _) = x â‰¤â¿ y

Î£-approx-order-is-approx-order
 : (X : ClosenessSpace ğ“¤)
 â†’ (P : âŸ¨ X âŸ© â†’ ğ“¥ Ì‡ )
 â†’ (p : (x : âŸ¨ X âŸ©) â†’ is-prop (P x))
 â†’ (_â‰¤â¿_ : âŸ¨ X âŸ© â†’ âŸ¨ X âŸ© â†’ â„• â†’ ğ“¦'  Ì‡)
 â†’ is-approx-order X _â‰¤â¿_
 â†’ is-approx-order (Î£-ClosenessSpace X P p) (Î£-approx-order P _â‰¤â¿_)
Î£-approx-order-is-approx-order
 X P p' _â‰¤â¿_ a = (Î» Ïµ â†’ (r Ïµ , t Ïµ , p Ïµ) , l Ïµ) , d , c
 where
  r : (Ïµ : â„•) â†’ reflexive (Î» x y â†’ Î£-approx-order P _â‰¤â¿_ x y Ïµ)
  r Ïµ x     = â‰¤â¿-refl      X a Ïµ (prâ‚ x)
  t : (Ïµ : â„•) â†’ transitive (Î» x y â†’ Î£-approx-order P _â‰¤â¿_ x y Ïµ)
  t Ïµ x y z = â‰¤â¿-trans     X a Ïµ (prâ‚ x) (prâ‚ y) (prâ‚ z)
  p : (Ïµ : â„•) â†’ is-prop-valued (Î» x y â†’ Î£-approx-order P _â‰¤â¿_ x y Ïµ)
  p Ïµ x y   = â‰¤â¿-prop      X a Ïµ (prâ‚ x) (prâ‚ y)
  l : (Ïµ : â„•) â†’ linear (Î» x y â†’ Î£-approx-order P _â‰¤â¿_ x y Ïµ)
  l Ïµ x y   = â‰¤â¿-linear    X a Ïµ (prâ‚ x) (prâ‚ y)
  d : (Ïµ : â„•) (x y : Î£ P) â†’ is-decidable (Î£-approx-order P _â‰¤â¿_ x y Ïµ)
  d Ïµ x y   = â‰¤â¿-decidable X a Ïµ (prâ‚ x) (prâ‚ y)
  c : (Ïµ : â„•) (x y : âŸ¨ Î£-ClosenessSpace X P p' âŸ©)
    â†’ C (Î£-ClosenessSpace X P p') Ïµ x y
    â†’ Î£-approx-order P _â‰¤â¿_ x y Ïµ
  c Ïµ x y   = â‰¤â¿-close X a Ïµ (prâ‚ x) (prâ‚ y)

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 Î£-approx-order-for
  : (X : ClosenessSpace ğ“¤)
  â†’ (P : âŸ¨ X âŸ© â†’ ğ“¥ Ì‡ )
  â†’ (p : (x : âŸ¨ X âŸ©) â†’ is-prop (P x))
  â†’ (_â‰¤_  : âŸ¨ X âŸ© â†’ âŸ¨ X âŸ© â†’ ğ“¦ Ì‡ )
  â†’ (_â‰¤â¿_ : âŸ¨ X âŸ© â†’ âŸ¨ X âŸ© â†’ â„• â†’ ğ“¦'  Ì‡)
  â†’ is-approx-order-for pt X _â‰¤_ _â‰¤â¿_
  â†’ is-approx-order-for pt
      (Î£-ClosenessSpace X P p)
      (Î£-order P _â‰¤_)
      (Î£-approx-order P _â‰¤â¿_)
 Î£-approx-order-for X P p _â‰¤_ _â‰¤â¿_ a
  = Î£-order-is-preorder P _â‰¤_ (â‰¤â¿-pre pt X a)
  , Î£-approx-order-is-approx-order X P p _â‰¤â¿_ (â‰¤â¿-approx pt X a)
  , f
  where
   f : is-approx-order-for' pt
         (Î£-ClosenessSpace X P p)
         (Î£-order P _â‰¤_)
         (Î£-approx-order P _â‰¤â¿_)
   f x y = â‰¤â¿-for pt X a (prâ‚ x) (prâ‚ y)
```

## Finite orders

```
_â‰¤Fin_ : {n : â„•} â†’ Fin n â†’ Fin n â†’ ğ“¤â‚€  Ì‡
_â‰¤Fin_ {succ n} ğŸ y = ğŸ™
_â‰¤Fin_ {succ n} (suc x) ğŸ = ğŸ˜
_â‰¤Fin_ {succ n} (suc x) (suc y) = x â‰¤Fin y

â‰¤Fin-is-linear-preorder : {n : â„•} â†’ is-linear-preorder (_â‰¤Fin_ {n})
â‰¤Fin-is-linear-preorder {n} = (r , t , p) , l
 where
  r : {n : â„•} â†’ reflexive (_â‰¤Fin_ {n})
  r {succ n} ğŸ = â‹†
  r {succ n} (suc x) = r x
  t : {n : â„•} â†’ transitive (_â‰¤Fin_ {n})
  t {succ n} ğŸ y z _ _ = â‹†
  t {succ n} (suc x) (suc y) (suc z) = t x y z
  p : {n : â„•} â†’ is-prop-valued (_â‰¤Fin_ {n})
  p {succ n} ğŸ y = ğŸ™-is-prop
  p {succ n} (suc x) ğŸ = ğŸ˜-is-prop
  p {succ n} (suc x) (suc y) = p x y
  l : {n : â„•} â†’ (x y : Fin n) â†’ (x â‰¤Fin y) + (y â‰¤Fin x)
  l {succ n} ğŸ y = inl â‹†
  l {succ n} (suc x) ğŸ = inr â‹†
  l {succ n} (suc x) (suc y) = l x y

finite-order : {F : ğ“¤ Ì‡ } â†’ finite-linear-order F â†’ F â†’ F â†’ ğ“¤â‚€  Ì‡
finite-order (n , (g , _)) = inclusion-order g _â‰¤Fin_ 

finite-order-is-linear-preorder
 : {F : ğ“¤ Ì‡ }
 â†’ (f : finite-linear-order F)
 â†’ is-linear-preorder (finite-order f)
finite-order-is-linear-preorder (n , (g , _))
 = inclusion-order-is-linear-preorder g _â‰¤Fin_ â‰¤Fin-is-linear-preorder

_<Fin_ : {n : â„•} â†’ Fin n â†’ Fin n â†’ ğ“¤â‚€ Ì‡
_<Fin_ {succ n} ğŸ ğŸ = ğŸ˜
_<Fin_ {succ n} ğŸ (suc x) = ğŸ™
_<Fin_ {succ n} (suc x) ğŸ = ğŸ˜
_<Fin_ {succ n} (suc x) (suc y) = x <Fin y

<Fin-is-strict-order : {n : â„•} â†’ is-strict-order (_<Fin_ {n})
<Fin-is-strict-order = i , t , a , p
 where
  i : {n : â„•} â†’ (x : Fin n) â†’ Â¬ (x <Fin x)
  i {succ n} ğŸ = id
  i {succ n} (suc x) = i x
  t : {n : â„•} â†’ transitive (_<Fin_ {n})
  t {succ n} ğŸ ğŸ ğŸ _                 = id
  t {succ n} ğŸ ğŸ (suc z) _ _         = â‹†
  t {succ n} ğŸ (suc y) ğŸ _           = id
  t {succ n} ğŸ (suc y) (suc z) _ _   = â‹†
  t {succ n} (suc x) ğŸ ğŸ _           = id
  t {succ n} (suc x) (suc y) ğŸ _     = id
  t {succ n} (suc x) (suc y) (suc z) = t x y z
  a : {n : â„•} â†’ (x y : Fin n) â†’ x <Fin y â†’ Â¬ (y <Fin x)
  a {succ n} ğŸ (suc y) x<y = id
  a {succ n} (suc x) (suc y) x<y = a x y x<y
  p : {n : â„•} â†’ is-prop-valued (_<Fin_ {n})
  p {succ n} ğŸ ğŸ = ğŸ˜-is-prop
  p {succ n} ğŸ (suc y) = ğŸ™-is-prop
  p {succ n} (suc x) ğŸ = ğŸ˜-is-prop
  p {succ n} (suc x) (suc y) = p x y

<Fin-trichotomous : {n : â„•} â†’ trichotomous (_<Fin_ {n})
<Fin-trichotomous {succ n} ğŸ ğŸ       = inr (inl refl)
<Fin-trichotomous {succ n} ğŸ (suc _) = inl â‹†
<Fin-trichotomous {succ n} (suc _) ğŸ = inr (inr â‹†)
<Fin-trichotomous {succ n} (suc x) (suc y)
 = Cases (<Fin-trichotomous x y)
     inl (cases (inr âˆ˜ inl âˆ˜ ap suc) (inr âˆ˜ inr))

finite-strict-order : {F : ğ“¤ Ì‡ } â†’ finite-linear-order F â†’ F â†’ F â†’ ğ“¤â‚€ Ì‡
finite-strict-order (n , (g , _)) = inclusion-order g _<Fin_
  
finite-strict-order-is-strict-order
 : {F : ğ“¤ Ì‡ } â†’ (f : finite-linear-order F)
 â†’ is-strict-order (finite-strict-order f)
finite-strict-order-is-strict-order (n , (g , _))
 = inclusion-order-is-strict-order g _<Fin_ <Fin-is-strict-order

finite-strict-order-trichotomous
 : {F : ğ“¤ Ì‡ } â†’ (f : finite-linear-order F)
 â†’ trichotomous (finite-strict-order f)
finite-strict-order-trichotomous (n , f)
 = embedding-strict-order-trichotomous (â‰ƒ-gives-â†ª f)
     _<Fin_ <Fin-trichotomous
```

## Discrete-sequence orders

```
discrete-lexicorder : {D : ğ“¤ Ì‡ }
                    â†’ is-discrete D
                    â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ )
                    â†’ (Î± Î² : â„• â†’ D)
                    â†’ ğ“¤ âŠ” ğ“¥  Ì‡ 
discrete-lexicorder f _<_ Î± Î²
 = (Î± âˆ¼ Î²) + (Î£ n ê‰ â„• , ((Î± âˆ¼â¿ Î²) n Ã— (Î± n) < (Î² n)))

discrete-lexicorder-is-preorder
 : {D : ğ“¤ Ì‡ } (d : is-discrete D)
 â†’ is-set D
 â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ )
 â†’ is-strict-order _<_
 â†’ is-preorder (discrete-lexicorder d _<_)
discrete-lexicorder-is-preorder d s _<_ (i' , t' , a' , p')
 = r , t , p
 where
  r : reflexive (discrete-lexicorder d _<_)
  r x = inl (Î» _ â†’ refl)
  t : transitive (discrete-lexicorder d _<_)
  t x y z (inl xâˆ¼y) (inl yâˆ¼z)
   = inl (Î» i â†’ xâˆ¼y i âˆ™ yâˆ¼z i)
  t x y z (inl xâˆ¼y) (inr (n , yâˆ¼â¿z , yn<zn))
   = inr (n , (Î» i i<n â†’ xâˆ¼y i âˆ™ yâˆ¼â¿z i i<n)
            , transport (_< z n) (xâˆ¼y n â»Â¹) yn<zn)
  t x y z (inr (n , xâˆ¼â¿y , xn<yn)) (inl yâˆ¼z)
   = inr (n , (Î» i i<n â†’ xâˆ¼â¿y i i<n âˆ™ yâˆ¼z i)
            , transport (x n <_) (yâˆ¼z n) xn<yn)
  t x y z (inr (n , xâˆ¼â¿y , xn<yn))
          (inr (m , yâˆ¼áµz , ym<zm)) with <-trichotomous n m
  ... | inl n<m
   = inr (n , (Î» i i<n â†’ xâˆ¼â¿y i i<n
                       âˆ™ yâˆ¼áµz i (<-trans i n m i<n n<m))
            , transport (x n <_) (yâˆ¼áµz n n<m) xn<yn)
  ... | inr (inl refl)
   = inr (n , (Î» i i<n â†’ xâˆ¼â¿y i i<n âˆ™ yâˆ¼áµz i i<n)
            , t' (x n) (y n) (z n) xn<yn ym<zm)
  ... | inr (inr m<n )
   = inr (m , (Î» i i<m â†’ xâˆ¼â¿y i (<-trans i m n i<m m<n)
                       âˆ™ yâˆ¼áµz i i<m)
            , transport (_< z m) (xâˆ¼â¿y m m<n â»Â¹) ym<zm)
  p : is-prop-valued (discrete-lexicorder d _<_)
  p x y = +-is-prop a b c
   where
    a : _
    a = Î -is-prop (fe _ _) (Î» _ â†’ s)
    b : _
    b (n , u , v) (m , w , e)
     = to-subtype-ï¼
        (Î» _ â†’ Ã—-is-prop
          (Î -is-prop (fe _ _)
            (Î» _ â†’ Î -is-prop (fe _ _)
              (Î» _ â†’ s)))
          (p' (x _) (y _)))
            (Cases (<-trichotomous n m)
              (Î» n<m â†’ ğŸ˜-elim (i' (y n)
                         (transport (_< y n) (w n n<m) v)))
              (cases id
              (Î» m<n â†’ ğŸ˜-elim (i' (x m)
                         (transport (x m <_) (u m m<n â»Â¹) e)))))
    c : _
    c g (n , w , v) = i' (y n) (transport (_< y n) (g n) v)
    
lexicorder-linearity-implies-LPO
 : {X : ğ“¤ Ì‡ }
 â†’ (f@(n , _) : finite-linear-order X)
 â†’ n > 1
 â†’ is-linear-preorder (discrete-lexicorder
                     (finite-is-discrete f) (finite-strict-order f))
 â†’ LPO
lexicorder-linearity-implies-LPO
 {ğ“¤} {X} f@(succ (succ n) , (g , (h , Î·) , _)) _ s@((r , t , p) , l) Î±
 = Cases (l (Î» _ â†’ dâ‚€) (Ï âˆ˜ Î±))
     (cases
       (Î» â‚€âˆ¼Î± â†’ inl (Î» n â†’ Ï-lc (â‚€âˆ¼Î± n â»Â¹)))
       (Î» (i , â‚€âˆ¼â±Î± , â‚€<Î±i) â†’ inr (i , Ï-lc (<-lemmaâ‚€ (Î± i) â‚€<Î±i))))
     (cases
       (Î» Î±âˆ¼â‚€ â†’ inl (Î» n â†’ Ï-lc (Î±âˆ¼â‚€ n)))
       (Î» (i , Î±âˆ¼â±â‚€ , Î±i<â‚€) â†’ ğŸ˜-elim (<-lemmaâ‚ (Î± i) Î±i<â‚€)))
 where
  _<â‚“_ = finite-strict-order f
  Î³ = finite-strict-order-is-strict-order f
  dâ‚€ dâ‚ : X
  dâ‚€ = h ğŸ
  dâ‚ = h ğŸ
  Ï : ğŸš â†’ X
  Ï â‚€ = dâ‚€
  Ï â‚ = dâ‚
  dâ‚€<dâ‚ : dâ‚€ <â‚“ dâ‚
  dâ‚€<dâ‚ = transport (_<Fin g (h ğŸ)) (Î· ğŸ â»Â¹)
            (transport (ğŸ <Fin_) (Î· ğŸ â»Â¹) â‹†)
  not-zero-means-one : (a : ğŸš) â†’ Ï a â‰  dâ‚€ â†’ Ï a ï¼ dâ‚
  not-zero-means-one â‚€ e = ğŸ˜-elim (e refl)
  not-zero-means-one â‚ _ = refl
  <-lemmaâ‚€ : (a : ğŸš) â†’ dâ‚€ <â‚“ Ï a â†’ Ï a ï¼ dâ‚
  <-lemmaâ‚€ â‚€   = ğŸ˜-elim âˆ˜ <-irrefâŸ¨ Î³ âŸ© dâ‚€
  <-lemmaâ‚€ â‚ _ = refl
  <-lemmaâ‚ : (a : ğŸš) â†’ Â¬ (Ï a <â‚“ dâ‚€)
  <-lemmaâ‚ â‚€ = <-irrefâŸ¨ Î³ âŸ© dâ‚€
  <-lemmaâ‚ â‚ = <-antiâŸ¨ Î³ âŸ© dâ‚€ dâ‚ dâ‚€<dâ‚
  Ï-lc : {a b : ğŸš} â†’ Ï a ï¼ Ï b â†’ a ï¼ b
  Ï-lc {â‚€} {â‚€} e = refl
  Ï-lc {â‚€} {â‚} e
   = ğŸ˜-elim (<-irrefâŸ¨ Î³ âŸ© dâ‚€ (transport (dâ‚€ <â‚“_) (e â»Â¹) dâ‚€<dâ‚))
  Ï-lc {â‚} {â‚€} e
   = ğŸ˜-elim (<-irrefâŸ¨ Î³ âŸ© dâ‚€ (transport (dâ‚€ <â‚“_) e dâ‚€<dâ‚))
  Ï-lc {â‚} {â‚} e = refl
 
finite-lexicorder
 : {F : ğ“¤ Ì‡ } (f : finite-linear-order F) (d : is-discrete F)
 â†’ (_<_ : F â†’ F â†’ ğ“¦ Ì‡ )
 â†’ (â„• â†’ F) â†’ (â„• â†’ F) â†’ ğ“¤ âŠ” ğ“¦  Ì‡
finite-lexicorder f d _<_ = discrete-lexicorder d _<_

discrete-approx-lexicorder : {D : ğ“¤ Ì‡ }
                           â†’ is-discrete D
                           â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ )
                           â†’ (Î± Î² : â„• â†’ D)
                           â†’ â„•
                           â†’ ğ“¤ âŠ” ğ“¥  Ì‡
discrete-approx-lexicorder d _<'_ Î± Î² n
 = (Î± âˆ¼â¿ Î²) n + (Î£ i ê‰ â„• , ((i < n) Ã— (Î± âˆ¼â¿ Î²) i Ã— (Î± i) <' (Î² i)))

discrete-approx-lexicorder-is-approx-order
 : {D : ğ“¤ Ì‡ } (ds : is-discrete D) (s : is-set D)
 â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ ) (s : is-strict-linear-order _<_)
 â†’ is-approx-order
     (â„•â†’D-ClosenessSpace ds)
     (discrete-approx-lexicorder ds _<_)
discrete-approx-lexicorder-is-approx-order
 {ğ“¤} {ğ“¥} {D} ds s _<'_ s'@((i' , t' , a' , p') , l')
 = (Î» Ïµ â†’ (r Ïµ , ((t Ïµ) , (p Ïµ))) , l Ïµ)
 , d
 , c
 where
  r : (n : â„•)
    â†’ reflexive (Î» x y â†’ discrete-approx-lexicorder ds _<'_ x y n)
  r n x = inl (Î» _ _ â†’ refl)
  t : (n : â„•)
    â†’ transitive (Î» x y â†’ discrete-approx-lexicorder ds _<'_ x y n)
  t n x y z (inl xâˆ¼â¿y) (inl yâˆ¼áµz)
   = inl (Î» i i<n â†’ xâˆ¼â¿y i i<n âˆ™ yâˆ¼áµz i i<n)
  t n x y z (inl xâˆ¼â¿y) (inr (i , i<n , yâˆ¼â±z , yi<zi))
   = inr (i , i<n , (Î» j j<i â†’ xâˆ¼â¿y j (<-trans j i n j<i i<n)
                             âˆ™ yâˆ¼â±z j j<i)
            , transport (_<' z i) (xâˆ¼â¿y i i<n â»Â¹) yi<zi)
  t n x y z (inr (i , i<n , xâˆ¼â±y , xi<yi)) (inl yâˆ¼â¿z)
   = inr (i , i<n
            , (Î» j j<i â†’ xâˆ¼â±y j j<i âˆ™ yâˆ¼â¿z j (<-trans j i n j<i i<n))
            , transport (x i <'_) (yâˆ¼â¿z i i<n) xi<yi)
  t n x y z (inr (i , i<n , xâˆ¼â±y , xi<yi))
            (inr (k , k<n , yâˆ¼áµz , yk<zk)) with <-trichotomous i k
  ... | inl i<k
   = inr (i , i<n
            , (Î» j j<i â†’ xâˆ¼â±y j j<i
                       âˆ™ yâˆ¼áµz j (<-trans j i k j<i i<k))
            , transport (x i <'_) (yâˆ¼áµz i i<k) xi<yi)
  ... | inr (inl refl)
   = inr (i , i<n
            , (Î» j j<i â†’ xâˆ¼â±y j j<i âˆ™ yâˆ¼áµz j j<i)
            , t' (x i) (y i) (z i) xi<yi yk<zk)
  ... | inr (inr k<i )
   = inr (k , k<n
            , (Î» j j<k â†’ xâˆ¼â±y j (<-trans j k i j<k k<i)
                       âˆ™ yâˆ¼áµz j j<k)
            , transport (_<' z k) (xâˆ¼â±y k k<i â»Â¹) yk<zk)
  p : (n : â„•)
    â†’ is-prop-valued (Î» x y â†’ discrete-approx-lexicorder ds _<'_ x y n)
  p n x y = +-is-prop (a n) b c
   where
    a : (i : â„•) â†’ is-prop ((x âˆ¼â¿ y) i)
    a _ = Î -is-prop (fe _ _) (Î» _ â†’ Î -is-prop (fe _ _) (Î» _ â†’ s))
    b : _
    b (i , i<n , xâˆ¼â±y , xi<yi) (j , j<n , xâˆ¼Ê²y , xj<yj)
     = to-subtype-ï¼
         (Î» k â†’ Ã—â‚ƒ-is-prop (<-is-prop-valued k n)
           (a k)
           (p' (x k) (y k)))
         (Cases (<-trichotomous i j)
           (Î» i<j â†’ ğŸ˜-elim (i' (y i)
                      (transport (_<' y i) (xâˆ¼Ê²y i i<j) xi<yi)))
           (cases id
           (Î» j<i â†’ ğŸ˜-elim (i' (y j)
                      (transport (_<' y j) (xâˆ¼â±y j j<i) xj<yj)))))
    c : _
    c xâˆ¼â¿y (i , i<n , xâˆ¼â±y , xi<yi)
     = i' (y i) (transport (_<' y i) (xâˆ¼â¿y i i<n) xi<yi)
  l : (n : â„•) â†’ (x y : â„• â†’ D)
    â†’ discrete-approx-lexicorder ds _<'_ x y n
    + discrete-approx-lexicorder ds _<'_ y x n
  l zero x y = inl (inl (Î» _ ()))
  l (succ n) x y with l n x y | l' (x n) (y n)
  ... | inl (inr (i , i<n , xâˆ¼â±y , xi<yi)) | _
   = inl (inr (i , <-trans i n (succ n) i<n (<-succ n)
                 , xâˆ¼â±y , xi<yi))
  ... | inr (inr (i , i<n , yâˆ¼â±x , yi<xi)) | _
   = inr (inr (i , <-trans i n (succ n) i<n (<-succ n)
                 , yâˆ¼â±x , yi<xi))
  ... | inl (inl xâˆ¼â¿y) | inl xn<yn
   = inl (inr (n , <-succ n , xâˆ¼â¿y , xn<yn))
  ... | inl (inl xâˆ¼â¿y) | inr (inl xn=yn)
   = inl (inl (Î» i i<sn â†’
       Cases (<-split i n i<sn)
         (xâˆ¼â¿y i)
         (Î» i=n â†’ transport (Î» - â†’ x - ï¼ y -) (i=n â»Â¹) xn=yn)))
  ... | inl (inl xâˆ¼â¿y) | inr (inr yn<xn)
   = inr (inr (n , <-succ n
                 , (Î» i i<sn â†’ xâˆ¼â¿y i i<sn â»Â¹) , yn<xn))
  ... | inr (inl yâˆ¼â¿x) | inl xn<yn
   = inl (inr (n , <-succ n
                 , (Î» i i<sn â†’ yâˆ¼â¿x i i<sn â»Â¹) , xn<yn))
  ... | inr (inl yâˆ¼â¿x) | inr (inl xn=yn)
   = inr (inl (Î» i i<sn â†’
       Cases (<-split i n i<sn)
         (yâˆ¼â¿x i)
         (Î» i=n â†’ transport (Î» - â†’ y - ï¼ x -) (i=n â»Â¹) (xn=yn â»Â¹))))
  ... | inr (inl yâˆ¼â¿x) | inr (inr yn<xn)
   = inr (inr (n , <-succ n , yâˆ¼â¿x , yn<xn))
  d : (Ïµ : â„•) (x y : â„• â†’ D)
    â†’ is-decidable (discrete-approx-lexicorder ds _<'_ x y Ïµ)
  d Ïµ x y
    = +-preserves-decidability (âˆ¼â¿-decidable (Î» _ â†’ ds) x y Ïµ)
        (bounded-decidable-Î£
          (Î» i â†’ Ã—-preserves-decidability
            (âˆ¼â¿-decidable (Î» _ â†’ ds) x y i)
            (strict-linear-order-decidable _<'_ s' (x i) (y i)))
          Ïµ)
  c : (n : â„•) â†’ (x y : â„• â†’ D)
    â†’ C (â„•â†’D-ClosenessSpace ds) n x y
    â†’ discrete-approx-lexicorder ds _<'_ x y n
  c 0 x y Cnxy
   = inl (Î» _ ())
  c (succ n) x y Cnxy
   = inl (ğŸš-decidableâ‚ (âˆ¼â¿-decidable (Î» _ â†’ ds) x y (succ n))
      (Cnxy n (â„•-to-â„•âˆ-diagonalâ‚ n)))

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 discrete-approx-lexicorder-for'
  : {D : ğ“¤ Ì‡ } (ds : is-discrete D) (i : is-set D)
  â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ )
  â†’ is-approx-order-for' pt
      (â„•â†’D-ClosenessSpace ds)
      (discrete-lexicorder ds _<_)
      (discrete-approx-lexicorder ds _<_)
 discrete-approx-lexicorder-for' ds i _<_ Î± Î² (inl Î±âˆ¼Î²)
  = âˆ£ (0 , Î» _ _ â†’ inl (Î» n _ â†’ Î±âˆ¼Î² n)) âˆ£ 
 discrete-approx-lexicorder-for' ds i _<_ Î± Î² (inr (n , Î±âˆ¼â¿Î² , Î±n<Î²n))
  = âˆ£ succ n , (Î» Ïµ n<Ïµ â†’ inr (n , n<Ïµ , Î±âˆ¼â¿Î² , Î±n<Î²n)) âˆ£

 discrete-approx-lexicorder-for
  : {D : ğ“¤ Ì‡ } (ds : is-discrete D) (i : is-set D)
  â†’ (_<_ : D â†’ D â†’ ğ“¥ Ì‡ ) (s : is-strict-linear-order _<_)
  â†’ is-approx-order-for pt
      (â„•â†’D-ClosenessSpace ds)
      (discrete-lexicorder ds _<_)
      (discrete-approx-lexicorder ds _<_)
 discrete-approx-lexicorder-for ds i _<_ (s , l)
  = discrete-lexicorder-is-preorder ds i _<_ s
  , discrete-approx-lexicorder-is-approx-order ds i _<_ (s , l)
  , discrete-approx-lexicorder-for' ds i _<_
```

## Specific example orders

```
â„•â†’ğŸš-lexicorder : (â„• â†’ ğŸš) â†’ (â„• â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
â„•â†’ğŸš-lexicorder
 = discrete-lexicorder ğŸš-is-discrete _<â‚‚_

â„•âˆ-lexicorder : â„•âˆ â†’ â„•âˆ â†’ ğ“¤â‚€ Ì‡
â„•âˆ-lexicorder = Î£-order is-decreasing â„•â†’ğŸš-lexicorder

<â‚‚-is-strict : is-strict-order _<â‚‚_
<â‚‚-is-strict
 = <â‚‚-irref 
 , <â‚‚-trans
 , <â‚‚-anti
 , Î» _ _ â†’ <â‚‚-is-prop-valued
 where
  <â‚‚-irref : (x : ğŸš) â†’ Â¬ (x <â‚‚ x)
  <â‚‚-irref x x<x = zero-is-not-one (prâ‚ Î³ â»Â¹ âˆ™ prâ‚‚ Î³)
   where
    Î³ : (x ï¼ â‚€) Ã— (x ï¼ â‚)
    Î³ = <â‚‚-criterion-converse x<x
  <â‚‚-anti : (x y : ğŸš) â†’ x <â‚‚ y â†’ Â¬ (y <â‚‚ x)
  <â‚‚-anti x y x<y y<x = zero-is-not-one (xâ‚€ â»Â¹ âˆ™ xâ‚)
   where
    xâ‚€ : x ï¼ â‚€
    xâ‚€ = prâ‚ (<â‚‚-criterion-converse x<y)
    xâ‚ : x ï¼ â‚
    xâ‚ = prâ‚‚ (<â‚‚-criterion-converse y<x)

<â‚‚-trichotomous : trichotomous _<â‚‚_
<â‚‚-trichotomous â‚€ â‚€ = inr (inl refl)
<â‚‚-trichotomous â‚€ â‚ = inl â‹†
<â‚‚-trichotomous â‚ â‚€ = inr (inr â‹†)
<â‚‚-trichotomous â‚ â‚ = inr (inl refl)

â„•â†’ğŸš-lexicorder-is-preorder : is-preorder â„•â†’ğŸš-lexicorder
â„•â†’ğŸš-lexicorder-is-preorder
 = discrete-lexicorder-is-preorder ğŸš-is-discrete
     ğŸš-is-set _<â‚‚_ <â‚‚-is-strict

â„•âˆ-lexicorder-is-preorder : is-preorder â„•âˆ-lexicorder
â„•âˆ-lexicorder-is-preorder
 = Î£-order-is-preorder is-decreasing
     â„•â†’ğŸš-lexicorder â„•â†’ğŸš-lexicorder-is-preorder

â„•â†’ğŸš-approx-lexicorder : (â„• â†’ ğŸš) â†’ (â„• â†’ ğŸš) â†’ â„• â†’ ğ“¤â‚€ Ì‡ 
â„•â†’ğŸš-approx-lexicorder = discrete-approx-lexicorder ğŸš-is-discrete _<â‚‚_

â„•â†’ğŸš-approx-lexicorder-is-approx-order
 : is-approx-order ğŸšá´º-ClosenessSpace â„•â†’ğŸš-approx-lexicorder
â„•â†’ğŸš-approx-lexicorder-is-approx-order
 = discrete-approx-lexicorder-is-approx-order
     ğŸš-is-discrete ğŸš-is-set _<â‚‚_ (<â‚‚-is-strict , <â‚‚-trichotomous)

â„•âˆ-approx-lexicorder : â„•âˆ â†’ â„•âˆ â†’ â„• â†’ ğ“¤â‚€ Ì‡
â„•âˆ-approx-lexicorder
 = Î£-approx-order is-decreasing â„•â†’ğŸš-approx-lexicorder

â„•âˆ-approx-lexicorder-is-approx-order
 : is-approx-order â„•âˆ-ClosenessSpace â„•âˆ-approx-lexicorder
â„•âˆ-approx-lexicorder-is-approx-order
 = Î£-approx-order-is-approx-order ğŸšá´º-ClosenessSpace
     is-decreasing (being-decreasing-is-prop (fe _ _))
     â„•â†’ğŸš-approx-lexicorder
     â„•â†’ğŸš-approx-lexicorder-is-approx-order
```

[â‡ Index](../html/TWA.Thesis.index.html)
