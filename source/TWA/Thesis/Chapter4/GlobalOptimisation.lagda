\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import MLTT.Spartan
open import UF.FunExt

module TWA.Thesis.Chapter4.GlobalOptimisation (fe : FunExt) where

open import TWA.Thesis.Chapter2.FiniteDiscrete
open import TWA.Thesis.Chapter3.ClosenessSpaces fe
open import TWA.Thesis.Chapter4.ApproxOrder fe

-- Definition 4.1.18
is-global-minimal : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡ )
                  â†’ (X â†’ Y) â†’ X â†’ ğ“¤ âŠ” ğ“¦  Ì‡
is-global-minimal {ğ“¤} {ğ“¥} {ğ“¦'} {X} _â‰¤_ f xâ‚€ = (x : X) â†’ f xâ‚€ â‰¤ f x

has-global-minimal : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡ )
                   â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¦  Ì‡
has-global-minimal f = Î£ âˆ˜ (is-global-minimal f)

-- Lemma 4.1.19
ğ”½-global-minimal : (n : â„•) â†’ ğ”½ n â†’ {Y : ğ“¤ Ì‡ }
                 â†’ (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡ )
                 â†’ is-linear-order _â‰¤_
                 â†’ (f : ğ”½ n â†’ Y)
                 â†’ has-global-minimal _â‰¤_ f
ğ”½-global-minimal 1 (inl â‹†) _â‰¤_ l f = inl â‹† , Î³
 where
  â‰¤ğ”½-refl = (prâ‚ âˆ˜ prâ‚) l
  Î³ : is-global-minimal _â‰¤_ f (inl â‹†)
  Î³ (inl â‹†) = â‰¤ğ”½-refl (f (inl â‹†))
ğ”½-global-minimal (succ (succ n)) x _â‰¤_ l f
 with ğ”½-global-minimal (succ n) (inl â‹†) _â‰¤_ l (f âˆ˜ inr)
... | (x'â‚€ , m) = Cases (â‰¤ğ”½-linear (f (inr x'â‚€)) (f (inl â‹†))) Î³â‚ Î³â‚‚
 where
  â‰¤ğ”½-linear = prâ‚‚ l
  â‰¤ğ”½-refl = (prâ‚ âˆ˜ prâ‚) l
  â‰¤ğ”½-trans = (prâ‚ âˆ˜ prâ‚‚ âˆ˜ prâ‚) l
  Î³â‚ : f (inr x'â‚€) â‰¤ f (inl â‹†  ) â†’ has-global-minimal _â‰¤_ f
  Î³â‚ x'â‚€â‰¤â‹† = inr x'â‚€ , Î³
   where
    Î³ : (x : ğ”½ (succ (succ n))) â†’ f (inr x'â‚€) â‰¤ f x
    Î³ (inl â‹†) = x'â‚€â‰¤â‹†
    Î³ (inr x) = m x
  Î³â‚‚ : f (inl â‹†  ) â‰¤ f (inr x'â‚€) â†’ has-global-minimal _â‰¤_ f
  Î³â‚‚ â‹†â‰¤x'â‚€ = inl â‹† , Î³
   where
    Î³ : (x : ğ”½ (succ (succ n))) â†’ f (inl â‹†) â‰¤ f x
    Î³ (inl â‹†) = â‰¤ğ”½-refl  (f (inl â‹†))
    Î³ (inr x) = â‰¤ğ”½-trans (f (inl â‹†)) (f (inr x'â‚€)) (f (inr x))
                  â‹†â‰¤x'â‚€ (m x)

finite-global-minimal : {X : ğ“¤ Ì‡ } {Y : ğ“¥  Ì‡ }
                      â†’ X â†’ finite-discrete X
                      â†’ (_â‰¤_ : Y â†’ Y â†’ ğ“¦ Ì‡ )
                      â†’ is-linear-order _â‰¤_
                      â†’ (f : X â†’ Y)
                      â†’ has-global-minimal _â‰¤_ f
finite-global-minimal x (0 , (_ , (h , _) , _)) _â‰¤_ l f
 = ğŸ˜-elim (h x)
finite-global-minimal x (succ n , e@(g , (h , Î·) , _)) _â‰¤_ l f
 with ğ”½-global-minimal (succ n) (inl â‹†) _â‰¤_ l (f âˆ˜ g)
... | (xâ‚€ , Î³â‚€) = g xâ‚€
                , Î» x â†’ transport (f (g xâ‚€) â‰¤_) (ap f (Î· x)) (Î³â‚€ (h x))

-- Definition 4.1.20
is_global-minimal : â„• â†’ {ğ“¤ ğ“¥ : Universe}
                  â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                  â†’ (_â‰¤â¿_ : Y â†’ Y â†’ â„• â†’ ğ“¦ Ì‡ )
                  â†’ (f : X â†’ Y) â†’ X â†’ ğ“¦ âŠ” ğ“¤  Ì‡ 
(is Ïµ global-minimal) {ğ“¤} {ğ“¥} {X} _â‰¤â¿_ f xâ‚€
 = (x : X) â†’ (f xâ‚€ â‰¤â¿ f x) Ïµ

has_global-minimal : â„• â†’ {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ }
                   â†’ {Y : ğ“¥ Ì‡ }
                   â†’ (_â‰¤â¿_ : Y â†’ Y â†’ â„• â†’ ğ“¦ Ì‡ )
                   â†’ (f : X â†’ Y) â†’ (ğ“¦ âŠ” ğ“¤) Ì‡ 
(has Ïµ global-minimal) {ğ“¤} {ğ“¥} {ğ“¦} {X} _â‰¤â¿_ f
 = Î£ ((is Ïµ global-minimal) {ğ“¤} {ğ“¥} {ğ“¦} {X} _â‰¤â¿_ f)

-- Lemma 4.1.21
ğ”½-Ïµ-global-minimal : (n : â„•) â†’ ğ”½ n
                   â†’ (Y : ClosenessSpace ğ“¥)
                   â†’ (_â‰¤_  : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ ğ“¦ Ì‡ )
                   â†’ (_â‰¤â¿_ : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ â„• â†’ ğ“¦'  Ì‡ )
                   â†’ is-approx-order Y _â‰¤_ _â‰¤â¿_
                   â†’ (Ïµ : â„•) â†’ (f : ğ”½ n â†’ âŸ¨ Y âŸ©)
                   â†’ (has Ïµ global-minimal) _â‰¤â¿_ f
ğ”½-Ïµ-global-minimal 1 (inl â‹†) Y _â‰¤_ _â‰¤â¿_ a Ïµ f
 = inl â‹† , Î³
 where
  Î³ : is Ïµ global-minimal _â‰¤â¿_ f (inl â‹†)
  Î³ (inl â‹†) = approx-order-refl Y _â‰¤_ _â‰¤â¿_ a Ïµ (f (inl â‹†)) 
ğ”½-Ïµ-global-minimal (succ (succ n)) _ Y _â‰¤_ _â‰¤â¿_ a Ïµ f 
 with ğ”½-Ïµ-global-minimal (succ n) (inl â‹†) Y _â‰¤_ _â‰¤â¿_ a Ïµ (f âˆ˜ inr) 
... | (xâ‚€ , m)
 = Cases (approx-order-linear Y _â‰¤_ _â‰¤â¿_ a Ïµ (f (inr xâ‚€)) (f (inl â‹†)))
     Î³â‚ Î³â‚‚
 where
  Î³â‚ : (f (inr xâ‚€) â‰¤â¿ f (inl â‹†)) Ïµ â†’ has Ïµ global-minimal _â‰¤â¿_ f
  Î³â‚ xâ‚€â‰¤â‹† = inr xâ‚€ , Î³
   where
    Î³ : is Ïµ global-minimal _â‰¤â¿_ f (inr xâ‚€)
    Î³ (inl â‹†) = xâ‚€â‰¤â‹†
    Î³ (inr x) = m x
  Î³â‚‚ : (f (inl â‹†) â‰¤â¿ f (inr xâ‚€)) Ïµ â†’ has Ïµ global-minimal _â‰¤â¿_ f
  Î³â‚‚ â‹†â‰¤xâ‚€ = inl â‹† , Î³
   where
    Î³ : is Ïµ global-minimal _â‰¤â¿_ f (inl â‹†)
    Î³ (inl â‹†) = approx-order-refl Y _â‰¤_ _â‰¤â¿_ a Ïµ (f (inl â‹†))
    Î³ (inr x) = approx-order-trans Y _â‰¤_ _â‰¤â¿_ a Ïµ
                  (f (inl â‹†)) (f (inr xâ‚€)) (f (inr x))
                  â‹†â‰¤xâ‚€ (m x)

F-Ïµ-global-minimal : {X : ğ“¤ Ì‡ } (Y : ClosenessSpace ğ“¥)
                   â†’ X â†’ finite-discrete X
                   â†’ (_â‰¤_  : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ ğ“¦ Ì‡ )
                   â†’ (_â‰¤â¿_ : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ â„• â†’ ğ“¦'  Ì‡ )
                   â†’ is-approx-order Y _â‰¤_ _â‰¤â¿_
                   â†’ (Ïµ : â„•) â†’ (f : X â†’ âŸ¨ Y âŸ©)
                   â†’ (has Ïµ global-minimal) _â‰¤â¿_ f
F-Ïµ-global-minimal Y x (n , (g , (h , Î·) , _)) _â‰¤_ _â‰¤â¿_ a Ïµ f
 with ğ”½-Ïµ-global-minimal n (h x) Y _â‰¤_ _â‰¤â¿_ a Ïµ (f âˆ˜ g)
... | (xâ‚€ , m)
 = g xâ‚€
 , Î» x â†’ transport (Î» - â†’ (f (g xâ‚€) â‰¤â¿ f -) Ïµ) (Î· x) (m (h x))

-- Lemma 4.1.23
cover-continuity-lemma
 : (X : ClosenessSpace ğ“¤) (Y : ClosenessSpace ğ“¥)
 â†’ (_â‰¤_  : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ ğ“¦ Ì‡ )
 â†’ (_â‰¤â¿_ : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ â„• â†’ ğ“¦'  Ì‡ )
 â†’ is-approx-order Y _â‰¤_ _â‰¤â¿_
 â†’ (Ïµ : â„•) â†’ (f : âŸ¨ X âŸ© â†’ âŸ¨ Y âŸ©) (Ï• : f-ucontinuous X Y f)
 â†’ let Î´ = prâ‚ (Ï• Ïµ) in ((X' , g , _) : (Î´ cover-of X) ğ“¤')
 â†’ finite-discrete X'
 â†’ (x : âŸ¨ X âŸ©) â†’ Î£ x' ê‰ X' , (f (g x') â‰¤â¿ f x) Ïµ
cover-continuity-lemma
 X Y _â‰¤_ _â‰¤â¿_ (_ , _ , c , a) Ïµ f Ï• (X' , g , Î·) e x
 = (prâ‚ (Î· x))
 , c Ïµ (f (g (prâ‚ (Î· x)))) (f x)
     (C-sym Y Ïµ (f x) (f (g (prâ‚ (Î· x))))
       (prâ‚‚ (Ï• Ïµ) x (g (prâ‚ (Î· x)))
         (prâ‚‚ (Î· x))))

-- Theorem 4.1.22
global-opt : (X : ClosenessSpace ğ“¤) (Y : ClosenessSpace ğ“¥)
           â†’ âŸ¨ X âŸ©
           â†’ (_â‰¤_  : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ ğ“¦ Ì‡ )
           â†’ (_â‰¤â¿_ : âŸ¨ Y âŸ© â†’ âŸ¨ Y âŸ© â†’ â„• â†’ ğ“¦'  Ì‡ )
           â†’ is-approx-order Y _â‰¤_ _â‰¤â¿_
           â†’ (Ïµ : â„•) â†’ (f : âŸ¨ X âŸ© â†’ âŸ¨ Y âŸ©) (Ï• : f-ucontinuous X Y f)
           â†’ totally-bounded X ğ“¤'
           â†’ (has Ïµ global-minimal) _â‰¤â¿_ f
global-opt {ğ“¤} {ğ“¥} {ğ“¦} {ğ“¦'} {ğ“¤'} X Y xâ‚ _â‰¤_ _â‰¤â¿_ a Ïµ f Ï• t
 = (g x'â‚€)
 , (Î» x â†’ approx-order-trans Y _â‰¤_ _â‰¤â¿_ a Ïµ
            (f (g x'â‚€)) (f (g (h x))) (f x)
            (m (h x)) (h-min x))
 where
  Î´ : â„•
  Î´ = prâ‚ (Ï• Ïµ)
  Î´-cover : (Î´ cover-of X) ğ“¤'
  Î´-cover = prâ‚ (t Î´)
  X' : ğ“¤'  Ì‡
  X' = prâ‚ Î´-cover
  X'-is-Î´-cover : X' is Î´ cover-of X
  X'-is-Î´-cover  = prâ‚‚ Î´-cover
  X'-is-finite : finite-discrete X'
  X'-is-finite = prâ‚‚ (t Î´)
  g : X' â†’ âŸ¨ X âŸ©
  g = prâ‚ X'-is-Î´-cover
  Î· : (x : âŸ¨ X âŸ©) â†’ Î£ x' ê‰ X' , (f (g x') â‰¤â¿ f x) Ïµ
  Î· = cover-continuity-lemma X Y _â‰¤_ _â‰¤â¿_ a Ïµ f Ï• Î´-cover X'-is-finite
  h : âŸ¨ X âŸ© â†’ X'
  h x = prâ‚ (Î· x)
  h-min : (x : âŸ¨ X âŸ©) â†’ (f (g (h x)) â‰¤â¿ f x) Ïµ
  h-min x = prâ‚‚ (Î· x)
  first  : has Ïµ global-minimal _â‰¤â¿_ (f âˆ˜ g)
  first
   = F-Ïµ-global-minimal Y (h xâ‚) X'-is-finite _â‰¤_ _â‰¤â¿_ a Ïµ (f âˆ˜ g)
  x'â‚€ : X'
  x'â‚€ = prâ‚ first
  m  : is Ïµ global-minimal _â‰¤â¿_ (f âˆ˜ g) x'â‚€
  m  = prâ‚‚ first
\end{code}
