[‚áê Index](../html/TWA.Thesis.index.html)

# Additional integer properties

```
{-# OPTIONS --without-K --exact-split --safe #-}
            
open import Integers.Addition renaming (_+_ to _+‚Ñ§_ ; _-_ to _‚Ñ§-_)
open import Integers.Multiplication renaming (_*_ to _‚Ñ§*_)
open import Integers.Negation renaming (-_ to ‚Ñ§-_)
open import Integers.Order
open import Integers.Type
open import MLTT.Spartan
open import Naturals.Addition renaming (_+_ to _+‚Ñï_)
open import Naturals.Multiplication renaming (_*_ to _‚Ñï*_)
open import Naturals.Order
open import Notation.Order hiding (_‚â§_‚â§_)
open import UF.Base
open import UF.Subsingletons

module TWA.Thesis.Chapter5.Integers where
```

## ‚Ñ§-elimination

```agda
‚Ñ§-elim : (P : ‚Ñ§ ‚Üí ùì§ Ãá )
       ‚Üí ((n : ‚Ñï) ‚Üí P (pos n)) ‚Üí ((n : ‚Ñï) ‚Üí P (negsucc n))
       ‚Üí Œ† P
‚Ñ§-elim P Pp Pn (pos     n) = Pp n
‚Ñ§-elim P Pp Pn (negsucc n) = Pn n
```

## Monotone and rec properties

```agda
succ-to-monotone' : (P : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ùì§ Ãá )
                  ‚Üí ((a : ‚Ñ§) ‚Üí P a a)
                  ‚Üí ((a b c : ‚Ñ§) ‚Üí P a b ‚Üí P b c ‚Üí P a c)
                  ‚Üí ((a : ‚Ñ§) ‚Üí P a (succ‚Ñ§ a))
                  ‚Üí (a b : ‚Ñ§) (n : ‚Ñï) ‚Üí a +pos n Ôºù b ‚Üí P a b
succ-to-monotone' P r t s a a zero refl = r a
succ-to-monotone' P r t s a b (succ n) refl
 = t a (succ‚Ñ§ a) b (s a)
     (succ-to-monotone' P r t s (succ‚Ñ§ a) (succ‚Ñ§ (a +pos n))
       n (‚Ñ§-left-succ-pos a n))

succ-to-monotone : (P : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ùì§ Ãá )
                 ‚Üí ((a : ‚Ñ§) ‚Üí P a a)
                 ‚Üí ((a b c : ‚Ñ§) ‚Üí P a b ‚Üí P b c ‚Üí P a c)
                 ‚Üí ((a : ‚Ñ§) ‚Üí P a (succ‚Ñ§ a))
                 ‚Üí (a b : ‚Ñ§) ‚Üí a ‚â§‚Ñ§ b ‚Üí P a b
succ-to-monotone P r t s a b (n , Œ≥)
 = succ-to-monotone' P r t s a b n Œ≥

‚â§-succ-to-monotone : (f : ‚Ñ§ ‚Üí ‚Ñ§) ‚Üí ((a : ‚Ñ§) ‚Üí f a ‚â§‚Ñ§ f (succ‚Ñ§ a))
                   ‚Üí (a b : ‚Ñ§) ‚Üí a ‚â§‚Ñ§ b ‚Üí f a ‚â§‚Ñ§ f b
‚â§-succ-to-monotone f = succ-to-monotone (Œª x y ‚Üí f x ‚â§‚Ñ§ f y)
                         (Œª x     ‚Üí ‚Ñ§‚â§-refl  (f x))
                         (Œª x y z ‚Üí ‚Ñ§‚â§-trans (f x) (f y) (f z))

rec-to-monotone : (f g : ‚Ñ§ ‚Üí ‚Ñ§) ‚Üí ((a b : ‚Ñ§) ‚Üí a ‚â§‚Ñ§ b ‚Üí f a ‚â§‚Ñ§ g b)
                ‚Üí (a b : ‚Ñ§) (n : ‚Ñï) ‚Üí a ‚â§‚Ñ§ b ‚Üí rec a f n ‚â§‚Ñ§ rec b g n
rec-to-monotone f g h a b zero a‚â§b
 = a‚â§b
rec-to-monotone f g h a b (succ n) a‚â§b
 = h (rec a f n) (rec b g n) (rec-to-monotone f g h a b n a‚â§b)

rec-f-Ôºù : {X : ùì§ Ãá } ‚Üí (f : X ‚Üí X) (x : X) (n : ‚Ñï)
        ‚Üí rec (f x) f n Ôºù rec x f (succ n) 
rec-f-Ôºù f x zero = refl
rec-f-Ôºù f x (succ n) = ap f (rec-f-Ôºù f x n)
```

## Sign and num for integers

```agda
sign : ‚Ñ§ ‚Üí (‚Ñï ‚Üí ‚Ñ§)
sign (pos     _) = pos
sign (negsucc _) = negsucc

num : ‚Ñ§ ‚Üí ‚Ñï
num  (pos     n) = n
num  (negsucc n) = n
```

## Natural number functions definitions and properties

```agda
_/2 : ‚Ñï ‚Üí ‚Ñï
0 /2 = 0
1 /2 = 0
succ (succ n) /2 = succ (n /2)

_/2' : ‚Ñ§ ‚Üí ‚Ñ§
pos x     /2' = pos (x /2)
negsucc x /2' = ‚Ñ§- (pos (succ x /2))

_‚Ñï^_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ‚Ñï
a ‚Ñï^ b = ((a ‚Ñï*_) ^ b) 1

infixl 33 _‚Ñï^_ 

2^ : ‚Ñï ‚Üí ‚Ñï
2^ = 2 ‚Ñï^_

power-of-pos-positive : ‚àÄ n ‚Üí is-pos-succ (pos (2^ n))
power-of-pos-positive 0 = ‚ãÜ
power-of-pos-positive (succ n)
 = transport is-pos-succ (pos-multiplication-equiv-to-‚Ñï 2 (2^ n)) I
 where
  I : is-pos-succ (pos 2 ‚Ñ§* pos (2^ n))
  I = is-pos-succ-mult (pos 2) (pos (2^ n)) ‚ãÜ (power-of-pos-positive n)

prod-of-powers : (n a b : ‚Ñï) ‚Üí n ‚Ñï^ a ‚Ñï* n ‚Ñï^ b Ôºù n ‚Ñï^ (a +‚Ñï b)
prod-of-powers n a zero     = refl
prod-of-powers n a (succ b) = I
 where
  I : n ‚Ñï^ a ‚Ñï* n ‚Ñï^ succ b Ôºù n ‚Ñï^ (a +‚Ñï succ b)
  I = n ‚Ñï^ a ‚Ñï* n ‚Ñï^ succ b
        Ôºù‚ü® refl ‚ü©
      n ‚Ñï^ a ‚Ñï* (n ‚Ñï* n ‚Ñï^ b)
        Ôºù‚ü® mult-associativity (n ‚Ñï^ a) n (n ‚Ñï^ b) ‚Åª¬π ‚ü©
      n ‚Ñï^ a ‚Ñï* n ‚Ñï* n ‚Ñï^ b
        Ôºù‚ü® ap (_‚Ñï* n ‚Ñï^ b) (mult-commutativity (n ‚Ñï^ a) n) ‚ü©
      n ‚Ñï* n ‚Ñï^ a ‚Ñï* n ‚Ñï^ b
        Ôºù‚ü® mult-associativity n (n ‚Ñï^ a) (n ‚Ñï^ b) ‚ü©
      n ‚Ñï* (n ‚Ñï^ a ‚Ñï* n ‚Ñï^ b)
        Ôºù‚ü® ap (n ‚Ñï*_) (prod-of-powers n a b) ‚ü©
      n ‚Ñï* n ‚Ñï^ (a +‚Ñï b)
        Ôºù‚ü® refl ‚ü©
      n ‚Ñï^ succ (a +‚Ñï b)
        Ôºù‚ü® refl ‚ü©
      n ‚Ñï^ (a +‚Ñï succ b)       ‚àé

exponents-of-two-ordered : (m : ‚Ñï) ‚Üí 2 ‚Ñï^ m < 2 ‚Ñï^ (succ m)
exponents-of-two-ordered 0        = ‚ãÜ
exponents-of-two-ordered (succ m)
 = transport‚ÇÇ _<_ I II
     (multiplication-preserves-strict-order (2 ‚Ñï^ m) (2 ‚Ñï^ succ m) 1 IH)
 where 
  IH : 2 ‚Ñï^ m < 2 ‚Ñï^ succ m
  IH = exponents-of-two-ordered m
  I : 2 ‚Ñï^ m ‚Ñï* 2 Ôºù 2 ‚Ñï^ succ m
  I = mult-commutativity (2 ‚Ñï^ m) 2
  II : 2 ‚Ñï^ succ m ‚Ñï* 2 Ôºù 2 ‚Ñï^ succ (succ m)
  II = mult-commutativity (2 ‚Ñï^ succ m) 2

div-by-two' : (k : ‚Ñï) ‚Üí k +‚Ñï k /2 Ôºù k
div-by-two' 0 = refl
div-by-two' (succ k)
 = (succ k +‚Ñï succ k) /2     Ôºù‚ü® ap _/2 (succ-left k (succ k)) ‚ü©
   succ (succ (k +‚Ñï k)) /2   Ôºù‚ü® refl ‚ü©
   succ ((k +‚Ñï k) /2)        Ôºù‚ü® ap succ (div-by-two' k) ‚ü©
   succ k                    ‚àé
```

## Integer order definitions and properties

```
‚Ñ§‚â§-decidable : (n m : ‚Ñ§) ‚Üí (n ‚â§ m) + ¬¨ (n ‚â§ m)
‚Ñ§‚â§-decidable n m
 = Cases (‚Ñ§-trichotomous m n)
     (inr ‚àò ‚Ñ§-less-not-bigger-or-equal m n)
     (inl ‚àò ‚Ñ§‚â§-attach n m)

pred-shift : (a b : ‚Ñ§) ‚Üí pred‚Ñ§ a ‚Ñ§- b Ôºù a ‚Ñ§- succ‚Ñ§ b
pred-shift a b = ‚Ñ§-left-pred a (‚Ñ§- b)
               ‚àô ‚Ñ§-right-pred a (‚Ñ§- b) ‚Åª¬π
               ‚àô ap (a +‚Ñ§_)
                   (succ‚Ñ§-lc (succpred‚Ñ§ _
                             ‚àô succpred‚Ñ§ _ ‚Åª¬π
                             ‚àô ap succ‚Ñ§ (negation-dist b (pos 1))))

‚Ñ§-less-not-equal : (a b : ‚Ñ§) ‚Üí a <‚Ñ§ b ‚Üí a ‚â† b
‚Ñ§-less-not-equal a a (n , a<a) refl = Œ≥ Œ≥'
 where
   Œ≥' : 0 Ôºù succ n
   Œ≥' = pos-lc (‚Ñ§+-lc _ _ a (a<a ‚Åª¬π ‚àô ‚Ñ§-left-succ-pos a n))
   Œ≥ : 0 ‚â† succ n
   Œ≥ ()

‚â§-succ‚Ñ§' : (x y : ‚Ñ§) ‚Üí succ‚Ñ§ x ‚â§ succ‚Ñ§ y ‚Üí x ‚â§ y
‚â§-succ‚Ñ§' x y (n , e) = n , succ‚Ñ§-lc (‚Ñ§-left-succ x (pos n) ‚Åª¬π ‚àô e) 

‚Ñ§‚â§-succ-inj : (a b : ‚Ñ§) ‚Üí a ‚â§‚Ñ§ b ‚Üí succ‚Ñ§ a ‚â§‚Ñ§ succ‚Ñ§ b
‚Ñ§‚â§-succ-inj a b (n , refl) = n , ‚Ñ§-left-succ-pos a n

‚Ñ§‚â§-succ‚Åø-inj
 : (a b : ‚Ñ§) (n : ‚Ñï) ‚Üí a ‚â§‚Ñ§ b ‚Üí (succ‚Ñ§ ^ n) a ‚â§‚Ñ§ (succ‚Ñ§ ^ n) b
‚Ñ§‚â§-succ‚Åø-inj = rec-to-monotone succ‚Ñ§ succ‚Ñ§ ‚Ñ§‚â§-succ-inj

‚Ñ§‚â§-pred-inj : (a b : ‚Ñ§) ‚Üí a ‚â§‚Ñ§ b ‚Üí pred‚Ñ§ a ‚â§‚Ñ§ pred‚Ñ§ b
‚Ñ§‚â§-pred-inj a b (n , refl) = n , ‚Ñ§-left-pred-pos a n

‚Ñ§‚â§-pred‚Åø-inj
 : (a b : ‚Ñ§) (n : ‚Ñï) ‚Üí a ‚â§‚Ñ§ b ‚Üí (pred‚Ñ§ ^ n) a ‚â§‚Ñ§ (pred‚Ñ§ ^ n) b
‚Ñ§‚â§-pred‚Åø-inj = rec-to-monotone pred‚Ñ§ pred‚Ñ§ ‚Ñ§‚â§-pred-inj

_‚â§‚Ñ§_‚â§‚Ñ§_ : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ùì§‚ÇÄ Ãá 
x ‚â§‚Ñ§ y ‚â§‚Ñ§ z = (x ‚â§‚Ñ§ y) √ó (y ‚â§‚Ñ§ z)

‚Ñ§‚â§¬≤-refl : (k : ‚Ñ§) ‚Üí k ‚â§‚Ñ§ k ‚â§‚Ñ§ k
‚Ñ§‚â§¬≤-refl k = ‚Ñ§‚â§-refl k , ‚Ñ§‚â§-refl k

‚â§‚Ñ§¬≤-is-prop : {l u : ‚Ñ§} (x : ‚Ñ§) ‚Üí is-prop (l ‚â§‚Ñ§ x ‚â§‚Ñ§ u)
‚â§‚Ñ§¬≤-is-prop {l} {u} x = √ó-is-prop (‚Ñ§‚â§-is-prop l x) (‚Ñ§‚â§-is-prop x u)

‚Ñ§[_,_] : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ùì§‚ÇÄ Ãá
‚Ñ§[ l , u ] = Œ£ z Íûâ ‚Ñ§ , (l ‚â§‚Ñ§ z ‚â§‚Ñ§ u)

‚Ñ§[_,_]-succ : (l u : ‚Ñ§) ‚Üí ‚Ñ§[ l , u ] ‚Üí ‚Ñ§[ l , succ‚Ñ§ u ]
‚Ñ§[ l , u ]-succ (z , l‚â§z , z‚â§u)
 = z , l‚â§z , ‚Ñ§‚â§-trans z u (succ‚Ñ§ u) z‚â§u (1 , refl) 

‚â§‚Ñ§-antisym : (x y : ‚Ñ§) ‚Üí x ‚â§‚Ñ§ y ‚â§‚Ñ§ x ‚Üí x Ôºù y
‚â§‚Ñ§-antisym x y (x‚â§y , y‚â§x)
 = Cases (‚Ñ§‚â§-split x y x‚â§y) (Cases (‚Ñ§‚â§-split y x y‚â§x)
     (Œª y<x x<y
      ‚Üí ùüò-elim (‚Ñ§-equal-not-less-than x (‚Ñ§<-trans x y x x<y y<x)))
     (Œª y=x _ ‚Üí y=x ‚Åª¬π))
     id

‚â§‚Ñ§-back : ‚àÄ x y ‚Üí x <‚Ñ§ y ‚Üí x ‚â§‚Ñ§ pred‚Ñ§ y
‚â§‚Ñ§-back x .(succ‚Ñ§ x +‚Ñ§ pos n) (n , refl)
 = ‚Ñ§‚â§-trans x (x +pos n) (pred‚Ñ§ (succ‚Ñ§ x +pos n))
     (n , refl)
     (transport ((x +pos n) ‚â§‚Ñ§_)
       (predsucc‚Ñ§ (x +pos n) ‚Åª¬π
       ‚àô ap pred‚Ñ§ (‚Ñ§-left-succ x (pos n) ‚Åª¬π))
       (‚Ñ§‚â§-refl (x +pos n)))

‚Ñ§-dich-succ : (x y : ‚Ñ§) 
            ‚Üí ((      x <‚Ñ§ y) + (y ‚â§‚Ñ§       x))
            ‚Üí ((succ‚Ñ§ x <‚Ñ§ y) + (y ‚â§‚Ñ§ succ‚Ñ§ x))
‚Ñ§-dich-succ x y (inl (0 , refl)) = inr (‚Ñ§‚â§-refl _)
‚Ñ§-dich-succ x y (inl (succ m , refl))
 = inl (m , ‚Ñ§-left-succ-pos (succ‚Ñ§ x) m)
‚Ñ§-dich-succ x y (inr (m , refl)) = inr (succ m , refl)

‚Ñ§-trich-succ : (x y : ‚Ñ§) 
             ‚Üí ((      x <‚Ñ§ y) + (      x Ôºù y) + (y <‚Ñ§       x))
             ‚Üí ((succ‚Ñ§ x <‚Ñ§ y) + (succ‚Ñ§ x Ôºù y) + (y <‚Ñ§ succ‚Ñ§ x))
‚Ñ§-trich-succ x y (inl (0           , sn+jÔºùi))
 = (inr ‚àò inl) sn+jÔºùi
‚Ñ§-trich-succ x y (inl (succ j      , sn+jÔºùi))
 = inl (j , (‚Ñ§-left-succ-pos (succ‚Ñ§ x) j ‚àô sn+jÔºùi))
‚Ñ§-trich-succ x y (inr (inl              nÔºùi))
 = (inr ‚àò inr) (0 , ap succ‚Ñ§ (nÔºùi ‚Åª¬π))
‚Ñ§-trich-succ x y (inr (inr (j      , sn+jÔºùi)))
 = (inr ‚àò inr) (succ j , ap succ‚Ñ§ sn+jÔºùi)

‚Ñ§-vert-trich-locate : ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ‚Ñ§ ‚Üí ùì§‚ÇÄ Ãá
‚Ñ§-vert-trich-locate z a b = (z <‚Ñ§ a) + (a ‚â§‚Ñ§ z ‚â§‚Ñ§ b) + (b <‚Ñ§ z)

‚Ñ§-vert-trich-succ : (z a b : ‚Ñ§) ‚Üí a <‚Ñ§ b
                  ‚Üí ‚Ñ§-vert-trich-locate        z  a b
                  ‚Üí ‚Ñ§-vert-trich-locate (succ‚Ñ§ z) a b
‚Ñ§-vert-trich-succ z a b (k , Œ∑) (inl (succ n , Œµ))
 = inl         (n , (‚Ñ§-left-succ-pos (succ‚Ñ§ z) n ‚àô Œµ))
‚Ñ§-vert-trich-succ z a b (k , Œ∑) (inl (0      , refl))
 = (inr ‚àò inl)
     ((0 , refl) , (succ k , (‚Ñ§-left-succ-pos (succ‚Ñ§ z) k ‚Åª¬π ‚àô Œ∑)))
‚Ñ§-vert-trich-succ z a b (k , Œ∑) (inr (inl ((n‚ÇÅ , Œµ‚ÇÅ) , succ n‚ÇÇ , Œµ‚ÇÇ)))
 = (inr ‚àò inl)
     ((succ n‚ÇÅ , (ap succ‚Ñ§ Œµ‚ÇÅ)) , (n‚ÇÇ , (‚Ñ§-left-succ-pos z n‚ÇÇ ‚àô Œµ‚ÇÇ)))
‚Ñ§-vert-trich-succ z a b (k , Œ∑) (inr (inl ((n‚ÇÅ , Œµ‚ÇÅ) , zero , Œµ‚ÇÇ)))
 = (inr ‚àò inr) (0 , ap succ‚Ñ§ (Œµ‚ÇÇ ‚Åª¬π))
‚Ñ§-vert-trich-succ z a b (k , Œ∑) (inr (inr (n , refl)))
 = (inr ‚àò inr) (succ n , refl)

‚Ñ§-vert-trich-all : (z a b : ‚Ñ§) ‚Üí ‚Ñ§-vert-trich-locate z a b
‚Ñ§-vert-trich-all z a b = Cases (‚Ñ§-trichotomous z a) inl
                 Œª a‚â§z ‚Üí Cases (‚Ñ§-trichotomous b z) (inr ‚àò inr)
                 Œª z‚â§b ‚Üí (inr ‚àò inl)
                           (‚Ñ§‚â§-attach _ _ a‚â§z , ‚Ñ§‚â§-attach _ _ z‚â§b)

‚Ñ§-vert-trich-is-prop : (z a b : ‚Ñ§) ‚Üí a <‚Ñ§ b
                     ‚Üí is-prop (‚Ñ§-vert-trich-locate z a b)
‚Ñ§-vert-trich-is-prop z a b a<b
 = +-is-prop
     (‚Ñ§<-is-prop z a) (+-is-prop (‚â§‚Ñ§¬≤-is-prop z) (‚Ñ§<-is-prop b z)
     (Œª (_ , z‚â§b) ‚Üí ‚Ñ§-bigger-or-equal-not-less z b z‚â§b))
     (Œª z<a ‚Üí cases
      (Œª (a‚â§z , _) ‚Üí ‚Ñ§-less-not-bigger-or-equal z a z<a a‚â§z)
      (‚Ñ§-bigger-or-equal-not-less z b
        (<-is-‚â§ z b (‚Ñ§<-trans z a b z<a a<b))))

‚Ñ§‚â§-progress : (a b c : ‚Ñ§)
            ‚Üí ((n , _) : a ‚â§ c) ‚Üí a ‚â§ b ‚Üí ((n‚ÇÇ , _) : b ‚â§ c)
            ‚Üí n‚ÇÇ < succ n
‚Ñ§‚â§-progress a b c a‚â§c (n‚ÇÅ , refl) (n‚ÇÇ , refl)
 = transport (n‚ÇÇ ‚â§_)
     (‚Ñ§‚â§-same-witness a c
       (‚Ñ§‚â§-trans a b c (n‚ÇÅ , refl) (n‚ÇÇ , refl)) a‚â§c)
     (‚â§-+' n‚ÇÅ n‚ÇÇ)

‚â•-lemma : (a b c : ‚Ñ§) ‚Üí a Ôºù b ‚Üí (p : a ‚â• c) ‚Üí (q : b ‚â• c)
        ‚Üí pr‚ÇÅ p Ôºù pr‚ÇÅ q
‚â•-lemma a a c refl (n , refl) (m , Œ≥) = pos-lc (‚Ñ§+-lc _ _ _ (Œ≥ ‚Åª¬π))
```

## Parity definitions and properties

```agda
odd even : ‚Ñ§ ‚Üí ùì§‚ÇÄ Ãá
odd (pos                   0) = ùüò
odd (pos                   1) = ùüô
odd (pos (succ (succ x)))     = odd (pos x)
odd (negsucc               0) = ùüô
odd (negsucc               1) = ùüò
odd (negsucc (succ (succ x))) = odd (negsucc x)
even x = ¬¨ odd x

even-or-odd? : (x : ‚Ñ§) ‚Üí even x + odd x
even-or-odd? (pos                   0) = inl (Œª x ‚Üí x)
even-or-odd? (pos                   1) = inr ‚ãÜ
even-or-odd? (pos (succ (succ x)))     = even-or-odd? (pos x)
even-or-odd? (negsucc               0) = inr ‚ãÜ
even-or-odd? (negsucc               1) = inl (Œª x ‚Üí x)
even-or-odd? (negsucc (succ (succ x))) = even-or-odd? (negsucc x)

odd-is-prop : (x : ‚Ñ§) ‚Üí is-prop (odd x)
odd-is-prop (pos                   0) = ùüò-is-prop
odd-is-prop (pos                   1) = ùüô-is-prop
odd-is-prop (pos (succ (succ x)))     = odd-is-prop (pos x)
odd-is-prop (negsucc               0) = ùüô-is-prop
odd-is-prop (negsucc               1) = ùüò-is-prop
odd-is-prop (negsucc (succ (succ x))) = odd-is-prop (negsucc x)

succ-odd-is-even : (x : ‚Ñ§) ‚Üí odd x ‚Üí even (succ‚Ñ§ x)
succ-odd-is-even (pos                          1) o = id
succ-odd-is-even (pos            (succ (succ x))) o
 = succ-odd-is-even (pos x) o
succ-odd-is-even (negsucc                      0) o = id
succ-odd-is-even (negsucc (succ (succ (succ x)))) o
 = succ-odd-is-even (negsucc (succ x)) o

succ-even-is-odd : (x : ‚Ñ§) ‚Üí even x ‚Üí odd (succ‚Ñ§ x)
succ-even-is-odd (pos                          0) e = ‚ãÜ
succ-even-is-odd (pos                          1) e = e ‚ãÜ
succ-even-is-odd (pos            (succ (succ x))) e
 = succ-even-is-odd (pos x) e
succ-even-is-odd (negsucc                      0) e = e ‚ãÜ
succ-even-is-odd (negsucc                      1) e = ‚ãÜ
succ-even-is-odd (negsucc                      2) e = e ‚ãÜ
succ-even-is-odd (negsucc (succ (succ (succ x)))) e
 = succ-even-is-odd (negsucc (succ x)) e

odd-succ-succ : (x : ‚Ñ§) ‚Üí odd x ‚Üí odd (succ‚Ñ§ (succ‚Ñ§ x))
odd-succ-succ (pos x) = id
odd-succ-succ (negsucc zero) = id
odd-succ-succ (negsucc (succ (succ x))) = id

even-succ-succ : (x : ‚Ñ§) ‚Üí even x ‚Üí even (succ‚Ñ§ (succ‚Ñ§ x))
even-succ-succ (pos x) = id
even-succ-succ (negsucc zero) = id
even-succ-succ (negsucc (succ (succ x))) = id

negation-preserves-parity : (x : ‚Ñ§) ‚Üí even x ‚Üí even (‚Ñ§- x)
negation-preserves-parity (pos 0) = id
negation-preserves-parity (pos (succ 0)) e = ùüò-elim (e ‚ãÜ)
negation-preserves-parity (pos (succ (succ 0))) e = id
negation-preserves-parity (pos (succ (succ (succ x)))) e
 = negation-preserves-parity (pos (succ x)) e
negation-preserves-parity (negsucc 0) e = ùüò-elim (e ‚ãÜ)
negation-preserves-parity (negsucc (succ 0)) e = id
negation-preserves-parity (negsucc (succ (succ x))) e
 = negation-preserves-parity (negsucc x)
     (even-succ-succ (negsucc (succ (succ x))) e)

even-lemma-pos : (x : ‚Ñï) ‚Üí even (pos x) ‚Üí (pos x /2') ‚Ñ§* pos 2 Ôºù pos x
even-lemma-pos 0 even-x = refl
even-lemma-pos (succ 0) even-x = ùüò-elim (even-x ‚ãÜ)
even-lemma-pos (succ (succ x)) even-x
 = succ‚Ñ§ (pos x /2') +‚Ñ§ succ‚Ñ§ (pos x /2')
     Ôºù‚ü® ‚Ñ§-left-succ (pos x /2') (succ‚Ñ§ (pos x /2')) ‚ü©
   succ‚Ñ§ (succ‚Ñ§ ((pos x /2') ‚Ñ§* pos 2))
     Ôºù‚ü® ap (Œª z ‚Üí succ‚Ñ§ (succ‚Ñ§ z)) (even-lemma-pos x even-x) ‚ü©
   pos (succ (succ x)) ‚àé

even-lemma-neg : (x : ‚Ñï) ‚Üí even (negsucc x)
               ‚Üí (negsucc x /2') ‚Ñ§* pos 2 Ôºù negsucc x
even-lemma-neg x even-x
 = (‚Ñ§- pos (succ x /2)) ‚Ñ§- pos (succ x /2)
     Ôºù‚ü® negation-dist (pos (succ x /2)) (pos (succ x /2)) ‚ü©
   ‚Ñ§- (pos (succ x /2) +‚Ñ§ pos (succ x /2))
     Ôºù‚ü® ap ‚Ñ§-_ (even-lemma-pos (succ x)
                  (negation-preserves-parity (negsucc x) even-x)) ‚ü©
   negsucc x ‚àé

even-lemma : (x : ‚Ñ§) ‚Üí even x ‚Üí (x /2') ‚Ñ§* pos 2 Ôºù x
even-lemma (pos x) = even-lemma-pos x
even-lemma (negsucc x) = even-lemma-neg x

odd-succ-succ' : (k : ‚Ñ§) ‚Üí odd (succ‚Ñ§ (succ‚Ñ§ k)) ‚Üí odd k
odd-succ-succ' (pos x) = id
odd-succ-succ' (negsucc zero) = id
odd-succ-succ' (negsucc (succ (succ x))) = id

even-succ-succ' : (k : ‚Ñ§) ‚Üí even (succ‚Ñ§ (succ‚Ñ§ k)) ‚Üí even k
even-succ-succ' (pos 0) e = id
even-succ-succ' (pos (succ 0)) e = ùüò-elim (e ‚ãÜ)
even-succ-succ' (pos (succ (succ x))) e = e
even-succ-succ' (negsucc 0) e = ùüò-elim (e ‚ãÜ)
even-succ-succ' (negsucc (succ 0)) e = id
even-succ-succ' (negsucc (succ (succ x))) e = e

times-two-even' : (k : ‚Ñ§) ‚Üí even (k +‚Ñ§ k)
times-two-even' (pos (succ k)) odd2k
 = times-two-even' (pos k)
     (odd-succ-succ' (pos k +‚Ñ§ pos k) (transport odd I odd2k))
 where
  I : pos (succ k) +‚Ñ§ pos (succ k) Ôºù pos k +‚Ñ§ pos (succ (succ k))
  I = ‚Ñ§-left-succ (pos k) (pos (succ k))
times-two-even' (negsucc (succ k)) odd2k
 = times-two-even' (negsucc k)
     (transport odd I
       (odd-succ-succ (negsucc (succ k) +‚Ñ§ negsucc (succ k)) odd2k))
 where
  I : succ‚Ñ§ (succ‚Ñ§ (negsucc (succ k) +‚Ñ§ negsucc (succ k)))
    Ôºù negsucc k +‚Ñ§ negsucc k
  I = succ‚Ñ§ (succ‚Ñ§ (negsucc (succ k) +‚Ñ§ negsucc (succ k)))
        Ôºù‚ü® refl ‚ü©
      succ‚Ñ§ (succ‚Ñ§ (pred‚Ñ§ (negsucc k) +‚Ñ§ pred‚Ñ§ (negsucc k)))
        Ôºù‚ü® refl ‚ü©
      succ‚Ñ§ (succ‚Ñ§ (pred‚Ñ§ (pred‚Ñ§ (negsucc k) +‚Ñ§ negsucc k)))
        Ôºù‚ü® ap (Œª a ‚Üí succ‚Ñ§ a)
               (succpred‚Ñ§ (pred‚Ñ§ (negsucc k) +‚Ñ§ negsucc k)) ‚ü©
      succ‚Ñ§ (pred‚Ñ§ (negsucc k) +‚Ñ§ negsucc k)
        Ôºù‚ü® ap succ‚Ñ§ (‚Ñ§-left-pred (negsucc k) (negsucc k)) ‚ü©
      succ‚Ñ§ (pred‚Ñ§ (negsucc k +‚Ñ§ negsucc k))
        Ôºù‚ü® succpred‚Ñ§ (negsucc k +‚Ñ§ negsucc k) ‚ü©
      negsucc k +‚Ñ§ negsucc k ‚àé

negsucc-lemma
 : (x : ‚Ñï) ‚Üí negsucc x +‚Ñ§ negsucc x Ôºù negsucc (x +‚Ñï succ x)
negsucc-lemma x
 = negsucc x +‚Ñ§ negsucc x
     Ôºù‚ü® refl ‚ü©
   (‚Ñ§- pos (succ x)) ‚Ñ§- pos (succ x)
     Ôºù‚ü® negation-dist (pos (succ x)) (pos (succ x)) ‚ü©
   ‚Ñ§- (pos (succ x) +‚Ñ§ pos (succ x))
     Ôºù‚ü® refl ‚ü©
   ‚Ñ§- succ‚Ñ§ (pos (succ x) +‚Ñ§ pos x)
     Ôºù‚ü® ap (Œª z ‚Üí ‚Ñ§- succ‚Ñ§ z) (distributivity-pos-addition (succ x) x) ‚ü©
   ‚Ñ§- succ‚Ñ§ (pos (succ x +‚Ñï x))
     Ôºù‚ü® refl ‚ü©
   negsucc (succ x +‚Ñï x)
     Ôºù‚ü® ap negsucc (addition-commutativity (succ x) x) ‚ü©
   negsucc (x +‚Ñï succ x) ‚àé

div-by-two : (k : ‚Ñ§) ‚Üí (k +‚Ñ§ k) /2' Ôºù k
div-by-two (pos k)
 = (pos k +‚Ñ§ pos k) /2'
     Ôºù‚ü® ap _/2' (distributivity-pos-addition k k) ‚ü©     
   pos (k +‚Ñï k) /2'
     Ôºù‚ü® ap pos (div-by-two' k) ‚ü©
   pos k ‚àé
div-by-two (negsucc x)
 = (negsucc x +‚Ñ§ negsucc x) /2'
     Ôºù‚ü® ap _/2' (negsucc-lemma x) ‚ü©
   negsucc (x +‚Ñï succ x) /2'
     Ôºù‚ü® refl ‚ü©
   ‚Ñ§- pos (succ (x +‚Ñï succ x) /2)
     Ôºù‚ü® ap (Œª z ‚Üí ‚Ñ§- pos (z /2)) (succ-left x (succ x) ‚Åª¬π) ‚ü©
   ‚Ñ§- pos ((succ x +‚Ñï succ x) /2)
     Ôºù‚ü® ap (Œª z ‚Üí ‚Ñ§- pos z) (div-by-two' (succ x)) ‚ü©
   negsucc x ‚àé
```

[‚áê Index](../html/TWA.Thesis.index.html)
