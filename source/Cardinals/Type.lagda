Jon Sterling, 25th March 2023.

The HoTT book shows that under excluded middle, there are weak successor
cardinals.  I show that under suitable propositional resizing assumptions, this
holds constructively.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import MLTT.Spartan
open import UF.Base
open import UF.Equiv
open import UF.FunExt
open import UF.PropTrunc
open import UF.Retracts
open import UF.SetTrunc
open import UF.Size
open import UF.Subsingletons
import Various.LawvereFPT as LFTP

module Cardinals.Type
 (fe : FunExt)
 (pe : PropExt)
 (st : set-truncations-exist)
 (pt : propositional-truncations-exist)
 (psz : Propositional-Resizing)
 (impred : (ğ“¤ ğ“¥ : Universe) â†’ Î©-Resizing ğ“¤ ğ“¥)
 where

open import UF.Embeddings
open import UF.Subsingletons-FunExt

import UF.Logic

open set-truncations-exist st
open propositional-truncations-exist pt
open UF.Logic.AllCombinators pt (fe _ _)

Card : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Card ğ“¤ = set-trunc (hSet ğ“¤)

Card-is-set : is-set (Card ğ“¤)
Card-is-set = set-trunc-is-set

_[â‰¤]_ : hSet ğ“¤ â†’ hSet ğ“¥ â†’ Î© (ğ“¤ âŠ” ğ“¥)
A [â‰¤] B = âˆ¥ underlying-set A â†ª underlying-set B âˆ¥Î©


_â‰¤_ : Card ğ“¤ â†’ Card ğ“¥ â†’ Î© (ğ“¤ âŠ” ğ“¥)
_â‰¤_ {ğ“¤} {ğ“¥} =
 set-trunc-rec _ lemÂ·0 Î» A â†’
 set-trunc-rec _ lemÂ·1 Î» B â†’
 A [â‰¤] B
 where
  abstract
   lemÂ·1 : is-set (Î© (ğ“¤ âŠ” ğ“¥))
   lemÂ·1 = Î©-is-set (fe _ _) (pe _)

   lemÂ·0 : is-set (Card ğ“¥ â†’ Î© (ğ“¤ âŠ” ğ“¥))
   lemÂ·0 = Î -is-set (fe _ _) Î» _ â†’ lemÂ·1

module _ {ğ“¤ ğ“¥} {A : hSet ğ“¤} {B : hSet ğ“¥} where
 abstract
  â‰¤-compute : (set-trunc-in A â‰¤ set-trunc-in B) ï¼ (A [â‰¤] B)
  â‰¤-compute =
   happly (set-trunc-ind-Î² _ _ _ _) (set-trunc-in B)
   âˆ™ set-trunc-ind-Î² _ _ _ _

â‰¤-refl : (Î± : Card ğ“¤) â†’ (Î± â‰¤ Î±) holds
â‰¤-refl =
 set-trunc-ind _ (Î» _ â†’ props-are-sets (holds-is-prop (_ â‰¤ _))) Î» A â†’
 transportâ»Â¹ _holds â‰¤-compute
 âˆ£ (id , id-is-embedding) âˆ£


_<_ : Card ğ“¤ â†’ Card ğ“¤ â†’ Î© (ğ“¤ âº)
Î± < Î² = (Î± â‰¤ Î²) âˆ§ (Î± â‰¢ Î²)

Î©' : (ğ“¤ : Universe) â†’ ğ“¤ Ì‡
Î©' ğ“¤ = resized ğ“¤ (Î© ğ“¤) (impred _ _)

Î©'-equiv : Î©' ğ“¤ â‰ƒ Î© ğ“¤
Î©'-equiv = resizing-condition _ (Î© _) (impred _ _)

resize-Î© : Î© ğ“¤ â†’ Î© ğ“¥
prâ‚ (resize-Î© Ï•) = resize psz (Ï• holds) (holds-is-prop Ï•)
prâ‚‚ (resize-Î© Ï•) = resize-is-prop psz (Ï• holds) (holds-is-prop Ï•)

resize-Î©-roundtrip : (Ï• : Î© ğ“¤) â†’ resize-Î© {ğ“¦} (resize-Î© Ï•) ï¼ Ï•
resize-Î©-roundtrip Ï• =
 to-Î£-ï¼
  (main ,
   being-prop-is-prop (fe _ _) _ _)
 where
  fwd : resize-Î© (resize-Î© Ï•) holds â†’ Ï• holds
  fwd =
   from-resize psz _ (holds-is-prop Ï•)
   âˆ˜ from-resize psz _ (resize-is-prop psz (Ï• holds) (holds-is-prop Ï•))

  bwd : Ï• holds â†’ resize-Î© (resize-Î© Ï•) holds
  bwd =
   to-resize psz _ (resize-is-prop psz (Ï• holds) (holds-is-prop Ï•))
    âˆ˜ to-resize psz (Ï• holds) (holds-is-prop Ï•)

  main : (resize-Î© (resize-Î© Ï•)) holds ï¼ Ï• holds
  main =
   pe _
    (holds-is-prop (resize-Î© (resize-Î© Ï•)))
    (holds-is-prop Ï•)
    fwd
    bwd


resize-Î©-is-equiv : is-equiv (resize-Î© {ğ“¤} {ğ“¥})
prâ‚ (prâ‚ resize-Î©-is-equiv) = resize-Î©
prâ‚‚ (prâ‚ resize-Î©-is-equiv) = resize-Î©-roundtrip
prâ‚ (prâ‚‚ resize-Î©-is-equiv) = resize-Î©
prâ‚‚ (prâ‚‚ resize-Î©-is-equiv) = resize-Î©-roundtrip

resize-Î©-â‰ƒ : Î© ğ“¤ â‰ƒ Î© ğ“¥
prâ‚ resize-Î©-â‰ƒ = resize-Î©
prâ‚‚ resize-Î©-â‰ƒ = resize-Î©-is-equiv

Î©'-is-set : is-set (Î©' ğ“¤)
Î©'-is-set =
 subtypes-of-sets-are-sets
  (eqtofun Î©'-equiv)
  (equivs-are-lc _ (eqtofun- Î©'-equiv))
  (Î©-is-set (fe _ _) (pe _))

module _ {ğ“¤ : Universe} where
 powerset : (A : ğ“¤ Ì‡ ) â†’ hSet ğ“¤
 prâ‚ (powerset A) = A â†’ Î©' ğ“¤
 prâ‚‚ (powerset A) =
  Î -is-set (fe _ _) Î» _ â†’
  Î©'-is-set

 singleton-embedding' : (A : hSet ğ“¤) â†’ underlying-set A â†ª (underlying-set A â†’ Î© ğ“¤)
 prâ‚ (singleton-embedding' A) x y = (x ï¼ y) , underlying-set-is-set A
 prâ‚‚ (singleton-embedding' A) Ï• = main
  where
   main : is-prop (Î£ z ê‰ underlying-set A , (Î» y â†’ (z ï¼ y) , prâ‚‚ A) ï¼ Ï•)
   main (u , Hu) (v , Hv) =
    to-Î£-ï¼
     (transport id (ap prâ‚ (happly (Hv âˆ™ Hu â»Â¹) v)) refl ,
      Î -is-set (fe _ _) (Î» _ â†’ Î©-is-set (fe _ _) (pe _)) _ _)

 singleton-embedding : (A : hSet ğ“¤) â†’ underlying-set A â†ª (underlying-set A â†’ Î©' ğ“¤)
 singleton-embedding A = aux âˆ˜â†ª singleton-embedding' A
  where
   aux : _ â†ª _
   prâ‚ aux = back-eqtofun Î©'-equiv âˆ˜_
   prâ‚‚ aux =
    lc-maps-into-sets-are-embeddings _
     (Î» Ï• â†’
      dfunext (fe _ _) Î» z â†’
      let Ïˆ = happly Ï• z in
      equivs-are-lc _ (inverses-are-equivs _ (prâ‚‚ Î©'-equiv)) Ïˆ)
     (Î -is-set (fe _ _) (Î» _ â†’ Î©'-is-set))

[weak-successor] : (A : hSet ğ“¤) â†’ Î£ Î² ê‰ Card ğ“¤ , (set-trunc-in A < Î²) holds
prâ‚ ([weak-successor] A) =
 set-trunc-in (powerset (underlying-set A))
prâ‚ (prâ‚‚ ([weak-successor] A)) =
 transportâ»Â¹
  _holds
  â‰¤-compute
  âˆ£ singleton-embedding A âˆ£
prâ‚‚ (prâ‚‚ ([weak-successor] A)) H =
 âˆ¥âˆ¥-rec ğŸ˜-is-prop main lem2
 where
  lem : (set-trunc-in (powerset (underlying-set A)) â‰¤ set-trunc-in A) holds
  lem = transport (Î» Î² â†’ (Î² â‰¤ set-trunc-in A) holds) H (â‰¤-refl _)

  lem2 : (powerset (underlying-set A) [â‰¤] A) holds
  lem2 = transport _holds â‰¤-compute lem

  main : ((underlying-set A â†’ Î©' ğ“¤) â†ª underlying-set A) â†’ ğŸ˜
  main Î¹ =
   LFTP.retract-version.cantor-theorem-for-embeddings fe pe psz
    (underlying-set A)
    Î¹'
    Î¹'-emb
   where
    Q : Î©â‚€ â‰ƒ Î©' ğ“¤
    Q = resize-Î©-â‰ƒ â— (â‰ƒ-sym Î©'-equiv)

    Î¹' : (underlying-set A â†’ Î©â‚€) â†’ underlying-set A
    Î¹' U = prâ‚ Î¹ (eqtofun Q âˆ˜ U)

    Î¹'-lc : left-cancellable Î¹'
    Î¹'-lc {U} {V} Ï• =
     dfunext (fe _ (ğ“¤â‚€ âº)) Î» x â†’
     equivs-are-lc (eqtofun Q) (prâ‚‚ Q)
      (happly (embeddings-are-lc (prâ‚ Î¹) (prâ‚‚ Î¹) Ï•) x)

    Î¹'-emb : is-embedding Î¹'
    Î¹'-emb =
     lc-maps-into-sets-are-embeddings _
      Î¹'-lc
      (underlying-set-is-set A)

weak-successor : (Î± : Card ğ“¤) â†’ Î£ Î² ê‰ Card ğ“¤ , (Î± < Î²) holds
weak-successor =
 set-trunc-ind _ lem [weak-successor]
 where
  abstract
   lem : (Î± : Card ğ“¤) â†’ is-set (Î£ Î² ê‰ Card ğ“¤ , (Î± < Î²) holds)
   lem Î± =
    Î£-is-set Card-is-set Î» Î² â†’
    props-are-sets (holds-is-prop (Î± < Î²))
