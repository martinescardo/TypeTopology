Andrew Sneap - November 2021

In this file I define the free rationals. They are rationals they may
not be in the lowest terms, with (a , b) : â„¤ Ã— â„• were â„¤ is the
numerator, and b represents a denominator of b+1, ruling out the
possibility of a zero-denominator.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan renaming (_+_ to _âˆ”_)

open import Integers.Abs
open import Integers.HCF
open import Integers.Multiplication
open import Integers.Order
open import Integers.Type
open import Naturals.Division
open import Naturals.HCF
open import Naturals.Multiplication renaming (_*_ to _â„•*_)
open import Naturals.Properties
open import TypeTopology.SigmaDiscreteAndTotallySeparated
open import UF.Base hiding (_â‰ˆ_)
open import UF.DiscreteAndSeparated
open import UF.Sets
open import UF.Subsingletons

module Rationals.Fractions where

ğ”½ : ğ“¤â‚€ Ì‡
ğ”½ = â„¤ Ã— â„•

is-in-lowest-terms : ğ”½ â†’ ğ“¤â‚€ Ì‡
is-in-lowest-terms (x , y) = coprime' (abs x) (succ y)

is-in-lowest-terms' : ğ”½ â†’ ğ“¤â‚€ Ì‡
is-in-lowest-terms' (x , y) = coprime (abs x) (succ y)

denom-zero-lt : (x : â„¤) â†’ is-in-lowest-terms (x , 0)
denom-zero-lt x = Î³
 where
  I : (d : â„•) â†’ is-common-divisor d (abs x) 1 â†’ d âˆ£ 1
  I d (_ , d-divides-1) = d-divides-1

  II : coprime (abs x) 1
  II = ((1-divides-all (abs x)) , 1-divides-all 1) , I

  Î³ : coprime' (abs x) 1
  Î³ = coprime-to-coprime' (abs x) 1 II

is-in-lowest-terms-is-prop : (q : ğ”½) â†’ is-prop (is-in-lowest-terms q)
is-in-lowest-terms-is-prop (x , y) = coprime'-is-prop (abs x) (succ y)

ğ”½-is-discrete : is-discrete ğ”½
ğ”½-is-discrete = Î£-is-discrete â„¤-is-discrete (Î» _ â†’ â„•-is-discrete)

ğ”½-is-set : is-set ğ”½
ğ”½-is-set = discrete-types-are-sets ğ”½-is-discrete

_â‰ˆ_ : (p q : ğ”½) â†’ ğ“¤â‚€ Ì‡
(x , a) â‰ˆ (y , b) = x * pos (succ b) ï¼ y * pos (succ a)

â‰ˆ-refl : (q : ğ”½) â†’ q â‰ˆ q
â‰ˆ-refl q = refl

â‰ˆ-sym : (p q : ğ”½) â†’ p â‰ˆ q â†’ q â‰ˆ p
â‰ˆ-sym p q e = e â»Â¹

â‰ˆ-trans : (p q r : ğ”½) â†’ p â‰ˆ q â†’ q â‰ˆ r â†’ p â‰ˆ r
â‰ˆ-trans (x , a) (y , b) (z , c) eâ‚ eâ‚‚ = conclusion
 where
  a' = pos (succ a)
  b' = pos (succ b)
  c' = pos (succ c)

  I : b' * (x * c') ï¼ b' * (z * a')
  I = b' * (x * c') ï¼âŸ¨ â„¤*-assoc b' x c' â»Â¹           âŸ©
      b' * x * c'   ï¼âŸ¨ ap (_* c') (â„¤*-comm b' x)     âŸ©
      x * b' * c'   ï¼âŸ¨ ap (_* c') eâ‚                 âŸ©
      y * a' * c'   ï¼âŸ¨ ap (_* c') (â„¤*-comm y a')     âŸ©
      a' * y * c'   ï¼âŸ¨ â„¤*-assoc a' y c'              âŸ©
      a' * (y * c') ï¼âŸ¨ ap (a' *_) eâ‚‚                 âŸ©
      a' * (z * b') ï¼âŸ¨ â„¤-mult-rearrangement' z b' a' âŸ©
      b' * (z * a') âˆ

  conclusion : (x , a) â‰ˆ (z , c)
  conclusion = â„¤-mult-left-cancellable (x * c') (z * a') b' id I

equiv-with-lowest-terms-is-equal : (a b : ğ”½)
                                 â†’ a â‰ˆ b
                                 â†’ is-in-lowest-terms a
                                 â†’ is-in-lowest-terms b
                                 â†’ a ï¼ b
equiv-with-lowest-terms-is-equal (x , a) (y , b) e ltâ‚ ltâ‚‚ = Î³
 where
  Î± : coprime (abs x) (succ a)
  Î± = coprime'-to-coprime (abs x) (succ a) ltâ‚

  Î² : coprime (abs y) (succ b)
  Î² = coprime'-to-coprime (abs y) (succ b) ltâ‚‚

  Î´ : abs x â„•* succ b ï¼ abs y â„•* succ a
  Î´ = abs x â„•* abs (pos (succ b)) ï¼âŸ¨ abs-over-mult x (pos (succ b)) â»Â¹ âŸ©
      abs (x * pos (succ b))      ï¼âŸ¨ ap abs e                          âŸ©
      abs (y * pos (succ a))      ï¼âŸ¨ abs-over-mult y (pos (succ a))    âŸ©
      abs y â„•* abs (pos (succ a)) âˆ

  I : succ a â„•* abs y ï¼ abs x â„•* succ b
  I = succ a â„•* abs y ï¼âŸ¨ mult-commutativity (succ a) (abs y) âŸ©
      abs y â„•* succ a ï¼âŸ¨ Î´ â»Â¹                                âŸ©
      abs x â„•* succ b âˆ

  II : (succ a) âˆ£ (abs x) â„•* (succ b)
  II = abs y , I

  III : succ b â„•* abs x ï¼ abs y â„•* succ a
  III = succ b â„•* abs x ï¼âŸ¨ mult-commutativity (succ b) (abs x) âŸ©
        abs x â„•* succ b ï¼âŸ¨ Î´                                   âŸ©
        abs y â„•* succ a âˆ

  IV : succ b âˆ£ abs y â„•* succ a
  IV = abs x , III

  V : coprime (succ a) (abs x)
  V = hcf-comm (abs x) (succ a) 1 Î±

  VI : coprime (succ b) (abs y)
  VI = hcf-comm (abs y) (succ b) 1 Î²

  a-divides-b : succ a âˆ£ succ b
  a-divides-b = coprime-with-division (succ a) (abs x) (succ b) V II

  b-divides-a : succ b âˆ£ succ a
  b-divides-a = coprime-with-division (succ b) (abs y) (succ a) VI IV

  Î³â‚ : a ï¼ b
  Î³â‚ = succ-lc (âˆ£-anti (succ a) (succ b) a-divides-b b-divides-a)

  e'' : x * pos (succ a) ï¼ y * pos (succ a)
  e'' = x * pos (succ a) ï¼âŸ¨ ap (x *_) (ap pos (ap succ Î³â‚)) âŸ©
        x * pos (succ b) ï¼âŸ¨ e                               âŸ©
        y * pos (succ a) âˆ

  Î³â‚‚ : x ï¼ y
  Î³â‚‚ = â„¤-mult-right-cancellable x y (pos (succ a)) id e''

  Î³ : x , a ï¼ y , b
  Î³ = to-Ã—-ï¼ Î³â‚‚ Î³â‚

open import Notation.CanonicalMap

â„¤-to-ğ”½ : â„¤ â†’ ğ”½
â„¤-to-ğ”½ z = z , 0

instance
 canonical-map-â„¤-to-ğ”½ : Canonical-Map â„¤ ğ”½
 Î¹ {{canonical-map-â„¤-to-ğ”½}} = â„¤-to-ğ”½

â„•-to-ğ”½ : â„• â†’ ğ”½
â„•-to-ğ”½ n = Î¹ (Î¹ n)

instance
 canonical-map-â„•-to-ğ”½ : Canonical-Map â„• ğ”½
 Î¹ {{canonical-map-â„•-to-ğ”½}} = â„•-to-ğ”½

\end{code}
