Tom de Jong, late February - early March 2022.
Refactored slightly on 26 June 2024.

We consider sup-complete dcpos. Of course, every sup-complete poset is a dcpo,
but because the basic object of our domain-theoretic development is a dcpo, the
formalization is structured around dcpos that are additionally sup-complete.

The main point of this file is to show that we can extend families into a
sup-complete dcpo to directed families.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc

module DomainTheory.Basics.SupComplete
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe)
       where

open PropositionalTruncation pt hiding (_âˆ¨_)

open import MLTT.List

open import UF.Equiv
open import UF.EquivalenceExamples

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥

\end{code}

We first define, using a record for convenience, when a dcpo additionally has
all (small) suprema.

\begin{code}

module _
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where
 record is-sup-complete : ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡  where
  field
   â‹ : {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©) â†’ âŸ¨ ğ““ âŸ©
   â‹-is-sup : {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©) â†’ is-sup (underlying-order ğ““) (â‹ Î±) Î±

  â‹-is-upperbound : {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©)
                  â†’ is-upperbound (underlying-order ğ““) (â‹ Î±) Î±
  â‹-is-upperbound Î± = sup-is-upperbound (underlying-order ğ““) (â‹-is-sup Î±)

  â‹-is-lowerbound-of-upperbounds : {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©)
                                 â†’ is-lowerbound-of-upperbounds
                                    (underlying-order ğ““) (â‹ Î±) Î±
  â‹-is-lowerbound-of-upperbounds Î± =
   sup-is-lowerbound-of-upperbounds (underlying-order ğ““) (â‹-is-sup Î±)

\end{code}

In particular, we get finite joins, which we first define.

\begin{code}

module _
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where

 âˆ¨-family : (x y : âŸ¨ ğ““ âŸ©) â†’ ğŸ™{ğ“¥} + ğŸ™{ğ“¥} â†’ âŸ¨ ğ““ âŸ©
 âˆ¨-family x y (inl _) = x
 âˆ¨-family x y (inr _) = y

 record has-finite-joins : ğ“¤ âŠ” ğ“£ âŠ” ğ“¥ Ì‡  where
  field
   âŠ¥ : âŸ¨ ğ““ âŸ©
   âŠ¥-is-least : is-least (underlying-order ğ““) âŠ¥
   _âˆ¨_ : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ©
   âˆ¨-is-sup : (x y : âŸ¨ ğ““ âŸ©)
            â†’ is-sup (underlying-order ğ““) (x âˆ¨ y) (âˆ¨-family x y)

  infix 100 _âˆ¨_

  âˆ¨-is-upperboundâ‚ : {x y : âŸ¨ ğ““ âŸ©} â†’ x âŠ‘âŸ¨ ğ““ âŸ© x âˆ¨ y
  âˆ¨-is-upperboundâ‚ {x} {y} = prâ‚ (âˆ¨-is-sup x y) (inl â‹†)

  âˆ¨-is-upperboundâ‚‚ : {x y : âŸ¨ ğ““ âŸ©} â†’ y âŠ‘âŸ¨ ğ““ âŸ© x âˆ¨ y
  âˆ¨-is-upperboundâ‚‚ {x} {y} = prâ‚ (âˆ¨-is-sup x y) (inr â‹†)

  âˆ¨-is-lowerbound-of-upperbounds : {x y z : âŸ¨ ğ““ âŸ©}
                                 â†’ x âŠ‘âŸ¨ ğ““ âŸ© z â†’ y âŠ‘âŸ¨ ğ““ âŸ© z
                                 â†’ x âˆ¨ y âŠ‘âŸ¨ ğ““ âŸ© z
  âˆ¨-is-lowerbound-of-upperbounds {x} {y} {z} u v = prâ‚‚ (âˆ¨-is-sup x y) z Î³
   where
    Î³ : is-upperbound (underlying-order ğ““) z (âˆ¨-family x y)
    Î³ (inl _) = u
    Î³ (inr _) = v

sup-complete-dcpo-has-finite-joins : (ğ““ : DCPO {ğ“¤} {ğ“£})
                                   â†’ is-sup-complete ğ““
                                   â†’ has-finite-joins ğ““
sup-complete-dcpo-has-finite-joins ğ““ sc =
 record { âŠ¥ = â‹ ğŸ˜-elim ;
          âŠ¥-is-least = Î» x â†’ â‹-is-lowerbound-of-upperbounds ğŸ˜-elim x ğŸ˜-induction ;
          _âˆ¨_ = Î» x y â†’ â‹ (âˆ¨-family ğ““ x y);
          âˆ¨-is-sup = Î» x y â†’ â‹-is-sup (âˆ¨-family ğ““ x y)
        }
  where
   open is-sup-complete sc

\end{code}

The converse holds as well: if a dcpo has finite joins then it is sup-complete.
This is because, by taking finite joins, we can replace an arbitrary family by
one that is directed and which has the same supremum.
This an important separate fact that we prove now.

\begin{code}

module make-family-directed
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (ğ““-has-finite-joins : has-finite-joins ğ““)
       where

 open has-finite-joins ğ““-has-finite-joins

 module _
         {I : ğ“¦ Ì‡ }
         (Î± : I â†’ âŸ¨ ğ““ âŸ©)
        where

  directify : List I â†’ âŸ¨ ğ““ âŸ©
  directify []      = âŠ¥
  directify (x âˆ· l) = Î± x âˆ¨ directify l

  directify-is-inhabited : âˆ¥ domain directify âˆ¥
  directify-is-inhabited = âˆ£ [] âˆ£

  ++-is-upperboundâ‚ : (l k : List I) â†’ directify l âŠ‘âŸ¨ ğ““ âŸ© directify (l ++ k)
  ++-is-upperboundâ‚ []      k = âŠ¥-is-least (directify ([] ++ k))
  ++-is-upperboundâ‚ (i âˆ· l) k =
   âˆ¨-is-lowerbound-of-upperbounds âˆ¨-is-upperboundâ‚
    (directify l              âŠ‘âŸ¨ ğ““ âŸ©[ ++-is-upperboundâ‚ l k ]
     directify (l ++ k)       âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚‚ ]
     Î± i âˆ¨ directify (l ++ k) âˆâŸ¨ ğ““ âŸ©)

  ++-is-upperboundâ‚‚ : (l k : List I) â†’ directify k âŠ‘âŸ¨ ğ““ âŸ© directify (l ++ k)
  ++-is-upperboundâ‚‚ []      k = reflexivity ğ““ (directify k)
  ++-is-upperboundâ‚‚ (i âˆ· l) k =
   directify k              âŠ‘âŸ¨ ğ““ âŸ©[ ++-is-upperboundâ‚‚ l k ]
   directify (l ++ k)       âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚‚ ]
   Î± i âˆ¨ directify (l ++ k) âˆâŸ¨ ğ““ âŸ©

  ++-is-lowerbound-of-upperbounds : (l k : List I) (x : âŸ¨ ğ““ âŸ©)
                                  â†’ directify l âŠ‘âŸ¨ ğ““ âŸ© x
                                  â†’ directify k âŠ‘âŸ¨ ğ““ âŸ© x
                                  â†’ directify (l ++ k) âŠ‘âŸ¨ ğ““ âŸ© x
  ++-is-lowerbound-of-upperbounds []      k x  u v = v
  ++-is-lowerbound-of-upperbounds (i âˆ· l) k x u v =
   âˆ¨-is-lowerbound-of-upperbounds â¦…1â¦† â¦…2â¦†
    where
     â¦…1â¦† = Î± i              âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚ ]
          Î± i âˆ¨ directify l âŠ‘âŸ¨ ğ““ âŸ©[ u ]
          x                 âˆâŸ¨ ğ““ âŸ©
     â¦…2â¦† : directify (l ++ k) âŠ‘âŸ¨ ğ““ âŸ© x
     â¦…2â¦† = ++-is-lowerbound-of-upperbounds l k x â¦…2'â¦† v
      where
       â¦…2'â¦† = directify l      âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚‚ ]
             Î± i âˆ¨ directify l âŠ‘âŸ¨ ğ““ âŸ©[ u ]
             x                 âˆâŸ¨ ğ““ âŸ©

  ++-is-binary-sup : (l k : List I)
                   â†’ is-sup (underlying-order ğ““) (directify (l ++ k))
                            (âˆ¨-family ğ““ (directify l) (directify k))
  ++-is-binary-sup l k = â¦…1â¦† , â¦…2â¦†
   where
    â¦…1â¦† : (b : ğŸ™ + ğŸ™)
        â†’ âˆ¨-family ğ““ (directify l) (directify k) b âŠ‘âŸ¨ ğ““ âŸ© directify (l ++ k)
    â¦…1â¦† (inl _) = ++-is-upperboundâ‚ l k
    â¦…1â¦† (inr _) = ++-is-upperboundâ‚‚ l k
    â¦…2â¦† : is-lowerbound-of-upperbounds (underlying-order ğ““)
           (directify (l ++ k)) (âˆ¨-family ğ““ (directify l) (directify k))
    â¦…2â¦† x x-ub = ++-is-lowerbound-of-upperbounds l k x
                  (x-ub (inl â‹†)) (x-ub (inr â‹†))

  directify-is-semidirected : is-Semidirected ğ““ directify
  directify-is-semidirected l k =
   âˆ£ (l ++ k) , ++-is-upperboundâ‚ l k , ++-is-upperboundâ‚‚ l k âˆ£

  directify-is-directed : is-Directed ğ““ directify
  directify-is-directed = (directify-is-inhabited , directify-is-semidirected)

  directify-upperbound : (x : âŸ¨ ğ““ âŸ©) â†’ is-upperbound (underlying-order ğ““) x Î±
                       â†’ is-upperbound (underlying-order ğ““) x directify
  directify-upperbound x x-is-ub []      = âŠ¥-is-least x
  directify-upperbound x x-is-ub (i âˆ· l) =
   âˆ¨-is-lowerbound-of-upperbounds (x-is-ub i) (directify-upperbound x x-is-ub l)

  directify-lowerbound-of-upperbounds :
     (x : âŸ¨ ğ““ âŸ©)
   â†’ is-lowerbound-of-upperbounds (underlying-order ğ““) x Î±
   â†’ is-lowerbound-of-upperbounds (underlying-order ğ““) x directify
  directify-lowerbound-of-upperbounds x x-is-lb y y-is-ub = x-is-lb y y-is-ub'
   where
    y-is-ub' : (i : I) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© y
    y-is-ub' i = Î± i             âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚ ]
                 Î± i âˆ¨ âŠ¥         âŠ‘âŸ¨ ğ““ âŸ©[ reflexivity ğ““ _  ]
                 directify [ i ] âŠ‘âŸ¨ ğ““ âŸ©[ y-is-ub [ i ]    ]
                 y               âˆâŸ¨ ğ““ âŸ©

  directify-sup : (x : âŸ¨ ğ““ âŸ©) â†’ is-sup (underlying-order ğ““) x Î±
                â†’ is-sup (underlying-order ğ““) x directify
  directify-sup x (x-is-ub , x-is-lb-of-ubs) =
   ( directify-upperbound x x-is-ub
   , directify-lowerbound-of-upperbounds x x-is-lb-of-ubs)

  directify-sup' : (x : âŸ¨ ğ““ âŸ©) â†’ is-sup (underlying-order ğ““) x directify
                 â†’ is-sup (underlying-order ğ““) x Î±
  directify-sup' x (x-is-ub , x-is-lb-of-ubs) =
   ( (Î» i â†’ Î± i              âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚ ]
             directify [ i ] âŠ‘âŸ¨ ğ““ âŸ©[ x-is-ub [ i ] ]
             x               âˆâŸ¨ ğ““ âŸ©)
   , (Î» y y-is-ub â†’ x-is-lb-of-ubs y (directify-upperbound y y-is-ub)))

\end{code}

Moreover, if each of the Î±áµ¢'s are compact, then so is every element in the
directified family, because taking finite joins preserves compactness.

\begin{code}

  directify-is-compact : ((i : I) â†’ is-compact ğ““ (Î± i))
                       â†’ (l : List I) â†’ is-compact ğ““ (directify l)
  directify-is-compact Î±s-are-compact []      =
   âŠ¥-is-compact (ğ““ , âŠ¥ , âŠ¥-is-least)
  directify-is-compact Î±s-are-compact (i âˆ· l) =
   binary-join-is-compact ğ““ âˆ¨-is-upperboundâ‚ âˆ¨-is-upperboundâ‚‚
    (Î» d â†’ âˆ¨-is-lowerbound-of-upperbounds) (Î±s-are-compact i) IH
    where
     IH : is-compact ğ““ (directify l)
     IH = directify-is-compact Î±s-are-compact l

\end{code}

When constructing small compact bases for exponentials, it turns out that it is
convenient to consider a variation: we consider the family of elements Î±áµ¢ less
than some given element x, and the corresponding family of lists l such that
directify l is less than x.

\begin{code}

  module _
          {x : âŸ¨ ğ““ âŸ©}
         where

   â†“-family : (Î£ i ê‰ I , Î± i âŠ‘âŸ¨ ğ““ âŸ© x) â†’ âŸ¨ ğ““ âŸ©
   â†“-family = Î± âˆ˜ prâ‚

   directify-â†“ : (Î£ l ê‰ List I , directify l âŠ‘âŸ¨ ğ““ âŸ© x) â†’ âŸ¨ ğ““ âŸ©
   directify-â†“ = directify âˆ˜ prâ‚

   directify-â†“-is-inhabited : âˆ¥ domain directify-â†“ âˆ¥
   directify-â†“-is-inhabited = âˆ£ [] , âŠ¥-is-least x âˆ£

   directify-â†“-is-semidirected : is-Semidirected ğ““ directify-â†“
   directify-â†“-is-semidirected (l , l-below-x) (k , k-below-x) =
    âˆ£ ((l ++ k) , ++-is-lowerbound-of-upperbounds l k x l-below-x k-below-x)
                , (++-is-upperboundâ‚ l k) , (++-is-upperboundâ‚‚ l k) âˆ£


   directify-â†“-is-directed : is-Directed ğ““ directify-â†“
   directify-â†“-is-directed =
    (directify-â†“-is-inhabited , directify-â†“-is-semidirected)

   directify-â†“-upperbound : is-upperbound (underlying-order ğ““) x directify-â†“
   directify-â†“-upperbound = prâ‚‚

   directify-â†“-sup : is-sup (underlying-order ğ““) x â†“-family
                   â†’ is-sup (underlying-order ğ““) x directify-â†“
   directify-â†“-sup (x-ub , x-lb-of-ubs) = (directify-â†“-upperbound , Î³)
    where
     Î³ : is-lowerbound-of-upperbounds (underlying-order ğ““) x directify-â†“
     Î³ y y-is-ub = x-lb-of-ubs y claim
      where
       claim : is-upperbound (underlying-order ğ““) y â†“-family
       claim (i , Î±áµ¢-below-x) =
        Î± i                     âŠ‘âŸ¨ ğ““ âŸ©[ âˆ¨-is-upperboundâ‚ ]
        directify-â†“ ([ i ] , u) âŠ‘âŸ¨ ğ““ âŸ©[ y-is-ub ([ i ] , u) ]
        y                       âˆâŸ¨ ğ““ âŸ©
         where
          u : Î± i âˆ¨ âŠ¥ âŠ‘âŸ¨ ğ““ âŸ© x
          u = âˆ¨-is-lowerbound-of-upperbounds Î±áµ¢-below-x (âŠ¥-is-least x)

   directify-â†“-is-compact : ((i : I) â†’ is-compact ğ““ (Î± i))
                          â†’ (j : domain directify-â†“)
                          â†’ is-compact ğ““ (directify-â†“ j)
   directify-â†“-is-compact Î±s-are-compact j =
    directify-is-compact Î±s-are-compact (prâ‚ j)

\end{code}

Finally if the dcpo is locally small, then the family directify-â†“ can be indexed
by a small type (provided the original family was indexed by a small type).

\begin{code}

 module _
         (ğ““-is-locally-small : is-locally-small ğ““)
         {I : ğ“¥ Ì‡ }
         (Î± : I â†’ âŸ¨ ğ““ âŸ©)
        where

  open is-locally-small ğ““-is-locally-small

  directify-â†“-small : (x : âŸ¨ ğ““ âŸ©) â†’ (Î£ l ê‰ List I , directify Î± l âŠ‘â‚› x) â†’ âŸ¨ ğ““ âŸ©
  directify-â†“-small x = directify Î± âˆ˜ prâ‚

  module _
          {x : âŸ¨ ğ““ âŸ©}
         where

   directify-â†“-small-â‰ƒ : domain (directify-â†“ Î±) â‰ƒ domain (directify-â†“-small x)
   directify-â†“-small-â‰ƒ =
    Î£-cong (Î» l â†’ â‰ƒ-sym âŠ‘â‚›-â‰ƒ-âŠ‘)

   directify-â†“-small-sup : is-sup (underlying-order ğ““) x (â†“-family Î±)
                         â†’ is-sup (underlying-order ğ““) x (directify-â†“-small x)
   directify-â†“-small-sup x-is-sup =
    reindexed-family-sup ğ““ directify-â†“-small-â‰ƒ
     (directify-â†“ Î±) x (directify-â†“-sup Î± x-is-sup)

   directify-â†“-small-is-directed : is-Directed ğ““ (directify-â†“-small x)
   directify-â†“-small-is-directed =
    reindexed-family-is-directed ğ““ directify-â†“-small-â‰ƒ
     (directify-â†“ Î±) (directify-â†“-is-directed Î±)

\end{code}

As announced, we get that dcpos with finite joins are sup-complete.

\begin{code}

dcpo-with-finite-joins-is-sup-complete : (ğ““ : DCPO {ğ“¤} {ğ“£})
                                       â†’ has-finite-joins ğ““
                                       â†’ is-sup-complete ğ““
dcpo-with-finite-joins-is-sup-complete ğ““ h =
 record {
  â‹ = sup ;
  â‹-is-sup = Î» Î± â†’ directify-sup' ğ““ h Î± (sup Î±)
                                  (âˆ-is-sup ğ““ (directify-is-directed ğ““ h Î±))
 }
  where
   open has-finite-joins h
   open make-family-directed
   sup : {I : ğ“¥ Ì‡} â†’ (I â†’ âŸ¨ ğ““ âŸ©) â†’ âŸ¨ ğ““ âŸ©
   sup {I} Î± = âˆ ğ““ (directify-is-directed ğ““ h Î±)

\end{code}

Finally, we re-export the directify constructions via this module for use in
other files.

\begin{code}

module sup-complete-dcpo
        (ğ““ : DCPO {ğ“¤} {ğ“£'})
        (ğ““-is-sup-complete : is-sup-complete ğ““)
       where

 open is-sup-complete ğ““-is-sup-complete
 open make-family-directed
       ğ““ (sup-complete-dcpo-has-finite-joins ğ““ ğ““-is-sup-complete)
      public

\end{code}
