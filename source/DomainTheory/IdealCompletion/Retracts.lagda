Tom de Jong, 31 January - 4 February 2022.
Material moved to separate file on 11 June 2022.

Suppose we are given a continuous dcpo D with small basis Î² : B â†’ D. We show
that D is a continuous retract of the ideal completion Idl(B,âŠ‘) which is an
algebraic dpco with a small compact basis. In fact, we even have an
embedding-projection pair.

We can also consider Idl(B,â‰ª) and we get a continuous dcpo that is isomorphic to
D, but notice that Idl(B,â‰ª) : ğ“¥-DCPO {ğ“¥ âº} {ğ“¥}, while D : ğ“¥-DCPO {ğ“¤} {ğ“£}. Thus,
a dcpo with a small basis can essentially be parameterized by a single universe
with a large carrier. Moreover, every dcpo with a small basis can be presented
using ideals.(â€ )

Similarly, an algebraic dcpo with small compact basis Î² : B â†’ D is isomorphic to
Idl(B,âŠ‘) and analogous remarks apply in this case.

(â€ ) Here, and similarly elsewhere, we really consider Idl(B,â‰ªâ‚›), where â‰ªâ‚› is
    equivalent to â‰ª on B, but takes values in ğ“¥.

    The size conditions on B and the order are similar to those in Corollary
    2.17 of "Continuous categories and exponentiable toposes" by Johnstone and
    Joyal.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan hiding (J)

open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons

module DomainTheory.IdealCompletion.Retracts
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (ğ“¥ : Universe) -- universe where the index types for directedness
                       -- completeness live
       where

open import UF.Equiv
open import UF.Retracts
open import UF.Powerset

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¥

open import DomainTheory.IdealCompletion.IdealCompletion pt fe pe ğ“¥
open import DomainTheory.IdealCompletion.Properties pt fe pe ğ“¥

open PropositionalTruncation pt

\end{code}

We will consider ideal completions of:
(1) a small basis ordered by âŠ‘;
(2) a small basis ordered by â‰ª;
(3) a small compact basis order by âŠ‘.

All of these share some common ingredients, which we capture in the following
module so that we can conveniently reuse them.

\begin{code}

module Idl-retract-common
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        {B : ğ“¥ Ì‡ }
        (Î² : B â†’ âŸ¨ ğ““ âŸ©)
        (Î²-is-small-basis : is-small-basis ğ““ Î²)
       where

 open is-small-basis Î²-is-small-basis

\end{code}

However we choose to order the basis, the map that takes an element x : D to the
subset {b : B âˆ£ b â‰ª x} is instrumental. We show that this assignment is
Scott continuous here.

\begin{code}

 â†¡á´®-subset : (x : âŸ¨ ğ““ âŸ©) â†’ ğ“Ÿ B
 â†¡á´®-subset x = (Î» b â†’ (b â‰ªá´®â‚› x , â‰ªá´®â‚›-is-prop-valued))

 â†¡á´®-subset-is-inhabited : (x : âŸ¨ ğ““ âŸ©) â†’ âˆ¥ ğ•‹ (â†¡á´®-subset x) âˆ¥
 â†¡á´®-subset-is-inhabited x = inhabited-if-Directed ğ““ (â†¡-inclusionâ‚› x) (â†¡á´®â‚›-is-directed x)

 â†¡á´®-is-monotone : (x y : âŸ¨ ğ““ âŸ©) â†’ x âŠ‘âŸ¨ ğ““ âŸ© y â†’ â†¡á´®-subset x âŠ† â†¡á´®-subset y
 â†¡á´®-is-monotone x y x-below-y b b-way-below-x =
  â‰ªá´®-to-â‰ªá´®â‚› (â‰ª-âŠ‘-to-â‰ª ğ““ (â‰ªá´®â‚›-to-â‰ªá´® b-way-below-x) x-below-y)

 â†¡á´®-is-continuous : {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} (Î´ : is-Directed ğ““ Î±)
                  â†’ is-sup _âŠ†_ (â†¡á´®-subset (âˆ ğ““ Î´)) (â†¡á´®-subset âˆ˜ Î±)
 â†¡á´®-is-continuous {I} {Î±} Î´ = (ub , lb-of-ubs)
  where
   ub : is-upperbound _âŠ†_ (â†¡á´®-subset (âˆ ğ““ Î´)) (â†¡á´®-subset âˆ˜ Î±)
   ub i b b-way-below-Î±áµ¢ =
    â‰ªá´®-to-â‰ªá´®â‚› (â‰ª-âŠ‘-to-â‰ª ğ““ (â‰ªá´®â‚›-to-â‰ªá´® b-way-below-Î±áµ¢) (âˆ-is-upperbound ğ““ Î´ i))
   lb-of-ubs : is-lowerbound-of-upperbounds _âŠ†_
                (â†¡á´®-subset (âˆ ğ““ Î´)) (â†¡á´®-subset âˆ˜ Î±)
   lb-of-ubs S S-is-ub b b-way-below-âˆÎ± =
    âˆ¥âˆ¥-rec (âˆˆ-is-prop S b) lemmaâ‚ interpolant
     where
      interpolant : âˆƒ c ê‰ B , (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c) Ã— (Î² c â‰ªâŸ¨ ğ““ âŸ© (âˆ ğ““ Î´))
      interpolant = â‰ª-unary-interpolation-basis ğ““ Î² Î²-is-small-basis
                     (â‰ªá´®â‚›-to-â‰ªá´® b-way-below-âˆÎ±)
      lemmaâ‚ : (Î£ c ê‰ B , (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c) Ã— (Î² c â‰ªâŸ¨ ğ““ âŸ© (âˆ ğ““ Î´)))
             â†’ b âˆˆ S
      lemmaâ‚ (c , b-way-below-c , c-way-below-âˆÎ±) =
       âˆ¥âˆ¥-rec (âˆˆ-is-prop S b) lemmaâ‚‚ wb-consequence
        where
         wb-consequence : âˆƒ i ê‰ I , Î² c âŠ‘âŸ¨ ğ““ âŸ© Î± i
         wb-consequence = c-way-below-âˆÎ± I Î± Î´ (reflexivity ğ““ (âˆ ğ““ Î´))
         lemmaâ‚‚ : (Î£ i ê‰ I , Î² c âŠ‘âŸ¨ ğ““ âŸ© Î± i) â†’ b âˆˆ S
         lemmaâ‚‚ (i , c-below-Î±áµ¢) =
          S-is-ub i b (â‰ªá´®-to-â‰ªá´®â‚› (â‰ª-âŠ‘-to-â‰ª ğ““ b-way-below-c c-below-Î±áµ¢))

\end{code}

We show that the supremum of {b : B âˆ£ b â‰ª x} equals x.

\begin{code}

 âˆ-of-directed-subset : (I : ğ“Ÿ B)
                      â†’ is-Directed ğ““ (Î² âˆ˜ ğ•‹-to-carrier I)
                      â†’ âŸ¨ ğ““ âŸ©
 âˆ-of-directed-subset I Î´ = âˆ ğ““ Î´

 â†¡á´®-section-of-âˆ : (x : âŸ¨ ğ““ âŸ©) (Î´ : is-Directed ğ““ (â†¡-inclusionâ‚› x))
                 â†’ âˆ-of-directed-subset (â†¡á´®-subset x) Î´ ï¼ x
 â†¡á´®-section-of-âˆ x Î´ = âˆ ğ““ Î´ ï¼âŸ¨ â¦…1â¦† âŸ©
                       âˆ ğ““ Îµ ï¼âŸ¨ â¦…2â¦† âŸ©
                       x     âˆ
  where
   Îµ : is-Directed ğ““ (â†¡-inclusionâ‚› x)
   Îµ = â†¡á´®â‚›-is-directed x
   â¦…1â¦† = âˆ-independent-of-directedness-witness ğ““ Î´ Îµ
   â¦…2â¦† = â†¡á´®â‚›-âˆ-ï¼ x

\end{code}

We present some criteria (which will find application later) for the composition
of â†¡á´®-subset and âˆ to be a deflation, inflation and retraction-section.

\begin{code}

 â†¡á´®-âˆ-deflation : (I : ğ“Ÿ B) {Î´ : is-Directed ğ““ (Î² âˆ˜ ğ•‹-to-carrier I)}
                â†’ ((b c : B) â†’ Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² c â†’ c âˆˆ I â†’ b âˆˆ I)
                â†’ â†¡á´®-subset (âˆ-of-directed-subset I Î´) âŠ† I
 â†¡á´®-âˆ-deflation I {Î´} I-lowerset b b-way-below-sup =
  âˆ¥âˆ¥-rec (âˆˆ-is-prop I b) lemma claim
   where
    claim : âˆƒ i ê‰ ğ•‹ I , Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² ((ğ•‹-to-carrier I) i)
    claim = â‰ªá´®â‚›-to-â‰ªá´® b-way-below-sup (ğ•‹ I) (Î² âˆ˜ ğ•‹-to-carrier I) Î´
             (reflexivity ğ““ (âˆ ğ““ Î´))
    lemma : (Î£ i ê‰ ğ•‹ I , Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² ((ğ•‹-to-carrier I) i))
          â†’ bÂ âˆˆ I
    lemma ((c , c-in-I) , b-below-c) = I-lowerset b c b-below-c c-in-I

 â†¡á´®-âˆ-inflation : (I : ğ“Ÿ B) {Î´ : is-Directed ğ““ (Î² âˆ˜ ğ•‹-to-carrier I)}
                â†’ ((b : B) â†’ b âˆˆ I â†’ âˆƒ c ê‰ B , (c âˆˆ I) Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c))
                â†’ I âŠ† â†¡á´®-subset (âˆ-of-directed-subset I Î´)
 â†¡á´®-âˆ-inflation I {Î´} I-rounded b b-in-I = âˆ¥âˆ¥-rec â‰ªá´®â‚›-is-prop-valued lemma claim
  where
   claim : âˆƒ c ê‰ B , (c âˆˆ I) Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c)
   claim = I-rounded b b-in-I
   lemma : (Î£ c ê‰ B , (c âˆˆ I) Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c))
         â†’ b â‰ªá´®â‚› âˆ-of-directed-subset I Î´
   lemma (c , c-in-I , b-way-below-c) =
    â‰ªá´®-to-â‰ªá´®â‚› (â‰ª-âŠ‘-to-â‰ª ğ““ b-way-below-c (âˆ-is-upperbound ğ““ Î´ (c , c-in-I)))

 âˆ-â†¡á´®-retract : (I : ğ“Ÿ B) {Î´ : is-Directed ğ““ (Î² âˆ˜ ğ•‹-to-carrier I)}
              â†’ ((b c : B) â†’ Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² c â†’ c âˆˆ I â†’ b âˆˆ I)
              â†’ ((b : B) â†’ b âˆˆ I â†’ âˆƒ c ê‰ B , (c âˆˆ I) Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c))
              â†’ â†¡á´®-subset (âˆ-of-directed-subset I Î´) ï¼ I
 âˆ-â†¡á´®-retract I {Î´} condâ‚ condâ‚‚ =
  subset-extensionality pe fe (â†¡á´®-âˆ-deflation I condâ‚) (â†¡á´®-âˆ-inflation I condâ‚‚)

\end{code}

If we assume the existence of some binary relation (which we think of as an
order) on B, then we can give some convenient criteria for â†¡á´® being a
semidirected and lower-closed.

\begin{code}

 module _
         (_â‰º_ : B â†’ B â†’ ğ“¥ Ì‡ )
        where

  â†¡á´®-lowerset-criterion : (x : âŸ¨ ğ““ âŸ©)
                        â†’ ((b c : B) â†’ b â‰º c â†’ Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² c)
                        â†’ (b c : B) â†’ b â‰º c â†’ c âˆˆ â†¡á´®-subset x â†’ b âˆˆ â†¡á´®-subset x
  â†¡á´®-lowerset-criterion x Î²-mon b c b-below-c c-way-below-x =
   â‰ªá´®-to-â‰ªá´®â‚› (âŠ‘-â‰ª-to-â‰ª ğ““ (Î²-mon b c b-below-c) (â‰ªá´®â‚›-to-â‰ªá´® c-way-below-x))

  â†¡á´®-semidirected-set-criterion : (x : âŸ¨ ğ““ âŸ©)
                                â†’ ((b c : B) â†’ Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c â†’ b â‰º c)
                                â†’ (a b : B) â†’ a âˆˆ â†¡á´®-subset x â†’ b âˆˆ â†¡á´®-subset x
                                â†’ âˆƒ c ê‰ B , c âˆˆ â†¡á´®-subset x Ã— (a â‰º c) Ã— (b â‰º c)
  â†¡á´®-semidirected-set-criterion x Î²-mon a b a-way-below-x b-way-below-x =
   âˆ¥âˆ¥-functor h (â‰ª-binary-interpolation-basis ğ““ Î² Î²-is-small-basis
                 (â‰ªá´®â‚›-to-â‰ªá´® a-way-below-x)
                 (â‰ªá´®â‚›-to-â‰ªá´® b-way-below-x))
    where
     h : (Î£ c ê‰ B , (Î² a â‰ªâŸ¨ ğ““ âŸ© Î² c) Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c) Ã— (Î² c â‰ªâŸ¨ ğ““ âŸ© x))
       â†’ (Î£ c ê‰ B , c âˆˆ â†¡á´®-subset x Ã— (a â‰º c) Ã— (b â‰º c))
     h (c , a-way-below-c , b-way-below-c , c-way-below-x) =
      (c , â‰ªá´®-to-â‰ªá´®â‚› c-way-below-x , Î²-mon a c a-way-below-c
                                   , Î²-mon b c b-way-below-c)

\end{code}

A major application of the theory of rounded ideals is the following: a
continuous dcpo D with a small basis Î² : B â†’ D is a continous retract (in fact,
we have an embedding-projection pair) of an algebraic dcpo, namely of Idl(B,âŠ‘).

\begin{code}

module Idl-continuous-retract-of-algebraic
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        {B : ğ“¥ Ì‡ }
        (Î² : B â†’ âŸ¨ ğ““ âŸ©)
        (Î²-is-small-basis : is-small-basis ğ““ Î²)
       where

 open is-small-basis Î²-is-small-basis
 open is-locally-small (locally-small-if-small-basis ğ““ Î² Î²-is-small-basis)

 _âŠ‘á´®_ : B â†’ B â†’ ğ“¥ Ì‡
 b âŠ‘á´® b' = Î² b âŠ‘â‚› Î² b'

 âŠ‘á´®-â‰ƒ-âŠ‘ : {b b' : B} â†’ (b âŠ‘á´® b') â‰ƒ (Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² b')
 âŠ‘á´®-â‰ƒ-âŠ‘ {b} {b'} = âŠ‘â‚›-â‰ƒ-âŠ‘

 âŠ‘á´®-is-prop-valued : {b b' : B} â†’ is-prop (b âŠ‘á´® b')
 âŠ‘á´®-is-prop-valued = equiv-to-prop âŠ‘á´®-â‰ƒ-âŠ‘ (prop-valuedness ğ““ _ _)

 âŠ‘á´®-is-reflexive : {b : B} â†’ b âŠ‘á´® b
 âŠ‘á´®-is-reflexive = âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒâ»Â¹ (reflexivity ğ““ _)

 âŠ‘á´®-is-transitive : {b b' b'' : B} â†’ b âŠ‘á´® b' â†’ b' âŠ‘á´® b'' â†’ b âŠ‘á´® b''
 âŠ‘á´®-is-transitive u v = âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒâ»Â¹
                         (transitivity ğ““ _ _ _ (âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒ u) (âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒ v))

 open Ideals-of-small-abstract-basis {B} _âŠ‘á´®_
        âŠ‘á´®-is-prop-valued
        (reflexivity-implies-INTâ‚‚ _âŠ‘á´®_ âŠ‘á´®-is-reflexive)
        (reflexivity-implies-INTâ‚€ _âŠ‘á´®_ âŠ‘á´®-is-reflexive)
        âŠ‘á´®-is-transitive
      public
 open Idl-retract-common ğ““ Î² Î²-is-small-basis public
 open Idl-mediating ğ““ Î² âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒ public

 to-Idl : âŸ¨ ğ““ âŸ© â†’ Idl
 to-Idl x = (Bâ‚“ , Bâ‚“-is-lowerset , Bâ‚“-is-directed-set)
  where
   Bâ‚“ : ğ“Ÿ B
   Bâ‚“ = â†¡á´®-subset x
   Bâ‚“-is-lowerset : is-lowerset Bâ‚“
   Bâ‚“-is-lowerset = â†¡á´®-lowerset-criterion _âŠ‘á´®_ x (Î» b c â†’ âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒ)
   Bâ‚“-is-semidirected-set : is-semidirected-set Bâ‚“
   Bâ‚“-is-semidirected-set =
    â†¡á´®-semidirected-set-criterion _âŠ‘á´®_ x
     (Î» b c b-way-below-c â†’ âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒâ»Â¹ (â‰ª-to-âŠ‘ ğ““ b-way-below-c))
   Bâ‚“-is-directed-set : is-directed-set Bâ‚“
   Bâ‚“-is-directed-set = (â†¡á´®-subset-is-inhabited x , Bâ‚“-is-semidirected-set)

 from-Idl : Idl â†’ âŸ¨ ğ““ âŸ©
 from-Idl I = Idl-mediating-map I

 Idl-retract : retract âŸ¨ ğ““ âŸ© of Idl
 Idl-retract = (r , s , Î³)
  where
   s : âŸ¨ ğ““ âŸ© â†’ Idl
   s = to-Idl
   r : Idl â†’ âŸ¨ ğ““ âŸ©
   r = from-Idl
   Î³ : (x : âŸ¨ ğ““ âŸ©) â†’ r (s x) ï¼ x
   Î³ x = â†¡á´®-section-of-âˆ x (Idl-mediating-directed (s x))

 Idl-deflation : (I : Idl) â†’ to-Idl (from-Idl I) âŠ‘âŸ¨ Idl-DCPO âŸ© I
 Idl-deflation ğ•€@(I , I-is-ideal) = â†¡á´®-âˆ-deflation I Î³
  where
   Î³ : (b c : B) â†’ Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² c â†’ c âˆˆ I â†’ b âˆˆ I
   Î³ b c b-below-c c-in-I = ideals-are-lowersets I I-is-ideal b c claim c-in-I
    where
     claim : b âŠ‘á´® c
     claim = âŒœ âŠ‘á´®-â‰ƒ-âŠ‘ âŒâ»Â¹ b-below-c

 to-Idl-is-continuous : is-continuous ğ““ Idl-DCPO to-Idl
 to-Idl-is-continuous I Î± Î´ =
  Idl-sups-from-powerset (to-Idl âˆ˜ Î±) (to-Idl (âˆ ğ““ Î´)) (â†¡á´®-is-continuous Î´)

 from-Idl-is-continuous : is-continuous Idl-DCPO ğ““ from-Idl
 from-Idl-is-continuous = Idl-mediating-map-is-continuous

 Idl-continuous-retract : ğ““ continuous-retract-of Idl-DCPO
 Idl-continuous-retract =
  record
   { s               = to-Idl
   ; r               = from-Idl
   ; s-section-of-r  = retract-condition Idl-retract
   ; s-is-continuous = to-Idl-is-continuous
   ; r-is-continuous = from-Idl-is-continuous
   }

 Idl-embedding-projection-pair : embedding-projection-pair-between ğ““ Idl-DCPO
 Idl-embedding-projection-pair =
  record
    { e               = to-Idl
    ; p               = from-Idl
    ; e-section-of-p  = retract-condition Idl-retract
    ; e-p-deflation   = Idl-deflation
    ; e-is-continuous = to-Idl-is-continuous
    ; p-is-continuous = from-Idl-is-continuous
    }

 Idl-is-algebraic : is-algebraic-dcpo Idl-DCPO
 Idl-is-algebraic = Idl-is-algebraic-dcpo (Î» b â†’ âŠ‘á´®-is-reflexive)

\end{code}

Of course, given a continuous dcpo D with small basis Î² : B â†’ D, we can also
consider Idl(B,â‰ª) which is isomorphic to D.

\begin{code}

module Idl-continuous
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        {B : ğ“¥ Ì‡ }
        (Î² : B â†’ âŸ¨ ğ““ âŸ©)
        (Î²-is-small-basis : is-small-basis ğ““ Î²)
       where

 open is-small-basis Î²-is-small-basis

 _â‰º_ : B â†’ B â†’ ğ“¥ Ì‡
 b â‰º b' = b â‰ªá´®â‚› Î² b'

 â‰º-â‰ƒ-â‰ª : {b b' : B} â†’ (b â‰º b') â‰ƒ (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² b')
 â‰º-â‰ƒ-â‰ª {b} {b'} = â‰ªá´®â‚›-â‰ƒ-â‰ªá´®

 â‰º-is-prop-valued : {b b' : B} â†’ is-prop (b â‰º b')
 â‰º-is-prop-valued = equiv-to-prop â‰º-â‰ƒ-â‰ª (â‰ª-is-prop-valued ğ““)

 â‰º-is-transitive : {b b' b'' : B} â†’ b â‰º b' â†’ b' â‰º b'' â†’ b â‰º b''
 â‰º-is-transitive u v =
  âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹ (â‰ª-is-transitive ğ““ (âŒœ â‰º-â‰ƒ-â‰ª âŒ u) (âŒœ â‰º-â‰ƒ-â‰ª âŒ v))

 â‰º-INTâ‚€ : (b : B) â†’ âˆƒ c ê‰ B , c â‰º b
 â‰º-INTâ‚€ b = âˆ¥âˆ¥-functor h
             (â‰ª-nullary-interpolation-basis ğ““ Î² Î²-is-small-basis (Î² b))
  where
   h : (Î£ c ê‰ B , Î² c â‰ªâŸ¨ ğ““ âŸ© Î² b) â†’ (Î£ c ê‰ B , c â‰º b)
   h (c , c-way-below-b) = (c , âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹ c-way-below-b)

 â‰º-INTâ‚‚ : {bâ‚ bâ‚‚ b : B} â†’ bâ‚ â‰º b â†’ bâ‚‚ â‰º b
        â†’ âˆƒ c ê‰ B , (bâ‚ â‰º c) Ã— (bâ‚‚ â‰º c) Ã— (c â‰º b)
 â‰º-INTâ‚‚ {bâ‚} {bâ‚‚} {b} bâ‚-below-b bâ‚‚-below-b =
  âˆ¥âˆ¥-functor h (â‰ª-binary-interpolation-basis ğ““ Î² Î²-is-small-basis
                (âŒœ â‰º-â‰ƒ-â‰ª âŒ bâ‚-below-b) (âŒœ â‰º-â‰ƒ-â‰ª âŒ bâ‚‚-below-b))
   where
    h : (Î£ c ê‰ B , (Î² bâ‚ â‰ªâŸ¨ ğ““ âŸ© Î² c)
                 Ã— (Î² bâ‚‚ â‰ªâŸ¨ ğ““ âŸ© Î² c)
                 Ã— (Î² c â‰ªâŸ¨ ğ““ âŸ© Î² b))
      â†’ (Î£ c ê‰ B , (bâ‚ â‰º c) Ã— (bâ‚‚ â‰º c) Ã— (c â‰º b))
    h (c , u , v , w) = (c , âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹ u , âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹ v , âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹ w)

 open Ideals-of-small-abstract-basis {B}  _â‰º_
                                     â‰º-is-prop-valued
                                     â‰º-INTâ‚‚
                                     â‰º-INTâ‚€
                                     â‰º-is-transitive

 open Idl-retract-common ğ““ Î² Î²-is-small-basis
 open Idl-mediating ğ““ Î² (â‰ª-to-âŠ‘ ğ““ âˆ˜ âŒœ â‰º-â‰ƒ-â‰ª âŒ)

 to-Idl : âŸ¨ ğ““ âŸ© â†’ Idl
 to-Idl x = (Bâ‚“ , Bâ‚“-is-lowerset , Bâ‚“-is-directed-set)
  where
   Bâ‚“ : ğ“Ÿ B
   Bâ‚“ = â†¡á´®-subset x
   Bâ‚“-is-lowerset : is-lowerset Bâ‚“
   Bâ‚“-is-lowerset = â†¡á´®-lowerset-criterion _â‰º_ x
                     (Î» b c b-below-c â†’ â‰ª-to-âŠ‘ ğ““ (âŒœ â‰º-â‰ƒ-â‰ª âŒ b-below-c))
   Bâ‚“-is-semidirected-set : is-semidirected-set Bâ‚“
   Bâ‚“-is-semidirected-set = â†¡á´®-semidirected-set-criterion _â‰º_ x
                             (Î» b c â†’ âŒœ â‰º-â‰ƒ-â‰ª âŒâ»Â¹)
   Bâ‚“-is-directed-set : is-directed-set Bâ‚“
   Bâ‚“-is-directed-set = (â†¡á´®-subset-is-inhabited x , Bâ‚“-is-semidirected-set)

 from-Idl : Idl â†’ âŸ¨ ğ““ âŸ©
 from-Idl I = Idl-mediating-map I

 to-Idl-is-continuous : is-continuous ğ““ Idl-DCPO to-Idl
 to-Idl-is-continuous I Î± Î´ =
  Idl-sups-from-powerset (to-Idl âˆ˜ Î±) (to-Idl (âˆ ğ““ Î´)) (â†¡á´®-is-continuous Î´)

 from-Idl-is-continuous : is-continuous Idl-DCPO ğ““ from-Idl
 from-Idl-is-continuous = Idl-mediating-map-is-continuous

 to-Idl-section-of-from-Idl : from-Idl âˆ˜ to-Idl âˆ¼ id
 to-Idl-section-of-from-Idl x =
  â†¡á´®-section-of-âˆ x (Idl-mediating-directed (to-Idl x))

 from-Idl-section-of-to-Idl : to-Idl âˆ˜ from-Idl âˆ¼ id
 from-Idl-section-of-to-Idl ğ•€@(I , I-is-ideal) =
  to-subtype-ï¼ (Î» J â†’ being-ideal-is-prop J) (âˆ-â†¡á´®-retract I claimâ‚ claimâ‚‚)
   where
    claimâ‚ : (b c : B) â†’ Î² b âŠ‘âŸ¨ ğ““ âŸ© Î² c â†’ c âˆˆ I â†’ b âˆˆ I
    claimâ‚ b c b-below-c c-in-I = âˆ¥âˆ¥-rec (âˆˆ-is-prop I b) h (roundedness ğ•€ c-in-I)
     where
      h : (Î£ c' ê‰ B , c' âˆˆ I Ã— (c â‰º c')) â†’ b âˆˆ I
      h (c' , c'-in-I , c-way-below-c') =
       ideals-are-lowersets I I-is-ideal b c' l c'-in-I
        where
         l : b â‰º c'
         l = (â‰ªá´®-to-â‰ªá´®â‚› (âŠ‘-â‰ª-to-â‰ª ğ““ b-below-c (â‰ªá´®â‚›-to-â‰ªá´® c-way-below-c')))
    claimâ‚‚ : (b : B) â†’ b âˆˆ I â†’ âˆƒ c ê‰ B , c âˆˆ I Ã— Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c
    claimâ‚‚ b b-in-I = âˆ¥âˆ¥-functor h (roundedness ğ•€ b-in-I)
     where
      h : (Î£ c ê‰ B , c âˆˆ I Ã— b â‰º c)
        â†’ (Î£ c ê‰ B , c âˆˆ I Ã— Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c)
      h (c , c-in-I , b-below-c) = (c , c-in-I , âŒœ â‰º-â‰ƒ-â‰ª âŒ b-below-c)

 Idl-â‰ƒ : ğ““ â‰ƒáµˆá¶œáµ–áµ’ Idl-DCPO
 Idl-â‰ƒ = (to-Idl , from-Idl , to-Idl-section-of-from-Idl
                            , from-Idl-section-of-to-Idl ,
          to-Idl-is-continuous , from-Idl-is-continuous)

\end{code}

Finally, if D is an algebraic dpco with small compact basis Î² : B â†’ D, then
Idl(B,âŠ‘) is isomorphic to D.

\begin{code}

module Idl-algebraic
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        {B : ğ“¥ Ì‡ }
        (Î² : B â†’ âŸ¨ ğ““ âŸ©)
        (Î²-is-small-compact-basis : is-small-compact-basis ğ““ Î²)
       where

 open is-small-compact-basis Î²-is-small-compact-basis
 open Idl-continuous-retract-of-algebraic ğ““ Î²
       (compact-basis-is-basis ğ““ Î² Î²-is-small-compact-basis)

 Idl-â‰ƒ : ğ““ â‰ƒáµˆá¶œáµ–áµ’ Idl-DCPO
 Idl-â‰ƒ = (to-Idl , from-Idl , retract-condition Idl-retract
                            , from-Idl-section-of-to-Idl ,
          to-Idl-is-continuous , from-Idl-is-continuous)
  where
   -- This is where we use --lossy-unification
   from-Idl-section-of-to-Idl : (I : âŸ¨ Idl-DCPO âŸ©) â†’ to-Idl (from-Idl I) ï¼ I
   from-Idl-section-of-to-Idl I =
    antisymmetry Idl-DCPO (to-Idl (from-Idl I)) I (Idl-deflation I) inflationary
     where
      inflationary : I âŠ‘âŸ¨ Idl-DCPO âŸ© to-Idl (from-Idl I)
      inflationary = â†¡á´®-âˆ-inflation (carrier I) condition
       where
        condition : (b : B) â†’ b âˆˆáµ¢ I â†’ âˆƒ c ê‰ B , c âˆˆáµ¢ I Ã— (Î² b â‰ªâŸ¨ ğ““ âŸ© Î² c)
        condition b b-in-I = âˆ£ b , b-in-I , basis-is-compact b âˆ£

\end{code}
