Tom de Jong and Ayberk Tosun, 4 & 5 October 2023


Given an algebraic dcpo ğ““, the subset K of compact elements forms a basis for ğ““.
In our predicative context, we consider *small* bases, however, and a priori
there is no reason for K to be a small type.

However, if ğ““ comes equipped with what we call a small compact basis, then set
replacementÂ¹ implies that K is small.

If we moreover assume univalence, then the relevant small condition is a
property, which means that having an unspecified small compact basis is
sufficient.

In particular, with set replacement and univalence, we can show:
  âˆ¥ has-specified-small-compact-basis ğ““ âˆ¥ â†’ has-specified-small-compact-basis ğ““.
In other words, the type (has-specified-small-compact-basis ğ““) has split support.

Having a specified small compact basis is useful as we can use the small basis
to replace large quantifications by small ones for example, for example to show
that exponentials are locally small.

The split support observation is due to Ayberk Tosun (23 September 2023) and was
formalised, with the addition of many explanatory comments, by Tom de Jong.
Ayberk previously formalised the result for spectrality in the context of locale
theory in Locales.SmallBasis (truncated-spectralá´°-implies-spectral).

Towards the end, we also clarify how the fact that K is the unique basis (as a
subset) consisting of compact elements fits in to our framework.


Â¹ Set replacement says: if f : X â†’ Y is a map from a small type to a locally
  small set, then the image of f is small.
  It is equivalent to having small set quotients, see Quotient.index.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc

module DomainTheory.BasesAndContinuity.CompactBasis
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe) -- where the index types for directed completeness live
       where

open PropositionalTruncation pt

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.ImageAndSurjection pt
open import UF.Sets
open import UF.Sets-Properties
open import UF.Size hiding (is-small ; is-locally-small)
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¥

\end{code}

As announced above, we start by establishing as many as properties of K as
possible without considering that K needs to be small.

For this, we only assume that our dcpo is algebraic (in the truncated sense).

\begin{code}

module basis-of-compact-elements
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (is-alg : is-algebraic-dcpo ğ““)
       where

 K : ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 K = Î£ x ê‰ âŸ¨ ğ““ âŸ© , is-compact ğ““ x

 Î¹ : K â†’ âŸ¨ ğ““ âŸ©
 Î¹ = prâ‚

 Î¹-is-compact : (c : K) â†’ is-compact ğ““ (Î¹ c)
 Î¹-is-compact = prâ‚‚

 â†“á´· : âŸ¨ ğ““ âŸ© â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 â†“á´· x = Î£ k ê‰ K , Î¹ k âŠ‘âŸ¨ ğ““ âŸ© x

 â†“á´·-inclusion : (x : âŸ¨ ğ““ âŸ©) â†’ â†“á´· x â†’ âŸ¨ ğ““ âŸ©
 â†“á´·-inclusion x = Î¹ âˆ˜ prâ‚

\end{code}

We show that every element x of ğ““ is the *directed* supremum of the inclusion
  (Î£ k ê‰ K , k âŠ‘âŸ¨ ğ““ âŸ© x) â†’ âŸ¨ ğ““ âŸ©.

\begin{code}

 â†“á´·-is-inhabited : (x : âŸ¨ ğ““ âŸ©) â†’ âˆ¥ â†“á´· x âˆ¥
 â†“á´·-is-inhabited x = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop Î³ is-alg
  where
   Î³ : structurally-algebraic ğ““
     â†’ âˆ¥ â†“á´· x âˆ¥
   Î³ str-alg = âˆ¥âˆ¥-functor f inh
    where
     open structurally-algebraic str-alg
     inh : âˆ¥ index-of-compact-family x âˆ¥
     inh = inhabited-if-Directed ğ““ (compact-family x)
                                   (compact-family-is-directed x)
     f : index-of-compact-family x â†’ â†“á´· x
     f i = (compact-family x i , compact-family-is-compact x i)
           , compact-family-is-upperbound x i

 â†“á´·-is-semidirected : (x : âŸ¨ ğ““ âŸ©) â†’ is-Semidirected ğ““ (â†“á´·-inclusion x)
 â†“á´·-is-semidirected x =
  âˆ¥âˆ¥-rec (being-semidirected-is-prop (underlying-order ğ““) (â†“á´·-inclusion x))
         Î³ is-alg
   where
    Î³ : structurally-algebraic ğ““
      â†’ is-Semidirected ğ““ (â†“á´·-inclusion x)
    Î³ str-alg ((câ‚ , câ‚-compact) , câ‚-below-x)
              ((câ‚‚ , câ‚‚-compact) , câ‚‚-below-x) =
     âˆ¥âˆ¥-recâ‚‚ âˆƒ-is-prop sd â¦…1â¦† â¦…2â¦†
      where
       open structurally-algebraic str-alg
       I = index-of-compact-family x
       Îº = compact-family x
       Î´ = compact-family-is-directed x
       â¦…1â¦† : âˆƒ iâ‚ ê‰ I , câ‚ âŠ‘âŸ¨ ğ““ âŸ© compact-family x iâ‚
       â¦…1â¦† = câ‚-compact I Îº Î´
                        (câ‚     âŠ‘âŸ¨ ğ““ âŸ©[ câ‚-below-x ]
                         x      âŠ‘âŸ¨ ğ““ âŸ©[ ï¼-to-âŠ’ ğ““ (compact-family-âˆ-ï¼ x) ]
                         âˆ ğ““ Î´ âˆâŸ¨ ğ““ âŸ©)
       â¦…2â¦† : âˆƒ iâ‚‚ ê‰ I , câ‚‚ âŠ‘âŸ¨ ğ““ âŸ© compact-family x iâ‚‚
       â¦…2â¦† = câ‚‚-compact I Îº Î´
                        (câ‚‚     âŠ‘âŸ¨ ğ““ âŸ©[ câ‚‚-below-x ]
                         x      âŠ‘âŸ¨ ğ““ âŸ©[ ï¼-to-âŠ’ ğ““ (compact-family-âˆ-ï¼ x) ]
                         âˆ ğ““ Î´ âˆâŸ¨ ğ““ âŸ©)
       sd : (Î£ iâ‚ ê‰ I , câ‚ âŠ‘âŸ¨ ğ““ âŸ© Îº iâ‚)
          â†’ (Î£ iâ‚‚ ê‰ I , câ‚‚ âŠ‘âŸ¨ ğ““ âŸ© Îº iâ‚‚)
          â†’ âˆƒ c ê‰ â†“á´· x , (câ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
                       Ã— (câ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
       sd (iâ‚ , câ‚-below-iâ‚) (iâ‚‚ , câ‚‚-below-iâ‚‚) = âˆ¥âˆ¥-functor f semidir
        where
         semidir : âˆƒ i ê‰ I , (Îº iâ‚ âŠ‘âŸ¨ ğ““ âŸ© Îº i) Ã— (Îº iâ‚‚ âŠ‘âŸ¨ ğ““ âŸ© Îº i)
         semidir = semidirected-if-Directed ğ““ Îº Î´ iâ‚ iâ‚‚
         f : (Î£ i ê‰ I , (Îº iâ‚ âŠ‘âŸ¨ ğ““ âŸ© Îº i) Ã— (Îº iâ‚‚ âŠ‘âŸ¨ ğ““ âŸ© Îº i))
           â†’ Î£ c ê‰ â†“á´· x , (câ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
                        Ã— (câ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
         f (i , iâ‚-below-i , iâ‚‚-below-i) = ((Îº i , compact-family-is-compact x i)
                                           , compact-family-is-upperbound x i)
                                           , (câ‚   âŠ‘âŸ¨ ğ““ âŸ©[ câ‚-below-iâ‚ ]
                                              Îº iâ‚ âŠ‘âŸ¨ ğ““ âŸ©[ iâ‚-below-i ]
                                              Îº i  âˆâŸ¨ ğ““ âŸ©)
                                           , (câ‚‚   âŠ‘âŸ¨ ğ““ âŸ©[ câ‚‚-below-iâ‚‚ ]
                                              Îº iâ‚‚ âŠ‘âŸ¨ ğ““ âŸ©[ iâ‚‚-below-i ]
                                              Îº i  âˆâŸ¨ ğ““ âŸ©)

 â†“á´·-is-directed : (x : âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ (â†“á´·-inclusion x)
 â†“á´·-is-directed x = â†“á´·-is-inhabited x , â†“á´·-is-semidirected x

 â†“á´·-is-upperbound : (x : âŸ¨ ğ““ âŸ©)
                  â†’ is-upperbound (underlying-order ğ““) x (â†“á´·-inclusion x)
 â†“á´·-is-upperbound x c = prâ‚‚ c

 â†“á´·-is-sup : (x : âŸ¨ ğ““ âŸ©) â†’ is-sup (underlying-order ğ““) x (â†“á´·-inclusion x)
 â†“á´·-is-sup x =
  âˆ¥âˆ¥-rec (is-sup-is-prop (underlying-order ğ““) (prâ‚ (axioms-of-dcpo ğ““))
                         x (â†“á´·-inclusion x))
         Î³ is-alg
   where
    Î³ : structurally-algebraic ğ““
      â†’ is-sup (underlying-order ğ““) x (â†“á´·-inclusion x)
    Î³ str-alg = â†“á´·-is-upperbound x , lb-of-ubs
     where
      open structurally-algebraic str-alg
      lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order ğ““) x
                                               (â†“á´·-inclusion x)
      lb-of-ubs y y-is-ub =
       transport (Î» - â†’ - âŠ‘âŸ¨ ğ““ âŸ© y)
                 (compact-family-âˆ-ï¼ x)
                 (âˆ-is-lowerbound-of-upperbounds ğ““
                   (compact-family-is-directed x) y lb-of-ubs')
        where
         lb-of-ubs' : (i : index-of-compact-family x)
                    â†’ compact-family x i âŠ‘âŸ¨ ğ““ âŸ© y
         lb-of-ubs' i = y-is-ub ((compact-family x i
                                , compact-family-is-compact x i)
                                , compact-family-is-upperbound x i)

\end{code}

Assuming set replacement and a *specified* small compact basis, we deduce that K
is a small type.

\begin{code}

module resize-using-specified-small-compact-basis
        (sr : Set-Replacement pt)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        ((B , Î² , scb) : has-specified-small-compact-basis ğ““)
       where

 open basis-of-compact-elements
       ğ““ (is-algebraic-dcpo-if-unspecified-small-compact-basis ğ““ âˆ£ B , Î² , scb âˆ£)

 open is-small-compact-basis scb
 open is-locally-small (locally-small-if-small-compact-basis ğ““ Î² scb)

 K-is-small' : is-small K
 K-is-small' = â‰ƒ-size-contravariance K-is-image image-is-small
  where
   Î²' : B â†’ K
   Î²' b = Î² b , basis-is-compact b
   Î²'-is-surjection : is-surjection Î²'
   Î²'-is-surjection (x , x-compact) =
    âˆ¥âˆ¥-functor
     (Î» {(b , refl) â†’ b , to-subtype-ï¼ (being-compact-is-prop ğ““) refl})
     (small-compact-basis-contains-all-compact-elements
      ğ““ Î² scb x x-compact)
   K-is-image : K â‰ƒ image Î²'
   K-is-image = â‰ƒ-sym (surjection-â‰ƒ-image Î²' Î²'-is-surjection)
   K-is-set : is-set K
   K-is-set = subtypes-of-sets-are-sets' Î¹
               (prâ‚-lc (Î» {x} â†’ being-compact-is-prop ğ““ x)) (sethood ğ““)
   K-is-locally-small : K is-locally ğ“¥ small
   K-is-locally-small (x , x-compact) (y , y-compact) =
      ((x âŠ‘â‚› y) Ã— (y âŠ‘â‚› x))
    , logically-equivalent-props-are-equivalent
       (Ã—-is-prop (âŠ‘â‚›-is-prop-valued x y) (âŠ‘â‚›-is-prop-valued y x))
       K-is-set
       (Î» (l , k) â†’ to-subtype-ï¼ (being-compact-is-prop ğ““)
                                  (antisymmetry ğ““ x y (âŠ‘â‚›-to-âŠ‘ l)
                                                      (âŠ‘â‚›-to-âŠ‘ k)))
       Î» {refl â†’ reflexivityâ‚› x , reflexivityâ‚› x}
   image-is-small : is-small (image Î²')
   image-is-small = sr Î²' (B , â‰ƒ-refl B) K-is-locally-small K-is-set

 Î¹-âŠ‘-is-small' : (x : âŸ¨ ğ““ âŸ©) (c : K) â†’ is-small (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x)
 Î¹-âŠ‘-is-small' x c =
  âŒœ local-smallness-equivalent-definitions ğ““ âŒ loc-small (Î¹ c) x
   where
    loc-small : is-locally-small ğ““
    loc-small = locally-small-if-small-compact-basis ğ““ Î² scb

\end{code}

If we additionally assume univalence, then we can prove that K is small from
assuming an *unspecified* small compact basis.

The need for univalence lies in the fact that it implies that smallness is a
property (being-small-is-prop).
(In fact, univalence is equivalent to this, in some precise sense, see UF.Size.)

\begin{code}

open import UF.Univalence

module resize-using-unspecified-small-compact-basis
        (ua : Univalence)
        (sr : Set-Replacement pt)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (uscb : has-unspecified-small-compact-basis ğ““)
       where

 open basis-of-compact-elements
       ğ““ (is-algebraic-dcpo-if-unspecified-small-compact-basis ğ““ uscb)

 K-is-small : is-small K
 K-is-small = âˆ¥âˆ¥-rec (being-small-is-prop ua K ğ“¥) Î³ uscb
  where
   Î³ : has-specified-small-compact-basis ğ““
     â†’ is-small K
   Î³ scb = K-is-small'
    where
     open resize-using-specified-small-compact-basis sr ğ““ scb

 Î¹-âŠ‘-is-small : (x : âŸ¨ ğ““ âŸ©) (c : K) â†’ is-small (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x)
 Î¹-âŠ‘-is-small = âˆ¥âˆ¥-rec (Î â‚‚-is-prop fe (Î» x c â†’ being-small-is-prop
                                                ua (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x) ğ“¥))
                       Î³ uscb
  where
   Î³ : has-specified-small-compact-basis ğ““
     â†’ (x : âŸ¨ ğ““ âŸ©) (c : K) â†’ is-small (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x)
   Î³ scb = Î¹-âŠ‘-is-small'
    where
     open resize-using-specified-small-compact-basis sr ğ““ scb

\end{code}

We now package things up, using the small copy Kâ‚› of K and a reindexing along
the equivalence Kâ‚› â‰ƒ K, to produce a small compact basis.

\begin{code}

 Kâ‚› : ğ“¥ Ì‡
 Kâ‚› = resized K K-is-small

 Î¹â‚› : Kâ‚› â†’ âŸ¨ ğ““ âŸ©
 Î¹â‚› = Î¹ âˆ˜ âŒœ resizing-condition K-is-small âŒ

 â†“-resizing : (x : âŸ¨ ğ““ âŸ©) â†’ â†“á´· x â‰ƒ â†“á´® ğ““ Î¹â‚› x
 â†“-resizing x =
  â‰ƒ-sym (Î£-change-of-variable _ âŒœ resizing-condition K-is-small âŒ
                              (âŒœâŒ-is-equiv (resizing-condition K-is-small)))

 â†“á´·â‚›-is-directed : (x : âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ (â†“-inclusion ğ““ Î¹â‚› x)
 â†“á´·â‚›-is-directed x =
  reindexed-family-is-directed ğ““ (â†“-resizing x) (â†“á´·-inclusion x) (â†“á´·-is-directed x)

 Î¹â‚›-is-small-compact-basis : is-small-compact-basis ğ““ Î¹â‚›
 Î¹â‚›-is-small-compact-basis =
  record
   { basis-is-compact = Î» k â†’ Î¹-is-compact (âŒœ resizing-condition K-is-small âŒ k)
   ; âŠ‘á´®-is-small = Î» x k â†’ Î¹-âŠ‘-is-small x (âŒœ resizing-condition K-is-small âŒ k)
   ; â†“á´®-is-directed = â†“á´·â‚›-is-directed
   ; â†“á´®-is-sup = Î» x â†’ reindexed-family-sup ğ““ (â†“-resizing x)
                                              (â†“á´·-inclusion x) x (â†“á´·-is-sup x)
   }

\end{code}

Finally, we arrive at the result announced at the top of this file: the type
(has-specified-small-compact-basis ğ““) has split support.

\begin{code}

open import UF.KrausLemma
open split-support-and-collapsibility pt

module _
        (ua : Univalence)
        (sr : Set-Replacement pt)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where

 specified-small-compact-basis-has-split-support :
  has-split-support (has-specified-small-compact-basis ğ““)
 specified-small-compact-basis-has-split-support uscb =
  Kâ‚› , Î¹â‚› , Î¹â‚›-is-small-compact-basis
   where
    open basis-of-compact-elements ğ““
          (is-algebraic-dcpo-if-unspecified-small-compact-basis ğ““ uscb)
    open resize-using-unspecified-small-compact-basis ua sr ğ““ uscb

\end{code}

In particular, we can extract algebraicity structure from an unspecified small
compact basis.

\begin{code}

 structurally-algebraic-if-unspecified-small-compact-basis :
    has-unspecified-small-compact-basis ğ““
  â†’ structurally-algebraic ğ““
 structurally-algebraic-if-unspecified-small-compact-basis =
  structurally-algebraic-if-specified-small-compact-basis ğ““
   âˆ˜ specified-small-compact-basis-has-split-support

\end{code}

Another formulation is that we have the following logical equivalence:

\begin{code}

 specified-unspecified-equivalence :
  has-specified-small-compact-basis ğ““ â†” has-unspecified-small-compact-basis ğ““
 specified-unspecified-equivalence =
    âˆ£_âˆ£
  , specified-small-compact-basis-has-split-support

\end{code}

We note that the above cannot be promoted to an equivalence of types, because
the left-hand side, the type (has-specified-small-compact-basis ğ““), is not a
proposition. This may seem puzzling, especially to domain theorists, as there is
a unique basis (as a subset) which consists of compact elements, so we stop to
explain this here.

Recall that
  has-specified-small-compact-basis ğ““ :=
    Î£ B ê‰ ğ“¥ Ì‡ , Î£ Î² ê‰ (B â†’ âŸ¨ ğ““ âŸ©) , (1) Ã— (2) Ã— (3)
with (1)â”€(3) expressing that:
  (1) all elements Î² b are compact,
  (2) (Î² b âŠ‘âŸ¨ ğ““ âŸ© x) is a small type for all elements x, and
  (3) the family (Î£ b ê‰ B , Î² b âŠ‘âŸ¨ ğ““ âŸ©) â†’ âŸ¨ ğ““ âŸ© is directed with sup x.

If we drop smallness condition (2) and allow for B to live in larger universes
than ğ“¥, then K, the subset of compact elements with its inclusion K â†’ âŸ¨ ğ““ âŸ©,
will fit the specification.

If we now add the condition that Î² is an embedding (as it is for K), then we get
the type
    Î£ B ê‰ _ Ì‡Ì‡ , Î£ Î² ê‰ (B â†’ âŸ¨ ğ““ âŸ©) , (1) Ã— (3) Ã— (Î² is an embedding)
which *is* a proposition: it has a unique element in case ğ““ is algebraic, given
by the *subset* of compact elements of ğ““. (This is true because any basis must
contain all compact elements.)

This raises a question: why don't we require Î² to be an embedding in our
definition of a small compact basis to obtain a property instead of truncating
the type has-specified-small-compact-basis?

The reason is that it could be useful, as we illustrate now:

  The canonical map from lists into the powerset of a set X,
    Î² : List X â†’ ğ“Ÿ X
  is a small compact basis for the algebraic dcpo ğ“Ÿ X. This map is not an
  embedding, as any permutation of a list will give rise to the same subset.

  If we insisted on having an embedding, we would instead have to use the
  inclusion of the Kuratowski finite subsets ğ“š X into ğ“Ÿ X.  However, ğ“š X is not
  a small type without additional assumptions, such as HITs or more specifically,
  set replacement (as ğ“š X is precisely the image of the map Î² : List X â†’ ğ“Ÿ X).

Returning to the main line of thought, we conclude that, in the presence of set
replacement and univalence, if there is some unspecified small compact basis,
then the subset K of compact elements is suitably small.
