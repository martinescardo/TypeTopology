Tom de Jong and Ayberk Tosun, 4 October 2023


RESULT
â”€â”€â”€â”€â”€â”€
Assuming set replacementÂ¹ and univalence, we show that:
  if a dcpo has an *unspecified* small compact basis, then it has a *specified*
  small compact basis.

That is, for a dcpo ğ““, we have:
  âˆ¥ has-specified-small-compact-basis ğ““ âˆ¥ â†’ has-specified-small-compact-basis ğ““.

In other words, the type
  has-specified-small-compact-basis ğ““
has split support.

Or, equivalently, we have a logical equivalence
  has-unspecified-small-compact-basis ğ““ â‡” has-specified-small-compact-basis ğ““.

In particular, we get extract algebraicity structure from an unspecified small
compact basis:
  has-unspecified-small-compact-basis ğ““ â†’ structurally-algebraic ğ““.

This result is due to Ayberk Tosun (19 September 2023) and was formalised here
on 4 October 2023 by Tom de Jong.


Â¹ Set replacement is equivalent to having small set quotients, see
  Quotient.index.


PROOF
â”€â”€â”€â”€â”€
The construction is perhaps somewhat lengthy, but the core idea is quite simple
and so we pause to explain it here.

If a dcpo ğ““ has a small compact basis Î² : B â†’ ğ““, then it must contain all
compact elements, as shown in the Bases file.

Given a dcpo ğ““, we consider the subset Î¹ : K â†ª ğ““ of compact elements of ğ““.
Knowing that ğ““ has an unspecified small compact basis is sufficient for proving
that K (almost) gives a small compact basis of ğ““, since the required properties
are mostly propositions.
For example, if ğ““ has an unspecified small compact basis, then, for any x : ğ““,
the family
  (Î£ k ê‰ K , Î¹ k âŠ‘âŸ¨ ğ““ âŸ© x) â†’ ğ““,
given by projecting first and then applying Î¹, is directed with supremum x.

All these properties are proved at the start of this file.


Two obstacles remain, however:
(1) the type K is (a priori) not small as it quantifies over all elements of ğ““,
(2) the type (Î¹ k âŠ‘âŸ¨ ğ““ âŸ© x) is (a priori) not small.

This is where univalence comes in: being a small type is a property if
univalence is assumed, which means we can try to prove (1) (and also (2), but
for truth values univalence is not strictly needed) by untruncating our
assumption of an unspecified small compact basis.

So we assume a small compact basis Î² : B â†’ ğ““ and we consider the map
  Î²' : B â†’ K
which is given by Î² and the witness that Î² maps to compact elements.
Since any small compact basis must contain K, the map Î²' is surjective, and
hence the image of Î²' is equivalent to K.

This is where set replacement comes in: it says that if f : X â†’ Y is a map from
a small type to a locally small set, then the image of f is small.
Our type B is small by assumption and K can be shown to be locally small by
using antisymmetry and the small basis. Hence, image Î²' and therefore K is
small.

Our specified small compact basis is then given by a small copy (up to
equivalence) of K.


\begin{code}

{-# OPTIONS --safe --without-K --exact-split #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc

module DomainTheory.BasesAndContinuity.CompactBasis-SplitSupport
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe) -- where the index types for directed completeness live
       where

open PropositionalTruncation pt

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Size hiding (is-small ; is-locally-small)
open import UF.Subsingletons

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¥

\end{code}

As announced above, we start by establishing as many as properties of K as
possible without considering that K needs to be small.

\begin{code}

module basis-of-compact-elements-preliminaries
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (uscb : has-unspecified-small-compact-basis ğ““)
       where

 K : ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 K = Î£ x ê‰ âŸ¨ ğ““ âŸ© , is-compact ğ““ x

 Î¹ : K â†’ âŸ¨ ğ““ âŸ©
 Î¹ = prâ‚

 Î¹-is-compact : (c : K) â†’ is-compact ğ““ (Î¹ c)
 Î¹-is-compact = prâ‚‚

 â†“á´· : âŸ¨ ğ““ âŸ© â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 â†“á´· x = Î£ k ê‰ K , Î¹ k âŠ‘âŸ¨ ğ““ âŸ© x

 â†“á´·-inclusion : (x : âŸ¨ ğ““ âŸ©) â†’ â†“á´· x â†’ âŸ¨ ğ““ âŸ©
 â†“á´·-inclusion x = Î¹ âˆ˜ prâ‚

 â†“á´·-is-inhabited : (x : âŸ¨ ğ““ âŸ©) â†’ âˆ¥ â†“á´· x âˆ¥
 â†“á´·-is-inhabited x = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop Î³ uscb
  where
   Î³ : has-specified-small-compact-basis ğ““
     â†’ âˆ¥ â†“á´· x âˆ¥
   Î³ (B , Î² , scb) = âˆ¥âˆ¥-functor (Î» (b , l) â†’ (Î² b , basis-is-compact b) , l) inh
    where
     open is-small-compact-basis scb
     inh : âˆ¥ â†“á´® ğ““ Î² x âˆ¥
     inh = inhabited-if-Directed ğ““ (â†“-inclusion ğ““ Î² x) (â†“á´®-is-directed x)

 â†“á´·-is-semidirected : (x : âŸ¨ ğ““ âŸ©) â†’ is-Semidirected ğ““ (â†“á´·-inclusion x)
 â†“á´·-is-semidirected x =
  âˆ¥âˆ¥-rec (being-semidirected-is-prop (underlying-order ğ““) (â†“á´·-inclusion x))
         Î³ uscb
   where
    Î³ : has-specified-small-compact-basis ğ““
      â†’ is-Semidirected ğ““ (â†“á´·-inclusion x)
    Î³ (B , Î² , scb) ((câ‚ , câ‚-compact) , câ‚-below-x)
                    ((câ‚‚ , câ‚‚-compact) , câ‚‚-below-x) =
     âˆ¥âˆ¥-recâ‚‚ âˆƒ-is-prop sd â¦…1â¦† â¦…2â¦†
      where
       open is-small-compact-basis scb
       sd : (Î£ bâ‚ ê‰ B , Î² bâ‚ ï¼ câ‚)
          â†’ (Î£ bâ‚‚ ê‰ B , Î² bâ‚‚ ï¼ câ‚‚)
          â†’ âˆƒ c ê‰ â†“á´· x , (câ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
                       Ã— (câ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x c)
       sd (bâ‚ , refl) (bâ‚‚ , refl) = âˆ¥âˆ¥-functor Î²' semidir
        where
         Î²' : (Î£ b ê‰ â†“á´® ğ““ Î² x , (Î² bâ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“-inclusion ğ““ Î² x b)
                              Ã— (Î² bâ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“-inclusion ğ““ Î² x b))
            â†’ (Î£ k ê‰ â†“á´· x , (Î² bâ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x k)
                          Ã— (Î² bâ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“á´·-inclusion x k))
         Î²' ((b , b-below-x) , lâ‚ , lâ‚‚) = ((Î² b , basis-is-compact b)
                                          , b-below-x) , lâ‚ , lâ‚‚
         semidir : âˆƒ b ê‰ â†“á´® ğ““ Î² x , (Î² bâ‚ âŠ‘âŸ¨ ğ““ âŸ© â†“-inclusion ğ““ Î² x b)
                                  Ã— (Î² bâ‚‚ âŠ‘âŸ¨ ğ““ âŸ© â†“-inclusion ğ““ Î² x b)
         semidir = semidirected-if-Directed ğ““
                    (â†“-inclusion ğ““ Î² x) (â†“á´®-is-directed x)
                    (bâ‚ , câ‚-below-x) (bâ‚‚ , câ‚‚-below-x)
       â¦…1â¦† : âˆƒ bâ‚ ê‰ B , Î² bâ‚ ï¼ câ‚
       â¦…1â¦† = small-compact-basis-contains-all-compact-elements
              ğ““ Î² scb câ‚ câ‚-compact
       â¦…2â¦† : âˆƒ bâ‚‚ ê‰ B , Î² bâ‚‚ ï¼ câ‚‚
       â¦…2â¦† = small-compact-basis-contains-all-compact-elements
              ğ““ Î² scb câ‚‚ câ‚‚-compact

 â†“á´·-is-directed : (x : âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ (â†“á´·-inclusion x)
 â†“á´·-is-directed x = â†“á´·-is-inhabited x , â†“á´·-is-semidirected x

 â†“á´·-is-upperbound : (x : âŸ¨ ğ““ âŸ©)
                  â†’ is-upperbound (underlying-order ğ““) x (â†“á´·-inclusion x)
 â†“á´·-is-upperbound x c = prâ‚‚ c

 â†“á´·-is-sup : (x : âŸ¨ ğ““ âŸ©) â†’ is-sup (underlying-order ğ““) x (â†“á´·-inclusion x)
 â†“á´·-is-sup x =
  âˆ¥âˆ¥-rec (is-sup-is-prop (underlying-order ğ““) (prâ‚ (axioms-of-dcpo ğ““))
                         x (â†“á´·-inclusion x))
         Î³ uscb
   where
    Î³ : has-specified-small-compact-basis ğ““
      â†’ is-sup (underlying-order ğ““) x (â†“á´·-inclusion x)
    Î³ (B , Î² , scb) = â†“á´·-is-upperbound x , lb-of-ubs
     where
      open is-small-compact-basis scb
      lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order ğ““) x
                                               (â†“á´·-inclusion x)
      lb-of-ubs y y-is-ub =
       transport (Î» - â†’ - âŠ‘âŸ¨ ğ““ âŸ© y)
                 (â†“á´®â‚›-âˆ-ï¼ x)
                 (âˆ-is-lowerbound-of-upperbounds ğ““
                  (â†“á´®â‚›-is-directed x) y lb-of-ubs')
        where
         lb-of-ubs' : (b : â†“á´®â‚› x) â†’ â†“-inclusionâ‚› x b âŠ‘âŸ¨ ğ““ âŸ© y
         lb-of-ubs' (b , l) = y-is-ub ((Î² b , basis-is-compact b) , âŠ‘á´®â‚›-to-âŠ‘á´® l)

\end{code}

Only now do we use univalence and set replacement to show that K is small.

\begin{code}

open import UF.Univalence

module basis-of-compact-elements
        (ua : Univalence)
        (sr : Set-Replacement pt)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (uscb : has-unspecified-small-compact-basis ğ““)
       where

 open import UF.ImageAndSurjection pt
 open import UF.Sets
 open import UF.Sets-Properties

 open basis-of-compact-elements-preliminaries ğ““ uscb

 K-is-small : is-small K
 K-is-small = âˆ¥âˆ¥-rec (being-small-is-prop ua K ğ“¥) Î³ uscb
  where
   Î³ : has-specified-small-compact-basis ğ““
     â†’ is-small K
   Î³ (B , Î² , scb) = â‰ƒ-size-contravariance K-is-image image-is-small
    where
     open is-small-compact-basis scb
     open is-locally-small (locally-small-if-small-compact-basis ğ““ Î² scb)
     Î²' : B â†’ K
     Î²' b = Î² b , basis-is-compact b
     Î²'-is-surjection : is-surjection Î²'
     Î²'-is-surjection (x , x-compact) =
      âˆ¥âˆ¥-functor
       (Î» {(b , refl) â†’ b , to-subtype-ï¼ (being-compact-is-prop ğ““) refl})
       (small-compact-basis-contains-all-compact-elements
        ğ““ Î² scb x x-compact)
     K-is-image : K â‰ƒ image Î²'
     K-is-image = â‰ƒ-sym (surjection-â‰ƒ-image Î²' Î²'-is-surjection)
     K-is-set : is-set K
     K-is-set = subtypes-of-sets-are-sets' Î¹
                 (prâ‚-lc (Î» {x} â†’ being-compact-is-prop ğ““ x)) (sethood ğ““)
     K-is-locally-small : K is-locally ğ“¥ small
     K-is-locally-small (x , x-compact) (y , y-compact) =
        ((x âŠ‘â‚› y) Ã— (y âŠ‘â‚› x))
      , logically-equivalent-props-are-equivalent
         (Ã—-is-prop (âŠ‘â‚›-is-prop-valued x y) (âŠ‘â‚›-is-prop-valued y x))
         K-is-set
         (Î» (l , k) â†’ to-subtype-ï¼ (being-compact-is-prop ğ““)
                                    (antisymmetry ğ““ x y (âŠ‘â‚›-to-âŠ‘ l)
                                                        (âŠ‘â‚›-to-âŠ‘ k)))
         Î» {refl â†’ reflexivityâ‚› x , reflexivityâ‚› x}
     image-is-small : is-small (image Î²')
     image-is-small = sr Î²' (B , â‰ƒ-refl B) K-is-locally-small K-is-set

 Î¹-âŠ‘-is-small : (x : âŸ¨ ğ““ âŸ©) (c : K) â†’ is-small (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x)
 Î¹-âŠ‘-is-small x c = âˆ¥âˆ¥-rec (being-small-is-prop ua (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x) ğ“¥)
                           Î³ uscb
  where
   Î³ : has-specified-small-compact-basis ğ““
     â†’ is-small (Î¹ c âŠ‘âŸ¨ ğ““ âŸ© x)
   Î³ (B , Î² , scb) =
    âŒœ local-smallness-equivalent-definitions ğ““ âŒ loc-small (Î¹ c) x
     where
      loc-small : is-locally-small ğ““
      loc-small = locally-small-if-small-compact-basis ğ““ Î² scb

\end{code}

We then use a resized version of K and Î¹ to get our small compact basis.

The results about directedness and suprema of â†“á´·-inclusion apply almost directly
as we are merely reindexing the family along an equivalence to a small type.

\begin{code}

 Kâ‚› : ğ“¥ Ì‡
 Kâ‚› = resized K K-is-small

 Î¹â‚› : Kâ‚› â†’ âŸ¨ ğ““ âŸ©
 Î¹â‚› = Î¹ âˆ˜ âŒœ resizing-condition K-is-small âŒ

 â†“-resizing : (x : âŸ¨ ğ““ âŸ©) â†’ â†“á´· x â‰ƒ â†“á´® ğ““ Î¹â‚› x
 â†“-resizing x =
  â‰ƒ-sym (Î£-change-of-variable _ âŒœ resizing-condition K-is-small âŒ
                              (âŒœâŒ-is-equiv (resizing-condition K-is-small)))

 â†“á´·â‚›-is-directed : (x : âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ (â†“-inclusion ğ““ Î¹â‚› x)
 â†“á´·â‚›-is-directed x =
  reindexed-family-is-directed ğ““ (â†“-resizing x) (â†“á´·-inclusion x) (â†“á´·-is-directed x)

 Î¹â‚›-is-small-compact-basis : is-small-compact-basis ğ““ Î¹â‚›
 Î¹â‚›-is-small-compact-basis =
  record
   { basis-is-compact = Î» k â†’ Î¹-is-compact (âŒœ resizing-condition K-is-small âŒ k)
   ; âŠ‘á´®-is-small = Î» x k â†’ Î¹-âŠ‘-is-small x (âŒœ resizing-condition K-is-small âŒ k)
   ; â†“á´®-is-directed = â†“á´·â‚›-is-directed
   ; â†“á´®-is-sup = Î» x â†’ reindexed-family-sup ğ““ (â†“-resizing x)
                                              (â†“á´·-inclusion x) x (â†“á´·-is-sup x)
   }

\end{code}

We summarise the main results of this file below.

\begin{code}

open import UF.KrausLemma
open split-support-and-collapsibility pt

module _
        (ua : Univalence)
        (sr : Set-Replacement pt)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where

 specified-small-compact-basis-has-split-support :
  has-split-support (has-specified-small-compact-basis ğ““)
 specified-small-compact-basis-has-split-support uscb =
  Kâ‚› , Î¹â‚› , Î¹â‚›-is-small-compact-basis
   where
    open basis-of-compact-elements-preliminaries ğ““ uscb
    open basis-of-compact-elements ua sr ğ““ uscb

 specified-unspecified-equivalence :
  has-specified-small-compact-basis ğ““ â‡” has-unspecified-small-compact-basis ğ““
 specified-unspecified-equivalence =
    âˆ£_âˆ£
  , specified-small-compact-basis-has-split-support

\end{code}

We stress that the above cannot be promoted to an equivalence of types as there
could be several small compact bases for ğ““. In a sense, the above says that
there is a canonical one though (which is given by all compact elements).


In particular, we get extract algebraicity structure from an unspecified small
compact basis.

\begin{code}

 structurally-algebraic-if-unspecified-small-compact-basis :
    has-unspecified-small-compact-basis ğ““
  â†’ structurally-algebraic ğ““
 structurally-algebraic-if-unspecified-small-compact-basis =
  structurally-algebraic-if-specified-small-compact-basis ğ““
   âˆ˜ specified-small-compact-basis-has-split-support

\end{code}
