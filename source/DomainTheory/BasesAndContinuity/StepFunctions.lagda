Tom de Jong, late February, early March 2022.

We use single step functions to construct a small compact basis on the
exponential of dcpos Eá´° where D and E have small compact bases and E is
sup-complete. This is used in DomainTheory.Bilimits.Dinfinity.lagda to show that
Scott's Dâˆ has a small compact basis, and hence is algebraic.

We can prove a similar result for dcpos with a small bases that are not
necessarily algebraic, but the formalization of this argument is not entirely
complete, as it depends on a lemma on the sup-completeness of the ideal
completion, see below for details.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}
-- The flag --lossy-unification roughly reduces the timechecking
-- time by 50%.

open import MLTT.Spartan hiding (J)
open import UF.FunExt
open import UF.PropTrunc

open import UF.Subsingletons

module DomainTheory.BasesAndContinuity.StepFunctions
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe) -- where the index types for directed completeness live
       where

open PropositionalTruncation pt hiding (_âˆ¨_)

open import UF.Base hiding (_â‰ˆ_)
open import UF.Equiv
open import UF.EquivalenceExamples

open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¥
open import DomainTheory.Basics.Exponential pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.Pointed pt fe ğ“¥ hiding (âŠ¥ ; âŠ¥-is-least)
open import DomainTheory.Basics.SupComplete pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥

\end{code}

We introduce single step functions to show that sup-complete algebraic dcpos are
closed under exponentials, cf. Exercise II-2.31 in "Continuous Lattices and
Domains" by Gierz et al.

Classically, a single step function
â¦… d â‡’ e â¦† is given like this:
 x â†¦ e if d âŠ‘ x
     âŠ¥ otherwise

Constructively, we can't expect to make this case distinction, so we define
single step functions using subsingleton suprema instead.

\begin{code}

module _
        (ğ““ : DCPO {ğ“¤}  {ğ“£})
        (ğ“” : DCPOâŠ¥ {ğ“¤'} {ğ“£'})
        (ğ““-is-locally-small : is-locally-small ğ““)
       where

 open is-locally-small ğ““-is-locally-small

 â¦…_â‡’_â¦† : âŸ¨ ğ““ âŸ© â†’ âŸª ğ“” âŸ« â†’ âŸ¨ ğ““ âŸ© â†’ âŸª ğ“” âŸ«
 â¦… d â‡’ e â¦† x = âˆË¢Ë¢ ğ“” Î± (âŠ‘â‚›-is-prop-valued d x)
  where
   Î± : d âŠ‘â‚› x â†’ âŸª ğ“” âŸ«
   Î± _ = e

 single-step-function-index : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ© â†’  ğ“¥ Ì‡
 single-step-function-index d x = d âŠ‘â‚› x

 single-step-function-index-is-prop : {d x : âŸ¨ ğ““ âŸ©}
                                    â†’ is-prop (single-step-function-index d x)
 single-step-function-index-is-prop {d} {x} = âŠ‘â‚›-is-prop-valued d x

 single-step-function-family : {d x : âŸ¨ ğ““ âŸ©} (e : âŸª ğ“” âŸ«)
                             â†’ single-step-function-index d x â†’ âŸª ğ“” âŸ«
 single-step-function-family e _ = e

 single-step-function-is-continuous : (d : âŸ¨ ğ““ âŸ©) (e : âŸª ğ“” âŸ«)
                                    â†’ is-compact ğ““ d
                                    â†’ is-continuous ğ““ (ğ“” â») â¦… d â‡’ e â¦†
 single-step-function-is-continuous d e d-is-compact I Î± Î´ = (ub , lb-of-ubs)
  where
   ub : (i : I) â†’ â¦… d â‡’ e â¦† (Î± i) âŠ‘âŸª ğ“” âŸ« â¦… d â‡’ e â¦† (âˆ ğ““ Î´)
   ub i = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ“” (Î» _ â†’ e)
           single-step-function-index-is-prop (â¦… d â‡’ e â¦† (âˆ ğ““ Î´))
           (Î» p â†’ âˆË¢Ë¢-is-upperbound ğ“” (Î» _ â†’ e)
                   single-step-function-index-is-prop
                   (d     âŠ‘â‚›[ p ]
                    Î± i   âŠ‘â‚›[ âŒœ âŠ‘â‚›-â‰ƒ-âŠ‘ âŒâ»Â¹ (âˆ-is-upperbound ğ““ Î´ i) ]
                    âˆ ğ““ Î´ âˆâ‚›))
   lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order (ğ“” â»))
                (â¦… d â‡’ e â¦† (âˆ ğ““ Î´)) (â¦… d â‡’ e â¦† âˆ˜ Î±)
   lb-of-ubs y y-is-ub = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ“” (Î» _ â†’ e)
                          single-step-function-index-is-prop y Î³
    where
     Î³ : (p : d âŠ‘â‚› âˆ ğ““ Î´) â†’ e âŠ‘âŸª ğ“” âŸ« y
     Î³ p = âˆ¥âˆ¥-rec (prop-valuedness (ğ“” â») e y)
            lemma (d-is-compact I Î± Î´ (âŒœ âŠ‘â‚›-â‰ƒ-âŠ‘ âŒ p))
      where
       lemma : (Î£ i ê‰ I , d âŠ‘âŸ¨ ğ““ âŸ© Î± i)
             â†’ e âŠ‘âŸª ğ“” âŸ« y
       lemma (i , u) = e               âŠ‘âŸª ğ“” âŸ«[ v ]
                       â¦… d â‡’ e â¦† (Î± i) âŠ‘âŸª ğ“” âŸ«[ y-is-ub i ]
                       y               âˆâŸª ğ“” âŸ«
        where
         v = âˆË¢Ë¢-is-upperbound ğ“” (Î» _ â†’ e) single-step-function-index-is-prop
              (âŒœ âŠ‘â‚›-â‰ƒ-âŠ‘ âŒâ»Â¹ u)

 â¦…_â‡’_â¦†[_] : (d : âŸ¨ ğ““ âŸ©) (e : âŸª ğ“” âŸ«)
          â†’ is-compact ğ““ d
          â†’ DCPO[ ğ““ , ğ“” â» ]
 â¦…_â‡’_â¦†[_] d e d-is-compact =
  (â¦… d â‡’ e â¦† , single-step-function-is-continuous d e d-is-compact)

 below-single-step-function-criterion : (d : âŸ¨ ğ““ âŸ©) (e : âŸª ğ“” âŸ«) (Îº : is-compact ğ““ d)
                                        (f : DCPO[ ğ““ , ğ“” â» ])
                                      â†’ â¦… d â‡’ e â¦†[ Îº ] âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© f
                                      â†” e âŠ‘âŸª ğ“” âŸ« [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© d
 below-single-step-function-criterion d e Îº f = â¦…1â¦† , â¦…2â¦†
  where
   â¦…1â¦† : â¦… d â‡’ e â¦†[ Îº ] âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© f
       â†’ e âŠ‘âŸª ğ“” âŸ« [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© d
   â¦…1â¦† u = e                  âŠ‘âŸª ğ“” âŸ«[ v ]
           â¦… d â‡’ e â¦† d        âŠ‘âŸª ğ“” âŸ«[ u d ]
           [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© d âˆâŸª ğ“” âŸ«
    where
     v = âˆË¢Ë¢-is-upperbound ğ“” (Î» _ â†’ e) single-step-function-index-is-prop
          (âŒœ âŠ‘â‚›-â‰ƒ-âŠ‘ âŒâ»Â¹ (reflexivity ğ““ d))
   â¦…2â¦† : e âŠ‘âŸª ğ“” âŸ« [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© d
      â†’ â¦… d â‡’ e â¦†[ Îº ] âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© f
   â¦…2â¦† u x = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ“” (Î» _ â†’ e)
              single-step-function-index-is-prop
              ([ ğ““ , ğ“” â» ]âŸ¨ f âŸ© x) Î³
    where
     Î³ : (p : d âŠ‘â‚› x) â†’ e âŠ‘âŸª ğ“” âŸ« [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© x
     Î³ p = e                  âŠ‘âŸª ğ“” âŸ«[ u ]
           [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© d âŠ‘âŸª ğ“” âŸ«[ v ]
           [ ğ““ , ğ“” â» ]âŸ¨ f âŸ© x âˆâŸª ğ“” âŸ«
      where
       v = monotone-if-continuous ğ““ (ğ“” â») f d x (âŒœ âŠ‘â‚›-â‰ƒ-âŠ‘ âŒ p)

 single-step-function-is-compact : (d : âŸ¨ ğ““ âŸ©) (e : âŸª ğ“” âŸ«)
                                   (Îº : is-compact ğ““ d)
                                 â†’ is-compact (ğ“” â») e
                                 â†’ is-compact (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) â¦… d â‡’ e â¦†[ Îº ]
 single-step-function-is-compact d e Îº e-is-compact I g Î´ e-below-âˆg =
  âˆ¥âˆ¥-functor lemma
             (e-is-compact I (pointwise-family ğ““ (ğ“” â») g d)
                           (pointwise-family-is-directed ğ““ (ğ“” â») g Î´ d) claim)
   where
    claim : e âŠ‘âŸª ğ“” âŸ« âˆ (ğ“” â») (pointwise-family-is-directed ğ““ (ğ“” â») g Î´ d)
    claim = lr-implication
             (below-single-step-function-criterion
               d e Îº (âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) {I} {g} Î´))
             e-below-âˆg
    lemma : (Î£ i ê‰ I , e âŠ‘âŸª ğ“” âŸ« [ ğ““ , ğ“” â» ]âŸ¨ g i âŸ© d)
          â†’ (Î£ i ê‰ I , â¦… d â‡’ e â¦†[ Îº ] âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© g i)
    lemma (i , u) = (i , v)
     where
      v : â¦… d â‡’ e â¦†[ Îº ] âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© g i
      v = rl-implication
           (below-single-step-function-criterion d e Îº (g i)) u

\end{code}

We now work towards constructing a small compact basis for the exponential
ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»). We start with a family of single step functions that we will
later directify by taking finite joins.

\begin{code}

 module _
         (Bá´° Bá´± : ğ“¥ Ì‡ )
         (Î²á´° : Bá´° â†’ âŸ¨ ğ““ âŸ©)
         (Î²á´± : Bá´± â†’ âŸª ğ“” âŸ«)
         (Îºá´° : is-small-compact-basis ğ““     Î²á´°)
         (Îºá´± : is-small-compact-basis (ğ“” â») Î²á´±)
        where

  single-step-functions : Bá´° Ã— Bá´± â†’ DCPO[ ğ““ , ğ“” â» ]
  single-step-functions (d , e) = â¦… Î²á´° d â‡’ Î²á´± e â¦†[ basis-is-compact d ]
   where
    open is-small-compact-basis Îºá´°

  module _
          (ğ“”-is-sup-complete : is-sup-complete (ğ“” â»))
          (f : âŸ¨ ğ““ âŸ© â†’ âŸª ğ“” âŸ«)
          (f-is-continuous : is-continuous ğ““ (ğ“” â») f)
         where

   ğ•— : DCPO[ ğ““ , ğ“” â» ]
   ğ•— = f , f-is-continuous

   open is-sup-complete ğ“”-is-sup-complete

   single-step-functions-below-function :
      (Î£ p ê‰ (Bá´° Ã— Bá´±) , single-step-functions p âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© ğ•—)
    â†’ DCPO[ ğ““ , ğ“” â» ]
   single-step-functions-below-function = single-step-functions âˆ˜ prâ‚

   single-step-functions-below-function-sup :
    is-sup (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))) ğ•—
           single-step-functions-below-function
   single-step-functions-below-function-sup = (ub , lb-of-ubs)
    where
     ub : is-upperbound (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))) ğ•—
           single-step-functions-below-function
     ub = prâ‚‚
     lb-of-ubs :
      is-lowerbound-of-upperbounds (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))) ğ•—
       single-step-functions-below-function
     lb-of-ubs ğ•˜@(g , _) g-is-ub x = fx-below-gx
      where
       module _ where
        open is-small-compact-basis Îºá´±
        claimâ‚ : (d : Bá´°) (e : Bá´±) â†’ e âŠ‘á´®â‚› f (Î²á´° d) â†’ Î²á´± e âŠ‘âŸª ğ“” âŸ« g (Î²á´° d)
        claimâ‚ d e u =
         lr-implication (below-single-step-function-criterion (Î²á´° d) (Î²á´± e)
                          (is-small-compact-basis.basis-is-compact Îºá´° d) ğ•˜)
                          (g-is-ub ((d , e) , v))
          where
           v : single-step-functions (d , e) âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© ğ•—
           v = rl-implication
                (below-single-step-function-criterion (Î²á´° d) (Î²á´± e)
                  (is-small-compact-basis.basis-is-compact Îºá´° d) ğ•—)
                (âŠ‘á´®â‚›-to-âŠ‘á´® u)
        claimâ‚‚ : (d : Bá´°) â†’ f (Î²á´° d) âŠ‘âŸª ğ“” âŸ« g (Î²á´° d)
        claimâ‚‚ d = f (Î²á´° d)                             âŠ‘âŸª ğ“” âŸ«[ â¦…1â¦† ]
                   âˆ (ğ“” â») (â†“á´®â‚›-is-directed (f (Î²á´° d))) âŠ‘âŸª ğ“” âŸ«[ â¦…2â¦† ]
                   g (Î²á´° d)                             âˆâŸª ğ“” âŸ«
         where
          â¦…1â¦† = â†“á´®â‚›-âˆ-âŠ’ (f (Î²á´° d))
          â¦…2â¦† = âˆ-is-lowerbound-of-upperbounds (ğ“” â») (â†“á´®â‚›-is-directed (f (Î²á´° d)))
                 (g (Î²á´° d)) (Î» (e , v) â†’ claimâ‚ d e v)

       open is-small-compact-basis Îºá´°
       Î´ : is-Directed ğ““ (â†“-inclusionâ‚› x)
       Î´ = â†“á´®â‚›-is-directed x
       Îµ : is-Directed (ğ“” â») (f âˆ˜ â†“-inclusionâ‚› x)
       Îµ = image-is-directed' ğ““ (ğ“” â») ğ•— Î´

       fx-below-gx : f x âŠ‘âŸª ğ“” âŸ« g x
       fx-below-gx = f x       âŠ‘âŸª ğ“” âŸ«[ â¦…1â¦† ]
                     f (âˆ ğ““ Î´) âŠ‘âŸª ğ“” âŸ«[ â¦…2â¦† ]
                     âˆ (ğ“” â») Îµ âŠ‘âŸª ğ“” âŸ«[ â¦…3â¦† ]
                     g x       âˆâŸª ğ“” âŸ«
        where
         â¦…1â¦† = ï¼-to-âŠ’ (ğ“” â») (ap f (â†“á´®â‚›-âˆ-ï¼ x))
         â¦…2â¦† = continuous-âˆ-âŠ‘ ğ““ (ğ“” â») ğ•— Î´
         â¦…3â¦† = âˆ-is-lowerbound-of-upperbounds (ğ“” â») Îµ (g x) Î³
          where
           Î³ : is-upperbound (underlying-order (ğ“” â»)) (g x) (f âˆ˜ â†“-inclusionâ‚› x)
           Î³ (d , u) = f (Î²á´° d) âŠ‘âŸª ğ“” âŸ«[ claimâ‚‚ d ]
                       g (Î²á´° d) âŠ‘âŸª ğ“” âŸ«[ v        ]
                       g x      âˆâŸª ğ“” âŸ«
            where
             v = monotone-if-continuous ğ““ (ğ“” â») ğ•˜ (Î²á´° d) x (âŠ‘á´®â‚›-to-âŠ‘á´® u)

\end{code}

Now we are in position to show that the exponential has a small compact basis.

\begin{code}

  module _
          (ğ“”-is-sup-complete : is-sup-complete (ğ“” â»))
         where

   private
    exp-is-sup-complete : is-sup-complete (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
    exp-is-sup-complete = exponential-is-sup-complete ğ““ (ğ“” â») ğ“”-is-sup-complete

   open sup-complete-dcpo (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) exp-is-sup-complete

   private
    B : ğ“¥ Ì‡
    B = domain (directify single-step-functions)
    Î² : B â†’ DCPO[ ğ““ , ğ“” â» ]
    Î² = directify single-step-functions

   exponential-has-small-compact-basis : Prop-Ext
                                       â†’ is-small-compact-basis (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) Î²
   exponential-has-small-compact-basis pe =
    record
     { basis-is-compact = â¦…1â¦†
     ; âŠ‘á´®-is-small      = â¦…2â¦†
     ; â†“á´®-is-directed   = â¦…3â¦†
     ; â†“á´®-is-sup        = â¦…4â¦†
     }
     where
      â¦…1â¦† : (b : B) â†’ is-compact (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) (Î² b)
      â¦…1â¦† = directify-is-compact single-step-functions
            (Î» (d , e) â†’ single-step-function-is-compact (Î²á´° d) (Î²á´± e)
                          (is-small-compact-basis.basis-is-compact Îºá´° d)
                          (is-small-compact-basis.basis-is-compact Îºá´± e))
      â¦…2â¦† : (f : âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ©) (b : B)
          â†’ is-small (Î² b âŠ‘âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ© f)
      â¦…2â¦† f b = âŒœ local-smallness-equivalent-definitions (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) âŒ
                 exp-is-locally-small (Î² b) f
       where
        exp-is-locally-small : is-locally-small (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
        exp-is-locally-small =
         locally-small-exponential-criterion pe ğ““ (ğ“” â»)
          âˆ£ Bá´° , Î²á´° , compact-basis-is-basis ğ““ Î²á´° Îºá´° âˆ£
          (locally-small-if-small-compact-basis (ğ“” â») Î²á´± Îºá´±)
      â¦…3â¦† : (f : âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ©)
          â†’ is-Directed (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) (â†“-inclusion (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) Î² f)
      â¦…3â¦† f = directify-â†“-is-directed single-step-functions {f}
      â¦…4â¦† : (f : âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») âŸ©)
          â†’ is-sup (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))) f
             (â†“-inclusion (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)) Î² f)
      â¦…4â¦† (f , f-is-cts) =
       directify-â†“-sup single-step-functions {f , f-is-cts}
        (single-step-functions-below-function-sup ğ“”-is-sup-complete
        f f-is-cts)

   exponential-has-specified-small-compact-basis : Prop-Ext
    â†’ has-specified-small-compact-basis (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
   exponential-has-specified-small-compact-basis pe =
    (B , Î² , exponential-has-small-compact-basis pe)

   exponential-is-structurally-algebraic : Prop-Ext
                                         â†’ structurally-algebraic (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
   exponential-is-structurally-algebraic pe =
    structurally-algebraic-if-specified-small-compact-basis (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
     (exponential-has-specified-small-compact-basis pe)

   exponential-is-algebraic : Prop-Ext â†’ is-algebraic-dcpo (ğ““ âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»))
   exponential-is-algebraic pe = âˆ£ exponential-is-structurally-algebraic pe âˆ£

\end{code}

The fact that sup-complete (algebraic) dcpos with small compact bases are closed
under exponentials can be used to show that the same holds for sup-complete
(continuous) dcpos with small bases.

The proof outline, which MartÃ­n EscardÃ³ explained to me, is as follows:

Start with dcpos ğ““ and ğ“” with small bases Î²á´° : Bá´° â†’ ğ““ and Î²á´± : Bá´± â†’ ğ“”. Then
ğ““ and ğ“” are continuous retracts of dcpos ğ““' and ğ“”' respectively both with small
compact bases, using the ideal completions of the bases.

Moreover, these retracts induce a continuous retract of the exponentials:
(ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) is a continuous retract of (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
and the latter is algebraic with a small compact basis by the above. Finally, we
can use the continuous retract to give a small basis on (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”).

NB: The above proof outline depends on the fact that ğ“”' and (hence)
(ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”') are sup-complete. This can be shown by using the concrete
definition of ğ“”' as the ideal completion of Î²á´±. This step has not been
formalized (yet), but all the other steps in the outline have.

TODO: Formalize the proof that Idl(Î²á´±,âŠ‘) is is sup-complete. It looks like it
will be helpful to prove separately that we can close bases under finite joins.

\begin{code}

module _
        (pe : Prop-Ext)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
        (ğ“” : DCPO {ğ“¤'} {ğ“£'})
        {Bá´° Bá´± : ğ“¥ Ì‡ }
        (Î²á´° : Bá´° â†’ âŸ¨ ğ““ âŸ©)
        (Î²á´± : Bá´± â†’ âŸ¨ ğ“” âŸ©)
        (Î²á´°-is-small-basis : is-small-basis ğ““ Î²á´°)
        (Î²á´±-is-small-basis : is-small-basis ğ“” Î²á´±)
        (ğ“”-is-sup-complete : is-sup-complete ğ“”)
       where

 open import DomainTheory.IdealCompletion.Retracts pt fe pe ğ“¥

 private
  module _ where
   open Idl-continuous-retract-of-algebraic ğ““ Î²á´° Î²á´°-is-small-basis
   ğ““' : DCPO {ğ“¥ âº} {ğ“¥}
   ğ““' = Idl-DCPO
   ğ““-continuous-retract-of-ğ““' : ğ““ continuous-retract-of ğ““'
   ğ““-continuous-retract-of-ğ““' = Idl-continuous-retract
  module _ where
   open Idl-continuous-retract-of-algebraic ğ“” Î²á´± Î²á´±-is-small-basis
   ğ“”' : DCPO {ğ“¥ âº} {ğ“¥}
   ğ“”' = Idl-DCPO
   ğ“”-continuous-retract-of-ğ“”' : ğ“” continuous-retract-of ğ“”'
   ğ“”-continuous-retract-of-ğ“”' = Idl-continuous-retract

  exp-continuous-retract : (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) continuous-retract-of (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
  exp-continuous-retract =
   record
    { s               = s
    ; r               = r
    ; s-section-of-r    = s-section-of-r
    ; s-is-continuous = s-is-cts
    ; r-is-continuous = r-is-cts
    }
    where
     module _ where
      open _continuous-retract-of_ ğ““-continuous-retract-of-ğ““'
      sá´° = s
      rá´° = r
      sá´°-is-cts = s-is-continuous
      rá´°-is-cts = r-is-continuous
      rá´°-sá´°-equation = s-section-of-r
     module _ where
      open _continuous-retract-of_ ğ“”-continuous-retract-of-ğ“”'
      sá´± = s
      rá´± = r
      sá´±-is-cts = s-is-continuous
      rá´±-is-cts = r-is-continuous
      rá´±-sá´±-equation = s-section-of-r
     s : âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“” âŸ© â†’ âŸ¨ ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”' âŸ©
     s f = DCPO-âˆ˜â‚ƒ ğ““' ğ““ ğ“” ğ“”' (rá´° , rá´°-is-cts) f (sá´± , sá´±-is-cts)
     r : âŸ¨ ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”' âŸ© â†’ âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“” âŸ©
     r g = DCPO-âˆ˜â‚ƒ ğ““ ğ““' ğ“”' ğ“” (sá´° , sá´°-is-cts) g (rá´± , rá´±-is-cts)
     s-section-of-r : r âˆ˜ s âˆ¼ id
     s-section-of-r (f , _) = to-continuous-function-ï¼ ğ““ ğ“” Î³
      where
       Î³ : rá´± âˆ˜ sá´± âˆ˜ f âˆ˜ rá´° âˆ˜ sá´° âˆ¼ f
       Î³ x = (rá´± âˆ˜ sá´± âˆ˜ f âˆ˜ rá´° âˆ˜ sá´°) x ï¼âŸ¨ rá´±-sá´±-equation ((f âˆ˜ rá´° âˆ˜ sá´°) x) âŸ©
             (f âˆ˜ rá´° âˆ˜ sá´°) x           ï¼âŸ¨ ap f (rá´°-sá´°-equation x) âŸ©
             f x                       âˆ
     s-is-cts : is-continuous (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”') s
     s-is-cts = DCPO-âˆ˜â‚ƒ-is-continuousâ‚‚ ğ““' ğ““ ğ“” ğ“”'
                 (rá´° , rá´°-is-cts) (sá´± , sá´±-is-cts)
     r-is-cts : is-continuous (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) r
     r-is-cts = DCPO-âˆ˜â‚ƒ-is-continuousâ‚‚ ğ““ ğ““' ğ“”' ğ“”
                 (sá´° , sá´°-is-cts) (rá´± , rá´±-is-cts)

\end{code}

Sup-completeness of ğ“”' is the only remaining hole in the formalization of the
argument outlined above.

\begin{code}

{- TODO
  ğ“”'-is-sup-complete : is-sup-complete ğ“”'
  ğ“”'-is-sup-complete = {!!}
-}

\end{code}

We assume sup-completeness of ğ“”' here to illustrate that the remainder of the
argument is fully formalized.

\begin{code}

  module _
          (ğ“”'-is-sup-complete : is-sup-complete ğ“”')
         where

   open sup-complete-dcpo ğ“”' ğ“”'-is-sup-complete

   exp-has-small-compact-basis : has-specified-small-compact-basis (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
   exp-has-small-compact-basis =
    exponential-has-specified-small-compact-basis ğ““' (ğ“”' , âŠ¥ , âŠ¥-is-least)
     (locally-small-if-small-compact-basis ğ““' Î²á´°' Îºá´°')
     Bá´°' Bá´±' Î²á´°' Î²á´±' Îºá´°' Îºá´±' ğ“”'-is-sup-complete pe
      where
       module _ where
        open Idl-continuous-retract-of-algebraic ğ““ Î²á´° Î²á´°-is-small-basis
        small-compact-basisá´°' : has-specified-small-compact-basis ğ““'
        small-compact-basisá´°' = Idl-has-specified-small-compact-basis
                                 (Î» _ â†’ âŠ‘á´®-is-reflexive)
        Bá´°' : ğ“¥ Ì‡
        Bá´°' = prâ‚ small-compact-basisá´°'
        Î²á´°' : Bá´°' â†’ âŸ¨ ğ““' âŸ©
        Î²á´°' = prâ‚ (prâ‚‚ small-compact-basisá´°')
        Îºá´°' : is-small-compact-basis ğ““' Î²á´°'
        Îºá´°' = prâ‚‚ (prâ‚‚ small-compact-basisá´°')
       module _ where
        open Idl-continuous-retract-of-algebraic ğ“” Î²á´± Î²á´±-is-small-basis
        small-compact-basisá´±' : has-specified-small-compact-basis ğ“”'
        small-compact-basisá´±' = Idl-has-specified-small-compact-basis
                                 (Î» _ â†’ âŠ‘á´®-is-reflexive)
        Bá´±' : ğ“¥ Ì‡
        Bá´±' = prâ‚ small-compact-basisá´±'
        Î²á´±' : Bá´±' â†’ âŸ¨ ğ“”' âŸ©
        Î²á´±' = prâ‚ (prâ‚‚ small-compact-basisá´±')
        Îºá´±' : is-small-compact-basis ğ“”' Î²á´±'
        Îºá´±' = prâ‚‚ (prâ‚‚ small-compact-basisá´±')

   exponential-has-small-basis : has-specified-small-basis (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”)
   exponential-has-small-basis = B , r âˆ˜ Î² ,
    small-basis-from-continuous-retract (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
     exp-continuous-retract pe Î² (compact-basis-is-basis (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”') Î² Îº)
    where
     open _continuous-retract-of_ exp-continuous-retract
     exp-small-compact-basis : has-specified-small-compact-basis (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
     exp-small-compact-basis = exp-has-small-compact-basis
     B : ğ“¥ Ì‡
     B = prâ‚ exp-has-small-compact-basis
     Î² : B â†’ DCPO[ ğ““' , ğ“”' ]
     Î² = prâ‚ (prâ‚‚ exp-has-small-compact-basis)
     Îº : is-small-compact-basis (ğ““' âŸ¹áµˆá¶œáµ–áµ’ ğ“”') Î²
     Îº = prâ‚‚ (prâ‚‚ exp-has-small-compact-basis)

\end{code}
