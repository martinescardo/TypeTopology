Tom de Jong, early January 2022.

In category theory, the Ind-completion freely adds filtered colimits. For a
poset, the Ind-completion can be described as the preorder of directed families
into the poset, ordered by cofinality.

We construct the Ind-completion for a ğ“¥-dcpo ğ““ and show it is a preorder. We
define and characterize what it means for the map Ind â†’ ğ““ that takes suprema to
have a left adjoint. We also consider the poset reflection Ind/â‰ˆ of Ind and
define what it means for the induced map Ind/â‰ˆ â†’ ğ““ to have a left adjoint.

This development is used in exploring possible notions of continuous dcpo in
ContinuityDiscussion.lagda. In particular, the observation that the
Ind-completion is a preorder and not a poset is seen to be important there.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (J)
open import UF.FunExt
open import UF.PropTrunc

module DomainTheory.BasesAndContinuity.IndCompletion
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe) -- where the index types for directed completeness live
       where

open PropositionalTruncation pt

open import UF.Base hiding (_â‰ˆ_)
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Sets

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥

module Ind-completion
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where

 Ind : ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 Ind = Î£ I ê‰ ğ“¥ Ì‡  , Î£ Î± ê‰ (I â†’ âŸ¨ ğ““ âŸ©) , is-Directed ğ““ Î±

 index-of-underlying-family : Ind â†’ ğ“¥ Ì‡
 index-of-underlying-family = prâ‚

 underlying-family : (Î± : Ind) â†’ index-of-underlying-family Î± â†’ âŸ¨ ğ““ âŸ©
 underlying-family Î± = prâ‚ (prâ‚‚ Î±)

 _â‰²_ : Ind â†’ Ind â†’ ğ“¥ âŠ” ğ“£ Ì‡
 (I , Î± , _) â‰² (J , Î² , _) = (i : I) â†’ âˆƒ j ê‰ J , Î± i âŠ‘âŸ¨ ğ““ âŸ© Î² j

 â‰²-is-prop-valued : (Î± Î² : Ind) â†’ is-prop (Î± â‰² Î²)
 â‰²-is-prop-valued Î± Î² = Î -is-prop fe (Î» i â†’ âˆ¥âˆ¥-is-prop)

 â‰²-is-reflexive : (Î± : Ind) â†’ Î± â‰² Î±
 â‰²-is-reflexive (I , Î± , Î´) i = âˆ£ i , reflexivity ğ““ (Î± i) âˆ£

 â‰²-is-transitive : (Ïƒ Ï„ Ï : Ind) â†’ Ïƒ â‰² Ï„ â†’ Ï„ â‰² Ï â†’ Ïƒ â‰² Ï
 â‰²-is-transitive (I , Î± , Î´) (J , Î² , Îµ) (K , Î³ , Ï•)
  Î±-cofinal-in-Î² Î²-cofinal-in-Î³ i = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop r (Î±-cofinal-in-Î² i)
   where
    r : (Î£ j ê‰ J , Î± i âŠ‘âŸ¨ ğ““ âŸ© Î² j)
      â†’ (âˆƒ k ê‰ K , Î± i âŠ‘âŸ¨ ğ““ âŸ© Î³ k)
    r (j , u) = âˆ¥âˆ¥-functor (Î» (k , v) â†’ k , (Î± i âŠ‘âŸ¨ ğ““ âŸ©[ u ]
                                             Î² j âŠ‘âŸ¨ ğ““ âŸ©[ v ]
                                             Î³ k âˆâŸ¨ ğ““ âŸ©))
                           (Î²-cofinal-in-Î³ j)

\end{code}

We now construct directed suprema of ğ“¥-small families in Ind.

\begin{code}

 Ind-âˆ : {I : ğ“¥ Ì‡ } (ğ“ : I â†’ Ind)
       â†’ is-directed _â‰²_ ğ“
       â†’ Ind
 Ind-âˆ {I} ğ“ (I-inhabited , ğ“-semidirected) =
  Î£ J , Î² , K-is-inhabited , Î²-is-semidirected
   where
    J : I â†’ ğ“¥ Ì‡
    J i = prâ‚ (ğ“ i)
    Î± : (i : I) â†’ J i â†’ âŸ¨ ğ““ âŸ©
    Î± i = prâ‚ (prâ‚‚ (ğ“ i))
    Î´ : (i : I) â†’ is-Directed ğ““ (Î± i)
    Î´ i = prâ‚‚ (prâ‚‚ (ğ“ i))
    K : ğ“¥ Ì‡
    K = Î£ J
    Î² : K â†’ âŸ¨ ğ““ âŸ©
    Î² (i , j) = Î± i j
    K-is-inhabited : âˆ¥ K âˆ¥
    K-is-inhabited =
     âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop h I-inhabited
      where
       h : I â†’ âˆ¥ K âˆ¥
       h i = âˆ¥âˆ¥-functor (Î» j â†’ (i , j)) (inhabited-if-Directed ğ““ (Î± i) (Î´ i))
    Î²-is-semidirected : is-semidirected (underlying-order ğ““) Î²
    Î²-is-semidirected (iâ‚ , jâ‚) (iâ‚‚ , jâ‚‚) =
     âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop f (ğ“-semidirected iâ‚ iâ‚‚)
      where
       f : (Î£ i ê‰ I , (ğ“ iâ‚ â‰² ğ“ i) Ã— (ğ“ iâ‚‚ â‰² ğ“ i))
         â†’ âˆƒ k ê‰ K , (Î² (iâ‚ , jâ‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k) Ã— (Î² (iâ‚‚ , jâ‚‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k)
       f (i , uâ‚ , uâ‚‚) = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop g (uâ‚ jâ‚)
        where
         g : (Î£ jâ±â‚ ê‰ J i , Î² (iâ‚ , jâ‚) âŠ‘âŸ¨ ğ““ âŸ© Î² (i , jâ±â‚))
           â†’ âˆƒ k ê‰ K , (Î² (iâ‚ , jâ‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k) Ã— (Î² (iâ‚‚ , jâ‚‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k)
         g (jâ±â‚ , vâ‚) = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop h (uâ‚‚ jâ‚‚)
          where
           h : (Î£ jâ±â‚‚ ê‰ J i , Î² (iâ‚‚ , jâ‚‚) âŠ‘âŸ¨ ğ““ âŸ© Î² (i , jâ±â‚‚))
             â†’ âˆƒ k ê‰ K , (Î² (iâ‚ , jâ‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k) Ã— (Î² (iâ‚‚ , jâ‚‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k)
           h (jâ±â‚‚ , vâ‚‚) = âˆ¥âˆ¥-functor r
                           (semidirected-if-Directed ğ““ (Î± i) (Î´ i) jâ±â‚ jâ±â‚‚)
            where
             r : (Î£ j ê‰ J i , (Î± i jâ±â‚ âŠ‘âŸ¨ ğ““ âŸ© Î± i j) Ã— (Î± i jâ±â‚‚ âŠ‘âŸ¨ ğ““ âŸ© Î± i j))
               â†’ Î£ k ê‰ K , (Î² (iâ‚ , jâ‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k) Ã— (Î² (iâ‚‚ , jâ‚‚) âŠ‘âŸ¨ ğ““ âŸ© Î² k)
             r (j , wâ‚ , wâ‚‚) = (i , j) ,
                               ( (Î² (iâ‚ , jâ‚)  âŠ‘âŸ¨ ğ““ âŸ©[ vâ‚ ]
                                  Î² (i  , jâ±â‚) âŠ‘âŸ¨ ğ““ âŸ©[ wâ‚ ]
                                  Î² (i  , j)   âˆâŸ¨ ğ““ âŸ©)
                               , (Î² (iâ‚‚ , jâ‚‚)  âŠ‘âŸ¨ ğ““ âŸ©[ vâ‚‚ ]
                                  Î² (i  , jâ±â‚‚) âŠ‘âŸ¨ ğ““ âŸ©[ wâ‚‚ ]
                                  Î² (i  , j)   âˆâŸ¨ ğ““ âŸ©))

 Ind-âˆ-is-directed : {I : ğ“¥ Ì‡ } (ğ“ : I â†’ Ind) (Î´ : is-directed _â‰²_ ğ“)
                   â†’ is-Directed ğ““ (underlying-family (Ind-âˆ ğ“ Î´))
 Ind-âˆ-is-directed ğ“ Î´ = prâ‚‚ (prâ‚‚ (Ind-âˆ ğ“ Î´))

 Ind-âˆ-is-upperbound : {I : ğ“¥ Ì‡ } (ğ“ : I â†’ Ind) (Î´ : is-directed _â‰²_ ğ“)
                     â†’ is-upperbound _â‰²_ (Ind-âˆ ğ“ Î´) ğ“
 Ind-âˆ-is-upperbound ğ“ Î´ i j =
  âˆ£ (i , j) , reflexivity ğ““ (prâ‚ (prâ‚‚ (ğ“ i)) j) âˆ£

 Ind-âˆ-is-lowerbound-of-upperbounds : {I : ğ“¥ Ì‡ } (ğ“ : I â†’ Ind)
                                      (Î´ : is-directed _â‰²_ ğ“)
                                    â†’ is-lowerbound-of-upperbounds _â‰²_
                                       (Ind-âˆ ğ“ Î´) ğ“
 Ind-âˆ-is-lowerbound-of-upperbounds {A} ğ“ _ _ ub (i , j) = ub i j

\end{code}

Taking suprema in our ğ“¥-dcpo ğ““ of directed families indexed into ğ““ defines a
monotone map from Ind to ğ““.

\begin{code}

 âˆ-map : Ind â†’ âŸ¨ ğ““ âŸ©
 âˆ-map (I , Î± , Î´) = âˆ ğ““ Î´

 â‰²-to-âŠ‘-of-âˆ : {I J : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} {Î² : J â†’ âŸ¨ ğ““ âŸ©}
               (Î´ : is-Directed ğ““ Î±) (Îµ : is-Directed ğ““ Î²)
             â†’ (I , Î± , Î´) â‰² (J , Î² , Îµ)
             â†’ âˆ ğ““ Î´ âŠ‘âŸ¨ ğ““ âŸ© âˆ ğ““ Îµ
 â‰²-to-âŠ‘-of-âˆ {I} {J} {Î±} {Î²} Î´ Îµ Î±-cofinal-in-Î² =
  âˆ-âŠ‘-if-cofinal ğ““ Î±-cofinal-in-Î² Î´ Îµ

 âˆ-map-is-monotone : (Î± Î² : Ind) â†’ Î± â‰² Î² â†’ âˆ-map Î± âŠ‘âŸ¨ ğ““ âŸ© âˆ-map Î²
 âˆ-map-is-monotone (I , Î± , Î´) (J , Î² , Îµ) = â‰²-to-âŠ‘-of-âˆ Î´ Îµ

\end{code}

Since we can view every element of ğ““ as a constant directed family into ğ““, we
also have a map in the other direction which comes in useful at times.

\begin{code}

 âŒ_âŒŸ : âŸ¨ ğ““ âŸ© â†’ (ğŸ™{ğ“¥} â†’ âŸ¨ ğ““ âŸ©)
 âŒ_âŒŸ x = Î» _ â†’ x

 âŒâŒŸ-is-directed : (x : âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ âŒ x âŒŸ
 âŒâŒŸ-is-directed x = âˆ£ â‹† âˆ£ , Ïƒ
  where
   Ïƒ : is-semidirected (underlying-order ğ““) (Î» _ â†’ x)
   Ïƒ i j = âˆ£ â‹† , reflexivity ğ““ x , reflexivity ğ““ x âˆ£

 Î¹ : âŸ¨ ğ““ âŸ© â†’ Ind
 Î¹ x = ğŸ™ , âŒ x âŒŸ , âŒâŒŸ-is-directed x

\end{code}

In our discussions on the notion of continuous dcpo we will be interested in
âˆ-map having a left adjoint, see ContinuityDiscussion.lagda.

We define what that means here and note that it is helpful to consider an
auxilliary relation between Ind(D) and D that we call "being left adjunct to",
because a map L : D â†’ Ind(D) is a left adjoint to âˆ-map precisely when L(x) is
left adjunct to x for every x : D.

\begin{code}

 _is-left-adjunct-to_ : Ind â†’ âŸ¨ ğ““ âŸ© â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 Î± is-left-adjunct-to x = (Î² : Ind) â†’ (Î± â‰² Î²) â†” (x âŠ‘âŸ¨ ğ““ âŸ© âˆ-map Î²)

 being-left-adjunct-to-is-prop : (Ïƒ : Ind) (x : âŸ¨ ğ““ âŸ©)
                               â†’ is-prop (Ïƒ is-left-adjunct-to x)
 being-left-adjunct-to-is-prop Ïƒ x =
  Î -is-prop fe (Î» Ï„ â†’ â†”-is-prop fe fe (â‰²-is-prop-valued Ïƒ Ï„)
                                      (prop-valuedness ğ““ x (âˆ-map Ï„)))

 left-adjoint-to-âˆ-map : (âŸ¨ ğ““ âŸ© â†’ Ind) â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 left-adjoint-to-âˆ-map L = (x : âŸ¨ ğ““ âŸ©) â†’ L x is-left-adjunct-to x

 being-left-adjoint-to-âˆ-map-is-prop : (L : âŸ¨ ğ““ âŸ© â†’ Ind)
                                     â†’ is-prop (left-adjoint-to-âˆ-map L)
 being-left-adjoint-to-âˆ-map-is-prop L =
  Î -is-prop fe (Î» x â†’ being-left-adjunct-to-is-prop (L x) x)

 âˆ-map-has-specified-left-adjoint : ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 âˆ-map-has-specified-left-adjoint = Î£ left-adjoint-to-âˆ-map

\end{code}

We can equivalently describe the adjoint-condition in terms of directed suprema
and the way-below relation.

\begin{code}

 _approximates_ : Ind â†’ âŸ¨ ğ““ âŸ© â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 (I , Î± , Î´) approximates x = (âˆ ğ““ Î´ ï¼ x) Ã— ((i : I) â†’ Î± i â‰ªâŸ¨ ğ““ âŸ© x)

 approximates-is-prop : (Ïƒ : Ind) (x : âŸ¨ ğ““ âŸ©) â†’ is-prop (Ïƒ approximates x)
 approximates-is-prop Ïƒ x =
  Ã—-is-prop (sethood ğ““) (Î -is-prop fe (Î» i â†’ â‰ª-is-prop-valued ğ““))

 is-approximating : (âŸ¨ ğ““ âŸ© â†’ Ind) â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ Ì‡
 is-approximating L = (x : âŸ¨ ğ““ âŸ©) â†’ (L x) approximates x

 left-adjunct-to-if-approximates : (Ïƒ : Ind) (x : âŸ¨ ğ““ âŸ©)
                                 â†’ Ïƒ approximates x â†’ Ïƒ is-left-adjunct-to x
 left-adjunct-to-if-approximates Ïƒ@(I , Î± , Î´) x (x-sup-of-Î± , Î±-way-below-x)
                                 Ï„@(J , Î² , Îµ) = â¦…â‡’â¦† , â¦…â‡â¦†
  where
   â¦…â‡’â¦† : Ïƒ â‰² Ï„ â†’ x âŠ‘âŸ¨ ğ““ âŸ© âˆ-map Ï„
   â¦…â‡’â¦† Î±-cofinal-in-Î² = transport (Î» - â†’ - âŠ‘âŸ¨ ğ““ âŸ© âˆ-map Ï„) x-sup-of-Î±
                        (â‰²-to-âŠ‘-of-âˆ Î´ Îµ Î±-cofinal-in-Î²)
   â¦…â‡â¦† : x âŠ‘âŸ¨ ğ““ âŸ© âˆ-map Ï„ â†’ Ïƒ â‰² Ï„
   â¦…â‡â¦† x-below-âˆÎ² i = Î±-way-below-x i J Î² Îµ x-below-âˆÎ²

 approximates-if-left-adjunct-to : (Ïƒ : Ind) (x : âŸ¨ ğ““ âŸ©)
                                 â†’ Ïƒ is-left-adjunct-to x
                                 â†’ Ïƒ approximates x
 approximates-if-left-adjunct-to Ïƒ@(I , Î± , Î´) x ladj =
  x-is-sup-of-Î± , Î±-way-below-x
   where
    Î±-way-below-x : (i : I) â†’ Î± i â‰ªâŸ¨ ğ““ âŸ© x
    Î±-way-below-x i J Î² Îµ x-below-âˆÎ² = h i
     where
      h : (I , Î± , Î´) â‰² (J , Î² , Îµ)
      h = rl-implication (ladj (J , Î² , Îµ)) x-below-âˆÎ²
    x-is-sup-of-Î± : âˆ ğ““ Î´ ï¼ x
    x-is-sup-of-Î± = antisymmetry ğ““ (âˆ ğ““ Î´) x â¦…1â¦† â¦…2â¦†
     where
      â¦…1â¦† : âˆ ğ““ Î´ âŠ‘âŸ¨ ğ““ âŸ© x
      â¦…1â¦† = âˆ-is-lowerbound-of-upperbounds ğ““ Î´ x
            (Î» i â†’ â‰ª-to-âŠ‘ ğ““ (Î±-way-below-x i))
      â¦…2â¦† : x âŠ‘âŸ¨ ğ““ âŸ© âˆ ğ““ Î´
      â¦…2â¦† = lr-implication (ladj Ïƒ) (â‰²-is-reflexive Ïƒ)

 approximate-left-adjunct-to-â‰ƒ : (Ïƒ : Ind) (x : âŸ¨ ğ““ âŸ©)
                               â†’ Ïƒ approximates x â‰ƒ Ïƒ is-left-adjunct-to x
 approximate-left-adjunct-to-â‰ƒ Ïƒ x = logically-equivalent-props-are-equivalent
                                      (approximates-is-prop Ïƒ x)
                                      (being-left-adjunct-to-is-prop Ïƒ x)
                                      (left-adjunct-to-if-approximates Ïƒ x)
                                      (approximates-if-left-adjunct-to Ïƒ x)

 left-adjoint-to-âˆ-map-characterization : (L : âŸ¨ ğ““ âŸ© â†’ Ind)
                                        â†’ is-approximating L
                                        â‰ƒ left-adjoint-to-âˆ-map L
 left-adjoint-to-âˆ-map-characterization L =
  Î -cong fe fe (Î» x â†’ approximate-left-adjunct-to-â‰ƒ (L x) x)

\end{code}

One may observe that the type (left-to-adjoint-to-âˆ-map L) does not require L to
be functorial/monotone, as would normally be required for an adjoint/Galois
connection. But this actually follows from the "hom-set" condition, as we show
now.

\begin{code}

 left-adjoint-to-âˆ-map-is-monotone : (L : âŸ¨ ğ““ âŸ© â†’ Ind)
                                   â†’ left-adjoint-to-âˆ-map L
                                   â†’ (x y : âŸ¨ ğ““ âŸ©)
                                   â†’ x âŠ‘âŸ¨ ğ““ âŸ© y
                                   â†’ L x â‰² L y
 left-adjoint-to-âˆ-map-is-monotone L L-left-adjoint x y x-below-y = Î³
  where
   Î³ : L x â‰² L y
   Î³ = rl-implication (L-left-adjoint x (L y)) x-below-âˆ-Ly
    where
     x-below-âˆ-Ly = x           âŠ‘âŸ¨ ğ““ âŸ©[ x-below-y             ]
                    y           âŠ‘âŸ¨ ğ““ âŸ©[ ï¼-to-âŠ’ ğ““ (prâ‚ approx) ]
                    âˆ-map (L y) âˆâŸ¨ ğ““ âŸ©
      where
       approx : L y approximates y
       approx = approximates-if-left-adjunct-to (L y) y (L-left-adjoint y)

\end{code}

Because Ind is a preorder and not a poset, the type expressing that âˆ-map has a
specified left adjoint is not a proposition, as the supposed left adjoint can
map elements of ğ““ to bicofinal (but nonequal) directed families.

We could take the poset reflection Ind/â‰ˆ of Ind and ask that the map Ind/â‰ˆ â†’ ğ““
induced by the supremum-map Ind â†’ ğ““ has a left adjoint to obtain a type that is
a proposition. We describe that process here.

This is *not* the same as asking that âˆ-map : Ind â†’ ğ““ has an unspecified left
adjoint, as we explain in ContinuityDiscussion.lagda.

\begin{code}

module Ind-completion-poset-reflection
        (pe : Prop-Ext)
        (ğ““ : DCPO {ğ“¤} {ğ“£})
       where

 open Ind-completion ğ““

 open import OrderedTypes.PosetReflection pt fe pe
 open poset-reflection Ind _â‰²_ â‰²-is-prop-valued â‰²-is-reflexive â‰²-is-transitive public

 Ind/â‰ˆ : ğ“¥ âº âŠ” ğ“£ âº âŠ” ğ“¤ Ì‡
 Ind/â‰ˆ = poset-reflection-carrier

 Ind/â‰ˆ-is-set : is-set Ind/â‰ˆ
 Ind/â‰ˆ-is-set = poset-reflection-is-set

 âˆ-map/-specification :
   âˆƒ! fÌƒ ê‰ (Ind/â‰ˆ â†’ âŸ¨ ğ““ âŸ©) , ((Ïƒ' Ï„' : Ind/â‰ˆ) â†’ Ïƒ' â‰¤ Ï„' â†’ fÌƒ Ïƒ' âŠ‘âŸ¨ ğ““ âŸ© fÌƒ Ï„')
                          Ã— (fÌƒ âˆ˜ Î· âˆ¼ âˆ-map)
 âˆ-map/-specification =
  universal-property (underlying-order ğ““) (sethood ğ““) (prop-valuedness ğ““)
                     (reflexivity ğ““) (transitivity ğ““) (antisymmetry ğ““)
                     âˆ-map âˆ-map-is-monotone

 âˆ-map/ : Ind/â‰ˆ â†’ âŸ¨ ğ““ âŸ©
 âˆ-map/ = âˆƒ!-witness âˆ-map/-specification

 âˆ-map/-triangle : (Î± : Ind) â†’ âˆ-map/ (Î· Î±) ï¼ âˆ-map Î±
 âˆ-map/-triangle = prâ‚‚ (âˆƒ!-is-witness âˆ-map/-specification)

 left-adjoint-to-âˆ-map/ : (âŸ¨ ğ““ âŸ© â†’ Ind/â‰ˆ)
                        â†’ ğ“¥ âº âŠ” ğ“£ âº âŠ” ğ“¤ Ì‡
 left-adjoint-to-âˆ-map/ L' =
  (x : âŸ¨ ğ““ âŸ©) (Î±' : Ind/â‰ˆ) â†’ (L' x â‰¤ Î±') â†” (x âŠ‘âŸ¨ ğ““ âŸ© âˆ-map/ Î±')

 being-left-adjoint-to-âˆ-map/-is-prop : (L' : âŸ¨ ğ““ âŸ© â†’ Ind/â‰ˆ)
                                      â†’ is-prop (left-adjoint-to-âˆ-map/ L')
 being-left-adjoint-to-âˆ-map/-is-prop L' =
  Î â‚‚-is-prop fe (Î» x Î±' â†’ Ã—-is-prop
                           (Î -is-prop fe (Î» _ â†’ prop-valuedness ğ““ x (âˆ-map/ Î±')))
                           (Î -is-prop fe (Î» _ â†’ â‰¤-is-prop-valued (L' x) Î±')))

 âˆ-map/-has-specified-left-adjoint : ğ“¥ âº âŠ” ğ“£ âº âŠ” ğ“¤ Ì‡
 âˆ-map/-has-specified-left-adjoint = Î£ left-adjoint-to-âˆ-map/

 âˆ-map/-having-left-adjoint-is-prop : is-prop âˆ-map/-has-specified-left-adjoint
 âˆ-map/-having-left-adjoint-is-prop
  (L , L-is-left-adjoint) (L' , L'-is-left-adjoint) =
   to-subtype-ï¼ being-left-adjoint-to-âˆ-map/-is-prop
                (dfunext fe (Î» x â†’ â‰¤-is-antisymmetric (L x) (L' x)
                  (rl-implication (L-is-left-adjoint x (L' x))
                                  (lr-implication (L'-is-left-adjoint x (L' x))
                                    (â‰¤-is-reflexive (L' x))))
                  (rl-implication (L'-is-left-adjoint x (L x))
                                  (lr-implication (L-is-left-adjoint x (L x))
                                    (â‰¤-is-reflexive (L x))))))

\end{code}
