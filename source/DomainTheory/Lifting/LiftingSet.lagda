sTom de Jong, 27 May 2019.
Refactored 29 April 2020.

We show that lifting (cf. EscardÃ³-Knapp) a set gives the free pointed dcpo on
that set.

When we start with a small set, then the lifting yields an algebraic pointed
dcpo as formalized in LiftingSetAlgebraic.lagda.

The construction that freely adds a least element to a dcpo is described in
LiftingDcpo.lagda.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons

module DomainTheory.Lifting.LiftingSet
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“£ : Universe)
        (pe : propext ğ“£)
       where

open import UF.Equiv
open import UF.Hedberg
open import UF.ImageAndSurjection pt
open import UF.Sets
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties

open PropositionalTruncation pt

open import Lifting.Lifting ğ“£ hiding (âŠ¥)
open import Lifting.Miscelanea ğ“£
open import Lifting.Miscelanea-PropExt-FunExt ğ“£ pe fe
open import Lifting.Monad ğ“£

open import DomainTheory.Basics.Dcpo pt fe ğ“£
open import DomainTheory.Basics.Miscelanea pt fe ğ“£
open import DomainTheory.Basics.Pointed pt fe ğ“£

open import OrderedTypes.Poset fe

\end{code}

We start by showing that the lifting of a set is indeed a pointed dcpo.

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (s : is-set X)
       where

 family-value-map : {I : ğ“£ Ì‡ }
                  â†’ (Î± : I â†’ ğ“› X)
                  â†’ (Î£ i ê‰ I , is-defined (Î± i)) â†’ X
 family-value-map Î± (i , d) = value (Î± i) d

 directed-family-value-map-is-wconstant : {I : ğ“£ Ì‡ }
                                        â†’ (Î± : I â†’ ğ“› X)
                                        â†’ (Î´ : is-directed _âŠ‘'_ Î± )
                                        â†’ wconstant (family-value-map Î±)
 directed-family-value-map-is-wconstant {I} Î± Î´ (iâ‚€ , dâ‚€) (iâ‚ , dâ‚) =
  Î³ (semidirected-if-directed _âŠ‘'_ Î± Î´ iâ‚€ iâ‚)
   where
    f : (Î£ i ê‰ I , is-defined (Î± i)) â†’ X
    f = family-value-map Î±
    Î³ : (âˆƒ k ê‰ I , (Î± iâ‚€ âŠ‘' Î± k) Ã— (Î± iâ‚ âŠ‘' Î± k)) â†’ f (iâ‚€ , dâ‚€) ï¼ f (iâ‚ , dâ‚)
    Î³ = âˆ¥âˆ¥-rec s g
     where
      g : (Î£ k ê‰ I , (Î± iâ‚€ âŠ‘' Î± k)
                   Ã— (Î± iâ‚ âŠ‘' Î± k)) â†’ f (iâ‚€ , dâ‚€) ï¼ f (iâ‚ , dâ‚)
      g (k , l , m) =
       f (iâ‚€ , dâ‚€)                             ï¼âŸ¨ refl âŸ©
       value (Î± iâ‚€) dâ‚€                         ï¼âŸ¨ ï¼-of-values-from-ï¼ (l dâ‚€) âŸ©
       value (Î± k) (ï¼-to-is-defined (l dâ‚€) dâ‚€) ï¼âŸ¨ ï¼-of-values-from-ï¼ ((m dâ‚) â»Â¹) âŸ©
       value (Î± iâ‚) dâ‚                         ï¼âŸ¨ refl âŸ©
       f (iâ‚ , dâ‚)                             âˆ

 lifting-sup-value : {I : ğ“£ Ì‡ }
                   â†’ (Î± : I â†’ ğ“› X)
                   â†’ (Î´ : is-directed _âŠ‘'_ Î± )
                   â†’ (âˆƒ i ê‰ I , is-defined (Î± i)) â†’ X
 lifting-sup-value {I} Î± Î´ =
  prâ‚ (wconstant-map-to-set-factors-through-truncation-of-domain
        s (family-value-map Î±)
        (directed-family-value-map-is-wconstant Î± Î´))

 lifting-sup : {I : ğ“£ Ì‡ } â†’ (Î± : I â†’ ğ“› X) â†’ (Î´ : is-directed _âŠ‘'_ Î±) â†’ ğ“› X
 lifting-sup {I} Î± Î´ =
  (âˆƒ i ê‰ I , is-defined (Î± i)) , lifting-sup-value Î± Î´ , âˆ¥âˆ¥-is-prop

 lifting-sup-is-upperbound : {I : ğ“£ Ì‡ } â†’ (Î± : I â†’ ğ“› X)
                             (Î´ : is-directed _âŠ‘'_ Î±)
                           â†’ (i : I) â†’ Î± i âŠ‘' lifting-sup Î± Î´
 lifting-sup-is-upperbound {I} Î± Î´ i = Î³
  where
   Î³ : Î± i âŠ‘' lifting-sup Î± Î´
   Î³ = âŠ‘-to-âŠ‘' (f , v)
    where
     f : is-defined (Î± i) â†’ is-defined (lifting-sup Î± Î´)
     f d = âˆ£ i , d âˆ£
     v : (d : is-defined (Î± i)) â†’ value (Î± i) d ï¼ value (lifting-sup Î± Î´) (f d)
     v d = value (Î± i) d                 ï¼âŸ¨ p    âŸ©
           lifting-sup-value Î± Î´ (f d)   ï¼âŸ¨ refl âŸ©
           value (lifting-sup Î± Î´) (f d) âˆ
      where
       p = (prâ‚‚ (wconstant-map-to-set-factors-through-truncation-of-domain
                  s (family-value-map Î±)
                  (directed-family-value-map-is-wconstant Î± Î´)))
           (i , d)

 family-defined-somewhere-sup-ï¼ : {I : ğ“£ Ì‡ } {Î± : I â†’ ğ“› X}
                                â†’ (Î´ : is-directed _âŠ‘'_ Î±)
                                â†’ (i : I)
                                â†’ is-defined (Î± i)
                                â†’ Î± i ï¼ lifting-sup Î± Î´
 family-defined-somewhere-sup-ï¼ {I} {Î±} Î´ i d =
  (lifting-sup-is-upperbound Î± Î´ i) d

 lifting-sup-is-lowerbound-of-upperbounds : {I : ğ“£ Ì‡ }
                                          â†’ {Î± : I â†’ ğ“› X}
                                          â†’ (Î´ : is-directed _âŠ‘'_ Î±)
                                          â†’ (v : ğ“› X)
                                          â†’ ((i : I) â†’ Î± i âŠ‘' v)
                                          â†’ lifting-sup Î± Î´ âŠ‘' v
 lifting-sup-is-lowerbound-of-upperbounds {I} {Î±} Î´ v b = h
  where
   h : lifting-sup Î± Î´ âŠ‘' v
   h d = âˆ¥âˆ¥-rec (lifting-of-set-is-set s) g d
    where
     g : (Î£ i ê‰ I , is-defined (Î± i)) â†’ lifting-sup Î± Î´ ï¼ v
     g (i , dáµ¢) = lifting-sup Î± Î´ ï¼âŸ¨ (family-defined-somewhere-sup-ï¼ Î´ i dáµ¢) â»Â¹ âŸ©
                  Î± i             ï¼âŸ¨ b i dáµ¢ âŸ©
                  v               âˆ

 ğ“›-DCPO : DCPO {ğ“£ âº âŠ” ğ“¤} {ğ“£ âº âŠ” ğ“¤}
 ğ“›-DCPO = ğ“› X , _âŠ‘'_ , pa , c
  where
   pa : PosetAxioms.poset-axioms _âŠ‘'_
   pa = sl , p , r , t , a
    where
     open PosetAxioms {_} {_} {ğ“› X} _âŠ‘'_
     sl : is-set (ğ“› X)
     sl = lifting-of-set-is-set s
     p : is-prop-valued
     p _ _ = âŠ‘'-prop-valued s
     r : is-reflexive
     r _ = âŠ‘'-is-reflexive
     a : is-antisymmetric
     a _ _ = âŠ‘'-is-antisymmetric
     t : is-transitive
     t _ _ _ = âŠ‘'-is-transitive
   c : (I : ğ“£ Ì‡ ) (Î± : I â†’ ğ“› X) â†’ is-directed _âŠ‘'_ Î± â†’ has-sup _âŠ‘'_ Î±
   c I Î± Î´ = lifting-sup Î± Î´ ,
             lifting-sup-is-upperbound Î± Î´ ,
             lifting-sup-is-lowerbound-of-upperbounds Î´

 ğ“›-DCPOâŠ¥ : DCPOâŠ¥ {ğ“£ âº âŠ” ğ“¤} {ğ“£ âº âŠ” ğ“¤}
 ğ“›-DCPOâŠ¥ = ğ“›-DCPO , l , (Î» _ â†’ unique-from-ğŸ˜)
  where
   l : ğ“› X
   l = (ğŸ˜ , ğŸ˜-elim , ğŸ˜-is-prop)

\end{code}

Now that we have the lifting as a dcpo, we prove that the lifting functor and
Kleisli extension yield continuous maps.

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (sâ‚€ : is-set X)
         {ğ“¥ : Universe}
         {Y : ğ“¥ Ì‡ }
         (sâ‚ : is-set Y)
       where

 â™¯-is-monotone : (f : X â†’ ğ“› Y) â†’ is-monotone (ğ“›-DCPO sâ‚€) (ğ“›-DCPO sâ‚) (f â™¯)
 â™¯-is-monotone f l m ineq d = ap (f â™¯) (ineq (â™¯-is-defined f l d))

 â™¯-is-continuous : (f : X â†’ ğ“› Y) â†’ is-continuous (ğ“›-DCPO sâ‚€) (ğ“›-DCPO sâ‚) (f â™¯)
 â™¯-is-continuous f I Î± Î´ = u , v
  where
   u : (i : I) â†’ (f â™¯) (Î± i) âŠ‘âŸ¨ (ğ“›-DCPO sâ‚) âŸ© (f â™¯) (âˆ (ğ“›-DCPO sâ‚€) Î´)
   u i = â™¯-is-monotone f (Î± i) (âˆ (ğ“›-DCPO sâ‚€) Î´)
         (âˆ-is-upperbound (ğ“›-DCPO sâ‚€) Î´ i)
   v : (m : âŸ¨ ğ“›-DCPO sâ‚ âŸ©)
     â†’ ((i : I) â†’ (f â™¯) (Î± i) âŠ‘âŸ¨ (ğ“›-DCPO sâ‚) âŸ© m)
     â†’ (f â™¯) (âˆ (ğ“›-DCPO sâ‚€) Î´) âŠ‘âŸ¨ (ğ“›-DCPO sâ‚) âŸ© m
   v m ineqs d =
    âˆ¥âˆ¥-rec (lifting-of-set-is-set sâ‚) g (â™¯-is-defined f (âˆ (ğ“›-DCPO sâ‚€) Î´) d)
     where
      g : (Î£ i ê‰ I , is-defined (Î± i)) â†’ (f â™¯) (âˆ (ğ“›-DCPO sâ‚€) Î´) ï¼ m
      g (i , dáµ¢) = (f â™¯) (âˆ (ğ“›-DCPO sâ‚€) Î´) ï¼âŸ¨ h i dáµ¢ âŸ©
                   (f â™¯) (Î± i)             ï¼âŸ¨ ineqs i (ï¼-to-is-defined (h i dáµ¢) d) âŸ©
                   m                       âˆ
       where
        h : (i : I) â†’ is-defined (Î± i) â†’ (f â™¯) (âˆ (ğ“›-DCPO sâ‚€) Î´) ï¼ (f â™¯) (Î± i)
        h i d = ap (f â™¯) ((family-defined-somewhere-sup-ï¼ sâ‚€ Î´ i d) â»Â¹)

 ğ“›Ì‡-continuous : (f : X â†’ Y) â†’ is-continuous (ğ“›-DCPO sâ‚€) (ğ“›-DCPO sâ‚) (ğ“›Ì‡ f)
 ğ“›Ì‡-continuous f = transport
                   (is-continuous (ğ“›-DCPO sâ‚€) (ğ“›-DCPO sâ‚))
                   (dfunext fe (ğ“›Ì‡-â™¯-âˆ¼ f))
                   (â™¯-is-continuous (Î· âˆ˜ f))

\end{code}

Finally we show that the lifting of a set gives the free pointed dcpo on that
set. The main technical tool in proving this is the use of subsingleton suprema,
cf. DomainTheory.Basics.Pointed.lagda, and the fact that every partial element
can be expressed as such a supremum.

\begin{code}

module lifting-is-free-pointed-dcpo-on-set
         {X : ğ“¤ Ì‡ }
         (X-is-set : is-set X)
         (ğ““ : DCPOâŠ¥ {ğ“¥} {ğ“¦})
         (f : X â†’ âŸª ğ““ âŸ«)
       where

 ğ“›X : DCPOâŠ¥ {ğ“£ âº âŠ” ğ“¤} {ğ“£ âº âŠ” ğ“¤}
 ğ“›X = ğ“›-DCPOâŠ¥ X-is-set

 fÌƒ : âŸª ğ“›X âŸ« â†’ âŸª ğ““ âŸ«
 fÌƒ (P , Ï• , P-is-prop) = âˆË¢Ë¢ ğ““ (f âˆ˜ Ï•) P-is-prop

 fÌƒ-is-strict : is-strict ğ“›X ğ““ fÌƒ
 fÌƒ-is-strict = strictness-criterion ğ“›X ğ““ fÌƒ Î³
  where
   Î³ : fÌƒ (âŠ¥ ğ“›X) âŠ‘âŸª ğ““ âŸ« âŠ¥ ğ““
   Î³ = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ““
        (f âˆ˜ unique-from-ğŸ˜) ğŸ˜-is-prop (âŠ¥ ğ““) ğŸ˜-induction

 fÌƒ-is-continuous : is-continuous (ğ“›X â») (ğ““ â») fÌƒ
 fÌƒ-is-continuous I Î± Î´ = ub , lb-of-ubs
  where
   s : ğ“› X
   s = âˆ (ğ“›X â») Î´
   Ï : (l : ğ“› X) â†’ is-prop (is-defined l)
   Ï = being-defined-is-prop
   lemma : (i : I) (p : is-defined (Î± i))
         â†’ value (Î± i) p ï¼ value s âˆ£ i , p âˆ£
   lemma i p = ï¼-of-values-from-ï¼
                (family-defined-somewhere-sup-ï¼ X-is-set Î´ i p)
   ub : (i : I) â†’ fÌƒ (Î± i) âŠ‘âŸª ğ““ âŸ« fÌƒ s
   ub i = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ““ (f âˆ˜ value (Î± i)) (Ï (Î± i)) (fÌƒ s) Î³
    where
     Î³ : (p : is-defined (Î± i))
       â†’ f (value (Î± i) p) âŠ‘âŸª ğ““ âŸ« fÌƒ s
     Î³ p = f (value (Î± i) p)     âŠ‘âŸª ğ““ âŸ«[ â¦…1â¦† ]
           f (value s âˆ£ i , p âˆ£) âŠ‘âŸª ğ““ âŸ«[ â¦…2â¦† ]
           fÌƒ s                   âˆâŸª ğ““ âŸ«
      where
       â¦…1â¦† = ï¼-to-âŠ‘ (ğ““ â») (ap f (lemma i p))
       â¦…2â¦† = âˆË¢Ë¢-is-upperbound ğ““ (f âˆ˜ value s) (Ï s) âˆ£ i , p âˆ£
   lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order (ğ““ â»))
                (fÌƒ s) (fÌƒ âˆ˜ Î±)
   lb-of-ubs y y-is-ub = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ““ (f âˆ˜ value s) (Ï s)
                          y Î³
    where
     Î³ : (q : is-defined s)
       â†’ (f (value s q)) âŠ‘âŸª ğ““ âŸ« y
     Î³ q = âˆ¥âˆ¥-rec (prop-valuedness (ğ““ â») (f (value s q)) y) r q
      where
       r : (Î£ i ê‰ I , is-defined (Î± i)) â†’ f (value s q) âŠ‘âŸª ğ““ âŸ« y
       r (i , p) = f (value s q)                     âŠ‘âŸª ğ““ âŸ«[ â¦…1â¦†       ]
                   f (value s âˆ£ i , p âˆ£)             âŠ‘âŸª ğ““ âŸ«[ â¦…2â¦†       ]
                   f (value (Î± i) p)                 âŠ‘âŸª ğ““ âŸ«[ â¦…3â¦†       ]
                   âˆË¢Ë¢ ğ““ (f âˆ˜ value (Î± i)) (Ï (Î± i)) âŠ‘âŸª ğ““ âŸ«[ y-is-ub i ]
                   y                                 âˆâŸª ğ““ âŸ«
        where
         â¦…1â¦† = ï¼-to-âŠ‘ (ğ““ â») (ap f (value-is-constant s q âˆ£ i , p âˆ£))
         â¦…2â¦† = ï¼-to-âŠ’ (ğ““ â») (ap f (lemma i p))
         â¦…3â¦† = âˆË¢Ë¢-is-upperbound ğ““ (f âˆ˜ value (Î± i)) (being-defined-is-prop (Î± i)) p

 fÌƒ-after-Î·-is-f : fÌƒ âˆ˜ Î· âˆ¼ f
 fÌƒ-after-Î·-is-f x = antisymmetry (ğ““ â») (fÌƒ (Î· x)) (f x) u v
  where
   u : fÌƒ (Î· x) âŠ‘âŸª ğ““ âŸ« f x
   u = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ““ (f âˆ˜ (Î» _ â†’ x)) ğŸ™-is-prop
        (f x) (Î» _ â†’ reflexivity (ğ““ â») (f x))
   v : f x âŠ‘âŸª ğ““ âŸ« fÌƒ (Î· x)
   v = âˆË¢Ë¢-is-upperbound ğ““ (Î» _ â†’ f x) ğŸ™-is-prop â‹†

 all-partial-elements-are-subsingleton-sups :
    (l : âŸª ğ“›X âŸ«)
  â†’ l ï¼ âˆË¢Ë¢ ğ“›X (Î· âˆ˜ value l) (being-defined-is-prop l)
 all-partial-elements-are-subsingleton-sups (P , Ï• , Ï) =
  antisymmetry (ğ“›X â») (P , Ï• , Ï) (âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï) u v
   where
    v : âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï âŠ‘' (P , Ï• , Ï)
    v = âˆË¢Ë¢-is-lowerbound-of-upperbounds ğ“›X (Î· âˆ˜ Ï•) Ï (P , Ï• , Ï)
         (Î» p â‹† â†’ (is-defined-Î·-ï¼ p) â»Â¹)
    u : (P , Ï• , Ï) âŠ‘' âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï
    u p = antisymmetry (ğ“›X â») (P , Ï• , Ï) (âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï)
           u' v
     where
      u' = (P , Ï• , Ï)      âŠ‘âŸª ğ“›X âŸ«[ ï¼-to-âŠ‘ (ğ“›X â») (is-defined-Î·-ï¼ p) ]
           Î· (Ï• p)          âŠ‘âŸª ğ“›X âŸ«[ âˆË¢Ë¢-is-upperbound ğ“›X (Î· âˆ˜ Ï•) Ï p ]
           âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï âˆâŸª ğ“›X âŸ«

 fÌƒ-is-unique : (g : âŸª ğ“›X âŸ« â†’ âŸª ğ““ âŸ«)
             â†’ is-continuous (ğ“›X â») (ğ““ â») g
             â†’ is-strict ğ“›X ğ““ g
             â†’ g âˆ˜ Î· ï¼ f
             â†’ g âˆ¼ fÌƒ
 fÌƒ-is-unique g con str eq (P , Ï• , Ï) = g (P , Ï• , Ï)        ï¼âŸ¨ â¦…1â¦†  âŸ©
                                        g (âˆË¢Ë¢ ğ“›X (Î· âˆ˜ Ï•) Ï) ï¼âŸ¨ â¦…2â¦†  âŸ©
                                        âˆË¢Ë¢ ğ““ (g âˆ˜ Î· âˆ˜ Ï•) Ï  ï¼âŸ¨ â¦…3â¦†  âŸ©
                                        âˆË¢Ë¢ ğ““ (f âˆ˜ Ï•) Ï      ï¼âŸ¨ refl âŸ©
                                        fÌƒ (P , Ï• , Ï)        âˆ
   where
    â¦…1â¦† = ap g (all-partial-elements-are-subsingleton-sups (P , Ï• , Ï))
    â¦…2â¦† = âˆË¢Ë¢-ï¼-if-continuous-and-strict ğ“›X ğ““ g con str (Î· âˆ˜ Ï•) Ï
    â¦…3â¦† = âˆË¢Ë¢-family-ï¼ ğ““ Ï (ap (_âˆ˜ Ï•) eq)

 ğ“›-gives-the-free-pointed-dcpo-on-a-set :
  âˆƒ! h ê‰ (âŸª ğ“›X âŸ« â†’ âŸª ğ““ âŸ«) , is-continuous (ğ“›X â») (ğ““ â») h
                          Ã— is-strict ğ“›X ğ““ h
                          Ã— (h âˆ˜ Î· ï¼ f)
 ğ“›-gives-the-free-pointed-dcpo-on-a-set =
  (fÌƒ , fÌƒ-is-continuous , fÌƒ-is-strict , (dfunext fe fÌƒ-after-Î·-is-f)) , Î³
   where
    Î³ : is-central (Î£ h ê‰ (âŸª ğ“›X âŸ« â†’ âŸª ğ““ âŸ«) , is-continuous (ğ“›X â») (ğ““ â») h
                                           Ã— is-strict ğ“›X ğ““ h
                                           Ã— (h âˆ˜ Î· ï¼ f))
         (fÌƒ , fÌƒ-is-continuous , fÌƒ-is-strict , dfunext fe fÌƒ-after-Î·-is-f)
    Î³ (g , cont , str , eq) =
     to-subtype-ï¼ (Î» h â†’ Ã—â‚ƒ-is-prop (being-continuous-is-prop (ğ“›X â») (ğ““ â») h)
                                    (being-strict-is-prop ğ“›X ğ““ h)
                                    (equiv-to-prop
                                      (â‰ƒ-funext fe (h âˆ˜ Î·) f)
                                      (Î -is-prop fe (Î» _ â†’ sethood (ğ““ â»)))))
                                    ((dfunext fe (fÌƒ-is-unique g cont str eq)) â»Â¹)

\end{code}

In general, the lifting of a set is only directed complete and does not have all
(small) sups, but if we lift propositions, then we do get all small suprema.

As an application, we use this to prove that ğ““âˆ is algebraic in
DomainTheory.Bilimits.Dinfinity.lagda.

\begin{code}

open import DomainTheory.Basics.SupComplete pt fe ğ“£

module _
        {P : ğ“¤ Ì‡ }
        (P-is-prop : is-prop P)
       where

 private
  ğ“›P :  DCPO {ğ“£ âº âŠ” ğ“¤} {ğ“£ âº âŠ” ğ“¤}
  ğ“›P = ğ“›-DCPO (props-are-sets (P-is-prop))

 lifting-of-prop-is-sup-complete : is-sup-complete ğ“›P
 lifting-of-prop-is-sup-complete = record { â‹ = sup ; â‹-is-sup = lemma }
  where
   sup-map : {I : ğ“£ Ì‡ } (Î± : I â†’ âŸ¨ ğ“›P âŸ©) â†’ (âˆƒ i ê‰ I , is-defined (Î± i)) â†’ P
   sup-map Î± = âˆ¥âˆ¥-rec P-is-prop (Î» (i , q) â†’ value (Î± i) q)
   sup : {I : ğ“£ Ì‡ } (Î± : I â†’ âŸ¨ ğ“›P âŸ©) â†’ âŸ¨ ğ“›P âŸ©
   sup {I} Î± = ((âˆƒ i ê‰ I , is-defined (Î± i)) , sup-map Î± , âˆƒ-is-prop)
   lemma : {I : ğ“£ Ì‡ } (Î± : I â†’ âŸ¨ ğ“›P âŸ©) â†’ is-sup _âŠ‘'_ (sup Î±) Î±
   lemma {I} Î± = (ub , lb-of-ubs)
    where
     ub : (i : I) â†’ Î± i âŠ‘' sup Î±
     ub i = âŠ‘-to-âŠ‘' (f , g)
      where
       f : is-defined (Î± i) â†’ âˆƒ i ê‰ I , is-defined (Î± i)
       f p = âˆ£ i , p âˆ£
       g : value (Î± i) âˆ¼ (Î» q â†’ sup-map Î± âˆ£ i , q âˆ£)
       g q = P-is-prop (value (Î± i) q) (sup-map Î± âˆ£ i , q âˆ£)
     lb-of-ubs : is-lowerbound-of-upperbounds _âŠ‘'_ (sup Î±) Î±
     lb-of-ubs l l-is-ub = âŠ‘-to-âŠ‘' (f , g)
      where
       f : (âˆƒ i ê‰ I , is-defined (Î± i)) â†’ is-defined l
       f = âˆ¥âˆ¥-rec (being-defined-is-prop l) h
        where
         h : (Î£ i ê‰ I , is-defined (Î± i)) â†’ is-defined l
         h (i , q) = ï¼-to-is-defined (l-is-ub i q) q
       g : sup-map Î± âˆ¼ (Î» q â†’ value l (f q))
       g q = P-is-prop (sup-map Î± q) (value l (f q))

\end{code}
