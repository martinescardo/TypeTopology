Tom de Jong, 5 May 2020 - 10 May 2020

We construct bilimits of diagrams indexed by directed posets in the category of
dcpos as objects and embedding-projection pairs as morphisms.

This formalization is based on Scott's "Continuous lattices"
(doi:10.1007/BFB0073967), specifically pages 124--126, but generalizes it from
â„•-indexed diagrams to diagrams indexed by a directed poset.

We specialize to â„•-indexed diagrams in Sequential.lagda.

We also prove that taking the bilmit preserves local smallness and that it is
closed under structural continuity/algebraicity and having a small (compact) basis.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

\end{code}

We use the flag --lossy-unification to speed up the type-checking.

This flag was kindly implemented by Andrea Vezzosi upon request.

Documentation for the flag (written by Andrea Vezzosi) can be found here:
https://agda.readthedocs.io/en/latest/language/lossy-unification.html

The most important takeaway from the documentation is that the flag is sound:

  "[...] if Agda accepts code with the flag enabled it should also accept it
  without the flag (with enough resources, and possibly needing extra type
  annotations)."

A related issue (originally opened by Wolfram Kahl in 2015) can be found here:
https://github.com/agda/agda/issues/1625

\begin{code}

open import MLTT.Spartan hiding (J)
open import UF.FunExt
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties

module DomainTheory.Bilimits.Directed
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe)
        (ğ“¤ ğ“£ : Universe)
       where

open PropositionalTruncation pt

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Hedberg
open import UF.ImageAndSurjection pt
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open import OrderedTypes.Poset fe

open import DomainTheory.Basics.Dcpo pt fe ğ“¥
open import DomainTheory.Basics.Exponential pt fe ğ“¥
open import DomainTheory.Basics.Miscelanea pt fe ğ“¥
open import DomainTheory.Basics.WayBelow pt fe ğ“¥

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¥
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¥

module Diagram
        {I : ğ“¥ Ì‡ }
        (_âŠ‘_ : I â†’ I â†’ ğ“¦ Ì‡ )
        (âŠ‘-refl : {i : I} â†’ i âŠ‘ i)
        (âŠ‘-trans : {i j k : I} â†’ i âŠ‘ j â†’ j âŠ‘ k â†’ i âŠ‘ k)
        (âŠ‘-prop-valued : (i j : I) â†’ is-prop (i âŠ‘ j))
        (I-inhabited : âˆ¥ I âˆ¥)
        (I-semidirected : (i j : I) â†’ âˆƒ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
        (ğ““ : I â†’ DCPO {ğ“¤} {ğ“£})
        (Îµ : {i j : I} â†’ i âŠ‘ j â†’ âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ““ j âŸ©)
        (Ï€ : {i j : I} â†’ i âŠ‘ j â†’ âŸ¨ ğ““ j âŸ© â†’ âŸ¨ ğ““ i âŸ©)
        (ÎµÏ€-deflation : {i j : I} (l : i âŠ‘ j) (x : âŸ¨ ğ““ j âŸ©)
                      â†’ Îµ l (Ï€ l x) âŠ‘âŸ¨ ğ““ j âŸ© x )
        (Îµ-section-of-Ï€ : {i j : I} (l : i âŠ‘ j) â†’ Ï€ l âˆ˜ Îµ l âˆ¼ id )
        (Îµ-is-continuous : {i j : I} (l : i âŠ‘ j)
                         â†’ is-continuous (ğ““ i) (ğ““ j) (Îµ {i} {j} l))
        (Ï€-is-continuous : {i j : I} (l : i âŠ‘ j)
                         â†’ is-continuous (ğ““ j) (ğ““ i) (Ï€ {i} {j} l))
        (Îµ-id : (i : I ) â†’ Îµ (âŠ‘-refl {i}) âˆ¼ id)
        (Ï€-id : (i : I ) â†’ Ï€ (âŠ‘-refl {i}) âˆ¼ id)
        (Îµ-comp : {i j k : I} (l : i âŠ‘ j) (m : j âŠ‘ k)
                â†’ Îµ m âˆ˜ Îµ l âˆ¼ Îµ (âŠ‘-trans l m))
        (Ï€-comp : {i j k : I} (l : i âŠ‘ j) (m : j âŠ‘ k)
                â†’ Ï€ l âˆ˜ Ï€ m âˆ¼ Ï€ (âŠ‘-trans l m))
       where

 ğ““âˆ-carrier : ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦ Ì‡
 ğ““âˆ-carrier =
  Î£ Ïƒ ê‰ ((i : I) â†’ âŸ¨ ğ““ i âŸ©) , ((i j : I) (l : i âŠ‘ j) â†’ Ï€ l (Ïƒ j) ï¼ Ïƒ i)

 â¦…_â¦† : ğ““âˆ-carrier â†’ (i : I) â†’ âŸ¨ ğ““ i âŸ©
 â¦…_â¦† = prâ‚

 Ï€-equality : (Ïƒ : ğ““âˆ-carrier) {i j : I} (l : i âŠ‘ j) â†’ Ï€ l (â¦… Ïƒ â¦† j) ï¼ â¦… Ïƒ â¦† i
 Ï€-equality Ïƒ {i} {j} l = prâ‚‚ Ïƒ i j l

 to-ğ““âˆ-ï¼ : {Ïƒ Ï„ : ğ““âˆ-carrier} â†’ ((i : I) â†’ â¦… Ïƒ â¦† i ï¼ â¦… Ï„ â¦† i) â†’ Ïƒ ï¼ Ï„
 to-ğ““âˆ-ï¼ h =
  to-subtype-ï¼ (Î» Ïƒ â†’ Î â‚ƒ-is-prop fe (Î» i j l â†’ sethood (ğ““ i))) (dfunext fe h)

 family-at-ith-component : {ğ“ : ğ“¥ Ì‡ } (Î± : ğ“ â†’ ğ““âˆ-carrier) (i : I) â†’ ğ“ â†’ âŸ¨ ğ““ i âŸ©
 family-at-ith-component Î± i a = â¦… Î± a â¦† i

 _â‰¼_ : ğ““âˆ-carrier â†’ ğ““âˆ-carrier â†’ ğ“¥ âŠ” ğ“£ Ì‡
 Ïƒ â‰¼ Ï„ = (i : I) â†’ â¦… Ïƒ â¦† i âŠ‘âŸ¨ ğ““ i âŸ© â¦… Ï„ â¦† i

 family-at-ith-component-is-directed : {ğ“ : ğ“¥ Ì‡ } (Î± : ğ“ â†’ ğ““âˆ-carrier)
                                       (Î´ : is-directed _â‰¼_ Î±) (i : I)
                                     â†’ is-Directed (ğ““ i)
                                        (family-at-ith-component Î± i)
 family-at-ith-component-is-directed {ğ“} Î± Î´ i =
  (inhabited-if-directed _â‰¼_ Î± Î´) , Î³
   where
    Î² : (i : I) â†’ ğ“ â†’ âŸ¨ ğ““ i âŸ©
    Î² = family-at-ith-component Î±
    Î³ : is-semidirected (underlying-order (ğ““ i)) (Î² i)
    Î³ aâ‚ aâ‚‚ = âˆ¥âˆ¥-functor g (semidirected-if-directed _â‰¼_ Î± Î´ aâ‚ aâ‚‚)
     where
      g : (Î£ a ê‰ ğ“ , (Î± aâ‚ â‰¼ Î± a) Ã— (Î± aâ‚‚ â‰¼ Î± a))
        â†’ (Î£ a ê‰ ğ“ , (Î² i aâ‚ âŠ‘âŸ¨ ğ““ i âŸ© Î² i a) Ã— (Î² i aâ‚‚ âŠ‘âŸ¨ ğ““ i âŸ© Î² i a))
      g (a , lâ‚ , lâ‚‚) = a , lâ‚ i , lâ‚‚ i

 ğ““âˆ-âˆ : {ğ“ : ğ“¥ Ì‡ } (Î± : ğ“ â†’ ğ““âˆ-carrier) (Î´ : is-directed _â‰¼_ Î±) â†’ ğ““âˆ-carrier
 ğ““âˆ-âˆ {ğ“} Î± Î´ = (Î» i â†’ âˆ (ğ““ i) (Î´' i)) , Ï†
  where
   Î² : (i : I) â†’ ğ“ â†’ âŸ¨ ğ““ i âŸ©
   Î² = family-at-ith-component Î±
   Î´' : (i : I) â†’ is-Directed (ğ““ i) (Î² i)
   Î´' = family-at-ith-component-is-directed Î± Î´
   Ï† : (i j : I) (l : i âŠ‘ j) â†’ Ï€ l (âˆ (ğ““ j) (Î´' j)) ï¼ âˆ (ğ““ i) (Î´' i)
   Ï† i j l = Ï€ l (âˆ (ğ““ j) (Î´' j))       ï¼âŸ¨ eqâ‚ âŸ©
             âˆ (ğ““ i) {ğ“} {Ï€ l âˆ˜ Î² j} Î´â‚ ï¼âŸ¨ eqâ‚‚ âŸ©
             âˆ (ğ““ i) {ğ“} {Î² i} Î´â‚‚       ï¼âŸ¨ eqâ‚ƒ âŸ©
             âˆ (ğ““ i) {ğ“} {Î² i} (Î´' i)   âˆ
    where
     Î´â‚ : is-Directed (ğ““ i) (Ï€ l âˆ˜ Î² j)
     Î´â‚ = image-is-directed' (ğ““ j) (ğ““ i) ((Ï€ l) , (Ï€-is-continuous l)) (Î´' j)
     h : Ï€ l âˆ˜ Î² j ï¼ Î² i
     h = dfunext fe (Î» a â†’ Ï€-equality (Î± a) l)
     Î´â‚‚ : is-Directed (ğ““ i) (Î² i)
     Î´â‚‚ = transport (is-Directed (ğ““ i)) h Î´â‚
     eqâ‚ = continuous-âˆ-ï¼ (ğ““ j) (ğ““ i) ((Ï€ l) , (Ï€-is-continuous l)) (Î´' j)
     eqâ‚‚ = âˆ-family-ï¼ (ğ““ i) h Î´â‚
     eqâ‚ƒ = âˆ-independent-of-directedness-witness (ğ““ i) Î´â‚‚ (Î´' i)

 ğ““âˆ : DCPO {ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦} {ğ“¥ âŠ” ğ“£}
 ğ““âˆ = (ğ““âˆ-carrier , _â‰¼_ , pa , dc)
  where
   pa : PosetAxioms.poset-axioms _â‰¼_
   pa = sl , pv , r , t , a
    where
     open PosetAxioms {ğ“¥ âŠ” ğ“¤ âŠ” ğ“¦} {ğ“¥ âŠ” ğ“£} {ğ““âˆ-carrier} _â‰¼_
     sl : is-set ğ““âˆ-carrier
     sl = subsets-of-sets-are-sets _ _
           (Î -is-set fe (Î» i â†’ sethood (ğ““ i)))
           (Î â‚ƒ-is-prop fe (Î» i j l â†’ sethood (ğ““ i)))
     pv : is-prop-valued
     pv Ïƒ Ï„ = Î -is-prop fe (Î» i â†’ prop-valuedness (ğ““ i) (â¦… Ïƒ â¦† i) (â¦… Ï„ â¦† i))
     r : is-reflexive
     r Ïƒ i = reflexivity (ğ““ i) (â¦… Ïƒ â¦† i)
     t : is-transitive
     t Ïƒ Ï„ Ï l k i = transitivity (ğ““ i) (â¦… Ïƒ â¦† i) (â¦… Ï„ â¦† i) (â¦… Ï â¦† i) (l i) (k i)
     a : is-antisymmetric
     a Ïƒ Ï„ l k =
      to-ğ““âˆ-ï¼ (Î» i â†’ antisymmetry (ğ““ i) (â¦… Ïƒ â¦† i) (â¦… Ï„ â¦† i) (l i) (k i))
   dc : is-directed-complete _â‰¼_
   dc ğ“ Î± Î´ = (ğ““âˆ-âˆ Î± Î´) , ub , lb-of-ubs
    where
     Î´' : (i : I) â†’ is-Directed (ğ““ i) (family-at-ith-component Î± i)
     Î´' = family-at-ith-component-is-directed Î± Î´
     ub : (a : ğ“) â†’ Î± a â‰¼ (ğ““âˆ-âˆ Î± Î´)
     ub a i = âˆ-is-upperbound (ğ““ i) (Î´' i) a
     lb-of-ubs : is-lowerbound-of-upperbounds _â‰¼_ (ğ““âˆ-âˆ Î± Î´) Î±
     lb-of-ubs Ï„ ub i = âˆ-is-lowerbound-of-upperbounds (ğ““ i) (Î´' i) (â¦… Ï„ â¦† i)
                        (Î» a â†’ ub a i)

 Ï€âˆ : (i : I) â†’ âŸ¨ ğ““âˆ âŸ© â†’ âŸ¨ ğ““ i âŸ©
 Ï€âˆ i (Ïƒ , _) = Ïƒ i

 Ï€âˆ-commutes-with-Ï€s : (i j : I) (l : i âŠ‘ j) â†’ Ï€ l âˆ˜ Ï€âˆ j âˆ¼ Ï€âˆ i
 Ï€âˆ-commutes-with-Ï€s i j l Ïƒ = Ï€-equality Ïƒ l

 Îº : {i j : I} â†’ âŸ¨ ğ““ i âŸ© â†’ (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k) â†’ âŸ¨ ğ““ j âŸ©
 Îº x (k , láµ¢ , lâ±¼) = Ï€ lâ±¼ (Îµ láµ¢ x)

 Îº-wconstant : (i j : I) (x : âŸ¨ ğ““ i âŸ©) â†’ wconstant (Îº x)
 Îº-wconstant i j x (k , láµ¢ , lâ±¼) (k' , láµ¢' , lâ±¼') =
  âˆ¥âˆ¥-rec (sethood (ğ““ j)) Î³ (I-semidirected k k')
   where
    Î³ : (Î£ m ê‰ I , k âŠ‘ m Ã— k' âŠ‘ m)
      â†’ Îº x (k , láµ¢ , lâ±¼) ï¼ Îº x (k' , láµ¢' , lâ±¼')
    Î³ (m , u , u') = Ï€ lâ±¼ (Îµ láµ¢ x)                           ï¼âŸ¨ eâ‚ âŸ©
                     Ï€ lâ±¼ (Ï€ u (Îµ u (Îµ láµ¢ x)))               ï¼âŸ¨ eâ‚‚ âŸ©
                     Ï€ (âŠ‘-trans lâ±¼ u) (Îµ u (Îµ láµ¢ x))         ï¼âŸ¨ eâ‚ƒ âŸ©
                     Ï€ (âŠ‘-trans lâ±¼ u) (Îµ (âŠ‘-trans láµ¢ u) x)   ï¼âŸ¨ eâ‚„ âŸ©
                     Ï€ (âŠ‘-trans lâ±¼ u) (Îµ (âŠ‘-trans láµ¢' u') x) ï¼âŸ¨ eâ‚… âŸ©
                     Ï€ (âŠ‘-trans lâ±¼ u) (Îµ u' (Îµ láµ¢' x))       ï¼âŸ¨ eâ‚† âŸ©
                     Ï€ (âŠ‘-trans lâ±¼' u') (Îµ u' (Îµ láµ¢' x))     ï¼âŸ¨ eâ‚‡ âŸ©
                     Ï€ lâ±¼' (Ï€ u' (Îµ u' (Îµ láµ¢' x)))           ï¼âŸ¨ eâ‚ˆ âŸ©
                     Ï€ lâ±¼' (Îµ láµ¢' x)                         âˆ
     where
      eâ‚ = ap (Ï€ lâ±¼) (Îµ-section-of-Ï€ u (Îµ láµ¢ x) â»Â¹)
      eâ‚‚ = Ï€-comp lâ±¼ u (Îµ u (Îµ láµ¢ x))
      eâ‚ƒ = ap (Ï€ (âŠ‘-trans lâ±¼ u)) (Îµ-comp láµ¢ u x)
      eâ‚„ = ap (Ï€ (âŠ‘-trans lâ±¼ u)) (ap (Î» - â†’ Îµ - x)
           (âŠ‘-prop-valued i m (âŠ‘-trans láµ¢ u) (âŠ‘-trans láµ¢' u')))
      eâ‚… = ap (Ï€ (âŠ‘-trans lâ±¼ u)) ((Îµ-comp láµ¢' u' x) â»Â¹)
      eâ‚† = ap (Î» - â†’ Ï€ - _) (âŠ‘-prop-valued j m (âŠ‘-trans lâ±¼ u) (âŠ‘-trans lâ±¼' u'))
      eâ‚‡ = (Ï€-comp lâ±¼' u' (Îµ u' (Îµ láµ¢' x))) â»Â¹
      eâ‚ˆ = ap (Ï€ lâ±¼') (Îµ-section-of-Ï€ u' (Îµ láµ¢' x))

 Ï : (i j : I) â†’ âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ““ j âŸ©
 Ï i j x = prâ‚ (wconstant-map-to-set-factors-through-truncation-of-domain
                 (sethood (ğ““ j)) (Îº x) (Îº-wconstant i j x))
               (I-semidirected i j)

 Ï-in-terms-of-Îº : {i j k : I} (láµ¢ : i âŠ‘ k) (lâ±¼ : j âŠ‘ k) (x : âŸ¨ ğ““ i âŸ©)
                 â†’ Ï i j x ï¼ Îº x (k , láµ¢ , lâ±¼)
 Ï-in-terms-of-Îº {i} {j} {k} láµ¢ lâ±¼ x =
  Ï i j x                 ï¼âŸ¨ refl âŸ©
  Î½ (I-semidirected i j)  ï¼âŸ¨ p âŸ©
  Î½ âˆ£ (k , láµ¢ , lâ±¼) âˆ£     ï¼âŸ¨ q âŸ©
  Îº x (k , láµ¢ , lâ±¼)       âˆ
   where
    s : is-set âŸ¨ ğ““ j âŸ©
    s = sethood (ğ““ j)
    Ï‰ : wconstant (Îº x)
    Ï‰ = Îº-wconstant i j x
    Ï† : Î£ r ê‰ ((âˆƒ k' ê‰ I , i âŠ‘ k' Ã— j âŠ‘ k') â†’ âŸ¨ ğ““ j âŸ©) , Îº x âˆ¼ r âˆ˜ âˆ£_âˆ£
    Ï† = wconstant-map-to-set-factors-through-truncation-of-domain s (Îº x) Ï‰
    Î½ : (âˆƒ k' ê‰ I , i âŠ‘ k' Ã— j âŠ‘ k') â†’ âŸ¨ ğ““ j âŸ©
    Î½ = prâ‚ Ï†
    p = ap Î½ (âˆ¥âˆ¥-is-prop (I-semidirected i j) âˆ£ k , láµ¢ , lâ±¼ âˆ£)
    q = (prâ‚‚ Ï† (k , láµ¢ , lâ±¼)) â»Â¹

 Îµâˆ : (i : I) â†’ âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ““âˆ âŸ©
 Îµâˆ i x = Ïƒ , Ï†
  where
   Ïƒ : (j : I) â†’ âŸ¨ ğ““ j âŸ©
   Ïƒ j = Ï i j x
   Ï† : (jâ‚ jâ‚‚ : I) (l : jâ‚ âŠ‘ jâ‚‚) â†’ Ï€ l (Ïƒ jâ‚‚) ï¼ Ïƒ jâ‚
   Ï† jâ‚ jâ‚‚ l = âˆ¥âˆ¥-rec (sethood (ğ““ jâ‚)) Î³ (I-semidirected i jâ‚‚)
    where
     Î³ : (Î£ k ê‰ I , i âŠ‘ k Ã— jâ‚‚ âŠ‘ k) â†’ Ï€ l (Ïƒ jâ‚‚) ï¼ Ïƒ jâ‚
     Î³ (k , láµ¢ , lâ‚‚) = Ï€ l (Ïƒ jâ‚‚)                  ï¼âŸ¨ refl âŸ©
                       Ï€ l (Ï i jâ‚‚ x)              ï¼âŸ¨ eâ‚   âŸ©
                       Ï€ l (Îº x (k , láµ¢ , lâ‚‚))     ï¼âŸ¨ refl âŸ©
                       Ï€ l (Ï€ lâ‚‚ (Îµ láµ¢ x))         ï¼âŸ¨ eâ‚‚   âŸ©
                       Ï€ (âŠ‘-trans l lâ‚‚) (Îµ láµ¢ x)   ï¼âŸ¨ refl âŸ©
                       Ï€ (âŠ‘-trans l lâ‚‚) (Îµ láµ¢ x)   ï¼âŸ¨ refl âŸ©
                       Îº x (k , láµ¢ , âŠ‘-trans l lâ‚‚) ï¼âŸ¨ eâ‚ƒ   âŸ©
                       Ï i jâ‚ x                    ï¼âŸ¨ refl âŸ©
                       Ïƒ jâ‚                        âˆ
      where
       eâ‚ = ap (Ï€ l) (Ï-in-terms-of-Îº láµ¢ lâ‚‚ x)
       eâ‚‚ = Ï€-comp l lâ‚‚ (Îµ láµ¢ x)
       eâ‚ƒ = (Ï-in-terms-of-Îº láµ¢ (âŠ‘-trans l lâ‚‚) x) â»Â¹

 Îµâˆ-commutes-with-Îµs : (i j : I) (l : i âŠ‘ j) â†’ Îµâˆ j âˆ˜ Îµ l âˆ¼ Îµâˆ i
 Îµâˆ-commutes-with-Îµs i j l x = to-ğ““âˆ-ï¼ Î³
  where
   Î³ : (k : I) â†’ â¦… Îµâˆ j (Îµ l x) â¦† k ï¼ â¦… Îµâˆ i x â¦† k
   Î³ k = âˆ¥âˆ¥-rec (sethood (ğ““ k)) g (I-semidirected j k)
    where
     g : (Î£ m ê‰ I , j âŠ‘ m Ã— k âŠ‘ m) â†’ â¦… Îµâˆ j (Îµ l x) â¦† k ï¼ â¦… Îµâˆ i x â¦† k
     g (m , lâ±¼ , lâ‚–) =
      â¦… Îµâˆ j (Îµ l x) â¦† k          ï¼âŸ¨ refl âŸ©
      Ï j k (Îµ l x)               ï¼âŸ¨ Ï-in-terms-of-Îº lâ±¼ lâ‚– (Îµ l x) âŸ©
      Îº (Îµ l x) (m , lâ±¼ , lâ‚–)     ï¼âŸ¨ refl âŸ©
      Ï€ lâ‚– (Îµ lâ±¼ (Îµ l x))         ï¼âŸ¨ ap (Ï€ lâ‚–) (Îµ-comp l lâ±¼ x) âŸ©
      Ï€ lâ‚– (Îµ (âŠ‘-trans l lâ±¼) x)   ï¼âŸ¨ refl âŸ©
      Îº x (m , âŠ‘-trans l lâ±¼ , lâ‚–) ï¼âŸ¨ (Ï-in-terms-of-Îº (âŠ‘-trans l lâ±¼) lâ‚– x) â»Â¹ âŸ©
      Ï i k x                     ï¼âŸ¨ refl âŸ©
      â¦… Îµâˆ i x â¦† k                âˆ

 Îµâˆ-section-of-Ï€âˆ : {i : I} â†’ Ï€âˆ i âˆ˜ Îµâˆ i âˆ¼ id
 Îµâˆ-section-of-Ï€âˆ {i} x =
  Ï€âˆ i (Îµâˆ i x)  ï¼âŸ¨ refl âŸ©
  â¦… Îµâˆ i x â¦† i              ï¼âŸ¨ refl âŸ©
  Ï i i x                   ï¼âŸ¨ Ï-in-terms-of-Îº âŠ‘-refl âŠ‘-refl x âŸ©
  Îº x (i , âŠ‘-refl , âŠ‘-refl) ï¼âŸ¨ refl âŸ©
  Ï€ âŠ‘-refl (Îµ âŠ‘-refl x)     ï¼âŸ¨ Îµ-section-of-Ï€ âŠ‘-refl x âŸ©
  x                         âˆ

 ÎµâˆÏ€âˆ-deflation : {i : I} (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ Îµâˆ i (Ï€âˆ i Ïƒ) âŠ‘âŸ¨ ğ““âˆ âŸ© Ïƒ
 ÎµâˆÏ€âˆ-deflation {i} Ïƒ j =
  âˆ¥âˆ¥-rec (prop-valuedness (ğ““ j) (â¦… Îµâˆ i (Ï€âˆ i Ïƒ) â¦† j) (â¦… Ïƒ â¦† j)) Î³
   (I-semidirected i j)
   where
    Î³ : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
      â†’ â¦… Îµâˆ i (Ï€âˆ i Ïƒ) â¦† j âŠ‘âŸ¨ ğ““ j âŸ© â¦… Ïƒ â¦† j
    Î³ (k , láµ¢ , lâ±¼) = â¦… Îµâˆ i (Ï€âˆ i Ïƒ) â¦† j          âŠ‘âŸ¨ ğ““ j âŸ©[ reflexivity (ğ““ j) _ ]
                      Ï i j (â¦… Ïƒ â¦† i)              âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ ]
                      Îº (â¦… Ïƒ â¦† i) (k , láµ¢ , lâ±¼)    âŠ‘âŸ¨ ğ““ j âŸ©[ reflexivity (ğ““ j) _ ]
                      Ï€ lâ±¼ (Îµ láµ¢ (â¦… Ïƒ â¦† i))        âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‚ ]
                      Ï€ lâ±¼ (Îµ láµ¢ (Ï€ láµ¢ (â¦… Ïƒ â¦† k))) âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ƒ ]
                      Ï€ lâ±¼ (â¦… Ïƒ â¦† k)               âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚„ ]
                      â¦… Ïƒ â¦† j                      âˆâŸ¨ ğ““ j âŸ©
     where
      uâ‚ = ï¼-to-âŠ‘ (ğ““ j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ (â¦… Ïƒ â¦† i))
      uâ‚‚ = ï¼-to-âŠ’ (ğ““ j) (ap (Ï€ lâ±¼ âˆ˜ Îµ láµ¢) (Ï€-equality Ïƒ láµ¢))
      uâ‚ƒ = monotone-if-continuous (ğ““ k) (ğ““ j) (Ï€ lâ±¼ , Ï€-is-continuous lâ±¼)
            (Îµ láµ¢ (Ï€ láµ¢ (â¦… Ïƒ â¦† k))) (â¦… Ïƒ â¦† k) (ÎµÏ€-deflation láµ¢ (â¦… Ïƒ â¦† k))
      uâ‚„ = ï¼-to-âŠ‘ (ğ““ j) (Ï€-equality Ïƒ lâ±¼)

 Ï€âˆ-is-continuous : (i : I) â†’ is-continuous ğ““âˆ (ğ““ i) (Ï€âˆ i)
 Ï€âˆ-is-continuous i ğ“ Î± Î´ = ub , lb-of-ubs
  where
   Î´' : (j : I) â†’ is-Directed (ğ““ j) (family-at-ith-component Î± j)
   Î´' = family-at-ith-component-is-directed Î± Î´
   ub : (a : ğ“) â†’ Ï€âˆ i (Î± a) âŠ‘âŸ¨ ğ““ i âŸ© Ï€âˆ i (âˆ ğ““âˆ {ğ“} {Î±} Î´)
   ub a = Ï€âˆ i (Î± a)            âŠ‘âŸ¨ ğ““ i âŸ©[ reflexivity (ğ““ i) _ ]
          â¦… Î± a â¦† i             âŠ‘âŸ¨ ğ““ i âŸ©[ âˆ-is-upperbound (ğ““ i) (Î´' i) a ]
          âˆ (ğ““ i) (Î´' i)        âŠ‘âŸ¨ ğ““ i âŸ©[ reflexivity (ğ““ i) _ ]
          â¦… âˆ ğ““âˆ {ğ“} {Î±} Î´ â¦† i  âŠ‘âŸ¨ ğ““ i âŸ©[ reflexivity (ğ““ i) _ ]
          Ï€âˆ i (âˆ ğ““âˆ {ğ“} {Î±} Î´) âˆâŸ¨ ğ““ i âŸ©
   lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order (ğ““ i))
                 (Ï€âˆ i (âˆ ğ““âˆ {ğ“} {Î±} Î´)) (Ï€âˆ i âˆ˜ Î±)
   lb-of-ubs x ub = Ï€âˆ i (âˆ ğ““âˆ {ğ“} {Î±} Î´) âŠ‘âŸ¨ ğ““ i âŸ©[ reflexivity (ğ““ i) _ ]
                    âˆ (ğ““ i) (Î´' i)        âŠ‘âŸ¨ ğ““ i âŸ©[ l ]
                    x                     âˆâŸ¨ ğ““ i âŸ©
    where
     l = âˆ-is-lowerbound-of-upperbounds (ğ““ i) (Î´' i) x ub

 Ï€âˆ' : (i : I) â†’ DCPO[ ğ““âˆ , ğ““ i ]
 Ï€âˆ' i = Ï€âˆ i , Ï€âˆ-is-continuous i

 Îµâˆ-is-monotone : (i : I) â†’ is-monotone (ğ““ i) ğ““âˆ (Îµâˆ i)
 Îµâˆ-is-monotone i x y l j =
  âˆ¥âˆ¥-rec (prop-valuedness (ğ““ j) (â¦… Îµâˆ i x â¦† j) (â¦… Îµâˆ i y â¦† j))
   Î³ (I-semidirected i j)
    where
     Î³ : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
       â†’ â¦… Îµâˆ i x â¦† j âŠ‘âŸ¨ ğ““ j âŸ© â¦… Îµâˆ i y â¦† j
     Î³ (k , láµ¢ , lâ±¼) = â¦… Îµâˆ i x â¦† j      âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ ]
                       Ï i j x           âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‚ ]
                       Îº x (k , láµ¢ , lâ±¼) âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ƒ ]
                       Ï€ lâ±¼ (Îµ láµ¢ x)     âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚„ ]
                       Ï€ lâ±¼ (Îµ láµ¢ y)     âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚… ]
                       Îº y (k , láµ¢ , lâ±¼) âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚† ]
                       Ï i j y           âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‡ ]
                       â¦… Îµâˆ i y â¦† j      âˆâŸ¨ ğ““ j âŸ©
      where
       uâ‚ = reflexivity (ğ““ j) (â¦… Îµâˆ i x â¦† j)
       uâ‚‚ = ï¼-to-âŠ‘ (ğ““ j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ x)
       uâ‚ƒ = reflexivity (ğ““ j) (Îº x (k , láµ¢ , lâ±¼))
       uâ‚„ = mÏ€ (Îµ láµ¢ x) (Îµ láµ¢ y) (mÎµ x y l)
        where
         mÎµ : is-monotone (ğ““ i) (ğ““ k) (Îµ láµ¢)
         mÎµ = monotone-if-continuous (ğ““ i) (ğ““ k)
               ((Îµ láµ¢) , (Îµ-is-continuous láµ¢))
         mÏ€ : is-monotone (ğ““ k) (ğ““ j) (Ï€ lâ±¼)
         mÏ€ = monotone-if-continuous (ğ““ k) (ğ““ j)
               ((Ï€ lâ±¼) , (Ï€-is-continuous lâ±¼))
       uâ‚… = reflexivity (ğ““ j) (Ï€ lâ±¼ (Îµ láµ¢ y))
       uâ‚† = ï¼-to-âŠ’ (ğ““ j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ y)
       uâ‚‡ = reflexivity (ğ““ j) (Ï i j y)

 Îµâˆ-is-continuous : (i : I) â†’ is-continuous (ğ““ i) ğ““âˆ (Îµâˆ i)
 Îµâˆ-is-continuous i = continuity-criterion' (ğ““ i) ğ““âˆ (Îµâˆ i) (Îµâˆ-is-monotone i) Î³
  where
   Î³ : (ğ“ : ğ“¥ Ì‡ )(Î± : ğ“ â†’ âŸ¨ ğ““ i âŸ©) (Î´ : is-Directed (ğ““ i) Î±)
     â†’ is-lowerbound-of-upperbounds (underlying-order ğ““âˆ)
        (Îµâˆ i (âˆ (ğ““ i) Î´)) (Îµâˆ i âˆ˜ Î±)
   Î³ ğ“ Î± Î´ Ïƒ ub j =
    âˆ¥âˆ¥-rec (prop-valuedness (ğ““ j) (â¦… Îµâˆ i (âˆ (ğ““ i) Î´) â¦† j) (â¦… Ïƒ â¦† j))
     g (I-semidirected i j)
      where
       g : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
         â†’ â¦… Îµâˆ i (âˆ (ğ““ i) Î´) â¦† j âŠ‘âŸ¨ ğ““ j âŸ© â¦… Ïƒ â¦† j
       g (k , láµ¢ , lâ±¼) =
        â¦… Îµâˆ i (âˆ (ğ““ i) Î´) â¦† j                  âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ ]
        Ï i j (âˆ (ğ““ i) Î´)                       âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‚ ]
        Îº (âˆ (ğ““ i) Î´) (k , láµ¢ , lâ±¼)             âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ƒ ]
        Ï€ lâ±¼ (Îµ láµ¢ (âˆ (ğ““ i) Î´))                 âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚„ ]
        âˆ (ğ““ j) {ğ“} {Ï€Îµ âˆ˜ Î±} Î´â‚                 âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚… ]
        âˆ (ğ““ j) {ğ“} {Î» a â†’ â¦… Îµâˆ i (Î± a) â¦† j} Î´â‚‚ âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚† ]
        â¦… Ïƒ â¦† j âˆâŸ¨ ğ““ j âŸ©
         where
          Ï€Îµ : âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ““ j âŸ©
          Ï€Îµ = Ï€ lâ±¼ âˆ˜ Îµ láµ¢
          Ï€Îµ-is-continuous : is-continuous (ğ““ i) (ğ““ j) Ï€Îµ
          Ï€Îµ-is-continuous = âˆ˜-is-continuous (ğ““ i) (ğ““ k) (ğ““ j) (Îµ láµ¢) (Ï€ lâ±¼)
                              (Îµ-is-continuous láµ¢) (Ï€-is-continuous lâ±¼)
          Ï€Îµ' : DCPO[ ğ““ i , ğ““ j ]
          Ï€Îµ' = Ï€Îµ , Ï€Îµ-is-continuous
          Î´â‚ : is-Directed (ğ““ j) (Ï€Îµ âˆ˜ Î±)
          Î´â‚ = image-is-directed' (ğ““ i) (ğ““ j) Ï€Îµ' Î´
          p : Ï€Îµ âˆ˜ Î± ï¼ (Î» a â†’ â¦… Îµâˆ i (Î± a) â¦† j)
          p = dfunext fe h
           where
            h : Ï€Îµ âˆ˜ Î± âˆ¼ (Î» a â†’ â¦… Îµâˆ i (Î± a) â¦† j)
            h a = Ï€Îµ (Î± a)              ï¼âŸ¨ refl âŸ©
                  Ï€ lâ±¼ (Îµ láµ¢ (Î± a))     ï¼âŸ¨ refl âŸ©
                  Îº (Î± a) (k , láµ¢ , lâ±¼) ï¼âŸ¨ (Ï-in-terms-of-Îº láµ¢ lâ±¼ (Î± a)) â»Â¹ âŸ©
                  Ï i j (Î± a)           ï¼âŸ¨ refl âŸ©
                  â¦… Îµâˆ i (Î± a) â¦† j      âˆ
          Î´â‚‚ : is-Directed (ğ““ j) (Î» a â†’ â¦… Îµâˆ i (Î± a) â¦† j)
          Î´â‚‚ = transport (is-Directed (ğ““ j)) p Î´â‚
          uâ‚ = reflexivity (ğ““Â j) (â¦… Îµâˆ i (âˆ (ğ““ i) Î´) â¦† j)
          uâ‚‚ = ï¼-to-âŠ‘ (ğ““ j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ (âˆ (ğ““ i) Î´))
          uâ‚ƒ = reflexivity (ğ““ j) (Îº (âˆ (ğ““ i) Î´) (k , láµ¢ , lâ±¼))
          uâ‚„ = continuous-âˆ-âŠ‘ (ğ““ i) (ğ““ j) Ï€Îµ' Î´
          uâ‚… = ï¼-to-âŠ‘ (ğ““ j) (âˆ-family-ï¼ (ğ““ j) p Î´â‚)
          uâ‚† = âˆ-is-lowerbound-of-upperbounds (ğ““ j) Î´â‚‚ (â¦… Ïƒ â¦† j) (Î» a â†’ ub a j)

 Îµâˆ' : (i : I) â†’ DCPO[ ğ““ i , ğ““âˆ ]
 Îµâˆ' i = Îµâˆ i , Îµâˆ-is-continuous i

\end{code}

This concludes the construction of the bilimit. We proceed by showing that it is
indeed the limit of the diagram.

\begin{code}

 module DcpoCone
         (ğ“” : DCPO {ğ“¤'} {ğ“£'})
         (f : (i : I) â†’ âŸ¨ ğ“” âŸ© â†’ âŸ¨ ğ““ i âŸ©)
         (f-cont : (i : I) â†’ is-continuous ğ“” (ğ““ i) (f i))
         (comm : (i j : I) (l : i âŠ‘ j) â†’ Ï€ l âˆ˜ f j âˆ¼ f i)
        where

  limit-mediating-arrow : âŸ¨ ğ“” âŸ© â†’ âŸ¨ ğ““âˆ âŸ©
  limit-mediating-arrow y = Ïƒ , Ï†
   where
    Ïƒ : (i : I) â†’ âŸ¨ ğ““ i âŸ©
    Ïƒ i = f i y
    Ï† : (i j : I) (l : i âŠ‘ j) â†’ Ï€ l (f j y) ï¼ f i y
    Ï† i j l = comm i j l y

  limit-mediating-arrow-commutes : (i : I) â†’ Ï€âˆ i âˆ˜ limit-mediating-arrow âˆ¼ f i
  limit-mediating-arrow-commutes i y = refl

  limit-mediating-arrow-is-unique : (g : âŸ¨ ğ“” âŸ© â†’ âŸ¨ ğ““âˆ âŸ©)
                                  â†’ ((i : I) â†’ Ï€âˆ i âˆ˜ g âˆ¼ f i)
                                  â†’ g âˆ¼ limit-mediating-arrow
  limit-mediating-arrow-is-unique g g-comm y =
   to-ğ““âˆ-ï¼ (Î» i â†’ g-comm i y)

  limit-mediating-arrow-is-monotone : is-monotone ğ“” ğ““âˆ limit-mediating-arrow
  limit-mediating-arrow-is-monotone x y l i = f i x âŠ‘âŸ¨ ğ““ i âŸ©[ m x y l ]
                                              f i y âˆâŸ¨ ğ““ i âŸ©
   where
    m : is-monotone ğ“” (ğ““ i) (f i)
    m = monotone-if-continuous ğ“” (ğ““ i) (f i , f-cont i)

  limit-mediating-arrow-is-continuous : is-continuous ğ“” ğ““âˆ limit-mediating-arrow
  limit-mediating-arrow-is-continuous = continuity-criterion' ğ“” ğ““âˆ m mon Î³
   where
    m : âŸ¨ ğ“” âŸ© â†’ âŸ¨ ğ““âˆ âŸ©
    m = limit-mediating-arrow
    mon : is-monotone ğ“” ğ““âˆ m
    mon = limit-mediating-arrow-is-monotone
    Î³ : (A : ğ“¥ Ì‡ )(Î± : A â†’ âŸ¨ ğ“” âŸ©) (Î´ : is-Directed ğ“” Î±)
      â†’ is-lowerbound-of-upperbounds (underlying-order ğ““âˆ) (m (âˆ ğ“” Î´)) (m âˆ˜ Î±)
    Î³ A Î± Î´ Ïƒ ub i = â¦… m (âˆ ğ“” Î´) â¦† i âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚ ]
                     f i (âˆ ğ“” Î´)     âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚‚ ]
                     âˆ (ğ““ i) Î´'      âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚ƒ ]
                     â¦… Ïƒ â¦† i          âˆâŸ¨ ğ““ i âŸ©
     where
      Î´' : is-Directed (ğ““ i) (f i âˆ˜ Î±)
      Î´' = image-is-directed' ğ“” (ğ““ i) (f i , f-cont i) Î´
      uâ‚ = reflexivity (ğ““ i) (â¦… m (âˆ ğ“” Î´) â¦† i)
      uâ‚‚ = continuous-âˆ-âŠ‘ ğ“” (ğ““ i) (f i , f-cont i) Î´
      uâ‚ƒ = âˆ-is-lowerbound-of-upperbounds (ğ““ i) Î´' (â¦… Ïƒ â¦† i) (Î» a â†’ ub a i)

\end{code}

Next, we wish to show that ğ““âˆ is also the colimit of the diagram. The following
are preliminaries for doing so.

\begin{code}

 Îµâˆ-family : âŸ¨ ğ““âˆ âŸ© â†’ I â†’ âŸ¨ ğ““âˆ âŸ©
 Îµâˆ-family Ïƒ i = Îµâˆ i (â¦… Ïƒ â¦† i)

 Îµâˆ-family-is-monotone : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (i j : I) â†’ i âŠ‘ j
                       â†’ Îµâˆ-family Ïƒ i âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ-family Ïƒ j
 Îµâˆ-family-is-monotone Ïƒ i j l k =
  âˆ¥âˆ¥-rec (prop-valuedness (ğ““ k) (â¦… Îµâˆ-family Ïƒ i â¦† k) (â¦… Îµâˆ-family Ïƒ j â¦† k))
   Î³ (I-semidirected j k)
    where
     Î³ : (Î£ m ê‰ I , j âŠ‘ m Ã— k âŠ‘ m)
       â†’ â¦… Îµâˆ-family Ïƒ i â¦† k âŠ‘âŸ¨ ğ““ k âŸ© â¦… Îµâˆ-family Ïƒ j â¦† k
     Î³ (m , lâ±¼ , lâ‚–) =
      â¦… Îµâˆ-family Ïƒ i â¦† k                 âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚ ]
      Ï i k (â¦… Ïƒ â¦† i)                     âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚‚ ]
      Îº (â¦… Ïƒ â¦† i) (m , âŠ‘-trans l lâ±¼ , lâ‚–) âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚ƒ ]
      Ï€ lâ‚– (Îµ (âŠ‘-trans l lâ±¼) (â¦… Ïƒ â¦† i))   âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚„ ]
      Ï€ lâ‚– (Îµ lâ±¼ (Îµ l (â¦… Ïƒ â¦† i)))         âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚… ]
      Ï€ lâ‚– (Îµ lâ±¼ (Îµ l (Ï€ l (â¦… Ïƒ â¦† j))))   âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚† ]
      Ï€ lâ‚– (Îµ lâ±¼ (â¦… Ïƒ â¦† j))               âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚‡ ]
      Îº (â¦… Ïƒ â¦† j) (m , lâ±¼ , lâ‚–)           âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚ˆ ]
      Ï j k (â¦… Ïƒ â¦† j)                     âŠ‘âŸ¨ ğ““ k âŸ©[ uâ‚‰ ]
      â¦… Îµâˆ-family Ïƒ j â¦† k                 âˆâŸ¨ ğ““ k âŸ©
       where
        uâ‚ = reflexivity (ğ““ k) (â¦… Îµâˆ-family Ïƒ i â¦† k)
        uâ‚‚ = ï¼-to-âŠ‘ (ğ““ k) (Ï-in-terms-of-Îº (âŠ‘-trans l lâ±¼) lâ‚– (â¦… Ïƒ â¦† i))
        uâ‚ƒ = reflexivity (ğ““ k) (Îº (â¦… Ïƒ â¦† i) (m , âŠ‘-trans l lâ±¼ , lâ‚–))
        uâ‚„ = ï¼-to-âŠ’ (ğ““ k) (ap (Ï€ lâ‚–) (Îµ-comp l lâ±¼ (â¦… Ïƒ â¦† i)))
        uâ‚… = ï¼-to-âŠ’ (ğ““ k) (ap (Ï€ lâ‚– âˆ˜ Îµ lâ±¼ âˆ˜ Îµ l) (Ï€-equality Ïƒ l))
        uâ‚† = mon (Îµ l (Ï€ l (â¦… Ïƒ â¦† j))) (â¦… Ïƒ â¦† j) (ÎµÏ€-deflation l (â¦… Ïƒ â¦† j))
         where
          mon : is-monotone (ğ““ j) (ğ““ k) (Ï€ lâ‚– âˆ˜ Îµ lâ±¼)
          mon = monotone-if-continuous (ğ““ j) (ğ““ k)
                 (Ï€ lâ‚– âˆ˜ Îµ lâ±¼ ,
                  âˆ˜-is-continuous (ğ““ j) (ğ““ m) (ğ““ k)
                  (Îµ lâ±¼) (Ï€ lâ‚–) (Îµ-is-continuous lâ±¼) (Ï€-is-continuous lâ‚–))
        uâ‚‡ = reflexivity (ğ““ k) (Îº (â¦… Ïƒ â¦† j) (m , lâ±¼ , lâ‚–))
        uâ‚ˆ = ï¼-to-âŠ’ (ğ““ k) (Ï-in-terms-of-Îº lâ±¼ lâ‚– (â¦… Ïƒ â¦† j))
        uâ‚‰ = reflexivity (ğ““ k) (â¦… Îµâˆ-family Ïƒ j â¦† k)

 Îµâˆ-family-is-directed : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ is-Directed ğ““âˆ (Îµâˆ-family Ïƒ)
 Îµâˆ-family-is-directed Ïƒ = I-inhabited , Î´
  where
   Î´ : is-semidirected (underlying-order ğ““âˆ) (Îµâˆ-family Ïƒ)
   Î´ i j = âˆ¥âˆ¥-functor Î³ (I-semidirected i j)
    where
     Î³ : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
       â†’ (Î£ k ê‰ I , Îµâˆ-family Ïƒ i âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ-family Ïƒ k
                  Ã— Îµâˆ-family Ïƒ j âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ-family Ïƒ k)
     Î³ (k , láµ¢ , lâ±¼) =
      k , Îµâˆ-family-is-monotone Ïƒ i k láµ¢ ,
          Îµâˆ-family-is-monotone Ïƒ j k lâ±¼

 âˆ-of-Îµâˆs : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ Ïƒ ï¼ âˆ ğ““âˆ {I} {Îµâˆ-family Ïƒ} (Îµâˆ-family-is-directed Ïƒ)
 âˆ-of-Îµâˆs Ïƒ = antisymmetry ğ““âˆ Ïƒ (âˆ ğ““âˆ Î´) a b
  where
   Î± : I â†’ âŸ¨ ğ““âˆ âŸ©
   Î± = Îµâˆ-family Ïƒ
   Î´ : is-Directed ğ““âˆ Î±
   Î´ = Îµâˆ-family-is-directed Ïƒ
   a : Ïƒ âŠ‘âŸ¨ ğ““âˆ âŸ© âˆ ğ““âˆ {I} {Î±} Î´
   a i = â¦… Ïƒ â¦† i                           âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚ ]
         Îµ âŠ‘-refl (â¦… Ïƒ â¦† i)                âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚‚ ]
         Ï€ âŠ‘-refl (Îµ âŠ‘-refl (â¦… Ïƒ â¦† i))     âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚ƒ ]
         Îº (â¦… Ïƒ â¦† i) (i , âŠ‘-refl , âŠ‘-refl) âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚„ ]
         Ï i i (â¦… Ïƒ â¦† i)                   âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚… ]
         â¦… Îµâˆ i (â¦… Ïƒ â¦† i) â¦† i              âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚† ]
         family-at-ith-component Î± i i     âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚‡ ]
         âˆ (ğ““ i) Î´'                        âŠ‘âŸ¨ ğ““ i âŸ©[ uâ‚ˆ ]
         â¦… (âˆ ğ““âˆ {I} {Î±} Î´) â¦† i            âˆâŸ¨ ğ““ i âŸ©
    where
     Î´' : is-Directed (ğ““ i) (family-at-ith-component Î± i)
     Î´' = family-at-ith-component-is-directed Î± Î´ i
     uâ‚ = ï¼-to-âŠ’ (ğ““ i) (Îµ-id i (â¦… Ïƒ â¦† i))
     uâ‚‚ = ï¼-to-âŠ’ (ğ““ i) (Ï€-id i (Îµ âŠ‘-refl (â¦… Ïƒ â¦† i)))
     uâ‚ƒ = reflexivity (ğ““ i) (Ï€ âŠ‘-refl (Îµ âŠ‘-refl (â¦… Ïƒ â¦† i)))
     uâ‚„ = ï¼-to-âŠ’ (ğ““ i) (Ï-in-terms-of-Îº âŠ‘-refl âŠ‘-refl (â¦… Ïƒ â¦† i))
     uâ‚… = reflexivity (ğ““ i) (Ï i i (â¦… Ïƒ â¦† i))
     uâ‚† = reflexivity (ğ““ i) (â¦… Îµâˆ i (â¦… Ïƒ â¦† i) â¦† i )
     uâ‚‡ = âˆ-is-upperbound (ğ““ i) Î´' i
     uâ‚ˆ = reflexivity (ğ““ i) (â¦… âˆ ğ““âˆ {I} {Î±} Î´ â¦† i)
   b : âˆ ğ““âˆ {I} {Î±} Î´ âŠ‘âŸ¨ ğ““âˆ âŸ© Ïƒ
   b = âˆ-is-lowerbound-of-upperbounds ğ““âˆ {I} {Î±} Î´ Ïƒ Î³
    where
     Î³ : (i : I) â†’ Î± i âŠ‘âŸ¨ ğ““âˆ âŸ© Ïƒ
     Î³ i j = âˆ¥âˆ¥-rec (prop-valuedness (ğ““ j) (â¦… Î± i â¦† j) (â¦… Ïƒ â¦† j))
              g (I-semidirected i j)
      where
       g : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k) â†’ â¦… Î± i â¦† j âŠ‘âŸ¨ ğ““ j âŸ© â¦… Ïƒ â¦† j
       g (k , láµ¢ , lâ±¼) = â¦… Î± i â¦† j                    âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ ]
                         â¦… Îµâˆ i (â¦… Ïƒ â¦† i) â¦† j         âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‚ ]
                         Ï i j (â¦… Ïƒ â¦† i)              âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚ƒ ]
                         Îº (â¦… Ïƒ â¦† i) (k , láµ¢ , lâ±¼)    âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚„ ]
                         Ï€ lâ±¼ (Îµ láµ¢ (â¦… Ïƒ â¦† i))        âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚… ]
                         Ï€ lâ±¼ (Îµ láµ¢ (Ï€ láµ¢ (â¦… Ïƒ â¦† k))) âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚† ]
                         Ï€ lâ±¼ (â¦… Ïƒ â¦† k)               âŠ‘âŸ¨ ğ““ j âŸ©[ uâ‚‡ ]
                         â¦… Ïƒ â¦† j                      âˆâŸ¨ ğ““ j âŸ©
        where
         uâ‚ = reflexivity (ğ““ j) (â¦… Î± i â¦† j)
         uâ‚‚ = reflexivity (ğ““ j) (â¦… Îµâˆ i (â¦… Ïƒ â¦† i) â¦† j)
         uâ‚ƒ = ï¼-to-âŠ‘ (ğ““ j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ (â¦… Ïƒ â¦† i))
         uâ‚„ = reflexivity (ğ““ j) (Îº (â¦… Ïƒ â¦† i) (k , láµ¢ , lâ±¼))
         uâ‚… = ï¼-to-âŠ’ (ğ““ j) (ap (Ï€ lâ±¼ âˆ˜ Îµ láµ¢) (Ï€-equality Ïƒ láµ¢))
         uâ‚† = mon (Îµ láµ¢ (Ï€ láµ¢ (â¦… Ïƒ â¦† k))) (â¦… Ïƒ â¦† k) (ÎµÏ€-deflation láµ¢ (â¦… Ïƒ â¦† k))
          where
           mon : is-monotone (ğ““ k) (ğ““ j) (Ï€ lâ±¼)
           mon = monotone-if-continuous (ğ““ k) (ğ““ j)
                  (Ï€ lâ±¼ , Ï€-is-continuous lâ±¼)
         uâ‚‡ = ï¼-to-âŠ‘ (ğ““ j) (Ï€-equality Ïƒ lâ±¼)

\end{code}

We now show that ğ““âˆ is the colimit of the diagram.

\begin{code}

 module DcpoCocone
         (ğ“” : DCPO {ğ“¤'} {ğ“£'})
         (g : (i : I) â†’ âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ“” âŸ©)
         (g-cont : (i : I) â†’ is-continuous (ğ““ i) ğ“” (g i))
         (comm : (i j : I) (l : i âŠ‘ j) â†’ g j âˆ˜ Îµ l âˆ¼ g i)
        where

  colimit-family : âŸ¨ ğ““âˆ âŸ© â†’ I â†’ âŸ¨ ğ“” âŸ©
  colimit-family Ïƒ i = g i (â¦… Ïƒ â¦† i)

  colimit-family-is-monotone : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (i j : I) (l : i âŠ‘ j)
                             â†’ colimit-family Ïƒ i âŠ‘âŸ¨ ğ“” âŸ© colimit-family Ïƒ j
  colimit-family-is-monotone Ïƒ i j l =
   g i (â¦… Ïƒ â¦† i)             âŠ‘âŸ¨ ğ“” âŸ©[ u ]
   g i (Ï€ l (â¦… Ïƒ â¦† j))       âŠ‘âŸ¨ ğ“” âŸ©[ v ]
   g j (Îµ l (Ï€ l (â¦… Ïƒ â¦† j))) âŠ‘âŸ¨ ğ“” âŸ©[ w ]
   g j (â¦… Ïƒ â¦† j)             âˆâŸ¨ ğ“” âŸ©
    where
     u = ï¼-to-âŠ’ ğ“” (ap (g i) (Ï€-equality Ïƒ l))
     v = ï¼-to-âŠ’ ğ“” (comm i j l (Ï€ l (â¦… Ïƒ â¦† j)))
     w = gm (Îµ l (Ï€ l (â¦… Ïƒ â¦† j))) (â¦… Ïƒ â¦† j) (ÎµÏ€-deflation l (â¦… Ïƒ â¦† j))
      where
       gm : is-monotone (ğ““ j) ğ“” (g j)
       gm = monotone-if-continuous (ğ““ j) ğ“” (g j , g-cont j)

  colimit-family-is-directed : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ is-Directed ğ“” (colimit-family Ïƒ)
  colimit-family-is-directed Ïƒ = I-inhabited , Î³
   where
    Î³ : is-semidirected (underlying-order ğ“”) (colimit-family Ïƒ)
    Î³ i j = âˆ¥âˆ¥-functor Ïˆ (I-semidirected i j)
     where
      Ïˆ : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
        â†’ (Î£ k ê‰ I , colimit-family Ïƒ i âŠ‘âŸ¨ ğ“” âŸ© colimit-family Ïƒ k
                   Ã— colimit-family Ïƒ j âŠ‘âŸ¨ ğ“” âŸ© colimit-family Ïƒ k)
      Ïˆ (k , láµ¢ , lâ±¼) =
       k , colimit-family-is-monotone Ïƒ i k láµ¢ ,
           colimit-family-is-monotone Ïƒ j k lâ±¼

  colimit-mediating-arrow : âŸ¨ ğ““âˆ âŸ© â†’ âŸ¨ ğ“” âŸ©
  colimit-mediating-arrow Ïƒ = âˆ ğ“” {I} {Ï†} Î´
   where
    Ï† : I â†’ âŸ¨ ğ“” âŸ©
    Ï† = colimit-family Ïƒ
    Î´ : is-Directed ğ“” Ï†
    Î´ = colimit-family-is-directed Ïƒ

  colimit-mediating-arrow-commutes : (i : I)
                                   â†’ colimit-mediating-arrow âˆ˜ Îµâˆ i âˆ¼ g i
  colimit-mediating-arrow-commutes i x =
   antisymmetry ğ“” (colimit-mediating-arrow ((Îµâˆ i) x)) (g i x) a b
    where
     Î´ : is-Directed ğ“” (colimit-family (Îµâˆ i x))
     Î´ = colimit-family-is-directed (Îµâˆ i x)
     a : colimit-mediating-arrow (Îµâˆ i x) âŠ‘âŸ¨ ğ“” âŸ© g i x
     a = âˆ-is-lowerbound-of-upperbounds ğ“” Î´ (g i x) Î³
      where
       Î³ : (j : I) â†’ colimit-family (Îµâˆ i x) j âŠ‘âŸ¨ ğ“” âŸ© g i x
       Î³ j = âˆ¥âˆ¥-rec (prop-valuedness ğ“” (colimit-family (Îµâˆ i x) j) (g i x))
              Ï• (I-semidirected i j)
        where
         Ï• : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
           â†’ colimit-family (Îµâˆ i x) j âŠ‘âŸ¨ ğ“” âŸ© g i x
         Ï• (k , láµ¢ , lâ±¼) =
          colimit-family (Îµâˆ i x) j  âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚ ]
          g j (Ï i j x)              âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚‚ ]
          g j (Îº x (k , láµ¢ , lâ±¼))    âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚ƒ ]
          g j (Ï€ lâ±¼ (Îµ láµ¢ x))        âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚„ ]
          g k (Îµ lâ±¼ (Ï€ lâ±¼ (Îµ láµ¢ x))) âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚… ]
          g k (Îµ láµ¢ x)               âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚† ]
          g i x                      âˆâŸ¨ ğ“” âŸ©
           where
            uâ‚ = reflexivity ğ“” (colimit-family (Îµâˆ i x) j)
            uâ‚‚ = ï¼-to-âŠ‘ ğ“” (ap (g j) (Ï-in-terms-of-Îº láµ¢ lâ±¼ x))
            uâ‚ƒ = reflexivity ğ“” (g j (Îº x (k , láµ¢ , lâ±¼)))
            uâ‚„ = ï¼-to-âŠ’ ğ“” (comm j k lâ±¼ (Ï€ lâ±¼ (Îµ láµ¢ x)))
            uâ‚… = m (Îµ lâ±¼ (Ï€ lâ±¼ (Îµ láµ¢ x))) (Îµ láµ¢ x) (ÎµÏ€-deflation lâ±¼ (Îµ láµ¢ x))
             where
              m : is-monotone (ğ““ k) ğ“” (g k)
              m = monotone-if-continuous (ğ““ k) ğ“” (g k , g-cont k)
            uâ‚† = ï¼-to-âŠ‘ ğ“” (comm i k láµ¢ x)
     b : g i x âŠ‘âŸ¨ ğ“” âŸ© colimit-mediating-arrow (Îµâˆ i x)
     b = g i x                            âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚ ]
         g i (Îµ âŠ‘-refl x)                 âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚‚ ]
         g i (Ï€ âŠ‘-refl (Îµ âŠ‘-refl x))      âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚ƒ ]
         g i (Îº x (i , âŠ‘-refl , âŠ‘-refl))  âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚„ ]
         g i (Ï i i x)                    âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚… ]
         g i (â¦… Îµâˆ i x â¦† i)               âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚† ]
         âˆ ğ“” Î´                            âŠ‘âŸ¨ ğ“” âŸ©[ vâ‚‡ ]
         colimit-mediating-arrow (Îµâˆ i x) âˆâŸ¨ ğ“” âŸ©
      where
       vâ‚ = ï¼-to-âŠ’ ğ“” (ap (g i) (Îµ-id i x))
       vâ‚‚ = ï¼-to-âŠ’ ğ“” (ap (g i) (Ï€-id i (Îµ âŠ‘-refl x)))
       vâ‚ƒ = reflexivity ğ“” (g i (Ï€ âŠ‘-refl (Îµ âŠ‘-refl x)))
       vâ‚„ = ï¼-to-âŠ’ ğ“” (ap (g i) (Ï-in-terms-of-Îº âŠ‘-refl âŠ‘-refl x))
       vâ‚… = reflexivity ğ“” (g i (Ï i i x))
       vâ‚† = âˆ-is-upperbound ğ“” Î´ i
       vâ‚‡ = reflexivity ğ“” (âˆ ğ“” Î´)

  colimit-mediating-arrow-is-unique : (h : âŸ¨ ğ““âˆ âŸ© â†’ âŸ¨ ğ“” âŸ©)
                                    â†’ is-continuous ğ““âˆ ğ“” h
                                    â†’ ((i : I) â†’ h âˆ˜ Îµâˆ i âˆ¼ g i)
                                    â†’ h âˆ¼ colimit-mediating-arrow
  colimit-mediating-arrow-is-unique h h-cont h-comm Ïƒ =
   h Ïƒ                                   ï¼âŸ¨ ap h (âˆ-of-Îµâˆs Ïƒ) âŸ©
   h (âˆ ğ““âˆ {I} {Î» i â†’ Îµâˆ i (â¦… Ïƒ â¦† i)} Î´) ï¼âŸ¨ eâ‚ âŸ©
   âˆ ğ“” {I} {Î» i â†’ h (Îµâˆ i (â¦… Ïƒ â¦† i))} Î´â‚ ï¼âŸ¨ eâ‚‚ âŸ©
   âˆ ğ“” {I} {Î» i â†’ g i (â¦… Ïƒ â¦† i)} Î´â‚‚      ï¼âŸ¨ eâ‚ƒ âŸ©
   âˆ ğ“” {I} {Î» i â†’ g i (â¦… Ïƒ â¦† i)} Î´â‚ƒ      ï¼âŸ¨ refl âŸ©
   colimit-mediating-arrow Ïƒ             âˆ
    where
     p : (Î» i â†’ (h âˆ˜ Îµâˆ i) (â¦… Ïƒ â¦† i)) ï¼ (Î» i â†’ g i (â¦… Ïƒ â¦† i))
     p = dfunext fe (Î» i â†’ h-comm i (â¦… Ïƒ â¦† i))
     Î´ : is-Directed ğ““âˆ {I} (Îµâˆ-family Ïƒ)
     Î´ = Îµâˆ-family-is-directed Ïƒ
     Î´â‚ : is-Directed ğ“” (h âˆ˜ Îµâˆ-family Ïƒ)
     Î´â‚ = image-is-directed' ğ““âˆ ğ“” (h , h-cont) {I} {Îµâˆ-family Ïƒ} Î´
     Î´â‚‚ : is-Directed ğ“” (Î» i â†’ g i (â¦… Ïƒ â¦† i))
     Î´â‚‚ = transport (is-Directed ğ“” {I}) p Î´â‚
     Î´â‚ƒ : is-Directed ğ“” (colimit-family Ïƒ)
     Î´â‚ƒ = colimit-family-is-directed Ïƒ
     eâ‚ = continuous-âˆ-ï¼ ğ““âˆ ğ“” (h , h-cont) {I} {Îµâˆ-family Ïƒ} Î´
     eâ‚‚ = âˆ-family-ï¼ ğ“” {I} p Î´â‚
     eâ‚ƒ = âˆ-independent-of-directedness-witness ğ“” Î´â‚‚ Î´â‚ƒ

  colimit-mediating-arrow-is-monotone : is-monotone ğ““âˆ ğ“”
                                         colimit-mediating-arrow
  colimit-mediating-arrow-is-monotone Ïƒ Ï„ l = Î³
   where
    Î´â‚ : is-Directed ğ“” (colimit-family Ïƒ)
    Î´â‚ = colimit-family-is-directed Ïƒ
    Î´â‚‚ : is-Directed ğ“” (colimit-family Ï„)
    Î´â‚‚ = colimit-family-is-directed Ï„
    Î³ : âˆ ğ“” Î´â‚ âŠ‘âŸ¨ ğ“” âŸ© âˆ ğ“” Î´â‚‚
    Î³ = âˆ-is-monotone ğ“” Î´â‚ Î´â‚‚ Ïˆ
     where
      Ïˆ : (i : I) â†’ colimit-family Ïƒ i âŠ‘âŸ¨ ğ“” âŸ© colimit-family Ï„ i
      Ïˆ i = g i (â¦… Ïƒ â¦† i) âŠ‘âŸ¨ ğ“” âŸ©[ m (â¦… Ïƒ â¦† i) (â¦… Ï„ â¦† i) (l i) ]
            g i (â¦… Ï„ â¦† i) âˆâŸ¨ ğ“” âŸ©
       where
        m : is-monotone (ğ““ i) ğ“” (g i)
        m = monotone-if-continuous (ğ““ i) ğ“” (g i , g-cont i)

  colimit-mediating-arrow-is-continuous : is-continuous ğ““âˆ ğ“”
                                           colimit-mediating-arrow
  colimit-mediating-arrow-is-continuous = continuity-criterion' ğ““âˆ ğ“” m mon Î³
   where
    m : âŸ¨ ğ““âˆ âŸ© â†’ âŸ¨ ğ“” âŸ©
    m = colimit-mediating-arrow
    mon : is-monotone ğ““âˆ ğ“” colimit-mediating-arrow
    mon = colimit-mediating-arrow-is-monotone
    Î³ : (A : ğ“¥ Ì‡ )(Î± : A â†’ âŸ¨ ğ““âˆ âŸ©) (Î´ : is-Directed ğ““âˆ Î±)
      â†’ is-lowerbound-of-upperbounds (underlying-order ğ“”) (m (âˆ ğ““âˆ {A} {Î±} Î´)) (m âˆ˜ Î±)
    Î³ A Î± Î´ y ub =
     âˆ-is-lowerbound-of-upperbounds ğ“”
      (colimit-family-is-directed (âˆ ğ““âˆ {A} {Î±} Î´)) y Ïˆ
      where
       Ïˆ : (i : I) â†’ g i (â¦… âˆ ğ““âˆ {A} {Î±} Î´ â¦† i) âŠ‘âŸ¨ ğ“” âŸ© y
       Ïˆ i = g i (â¦… âˆ ğ““âˆ {A} {Î±} Î´ â¦† i)         âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚ ]
             âˆ ğ“” {A} {Î» a â†’ g i (â¦… Î± a â¦† i)} Î´â‚‚ âŠ‘âŸ¨ ğ“” âŸ©[ uâ‚‚ ]
             y                                  âˆâŸ¨ ğ“” âŸ©
        where
         Î´â‚ : is-Directed (ğ““ i) (family-at-ith-component Î± i)
         Î´â‚ = family-at-ith-component-is-directed Î± Î´ i
         Î´â‚‚ : is-Directed ğ“” (Î» a â†’ g i (â¦… Î± a â¦† i))
         Î´â‚‚ = image-is-directed' (ğ““ i) ğ“” (g i , g-cont i) Î´â‚
         uâ‚ = continuous-âˆ-âŠ‘ (ğ““ i) ğ“” (g i , g-cont i) Î´â‚
         uâ‚‚ = âˆ-is-lowerbound-of-upperbounds ğ“” Î´â‚‚ y Ï•
          where
           Ï• : (a : A) â†’ g i (â¦… Î± a â¦† i) âŠ‘âŸ¨ ğ“” âŸ© y
           Ï• a = g i (â¦… Î± a â¦† i)                         âŠ‘âŸ¨ ğ“” âŸ©[ v    ]
                 âˆ ğ“” (colimit-family-is-directed (Î± a)) âŠ‘âŸ¨ ğ“” âŸ©[ ub a ]
                 y                                      âˆâŸ¨ ğ“” âŸ©
            where
             v = âˆ-is-upperbound ğ“” (colimit-family-is-directed (Î± a)) i

\end{code}

Finally, we consider a curried version of Îµâˆ-family, which will prove useful
(see Dinfinity.lagda) in the construction of Scott's Dâˆ for which Dâˆ is
isomorphic to its own self-exponential.

\begin{code}

 ÎµâˆÏ€âˆ-family : I â†’ âŸ¨ ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ âŸ©
 ÎµâˆÏ€âˆ-family i = DCPO-âˆ˜ ğ““âˆ (ğ““ i) ğ““âˆ (Ï€âˆ' i) (Îµâˆ' i)

 ÎµâˆÏ€âˆ-family-is-monotone : {i j : I} â†’ i âŠ‘ j
                         â†’ ÎµâˆÏ€âˆ-family i âŠ‘âŸ¨ ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ âŸ© ÎµâˆÏ€âˆ-family j
 ÎµâˆÏ€âˆ-family-is-monotone {i} {j} l Ïƒ = Îµâˆ-family-is-monotone Ïƒ i j l

 ÎµâˆÏ€âˆ-family-is-directed : is-Directed (ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ) ÎµâˆÏ€âˆ-family
 ÎµâˆÏ€âˆ-family-is-directed = I-inhabited , Î´
  where
   Î´ : is-semidirected (underlying-order (ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ)) ÎµâˆÏ€âˆ-family
   Î´ i j = âˆ¥âˆ¥-functor Î³ (I-semidirected i j)
    where
     Î³ : (Î£ k ê‰ I , i âŠ‘ k Ã— j âŠ‘ k)
       â†’ (Î£ k ê‰ I , ÎµâˆÏ€âˆ-family i âŠ‘âŸ¨ ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ âŸ© ÎµâˆÏ€âˆ-family k
                  Ã— ÎµâˆÏ€âˆ-family j âŠ‘âŸ¨ ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ âŸ© ÎµâˆÏ€âˆ-family k)
     Î³ (k , láµ¢ , lâ±¼) =
      k , ÎµâˆÏ€âˆ-family-is-monotone láµ¢ ,
          ÎµâˆÏ€âˆ-family-is-monotone lâ±¼

 âˆ-of-ÎµâˆÏ€âˆs-is-id : âˆ (ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ) {I} {ÎµâˆÏ€âˆ-family} ÎµâˆÏ€âˆ-family-is-directed
                  ï¼ id , id-is-continuous ğ““âˆ
 âˆ-of-ÎµâˆÏ€âˆs-is-id = to-continuous-function-ï¼ ğ““âˆ ğ““âˆ Î³
  where
   Î´ : is-Directed (ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ) ÎµâˆÏ€âˆ-family
   Î´ = ÎµâˆÏ€âˆ-family-is-directed
   Î³ : [ ğ““âˆ , ğ““âˆ ]âŸ¨ âˆ (ğ““âˆ âŸ¹áµˆá¶œáµ–áµ’ ğ““âˆ) {I} {ÎµâˆÏ€âˆ-family} Î´ âŸ© âˆ¼ id
   Î³ Ïƒ = âˆ ğ““âˆ {I} {Î» i â†’ Îµâˆ i (â¦… Ïƒ â¦† i)} Î´â‚ ï¼âŸ¨ eâ‚ âŸ©
         âˆ ğ““âˆ {I} {Î» i â†’ Îµâˆ i (â¦… Ïƒ â¦† i)} Î´â‚‚ ï¼âŸ¨ eâ‚‚ âŸ©
         Ïƒ                                  âˆ
    where
     Î´â‚ : is-Directed ğ““âˆ (Î» i â†’ Îµâˆ i (â¦… Ïƒ â¦† i))
     Î´â‚ = pointwise-family-is-directed ğ““âˆ ğ““âˆ ÎµâˆÏ€âˆ-family Î´ Ïƒ
     Î´â‚‚ : is-Directed ğ““âˆ (Î» i â†’ Îµâˆ i (â¦… Ïƒ â¦† i))
     Î´â‚‚ = Îµâˆ-family-is-directed Ïƒ
     eâ‚ = âˆ-independent-of-directedness-witness ğ““âˆ Î´â‚ Î´â‚‚
     eâ‚‚ = (âˆ-of-Îµâˆs Ïƒ) â»Â¹

\end{code}

Added 9 February 2022.

If every dcpo in the diagram is locally small, then so is its bilimit.

\begin{code}

 ğ““âˆ-is-locally-small : ((i : I) â†’ is-locally-small (ğ““ i))
                     â†’ is-locally-small ğ““âˆ
 ğ““âˆ-is-locally-small ls = record { _âŠ‘â‚›_ = _âŠ‘â‚›âŸ¨âˆâŸ©_ ; âŠ‘â‚›-â‰ƒ-âŠ‘ = Î³ }
  where
   small-order : (i : I) â†’ âŸ¨ ğ““ i âŸ© â†’ âŸ¨ ğ““ i âŸ© â†’ ğ“¥ Ì‡
   small-order i = _âŠ‘â‚›_ where open is-locally-small (ls i)
   syntax small-order i x y = x âŠ‘â‚›âŸ¨ i âŸ© y
   âŠ‘â‚›-â‰ƒ-âŠ‘-at : (i : I) {x y : âŸ¨ ğ““ i âŸ©} â†’ x âŠ‘â‚›âŸ¨ i âŸ© y â‰ƒ x âŠ‘âŸ¨ ğ““ i âŸ© y
   âŠ‘â‚›-â‰ƒ-âŠ‘-at i = âŠ‘â‚›-â‰ƒ-âŠ‘ where open is-locally-small (ls i)
   _âŠ‘â‚›âŸ¨âˆâŸ©_ : âŸ¨ ğ““âˆ âŸ© â†’ âŸ¨ ğ““âˆ âŸ© â†’ ğ“¥ Ì‡
   Ïƒ âŠ‘â‚›âŸ¨âˆâŸ© Ï„ = (i : I) â†’ â¦… Ïƒ â¦† i âŠ‘â‚›âŸ¨ i âŸ© â¦… Ï„ â¦† i
   Î³ : {Ïƒ Ï„ : âŸ¨ ğ““âˆ âŸ©} â†’ (Ïƒ âŠ‘â‚›âŸ¨âˆâŸ© Ï„) â‰ƒ (Ïƒ âŠ‘âŸ¨ ğ““âˆ âŸ© Ï„)
   Î³ {Ïƒ} {Ï„} = Î -cong fe fe (Î» i â†’ âŠ‘â‚›-â‰ƒ-âŠ‘-at i)

\end{code}

Next we are going to show that taking the bilimit is closed under structural
continuity/algebraicity and having a small (compact) basis.

To ease the development we first develop some generalities. Given I-indexed
families Î±áµ¢ from Jáµ¢ into ğ““áµ¢, we construct a family Î±âˆ from Î£ J to ğ““âˆ and present
criteria for calculating its supremum and for it being directed.

\begin{code}

 module ğ““âˆ-family
         (J : (i : I) â†’ ğ“¥ Ì‡ )
         (Î± : (i : I) â†’ J i â†’ âŸ¨ ğ““ i âŸ©)
        where

  Jâˆ : ğ“¥ Ì‡
  Jâˆ = Î£ i ê‰ I , J i

  Jâˆ-is-inhabited : ((i : I) â†’ âˆ¥ J i âˆ¥) â†’ âˆ¥ Jâˆ âˆ¥
  Jâˆ-is-inhabited J-inh =
   âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop (Î» i â†’ âˆ¥âˆ¥-functor (Î» j â†’ (i , j)) (J-inh i)) I-inhabited

  Î±âˆ : Jâˆ â†’ âŸ¨ ğ““âˆ âŸ©
  Î±âˆ (i , j) = Îµâˆ i (Î± i j)

  Î±âˆ-is-sup-lemma : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (Î´ : ((i : I) â†’ is-Directed (ğ““ i) (Î± i)))
                  â†’ ((i : I) â†’ âˆ (ğ““ i) (Î´ i) ï¼ â¦… Ïƒ â¦† i)
                  â†’ is-sup _â‰¼_ Ïƒ Î±âˆ
  Î±âˆ-is-sup-lemma Ïƒ Î´ e =
   transport (Î» - â†’ is-sup _â‰¼_ - Î±âˆ) (Ïƒ-is-sup â»Â¹) (ub , lb-of-ubs)
    where
     Î´' : (i : I) â†’ is-Directed ğ““âˆ (Îµâˆ i âˆ˜ Î± i)
     Î´' i = image-is-directed' (ğ““ i) ğ““âˆ (Îµâˆ' i) (Î´ i)
     eâ‚ : Îµâˆ-family Ïƒ ï¼ (Î» i â†’ Îµâˆ i (âˆ (ğ““ i) (Î´ i)))
     eâ‚ = dfunext fe (Î» i â†’ ap (Îµâˆ i) (e i) â»Â¹)
     eâ‚‚ : (Î» i â†’ Îµâˆ i (âˆ (ğ““ i) (Î´ i))) ï¼ (Î» i â†’ âˆ ğ““âˆ (Î´' i))
     eâ‚‚ = dfunext fe (Î» i â†’ continuous-âˆ-ï¼ (ğ““ i) ğ““âˆ (Îµâˆ' i) (Î´ i))

     Î´â‚ : is-Directed ğ““âˆ (Î» (i : I) â†’ Îµâˆ i (âˆ (ğ““ i) (Î´ i)))
     Î´â‚ = transport (is-Directed ğ““âˆ) eâ‚ (Îµâˆ-family-is-directed Ïƒ)
     Î´â‚‚ : is-Directed ğ““âˆ (Î» i â†’ âˆ ğ““âˆ (Î´' i))
     Î´â‚‚ = transport (is-Directed ğ““âˆ) eâ‚‚ Î´â‚

     Ïƒ-is-sup = Ïƒ                              ï¼âŸ¨ âˆ-of-Îµâˆs Ïƒ âŸ©
                âˆ ğ““âˆ (Îµâˆ-family-is-directed Ïƒ) ï¼âŸ¨ â¦…1â¦† âŸ©
                âˆ ğ““âˆ Î´â‚                        ï¼âŸ¨ â¦…2â¦† âŸ©
                âˆ ğ““âˆ Î´â‚‚                        âˆ
      where
       â¦…1â¦† = âˆ-family-ï¼ ğ““âˆ eâ‚ (Îµâˆ-family-is-directed Ïƒ)
       â¦…2â¦† = âˆ-family-ï¼ ğ““âˆ eâ‚‚ Î´â‚

     ub : (k : Jâˆ) â†’ Î±âˆ k â‰¼ âˆ ğ““âˆ Î´â‚‚
     ub (i , j) = transitivity ğ““âˆ (Î±âˆ (i , j)) (âˆ ğ““âˆ (Î´' i)) (âˆ ğ““âˆ Î´â‚‚) â¦…1â¦† â¦…2â¦†
      where
       â¦…1â¦† : Î±âˆ (i , j) âŠ‘âŸ¨ ğ““âˆ âŸ© âˆ ğ““âˆ (Î´' i)
       â¦…1â¦† = âˆ-is-upperbound ğ““âˆ (Î´' i) j
       â¦…2â¦† : âˆ ğ““âˆ (Î´' i) âŠ‘âŸ¨ ğ““âˆ âŸ© âˆ ğ““âˆ Î´â‚‚
       â¦…2â¦† = âˆ-is-upperbound ğ““âˆ Î´â‚‚ i

     lb-of-ubs : is-lowerbound-of-upperbounds _â‰¼_ (âˆ ğ““âˆ Î´â‚‚) Î±âˆ
     lb-of-ubs Ï„ Ï„-is-ub = âˆ-is-lowerbound-of-upperbounds ğ““âˆ Î´â‚‚ Ï„
                            (Î» i â†’ âˆ-is-lowerbound-of-upperbounds ğ““âˆ (Î´' i) Ï„
                                    (Î» j â†’ Ï„-is-ub (i , j)))

  Î±âˆ-is-directed-sup-lemma : (Ïƒ : âŸ¨ ğ““âˆ âŸ©)
                             (Î´ : ((i : I) â†’ is-Directed (ğ““ i) (Î± i)))
                           â†’ ((i : I) â†’ âˆ (ğ““ i) (Î´ i) ï¼ â¦… Ïƒ â¦† i)
                           â†’ (Î´âˆ : is-Directed ğ““âˆ Î±âˆ) â†’ âˆ ğ““âˆ Î´âˆ ï¼ Ïƒ
  Î±âˆ-is-directed-sup-lemma Ïƒ Î´ e Î´âˆ =
   antisymmetry ğ““âˆ (âˆ ğ““âˆ Î´âˆ) Ïƒ
                (âˆ-is-lowerbound-of-upperbounds ğ““âˆ Î´âˆ Ïƒ
                  (sup-is-upperbound _â‰¼_ Ïƒ-is-sup))
                (sup-is-lowerbound-of-upperbounds _â‰¼_ Ïƒ-is-sup (âˆ ğ““âˆ Î´âˆ)
                  (âˆ-is-upperbound ğ““âˆ Î´âˆ))
    where
     Ïƒ-is-sup : is-sup _â‰¼_ Ïƒ Î±âˆ
     Ïƒ-is-sup = Î±âˆ-is-sup-lemma Ïƒ Î´ e

  Î±âˆ-is-directed-lemma : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (Î´ : ((i : I) â†’ is-Directed (ğ““ i) (Î± i)))
                       â†’ ((i : I) â†’ âˆ (ğ““ i) (Î´ i) ï¼ â¦… Ïƒ â¦† i)
                       â†’ ((i : I) (j : J i) â†’ Î± i j â‰ªâŸ¨ ğ““ i âŸ© â¦… Ïƒ â¦† i)
                       â†’ is-Directed ğ““âˆ Î±âˆ
  Î±âˆ-is-directed-lemma Ïƒ Î´ sup Î±s-wb = Ind-âˆ-is-directed Îµâˆ-after-Î± dir
   where
    open import DomainTheory.BasesAndContinuity.IndCompletion pt fe ğ“¥
    open Ind-completion ğ““âˆ
    Î´' : (i : I) â†’ is-Directed ğ““âˆ (Îµâˆ i âˆ˜ Î± i)
    Î´' i = image-is-directed' (ğ““ i) ğ““âˆ (Îµâˆ' i) (Î´ i)
    Îµâˆ-after-Î± : I â†’ Ind
    Îµâˆ-after-Î± i = J i , Îµâˆ i âˆ˜ Î± i , Î´' i
    dir : is-directed _â‰²_ Îµâˆ-after-Î±
    dir = I-inhabited , semidir
     where
      semidir : is-semidirected _â‰²_ Îµâˆ-after-Î±
      semidir iâ‚ iâ‚‚ =
       âˆ¥âˆ¥-functor (Î» (i , lâ‚ , lâ‚‚) â†’ i , cofinality-lemma lâ‚
                                       , cofinality-lemma lâ‚‚)
                  (I-semidirected iâ‚ iâ‚‚)
       where
        cofinality-lemma : {i i' : I} â†’ i âŠ‘ i' â†’ Îµâˆ-after-Î± i â‰² Îµâˆ-after-Î± i'
        cofinality-lemma {i} {i'} l j =
         âˆ¥âˆ¥-functor lem (wb (J i') (Î± i') (Î´ i') (ï¼-to-âŠ’ (ğ““ i') (sup i')))
          where
           lem : (Î£ j' ê‰ J i' , Îµ l (Î± i j) âŠ‘âŸ¨ ğ““ i' âŸ© Î± i' j')
               â†’ (Î£ j' ê‰ J i' , Îµâˆ i (Î± i j) âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ i' (Î± i' j'))
           lem (j' , u) = j' , transitivity ğ““âˆ (Îµâˆ i (Î± i j))
                                               (Îµâˆ i' (Îµ l (Î± i j)))
                                               (Îµâˆ i' (Î± i' j'))
                                               â¦…1â¦† â¦…2â¦†
            where
             â¦…1â¦† : Îµâˆ i (Î± i j) âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ i' (Îµ l (Î± i j))
             â¦…1â¦† = ï¼-to-âŠ‘ ğ““âˆ ((Îµâˆ-commutes-with-Îµs i i' l (Î± i j)) â»Â¹)
             â¦…2â¦† : Îµâˆ i' (Îµ l (Î± i j)) âŠ‘âŸ¨ ğ““âˆ âŸ© Îµâˆ i' (Î± i' j')
             â¦…2â¦† = monotone-if-continuous (ğ““ i') ğ““âˆ (Îµâˆ' i')
                    (Îµ l (Î± i j)) (Î± i' j') u
           wb : Îµ l (Î± i j) â‰ªâŸ¨ ğ““ i' âŸ© â¦… Ïƒ â¦† i'
           wb = â‰ª-âŠ‘-to-â‰ª (ğ““ i') wb' ineq
            where
             wb' : Îµ l (Î± i j) â‰ªâŸ¨ ğ““ i' âŸ© Îµ l (â¦… Ïƒ â¦† i)
             wb' = embeddings-preserve-â‰ª (ğ““ i) (ğ““ i')
                                         (Îµ l) (Îµ-is-continuous l)
                                         (Ï€ l) (Ï€-is-continuous l)
                                         (Îµ-section-of-Ï€ l) (ÎµÏ€-deflation l)
                                         (Î± i j) (â¦… Ïƒ â¦† i) (Î±s-wb i j)
             ineq = Îµ l (Ï€âˆ i Ïƒ)        âŠ‘âŸ¨ ğ““ i' âŸ©[ â¦…1â¦† ]
                    Îµ l (Ï€ l (Ï€âˆ i' Ïƒ)) âŠ‘âŸ¨ ğ““ i' âŸ©[ â¦…2â¦† ]
                    â¦… Ïƒ â¦† i'            âˆâŸ¨ ğ““ i' âŸ©
              where
               â¦…1â¦† = ï¼-to-âŠ‘ (ğ““ i') (ap (Îµ l) ((Ï€âˆ-commutes-with-Ï€s i i' l Ïƒ) â»Â¹))
               â¦…2â¦† = ÎµÏ€-deflation l (Ï€âˆ i' Ïƒ)

\end{code}

The construction that defines the family Î±âˆ into ğ““âˆ preserves the way-below
relation and compactness in a sense made precise below.

\begin{code}

  Î±âˆ-is-way-below : (Ïƒ : âŸ¨ ğ““âˆ âŸ©)
                  â†’ ((i : I) (j : J i) â†’ Î± i j â‰ªâŸ¨ ğ““ i âŸ© â¦… Ïƒ â¦† i)
                  â†’ (j : Jâˆ) â†’ Î±âˆ j â‰ªâŸ¨ ğ““âˆ âŸ© Ïƒ
  Î±âˆ-is-way-below Ïƒ wb (i , j) = â‰ª-âŠ‘-to-â‰ª ğ““âˆ lem (ÎµâˆÏ€âˆ-deflation Ïƒ)
   where
    lem : Îµâˆ i (Î± i j) â‰ªâŸ¨ ğ““âˆ âŸ© Îµâˆ i (Ï€âˆ i Ïƒ)
    lem = embeddings-preserve-â‰ª (ğ““ i) ğ““âˆ
           (Îµâˆ i) (Îµâˆ-is-continuous i)
           (Ï€âˆ i) (Ï€âˆ-is-continuous i)
           Îµâˆ-section-of-Ï€âˆ ÎµâˆÏ€âˆ-deflation
           (Î± i j) (Ï€âˆ i Ïƒ)
           (wb i j)

  Î±âˆ-is-compact : ((i : I) (j : J i) â†’ is-compact (ğ““ i) (Î± i j))
                â†’ (j : Jâˆ) â†’ is-compact ğ““âˆ (Î±âˆ j)
  Î±âˆ-is-compact Îº (i , j) = embeddings-preserve-compactness (ğ““ i) ğ““âˆ
                             (Îµâˆ i) (Îµâˆ-is-continuous i)
                             (Ï€âˆ i) (Ï€âˆ-is-continuous i)
                             Îµâˆ-section-of-Ï€âˆ ÎµâˆÏ€âˆ-deflation
                             (Î± i j) (Îº i j)

\end{code}

It is now fairly straightforward to prove that if each ğ““áµ¢ is structurally
continuous, then so is its bilimit ğ““âˆ.

Note how we don't expect to have a similar result for ordinary continuity,
because this seems to need instances of the axiom of choice in general.

\begin{code}

 ğ““âˆ-structurally-continuous : ((i : I) â†’ structurally-continuous (ğ““ i))
                            â†’ structurally-continuous ğ““âˆ
 ğ““âˆ-structurally-continuous ğ“’ =
  record
   { index-of-approximating-family     = Jâˆâº
   ; approximating-family              = Î±âˆâº
   ; approximating-family-is-directed  = Î±âˆâº-is-directed
   ; approximating-family-is-way-below = Î±âˆâº-is-way-below
   ; approximating-family-âˆ-ï¼          = Î±âˆâº-âˆ-ï¼
   }
   where
    open structurally-continuous
    J : (i : I) â†’ âŸ¨ ğ““ i âŸ© â†’ ğ“¥ Ì‡
    J i = index-of-approximating-family (ğ“’ i)
    Î± : (i : I) (x : âŸ¨ ğ““ i âŸ©) â†’ J i x â†’ âŸ¨ ğ““ i âŸ©
    Î± i = approximating-family (ğ“’ i)
    Î´ : (i : I) (x : âŸ¨ ğ““ i âŸ©) â†’ is-Directed (ğ““ i) (Î± i x)
    Î´ i = approximating-family-is-directed (ğ“’ i)

    Jâº : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ I â†’ ğ“¥ Ì‡
    Jâº Ïƒ i = J i (â¦… Ïƒ â¦† i)
    Î±âº : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (i : I) â†’ Jâº Ïƒ i â†’ âŸ¨ ğ““ i âŸ©
    Î±âº Ïƒ i = Î± i (â¦… Ïƒ â¦† i)

    module _
            (Ïƒ : âŸ¨ ğ““âˆ âŸ©)
           where

     open ğ““âˆ-family (Jâº Ïƒ) (Î±âº Ïƒ)

     Jâˆâº :  ğ“¥ Ì‡
     Jâˆâº = Jâˆ
     Î±âˆâº : Jâˆâº â†’ âŸ¨ ğ““âˆ âŸ©
     Î±âˆâº = Î±âˆ
     Î±âˆâº-is-way-below : is-way-upperbound ğ““âˆ Ïƒ Î±âˆâº
     Î±âˆâº-is-way-below = Î±âˆ-is-way-below Ïƒ
                         (Î» i j â†’ approximating-family-is-way-below (ğ“’ i)
                                   (â¦… Ïƒ â¦† i) j)
     Î±âˆâº-is-directed : is-Directed ğ““âˆ Î±âˆâº
     Î±âˆâº-is-directed = Î±âˆ-is-directed-lemma Ïƒ
                        (Î» i â†’ Î´ i (â¦… Ïƒ â¦† i))
                        (Î» i â†’ approximating-family-âˆ-ï¼ (ğ“’ i) (â¦… Ïƒ â¦† i))
                        (Î» i â†’ approximating-family-is-way-below (ğ“’ i) (â¦… Ïƒ â¦† i))
     Î±âˆâº-âˆ-ï¼ : âˆ ğ““âˆ Î±âˆâº-is-directed ï¼ Ïƒ
     Î±âˆâº-âˆ-ï¼ = Î±âˆ-is-directed-sup-lemma Ïƒ
                (Î» i â†’ Î´ i (â¦… Ïƒ â¦† i))
                (Î» i â†’ approximating-family-âˆ-ï¼ (ğ“’ i) (â¦… Ïƒ â¦† i))
                Î±âˆâº-is-directed

\end{code}

Similarly, if each ğ““áµ¢ is structurally algebraic then so is its bilimit ğ““âˆ.

\begin{code}

 ğ““âˆ-structurally-algebraic : ((i : I) â†’ structurally-algebraic (ğ““ i))
                           â†’ structurally-algebraic ğ““âˆ
 ğ““âˆ-structurally-algebraic ğ“ =
  record
   { index-of-compact-family    = index-of-approximating-family Câˆ
   ; compact-family             = approximating-family Câˆ
   ; compact-family-is-directed = approximating-family-is-directed Câˆ
   ; compact-family-is-compact  = Î³
   ; compact-family-âˆ-ï¼         = approximating-family-âˆ-ï¼ Câˆ
   }
   where
    open structurally-continuous
    open structurally-algebraic
    ğ“’ : (i : I) â†’ structurally-continuous (ğ““ i)
    ğ“’ i = structurally-continuous-if-structurally-algebraic (ğ““ i) (ğ“ i)
    Câˆ : structurally-continuous ğ““âˆ
    Câˆ = ğ““âˆ-structurally-continuous ğ“’
    Jâˆ : âŸ¨ ğ““âˆ âŸ© â†’ ğ“¥ Ì‡
    Jâˆ = index-of-approximating-family Câˆ
    Î±âˆ : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) â†’ Jâˆ Ïƒ â†’ âŸ¨ ğ““âˆ âŸ©
    Î±âˆ = approximating-family Câˆ
    Î³ : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (j : Jâˆ Ïƒ) â†’ is-compact ğ““âˆ (Î±âˆ Ïƒ j)
    Î³ Ïƒ (i , j) = embeddings-preserve-compactness (ğ““ i) ğ““âˆ
                   (Îµâˆ i) (Îµâˆ-is-continuous i) (Ï€âˆ i) (Ï€âˆ-is-continuous i)
                   Îµâˆ-section-of-Ï€âˆ ÎµâˆÏ€âˆ-deflation
                   (compact-family (ğ“ i) (â¦… Ïƒ â¦† i) j)
                   (compact-family-is-compact (ğ“ i) (â¦… Ïƒ â¦† i) j)

\end{code}

With a little bit more work, we can show that if each ğ““áµ¢ comes equipped with a
small (compact) basis, then the bilimit ğ““âˆ does too.

\begin{code}

 ğ““âˆ-has-small-basis : ((i : I) â†’ has-specified-small-basis (ğ““ i))
                    â†’ has-specified-small-basis ğ““âˆ
 ğ““âˆ-has-small-basis ğ“‘ = (Bâˆ , Î²âˆ , Î²âˆ-is-small-basis)
  where
   B : I â†’ ğ“¥ Ì‡
   B i = prâ‚ (ğ“‘ i)
   Î² : (i : I) â†’ B i â†’ âŸ¨ ğ““ i âŸ©
   Î² i = prâ‚ (prâ‚‚ (ğ“‘ i))
   Î²-is-small-basis : (i : I) â†’ is-small-basis (ğ““ i) (Î² i)
   Î²-is-small-basis i = prâ‚‚ (prâ‚‚ (ğ“‘ i))
   Bâˆ : ğ“¥ Ì‡
   Bâˆ = Î£ i ê‰ I , B i
   Î²âˆ : Bâˆ â†’ âŸ¨ ğ““âˆ âŸ©
   Î²âˆ (i , b) = Îµâˆ i (Î² i b)

   ğ““s-are-locally-small : (i : I) â†’ is-locally-small (ğ““ i)
   ğ““s-are-locally-small i = locally-small-if-small-basis (ğ““ i) (Î² i)
                             (Î²-is-small-basis i)

   ğ““s-are-structurally-continuous : (i : I) â†’ structurally-continuous (ğ““ i)
   ğ““s-are-structurally-continuous i =
    structurally-continuous-if-specified-small-basis (ğ““ i)
     (B i , Î² i , Î²-is-small-basis i)

   Î²âˆ-is-small-basis : is-small-basis ğ““âˆ Î²âˆ
   Î²âˆ-is-small-basis =
    record
     { â‰ªá´®-is-small    = lemmaâ‚
     ; â†¡á´®-is-directed = lemmaâ‚‚
     ; â†¡á´®-is-sup      = lemmaâ‚ƒ
     }
     where
      open is-small-basis

      lemmaâ‚ : (Ïƒ : âŸ¨ ğ““âˆ âŸ©) (b : Bâˆ) â†’ is-small (Î²âˆ b â‰ªâŸ¨ ğ““âˆ âŸ© Ïƒ)
      lemmaâ‚ Ïƒ (i , b) =
       â‰ª-is-small-valued-str ğ““âˆ
        (ğ““âˆ-structurally-continuous ğ““s-are-structurally-continuous)
        (ğ““âˆ-is-locally-small ğ““s-are-locally-small)
        (Î²âˆ (i , b)) Ïƒ

      module _
              (Ïƒ : âŸ¨ ğ““âˆ âŸ©)
             where

       â†¡á´®â‚›âº : (i : I) â†’ ğ“¥ Ì‡
       â†¡á´®â‚›âº i = â†¡á´®â‚› (Î²-is-small-basis i) (â¦… Ïƒ â¦† i)
       â†¡Î¹â‚›âº : (i : I) â†’ â†¡á´®â‚›âº i â†’ âŸ¨ ğ““ i âŸ©
       â†¡Î¹â‚›âº i = â†¡-inclusionâ‚› (Î²-is-small-basis i) (â¦… Ïƒ â¦† i)
       open ğ““âˆ-family â†¡á´®â‚›âº â†¡Î¹â‚›âº

       Î¹ : Jâˆ â†’ â†¡á´® ğ““âˆ Î²âˆ Ïƒ
       Î¹ (i , b , u) = ((i , b) , v)
        where
         v : Îµâˆ i (Î² i b) â‰ªâŸ¨ ğ““âˆ âŸ© Ïƒ
         v = â‰ª-âŠ‘-to-â‰ª ğ““âˆ w (ÎµâˆÏ€âˆ-deflation Ïƒ)
          where
           w : Îµâˆ i (Î² i b) â‰ªâŸ¨ ğ““âˆ âŸ© Îµâˆ i (â¦… Ïƒ â¦† i)
           w = embeddings-preserve-â‰ª (ğ““ i) ğ““âˆ
                (Îµâˆ i) (Îµâˆ-is-continuous i) (Ï€âˆ i) (Ï€âˆ-is-continuous i)
                Îµâˆ-section-of-Ï€âˆ ÎµâˆÏ€âˆ-deflation
                (Î² i b) (â¦… Ïƒ â¦† i) (â‰ªá´®â‚›-to-â‰ªá´® (Î²-is-small-basis i) u)

       sublemmaâ‚ : is-Directed ğ““âˆ (â†¡-inclusion ğ““âˆ Î²âˆ Ïƒ âˆ˜ Î¹)
       sublemmaâ‚ = Î±âˆ-is-directed-lemma Ïƒ
                    (Î» i â†’ â†¡á´®â‚›-is-directed (Î²-is-small-basis i) (â¦… Ïƒ â¦† i))
                    (Î» i â†’ â†¡á´®â‚›-âˆ-ï¼ (Î²-is-small-basis i) (â¦… Ïƒ â¦† i))
                    (Î» i â†’ â†¡á´®â‚›-is-way-below (Î²-is-small-basis i) (â¦… Ïƒ â¦† i))

       sublemmaâ‚‚ : Ïƒ ï¼ âˆ ğ““âˆ sublemmaâ‚
       sublemmaâ‚‚ = (Î±âˆ-is-directed-sup-lemma Ïƒ Î´s es sublemmaâ‚) â»Â¹
        where
         Î´s : (i : I) â†’ is-Directed (ğ““ i) (â†¡-inclusionâ‚› (Î²-is-small-basis i) (â¦… Ïƒ â¦† i))
         Î´s i = â†¡á´®â‚›-is-directed (Î²-is-small-basis i) (â¦… Ïƒ â¦† i)
         es : (i : I) â†’ âˆ (ğ““ i) (Î´s i) ï¼ â¦… Ïƒ â¦† i
         es i = â†¡á´®â‚›-âˆ-ï¼ (Î²-is-small-basis i) (â¦… Ïƒ â¦† i)

       lemmaâ‚‚ : is-Directed ğ““âˆ (â†¡-inclusion ğ““âˆ Î²âˆ Ïƒ)
       lemmaâ‚‚ = â†¡á´®-directedness-criterion ğ““âˆ Î²âˆ Ïƒ Î¹
                 sublemmaâ‚ (ï¼-to-âŠ‘ ğ““âˆ sublemmaâ‚‚)

       lemmaâ‚ƒ : is-sup (underlying-order ğ““âˆ) Ïƒ (â†¡-inclusion ğ““âˆ Î²âˆ Ïƒ)
       lemmaâ‚ƒ = â†¡á´®-sup-criterion ğ““âˆ Î²âˆ Ïƒ Î¹ claim
        where
         claim : is-sup (underlying-order ğ““âˆ) Ïƒ (â†¡-inclusion ğ““âˆ Î²âˆ Ïƒ âˆ˜ Î¹)
         claim =
          transport (Î» - â†’ is-sup (underlying-order ğ““âˆ) - (â†¡-inclusion ğ““âˆ Î²âˆ Ïƒ âˆ˜ Î¹))
                    (sublemmaâ‚‚ â»Â¹)
                    (âˆ-is-sup ğ““âˆ sublemmaâ‚)

 ğ““âˆ-has-small-compact-basis :
    ((i : I) â†’ has-specified-small-compact-basis (ğ““ i))
  â†’ has-specified-small-compact-basis ğ““âˆ
 ğ““âˆ-has-small-compact-basis Îº = (Bâˆ , Î²âˆ , Î³)
  where
   B : (i : I) â†’ ğ“¥ Ì‡
   B i = prâ‚ (Îº i)
   Î² : (i : I) â†’ B i â†’ âŸ¨ ğ““ i âŸ©
   Î² i = prâ‚ (prâ‚‚ (Îº i))
   Î²-is-small-compact-basis : (i : I) â†’ is-small-compact-basis (ğ““ i) (Î² i)
   Î²-is-small-compact-basis i = prâ‚‚ (prâ‚‚ (Îº i))
   Î²-is-small-basis : (i : I) â†’ is-small-basis (ğ““ i) (Î² i)
   Î²-is-small-basis i = compact-basis-is-basis (ğ““ i) (Î² i)
                         (Î²-is-small-compact-basis i)

   ğ”¹ : has-specified-small-basis ğ““âˆ
   ğ”¹ = ğ““âˆ-has-small-basis (Î» i â†’ (B i , Î² i , Î²-is-small-basis i))
   Bâˆ : ğ“¥ Ì‡
   Bâˆ = prâ‚ ğ”¹
   Î²âˆ : Bâˆ â†’ âŸ¨ ğ““âˆ âŸ©
   Î²âˆ = prâ‚ (prâ‚‚ ğ”¹)
   Î²âˆ-is-small-basis : is-small-basis ğ““âˆ Î²âˆ
   Î²âˆ-is-small-basis = prâ‚‚ (prâ‚‚ ğ”¹)

   Î³ : is-small-compact-basis ğ““âˆ Î²âˆ
   Î³ = small-and-compact-basis ğ““âˆ Î²âˆ Î²âˆ-is-small-basis Î²âˆ-is-compact
    where
     open is-small-compact-basis
     Î²âˆ-is-compact : (b : Bâˆ) â†’ is-compact ğ““âˆ (Î²âˆ b)
     Î²âˆ-is-compact (i , b) = embeddings-preserve-compactness (ğ““ i) ğ““âˆ
                              (Îµâˆ i) (Îµâˆ-is-continuous i)
                              (Ï€âˆ i) (Ï€âˆ-is-continuous i)
                              Îµâˆ-section-of-Ï€âˆ ÎµâˆÏ€âˆ-deflation
                              (Î² i b)
                              (basis-is-compact (Î²-is-small-compact-basis i) b)

\end{code}
