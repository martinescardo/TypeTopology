Tom de Jong, 25 January 2022

We consider the powerset of a set as a pointed dcpo. Given a set X : ğ“¤, the
ğ“¥-valued subsets X â†’ Î© ğ“¥ are a pointed ğ“¥-dcpo.

Taking ğ“¥ to be equal to ğ“¤ is more interesting as in this case we get a pointed
dcpo with a small compact basis (given by lists on X). In particular, the
powerset is algebraic and we characterize its compact elements as the Kuratowski
finite subsets.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Sets

module DomainTheory.Examples.Powerset
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        {X : ğ“¤ Ì‡ }
        (X-is-set : is-set X)
       where

open import MLTT.List

open import UF.Equiv
open import UF.ImageAndSurjection pt
open import UF.Powerset
open import UF.Powerset-Fin pt
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier
open import UF.SubtypeClassifier-Properties

open import OrderedTypes.Poset fe

open binary-unions-of-subsets pt
open canonical-map-from-lists-to-subsets X-is-set
open PropositionalTruncation pt
open singleton-subsets X-is-set

module _
        (ğ“¥ : Universe)
       where

 open import DomainTheory.Basics.Dcpo pt fe ğ“¥
 open import DomainTheory.Basics.Pointed pt fe ğ“¥

 open unions-of-small-families pt ğ“¥ ğ“¥ X

 generalized-ğ“Ÿ-DCPO : DCPO {ğ“¥ âº âŠ” ğ“¤} {ğ“¤ âŠ” ğ“¥}
 generalized-ğ“Ÿ-DCPO = (X â†’ Î© ğ“¥) , _âŠ†_ ,
                      ( powersets-are-sets'' fe fe pe
                      , âŠ†-is-prop' fe fe
                      , âŠ†-refl'
                      , âŠ†-trans'
                      , Î» A B â†’ subset-extensionality'' pe fe fe)
                      , dir-compl
  where
   dir-compl : is-directed-complete _âŠ†_
   dir-compl I Î± Î´ = â‹ƒ Î± , â‹ƒ-is-upperbound Î± , â‹ƒ-is-lowerbound-of-upperbounds Î±

 generalized-ğ“Ÿ-DCPOâŠ¥ : DCPOâŠ¥ {ğ“¥ âº âŠ” ğ“¤} {ğ“¤ âŠ” ğ“¥}
 generalized-ğ“Ÿ-DCPOâŠ¥ = (generalized-ğ“Ÿ-DCPO , âˆ… , âˆ…-is-least')

\end{code}

We now specialize to taking ğ“¥ to be equal to ğ“¤, the universe where X lives and
we prove that lists on X give a small compact basis for the powerset.

\begin{code}

open import DomainTheory.Basics.Dcpo pt fe ğ“¤
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import DomainTheory.Basics.Pointed pt fe ğ“¤
open import DomainTheory.Basics.WayBelow pt fe ğ“¤

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤

open unions-of-small-families pt ğ“¤ ğ“¤ X

ğ“Ÿ-DCPO : DCPO {ğ“¤ âº} {ğ“¤}
ğ“Ÿ-DCPO = generalized-ğ“Ÿ-DCPO ğ“¤

ğ“Ÿ-DCPOâŠ¥ : DCPOâŠ¥ {ğ“¤ âº} {ğ“¤}
ğ“Ÿ-DCPOâŠ¥ = generalized-ğ“Ÿ-DCPOâŠ¥ ğ“¤

Îºâº : (A : ğ“Ÿ X) â†’ (Î£ l ê‰ List X , Îº l âŠ† A) â†’ ğ“Ÿ X
Îºâº A = Îº âˆ˜ prâ‚

Îºâº-is-directed : (A : ğ“Ÿ X) â†’ is-Directed ğ“Ÿ-DCPO (Îºâº A)
Îºâº-is-directed A = inh , semidir
 where
  inh : âˆƒ l ê‰ List X , Îº l âŠ† A
  inh = âˆ£ [] , (âˆ…-is-least A) âˆ£
  semidir : is-semidirected _âŠ†_ (Îºâº A)
  semidir (lâ‚ , sâ‚) (lâ‚‚ , sâ‚‚) = âˆ£ ((lâ‚ ++ lâ‚‚) , s) , uâ‚ , uâ‚‚ âˆ£
   where
    e : Îº (lâ‚ ++ lâ‚‚) ï¼ Îº lâ‚ âˆª Îº lâ‚‚
    e = Îº-of-concatenated-lists-is-union pe fe lâ‚ lâ‚‚
    u : (Îº lâ‚ âˆª Îº lâ‚‚) âŠ† Îº (lâ‚ ++ lâ‚‚)
    u = ï¼-to-âŠ’ ğ“Ÿ-DCPO e
    -- Unfortunately, using the âŠ‘âŸ¨ ğ“Ÿ-DCPO âŸ©-syntax here gives
    -- implicit arguments problems, so we use âŠ†-trans instead.
    uâ‚ : Îº lâ‚ âŠ† Îº (lâ‚ ++ lâ‚‚)
    uâ‚ = âŠ†-trans (Îº lâ‚) (Îº lâ‚ âˆª Îº lâ‚‚) (Îº (lâ‚ ++ lâ‚‚))
          (âˆª-is-upperboundâ‚ (Îº lâ‚) (Îº lâ‚‚)) u
    uâ‚‚ = âŠ†-trans (Îº lâ‚‚) (Îº lâ‚ âˆª Îº lâ‚‚) (Îº (lâ‚ ++ lâ‚‚))
          (âˆª-is-upperboundâ‚‚ (Îº lâ‚) (Îº lâ‚‚)) u
    s : Îº (lâ‚ ++ lâ‚‚) âŠ† A
    s = âŠ†-trans (Îº (lâ‚ ++ lâ‚‚)) (Îº lâ‚ âˆª Îº lâ‚‚) A â¦…1â¦† â¦…2â¦†
     where
      â¦…1â¦† : Îº (lâ‚ ++ lâ‚‚) âŠ† (Îº lâ‚ âˆª Îº lâ‚‚)
      â¦…1â¦† = ï¼-to-âŠ‘ ğ“Ÿ-DCPO e
      â¦…2â¦† : (Îº lâ‚ âˆª Îº lâ‚‚) âŠ† A
      â¦…2â¦† = âˆª-is-lowerbound-of-upperbounds (Îº lâ‚) (Îº lâ‚‚) A sâ‚ sâ‚‚

Îºâº-sup : (A : ğ“Ÿ X) â†’ is-sup _âŠ†_ A (Îºâº A)
Îºâº-sup A = ub , lb-of-ubs
 where
  ub : is-upperbound _âŠ†_ A (Îºâº A)
  ub (l , l-subset-A) x x-in-l = l-subset-A x x-in-l
  lb-of-ubs : is-lowerbound-of-upperbounds _âŠ†_ A (Îºâº A)
  lb-of-ubs B B-is-ub x x-in-A = B-is-ub ([ x ] , s) x (âˆª-is-upperboundâ‚ â´ x âµ âˆ… x âˆˆ-â´âµ)
   where
    s : (â´ x âµ âˆª âˆ…) âŠ† A
    s = âˆª-is-lowerbound-of-upperbounds â´ x âµ âˆ… A
         (lr-implication (â´âµ-subset-characterization A) x-in-A)
         (âˆ…-is-least A)

Îºâº-â‹ƒ-âŠ† : (A : ğ“Ÿ X) â†’ â‹ƒ (Îºâº A) âŠ† A
Îºâº-â‹ƒ-âŠ† A = â‹ƒ-is-lowerbound-of-upperbounds (Îºâº A) A
        (sup-is-upperbound _âŠ†_ {_} {_} {A} {Îºâº A} (Îºâº-sup A))

Îºâº-â‹ƒ-âŠ‡ : (A : ğ“Ÿ X) â†’ â‹ƒ (Îºâº A) âŠ‡ A
Îºâº-â‹ƒ-âŠ‡ A = sup-is-lowerbound-of-upperbounds _âŠ†_ {_} {_} {A} {Îºâº A}
            (Îºâº-sup A) (â‹ƒ (Îºâº A))
            (â‹ƒ-is-upperbound (Îºâº A))

Îºâº-â‹ƒ-ï¼ : (A : ğ“Ÿ X) â†’ â‹ƒ (Îºâº A) ï¼ A
Îºâº-â‹ƒ-ï¼ A = subset-extensionality pe fe (Îºâº-â‹ƒ-âŠ† A) (Îºâº-â‹ƒ-âŠ‡ A)

Kuratowski-finite-subset-if-compact : (A : ğ“Ÿ X)
                                    â†’ is-compact ğ“Ÿ-DCPO A
                                    â†’ is-Kuratowski-finite-subset A
Kuratowski-finite-subset-if-compact A c =
 Kuratowski-finite-subset-if-in-image-of-Îº A Î³
  where
   claim : âˆƒ lâº ê‰ (Î£ l ê‰ List X , Îº l âŠ† A) , A âŠ† Îºâº A lâº
   claim = c (domain (Îºâº A)) (Îºâº A) (Îºâº-is-directed A) (Îºâº-â‹ƒ-âŠ‡ A)
   Î³ : A âˆˆimage Îº
   Î³ = âˆ¥âˆ¥-functor h claim
    where
     h : (Î£ lâº ê‰ (Î£ l ê‰ List X , Îº l âŠ† A) , A âŠ† Îºâº A lâº)
       â†’ Î£ l ê‰ List X , Îº l ï¼ A
     h ((l , s) , t) = (l , subset-extensionality pe fe s t)

âˆ…-is-compact : is-compact ğ“Ÿ-DCPO âˆ…
âˆ…-is-compact = âŠ¥-is-compact ğ“Ÿ-DCPOâŠ¥

singletons-are-compact : (x : X) â†’ is-compact ğ“Ÿ-DCPO â´ x âµ
singletons-are-compact x I Î± Î´ l = âˆ¥âˆ¥-functor h (l x âˆˆ-â´âµ)
 where
  h : (Î£ i ê‰ I , x âˆˆ Î± i)
    â†’ (Î£ i ê‰ I , â´ x âµ âŠ† Î± i)
  h (i , m) = (i , lr-implication (â´âµ-subset-characterization (Î± i)) m)

âˆª-is-compact : (A B : ğ“Ÿ X)
             â†’ is-compact ğ“Ÿ-DCPO A
             â†’ is-compact ğ“Ÿ-DCPO B
             â†’ is-compact ğ“Ÿ-DCPO (A âˆª B)
âˆª-is-compact A B =
 binary-join-is-compact ğ“Ÿ-DCPO {A} {B} {A âˆª B}
  (âˆª-is-upperboundâ‚ A B) (âˆª-is-upperboundâ‚‚ A B)
  (âˆª-is-lowerbound-of-upperbounds A B)

compact-if-Kuratowski-finite-subset : (A : ğ“Ÿ X)
                                    â†’ is-Kuratowski-finite-subset A
                                    â†’ is-compact ğ“Ÿ-DCPO A
compact-if-Kuratowski-finite-subset A k = lemma (A , k)
 where
  Q : ğ“š X â†’ ğ“¤ âº Ì‡
  Q A = is-compact ğ“Ÿ-DCPO (prâ‚ A)
  lemma : (A : ğ“š X) â†’ Q A
  lemma = Kuratowski-finite-subset-induction pe fe X X-is-set Q
           (Î» A â†’ being-compact-is-prop ğ“Ÿ-DCPO (prâ‚ A))
           âˆ…-is-compact
           singletons-are-compact
           (Î» A B â†’ âˆª-is-compact (prâ‚ A) (prâ‚ B))

Îº-is-small-compact-basis : is-small-compact-basis ğ“Ÿ-DCPO Îº
Îº-is-small-compact-basis =
 record
  { basis-is-compact = Î» l â†’ compact-if-Kuratowski-finite-subset (Îº l)
                            (Îº-of-list-is-Kuratowski-finite-subset l)
  ; âŠ‘á´®-is-small      = Î» A l â†’ (Îº l âŠ† A , â‰ƒ-refl (Îº l âŠ† A))
  ; â†“á´®-is-directed   = Îºâº-is-directed
  ; â†“á´®-is-sup        = Îºâº-sup
  }

ğ“Ÿ-has-specified-small-compact-basis : has-specified-small-compact-basis ğ“Ÿ-DCPO
ğ“Ÿ-has-specified-small-compact-basis = (List X , Îº , Îº-is-small-compact-basis)

ğ“Ÿ-structurally-algebraic : structurally-algebraic ğ“Ÿ-DCPO
ğ“Ÿ-structurally-algebraic =
 structurally-algebraic-if-specified-small-compact-basis
  ğ“Ÿ-DCPO ğ“Ÿ-has-specified-small-compact-basis

ğ“Ÿ-is-algebraic-dcpo : is-algebraic-dcpo ğ“Ÿ-DCPO
ğ“Ÿ-is-algebraic-dcpo = âˆ£ ğ“Ÿ-structurally-algebraic âˆ£

\end{code}
