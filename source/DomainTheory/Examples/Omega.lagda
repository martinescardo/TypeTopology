Tom de Jong, 28 February 2022

We show that the type Î© ğ“¤ of propositions in a universe ğ“¤ form an algebraic
pointed ğ“¤-dcpo.

In fact, we show that the Booleans give a small compact basis for Î© ğ“¤ and
characterize the compact elements of Î© ğ“¤ as the decidable propositions.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons

module DomainTheory.Examples.Omega
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (ğ“¤ : Universe)
       where

open PropositionalTruncation pt

open import NotionsOfDecidability.Decidable

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.ImageAndSurjection pt
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier hiding (âŠ¥)
open import UF.SubtypeClassifier-Properties
open import UF.Sets
open import Posets.Poset fe

open import DomainTheory.Basics.Dcpo pt fe ğ“¤
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import DomainTheory.Basics.Pointed pt fe ğ“¤
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤

_âŠ‘_ : Î© ğ“¤ â†’ Î© ğ“¤ â†’ ğ“¤ Ì‡
P âŠ‘ Q = P holds â†’ Q holds

âŠ‘-is-reflexive : (P : Î© ğ“¤) â†’ P âŠ‘ P
âŠ‘-is-reflexive _ = id

âŠ‘-is-prop-valued : (P Q : Î© ğ“¤) â†’ is-prop (P âŠ‘ Q)
âŠ‘-is-prop-valued P Q = Î -is-prop fe (Î» _ â†’ holds-is-prop Q)

âŠ‘-is-transitive : (P Q R : Î© ğ“¤) â†’ P âŠ‘ Q â†’ Q âŠ‘ R â†’ P âŠ‘ R
âŠ‘-is-transitive P Q R f g p = g (f p)

âŠ‘-is-antisymmetric : (P Q : Î© ğ“¤) â†’ P âŠ‘ Q â†’ Q âŠ‘ P â†’ P ï¼ Q
âŠ‘-is-antisymmetric P Q f g =
 to-subtype-ï¼ (Î» _ â†’ being-prop-is-prop fe)
              (pe (holds-is-prop P) (holds-is-prop Q) f g)

Î©-DCPO : DCPO {ğ“¤ âº} {ğ“¤}
Î©-DCPO = (Î© ğ“¤ , _âŠ‘_
       , (Î©-is-set fe pe
       , âŠ‘-is-prop-valued
       , âŠ‘-is-reflexive
       , âŠ‘-is-transitive
       , âŠ‘-is-antisymmetric)
       , Î³)
 where
  Î³ : is-directed-complete _âŠ‘_
  Î³ I Î± Î´ = (sup , ub , lb-of-ubs)
   where
    sup : Î© ğ“¤
    sup = ((âˆƒ i ê‰ I , Î± i holds) , âˆƒ-is-prop)
    ub : is-upperbound _âŠ‘_ sup Î±
    ub i p = âˆ£ i , p âˆ£
    lb-of-ubs : is-lowerbound-of-upperbounds _âŠ‘_ sup Î±
    lb-of-ubs Q Q-is-ub = âˆ¥âˆ¥-rec (holds-is-prop Q) h
     where
      h : (Î£ i ê‰ I , Î± i holds) â†’ Q holds
      h (i , p) = Q-is-ub i p

Î©-DCPOâŠ¥ : DCPOâŠ¥ {ğ“¤ âº} {ğ“¤}
Î©-DCPOâŠ¥ = Î©-DCPO , ((ğŸ˜ , ğŸ˜-is-prop) , (Î» _ â†’ ğŸ˜-elim))

\end{code}

We proceed by showing that the Booleans give a small compact basis for Î© ğ“¤.

\begin{code}

âŠ¤-is-greatest : (P : Î© ğ“¤) â†’ P âŠ‘ âŠ¤
âŠ¤-is-greatest P _ = â‹†

Bool : ğ“¤ Ì‡
Bool = ğŸ™{ğ“¤} + ğŸ™{ğ“¤}

Îº : Bool â†’ Î© ğ“¤
Îº (inl _) = âŠ¥ Î©-DCPOâŠ¥
Îº (inr _) = âŠ¤

Îºâº : (P : Î© ğ“¤) â†’ (Î£ b ê‰ Bool , Îº b âŠ‘ P) â†’ Î© ğ“¤
Îºâº P = Îº âˆ˜ prâ‚

Îºâº-is-directed : (P : Î© ğ“¤) â†’ is-Directed Î©-DCPO (Îºâº P)
Îºâº-is-directed P = inh , semidir
 where
  inh : âˆ¥ domain (Îºâº P) âˆ¥
  inh = âˆ£ inl â‹† , âŠ¥-is-least Î©-DCPOâŠ¥ P âˆ£
  semidir : is-semidirected _âŠ‘_ (Îºâº P)
  semidir (inl â‹† , _) i = âˆ£ i , âŠ¥-is-least Î©-DCPOâŠ¥ (Îºâº P i)
                              , âŠ‘-is-reflexive (Îºâº P i) âˆ£
  semidir (inr â‹† , u) j = âˆ£ (inr â‹† , u) , âŠ‘-is-reflexive âŠ¤
                                        , âŠ¤-is-greatest (Îºâº P j) âˆ£

Îºâº-sup : (P : Î© ğ“¤) â†’ is-sup _âŠ‘_ P (Îºâº P)
Îºâº-sup P = ub , lb-of-ubs
 where
  ub : is-upperbound _âŠ‘_ P (Îºâº P)
  ub (i , u) = u
  lb-of-ubs : is-lowerbound-of-upperbounds _âŠ‘_ P (Îºâº P)
  lb-of-ubs Q Q-is-ub p = Q-is-ub (inr â‹† , (Î» _ â†’ p)) â‹†

âŠ¤-is-compact : is-compact Î©-DCPO âŠ¤
âŠ¤-is-compact I Î± Î´ âŠ¤-below-âˆÎ± = âˆ¥âˆ¥-functor Î³ (âŠ¤-below-âˆÎ± â‹†)
 where
  Î³ : (Î£ i ê‰ I , Î± i holds) â†’ (Î£ i ê‰ I , âŠ¤ âŠ‘ Î± i)
  Î³ (i , p) = (i , (Î» _ â†’ p))

compact-if-in-image-of-Îº : (P : Î© ğ“¤) â†’ P âˆˆimage Îº â†’ is-compact Î©-DCPO P
compact-if-in-image-of-Îº P P-in-image-of-Îº =
 âˆ¥âˆ¥-rec (being-compact-is-prop Î©-DCPO P) Î³ P-in-image-of-Îº
  where
   Î³ : (Î£ b ê‰ Bool , Îº b ï¼ P) â†’ is-compact Î©-DCPO P
   Î³ (inl â‹† , refl) = âŠ¥-is-compact Î©-DCPOâŠ¥
   Î³ (inr â‹† , refl) = âŠ¤-is-compact

in-image-of-Îº-if-compact : (P : Î© ğ“¤) â†’ is-compact Î©-DCPO P â†’ P âˆˆimage Îº
in-image-of-Îº-if-compact P P-cpt = âˆ¥âˆ¥-functor goal claim
 where
  I : ğ“¤ Ì‡
  I = ğŸ™{ğ“¤} + (P holds)
  Î± : I â†’ Î© ğ“¤
  Î± = add-âŠ¥ Î©-DCPOâŠ¥ (Î» _ â†’ âŠ¤)
  Î´ : is-Directed Î©-DCPO Î±
  Î´ = add-âŠ¥-is-directed Î©-DCPOâŠ¥
       (subsingleton-indexed-is-semidirected Î©-DCPO (Î» _ â†’ âŠ¤) (holds-is-prop P))
  P-below-âˆÎ± : P âŠ‘ âˆ Î©-DCPO {I} {Î±} Î´
  P-below-âˆÎ± p = âˆ£ inr p , â‹† âˆ£
  claim : âˆƒ i ê‰ I , P âŠ‘ Î± i
  claim = P-cpt I Î± Î´ P-below-âˆÎ±
  goal : (Î£ i ê‰ I , P âŠ‘ Î± i) â†’ Î£ b ê‰ Bool , Îº b ï¼ P
  goal (inl â‹† , u) = (inl â‹† , âŠ‘-is-antisymmetric (âŠ¥ Î©-DCPOâŠ¥) P
                               (âŠ¥-is-least Î©-DCPOâŠ¥ P) u)
  goal (inr p , u) = (inr â‹† , âŠ‘-is-antisymmetric âŠ¤ P (Î» _ â†’ p) u)

Îº-is-small-compact-basis : is-small-compact-basis Î©-DCPO Îº
Îº-is-small-compact-basis =
 record
  { basis-is-compact = Î» b â†’ compact-if-in-image-of-Îº (Îº b) âˆ£ b , refl âˆ£
  ; âŠ‘á´®-is-small      = Î» P b â†’ (Îº b âŠ‘ P , â‰ƒ-refl (Îº b âŠ‘ P))
  ; â†“á´®-is-directed   = Îºâº-is-directed
  ; â†“á´®-is-sup        = Îºâº-sup
  }

Î©-has-specified-small-compact-basis : has-specified-small-compact-basis Î©-DCPO
Î©-has-specified-small-compact-basis = (Bool , Îº , Îº-is-small-compact-basis)

\end{code}

Hence, Î© ğ“¤ is algebraic.

\begin{code}

Î©-structurally-algebraic : structurally-algebraic Î©-DCPO
Î©-structurally-algebraic =
 structurally-algebraic-if-specified-small-compact-basis Î©-DCPO
  Î©-has-specified-small-compact-basis

Î©-is-algebraic-dcpo : is-algebraic-dcpo Î©-DCPO
Î©-is-algebraic-dcpo = âˆ£ Î©-structurally-algebraic âˆ£

\end{code}

Finally, we show that the compact elements of Î© ğ“¤ are exactly the decidable
propositions.

\begin{code}

compact-iff-decidable : (P : Î© ğ“¤) â†’ is-compact Î©-DCPO P â†” is-decidable (P holds)
compact-iff-decidable P = â¦…â‡’â¦† , â¦…â‡â¦†
 where
  â¦…â‡’â¦† : is-compact Î©-DCPO P â†’ is-decidable (P holds)
  â¦…â‡’â¦† c = âˆ¥âˆ¥-rec (decidability-of-prop-is-prop fe (holds-is-prop P))
                 Î³ (in-image-of-Îº-if-compact P c)
   where
    Î³ : (Î£ b ê‰ Bool , Îº b ï¼ P) â†’ is-decidable (P holds)
    Î³ (inl â‹† , refl) = ğŸ˜-is-decidable
    Î³ (inr â‹† , refl) = ğŸ™-is-decidable
  â¦…â‡â¦† : is-decidable (P holds) â†’ is-compact Î©-DCPO P
  â¦…â‡â¦† (inl p) = transport (is-compact Î©-DCPO) e âŠ¤-is-compact
   where
    e : âŠ¤ ï¼ P
    e = to-subtype-ï¼ (Î» _ â†’ being-prop-is-prop fe)
                     (pe ğŸ™-is-prop (holds-is-prop P)
                         (Î» _ â†’ p) (Î» _ â†’ â‹†))
  â¦…â‡â¦† (inr q) = transport (is-compact Î©-DCPO) e (âŠ¥-is-compact Î©-DCPOâŠ¥)
   where
    e : âŠ¥ Î©-DCPOâŠ¥ ï¼ P
    e = to-subtype-ï¼ (Î» _ â†’ being-prop-is-prop fe)
                     (pe ğŸ˜-is-prop (holds-is-prop P)
                         ğŸ˜-elim (âŒœ one-ğŸ˜-only âŒ âˆ˜ q))

\end{code}
