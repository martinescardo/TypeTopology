Martin Escardo, December 2020

A notion of disconnectedness. This is different from homotopy
disconnectedness in the sense of the HoTT book. It is based on the
topological, rather than homotopical, interpretation of types.

A discussion of such models is in

  M.H. Escardo and Chuangjie Xu. A constructive manifestation of the
  Kleene-Kreisel continuous functionals. Annals of Pure and Applied
  Logic, 2016.

and

  M.H. Escardo and Thomas Streicher. Annals of Pure and Applied Logic,
  2016.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module DisconnectedTypes where

open import SpartanMLTT
open import Two-Properties
open import NaturalNumbers-Properties
open import DiscreteAndSeparated
open import UF-Retracts
open import UF-Equiv

\end{code}

The following notions of disconnectedness are data rather than
property.

\begin{code}

disconnectedâ‚€ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
disconnectedâ‚€ X = retract ğŸš of X

disconnectedâ‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
disconnectedâ‚ X = Î£ p ê‰ (X â†’ ğŸš) , fiber p â‚€ Ã— fiber p â‚

disconnectedâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
disconnectedâ‚‚ {ğ“¤} X = Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚)

disconnectedâ‚ƒ : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
disconnectedâ‚ƒ {ğ“¤} X = Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X)

disconnected-eq : (X : ğ“¤ Ì‡ )
                â†’ (disconnectedâ‚€ X â†’ disconnectedâ‚ X)
                Ã— (disconnectedâ‚ X â†’ disconnectedâ‚‚ X)
                Ã— (disconnectedâ‚‚ X â†’ disconnectedâ‚ƒ X)
                Ã— (disconnectedâ‚ƒ X â†’ disconnectedâ‚€ X)

disconnected-eq {ğ“¤} X = (f , g , h , k)
 where
  f : (Î£ p ê‰ (X â†’ ğŸš) , Î£ s ê‰ (ğŸš â†’ X) , p âˆ˜ s âˆ¼ id)
    â†’ Î£ p ê‰ (X â†’ ğŸš) , (Î£ x ê‰ X , p x â‰¡ â‚€) Ã— (Î£ x ê‰ X , p x â‰¡ â‚)
  f (p , s , e) = p , (s â‚€ , e â‚€) , (s â‚ , e â‚)

  g : (Î£ p ê‰ (X â†’ ğŸš) , (Î£ x ê‰ X , p x â‰¡ â‚€) Ã— (Î£ x ê‰ X , p x â‰¡ â‚))
    â†’ Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚)
  g (p , (xâ‚€ , eâ‚€) , (xâ‚ , eâ‚)) = (Î£ x ê‰ X , p x â‰¡ â‚€) ,
                                  (Î£ x ê‰ X , p x â‰¡ â‚) ,
                                  (xâ‚€ , eâ‚€) ,
                                  (xâ‚ , eâ‚) ,
                                  qinveq Ï• (Î³ , Î³Ï• , Ï•Î³)
   where
    Ï• : X â†’ (Î£ x ê‰ X , p x â‰¡ â‚€) + (Î£ x ê‰ X , p x â‰¡ â‚)
    Ï• x = ğŸš-equality-cases
           (Î» (râ‚€ : p x â‰¡ â‚€) â†’ inl (x , râ‚€))
           (Î» (râ‚ : p x â‰¡ â‚) â†’ inr (x , râ‚))

    Î³ : (Î£ x ê‰ X , p x â‰¡ â‚€) + (Î£ x ê‰ X , p x â‰¡ â‚) â†’ X
    Î³ (inl (x , râ‚€)) = x
    Î³ (inr (x , râ‚)) = x

    Ï•Î³ : Ï• âˆ˜ Î³ âˆ¼ id
    Ï•Î³ (inl (x , râ‚€)) = ğŸš-equality-casesâ‚€ râ‚€
    Ï•Î³ (inr (x , râ‚)) = ğŸš-equality-casesâ‚ râ‚

    Î³Ï• : Î³ âˆ˜ Ï• âˆ¼ id
    Î³Ï• x = ğŸš-equality-cases
           (Î» (râ‚€ : p x â‰¡ â‚€) â†’ ap Î³ (ğŸš-equality-casesâ‚€ râ‚€))
           (Î» (râ‚ : p x â‰¡ â‚) â†’ ap Î³ (ğŸš-equality-casesâ‚ râ‚))

  h : (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚))
    â†’ (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X))
  h (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , (Ï• , Î³Ï•) , _)) = (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , Ï• , Î³Ï•))

  k : (Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (retract (Xâ‚€ + Xâ‚) of X))
    â†’ Î£ p ê‰ (X â†’ ğŸš) , Î£ s ê‰ (ğŸš â†’ X) , p âˆ˜ s âˆ¼ id
  k (Xâ‚€ , Xâ‚ , xâ‚€ , xâ‚ , (Î³ , Ï• , Î³Ï•)) = p , s , ps
   where
    p : X â†’ ğŸš
    p x = Cases (Î³ x) (Î» _ â†’ â‚€) (Î» _ â†’ â‚)

    s : ğŸš â†’ X
    s â‚€ = Ï• (inl xâ‚€)
    s â‚ = Ï• (inr xâ‚)

    ps : p âˆ˜ s âˆ¼ id
    ps â‚€ = ap (cases (Î» _ â†’ â‚€) (Î» _ â†’ â‚)) (Î³Ï• (inl xâ‚€))
    ps â‚ = ap (cases (Î» _ â†’ â‚€) (Î» _ â†’ â‚)) (Î³Ï• (inr xâ‚))

\end{code}

The following is our official notion of disconnectedness (logically
equivalent to the other ones):

\begin{code}

disconnected : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
disconnected = disconnectedâ‚€

\end{code}

Some examples:

\begin{code}

ğŸš-disconnected : disconnected ğŸš
ğŸš-disconnected = identity-retraction

â„•-disconnected : disconnected â„•
â„•-disconnected = (r , s , rs)
 where
  r : â„• â†’ ğŸš
  r zero     = â‚€
  r (succ n) = â‚

  s : ğŸš â†’ â„•
  s â‚€ = zero
  s â‚ = succ zero

  rs : (n : ğŸš) â†’ r (s n) â‰¡ n
  rs â‚€ = refl
  rs â‚ = refl

isolated-point-different-from-another-point-gives-disconnected :

    {Y : ğ“¥ Ì‡ }
  â†’ (Î£ yâ‚€ ê‰ Y , Î£ yâ‚ ê‰ Y , (yâ‚€ â‰¢ yâ‚) Ã— is-isolated yâ‚€ )
  â†’ disconnected Y

isolated-point-different-from-another-point-gives-disconnected (yâ‚€ , yâ‚ , ne , i) =
  ğŸš-retract-of-non-trivial-type-with-isolated-point ne i

discrete-type-with-two-different-points-gives-disconnected :

    {Y : ğ“¥ Ì‡ }
  â†’ (Î£ yâ‚€ ê‰ Y , Î£ yâ‚ ê‰ Y , yâ‚€ â‰¢ yâ‚)
  â†’ is-discrete Y
  â†’ disconnected Y

discrete-type-with-two-different-points-gives-disconnected (yâ‚€ , yâ‚ , ne) d =
  isolated-point-different-from-another-point-gives-disconnected (yâ‚€ , yâ‚ , ne , d yâ‚€)

â„•-disconnected' : disconnected â„•
â„•-disconnected' = discrete-type-with-two-different-points-gives-disconnected
                     (0 , 1 , succ-no-fp 0)
                     â„•-is-discrete

disconnected-retract : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                     â†’ retract X of Y
                     â†’ disconnected X
                     â†’ disconnected Y
disconnected-retract = retracts-compose

\end{code}

TODO. Define totally disconnected. Then maybe for compact types the
notions of total disconnectedness and total separatedness agree.

The negation of disconnectedness can be expressed positively in
various equivalent ways.

\begin{code}

open import TotallySeparated
open import UF-Base
open import UF-FunExt
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-Miscelanea

is-connectedâ‚€ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚€ X = (f : X â†’ ğŸš) â†’ wconstant f

is-connectedâ‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚ X = (x y : X) â†’ x â‰¡â‚‚ y

is-connectedâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connectedâ‚‚ X = Â¬ disconnected X


is-connectedâ‚€-gives-is-connectedâ‚ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚€ X â†’ is-connectedâ‚ X
is-connectedâ‚€-gives-is-connectedâ‚ i x y p = i p x y

is-connectedâ‚-gives-is-connectedâ‚€ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚ X â†’ is-connectedâ‚€ X
is-connectedâ‚-gives-is-connectedâ‚€ Ï• f x y = Ï• x y f

is-connectedâ‚€-gives-is-connectedâ‚‚ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚€ X â†’ is-connectedâ‚‚ X
is-connectedâ‚€-gives-is-connectedâ‚‚ c (r , s , rs) = n (c r)
 where
  n : Â¬ wconstant r
  n Îº = zero-is-not-one (â‚€       â‰¡âŸ¨ (rs â‚€)â»Â¹ âŸ©
                         r (s â‚€) â‰¡âŸ¨ Îº (s â‚€) (s â‚) âŸ©
                         r (s â‚) â‰¡âŸ¨ rs â‚ âŸ©
                         â‚       âˆ)

disconnected-types-are-not-connected : {X : ğ“¤ Ì‡ } â†’ disconnected X â†’ Â¬ is-connectedâ‚€ X
disconnected-types-are-not-connected c d = is-connectedâ‚€-gives-is-connectedâ‚‚ d c

is-connectedâ‚‚-gives-is-connectedâ‚€ : {X : ğ“¤ Ì‡ } â†’ is-connectedâ‚‚ X â†’ is-connectedâ‚€ X
is-connectedâ‚‚-gives-is-connectedâ‚€ {ğ“¤} {X} n f x y = ğŸš-is-Â¬Â¬-separated (f x) (f y) Ï•
 where
  Ï• : Â¬Â¬ (f x â‰¡ f y)
  Ï• u = n (f , s , fs)
   where
    s : ğŸš â†’ X
    s â‚€ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x â‰¡ â‚€) â†’ x)
           (Î» (pâ‚ : f x â‰¡ â‚) â†’ y)
    s â‚ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x â‰¡ â‚€) â†’ y)
           (Î» (pâ‚ : f x â‰¡ â‚) â†’ x)

    a : f x â‰¡ â‚ â†’ f y â‰¡ â‚€
    a p = different-from-â‚-equal-â‚€ (Î» (q : f y â‰¡ â‚) â†’ u (p âˆ™ (q â»Â¹)))

    b : f x â‰¡ â‚€ â†’ f y â‰¡ â‚
    b p = different-from-â‚€-equal-â‚ (Î» (q : f y â‰¡ â‚€) â†’ u (p âˆ™ q â»Â¹))

    fs : f âˆ˜ s âˆ¼ id
    fs â‚€ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x â‰¡ â‚€) â†’ f (s â‚€) â‰¡âŸ¨ ap f (ğŸš-equality-casesâ‚€ pâ‚€) âŸ©
                               f x     â‰¡âŸ¨ pâ‚€ âŸ©
                               â‚€       âˆ)
           (Î» (pâ‚ : f x â‰¡ â‚) â†’ f (s â‚€) â‰¡âŸ¨ ap f (ğŸš-equality-casesâ‚ pâ‚) âŸ©
                               f y     â‰¡âŸ¨ a pâ‚ âŸ©
                               â‚€       âˆ)
    fs â‚ = ğŸš-equality-cases
           (Î» (pâ‚€ : f x â‰¡ â‚€) â†’ f (s â‚) â‰¡âŸ¨ ap f (ğŸš-equality-casesâ‚€ pâ‚€) âŸ©
                               f y     â‰¡âŸ¨ b pâ‚€ âŸ©
                               â‚       âˆ)
           (Î» (pâ‚ : f x â‰¡ â‚) â†’ f (s â‚) â‰¡âŸ¨ ap f (ğŸš-equality-casesâ‚ pâ‚) âŸ©
                               f x     â‰¡âŸ¨ pâ‚ âŸ©
                               â‚       âˆ)

is-connected : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-connected = is-connectedâ‚€

being-connected-is-prop : {X : ğ“¤ Ì‡ } â†’ Fun-Ext â†’ is-prop (is-connected X)
being-connected-is-prop fe = Î â‚ƒ-is-prop fe (Î» f x y â†’ ğŸš-is-set)

\end{code}

TODO. Is it possible to define sensible analogues for types of total
disconnectedness and zero-dimensionality for topological spaces?
