Martin Escardo, 28th September 2018, 11th October 2018.

This file has two parts.

Part 1 (11th October 2018) is based on a well-known argument,

 Thierry Coquand, The paradox of trees in type theory
 BIT Numerical Mathematics, March 1992, Volume 32, Issue 1, pp 10â€“14
 https://pdfs.semanticscholar.org/f2f3/30b27f1d7ca99c2550f96581a4400c209ef8.pdf

(see also http://www.cs.nott.ac.uk/~psztxa/g53cfr/l20.html/l20.html),
but phrased in terms of LFPT. See also the module See the module
LawvereFPT for a formulation and proof that doesn't assume
type-in-type.

Part 2 (28th September 2018) is based on a recent argument by Ingo
Blechschmidt. See also the module LawvereFPT.

\begin{code}

{-# OPTIONS --without-K --type-in-type #-}

module Unsafe.Type-in-Type-False where

module coquand where

 open import MLTT.Spartan
 open import Various.LawvereFPT

 Y : {X : Set} â†’ (X â†’ X) â†’ X
 Y {X} f = prâ‚ (Î³ f)
  where
   data ğ• : Set where
    sup : (T : Set) â†’ (T â†’ ğ•) â†’ ğ•
   e : ğ• â†’ ğ• â†’ Set
   e (sup T Ï†) w = Î£ t ê‰ T , Ï† t ï¼ w

   R : ğ•
   R = sup (Î£ w ê‰ ğ• , (e w w â†’ X)) prâ‚

   A : Set
   A = e R R

   r : A â†’ (A â†’ X)
   r ((.R , f) , refl) = f

   s : (A â†’ X) â†’ A
   s f = (R , f) , refl

   rs : (f : A â†’ X) â†’ r (s f) ï¼ f
   rs f = refl

   Î³ : (f : X â†’ X) â†’ Î£ x ê‰ X , x ï¼ f x
   Î³ = retract-version.LFPT (r , s , rs)

\end{code}

Then Y is a definitional fixed-point combinator (because the function
s is a definitional section of the function r):

\begin{code}

 Y-is-fp-combinator : {X : Set} (f : X â†’ X) â†’ f (Y f) ï¼ Y f
 Y-is-fp-combinator f = refl

 Contradiction : ğŸ˜
 Contradiction = Y id

\end{code}

Part 2. As mentioned above, this is an application of work of Ingo
Blechschmidt (see the module LavwereFPT) to show that type-in-type,
Streicher's K-axiom, functional and propositional extensionality are
together impossible.

A universe closed under ğŸ˜, Î , Î£ and identity type is enough to get
a contradiction.

In particular, W-types are not needed.

NB. We use the option without-K but then postulate K, so that the uses
of K can be seen explicitly.

postulate K-axiom : (X : Set) â†’ is-set X
postulate funext  : {X : Set} {A : X â†’ Set} {f g : Î  A} â†’ f âˆ¼ g â†’ f ï¼ g
postulate propext : {P Q : Set} â†’ is-prop P â†’ is-prop Q â†’ (P â†’ Q) â†’ (Q â†’ P) â†’ P ï¼ Q

NB. The universe of types is called Set in Agda. This terminology is
consistent with the K axiom.

We don't use any libraries, not even our own libraries, in order to
easily check which closure properties of the universe are needed.
This Agda file is self-contained.

\begin{code}

module blechschmidt where

\end{code}

 We first define ğŸ˜, Î£ and the identity type (written _ï¼_), and name
 the predefined construction Î :

 \begin{code}

 data ğŸ˜ : Set where

 ğŸ˜-elim : {A : Set} â†’ ğŸ˜ â†’ A
 ğŸ˜-elim ()

 Î  : {X : Set} (Y : X â†’ Set) â†’ Set
 Î  Y = (x : _) â†’ Y x

 record Î£ {X : Set} (Y : X â†’ Set) : Set where
   constructor _,_
   field
    prâ‚ : X
    prâ‚‚ : Y prâ‚

 open Î£ public

 Sigma : (X : Set) (Y : X â†’ Set) â†’ Set
 Sigma X Y = Î£ Y

 syntax Sigma A (Î» x â†’ b) = Î£ x ê‰ A , b

 infixr -1 Sigma


 _Ã—_ : Set â†’ Set â†’ Set
 X Ã— Y = Î£ x ê‰ X , Y

 data _ï¼_ {X : Set} : X â†’ X â†’ Set where
   refl : {x : X} â†’ x ï¼ x

 \end{code}

 Function identity and composition:

 \begin{code}

 id : {X : Set} â†’ X â†’ X
 id x = x

 _âˆ˜_ : {X Y : Set} {Z : Y â†’ Set}
     â†’ ((y : Y) â†’ Z y)
     â†’ (f : X â†’ Y) (x : X) â†’ Z (f x)
 g âˆ˜ f = Î» x â†’ g (f x)

 domain : {X : Set} {Y : Set} â†’ (X â†’ Y) â†’ Set
 domain {X} {Y} f = X

 codomain : {X : Set} {Y : Set} â†’ (X â†’ Y) â†’ Set
 codomain {X} {Y} f = Y

 \end{code}

 Equality basics:

 \begin{code}

 transport : {X : Set} (A : X â†’ Set) {x y : X}
           â†’ x ï¼ y â†’ A x â†’ A y
 transport A refl = id

 ap : {X Y : Set} (f : X â†’ Y) {x x' : X} â†’ x ï¼ x' â†’ f x ï¼ f x'
 ap f p = transport (Î» - â†’ f _ ï¼ f -) p refl

 _â»Â¹ : {X : Set} â†’ {x y : X} â†’ x ï¼ y â†’ y ï¼ x
 p â»Â¹ = transport (Î» - â†’ - ï¼ _) p refl

 _âˆ™_ : {X : Set} {x y z : X} â†’ x ï¼ y â†’ y ï¼ z â†’ x ï¼ z
 p âˆ™ q = transport (Î» x â†’ _ ï¼ x) q p

 to-Î£-ï¼ : {X : Set} {A : X â†’ Set} {Ïƒ Ï„ : Î£ A}
        â†’ (Î£ p ê‰ prâ‚ Ïƒ ï¼ prâ‚ Ï„ , transport A p (prâ‚‚ Ïƒ) ï¼ prâ‚‚ Ï„)
        â†’ Ïƒ ï¼ Ï„
 to-Î£-ï¼ (refl , refl) = refl

 _âˆ¼_ : {X : Set} {A : X â†’ Set} â†’ Î  A â†’ Î  A â†’ Set
 f âˆ¼ g = (x : _) â†’ f x ï¼ g x

 \end{code}

 Function extensionality axiom:

 \begin{code}

 postulate funext : {X : Set} {A : X â†’ Set} {f g : Î  A} â†’ f âˆ¼ g â†’ f ï¼ g

 \end{code}

 Propositions and sets and the K axiom:

 \begin{code}

 is-prop : Set â†’ Set
 is-prop X = (x y : X) â†’ x ï¼ y

 is-set : Set â†’ Set
 is-set X = {x y : X} â†’ is-prop (x ï¼ y)

 postulate K-axiom : (X : Set) â†’ is-set X

 \end{code}

 Because we are using type-in-type:

 \begin{code}

 Set-is-set : is-set Set
 Set-is-set = K-axiom Set

 being-prop-is-prop : {X : Set} â†’ is-prop (is-prop X)
 being-prop-is-prop {X} f g = funext (Î» x â†’ funext (c x))
  where
   c : (x y : X) â†’ f x y ï¼ g x y
   c x y = K-axiom X (f x y) (g x y)

 Î -is-prop : {X : Set} {A : X â†’ Set}
           â†’ ((x : X) â†’ is-prop (A x)) â†’ is-prop (Î  A)
 Î -is-prop i f g = funext (Î» x â†’ i x (f x) (g x))

 \end{code}

 Propositional extensionality axiom:

 \begin{code}

 postulate propext : {P Q : Set} â†’ is-prop P â†’ is-prop Q â†’ (P â†’ Q) â†’ (Q â†’ P) â†’ P ï¼ Q

 \end{code}

 Because we have type-in-type and function extensionality, we can
 define propositional truncations (following Voevodsky):

 \begin{code}

 âˆ¥_âˆ¥ : Set â†’ Set
 âˆ¥ X âˆ¥ = (P : Set) â†’ is-prop P â†’ (X â†’ P) â†’ P

 âˆ¥âˆ¥-is-prop : {X : Set} â†’ is-prop âˆ¥ X âˆ¥
 âˆ¥âˆ¥-is-prop {X} = Î -is-prop (Î» P â†’ Î -is-prop (Î» i â†’ Î -is-prop (Î» u â†’ i)))

 âˆ£_âˆ£ : {X : Set} â†’ X â†’ âˆ¥ X âˆ¥
 âˆ£ x âˆ£ = Î» P _ u â†’ u x

 âˆ¥âˆ¥-rec : {X P : Set} â†’ is-prop P â†’ (X â†’ P) â†’ âˆ¥ X âˆ¥ â†’ P
 âˆ¥âˆ¥-rec {X} {P} isp Ï† s = s P isp Ï†

 Î© : Set
 Î© = Î£ P ê‰ Set , is-prop P

 _holds : Î© â†’ Set
 _holds = prâ‚

 holds-is-prop : (p : Î©) â†’ is-prop (p holds)
 holds-is-prop = prâ‚‚

 ğŸ˜-is-prop : is-prop ğŸ˜
 ğŸ˜-is-prop x y = ğŸ˜-elim x

 Â¬_ : Set â†’ Set
 Â¬ X = X â†’ ğŸ˜

 not : Î© â†’ Î©
 not (P , i) = (Â¬ P , Î -is-prop (Î» x â†’ ğŸ˜-is-prop))

 \end{code}

 Retracts and equivalences basics:

 \begin{code}

 has-section : {X Y : Set} â†’ (X â†’ Y) â†’ Set
 has-section r = Î£ s ê‰ (codomain r â†’ domain r) , r âˆ˜ s âˆ¼ id

 retract_of_ : Set â†’ Set â†’ Set
 retract Y of X = Î£ r ê‰ (X â†’ Y) , has-section r

 retracts-compose : {X Y Z : Set} â†’ retract Y of X â†’ retract Z of Y â†’ retract Z of X
 retracts-compose (r , (s , Î·)) (r' , (s' , Î·')) = r' âˆ˜ r ,
                                                   (s âˆ˜ s' ,
                                                   Î» z â†’ ap r' (Î· (s' z)) âˆ™ Î·' z)

 Id-retract : {X Y : Set} â†’ X ï¼ Y â†’ retract Y of X
 Id-retract refl = id , id , (Î» y â†’ refl)

 is-section : {X Y : Set} â†’ (X â†’ Y) â†’ Set
 is-section s = Î£ r ê‰ (codomain s â†’ domain s), r âˆ˜ s âˆ¼ id

 is-equiv : {X Y : Set} â†’ (X â†’ Y) â†’ Set
 is-equiv f = has-section f Ã— is-section f

 _â‰ƒ_ : Set â†’ Set â†’ Set
 X â‰ƒ Y = Î£ f ê‰ (X â†’ Y) , is-equiv f

 idtoeq : (X Y : Set) â†’ X ï¼ Y â†’ X â‰ƒ Y
 idtoeq X Y refl = id , (id , (Î» (x : X) â†’ refl)) , id , (Î» (y : Y) â†’ refl)

 equiv-retract : {X Y : Set} â†’ X â‰ƒ Y â†’ retract Y of X
 equiv-retract (f , (g , fg) , (h , hf)) = f , g , fg

 \end{code}

 Having defined our basic type theory, postulated our axioms, and
 developed some minimal machinery, we are now ready to embark into our
 proof of false.

 Our main tool is Lawvere's fixed point theorem (formulated and proved
 for retractions rather than surjections, for simplicity, as this is
 enough for us):

 \begin{code}

 LFPT : {A : Set} {X : Set}
      â†’ retract (A â†’ X) of A
      â†’ (f : X â†’ X) â†’ Î£ x ê‰ X , x ï¼ f x
 LFPT {A} {X} (r , (s , Î·)) f = x , p
  where
   g : A â†’ X
   g a = f (r a a)

   x : X
   x = r (s g) (s g)

   p : x ï¼ f x
   p = ap (Î» - â†’ - (s g)) (Î· g)

 LFPT-ï¼ : {A : Set} {X : Set}
        â†’ A ï¼ (A â†’ X)
        â†’ (f : X â†’ X) â†’ Î£ x ê‰ X , x ï¼ f x
 LFPT-ï¼ p = LFPT (Id-retract p)

 \end{code}

 A simple application is to show that negation doesn't have fixed
 points:

 \begin{code}

 not-no-fp : Â¬ (Î£ P ê‰ Î© , P ï¼ not P)
 not-no-fp (P , p) = prâ‚ (Î³ id)
  where
   q : P holds ï¼ Â¬ (P holds)
   q = ap _holds p

   Î³ : (f : ğŸ˜ â†’ ğŸ˜) â†’ Î£ x ê‰ ğŸ˜ , x ï¼ f x
   Î³ = LFPT-ï¼ q

 \end{code}

 It is here that we need proposition extensionality in a crucial way:

 \begin{code}

 Î -projection-has-section :
    {A : Set} {X : A â†’ Set}
  â†’ (Aâ‚€ : A) â†’ has-section (Î» (f : (A : A) â†’ X A â†’ Î©) â†’ f Aâ‚€)
 Î -projection-has-section {A} {X} Aâ‚€ = s , Î·
  where
   s : (X Aâ‚€ â†’ Î©) â†’ ((A : A) â†’ X A â†’ Î©)
   s Ï† A x = âˆ¥ (Î£ p ê‰ A ï¼ Aâ‚€ , Ï† (transport X p x) holds)âˆ¥ , âˆ¥âˆ¥-is-prop

   Î· : (Ï† : X Aâ‚€ â†’ Î©) â†’ s Ï† Aâ‚€ ï¼ Ï†
   Î· Ï† = funext Î³
    where
     a : (xâ‚€ : X Aâ‚€) â†’ âˆ¥(Î£ p ê‰ Aâ‚€ ï¼ Aâ‚€ , Ï† (transport X p xâ‚€) holds)âˆ¥ â†’ Ï† xâ‚€ holds
     a xâ‚€ = âˆ¥âˆ¥-rec (holds-is-prop (Ï† xâ‚€)) f
      where
       f : (Î£ p ê‰ Aâ‚€ ï¼ Aâ‚€ , Ï† (transport X p xâ‚€) holds) â†’ Ï† xâ‚€ holds
       f (p , h) = transport _holds t h
        where
         r : p ï¼ refl
         r = K-axiom A p refl

         t : Ï† (transport X p xâ‚€) ï¼ Ï† xâ‚€
         t = ap (Î» - â†’ Ï† (transport X - xâ‚€)) r

     b : (xâ‚€ : X Aâ‚€) â†’ Ï† xâ‚€ holds â†’ âˆ¥(Î£ p ê‰ Aâ‚€ ï¼ Aâ‚€ , Ï† (transport X p xâ‚€) holds)âˆ¥
     b xâ‚€ h = âˆ£ refl , h âˆ£

     Î³ : (xâ‚€ : X Aâ‚€) â†’ (âˆ¥(Î£ p ê‰ Aâ‚€ ï¼ Aâ‚€ , Ï† (transport X p xâ‚€) holds)âˆ¥ , âˆ¥âˆ¥-is-prop) ï¼ Ï† xâ‚€
     Î³ xâ‚€ = to-Î£-ï¼ (propext âˆ¥âˆ¥-is-prop (holds-is-prop (Ï† xâ‚€)) (a xâ‚€) (b xâ‚€) ,
                    being-prop-is-prop (holds-is-prop _) (holds-is-prop (Ï† xâ‚€)) )

 \end{code}

 And here is the crucial use of LFPT:

 \begin{code}

 usr-lemma : {A : Set} (X : A â†’ Set)
           â†’ (aâ‚€ : A)
           â†’ retract ((a : A) â†’ X a â†’ Î©) of X aâ‚€
           â†’ (f : Î© â†’ Î©) â†’ Î£ P ê‰ Î© , P ï¼ f P
 usr-lemma {A} X aâ‚€ retr = LFPT retr'
  where
   retr' : retract (X aâ‚€ â†’ Î©) of X aâ‚€
   retr' = retracts-compose
            retr
            ((Î» f â†’ f aâ‚€) , Î -projection-has-section aâ‚€)

 \end{code}

 Using this, we see that for every family X : A â†’ Set we can construct
 a type not in the image of X:

 \begin{code}

 universe-regular-â‰ƒ : (A : Set) (X : A â†’ Set) â†’ Î£ B ê‰ Set , ((a : A) â†’ Â¬ (X a â‰ƒ B))
 universe-regular-â‰ƒ A X = B , Ï†
   where
    B : Set
    B = (a : A) â†’ X a â†’ Î©

    Ï† : (a : A) â†’ Â¬ (X a â‰ƒ B)
    Ï† a p = not-no-fp (Î³ not)
     where
      retr : retract B of (X a)
      retr = equiv-retract p

      Î³ : (f : Î© â†’ Î©) â†’ Î£ P ê‰ Î© , P ï¼ f P
      Î³ = usr-lemma {A} X a retr

 universe-regular : (A : Set) (X : A â†’ Set) â†’ Î£ B ê‰ Set , ((a : A) â†’ Â¬ (X a ï¼ B))
 universe-regular A X = Î³ (universe-regular-â‰ƒ A X)
  where
   Î³ : (Î£ B ê‰ Set , ((a : A) â†’ Â¬ (X a â‰ƒ B)))
     â†’ (Î£ B ê‰ Set , ((a : A) â†’ Â¬ (X a ï¼ B)))
   Î³ (B , Ï†) = B , (Î» a p â†’ Ï† a (idtoeq (X a) B p))

 \end{code}

 And in particular we have that

 \begin{code}

 families-do-not-have-sections : (A : Set) (X : A â†’ Set) â†’ Â¬ has-section X
 families-do-not-have-sections A X (s , Î·) = Ï† (s B) (Î· B)
  where
   B : Set
   B = prâ‚ (universe-regular A X)

   Ï† : âˆ€ a â†’ Â¬ (X a ï¼ B)
   Ï† = prâ‚‚ (universe-regular A X)

 \end{code}

 We now consider A = Set and X = id to get the desired contradiction,
 as the identity function obviously does have a section, namely itself:

 \begin{code}

 contradiction : ğŸ˜
 contradiction = families-do-not-have-sections Set id (id , (Î» X â†’ refl))

 \end{code}

 Question: Without assuming type-in-type, can we instead derive a
 contradiction from the existence of a sufficiently large universe U
 with a type X: U such that Xâ‰ƒU?

 Fixities and precedences:

 \begin{code}

 infixl 5 _âˆ˜_
 infixr 4 _,_
 infixr 2 _Ã—_
 infix  0 _ï¼_
 infix  4  _âˆ¼_
 infix  50 Â¬_

 \end{code}
