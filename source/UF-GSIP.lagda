Martin Escardo, August 2018.

A structure identity principle for types, rather than categories as in
the HoTT Book.

This tries to make previous work by Coquand and Danielsson [1] more
general.

[1] https://www.sciencedirect.com/science/article/pii/S0019357713000694 , 2013

The abstract development is followed by some concrete examples.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-Base
open import UF-Equiv
open import UF-Univalence
open import UF-Yoneda

module UF-GSIP where

\end{code}

We consider the type ğ•Š of types X : U Ì‡ equipped with structure m : S X,
where the universe U is univalent and S : U Ì‡ â†’ V Ì‡ is a parameter:

\begin{code}

module gsipâ‚€
        (U V : Universe)
        (ua : is-univalent U)
        (S : U Ì‡ â†’ V Ì‡)
       where

 ğ•Š : U â€² âŠ” V Ì‡
 ğ•Š = Î£ \(X : U Ì‡) â†’ S X

\end{code}

The underlying set and structure are given by the first and second
projections:

\begin{code}

 âŸ¨_âŸ© : ğ•Š â†’ U Ì‡
 âŸ¨ X , s âŸ© = X

 structure : (A : ğ•Š) â†’ S âŸ¨ A âŸ©
 structure (X , s) = s

\end{code}

 If S comes with suitable data, including S-equiv discussed below, we
 can characterize equality in ğ•Š as equivalence of underlying sets
 subject to a suitable condition involving the data:

   (A â‰¡ B) â‰ƒ Î£ \(f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) â†’ Î£ \(e : is-equiv f) â†’ S-equiv A B f e

 It is important that such a condition is not necessarily property but
 actually data in general.

 Thus

  (1) For an equivalence f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ© we want data that
      establishes that it is an equivalence in the sense of
      S-structure, in some abstract sense, specified by S-equiv.

 One possible list of data for S and S-equiv is the following:

  (2) When f is the identity equivalence, we want the data S-equiv to
      be given, and we name it S-refl.

  (3) Moreover, when f : âŸ¨ X , s âŸ© â†’ âŸ¨ X , t âŸ© is the identity
      function, we want the data for (1) to give data for the equality
      s â‰¡ t of structures. This is specified by the function
      â‰¡-S-structure.

  (4) We need a technical transport condition (which is not
      surprising, as equality of Î£-types is given by transport of the
      second component), specified by the function S-transport below,
      relating the data specified by the functions â‰¡-S-structure and
      S-refl.

These assumptions (1)-(4) are given as module parameters for gsipâ‚:

\begin{code}

 module gsipâ‚
         (S-equiv : (A B : ğ•Š) â†’ (f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) â†’ is-equiv f â†’ U âŠ” V Ì‡)
         (S-refl : (A : ğ•Š) â†’ S-equiv A A id (id-is-equiv âŸ¨ A âŸ©))
         (â‰¡-S-structure : (X : U Ì‡) (s t : S X) â†’ S-equiv (X , s) (X , t) id (id-is-equiv X) â†’ s â‰¡ t)
         (S-transport : (A : ğ•Š) (s : S âŸ¨ A âŸ©) (Ï… : S-equiv (âŸ¨ A âŸ© , structure A) (âŸ¨ A âŸ© , s) id (id-is-equiv âŸ¨ A âŸ©))
                      â†’ transport (Î» - â†’ S-equiv A (âŸ¨ A âŸ© , -) id (id-is-equiv âŸ¨ âŸ¨ A âŸ© , - âŸ©))
                               (â‰¡-S-structure âŸ¨ A âŸ© (structure A) s Ï…)
                               (S-refl A)
                      â‰¡ Ï…)
        where

\end{code}

 Under these assumptions, we show that equality in ğ•Š is equivalent
 to _â‰ƒâ‚›_ defined as follows:

\begin{code}

  _â‰ƒâ‚›_ : ğ•Š â†’ ğ•Š â†’ U âŠ” V Ì‡
  A â‰ƒâ‚› B = Î£ \(f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) â†’ Î£ \(e : is-equiv f) â†’ S-equiv A B f e

\end{code}

  This defines an ğ•Š-equivalence to be an equivalence of underlying
  sets that is an S-structure equivalence in the sense abstractly
  specified by the function S-equiv. Then the assumption S-refl allows
  us to have an equivalence of any element of ğ•Š with itself:

\begin{code}

  â‰ƒâ‚›-refl : (A : ğ•Š) â†’ A â‰ƒâ‚› A
  â‰ƒâ‚›-refl A = id , id-is-equiv âŸ¨ A âŸ© , S-refl A

\end{code}

  And hence an equality gives an ğ•Š-equivalence by induction in the
  usual way:

\begin{code}

  idtoeqâ‚› : (A B : ğ•Š) â†’ A â‰¡ B â†’ A â‰ƒâ‚› B
  idtoeqâ‚› A .A refl = â‰ƒâ‚›-refl A

\end{code}

  We use the following auxiliary constructions to define an inverse of
  idtoeqâ‚› by equivalence induction (the function JEq):

\begin{code}

  private
    Î¨ : (A : ğ•Š) (Y : U Ì‡) â†’ âŸ¨ A âŸ© â‰ƒ Y â†’ U â€² âŠ” V Ì‡
    Î¨ A Y (f , e) = (s : S Y) â†’ S-equiv A (Y , s) f e â†’ A â‰¡ (Y , s)
    Ïˆ : (A : ğ•Š) â†’ Î¨ A âŸ¨ A âŸ© (â‰ƒ-refl âŸ¨ A âŸ©)
    Ïˆ A s Ï… = to-Î£-â‰¡' (â‰¡-S-structure âŸ¨ A âŸ© (structure A) s Ï…)

  eqtoidâ‚› : (A B : ğ•Š) â†’ A â‰ƒâ‚› B â†’ A â‰¡ B
  eqtoidâ‚› A B (f , e , Ï…) = JEq ua âŸ¨ A âŸ© (Î¨ A) (Ïˆ A) âŸ¨ B âŸ© (f , e) (structure B) Ï…

\end{code}

  So far we have used the hypotheses

     * S-equiv (to define _â‰¡â‚›_),
     * S-refl (to define idtoeqâ‚›), and
     * â‰¡-S-structure (to define eqtoidâ‚›).

  Next we use the remaining hypothesis S-transport to show that
  eqtoidâ‚› is a left-inverse of idtoeqâ‚›:

\begin{code}

  idtoeq-eqtoidâ‚› : (A B : ğ•Š) (Îµ : A â‰ƒâ‚› B) â†’ idtoeqâ‚› A B (eqtoidâ‚› A B Îµ) â‰¡ Îµ
  idtoeq-eqtoidâ‚› A B (f , e , Ï…) = JEq ua âŸ¨ A âŸ© Î¦ Ï† âŸ¨ B âŸ© (f , e) (structure B) Ï…
   where
    Î¦ : (Y : U Ì‡) â†’ âŸ¨ A âŸ© â‰ƒ Y â†’ U âŠ” V Ì‡
    Î¦ Y (f , e) = (m : S Y)
                  (Ï… : S-equiv A (Y , m) f e)
                â†’ idtoeqâ‚› A (Y , m) (eqtoidâ‚› A (Y , m) (f , e , Ï…)) â‰¡ f , e , Ï…
    Ï† : Î¦ âŸ¨ A âŸ© (â‰ƒ-refl âŸ¨ A âŸ©)
    Ï† s Ï… = z
     where
      A' : ğ•Š
      A' = âŸ¨ A âŸ© , s
      observationâ‚€ : A â‰¡ A'
      observationâ‚€ = JEq ua âŸ¨ A âŸ© (Î¨ A) (Ïˆ A) âŸ¨ A âŸ© (â‰ƒ-refl âŸ¨ A âŸ©) s Ï…
      observationâ‚ : S-equiv A A' id (id-is-equiv âŸ¨ A âŸ©)
      observationâ‚ = Ï…
      refl' : A â‰ƒâ‚› A'
      refl' = id , id-is-equiv âŸ¨ A âŸ© , Ï…
      observationâ‚‚ : eqtoidâ‚› A A' refl' â‰¡ JEq ua âŸ¨ A âŸ© (Î¨ A) (Ïˆ A) âŸ¨ A âŸ© (â‰ƒ-refl âŸ¨ A âŸ©) s Ï…
      observationâ‚‚ = refl
      p : structure A â‰¡ s
      p = â‰¡-S-structure âŸ¨ A âŸ© (structure A) s Ï…
      q : JEq ua âŸ¨ A âŸ© (Î¨ A) (Ïˆ A) âŸ¨ A âŸ© (â‰ƒ-refl âŸ¨ A âŸ©) s Ï… â‰¡ to-Î£-â‰¡' p
      q = ap (Î» h â†’ h s Ï…) (JEq-comp ua âŸ¨ A âŸ© (Î¨ A) (Ïˆ A))
      r : idtoeqâ‚› A A' (eqtoidâ‚› A A' refl') â‰¡ idtoeqâ‚› A A' (to-Î£-â‰¡' p)
      r = ap (idtoeqâ‚› A A') q
      u : structure A â‰¡ s â†’ S-equiv A A' id (id-is-equiv âŸ¨ A âŸ©)
      u p = transport (Î» - â†’ S-equiv A (âŸ¨ A âŸ© , -) id (id-is-equiv âŸ¨ âŸ¨ A âŸ© , - âŸ©)) p (S-refl A)
      v : u p â‰¡ Ï…
      v = S-transport A s Ï…
      w : id , id-is-equiv âŸ¨ A âŸ© , u p â‰¡ refl'
      w = to-Î£-â‰¡' (to-Î£-â‰¡' v)
      x : (p : structure A â‰¡ s) â†’ idtoeqâ‚› A A' (to-Î£-â‰¡' p) â‰¡ id , id-is-equiv âŸ¨ A âŸ© , u p
      x refl = refl
      y : idtoeqâ‚› A A' (to-Î£-â‰¡' p) â‰¡ refl'
      y = x p âˆ™ w
      z : idtoeqâ‚› A A' (eqtoidâ‚› A A' refl') â‰¡ refl'
      z = r âˆ™ y

\end{code}

  Being a natural left-inverse of idtoeqâ‚›, the function eqtoidâ‚› is
  also a right-inverse, by a general property of the identity type
  (namely the one called nat-retraction-is-equiv in our development
  (in the module UF-Yoneda):

\begin{code}

  uaâ‚› : (A B : ğ•Š) â†’ is-equiv (idtoeqâ‚› A B)
  uaâ‚› A = nat-retraction-is-equiv A
            (idtoeqâ‚› A)
            (Î» B â†’ eqtoidâ‚› A B , idtoeq-eqtoidâ‚› A B)

  eqtoid-idtoeqâ‚› : (A B : ğ•Š) (p : A â‰¡ B) â†’  eqtoidâ‚› A B (idtoeqâ‚› A B p) â‰¡ p
  eqtoid-idtoeqâ‚› A B = prâ‚(prâ‚‚ (is-equiv-qinv (idtoeqâ‚› A B) (uaâ‚› A B)))

\end{code}

This completes the proof of the abstract SIP considered here.

We now consider some concrete examples to illustrate how this works in
practice.

An âˆ-magma is a type, not assumed to be a set, equipped with a binary
operation. The above gives a characterization of equality of âˆ-magmas:

\begin{code}

module âˆ-magma (U : Universe) (ua : is-univalent U) where

 open gsipâ‚€ U U ua (Î» X â†’ X â†’ X â†’ X)
 open gsipâ‚ (Î» A B f e â†’ (Î» x x' â†’ f (structure A x x')) â‰¡ (Î» x x' â†’ structure B (f x) (f x')))
            (Î» A â†’ refl)
            (Î» X m n â†’ id)
            (Î» A m Ï… â†’ refl-left-neutral)

 fact : (A B : ğ•Š)
      â†’ (A â‰¡ B) â‰ƒ Î£ \(f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) â†’ is-equiv f Ã— ((Î» x x' â†’ f (structure A x x'))
                                                       â‰¡ (Î» x x' â†’ structure B (f x) (f x')))
 fact A B = idtoeqâ‚› A B , uaâ‚› A B

\end{code}

 Perhaps the following reformulation is more appealing:

\begin{code}

 fact' : (X Y : U Ì‡) (m : X â†’ X â†’ X) (n : Y â†’ Y â†’ Y)
       â†’ ((X , m) â‰¡ (Y , n))
         â‰ƒ Î£ \(f : X â†’ Y) â†’ is-equiv f Ã— ((Î» x x' â†’ f (m x x')) â‰¡ (Î» x x' â†’ n (f x) (f x')))
 fact' X Y m n = fact (X , m) (Y , n)

\end{code}

 Of course, the condition (Î» x x' â†’ f (m x x')) â‰¡ (Î» x x' â†’ n (f x) (f x'))
 is equivalent to (x x' : X) â†’ f (m x x') â‰¡ n (f x) (f x') by function
 extensionality, which is the natural formulation of magma
 homomorphism.


As a second example, a topology on a set X is a set of subsets of X
satisfying suitable axioms. A set of subsets amounts to a map
(X â†’ Î©) â†’ Î©. Dropping the assumption that the type X is a set and the
axioms for topologies, and generalizing Î© to an arbitrary type R, we
get âˆ-proto-topological spaces.

\begin{code}

module âˆ-proto-topological-spaces (U V : Universe) (ua : is-univalent U) (R : V Ì‡) where

 open gsipâ‚€ U (U âŠ” V) ua (Î» X â†’ (X â†’ R) â†’ R)
 open gsipâ‚ (Î» A B f e â†’ (Î» V â†’ structure A (V âˆ˜ f)) â‰¡ structure B)
            (Î» A â†’ refl)
            (Î» X Ï„ Ïƒ â†’ id)
            (Î» A Ï„ Ï… â†’ refl-left-neutral)

 fact : (A B : ğ•Š)
      â†’ (A â‰¡ B) â‰ƒ Î£ \(f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) â†’ is-equiv f Ã— ((Î» V â†’ structure A (Î» x â†’ V (f x))) â‰¡ structure B)
 fact A B = idtoeqâ‚› A B , uaâ‚› A B

\end{code}

 Or in perhaps more appealing terms:

\begin{code}

 fact' : (X Y : U Ì‡) (Ï„ : (X â†’ R) â†’ R) (Ïƒ : (Y â†’ R) â†’ R)
       â†’ ((X , Ï„) â‰¡ (Y , Ïƒ)) â‰ƒ Î£ \(f : X â†’ Y) â†’ is-equiv f Ã— ((Î» V â†’ Ï„ (V âˆ˜ f)) â‰¡ Ïƒ)
 fact' X Y Ïƒ Ï„ = fact (X , Ïƒ) (Y , Ï„)

\end{code}

 Again by function extensionality, structure preservation is equivalent
 to (V : Y â†’ R) â†’ Ï„(V âˆ˜ f) â‰¡ Ïƒ V. We can read this, at least when R is
 the type Î© of truth-values, by saying that a set V : Y â†’ R is Ïƒ-open
 precisely when its inverse image V âˆ˜ f is Ï„-open.

 Thus, if we say that an equivalence f : X â†’ Y is an âˆ-homeomorphism
 when an "R-set" V : Y â†’ R is Ïƒ-open precisely when its f-inverse image
 V âˆ˜ f : X â†’ R is Ï„-open, then the above says that two
 âˆ-proto-topological spaces are equal iff they are âˆ-homeomorphic.


Perhaps it is possible to derive the SIP for 1-categories from the
above SIP for types equipped with structure. But this is not the point
we are trying to make. The point is to give a criterion for natural
characterizations of equality of types equipped with structure, before
we know they form (âˆ-)categories, and even if they don't.

Another example that should be accounted for by the methods developed
here is equality of ordinals (in the module OrdinalOfOrdinals), which
is what prompted us to think about the subject of this module.

TODO. Add many more examples, including monoids (sets equipped with an
associative binary operation with a neutral element), topologies (sets
equipped with a set of subsets closed under finite intersections and
arbitrary unions (of families, to avoid having to rely on resizing)),
among other natural ones to prove the usefulness of the above abstract
formulation and proof of equality of types equipped with structure.
