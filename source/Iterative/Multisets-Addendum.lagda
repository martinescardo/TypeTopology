Martin Escardo July 2023.

Some constructions with iterative multisets.

 * The universe is a retract of the type ğ•„ of iterative multisets.
 * ğ•„ is algebraicly injective.


\begin{code}

{-# OPTIONS --safe --without-K --exact-split #-}

open import MLTT.Spartan
open import UF.Univalence

module Iterative.Multisets-Addendum
        (ğ“¤ : Universe)
        (ua : Univalence)
       where

open import Iterative.Multisets ğ“¤
open import Iterative.Sets ğ“¤ ua
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Miscelanea
open import UF.PropIndexedPiSigma
open import UF.Retracts
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Univalence
open import W.Type
open import W.Properties (ğ“¤ Ì‡) id

private
 ğ“¤âº : Universe
 ğ“¤âº = ğ“¤ âº

 fe : Fun-Ext
 fe = Univalence-gives-Fun-Ext ua

 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open import InjectiveTypes.Blackboard fe'

\end{code}

The universe ğ“¤ is embedded as a retract of ğ•„.

\begin{code}

ğŸ˜á´¹ : ğ•„
ğŸ˜á´¹ = ssup ğŸ˜ unique-from-ğŸ˜

ğŸ˜á´¹-is-iset : is-iterative-set ğŸ˜á´¹
ğŸ˜á´¹-is-iset = unique-from-ğŸ˜-is-embedding , (Î» (x : ğŸ˜) â†’ ğŸ˜-elim x)

ğŸ˜á´¹-is-h-isolated : is-h-isolated ğŸ˜á´¹
ğŸ˜á´¹-is-h-isolated {ssup X Ï†} = isets-are-h-isolated ğŸ˜á´¹ ğŸ˜á´¹-is-iset

ğ“¤-to-ğ•„ : ğ“¤ Ì‡ â†’ ğ•„
ğ“¤-to-ğ•„ X = ssup X (Î» x â†’ ğŸ˜á´¹)

ğ“¤-to-ğ•„-is-section : ğ•„-root âˆ˜ ğ“¤-to-ğ•„ âˆ¼ id
ğ“¤-to-ğ•„-is-section X = refl

ğ“¤-is-retract-of-ğ•„ : retract (ğ“¤ Ì‡ ) of ğ•„
ğ“¤-is-retract-of-ğ•„ = ğ•„-root , ğ“¤-to-ğ•„ , ğ“¤-to-ğ•„-is-section

ğ“¤-to-ğ•„-is-embedding : is-embedding ğ“¤-to-ğ•„
ğ“¤-to-ğ•„-is-embedding M@(ssup Y Ï†) = II
 where
  I = fiber ğ“¤-to-ğ•„ M â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
      (Î£ X ê‰ ğ“¤ Ì‡ , ssup X (Î» x â†’ ğŸ˜á´¹) ï¼ (ssup Y Ï†))                     â‰ƒâŸ¨ Iâ‚€ âŸ©
      (Î£ X ê‰ ğ“¤ Ì‡ , Î£ p ê‰ X ï¼ Y , (Î» x â†’ ğŸ˜á´¹) ï¼ Ï† âˆ˜ Idtofun p)          â‰ƒâŸ¨ Iâ‚ âŸ©
      (Î£ (X , p) ê‰ (Î£ X ê‰ ğ“¤ Ì‡ , X ï¼ Y) , (Î» x â†’ ğŸ˜á´¹) ï¼ Ï† âˆ˜ Idtofun p)  â– 
   where
    Iâ‚€ = Î£-cong (Î» X â†’ ğ•„-ï¼)
    Iâ‚ = â‰ƒ-sym Î£-assoc

  II : is-prop (fiber ğ“¤-to-ğ•„ M)
  II = equiv-to-prop I
        (subsets-of-props-are-props _ _
          (singleton-types'-are-props Y)
          (constant-maps-are-h-isolated fe ğŸ˜á´¹ ğŸ˜á´¹-is-h-isolated))

\end{code}

The type of multisets is algebraicly injective.

\begin{code}

Î£á´¹ : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğ•„) â†’ ğ•„
Î£á´¹ {X} A = ssup
            (Î£ x ê‰ X , ğ•„-root (A x))
            (Î» (x , y) â†’ ğ•„-forest (A x) y)

prop-indexed-sumá´¹ : {X : ğ“¤ Ì‡ } {A : X â†’ ğ•„}
                  â†’ is-prop X
                  â†’ (xâ‚€ : X) â†’ Î£á´¹ A ï¼ A xâ‚€
prop-indexed-sumá´¹ {X} {A} i xâ‚€ = V
 where
  ğ•— = (Î£ x ê‰ X , ğ•„-root (A x)) â‰ƒâŸ¨ prop-indexed-sum i xâ‚€ âŸ©
      ğ•„-root (A xâ‚€)            â– 

  remark : âŒœ ğ•— âŒ ï¼ (Î» (x , y) â†’ transport (Î» - â†’ W-root (A -)) (i x xâ‚€) y)
  remark = refl

  I : ((x , y) : Î£ x ê‰ X , ğ•„-root (A x))
      (p : x ï¼ xâ‚€)
    â†’ ğ•„-forest (A x) y ï¼ ğ•„-forest (A xâ‚€) (transport (Î» - â†’ W-root (A -)) p y)
  I _ refl = refl

  II : ((x , y) : Î£ x ê‰ X , ğ•„-root (A x))
     â†’ ğ•„-forest (A x) y ï¼ ğ•„-forest (A xâ‚€) (âŒœ ğ•— âŒ (x , y))
  II (x , y) = I (x , y) (i x xâ‚€)

  III : ((x , y) : Î£ x ê‰ X , ğ•„-root (A x))
     â†’ ğ•„-forest (A x) y â‰ƒá´¹ ğ•„-forest (A xâ‚€) (âŒœ ğ•— âŒ (x , y))
  III Ïƒ = idtoeqá´¹ _ _ (II Ïƒ)

  IV : Î£á´¹ A â‰ƒá´¹ ssup (ğ•„-root (A xâ‚€)) (ğ•„-forest (A xâ‚€))
  IV = ğ•— , III

  V = Î£á´¹ A                                    ï¼âŸ¨ âŒœ ğ•„-=-â‰ƒ ua _ _ âŒâ»Â¹ IV âŸ©
      ssup (ğ•„-root (A xâ‚€)) (ğ•„-forest (A xâ‚€)) ï¼âŸ¨ ğ•„-Î· (A xâ‚€) âŸ©
      A xâ‚€                                    âˆ

ğ•„-is-ainjective : ainjective-type ğ•„ ğ“¤ ğ“¤
ğ•„-is-ainjective {X} {Y} j j-emb f = f\j , f\j-ext
 where
  A : (y : Y) â†’ fiber j y â†’ ğ•„
  A y (x , _) = f x

  f\j : Y â†’ ğ•„
  f\j y = Î£á´¹ (A y)

  f\j-ext : f\j âˆ˜ j âˆ¼ f
  f\j-ext x = prop-indexed-sumá´¹ {fiber j (j x)} {A (j x)} (j-emb (j x)) (x , refl)

\end{code}
