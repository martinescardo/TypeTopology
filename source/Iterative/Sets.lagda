Martin Escardo & Tom de Jong, June 2023.

Iterative sets.

We define the type of iterative sets as a subtype of that of multisets.

  * H. R. Gylterud, "From multisets to sets in homotopy type theory".
    The Journal of Symbolic Logic, vol. 83, no. 3, pp. 1132â€“1146,
    2018. https://doi.org/10.1017/jsl.2017.84

See the module Iterative.index for further bibliographic references.

The previous module Iterative.Multisets doesn't make significant use
of univalence, and so we assumed it only for specific
constructions. But here the use of univalence is more pervasive, and
so we assume it globally.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan
open import UF.Univalence

module Iterative.Sets
        (ua : Univalence)
        (ğ“¤ : Universe)
       where

open import UF.FunExt
open import UF.UA-FunExt

private
 ğ“¤âº : Universe
 ğ“¤âº = ğ“¤ âº

 fe : Fun-Ext
 fe = Univalence-gives-Fun-Ext ua

 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open import Iterative.Multisets ğ“¤
open import Ordinals.Notions
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.PairFun
open import UF.Retracts
open import UF.Sets
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import W.Type
open import W.Properties (ğ“¤ Ì‡ ) id

\end{code}

An iterative set is a multiset whose subforests are all
embeddings. The effect of that is that the membership relation on
iterative sets is proposition-valued, rather than just type-valued, as
is the case for general multisets.

\begin{code}

is-iterative-set : ğ•„ â†’ ğ“¤âº Ì‡
is-iterative-set (ssup X Ï†) = is-embedding Ï†
                            Ã— ((x : X) â†’ is-iterative-set (Ï† x))
\end{code}

It is convenient to name the projections for the sake of clarity:

\begin{code}

ğ•„-forest-is-embedding : (M : ğ•„)
                      â†’ is-iterative-set M
                      â†’ is-embedding (ğ•„-forest M)
ğ•„-forest-is-embedding (ssup _ _) = prâ‚

ğ•„-subtrees-are-iterative : (M : ğ•„)
                         â†’ is-iterative-set M
                         â†’ (x : ğ•„-root M) â†’ is-iterative-set (ğ•„-forest M x)
ğ•„-subtrees-are-iterative (ssup _ _) = prâ‚‚

\end{code}

It is crucial that the notion of iterative set is property rather than
data:

\begin{code}

being-iset-is-prop : (M : ğ•„) â†’ is-prop (is-iterative-set M)
being-iset-is-prop (ssup X Ï†) =
 Ã—-is-prop
  (being-embedding-is-prop fe Ï†)
  (Î -is-prop fe (Î» x â†’ being-iset-is-prop (Ï† x)))

\end{code}

The type of iterative sets as a subtype of that of iterative
multisets:

\begin{code}

ğ• : ğ“¤âº Ì‡
ğ• = Î£ M ê‰ ğ•„ , is-iterative-set M

ğ•-is-locally-small : is-locally-small ğ•
ğ•-is-locally-small = subtype-is-locally-small
                      being-iset-is-prop
                      (ğ•„-is-locally-small ua)
\end{code}

We again name the projections for the sake of clarity:

\begin{code}

underlying-mset : ğ• â†’ ğ•„
underlying-mset = prâ‚

isets-are-iterative : (A : ğ•) â†’ is-iterative-set (underlying-mset A)
isets-are-iterative = prâ‚‚

\end{code}

Because the notion of iterative set is property, we get that ğ• is
indeed a subtype of ğ•„.

\begin{code}

underlying-mset-is-embedding : is-embedding underlying-mset
underlying-mset-is-embedding = prâ‚-is-embedding being-iset-is-prop

\end{code}

We define the root and the forest of an iterative set in terms of
those for multisets, but we need to add a "proof obligation" in the
case of the forest.

\begin{code}

ğ•-root : ğ• â†’ ğ“¤ Ì‡
ğ•-root A = ğ•„-root (underlying-mset A)

ğ•-forest : (A : ğ•) â†’ ğ•-root A â†’ ğ•
ğ•-forest A x = ğ•„-forest (underlying-mset A) x ,
               ğ•„-subtrees-are-iterative
                (underlying-mset A)
                (isets-are-iterative A)
                x

\end{code}

A criterion for equality in ğ•:

\begin{code}

to-ğ•-ï¼ : {X Y : ğ“¤ Ì‡ }
          {Ï† : X â†’ ğ•„}
          {Î³ : Y â†’ ğ•„}
        â†’ (Î£ p ê‰ X ï¼ Y , Ï† ï¼ Î³ âˆ˜ Idtofun p)
        â†’ (i : is-iterative-set (ssup X Ï†))
          (j : is-iterative-set (ssup Y Î³))
        â†’ (ssup X Ï† , i) ï¼[ ğ• ] (ssup Y Î³ , j)
to-ğ•-ï¼ Ïƒ i j = to-subtype-ï¼ being-iset-is-prop (to-ğ•„-ï¼ Ïƒ)

\end{code}

We define membership of iterative sets in terms of that for multisets:

\begin{code}

_âˆˆ_ : ğ• â†’ ğ• â†’ ğ“¤âº Ì‡
A âˆˆ B = underlying-mset A â… underlying-mset B

\end{code}

As is the case for iterative multisets, there is a resized down,
equivalent definition of membership.

\begin{code}

_âˆˆâ»_ : ğ• â†’ ğ• â†’ ğ“¤ Ì‡
A âˆˆâ» B = underlying-mset A â…â» underlying-mset B

âˆˆâ»â‰ƒâˆˆ : (A B : ğ•) â†’ (A âˆˆ B) â‰ƒ (A âˆˆâ» B)
âˆˆâ»â‰ƒâˆˆ A B = â…â»â‰ƒâ… ua (underlying-mset A) (underlying-mset B)

\end{code}

As discussed above, the membership relation becomes a proposition
precisely because we required forests to be embeddings to define the
subtype of iterative sets.

\begin{code}

âˆˆ-is-prop-valued : (A B : ğ•) â†’ is-prop (A âˆˆ B)
âˆˆ-is-prop-valued (M , _) (ssup X Ï† , Ï†-emb , _) = Ï†-emb M

ğ•-forest-âˆˆ : (A : ğ•) (x : ğ•-root A) â†’ ğ•-forest A x âˆˆ A
ğ•-forest-âˆˆ A x = ğ•„-forest-â… (underlying-mset A) x

\end{code}

The subset relation is defined in the usual way and is
proposition-valued:

\begin{code}

_âŠ†_ : ğ• â†’ ğ• â†’ ğ“¤âº Ì‡
A âŠ† B = (C : ğ•) â†’ C âˆˆ A â†’ C âˆˆ B

âŠ†-is-prop-valued : (A B : ğ•) â†’ is-prop (A âŠ† B)
âŠ†-is-prop-valued A B = Î â‚‚-is-prop fe (Î» C _ â†’ âˆˆ-is-prop-valued C B)

\end{code}

It is in the following that the univalence axiom is used for the first
time, to establish the extensionality axiom for iterative sets:

\begin{code}

âˆˆ-is-extensional : (A B : ğ•) â†’ A âŠ† B â†’ B âŠ† A â†’ A ï¼ B
âˆˆ-is-extensional A@(M@(ssup X Ï†) , Ï†-emb , g)
                 B@(N@(ssup Y Î³) , Î³-emb , h) u v = V
 where
  have-uv : (A âŠ† B) Ã— (B âŠ† A)
  have-uv = u , v

  I : (x : X) â†’ Î£ y ê‰ Y , Î³ y ï¼ Ï† x
  I x = u (Ï† x , g x) (ğ•„-forest-â… M x)

  II : (y : Y) â†’ Î£ x ê‰ X , Ï† x ï¼ Î³ y
  II y = v (Î³ y , h y) (ğ•„-forest-â… N y)

  f : X â†’ Y
  f x = prâ‚ (I x)

  fâ»Â¹ : Y â†’ X
  fâ»Â¹ y = prâ‚ (II y)

  Î· : fâ»Â¹ âˆ˜ f âˆ¼ id
  Î· x = embeddings-are-lc Ï† Ï†-emb
         (Ï† (fâ»Â¹ (f x)) ï¼âŸ¨ prâ‚‚ (II (f x)) âŸ©
          Î³ (f x)       ï¼âŸ¨ prâ‚‚ (I x) âŸ©
          Ï† x           âˆ)

  Îµ : f âˆ˜ fâ»Â¹ âˆ¼ id
  Îµ y = embeddings-are-lc Î³ Î³-emb
         (Î³ (f (fâ»Â¹ y)) ï¼âŸ¨ prâ‚‚ (I (fâ»Â¹ y)) âŸ©
          Ï† (fâ»Â¹ y)     ï¼âŸ¨ prâ‚‚ (II y) âŸ©
          Î³ y           âˆ)

  ğ•— : X â‰ƒ Y
  ğ•— = qinveq f (fâ»Â¹ , Î· , Îµ)

  p : X ï¼ Y
  p = eqtoid (ua ğ“¤) X Y ğ•—

  III : Idtofun p ï¼ f
  III = Idtofun-eqtoid (ua ğ“¤) ğ•—

  IV = Î» x â†’
   Ï† x             ï¼âŸ¨ (prâ‚‚ (I x))â»Â¹ âŸ©
   Î³ (f x)         ï¼âŸ¨ ap (Î» - â†’ Î³ (- x)) (III â»Â¹) âŸ©
   Î³ (Idtofun p x) âˆ

  V : A ï¼ B
  V = to-ğ•-ï¼ (p , dfunext fe IV) (Ï†-emb , g) (Î³-emb , h)

\end{code}

It follows that ğ• is 0-type, or set, in the sense of the HoTT
book. But notice that we now have two notions of set in this
discussion: the "material" (iterative set) one and the "structural"
one (0-type or set). The reader should keep this distinction in mind
for the comments and code below.

The following uses the fact that any type with an extensional order is
automatically a set.

\begin{code}

ğ•-is-set : is-set ğ•
ğ•-is-set = extensionally-ordered-types-are-sets _âˆˆ_ fe'
            âˆˆ-is-prop-valued
            âˆˆ-is-extensional

\end{code}

Here is a second, more direct, proof of this fact.

The following says that ssup Ï† ï¼ M is a proposition for every M : ğ•„
if Ï† is an embedding.

The following doesn't seem to have been observed before in the
literature.

\begin{code}

ğ•„-ssup-is-h-isolated : (X : ğ“¤ Ì‡ ) (Ï† : X â†’ ğ•„)
                     â†’ is-embedding Ï†
                     â†’ is-h-isolated (ssup X Ï†)
ğ•„-ssup-is-h-isolated X Ï† Ï†-emb {M} = III
 where
  I = (ssup X Ï† ï¼ M)                        â‰ƒâŸ¨ ï¼-flip âŸ©
      (M ï¼ ssup X Ï†)                        â‰ƒâŸ¨ ğ•„-ï¼' M (ssup X Ï†) âŸ©
      fiber ((Ï† âˆ˜_) âˆ˜ Idtofun) (ğ•„-forest M)  â– 

  II : is-embedding ((Ï† âˆ˜_) âˆ˜ Idtofun)
  II = âˆ˜-is-embedding
        (Idtofun-is-embedding (ua ğ“¤) fe)
        (postcomp-is-embedding fe' Ï† Ï†-emb)

  III : is-prop (ssup X Ï† ï¼ M)
  III = equiv-to-prop I (II (ğ•„-forest M))

\end{code}

And a particular case of this is that if M is an iterative set then
M ï¼ N is a proposition for every *multiset* N.

\begin{code}

isets-are-h-isolated : (M : ğ•„)
                     â†’ is-iterative-set M
                     â†’ is-h-isolated M
isets-are-h-isolated (ssup X Ï†) (Ï†-emb , _) = ğ•„-ssup-is-h-isolated X Ï† Ï†-emb

\end{code}

Because a subtype of any type whatsoever consisting of h-isolated
elements is a 0-type, we get a second proof that the type of iterative
sets is a 0-type.

\begin{code}

ğ•-is-set' : is-set ğ•
ğ•-is-set' {M , M-is-is-set} =
 equiv-to-prop
  (â‰ƒ-sym (to-subtype-ï¼-â‰ƒ being-iset-is-prop))
  (isets-are-h-isolated M M-is-is-set)

\end{code}

By definition, an iterative multiset is an iterative set if its
ğ•„-forests are all embeddings. The ğ•-forests are also embeddings:

\begin{code}

ğ•-forest-is-embedding : (A : ğ•) â†’ is-embedding (ğ•-forest A)
ğ•-forest-is-embedding A@(ssup X Ï† , Ï†-emb , Ï†-iter) =
 pair-fun-is-embedding-special Ï† Ï†-iter Ï†-emb being-iset-is-prop

\end{code}

We construct elements of ğ•„ using the constructor ssup. We now
introduce a corresponding constructor ğ•-ssup to construct elements of
the type ğ•.

\begin{code}

ğ•-ssup : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) â†’ is-embedding Ï• â†’ ğ•
ğ•-ssup X Ï• Ï•-emb = ssup X Ï† , Ï†-emb , Ï†-iset
 where
  Ï† : X â†’ ğ•„
  Ï† = underlying-mset âˆ˜ Ï•

  Ï†-iset : (x : X) â†’ is-iterative-set (Ï† x)
  Ï†-iset = isets-are-iterative âˆ˜ Ï•

  Ï†-emb : is-embedding Ï†
  Ï†-emb = âˆ˜-is-embedding Ï•-emb underlying-mset-is-embedding

\end{code}

It behaves as expected with respect to the corresponding destructors:

\begin{code}

ğ•-ssup-root : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
            â†’ ğ•-root (ğ•-ssup X Ï• e) ï¼ X
ğ•-ssup-root X Ï• e = refl

ğ•-ssup-forest : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
              â†’ ğ•-forest (ğ•-ssup X Ï• e) ï¼ Ï•
ğ•-ssup-forest X Ï• e = refl

\end{code}

Notice that the identifications are definitional.

We have the following Î· rules for ğ•, where the first is more general
and the second is more natural. In both cases the identifications are
not definitional.

\begin{code}

ğ•-Î·' : (A : ğ•) (e : is-embedding (ğ•-forest A))
     â†’ ğ•-ssup (ğ•-root A) (ğ•-forest A) e ï¼ A
ğ•-Î·' (ssup _ _ , _) _ = to-subtype-ï¼ being-iset-is-prop refl

ğ•-Î· : (A : ğ•) â†’ ğ•-ssup (ğ•-root A) (ğ•-forest A) (ğ•-forest-is-embedding A) ï¼ A
ğ•-Î· A = ğ•-Î·' A (ğ•-forest-is-embedding A)

\end{code}

Here are two characterizations of the membership relation:

\begin{code}

âˆˆ-behaviour : (A : ğ•) (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
            â†’ A âˆˆ ğ•-ssup X Ï• e â‰ƒ (Î£ x ê‰ X , Ï• x ï¼ A)
âˆˆ-behaviour A X Ï• e =
 (A âˆˆ ğ•-ssup X Ï• e)                                     â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
 (Î£ x ê‰ X , underlying-mset (Ï• x) ï¼ underlying-mset A) â‰ƒâŸ¨ Î£-cong I âŸ©
 (Î£ x ê‰ X , Ï• x ï¼ A)                                   â– 
  where
   I : (x : X) â†’ (underlying-mset (Ï• x) ï¼ underlying-mset A) â‰ƒ (Ï• x ï¼ A)
   I x = embedding-criterion-converse
          underlying-mset
          underlying-mset-is-embedding
          (Ï• x)
          A

âˆˆ-behaviour' : (A B : ğ•) â†’ A âˆˆ B â‰ƒ (Î£ x ê‰ ğ•-root B , ğ•-forest B x ï¼ A)
âˆˆ-behaviour' A B =
 transport
  (Î» - â†’ A âˆˆ - â‰ƒ (Î£ x ê‰ ğ•-root - , ğ•-forest - x ï¼ A))
  (ğ•-Î· B)
  (âˆˆ-behaviour A (ğ•-root B) (ğ•-forest B) (ğ•-forest-is-embedding B))

private
 âˆˆ-remark : (A B : ğ•) â†’ A âˆˆ B â‰ƒ fiber (ğ•-forest B) A
 âˆˆ-remark = âˆˆ-behaviour'

\end{code}

It also follows from the facts that ğ• is a set and that ğ•-forest is an
embedding that the root of any iterative set is a 0-type:

\begin{code}

ğ•-root-is-set : (A : ğ•) â†’ is-set (ğ•-root A)
ğ•-root-is-set A = subtypes-of-sets-are-sets
                   (ğ•-forest A)
                   (ğ•-forest-is-embedding A)
                   ğ•-is-set
\end{code}

It would be nice if we could define ğ• inductively as follows:

 data ğ• : ğ“¤âº Ì‡ where
  ğ•-ssup : (X : ğ“¤ Ì‡ ) (Ï† : X â†’ ğ•) â†’ is-embedding Ï† â†’ ğ•

However, this is not a strictly positive definition, for the criterion
of strict positivity adopted by Agda, and so it is not accepted.

Nevertheless, all iterative sets *are* generated by the "constructor"
ğ•-ssup, in the following sense, so that we can view ğ• as really
inductively defined by the above data declaration.

The following result, implementing the above idea, seems to be new.

\begin{code}

ğ•-Induction'
 : (P : ğ• â†’ ğ“¥ Ì‡ )
   (f : (A : ğ•) â†’ ((x : ğ•-root A) â†’ P (ğ•-forest A x)) â†’ P A)
 â†’ Î£ h ê‰ ((A : ğ•) â†’ P A)
       , ((A : ğ•) â†’ h A ï¼ f A (Î» x â†’ h (ğ•-forest A x)))
ğ•-Induction' P f = (Î» (M , i) â†’ H M i) , p
 where
  H : (M : ğ•„) (i : is-iterative-set M) â†’ P (M , i)
  H M@(ssup X Ï†) i@(_ , Ï†-iter) = f (M , i) (Î» x â†’ H (Ï† x) (Ï†-iter x))

  p : (A : ğ•) â†’ _ ï¼ _
  p (M@(ssup X Ï†) , i@(_ , Ï†-iter)) = refl

ğ•-Induction
 : (P : ğ• â†’ ğ“¥ Ì‡ )
 â†’ (f : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
      â†’ ((x : X) â†’ P (Ï• x))
      â†’ P (ğ•-ssup X Ï• e))
 â†’ Î£ h ê‰ ((A : ğ•) â†’ P A)
       , ((X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
       â†’ h (ğ•-ssup X Ï• e) ï¼ f X Ï• e (Î» x â†’ h (Ï• x)))
ğ•-Induction {ğ“¥} P f = h , IV
 where
  f' : (A : ğ•) â†’ ((x : ğ•-root A) â†’ P (ğ•-forest A x)) â†’ P A
  f' A@(M@(ssup X Ï†) , i@(Ï†-emb , Ï†-iter)) g = II
   where
    I : P (ğ•-ssup X (ğ•-forest A) (ğ•-forest-is-embedding A))
    I = f X (ğ•-forest A) (ğ•-forest-is-embedding A) g

    II : P A
    II = transport P (ğ•-Î· A) I

  h : (A : ğ•) â†’ P A
  h = prâ‚ (ğ•-Induction' P f')

  III : (A : ğ•) â†’ h A ï¼ f' A (Î» x â†’ h (ğ•-forest A x))
  III = prâ‚‚ (ğ•-Induction' P f')

  IV : (X : ğ“¤ Ì‡) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
     â†’ h (ğ•-ssup X Ï• e) ï¼ f X Ï• e (Î» x â†’ h (Ï• x))
  IV X Ï• e =
   h A                                                               ï¼âŸ¨ III A âŸ©
   f' A (Î» x â†’ h (Ï• x))                                              ï¼âŸ¨ refl âŸ©
   t P                (ğ•-Î· A)             (f X Ï• e' (Î» x â†’ h (Ï• x))) ï¼âŸ¨ i âŸ©
   t P                (ap (ğ•-ssup X Ï•) p) (f X Ï• e' (Î» x â†’ h (Ï• x))) ï¼âŸ¨ ii âŸ©
   t (P âˆ˜ ğ•-ssup X Ï•) p                   (f X Ï• e' (Î» x â†’ h (Ï• x))) ï¼âŸ¨ iii âŸ©
   f X Ï• e (Î» x â†’ h (Ï• x))                                           âˆ
    where
     t = transport
     A  = ğ•-ssup X Ï• e
     e' = ğ•-forest-is-embedding A

     p : e' ï¼ e
     p = being-embedding-is-prop fe Ï• e' e

     q : ğ•-Î· A ï¼ ap (ğ•-ssup X Ï•) p
     q = ğ•-is-set _ _

     i   = ap (Î» - â†’ t P - (f X Ï• e' (Î» x â†’ h (Ï• x)))) q
     ii  = (transport-ap P (ğ•-ssup X Ï•) p)â»Â¹
     iii = apd (Î» - â†’ f X Ï• - (Î» x â†’ h (Ï• x))) p

ğ•-induction : (P : ğ• â†’ ğ“¥ Ì‡ )
            â†’ ((X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
                  â†’ ((x : X) â†’ P (Ï• x))
                  â†’ P (ğ•-ssup X Ï• e))
            â†’ (A : ğ•) â†’ P A
ğ•-induction P f = prâ‚ (ğ•-Induction P f)

ğ•-induction-behaviour
 : (P : ğ• â†’ ğ“¥ Ì‡ )
 â†’ (f : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
      â†’ ((x : X) â†’ P (Ï• x))
      â†’ P (ğ•-ssup X Ï• e))
 â†’ (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
 â†’ ğ•-induction P f (ğ•-ssup X Ï• e) ï¼ f X Ï• e (Î» x â†’ ğ•-induction P f (Ï• x))
ğ•-induction-behaviour P f = prâ‚‚ (ğ•-Induction P f)

ğ•-recursion : (P : ğ“¥ Ì‡ )
            â†’ ((X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•)
                  â†’ is-embedding Ï•
                  â†’ (X â†’ P)
                  â†’ P)
            â†’ ğ• â†’ P
ğ•-recursion P = ğ•-induction (Î» _ â†’ P)

ğ•-recursion-behaviour
 : (P : ğ“¥ Ì‡ )
 â†’ (f : (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
      â†’ (X â†’ P)
      â†’ P)
 â†’ (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
 â†’ ğ•-recursion P f (ğ•-ssup X Ï• e) ï¼ f X Ï• e (Î» x â†’ ğ•-recursion P f (Ï• x))
ğ•-recursion-behaviour P = ğ•-induction-behaviour (Î» _ â†’ P)

ğ•-iteration : (P : ğ“¥ Ì‡ )
            â†’ ((X : ğ“¤ Ì‡ ) â†’ (X â†’ P) â†’ P)
            â†’ ğ• â†’ P
ğ•-iteration P f = ğ•-recursion P (Î» X Ï• e â†’ f X)

ğ•-iteration-behaviour
 : (P : ğ“¥ Ì‡ )
 â†’ (f : (X : ğ“¤ Ì‡ ) â†’ (X â†’ P) â†’ P)
 â†’ (X : ğ“¤ Ì‡ ) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
 â†’ ğ•-iteration P f (ğ•-ssup X Ï• e) ï¼ f X (Î» x â†’ ğ•-iteration P f (Ï• x))
ğ•-iteration-behaviour P f = ğ•-recursion-behaviour P (Î» X Ï• e â†’ f X)

\end{code}

So we are essentially working with (an encoding) of the above
non-strictly positive data type.

The usual induction principle for iterative sets follows directly from
the above form of induction. This consequence is already in Gylterud [4].

\begin{code}

âˆˆ-induction : (P : ğ• â†’ ğ“¥ Ì‡ )
            â†’ ((A : ğ•) â†’ ((B : ğ•) â†’ B âˆˆ A â†’ P B) â†’ P A)
            â†’ (A : ğ•) â†’ P A
âˆˆ-induction P IH = ğ•-induction P f
 where
  f : (X : ğ“¤ Ì‡) (Ï• : X â†’ ğ•) (e : is-embedding Ï•)
    â†’ ((x : X) â†’ P (Ï• x))
    â†’ P (ğ•-ssup X Ï• e)
  f X Ï• e IH' = IH A s
   where
    A : ğ•
    A = ğ•-ssup X Ï• e

    s : (B : ğ•) â†’ B âˆˆ A â†’ P B
    s B@(.(underlying-mset (Ï• x)) , j) (x , refl) = II
     where
      I : P (Ï• x)
      I = IH' x

      II : P (underlying-mset (Ï• x) , j)
      II = transport P (to-subtype-ï¼ being-iset-is-prop refl) I

\end{code}

And then it follows immediately that the membership relation is
accessible:

\begin{code}

âˆˆ-is-accessible : (A : ğ•) â†’ is-accessible _âˆˆ_ A
âˆˆ-is-accessible = âˆˆ-induction (is-accessible _âˆˆ_) (Î» _ â†’ acc)

\end{code}

Singleton sets can be constructed as follows.

\begin{code}

â´_âµ : (A : ğ•) â†’ ğ•
â´ A âµ = ğ•-ssup ğŸ™ (Î» _ â†’ A) (global-point-is-embedding (Î» _ â†’ A) ğ•-is-set)

â´âµ-behaviour : (A : ğ•) (B : ğ•) â†’ B âˆˆ â´ A âµ â‰ƒ (B ï¼ A)
â´âµ-behaviour A B = B âˆˆ â´ A âµ    â‰ƒâŸ¨ âˆˆ-behaviour' B â´ A âµ âŸ©
                   ğŸ™ Ã— (A ï¼ B) â‰ƒâŸ¨ ğŸ™-lneutral âŸ©
                   (A ï¼ B)     â‰ƒâŸ¨ ï¼-flip âŸ©
                   (B ï¼ A)     â– 

\end{code}

Given a family of iterative sets indexed by a small type, we construct
its union as in [4].

We make use of propositional truncations (to define the image of a
map) and of set replacement (which follows from having set quotients).

\begin{code}

open import UF.PropTrunc

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt
 open import UF.ImageAndSurjection pt

 module unions-of-iterative-sets (sr : Set-Replacement pt) where

  private
   module union-construction
          {I : ğ“¤ Ì‡  }
          (ğ“ : I â†’ ğ•)
         where

    im : ğ“¤âº Ì‡
    im = image ğ“
    im-is-small : image ğ“ is ğ“¤ small
    im-is-small = sr ğ“ (I , â‰ƒ-refl I) ğ•-is-locally-small ğ•-is-set
    imâ» : ğ“¤ Ì‡
    imâ» = resized im im-is-small
    imâ»-â‰ƒ-im : imâ» â‰ƒ im
    imâ»-â‰ƒ-im = resizing-condition im-is-small
    Ï€ : im â†’ ğ•
    Ï€ = restriction ğ“
    Ï€â» : imâ» â†’ ğ•
    Ï€â» = Ï€ âˆ˜ âŒœ imâ»-â‰ƒ-im âŒ
    Ï€-is-embedding : is-embedding Ï€
    Ï€-is-embedding = restrictions-are-embeddings ğ“
    Ï€â»-is-embedding : is-embedding Ï€â»
    Ï€â»-is-embedding = âˆ˜-is-embedding
                       (equivs-are-embeddings
                         âŒœ imâ»-â‰ƒ-im âŒ
                         (âŒœâŒ-is-equiv imâ»-â‰ƒ-im))
                       Ï€-is-embedding

  â‹ƒ : {I : ğ“¤ Ì‡  } (ğ“ : I â†’ ğ•) â†’ ğ•
  â‹ƒ {I} ğ“ = ğ•-ssup imâ» Ï€â» Ï€â»-is-embedding
   where
    open union-construction ğ“

  â‹ƒ-behaviour : {I : ğ“¤ Ì‡  } (ğ“ : I â†’ ğ•) (B : ğ•)
              â†’ B âˆˆ â‹ƒ ğ“ â‰ƒ (âˆƒ i ê‰ I , B ï¼ ğ“ i)
  â‹ƒ-behaviour {I} ğ“ B =
   B âˆˆ â‹ƒ ğ“                                    â‰ƒâŸ¨ âˆˆ-behaviour' B (â‹ƒ ğ“) âŸ©
   (Î£ j ê‰ imâ» , Ï€â» j ï¼ B)                    â‰ƒâŸ¨ eâ‚ âŸ©
   (Î£ j ê‰ im , Ï€ j ï¼ B)                      â‰ƒâŸ¨ Î£-assoc âŸ©
   (Î£ C ê‰ ğ• , C âˆˆimage ğ“ Ã— (C ï¼ B))          â‰ƒâŸ¨ Î£-cong (Î» C â†’ Ã—-comm) âŸ©
   (Î£ C ê‰ ğ• , (C ï¼ B) Ã— (C âˆˆimage ğ“))        â‰ƒâŸ¨ â‰ƒ-sym Î£-assoc âŸ©
   (Î£ s ê‰ singleton-type' B , prâ‚ s âˆˆimage ğ“) â‰ƒâŸ¨ â‰ƒ-sym eâ‚‚ âŸ©
   ğŸ™ {ğ“¤} Ã— B âˆˆimage ğ“                         â‰ƒâŸ¨ ğŸ™-lneutral âŸ©
   (âˆƒ i ê‰ I , ğ“ i ï¼ B)                       â‰ƒâŸ¨ âˆƒ-cong pt (Î» i â†’ ï¼-flip) âŸ©
   (âˆƒ i ê‰ I , B ï¼ ğ“ i)                       â– 
    where
     open union-construction ğ“
     eâ‚ = Î£-change-of-variable-â‰ƒ _ imâ»-â‰ƒ-im
     eâ‚‚ = Î£-change-of-variable-â‰ƒ _
           (singleton-â‰ƒ-ğŸ™' (singleton-types'-are-singletons B))

\end{code}

Any iterative set is the union of its ğ•-forest.

\begin{code}

  â‹ƒ-Î· : (A : ğ•) â†’ â‹ƒ (ğ•-forest A) ï¼ A
  â‹ƒ-Î· A = âˆˆ-is-extensional (â‹ƒ (ğ•-forest A)) A i j
   where
    i : â‹ƒ (ğ•-forest A) âŠ† A
    i B m = âˆ¥âˆ¥-rec (âˆˆ-is-prop-valued B A) f (âŒœ â‹ƒ-behaviour (ğ•-forest A) B âŒ m)
     where
      f : (Î£ a ê‰ ğ•-root A , B ï¼ ğ•-forest A a) â†’ B âˆˆ A
      f (a , refl) = ğ•-forest-âˆˆ A a
    j : A âŠ† â‹ƒ (ğ•-forest A)
    j B m = âŒœ â‹ƒ-behaviour (ğ•-forest A) B âŒâ»Â¹ âˆ£ a , e âˆ£
     where
      abstract
       m' : Î£ a ê‰ ğ•-root A , ğ•-forest A a ï¼ B
       m' = âŒœ âˆˆ-behaviour' B A âŒ m
       a : ğ•-root A
       a = prâ‚ m'
       e : B ï¼ ğ•-forest A a
       e = (prâ‚‚ m') â»Â¹

\end{code}

Unions allow us to construct a retraction of the inclusion ğ• â†ª ğ•„, and
this seems to be a new result.

\begin{code}

  ğ•„-to-ğ• : ğ•„ â†’ ğ•
  ğ•„-to-ğ• (ssup X Ï†) = â‹ƒ (ğ•„-to-ğ• âˆ˜ Ï†)

  ğ•„-to-ğ•-is-retraction-of-inclusion : ğ•„-to-ğ• âˆ˜ underlying-mset ï¼ id
  ğ•„-to-ğ•-is-retraction-of-inclusion = dfunext fe (âˆˆ-induction _ f)
   where
    f : (A : ğ•) â†’ ((B : ğ•) â†’ B âˆˆ A â†’ (ğ•„-to-ğ• âˆ˜ underlying-mset) B ï¼ B)
      â†’ (ğ•„-to-ğ• âˆ˜ underlying-mset) A ï¼ A
    f A IH = ğ•„-to-ğ• Aâ‚˜                                 ï¼âŸ¨ I âŸ©
             ğ•„-to-ğ• (ssup (ğ•-root A) (ğ•„-forest Aâ‚˜))    ï¼âŸ¨ refl âŸ©
             â‹ƒ (ğ•„-to-ğ• âˆ˜ ğ•„-forest Aâ‚˜)                  ï¼âŸ¨ refl âŸ©
             â‹ƒ (ğ•„-to-ğ• âˆ˜ underlying-mset âˆ˜ ğ•-forest A) ï¼âŸ¨ II âŸ©
             â‹ƒ (ğ•-forest A)                            ï¼âŸ¨ â‹ƒ-Î· A âŸ©
             A                                         âˆ
              where
               Aâ‚˜ : ğ•„
               Aâ‚˜ = underlying-mset A
               I  = ap (ğ•„-to-ğ• âˆ˜ underlying-mset) (ğ•-Î· A â»Â¹)
               II = ap â‹ƒ (dfunext fe (Î» a â†’ IH (ğ•-forest A a) (ğ•-forest-âˆˆ A a)))

  ğ•-is-retract-of-ğ•„ : retract ğ• of ğ•„
  ğ•-is-retract-of-ğ•„ = ğ•„-to-ğ• ,
                      underlying-mset ,
                      happly ğ•„-to-ğ•-is-retraction-of-inclusion

\end{code}
