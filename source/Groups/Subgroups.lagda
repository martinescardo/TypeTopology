--------------------------------------------------------------------------------
Ettore Aldrovandi ealdrovandi@fsu.edu
Keri D'Angelo kd349@cornell.edu

June 2022
--------------------------------------------------------------------------------

This only exists to use the subgroups code from UF-SIP-Examples with
Groups interface. The code is almost literally imported from the
subgroup module in UF-SIP-Examples with minor adaptations, since the
interface defined by Groups is different. This relies on the proof
that the group axioms, as defined in Groups, form a proposition.


\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.Base hiding (_â‰ˆ_)
open import UF.Classifiers
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Powerset
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Univalence

open import Groups.Type renaming (_â‰…_ to _â‰£_)

module Groups.Subgroups
       (ğ“¤ : Universe)
       (ua : Univalence) where


fe : âˆ€ {ğ“¥} {ğ“¦} â†’ funext ğ“¥ ğ“¦
fe {ğ“¥} {ğ“¦} = univalence-gives-funext' ğ“¥ ğ“¦ (ua ğ“¥) (ua (ğ“¥ âŠ” ğ“¦))

module _ (G : Group ğ“¤) where

  private
    _Â·_ : âŸ¨ G âŸ© â†’ âŸ¨ G âŸ© â†’ âŸ¨ G âŸ©
    _Â·_ = multiplication G

    e : âŸ¨ G âŸ©
    e = unit G

    infixl 42 _Â·_

  group-closed : (âŸ¨ G âŸ© â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  group-closed ğ“ = ğ“ (unit G)
                 Ã— ((x y : âŸ¨ G âŸ©) â†’ ğ“ x â†’ ğ“ y â†’ ğ“ (x Â· y))
                 Ã— ((x : âŸ¨ G âŸ©) â†’ ğ“ x â†’ ğ“ (inv G x))

  Subgroups : ğ“¤ âº Ì‡
  Subgroups = Î£ A ê‰ ğ“Ÿ âŸ¨ G âŸ© , group-closed (_âˆˆ A)

  âŸª_âŸ« : Subgroups â†’ ğ“Ÿ âŸ¨ G âŸ©
  âŸª A , u , c , Î¹ âŸ« = A

  being-group-closed-subset-is-prop : (A : ğ“Ÿ âŸ¨ G âŸ©)
                                    â†’ is-prop (group-closed (_âˆˆ A))
  being-group-closed-subset-is-prop A = Ã—-is-prop
                                            (âˆˆ-is-prop A (unit G))
                                         (Ã—-is-prop
                                            (Î -is-prop fe
                                               (Î» x â†’ Î -is-prop fe
                                               (Î» y â†’ Î -is-prop fe
                                               (Î» _ â†’ Î -is-prop fe
                                               (Î» _ â†’ âˆˆ-is-prop A (x Â· y))))))
                                            (Î -is-prop fe
                                               (Î» x â†’ Î -is-prop fe
                                               (Î» _ â†’ âˆˆ-is-prop A (inv G x)))))

  âŸªâŸ«-is-embedding : is-embedding âŸª_âŸ«
  âŸªâŸ«-is-embedding = prâ‚-is-embedding being-group-closed-subset-is-prop

  ap-âŸªâŸ« : (S T : Subgroups) â†’ S ï¼ T â†’ âŸª S âŸ« ï¼ âŸª T âŸ«
  ap-âŸªâŸ« S T = ap âŸª_âŸ«

  ap-âŸªâŸ«-is-equiv : (S T : Subgroups) â†’ is-equiv (ap-âŸªâŸ« S T)
  ap-âŸªâŸ«-is-equiv = embedding-gives-embedding' âŸª_âŸ« âŸªâŸ«-is-embedding

  subgroups-form-a-set : is-set Subgroups
  subgroups-form-a-set {S} {T} = equiv-to-prop
                                  (ap-âŸªâŸ« S T , ap-âŸªâŸ«-is-equiv S T)
                                  (ğ“Ÿ-is-set ua)

  subgroup-equality : (S T : Subgroups)
                    â†’ (S ï¼ T)
                    â‰ƒ ((x : âŸ¨ G âŸ©) â†’ (x âˆˆ âŸª S âŸ«) â†” (x âˆˆ âŸª T âŸ«))

  subgroup-equality S T = Î³
   where
    f : S ï¼ T â†’ (x : âŸ¨ G âŸ©) â†’ x âˆˆ âŸª S âŸ« â†” x âˆˆ âŸª T âŸ«
    f p x = transport (Î» - â†’ x âˆˆ âŸª - âŸ«) p , transport (Î» - â†’ x âˆˆ âŸª - âŸ«) (p â»Â¹)

    h : ((x : âŸ¨ G âŸ©) â†’ x âˆˆ âŸª S âŸ« â†” x âˆˆ âŸª T âŸ«) â†’ âŸª S âŸ« ï¼ âŸª T âŸ«
    h Ï† = subset-extensionality' ua Î± Î²
     where
      Î± : âŸª S âŸ« âŠ† âŸª T âŸ«
      Î± x = lr-implication (Ï† x)

      Î² : âŸª T âŸ« âŠ† âŸª S âŸ«
      Î² x = rl-implication (Ï† x)

    g : ((x : âŸ¨ G âŸ©) â†’ x âˆˆ âŸª S âŸ« â†” x âˆˆ âŸª T âŸ«) â†’ S ï¼ T
    g = inverse (ap-âŸªâŸ« S T) (ap-âŸªâŸ«-is-equiv S T) âˆ˜ h

    Î³ : (S ï¼ T) â‰ƒ ((x : âŸ¨ G âŸ©) â†’ x âˆˆ âŸª S âŸ« â†” x âˆˆ âŸª T âŸ«)
    Î³ = logically-equivalent-props-are-equivalent
         subgroups-form-a-set
         (Î -is-prop fe
           (Î» x â†’ Ã—-is-prop
                   (Î -is-prop fe (Î» _ â†’ âˆˆ-is-prop âŸª T âŸ« x))
                   (Î -is-prop fe (Î» _ â†’ âˆˆ-is-prop âŸª S âŸ« x)))) f g

  T : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
  T X = Î£ _Â·_  ê‰ group-structure X , group-axioms X _Â·_

  module _ {X : ğ“¤ Ì‡ } (h : X â†’ âŸ¨ G âŸ©) (e : is-embedding h) where

   private
    h-lc : left-cancellable h
    h-lc = embeddings-are-lc h e

   having-group-closed-fiber-is-prop : is-prop (group-closed (fiber h))
   having-group-closed-fiber-is-prop = being-group-closed-subset-is-prop
                                        (Î» x â†’ (fiber h x , e x))

   at-most-one-homomorphic-structure : is-prop (Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h)
   at-most-one-homomorphic-structure ((_*_ , gaxiom) , pmult) ((_*'_ , gaxiom') , pmult')
     = to-subtype-ï¼ (Î» Ï„ â†’ being-hom-is-prop fe ((X , Ï„)) G h) Î´
    where
     Ï„ Ï„' : T X
     Ï„  = _*_ , gaxiom
     Ï„' = _*'_ , gaxiom'

     p : _*_ ï¼ _*'_
     p = dfunext fe (Î» x â†’ dfunext fe (Î» y â†’ h-lc ( h (x * y)  ï¼âŸ¨ pmult âŸ©
                                                    h x Â· h y  ï¼âŸ¨ pmult' â»Â¹ âŸ©
                                                    h (x *' y) âˆ)))
     Î´ : Ï„ ï¼ Ï„'
     Î´ = to-subtype-ï¼ (Î» _ â†’ group-axioms-is-prop fe X _) p

   group-closed-fiber-gives-homomorphic-structure : funext ğ“¤ ğ“¤
                                                  â†’ group-closed (fiber h)
                                                  â†’ (Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h)

   group-closed-fiber-gives-homomorphic-structure fe (unitc , mulc , invc) = Ï„ , i
    where
     Ï† : (x : X) â†’ fiber h (h x)
     Ï† x = (x , ğ“»ğ“®ğ’»ğ“µ (h x))

     unitH : X
     unitH = fiber-point unitc

     _*_ : X â†’ X â†’ X
     x * y = fiber-point (mulc (h x) (h y) (Ï† x) (Ï† y))

     invH : X â†’ X
     invH x = fiber-point (invc (h x) (Ï† x))

     pmul : (x y : X) â†’ h (x * y) ï¼ h x Â· h y
     pmul x y = fiber-identification (mulc (h x) (h y) (Ï† x) (Ï† y))

     punit : h unitH ï¼ unit G
     punit = fiber-identification unitc

     pinv : (x : X) â†’ h (invH x) ï¼ inv G (h x)
     pinv x = fiber-identification (invc (h x) (Ï† x))

     unitH-left : (x : X) â†’ unitH * x ï¼ x
     unitH-left x = h-lc (h (unitH * x) ï¼âŸ¨ pmul unitH x âŸ©
                          h unitH Â· h x ï¼âŸ¨ ap (_Â· h x) punit âŸ©
                          unit G Â· h x  ï¼âŸ¨ unit-left G (h x) âŸ©
                          h x           âˆ)

     unitH-right : (x : X) â†’ x * unitH ï¼ x
     unitH-right x = h-lc (h (x * unitH) ï¼âŸ¨ pmul x unitH âŸ©
                           h x Â· h unitH ï¼âŸ¨ ap (h x Â·_) punit âŸ©
                           h x Â· unit G  ï¼âŸ¨ unit-right G (h x) âŸ©
                           h x           âˆ)

     assocH : (x y z : X) â†’ ((x * y) * z) ï¼ (x * (y * z))
     assocH x y z = h-lc (h ((x * y) * z)   ï¼âŸ¨ pmul (x * y) z âŸ©
                          h (x * y) Â· h z   ï¼âŸ¨ ap (_Â· h z) (pmul x y) âŸ©
                          (h x Â· h y) Â· h z ï¼âŸ¨ assoc G (h x) (h y) (h z) âŸ©
                          h x Â· (h y Â· h z) ï¼âŸ¨ (ap (h x Â·_) (pmul y z))â»Â¹ âŸ©
                          h x Â· h (y * z)   ï¼âŸ¨ (pmul x (y * z))â»Â¹ âŸ©
                          h (x * (y * z))   âˆ)

     group-axiomH : (x : X) â†’ Î£ x' ê‰ X , (x' * x ï¼ unitH) Ã— (x * x' ï¼ unitH)
     group-axiomH x = invH x , h-lc (h (invH x * x)    ï¼âŸ¨ pmul (invH x) x âŸ©
                                     h (invH x) Â· h x  ï¼âŸ¨ ap (_Â· h x) (pinv x) âŸ©
                                     inv G (h x) Â· h x ï¼âŸ¨ inv-left G (h x)  âŸ©
                                     unit G            ï¼âŸ¨ punit â»Â¹ âŸ©
                                     h unitH âˆ) ,

                               h-lc (h (x * invH x)    ï¼âŸ¨ pmul x (invH x) âŸ©
                                     h x Â· h (invH x)  ï¼âŸ¨ ap (h x Â·_) (pinv x) âŸ©
                                     h x Â· inv G (h x) ï¼âŸ¨ inv-right G (h x) âŸ©
                                     unit G            ï¼âŸ¨ punit â»Â¹ âŸ©
                                     h unitH âˆ)

     j : is-set X
     j = subtypes-of-sets-are-sets' h h-lc (groups-are-sets G)

     Ï„ : T X
     Ï„ = _*_ , (j , (assocH , unitH , (unitH-left , (unitH-right , group-axiomH))))


     i : is-hom (X , Ï„) G h
     i {x} {y} = pmul x y


   homomorphic-structure-gives-group-closed-fiber : (Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h)
                                                  â†’ group-closed (fiber h)

   homomorphic-structure-gives-group-closed-fiber ((_*_ , gaxiom) , pmult) = (unitc , mulc , invc)
    where
     H : Group ğ“¤
     H = X , (_*_ , gaxiom)

     unitH : X
     unitH = prâ‚ (prâ‚‚ (prâ‚‚ gaxiom))

     unitc : fiber h (unit G)
     unitc = unitH , (homs-preserve-unit H G h pmult)


     mulc : ((x y : âŸ¨ G âŸ©) â†’ fiber h x â†’ fiber h y â†’ fiber h (x Â· y))
     mulc x y (a , p) (b , q) = (a * b) ,
                                (h (a * b) ï¼âŸ¨ pmult {a} {b} âŸ©
                                 h a Â· h b ï¼âŸ¨ apâ‚‚ (Î» - -' â†’ - Â· -') p q âŸ©
                                 x Â· y     âˆ)

     invc : ((x : âŸ¨ G âŸ©) â†’ fiber h x â†’ fiber h (inv G x))
     invc x (a , p) = inv H a ,
                      (h (inv H a) ï¼âŸ¨ homs-preserve-invs H G h pmult a âŸ©
                       inv G (h a) ï¼âŸ¨ ap (inv G) p âŸ©
                       inv G x     âˆ)


   fiber-structure-lemma : funext ğ“¤ ğ“¤
                         â†’ group-closed (fiber h)
                         â‰ƒ (Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h)

   fiber-structure-lemma fe = logically-equivalent-props-are-equivalent
                               having-group-closed-fiber-is-prop
                               at-most-one-homomorphic-structure
                               (group-closed-fiber-gives-homomorphic-structure fe)
                               homomorphic-structure-gives-group-closed-fiber


  characterization-of-the-type-of-subgroups : Subgroups â‰ƒ (Î£ H ê‰ Group ğ“¤
                                                         , Î£ h ê‰ (âŸ¨ H âŸ© â†’ âŸ¨ G âŸ©)
                                                         , is-embedding h
                                                         Ã— is-hom H G h)
  characterization-of-the-type-of-subgroups =

   Subgroups                                                                           â‰ƒâŸ¨ i âŸ©
   (Î£ A ê‰ ğ“Ÿ âŸ¨ G âŸ© , group-closed (_âˆˆ A))                                                â‰ƒâŸ¨ ii âŸ©
   (Î£ (X , h , e) ê‰ Subtypes âŸ¨ G âŸ© , group-closed (fiber h))                             â‰ƒâŸ¨ iii âŸ©
   (Î£ X ê‰ ğ“¤ Ì‡ , Î£ (h , e) ê‰ X â†ª âŸ¨ G âŸ© , group-closed (fiber h))                          â‰ƒâŸ¨ iv âŸ©
   (Î£ X ê‰ ğ“¤ Ì‡ , Î£ (h , e) ê‰ X â†ª âŸ¨ G âŸ© , Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h)                   â‰ƒâŸ¨ v âŸ©
   (Î£ X ê‰ ğ“¤ Ì‡ , Î£ h ê‰ (X â†’ âŸ¨ G âŸ©) , Î£ e ê‰ is-embedding h , Î£ Ï„ ê‰ T X , is-hom (X , Ï„) G h) â‰ƒâŸ¨ vi âŸ©
   (Î£ X ê‰ ğ“¤ Ì‡ , Î£ h ê‰ (X â†’ âŸ¨ G âŸ©) , Î£ Ï„ ê‰ T X , Î£ e ê‰ is-embedding h , is-hom (X , Ï„) G h) â‰ƒâŸ¨ vii âŸ©
   (Î£ X ê‰ ğ“¤ Ì‡ , Î£ Ï„ ê‰ T X , Î£ h ê‰ (X â†’ âŸ¨ G âŸ©) , is-embedding h Ã— is-hom (X , Ï„) G h)       â‰ƒâŸ¨ viii âŸ©
   (Î£ H ê‰ Group ğ“¤ , Î£ h ê‰ (âŸ¨ H âŸ© â†’ âŸ¨ G âŸ©) , is-embedding h Ã— is-hom H G h)                  â– 

      where
       open special-classifier-single-universe ğ“¤

       Ï† : Subtypes âŸ¨ G âŸ© â†’ ğ“Ÿ âŸ¨ G âŸ©
       Ï† = Ï‡-special is-prop âŸ¨ G âŸ©

       j : is-equiv Ï†
       j = Ï‡-special-is-equiv (ua ğ“¤) fe is-prop âŸ¨ G âŸ©

       i    = â‰ƒ-refl Subgroups
       ii   = â‰ƒ-sym (Î£-change-of-variable (Î» (A : ğ“Ÿ âŸ¨ G âŸ©) â†’ group-closed (_âˆˆ A)) Ï† j)
       iii  = Î£-assoc
       iv   = Î£-cong (Î» X â†’ Î£-cong (Î» (h , e) â†’ fiber-structure-lemma h e fe))
       v    = Î£-cong (Î» X â†’ Î£-assoc)
       vi   = Î£-cong (Î» X â†’ Î£-cong (Î» h â†’ Î£-flip))
       vii  = Î£-cong (Î» X â†’ Î£-flip)
       viii = â‰ƒ-sym Î£-assoc


  induced-group : Subgroups â†’ Group ğ“¤
  induced-group S = prâ‚ (âŒœ characterization-of-the-type-of-subgroups âŒ S)

\end{code}
