--------------------------------------------------------------------------------
Ettore Aldrovandi aldrovandi@math.fsu.edu
Keri D'Angelo kd349@cornell.edu

July 1, 2021
--------------------------------------------------------------------------------

\begin{code}

{-# OPTIONS --without-K --safe #-}


open import SpartanMLTT
open import Unit-Properties
open import UF-Base
open import UF-Subsingletons
open import UF-Equiv
open import UF-Retracts
open import UF-Embeddings
open import Groups

\end{code}

We define the kernel of a group homomorphism $f : A â†’ B$ as the fiber of f at the unit eâŸ¨ B âŸ©

\begin{code}

module Groups.Kernel where

module _ (A : Group ğ“¤) (B : Group ğ“¥) 
         (f : âŸ¨ A âŸ© â†’ âŸ¨ B âŸ©) (isf : is-hom A B f) where

  kernel : Group (ğ“¤ âŠ” ğ“¥)
  kernel = K , group-structure-k ,
               is-set-k ,
               assoc-k ,
               unit-k ,
               left-neutral-k ,
               (right-neutral-k , Î» x â†’ (inv-k x) , (inv-left-k x , inv-right-k x))
    where
      K : ğ“¤ âŠ” ğ“¥ Ì‡
      K = fiber f eâŸ¨ B âŸ©

      group-structure-k : group-structure K
      prâ‚ (group-structure-k (a , p) (a' , p')) = a Â·âŸ¨ A âŸ© a'
      prâ‚‚ (group-structure-k (a , p) (a' , p')) = f (a Â·âŸ¨ A âŸ© a')    â‰¡âŸ¨ isf âŸ©
                                                  f a Â·âŸ¨ B âŸ© f a'    â‰¡âŸ¨ ap (Î» v â†’ v Â·âŸ¨ B âŸ© f a') p âŸ©
                                                  eâŸ¨ B âŸ© Â·âŸ¨ B âŸ© f a'  â‰¡âŸ¨ unit-left B (f a') âŸ©
                                                  f a'              â‰¡âŸ¨ p' âŸ©
                                                  eâŸ¨ B âŸ© âˆ

      is-set-k : is-set K
      is-set-k = Î£-is-set (group-is-set A) Î» a â†’ props-are-sets (group-is-set B)


      assoc-k : associative group-structure-k
      assoc-k (a , p) (aâ‚ , pâ‚) (aâ‚‚ , pâ‚‚) = to-Î£-â‰¡ ((assoc A a aâ‚ aâ‚‚) , group-is-set B _ _)

      unit-k : K
      prâ‚ unit-k = eâŸ¨ A âŸ©
      prâ‚‚ unit-k = homs-preserve-unit A B f isf

      left-neutral-k : left-neutral unit-k group-structure-k
      left-neutral-k (a , p) = to-Î£-â‰¡ ((unit-left A a) , (group-is-set B _ _))

      right-neutral-k : right-neutral unit-k group-structure-k
      right-neutral-k (a , p) = to-Î£-â‰¡ ((unit-right A a) , (group-is-set B _ _))

      inv-k : K â†’ K
      prâ‚ (inv-k (a , p)) = inv A a
      prâ‚‚ (inv-k (a , p)) = f (inv A a)                      â‰¡âŸ¨ homs-preserve-invs A B f isf _ âŸ©
                            inv B (f a)                      â‰¡âŸ¨ ap (inv B) p âŸ©
                            inv B (unit B)                   â‰¡âŸ¨ (unit-right B _ ) â»Â¹ âŸ©
                            (inv B (unit B)) Â·âŸ¨ B âŸ© (unit B)  â‰¡âŸ¨ inv-left B _ âŸ©
                             unit B âˆ

      inv-left-k : (x : K) â†’ group-structure-k (inv-k x) x â‰¡ unit-k
      inv-left-k (a , p) = to-Î£-â‰¡ ((inv-left A a) , (group-is-set B _ _))

      inv-right-k : (x : K) â†’ group-structure-k x (inv-k x) â‰¡ unit-k
      inv-right-k (a , p) = to-Î£-â‰¡ ((inv-right A a) , (group-is-set B _ _))


  -- Canonical map from the kernel
  kernel-map : âŸ¨ kernel âŸ© â†’ âŸ¨ A âŸ©
  kernel-map = Î» { (a , p) â†’ a}

  -- Canonical map is a homomorphism
  kernel-map-is-hom : is-hom kernel A kernel-map
  kernel-map-is-hom = refl

  -- Canonical map is left cancellable  
  kernel-map-is-lc : left-cancellable kernel-map
  kernel-map-is-lc {a , p} {a' , p'} u = to-Î£-â‰¡ (u , (group-is-set B _ _))

  -- Canonical map is an embedding
  kernel-map-is-embedding : is-embedding kernel-map
  kernel-map-is-embedding = lc-maps-into-sets-are-embeddings kernel-map kernel-map-is-lc (group-is-set A)

  -- Kernel is normal
  kernel-is-normal : âŸ¨ A âŸ© â†’ âŸ¨ kernel âŸ© â†’ âŸ¨ kernel âŸ© 
  prâ‚ (kernel-is-normal x (a , p)) = (x Â·âŸ¨ A âŸ© a) Â·âŸ¨ A âŸ© (inv A x)
  prâ‚‚ (kernel-is-normal x (a , p)) = f ((x Â·âŸ¨ A âŸ© a) Â·âŸ¨ A âŸ© (inv A x))      â‰¡âŸ¨ isf âŸ©
                                     f (x Â·âŸ¨ A âŸ© a) Â·âŸ¨ B âŸ© f (inv A x)      â‰¡âŸ¨ ap (Î» v â†’ v Â·âŸ¨ B âŸ© f (inv A x)) isf âŸ©
                                     (f x Â·âŸ¨ B âŸ© f a) Â·âŸ¨ B âŸ© f (inv A x)    â‰¡âŸ¨ i âŸ©
                                     (f x Â·âŸ¨ B âŸ© eâŸ¨ B âŸ©) Â·âŸ¨ B âŸ© f (inv A x)  â‰¡âŸ¨ ii âŸ©
                                     f x Â·âŸ¨ B âŸ© f (inv A x)                â‰¡âŸ¨ iii âŸ©
                                     f x Â·âŸ¨ B âŸ© (inv B (f x))              â‰¡âŸ¨ inv-right B _ âŸ©
                                     eâŸ¨ B âŸ© âˆ
                        where
                         i   = ap (Î» v â†’ v Â·âŸ¨ B âŸ© f (inv A x)) (ap (Î» v â†’ f x Â·âŸ¨ B âŸ© v) p)
                         ii  = ap (Î» v â†’ v Â·âŸ¨ B âŸ© f (inv A x)) (unit-right B _)
                         iii = ap (Î» v â†’ f x Â·âŸ¨ B âŸ© v) (homs-preserve-invs A B f isf _)

\end{code}

The Universal property of the kernel is, as usual, that For any group
homomorphism $u : G â†’ A$ such that $f âˆ˜ u$ is the trivial map there exists a
(unique) homomorphism $G â†’ \Ker(f)$. The map itself does not require
extra axioms

\begin{code}


  kernel-universal-map : (G : Group ğ“¦) (u : âŸ¨ G âŸ© â†’ âŸ¨ A âŸ©) (isu : is-hom G A u) â†’
                         ((g : âŸ¨ G âŸ©) â†’ f (u g) â‰¡ eâŸ¨ B âŸ©) â†’ 
                         âŸ¨ G âŸ© â†’ âŸ¨ kernel âŸ©
  kernel-universal-map G u isu Î³ = Î» g â†’ (u g) , (Î³ g)

  kernel-universal-map-is-hom : (G : Group ğ“¦) (u : âŸ¨ G âŸ© â†’ âŸ¨ A âŸ©) (isu : is-hom G A u) â†’
                                (Î³ : (g : âŸ¨ G âŸ©) â†’ f (u g) â‰¡ eâŸ¨ B âŸ©) â†’
                                is-hom G kernel (kernel-universal-map G u isu Î³)
  kernel-universal-map-is-hom G u isu Î³ {x} {y} = to-Î£-â‰¡ (isu , group-is-set B _ _)


  {- 
     FIXME: to claim universality we must show that v : âŸ¨ G âŸ© â†’ âŸ¨ kernel âŸ©
            is unique.
            We should also prove it with equality kernel-map âˆ˜ v â‰¡ u using
            function extensionality
   -}

  kernel-universal : (G : Group ğ“¦) (u : âŸ¨ G âŸ© â†’ âŸ¨ A âŸ©) (isu : is-hom G A u) â†’
                     ((g : âŸ¨ G âŸ©) â†’ f (u g) â‰¡ eâŸ¨ B âŸ©) â†’ 
                     Î£ (Î» v â†’ kernel-map âˆ˜ v âˆ¼ u)
  kernel-universal G u isu Î³ = kernel-universal-map G u isu Î³ , Î» g â†’ refl

\end{code}
