Lane Biocini
21 January 2026

The twist automorphism of PSL(2,â„¤): the unique non-trivial element of
Out(PSL(2,â„¤)) â‰… â„¤/2â„¤. Geometrically, it corresponds to complex
conjugation on the upper half-plane action.

Algebraically, twist swaps r â†” rÂ² while fixing s:
  twist(s x) = s(twist x)
  twist(r x) = rÂ²(twist x)

Unlike the group inverse (which reverses word order), twist preserves
word order but inverts each generator individually. It is involutive
and respects multiplication, making it a group automorphism.

\begin{code}

{-# OPTIONS --safe --without-K --exact-split #-}

module Groups.ModularGroup.Twist where

open import MLTT.Spartan
open import UF.Base
open import UF.FunExt
open import Groups.ModularGroup.Type
open import Groups.ModularGroup.Base
open import Groups.ModularGroup.Properties
open import Groups.ModularGroup.Induction
open import Groups.ModularGroup.Multiplication

twist-Î· : S-edge â†’ PSL2Z
twist-Î¸ : R-edge â†’ PSL2Z

twist-Î· eâ‚€         = E
twist-Î· eâ‚         = S
twist-Î· (cross re) = s (twist-Î¸ re)

twist-Î¸ (r+ se) = rÂ² (twist-Î· se)
twist-Î¸ (r- se) = r (twist-Î· se)

twist : PSL2Z â†’ PSL2Z
twist (Î· se) = twist-Î· se
twist (Î¸ re) = twist-Î¸ re

twist-s : (x : PSL2Z) â†’ twist (s x) ï¼ s (twist x)
twist-s (Î· eâ‚€)    = refl
twist-s (Î· eâ‚)    = refl
twist-s (srâˆ™ se)  = sÂ² (r (r (twist-Î· se))) â»Â¹
twist-s (srÂ²âˆ™ se) = sÂ² (r (twist-Î· se)) â»Â¹
twist-s (râˆ™ se)   = refl
twist-s (rÂ²âˆ™ se)  = refl

twist-r : (x : PSL2Z) â†’ twist (r x) ï¼ rÂ² (twist x)
twist-r (Î· eâ‚€)    = refl
twist-r (Î· eâ‚)    = refl
twist-r (srâˆ™ se)  = refl
twist-r (srÂ²âˆ™ se) = refl
twist-r (râˆ™ se)   = rÂ³ (r (twist-Î· se)) â»Â¹
twist-r (rÂ²âˆ™ se)  = rÂ³ (twist-Î· se) â»Â¹

twist-rÂ² : (x : PSL2Z) â†’ twist (rÂ² x) ï¼ r (twist x)
twist-rÂ² (Î· eâ‚€)          = refl
twist-rÂ² (Î· eâ‚)          = refl
twist-rÂ² (srâˆ™ se)        = refl
twist-rÂ² (srÂ²âˆ™ se)       = refl
twist-rÂ² (râˆ™ se)         = rÂ³ (twist-Î· se) â»Â¹
twist-rÂ² (rÂ²âˆ™ eâ‚€)        = refl
twist-rÂ² (rÂ²âˆ™ eâ‚)        = refl
twist-rÂ² (rÂ²âˆ™ (cross x)) = refl

twist-involute : (x : PSL2Z) â†’ twist (twist x) ï¼ x
twist-involute (Î· eâ‚€)    = refl
twist-involute (Î· eâ‚)    = refl
twist-involute (srâˆ™ se)  =
  twist (s (rÂ² (twist-Î· se))) ï¼âŸ¨ twist-s (rÂ² (twist-Î· se)) âŸ©
  s (twist (rÂ² (twist-Î· se))) ï¼âŸ¨ ap s (twist-rÂ² (twist-Î· se)) âŸ©
  s (r (twist (twist-Î· se)))  ï¼âŸ¨ ap (s âˆ˜ r) (twist-involute (Î· se)) âŸ©
  s (r (Î· se))                ï¼âŸ¨ sr-Î· se âŸ©
  srâˆ™ se                      âˆ
twist-involute (srÂ²âˆ™ se) =
  twist (twist (srÂ²âˆ™ se))     ï¼âŸ¨ twist-s (r (twist-Î· se)) âŸ©
  s (twist (r (twist-Î· se)))  ï¼âŸ¨ ap s (twist-r (twist-Î· se)) âŸ©
  s (rÂ² (twist (twist-Î· se))) ï¼âŸ¨ ap (s âˆ˜ rÂ²) (twist-involute (Î· se)) âŸ©
  s (rÂ² (Î· se))               ï¼âŸ¨ srÂ²-Î· se âŸ©
  srÂ²âˆ™ se                     âˆ
twist-involute (râˆ™ se) =
  twist (rÂ² (twist-Î· se))     ï¼âŸ¨ twist-rÂ² (twist-Î· se) âŸ©
  r (twist (twist-Î· se))      ï¼âŸ¨ ap r (twist-involute (Î· se)) âŸ©
  r (Î· se)                    ï¼âŸ¨ r-Î· se âŸ©
  râˆ™ se                       âˆ
twist-involute (rÂ²âˆ™ se)  =
  twist (twist (rÂ²âˆ™ se))      ï¼âŸ¨ twist-r (twist-Î· se) âŸ©
  rÂ² (twist (twist-Î· se))     ï¼âŸ¨ ap rÂ² (twist-involute (Î· se)) âŸ©
  rÂ² (Î· se)                   ï¼âŸ¨ rÂ²-Î· se âŸ©
  rÂ²âˆ™ se                      âˆ

twist-product : (x y : PSL2Z) â†’ twist (x Â· y) ï¼ twist x Â· twist y
twist-product = PSL2Z-ind base-E base-S ind-s ind-r
 where
  base-E : (y : PSL2Z) â†’ twist (E Â· y) ï¼ twist E Â· twist y
  base-E y = refl

  base-S : (y : PSL2Z) â†’ twist (S Â· y) ï¼ twist S Â· twist y
  base-S y = twist-s y

  ind-s : (re : R-edge)
        â†’ ((y : PSL2Z) â†’ twist ((Î¸ re) Â· y) ï¼ twist (Î¸ re) Â· twist y)
        â†’ (y : PSL2Z) â†’ twist ((sâˆ™ re) Â· y) ï¼ twist (sâˆ™ re) Â· twist y
  ind-s re ih y =
   twist ((sâˆ™ re) Â· y)            ï¼âŸ¨ ap twist (Â·-s-left (Î¸ re) y) âŸ©
   twist (s ((Î¸ re) Â· y))         ï¼âŸ¨ twist-s ((Î¸ re) Â· y) âŸ©
   s (twist ((Î¸ re) Â· y))         ï¼âŸ¨ ap s (ih y) âŸ©
   s (twist (Î¸ re) Â· twist y)     ï¼âŸ¨ Â·-s-left (twist (Î¸ re)) (twist y) â»Â¹ âŸ©
   (s (twist (Î¸ re))) Â· twist y   ï¼âŸ¨ ap (_Â· twist y) (twist-s (Î¸ re) â»Â¹) âŸ©
   twist (sâˆ™ re) Â· twist y        âˆ

  ind-r : (d : R-sgn) (se : S-edge)
        â†’ ((y : PSL2Z) â†’ twist ((Î· se) Â· y) ï¼ twist (Î· se) Â· twist y)
        â†’ (y : PSL2Z) â†’ twist ((Ï d se) Â· y) ï¼ twist (Ï d se) Â· twist y
  ind-r cw se ih y =
   twist ((râˆ™ se) Â· y)            ï¼âŸ¨ ap (twist âˆ˜ (_Â· y)) (r-Î· se â»Â¹) âŸ©
   twist ((r (Î· se)) Â· y)         ï¼âŸ¨ ap twist (Â·-r-left (Î· se) y) âŸ©
   twist (r ((Î· se) Â· y))         ï¼âŸ¨ twist-r ((Î· se) Â· y) âŸ©
   rÂ² (twist ((Î· se) Â· y))        ï¼âŸ¨ ap rÂ² (ih y) âŸ©
   rÂ² (twist (Î· se) Â· twist y)    ï¼âŸ¨ Â·-rÂ²-left (twist (Î· se)) (twist y) â»Â¹ âŸ©
   (rÂ² (twist (Î· se))) Â· twist y  ï¼âŸ¨ ap (_Â· twist y) (twist-r (Î· se) â»Â¹) âŸ©
   twist (r (Î· se)) Â· twist y     ï¼âŸ¨ ap (_Â· twist y) (ap twist (r-Î· se)) âŸ©
   twist (râˆ™ se) Â· twist y        âˆ
  ind-r ccw se ih y =
   twist ((rÂ²âˆ™ se) Â· y)           ï¼âŸ¨ ap (twist âˆ˜ (_Â· y)) (rÂ²-Î· se â»Â¹) âŸ©
   twist ((rÂ² (Î· se)) Â· y)        ï¼âŸ¨ ap twist (Â·-rÂ²-left (Î· se) y) âŸ©
   twist (rÂ² ((Î· se) Â· y))        ï¼âŸ¨ twist-rÂ² ((Î· se) Â· y) âŸ©
   r (twist ((Î· se) Â· y))         ï¼âŸ¨ ap r (ih y) âŸ©
   r (twist (Î· se) Â· twist y)     ï¼âŸ¨ Â·-r-left (twist (Î· se)) (twist y) â»Â¹ âŸ©
   (r (twist (Î· se))) Â· twist y   ï¼âŸ¨ ap (_Â· twist y) (twist-rÂ² (Î· se) â»Â¹) âŸ©
   twist (rÂ² (Î· se)) Â· twist y    ï¼âŸ¨ ap (_Â· twist y) (ap twist (rÂ²-Î· se)) âŸ©
   twist (rÂ²âˆ™ se) Â· twist y       âˆ

module _ (fe : funext ğ“¤â‚€ ğ“¤â‚€) where
 twist-gen-iter : twist ï¼ PSL2Z-gen-iter E s rÂ²
 twist-gen-iter = dfunext fe lemma
  where
   iter : PSL2Z â†’ PSL2Z
   iter = PSL2Z-gen-iter E s rÂ²

   lemma : (x : PSL2Z) â†’ twist x ï¼ iter x
   lemma =
    PSL2Z-Î· s rÂ² twist iter refl twist-s PSL2Z-gen-iter-s twist-r
     PSL2Z-gen-iter-r

 twist-iter : twist ï¼ PSL2Z-iter E S s rÂ² r
 twist-iter = dfunext fe lemma
  where
   iter : PSL2Z â†’ PSL2Z
   iter = PSL2Z-iter E S s rÂ² r

   lemma-Î· : (se : S-edge) â†’ twist-Î· se ï¼ iter (Î· se)
   lemma-Î¸ : (re : R-edge) â†’ twist-Î¸ re ï¼ iter (Î¸ re)

   lemma-Î· eâ‚€         = refl
   lemma-Î· eâ‚         = refl
   lemma-Î· (cross re) = ap s (lemma-Î¸ re)

   lemma-Î¸ (r+ se) = ap rÂ² (lemma-Î· se)
   lemma-Î¸ (r- se) = ap r (lemma-Î· se)

   lemma : (x : PSL2Z) â†’ twist x ï¼ iter x
   lemma (Î· se) = lemma-Î· se
   lemma (Î¸ re) = lemma-Î¸ re

\end{code}
