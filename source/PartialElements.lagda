Martin Escardo 25th October 2018.

The type of partial elements of a type (or lifting).
(Cf. my former student Cory Knapp's PhD thesis).

We focus, to begin with, on the fact that the canonical map into the
lifting is an embedding, which is easy for sets, but seems less easy for
arbitrary types.

\begin{code}

{-# OPTIONS --without-K --exact-split #-}

open import SpartanMLTT

module PartialElements where

open import UF-Base
open import UF-Subsingletons hiding (âŠ¥)
open import UF-Embedding
open import UF-FunExt
open import UF-Equiv
open import UF-Subsingletons-FunExt
open import UF-Retracts

\end{code}

The domain of definition of a partial element is taken to be in an
arbitrary universe ğ“£.

\begin{code}

module _ (ğ“£ : Universe) where

\end{code}

We discuss the type ğ“› X of partial elements of a type X, also called
the lifting of X.

\begin{code}

 ğ“› : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“£ âº Ì‡
 ğ“› X = Î£ \(P : ğ“£ Ì‡) â†’ is-prop P Ã— (P â†’ X)

\end{code}

The "total" elements of ğ“› X:

\begin{code}

 Î· : {X : ğ“¤ Ì‡} â†’ X â†’ ğ“› X
 Î· x = ğŸ™ , ğŸ™-is-prop , (Î» _ â†’ x)

\end{code}

Its "undefined" element:

\begin{code}

 âŠ¥ : {X : ğ“¤ Ì‡} â†’ ğ“› X
 âŠ¥ = ğŸ˜ , ğŸ˜-is-prop , unique-from-ğŸ˜

\end{code}

Our strategy to show that Î· is an embedding (has subsingleton fibers)
is to exhibit it as the composite of two embeddings (the first of
which is actually an equivalence).

\begin{code}

 ğ“š : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“£ âº Ì‡
 ğ“š X = Î£ \(P : ğ“£ Ì‡) â†’ is-singleton P Ã— (P â†’ X)

 Îº : {X : ğ“¤ Ì‡} â†’ X â†’ ğ“š X
 Îº x = ğŸ™ , ğŸ™-is-singleton , (Î» _ â†’ x)

 Î¶ : (X : ğ“¤ Ì‡) (P : ğ“£ Ì‡) â†’ is-singleton P Ã— (P â†’ X) â†’ is-prop P Ã— (P â†’ X)
 Î¶ X P (i , Ï†) = singletons-are-props i , Ï†

 ğ“š-to-ğ“› : (X : ğ“¤ Ì‡) â†’ ğ“š X â†’ ğ“› X
 ğ“š-to-ğ“› X = NatÎ£ (Î¶ X)

 Î·-composite : funext ğ“£ ğ“£ â†’ funext ğ“¤ (ğ“£ âº âŠ” ğ“¤)
             â†’ {X : ğ“¤ Ì‡} â†’ Î· â‰¡ ğ“š-to-ğ“› X âˆ˜ Îº
 Î·-composite fe fe' {X} = dfunext fe' h
  where
   h : (x : X) â†’ (ğŸ™ , ğŸ™-is-prop ,                             Î» _ â†’ x)
               â‰¡ (ğŸ™ , singletons-are-props (ğŸ™-is-singleton) , Î» _ â†’ x)
   h x = to-Î£-â‰¡ (refl , to-Ã—-â‰¡ (being-a-prop-is-a-prop fe _ _) refl)

\end{code}

The fact that ğ“š-to-ğ“› is an embedding can be proved by obtaining it as
a combination of maps that we already know to be embeddings, using
Ã—-embedding, maps-of-props-are-embeddings, id-is-embedding, and
NatÎ£-embedding.:

\begin{code}

 Î¶-is-embedding : funext ğ“£ ğ“£ â†’ (X : ğ“¤ Ì‡) (P : ğ“£ Ì‡) â†’ is-embedding (Î¶ X P)
 Î¶-is-embedding fe X P = Ã—-embedding
                           singletons-are-props
                           id
                           (maps-of-props-are-embeddings
                              singletons-are-props
                              (is-singleton-is-a-prop fe)
                              (being-a-prop-is-a-prop fe))
                           id-is-embedding

 ğ“š-to-ğ“›-is-embedding : funext ğ“£ ğ“£
                     â†’ (X : ğ“¤ Ì‡) â†’ is-embedding (ğ“š-to-ğ“› X)
 ğ“š-to-ğ“›-is-embedding fe X = NatÎ£-is-embedding
                                  (Î» P â†’ is-singleton P Ã— (P â†’ X))
                                  (Î» P â†’ is-prop P Ã— (P â†’ X))
                                  (Î¶ X)
                                  (Î¶-is-embedding fe X)
\end{code}

That Îº is an equivalence corresponds to the fact that the lifting of a
type X with respect to the dominance "is-singleton" is equivalent to X
itself.

\begin{code}

 Îº-is-equiv : propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤
            â†’ {X : ğ“¤ Ì‡} â†’ is-equiv (Îº {ğ“¤} {X})
 Îº-is-equiv {ğ“¤} pe fe fe' {X} = qinvs-are-equivs Îº (Î½ , (Î½Îº , ÎºÎ½))
  where
   Î½ : {X : ğ“¤ Ì‡} â†’ ğ“š X â†’ X
   Î½ (P , i , Ï†) = Ï† (is-singleton-pointed i)
   Î½Îº : {X : ğ“¤ Ì‡} (x : X) â†’ Î½ (Îº x) â‰¡ x
   Î½Îº x = refl
   ÎºÎ½ : (m : ğ“š X) â†’ Îº (Î½ m) â‰¡ m
   ÎºÎ½ (P , i , Ï†) = u
    where
     t : ğŸ™ â‰¡ P
     t = pe ğŸ™-is-prop (singletons-are-props i) (Î» _ â†’ is-singleton-pointed i) unique-to-ğŸ™
     s : (t : ğŸ™ â‰¡ P)
       â†’ transport (Î» - â†’ is-singleton - Ã— (- â†’ X)) t (ğŸ™-is-singleton , (Î» _ â†’ Ï† (is-singleton-pointed i)))
       â‰¡ i , Ï†
     s refl = to-Ã—-â‰¡ a b
       where
        a : ğŸ™-is-singleton â‰¡ i
        a = (singletons-are-props (pointed-props-are-singletons ğŸ™-is-singleton (is-singleton-is-a-prop fe))
                                  ğŸ™-is-singleton i)
        b : (Î» x â†’ Ï† (is-singleton-pointed i)) â‰¡ Ï†
        b = dfunext fe' (Î» x â†’ ap Ï† (ğŸ™-is-prop (is-singleton-pointed i) x))
     u : ğŸ™ , ğŸ™-is-singleton , (Î» _ â†’ Ï† (is-singleton-pointed i)) â‰¡ P , i , Ï†
     u = to-Î£-â‰¡ (t , s t)

 Îº-is-embedding : propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤
                â†’ {X : ğ“¤ Ì‡} â†’ is-embedding (Îº {ğ“¤} {X})
 Îº-is-embedding pe fe fe' = equivs-are-embeddings Îº (Îº-is-equiv pe fe fe')

\end{code}

Finally, Î· is an embedding because it is equal to the composition of
two embeddings:

\begin{code}

 Î·-is-embedding : propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤ â†’ funext ğ“¤ (ğ“£ âº âŠ” ğ“¤)
                â†’ {X : ğ“¤ Ì‡} â†’ is-embedding (Î· {ğ“¤} {X})
 Î·-is-embedding pe fe fe' fe'' {X} =
   back-transport
    is-embedding
    (Î·-composite fe fe'')
    (comp-embedding (Îº-is-embedding pe fe fe') (ğ“š-to-ğ“›-is-embedding fe X))
\end{code}

We now give meaningful names to the projections:

\begin{code}

 is-defined : {X : ğ“¤ Ì‡} â†’ ğ“› X â†’ ğ“£ Ì‡
 is-defined (P , i , Ï†) = P

 being-defined-is-a-prop : {X : ğ“¤ Ì‡} (l : ğ“›  X) â†’ is-prop (is-defined l)
 being-defined-is-a-prop (P , i , Ï†) = i

 value : {X : ğ“¤ Ì‡} (l : ğ“›  X) â†’ is-defined l â†’ X
 value (P , i , Ï†) = Ï†

\end{code}

Next we show that for any l : ğ“› X,

 fiber Î· l â‰ƒ is-defined l,

using the fact that the fiber is a proposition by virtue of Î· being an
embedding.

For this purpose, it is convenient to work with the information
"order" on ğ“› X, which will really be a (partial) order only when X is
a set:

\begin{code}

 _âŠ‘_ : {X : ğ“¤ Ì‡} â†’ ğ“› X â†’ ğ“› X â†’ ğ“¤ âŠ” ğ“£ Ì‡
 l âŠ‘ m = Î£ \(f : is-defined l â†’ is-defined m) â†’ (d : is-defined l) â†’ value l d â‰¡ value m (f d)

\end{code}

If X is a set, then ğ“› X is a poset under _âŠ‘_ (TODO). In the general
case, it should be some sort of univalent âˆ-category with
hom-âˆ-groupoids x âŠ‘ y.

\begin{code}

 âŠ‘-id : {X : ğ“¤ Ì‡} (l : ğ“› X) â†’ l âŠ‘ l
 âŠ‘-id (P , i , Ï†) = id , (Î» x â†’ refl)

 âŠ‘-id' : {X : ğ“¤ Ì‡} (l m : ğ“› X) â†’ l â‰¡ m â†’ l âŠ‘ m
 âŠ‘-id' l m p = transport (Î» - â†’ l âŠ‘ -) p (âŠ‘-id l)

 âŠ‘-âˆ˜ : {X : ğ“¤ Ì‡} (l m n : ğ“› X)
     â†’ l âŠ‘ m â†’ m âŠ‘ n â†’ l âŠ‘ n
 âŠ‘-âˆ˜ l m n (f , Î´) (g , Îµ) = g âˆ˜ f , (Î» d â†’ Î´ d âˆ™ Îµ (f d))

 âŠ‘-anti : propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤
        â†’ {X : ğ“¤ Ì‡} {l m : ğ“› X}
        â†’ (l âŠ‘ m) Ã— (m âŠ‘ l) â†’ l â‰¡ m
 âŠ‘-anti pe fe fe' {X} {Q , j , Î³} {P , i , Ï†} ((f , Î´) , (g , Îµ)) = e
  where
   a : Q â‰¡ P
   a = pe j i f g
   b : Idtofun (a â»Â¹) â‰¡ g
   b = dfunext fe (Î» p â†’ j (Idtofun (a â»Â¹) p) (g p))
   c : transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)
     â‰¡ (transport is-prop a j , transport (Î» - â†’ - â†’ X) a Î³)
   c = transport-Ã— is-prop (Î» - â†’ - â†’ X) a
   d : prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)) â‰¡ Ï†
   d = prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³))
             â‰¡âŸ¨ ap prâ‚‚ c âŸ©
       transport (Î» - â†’ - â†’ X) a Î³
             â‰¡âŸ¨ transport-is-pre-comp a Î³ âŸ©
       Î³ âˆ˜ Idtofun (a â»Â¹)
             â‰¡âŸ¨ ap (Î» - â†’ Î³ âˆ˜ -) b âŸ©
       Î³ âˆ˜ g
             â‰¡âŸ¨ (dfunext fe' Îµ)â»Â¹ âŸ©
       Ï†     âˆ
   e : Q , j , Î³ â‰¡ P , i , Ï†
   e = to-Î£-â‰¡ (a , to-Ã—-â‰¡ (being-a-prop-is-a-prop fe _ i) d)

\end{code}

We haven't used Î´ in the above proof. But we could use it instead of
Îµ, by defining Îµ' from Î´ as follows, and then using (dfunext fe' Îµ')
instead of (dfunext fe' Îµ)â»Â¹ in the above proof:

\begin{code}

   Îµ' : (p : P) â†’ Î³ (g p) â‰¡ Ï† p
   Îµ' p = Î´ (g p) âˆ™ ap Ï† (i (f (g p)) p)

\end{code}

We can now establish the promised fact:

\begin{code}

 Î·-fiber-same-as-is-defined :
     propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤ â†’ funext ğ“¤ (ğ“£ âº âŠ” ğ“¤)
   â†’ {X : ğ“¤ Ì‡} (l : ğ“› X)
   â†’ (Î£ \(x : X) â†’ Î· x â‰¡ l) â‰ƒ is-defined l
 Î·-fiber-same-as-is-defined pe fe fe' fe'' {X} l = f l , ((g l , fg) , (g l , gf))
  where
   f : (l : ğ“› X) â†’ fiber Î· l â†’ is-defined l
   f (.ğŸ™ , .ğŸ™-is-prop , .(Î» _ â†’ x)) (x , refl) = *
   g : (l : ğ“› X) â†’ is-defined l â†’ fiber Î· l
   g (P , i , Ï†) p = Ï† p , âŠ‘-anti pe fe fe' (a , b)
    where
     a : Î· (Ï† p) âŠ‘ (P , i , Ï†)
     a = (Î» _ â†’ p) , (Î» _ â†’ refl)
     b : (P , i , Ï†) âŠ‘ Î· (Ï† p)
     b = (Î» _ â†’ *) , (Î» q â†’ ap Ï† (i q p))
   fg : (d : is-defined l) â†’ f l (g l d) â‰¡ d
   fg d = being-defined-is-a-prop l (f l (g l d)) d
   gf : (z : fiber Î· l) â†’ g l (f l z) â‰¡ z
   gf z = Î·-is-embedding pe fe fe' fe'' l (g l (f l z)) z

\end{code}

They can't be equal, in the absence of cumulativity (and propositional
resizing), as the lhs lives in a universe higher than the rhs, and
equality is well-typed only for elements of the same type (here of the
same universe). This can be seen by adding type annotations to the
formulation of the above equivalence:

\begin{code}

 private
  Î·-fiber-same-as-is-defined' :
       propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤ â†’ funext ğ“¤ (ğ“£ âº âŠ” ğ“¤)
    â†’ {X : ğ“¤ Ì‡} (l : ğ“› X)
    â†’ (fiber Î· l âˆ¶ ğ“£ âº âŠ” ğ“¤ Ì‡) â‰ƒ (is-defined l âˆ¶ ğ“£ Ì‡)
  Î·-fiber-same-as-is-defined' = Î·-fiber-same-as-is-defined

\end{code}

For no choice of universes ğ“¤ and ğ“£ can we have ğ“£ ' âŠ” ğ“¤ to coincide
with ğ“£. However, for dominances other than is-prop, it is possible to
have the equality beyween the fiber of l and the definedness of l.

TODO: Could the map (anti l m) be an equivalence? No. We should
instead have an equivalence (l âŠ‘ m) Ã— (m âŠ‘ l) â†’ (l â‰¡ m) Ã— (l â‰¡ m),
reflecting the fact that there were two candidate proofs for the
equality, as discussed above.

But it is probably better to have the following version of âŠ‘-anti,
which should be an equivalence for each l and m:

\begin{code}

 âŠ‘-anti' : propext ğ“£ â†’ funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤
        â†’ {X : ğ“¤ Ì‡} (l m : ğ“› X) â†’ (l âŠ‘ m) Ã— (is-defined m â†’ is-defined l) â†’ l â‰¡ m
 âŠ‘-anti' pe fe fe' {X} (Q , j , Î³) (P , i , Ï†) ((f , Î´) , g) = e
  where
   Îµ' : (p : P) â†’ Î³ (g p) â‰¡ Ï† p
   Îµ' p = Î´ (g p) âˆ™ ap Ï† (i (f (g p)) p)
   a : Q â‰¡ P
   a = pe j i f g
   b : Idtofun (a â»Â¹) â‰¡ g
   b = dfunext fe (Î» p â†’ j (Idtofun (a â»Â¹) p) (g p))
   c : transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)
     â‰¡ (transport is-prop a j , transport (Î» - â†’ - â†’ X) a Î³)
   c = transport-Ã— is-prop (Î» - â†’ - â†’ X) a
   d : prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)) â‰¡ Ï†
   d = prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³))
             â‰¡âŸ¨ ap prâ‚‚ c âŸ©
       transport (Î» - â†’ - â†’ X) a Î³
             â‰¡âŸ¨ transport-is-pre-comp a Î³ âŸ©
       Î³ âˆ˜ Idtofun (a â»Â¹)
             â‰¡âŸ¨ ap (Î» - â†’ Î³ âˆ˜ -) b âŸ©
       Î³ âˆ˜ g
             â‰¡âŸ¨ dfunext fe' Îµ' âŸ©
       Ï†     âˆ
   e : Q , j , Î³ â‰¡ P , i , Ï†
   e = to-Î£-â‰¡ (a , to-Ã—-â‰¡ (being-a-prop-is-a-prop fe _ i) d)


 âŠ‘-anti'-inverse :  {X : ğ“¤ Ì‡} (l m : ğ“› X)
                 â†’ l â‰¡ m â†’ (l âŠ‘ m) Ã— (is-defined m â†’ is-defined l)
 âŠ‘-anti'-inverse l .l refl = âŠ‘-id l , id

 Î·-maximal : {X : ğ“¤ Ì‡} (x : X) (l : ğ“› X) â†’ Î· x âŠ‘ l â†’ l âŠ‘ Î· x
 Î·-maximal x (P , i , Î³) (f , Î´) = (Î» p â†’ *) , (Î» p â†’ ap Î³ (i p (f *)) âˆ™ (Î´ *)â»Â¹)

 âŠ¥-least : {X : ğ“¤ Ì‡} (x : X) â†’ âŠ¥ âŠ‘ Î· x
 âŠ¥-least x = unique-from-ğŸ˜ , Î» z â†’ unique-from-ğŸ˜ z


 Î·-â‰¡-gives-âŠ‘ : {X : ğ“¤ Ì‡} {x y : X} â†’ x â‰¡ y â†’ Î· x âŠ‘ Î· y
 Î·-â‰¡-gives-âŠ‘ {ğ“¤} {X} {x} {y} p = id , (Î» d â†’ p)

 Î·-âŠ‘-gives-â‰¡ : {X : ğ“¤ Ì‡} {x y : X} â†’ Î· x âŠ‘ Î· y â†’ x â‰¡ y
 Î·-âŠ‘-gives-â‰¡ (f , Î´) = Î´ *

 Î·-â‰¡-gives-âŠ‘-is-equiv : funext ğ“£ ğ“£ â†’ funext ğ“£ ğ“¤
                      â†’ {X : ğ“¤ Ì‡} {x y : X}
                      â†’ is-equiv (Î·-â‰¡-gives-âŠ‘ {ğ“¤} {X} {x} {y})
 Î·-â‰¡-gives-âŠ‘-is-equiv {ğ“¤} fe fe' {X} {x} {y} = qinvs-are-equivs Î·-â‰¡-gives-âŠ‘ (Î·-âŠ‘-gives-â‰¡ , Î± , Î²)
  where
   Î± : {x y : X} (p : x â‰¡ y) â†’  Î·-âŠ‘-gives-â‰¡ (Î·-â‰¡-gives-âŠ‘ p) â‰¡ p
   Î± p = refl

   Î² : {x y : X} (q : Î· x âŠ‘ Î· y) â†’ Î·-â‰¡-gives-âŠ‘ (Î·-âŠ‘-gives-â‰¡ q) â‰¡ q
   Î² (f , Î´) = to-Ã—-â‰¡ (dfunext fe (Î» x â†’ ğŸ™-is-prop x (f x)))
                      (dfunext fe' (Î» x â†’ ap Î´ (ğŸ™-is-prop * x)))


{- TODO
âŠ‘-directed-complete : {X I : ğ“¤ Ì‡} (l : I â†’ ğ“› X)
                    â†’ ((i j : I) â†’ Î£ \(k : I) â†’ (l i âŠ‘ l k) Ã— (l j âŠ‘ l k))
                    â†’ Î£ \(m : ğ“› X) â†’ ((i : I) â†’ l i âŠ‘ m)
                                   Ã— ((n : ğ“› X) â†’ ((i : I) â†’ l i âŠ‘ n) â†’ m âŠ‘ n)
âŠ‘-directed-complete = {!!}
-}

\end{code}

We should also do least fixed points of continuous maps.

Added 7th November 2018. 'Monad' structure on ğ“›.

\begin{code}

ğ“›-lift : âˆ€ ğ“£ {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ ğ“› ğ“£ Y) â†’ (ğ“› ğ“£ X â†’ ğ“› ğ“£ Y)
ğ“›-lift ğ“£ f (P , i , Ï†) = (Î£ \(p : P) â†’ is-defined ğ“£ (f (Ï† p))) ,
                          Î£-is-prop i (Î» p â†’ being-defined-is-a-prop ğ“£ (f (Ï† p))) ,
                          Î» Ïƒ â†’ value ğ“£ (f (Ï† (prâ‚ Ïƒ))) (prâ‚‚ Ïƒ)

ğ“›- : âˆ€ ğ“£ {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“› ğ“£ X â†’ ğ“› ğ“£ Y
ğ“›- ğ“£ f (P , i , Ï†) = P , i , f âˆ˜ Ï†

ğ“›-id : âˆ€ ğ“£ {X : ğ“¤ Ì‡} â†’ ğ“›- ğ“£ id â‰¡ id
ğ“›-id {ğ“¤} ğ“£ {X} = refl {ğ“¤ âŠ” (ğ“£ âº)} {ğ“› ğ“£ X â†’ ğ“› ğ“£ X}

ğ“›-âˆ˜ : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {Z : ğ“¦ Ì‡} (f : X â†’ Y) (g : Y â†’ Z)
    â†’ ğ“›- ğ“£ (g âˆ˜ f) â‰¡ ğ“›- ğ“£ g âˆ˜ ğ“›- ğ“£ f
ğ“›-âˆ˜ f g = refl

Î·-natural : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y) â†’ Î· ğ“£ âˆ˜ f â‰¡ ğ“›- ğ“£ f âˆ˜ Î· ğ“£
Î·-natural f = refl

Î¼ : (ğ“£ : Universe) {X : ğ“¤ Ì‡} â†’ ğ“› ğ“£ (ğ“› ğ“£ X) â†’ ğ“› ğ“£ X
Î¼ {ğ“¤} ğ“£ {X} = ğ“›-lift ğ“£ id

ğ“›* : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y) â†’ is-embedding f â†’ ğ“› ğ“£ Y â†’ ğ“› (ğ“¤ âŠ” ğ“¥ âŠ” ğ“£) X
ğ“›* f e (Q , j , Î³) = (Î£ \(q : Q) â†’ fiber f (Î³ q)) ,
                      Î£-is-prop j (e âˆ˜ Î³) ,
                      Î» p â†’ prâ‚ (prâ‚‚ p)

Î¼* : (ğ“£ ğ“£' : Universe) {X : ğ“¤ Ì‡} â†’ funext ğ“£ ğ“£ â†’ funext ğ“£' ğ“£' â†’ funext ğ“£' ğ“¤ â†’ funext ğ“¤ (ğ“¤ âŠ” (ğ“£' âº)) â†’ propext ğ“£'
  â†’ ğ“› ğ“£ (ğ“› ğ“£' X) â†’ ğ“› (ğ“¤ âŠ” ğ“£ âŠ” (ğ“£' âº)) X
Î¼* {ğ“¤} ğ“£ ğ“£' {X} fe fe' fe'' fe''' pe = ğ“›* (Î· ğ“£') (Î·-is-embedding ğ“£' pe fe' fe'' fe''')

{-
Î¼-natural : (ğ“£ ğ“£' : Universe) (fe : funext ğ“£ ğ“£) (fe' : funext ğ“£' ğ“£') (fe'' : funext ğ“£' ğ“¤) (fe''' : funext ğ“¤ (ğ“¤ âŠ” (ğ“£' âº))) (pe : propext ğ“£')
          â†’ {X : ğ“¤ Ì‡} {Y : ğ“¤ Ì‡} (f : X â†’ Y) â†’ ğ“›- (ğ“¤ âŠ” ğ“£ âŠ” (ğ“£' âº)) f âˆ˜ Î¼ ğ“£ ğ“£' fe fe' fe'' fe''' pe
                                            â‰¡ Î¼ ğ“£ ğ“£' fe fe' fe'' fe''' pe âˆ˜ ğ“›- ğ“£ (ğ“›- ğ“£' f)
Î¼-natural ğ“£ ğ“£' fe fe' fe'' fe''' pe f = {!refl!}
-}

\end{code}

Lift monad to be continued in due course.

Added 8th November 2018.

\begin{code}

pus : (ğ“£ : Universe) {X : ğ“¤ Ì‡} {P : ğ“£ Ì‡} â†’ ğ“› ğ“£ X â†’ (P â†’ ğ“› ğ“£ X)
pus ğ“£ l p = l

sup : (ğ“£ : Universe) {X : ğ“¤ Ì‡} {P : ğ“£ Ì‡} â†’ is-prop P â†’ (P â†’ ğ“› ğ“£ X) â†’ ğ“› ğ“£ X
sup ğ“£ {X} {P} i Ï† = Î¼ ğ“£ (P , i , Ï†)

{-
sup-adj : (ğ“£ : Universe) {X : ğ“¤ Ì‡} (P : ğ“£ Ì‡) (i : is-prop P) (Ï† : P â†’ ğ“› ğ“£ X) (l : ğ“› ğ“£ X)
        â†’ (_âŠ‘_ ğ“£ (sup ğ“£ i Ï†) l) â‰ƒ ((p : P) â†’ _âŠ‘_ ğ“£ (Ï† p) l)
sup-adj = {!!}

sup-reflective : (ğ“£ : Universe) {X : ğ“¤ Ì‡} (P : ğ“£ Ì‡) (i : is-prop P) (Ï† : P â†’ ğ“› ğ“£ X) (l : ğ“› ğ“£ X)
               â†’ (p : P) â†’ Ï† p â‰¡ sup ğ“£ i Ï†
sup-reflective ğ“£ P i Ï† l p = {!!}
-}

\end{code}

This has a connection with injectivity.
