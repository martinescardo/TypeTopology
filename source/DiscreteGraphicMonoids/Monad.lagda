Martin Escardo and Paulo Oliva, April 2024

Lists without repetitions over a discrete types form a monad, as a
corollary of the fact that lists without repetitions over a discrete
type form the free discrete graphic monoid.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module DiscreteGraphicMonoids.Monad
        (fe : Fun-Ext)
       where

open import DiscreteGraphicMonoids.Free fe
open import DiscreteGraphicMonoids.LWRDGM fe
open import DiscreteGraphicMonoids.ListsWithoutRepetitions fe
open import DiscreteGraphicMonoids.Type
open import MLTT.Spartan
open import UF.DiscreteAndSeparated

module _ {ğ“¤ ğ“¥ : Universe}
         {X : ğ“¤ Ì‡ }
         {{X-is-discrete' : is-discrete' X}}
         {Y : ğ“¥ Ì‡ }
         {{Y-is-discrete' : is-discrete' Y}}
      where

 extâ» : (X â†’ Listâ» Y) â†’ Listâ» X â†’ Listâ» Y
 extâ» = extension (LDGM Y)

 unitâ» : (f : X â†’ Listâ» Y) (x : X) â†’ extâ» f (Î·â» x) ï¼ f x
 unitâ» = triangle (LDGM Y)

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         {{X-is-discrete' : is-discrete' X}}
       where

 extâ»-Î·â» : extâ» Î·â» âˆ¼ ğ‘–ğ‘‘ (Listâ» X)
 extâ»-Î·â» ğ”ğ“¼ = uniqueness (LDGM X)
               Î·â»
               id
               (id-is-hom (LDGM X))
               (Î» _ â†’ refl) ğ”ğ“¼

module _ {ğ“¤ ğ“¥ ğ“¦ : Universe}
         {X : ğ“¤ Ì‡ }
         {{X-is-discrete' : is-discrete' X}}
         {Y : ğ“¥ Ì‡ }
         {{Y-is-discrete' : is-discrete' Y}}
         {Z : ğ“¦ Ì‡ }
         {{Z-is-discrete' : is-discrete' Z}}
       where

 assocâ» : (g : Y â†’ Listâ» Z) (f : X â†’ Listâ» Y) (ğ”ğ“¼ : Listâ» X)
        â†’ extâ» (Î» x â†’ extâ» g (f x)) ğ”ğ“¼ ï¼ extâ» g (extâ» f ğ”ğ“¼)
 assocâ» g f ğ”ğ“¼ = III
  where
   I : is-hom (LDGM X) (LDGM Z) (Î» x â†’ extâ» g (extâ» f x))
   I = Iâ‚ , Iâ‚‚
    where
     Iâ‚ : extâ» g (extâ» f []â») ï¼ []â»
     Iâ‚ = refl

     Iâ‚‚ : (ğ”ğ“¼ ğ”‚ğ“¼ : âŸ¨ LDGM X âŸ©)
        â†’ extâ» g (extâ» f (ğ”ğ“¼ Â· ğ”‚ğ“¼)) ï¼ extâ» g (extâ» f ğ”ğ“¼) Â· extâ» g (extâ» f ğ”‚ğ“¼)
     Iâ‚‚ = homs-preserve-mul (LDGM X) (LDGM Z) ((Î» x â†’ extâ» g (extâ» f x)))
           (âˆ˜-is-hom (LDGM X) (LDGM Y) (LDGM Z)
             (extâ» f)
             (extâ» g)
             (extension-is-hom (LDGM Y) f)
             (extension-is-hom (LDGM Z) g))

   II : (Î» x â†’ extâ» g (extâ» f x)) âˆ˜ Î·â» âˆ¼ (Î» x â†’ extâ» g (f x))
   II = Î» x â†’ ((Î» ğ”ğ“¼ â†’ extâ» g (extâ» f ğ”ğ“¼)) âˆ˜ Î·â») x ï¼âŸ¨ refl âŸ©
              extâ» g (extâ» f (Î·â» x))               ï¼âŸ¨ IIâ‚€ x âŸ©
              extâ» g (f x)                         âˆ
               where
                IIâ‚€ = Î» x â†’ ap (extâ» g) (triangle (LDGM Y) f x)

   III : extâ» (Î» x â†’ extâ» g (f x)) ğ”ğ“¼ ï¼ extâ» g (extâ» f ğ”ğ“¼)
   III = uniqueness (LDGM Z)
          (Î» x â†’ extâ» g (f x))
          (extâ» g âˆ˜ extâ» f)
          I
          II
          ğ”ğ“¼

\end{code}
