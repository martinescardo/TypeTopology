By Martin Escardo, 2 September 2011.

Modified in December 2011 assuming the axiom of extensionality (which
is not used directly in this module, but instead in
GenericConvergentSequence).

We prove that the generic convergent sequence â„•âˆ is searchable, which
amounts to Theorem-3Â·6 of the paper

   http://www.cs.bham.ac.uk/~mhe/papers/omniscient.pdf,
   http://www.cs.bham.ac.uk/~mhe/.talks/dagstuhl2011/omniscient.pdf

and conclude as a corollary that it is searchable and satisfies the
principle of omniscience.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

\end{code}


(Continuity axioms and the fan principle are not assumed.)

\begin{code}

open import SpartanMLTT
open import UF-FunExt
open import UF-PropTrunc
open import GenericConvergentSequence
open import SearchableTypes

module ConvergentSequenceSearchable (fe : funext Uâ‚€ Uâ‚€) where

\end{code}

This is the main theorem proved in this module:

\begin{code}

â„•âˆ-searchable : searchable â„•âˆ
â„•âˆ-searchable p = a , Lemma
 where
  Î± : â„• â†’ ğŸš
  Î± 0       = p(under 0)
  Î±(succ n) = minğŸš (Î± n) (p(under(succ n)))

  a : â„•âˆ
  a = (Î± , Î» i â†’ Lemma[minabâ‰¤â‚‚a])

  Daggerâ‚€ : (n : â„•) â†’ a â‰¡ under n â†’ p(under n) â‰¡ â‚€
  Daggerâ‚€ 0 r =  ap (Î» - â†’ incl - 0) r
  Daggerâ‚€ (succ n) r = w â»Â¹ âˆ™ t âˆ™ under-diagonalâ‚€ n
   where
    s : Î± n â‰¡ incl (under (succ n)) n
    s = ap (Î» - â†’ incl - n) r

    t : Î±(succ n) â‰¡ incl (under (succ n)) (succ n)
    t = ap (Î» - â†’ incl - (succ n)) r

    w : Î±(succ n) â‰¡ p(under(succ n))
    w = ap (Î» - â†’ minğŸš - (p(under(succ n)))) (s â€¯âˆ™ under-diagonalâ‚ n)

  Daggerâ‚ : a â‰¡ âˆ â†’ (n : â„•) â†’ p(under n) â‰¡ â‚
  Daggerâ‚ r 0 = ap (Î» - â†’ incl - 0) r
  Daggerâ‚ r (succ n) = w â»Â¹ âˆ™ t
   where
    s : Î± n â‰¡ â‚
    s = ap (Î» - â†’ incl - n) r

    t : Î±(succ n) â‰¡ â‚
    t = ap (Î» - â†’ incl - (succ n)) r

    w : Î±(succ n) â‰¡ p(under(succ n))
    w = ap (Î» - â†’ minğŸš - (p(under(succ n)))) s

  Claimâ‚€ : p a â‰¡ â‚ â†’ (n : â„•) â†’ a â‰¢ under n
  Claimâ‚€ r n s = Lemma[bâ‰¡â‚â†’bâ‰¢â‚€] r (Lemma s)
   where
    Lemma : a â‰¡ under n â†’ p a â‰¡ â‚€
    Lemma t = ap p t âˆ™ Daggerâ‚€ n t

  Claimâ‚ : p a â‰¡ â‚ â†’ a â‰¡ âˆ
  Claimâ‚ r = not-â„•-is-âˆ fe (Claimâ‚€ r)

  Claimâ‚‚ : p a â‰¡ â‚ â†’ (n : â„•) â†’ p(under n) â‰¡ â‚
  Claimâ‚‚ r = Daggerâ‚(Claimâ‚ r)

  Claimâ‚ƒ : p a â‰¡ â‚ â†’ p âˆ â‰¡ â‚
  Claimâ‚ƒ r = (ap p (Claimâ‚ r))â»Â¹ âˆ™ r

  Lemma : p a â‰¡ â‚ â†’ (v : â„•âˆ) â†’ p v â‰¡ â‚
  Lemma r = â„•âˆ-ğŸš-density fe (Claimâ‚‚ r) (Claimâ‚ƒ r)

\end{code}

Corollaries:

\begin{code}

open import OmniscientTypes
open import DiscreteAndSeparated

â„•âˆ-omniscient : omniscient â„•âˆ
â„•âˆ-omniscient = searchable-implies-omniscient (â„•âˆ-searchable)

â„•âˆâ†’â„•-discrete : discrete(â„•âˆ â†’ â„•)
â„•âˆâ†’â„•-discrete = omniscient-discrete-discrete fe â„•âˆ-omniscient (Î» u â†’ â„•-discrete)

â„•âˆâ†’ğŸš-discrete : discrete(â„•âˆ â†’ ğŸš)
â„•âˆâ†’ğŸš-discrete = omniscient-discrete-discrete fe â„•âˆ-omniscient (Î» u â†’ ğŸš-discrete)

module _ (fe' : âˆ€ U V â†’ funext U V) (pt : PropTrunc) where

 open import 2CompactTypes (fe') (pt)

 â„•âˆ-is-strongly-ğŸš-overt : strongly-ğŸš-overt â„•âˆ
 â„•âˆ-is-strongly-ğŸš-overt = omniscient-Compact â„•âˆ-omniscient

 â„•âˆ-is-ğŸš-compact : ğŸš-compact â„•âˆ
 â„•âˆ-is-ğŸš-compact = ğŸš-so-c â„•âˆ-is-strongly-ğŸš-overt

\end{code}
