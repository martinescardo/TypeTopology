Martin Escardo, July 2025.

ğŸš-injecting maps and ğŸš-injective types.

The motivation for this discussion is two-fold:

(1) Injective types don't have any non-trivial decidable properties in general.

(2) In particular, totally separated types, which have plenty of
    decidable properties by definition, are not innjective in general.

    Can the totally separated types be the injective types with
    respect to *some* class of maps?

    We consider ğŸš-injecting maps as a candidate for that. But, for the
    moment, they are not necessarily the answer, although we can show
    that injective types over ğŸš-injecting maps are totally separared.

\begin{code}

{-# OPTIONS --safe --without-K  #-}

open import UF.FunExt

module gist.2-injective-types (fe : FunExt) where

open import MLTT.Spartan
open import TypeTopology.TotallySeparated
open import UF.Embeddings
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

private
 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

\end{code}

The double-dualization monad with base ğŸš, incompletely defined.

\begin{code}

K : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
K X = (X â†’ ğŸš) â†’ ğŸš

K-functor : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ K X â†’ K Y
K-functor f Ï• v = Ï• (v âˆ˜ f)

Î· : {X : ğ“¤ Ì‡ } â†’ X â†’ K X
Î· x u = u x

Î¼ : {X : ğ“¤ Ì‡ } â†’ K (K X) â†’ K X
Î¼ F u = F (Î» Ï• â†’ Ï• u)

K-ext : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ K Y) â†’ K X â†’ K Y
K-ext f Ï• v = Ï• (Î» x â†’ f x v)

K-strength : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ X Ã— K Y â†’ K (X Ã— Y)
K-strength (x , Î³) w = Î³ (Î» y â†’ w (x , y))

K-prod : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ K X Ã— K Y â†’ K (X Ã— Y)
K-prod (Ï• , Î³) w = Ï• (Î» x â†’ Î³ (Î» y â†’ w (x , y)))

K-depprod : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ K X â†’ (X â†’ K Y) â†’ K (X Ã— Y)
K-depprod Ï• Î³ w = Ï• (Î» x â†’ Î³ x (Î» y â†’ w (x , y)))

\end{code}

(We probably defined above more than what we need for now, but we may
need it in the future to answer some of the questions below.)

A map j : X â†’ Y is ğŸš-injecting if the type ğŸš is algebraically
injective with respect to it. We don't require ğŸš-injecting maps to be
embeddings (but see the discussion below).

\begin{code}

ğŸš-injecting : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
ğŸš-injecting j = (f : domain j â†’ ğŸš) â†’ Î£ f' ê‰ (codomain j â†’ ğŸš) , f' âˆ˜ j âˆ¼ f

\end{code}

A type is ğŸš-injective if it is injective over ğŸš-injecting maps.

\begin{code}

ğŸš-injective : ğ“¦ Ì‡ â†’ (ğ“¤ ğ“¥ : Universe) â†’ ğ“¦ âŠ” (ğ“¤ âŠ” ğ“¥)âº Ì‡
ğŸš-injective {ğ“¦} D ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                          (j : X â†’ Y)
                        â†’ ğŸš-injecting j
                        â†’ (f : X â†’ D)
                        â†’ Î£ f' ê‰ (Y â†’ D) , f' âˆ˜ j âˆ¼ f

\end{code}

We name the two projections `extension` and `extension-property`.

\begin{code}

module _
         {D : ğ“¦ Ì‡ } {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
         (i : ğŸš-injective D ğ“¤ ğ“¥)
         (j : X â†’ Y)
         (k : ğŸš-injecting j)
         (f : X â†’ D)
       where

 extension : (Y â†’ D)
 extension = prâ‚ (i j k f)

 extension-property : extension âˆ˜ j âˆ¼ f
 extension-property = prâ‚‚ (i j k f)

\end{code}

We'll see below that the extension property really is property, rather
than general data.

By construction, the type ğŸš is ğŸš-injective.

\begin{code}

ğŸš-is-ğŸš-injective : ğŸš-injective ğŸš ğ“¤ ğ“¥
ğŸš-is-ğŸš-injective j = id

\end{code}

The ğŸš-injective types are closed under products and retracts.

\begin{code}

ğŸš-injectives-closed-under-Î  : {I : ğ“£ Ì‡ } {D : I â†’ ğ“¦ Ì‡ }
                            â†’ ((i : I) â†’ ğŸš-injective (D i) ğ“¤ ğ“¥)
                            â†’ ğŸš-injective (Î  D) ğ“¤ ğ“¥
ğŸš-injectives-closed-under-Î  {ğ“£} {ğ“¦} {I} {D} D-ğŸš-inj j j-ğŸš-injecting f =
 (Î» y i â†’ extension (D-ğŸš-inj i) j j-ğŸš-injecting (Î» x â†’ f x i) y) ,
 (Î» x â†’ dfunext fe'
 (Î» i â†’ extension-property (D-ğŸš-inj i) j j-ğŸš-injecting (Î» x â†’ f x i) x))

\end{code}

Free algebras of the ğŸš-based double dualization monad are injective.

\begin{code}

K-ğŸš-injective : {X : ğ“£ Ì‡ } â†’ ğŸš-injective (K X) ğ“¤ ğ“¥
K-ğŸš-injective = ğŸš-injectives-closed-under-Î  (Î» i â†’ ğŸš-is-ğŸš-injective)

\end{code}

The unit of the monad is ğŸš-injecting.

\begin{code}

Î·-ğŸš-injecting : {X : ğ“¤ Ì‡ } â†’ ğŸš-injecting (Î· {ğ“¤} {X})
Î·-ğŸš-injecting f = (Î» Ï• â†’ Ï• f) , (Î» x â†’ refl)

\end{code}

For future reference, notice that the map Î· {ğ“¤} {X} is not in general
an embedding.

Hence every ğŸš-injective type is a retract of a free algebra.

\begin{code}

ğŸš-injectives-are-K-retracts : {D : ğ“¤ Ì‡ }
                            â†’ ğŸš-injective D ğ“¤ ğ“¤
                            â†’ retract D of K D
ğŸš-injectives-are-K-retracts D-ğŸš-inj =
 extension D-ğŸš-inj Î· Î·-ğŸš-injecting id ,
 Î· ,
 extension-property D-ğŸš-inj Î· Î·-ğŸš-injecting id

\end{code}

And therefore the ğŸš-injective types are all totally separated, because
the type ğŸš is totally separated, and the totally separated types are
closed under products and retracts.

\begin{code}

K-is-totally-separated : {X : ğ“¤ Ì‡ } â†’ is-totally-separated (K X)
K-is-totally-separated = Î -is-totally-separated fe' (Î» _ â†’ ğŸš-is-totally-separated)

ğŸš-injectives-are-totally-separated : {D : ğ“¤ Ì‡ }
                                   â†’ ğŸš-injective D ğ“¤ ğ“¤
                                   â†’ is-totally-separated D
ğŸš-injectives-are-totally-separated D-ğŸš-inj =
 retract-of-totally-separated
  (ğŸš-injectives-are-K-retracts D-ğŸš-inj)
  K-is-totally-separated

\end{code}

This shows that ğŸš-injecting map doesn't need to be an
embedding. Indeed, we have seen that the map Î· : X â†’ K X is always
ğŸš-injecting, but we also know that it is an embedding if and only if X
is totally separated.

\begin{code}

_ : {X : ğ“¤ Ì‡ } â†’ is-embedding (Î· {ğ“¤} {X}) â†” is-totally-separated X
_ = totally-separatedâ‚‚-gives-totally-separated fe' ,
    totally-separated-gives-totally-separatedâ‚‚ fe'

\end{code}

The extension property really is property, even though the choice of
extension is data.

\begin{code}

module _
         {D : ğ“¤ Ì‡ } {X : ğ“¤ Ì‡ } {Y : ğ“¤ Ì‡ }
         (i : ğŸš-injective D ğ“¤ ğ“¤)
         (j : X â†’ Y)
         (k : ğŸš-injecting j)
         (f : X â†’ D)
       where

 extension-property-is-prop : is-prop (extension i j k f âˆ˜ j âˆ¼ f)
 extension-property-is-prop =
  Î -is-prop fe'
   (Î» x â†’ totally-separated-types-are-sets fe' D
           (ğŸš-injectives-are-totally-separated i))

\end{code}

TODO. Can we generalize the universes in `ğŸš-injectives-are-totally-separated`
and (hence) the above?

Can we show that every totally separated type A is ğŸš-injective? I
can't even show, at the time of writing, that â„•, a totally separated
type, is ğŸš-injective.

Do totally separated types "think that ğŸš-injective types are
embeddings"? Formulate this question precisely.

Can we show that the totally separated types are precisely the
algebras of the ğŸš-based double dualization monad?

Now let's go back to (algebraic) injectivity with respect to all
embeddings. Say that a map j : X â†’ Y is injecting if all algebraically
injective types with respect to embeddings are injective with respect
to j. Question. Can we show that j is necessarily an embedding?
