Jon Sterling, started 16th Dec 2022

The upshift and downshift, when viewed in terms of the categories obtained from
the duploid, will ultimately form a pair of adjunctions `â†‘âŠ£â†“` and `â‡“âŠ£â‡‘`
respectively:

1. The upshift becomes a *left* adjoint functor `â†‘ : ð“Ÿ-thunk â†’ ð“-lin` from the
category of positive types and thunkable maps to the category of negative
objects and linear maps. Its right adjoint is the downshift `â†“ : ð“-lin â†’
ð“Ÿ-thunk`.

2. The upshift becomes a *right* adjoint functor `â‡‘ : ð“Ÿ â†’ ð“` from the category
of positive types and all maps to the category of negative objects and all
maps. Its left adjoint is the downshift `â‡“ : ð“ â†’ ð“Ÿ`.

The category of positive objects and all maps is the Kleisli category for the
monad of the adjunction `â†‘âŠ£â†“`; the category of negative objects and all maps is
the Kleisli category for the comonad of `â†‘âŠ£â†“`. Then the (flipped) adjunction
`â‡“âŠ£â‡‘` is the usual adjunction between the Kleisli categories for the monad and
the comonad respectively.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc
import Duploids.Duploid

module Duploids.ShiftFunctors
 (fe : Fun-Ext)
 (pt : propositional-truncations-exist)
 where

open import UF.Base
open import UF.Retracts
open import UF.Subsingletons

open import Categories.Category fe
open import Categories.Functor fe
open import Categories.NaturalTransformation fe
open import Categories.Adjunction fe
open import Duploids.Preduploid
open import Duploids.Duploid fe pt

module _ (ð““ : duploid ð“¤ ð“¥) where
 private module ð““ = duploid ð““
 open duploid-extras ð““
 open duploid-notation ð““
 open functor-of-precategories

 open import Duploids.Categories fe pt ð““.underlying-preduploid

 -- TODO: move these lemmas elsewhere
 module _ {U V : _} {i : U ð““.âŠ¢ U} {f : U ð““.âŠ¢ V} where
  abstract
   lem-rewrite-idn-L
    : i ï¼ ð““.idn _
    â†’ i >> f ï¼ f
   lem-rewrite-idn-L p =
    i >> f ï¼âŸ¨ ap (_>> f) p âŸ©
    ð““.idn _ >> f ï¼âŸ¨ ð““.idn-L _ _ _ âŸ©
    f âˆŽ

 module _ {U V : _} {i : V ð““.âŠ¢ V} {f : U ð““.âŠ¢ V} where
  abstract
   lem-rewrite-idn-R
    : i ï¼ ð““.idn _
    â†’ f >> i ï¼ f
   lem-rewrite-idn-R p =
    f >> i ï¼âŸ¨ ap (f >>_) p âŸ©
    f >> ð““.idn _ ï¼âŸ¨ ð““.idn-R _ _ _ âŸ©
    f âˆŽ

 module _ {U V : _} {f : U ð““.âŠ¢ V} where
  abstract
   lem-ð’¹[ð’»-] : ð’¹ >> (ð’» >> f) ï¼ f
   lem-ð’¹[ð’»-] =
    ð’¹ >> (ð’» >> f) ï¼âŸ¨ ð““.delay-thunkable _ _ _ _ â»Â¹ âŸ©
    (ð’¹ >> ð’») >> f ï¼âŸ¨ lem-rewrite-idn-L (prâ‚‚ ð““.force-delay-inverse) âŸ©
    f âˆŽ

   lem-[-ð“Œ]ð“Š : (f >> ð“Œ) >> ð“Š ï¼ f
   lem-[-ð“Œ]ð“Š =
    (f >> ð“Œ) >> ð“Š ï¼âŸ¨ ð““.unwrap-linear _ _ _ _ âŸ©
    f >> (ð“Œ >> ð“Š) ï¼âŸ¨ lem-rewrite-idn-R (prâ‚ ð““.wrap-unwrap-inverse) âŸ©
    f âˆŽ

   lem-ð“Œ[ð“Š-] : ð“Œ >> (ð“Š >> f) ï¼ f
   lem-ð“Œ[ð“Š-] =
    ð“Œ >> (ð“Š >> f) ï¼âŸ¨ ð““.wrap-thunkable _ _ _ _ â»Â¹ âŸ©
    (ð“Œ >> ð“Š) >> f ï¼âŸ¨ lem-rewrite-idn-L (prâ‚ ð““.wrap-unwrap-inverse) âŸ©
    f âˆŽ

   lem-[-ð’¹]ð’» : (f >> ð’¹) >> ð’» ï¼ f
   lem-[-ð’¹]ð’» =
    (f >> ð’¹) >> ð’» ï¼âŸ¨ ð““.force-linear _ _ _ _ âŸ©
    f >> (ð’¹ >> ð’») ï¼âŸ¨ lem-rewrite-idn-R (prâ‚‚ ð““.force-delay-inverse) âŸ©
    f âˆŽ

 module _ {U V : _} {f : ð““.â‡“ U ð““.âŠ¢ V} where
  abstract
   lem-ð“Š[ð“Œ-] : ð“Š >> (ð“Œ >> f) ï¼ f
   lem-ð“Š[ð“Œ-] =
    (ð“Š >> (ð“Œ >> f)) ï¼âŸ¨ f-lin _ _ _ _ â»Â¹ âŸ©
    (ð“Š >> ð“Œ) >> f ï¼âŸ¨ lem-rewrite-idn-L (prâ‚‚ ð““.wrap-unwrap-inverse) âŸ©
    f âˆŽ
    where
     f-lin : ð““.is-linear f
     f-lin = ð““.â‡“-positive U V f


 -- forget linearity
 module ForgetLinearity where
  structure : functor-structure ð“¢ ð“
  prâ‚ structure A = A
  prâ‚‚ structure A B f = prâ‚ f

  axioms : functor-axioms ð“¢ ð“ structure
  prâ‚ axioms _ = refl
  prâ‚‚ axioms _ _ _ _ _ = refl

  fun : functor ð“¢ ð“
  fun = make structure axioms

 ð“¢â‡’ð“ = ForgetLinearity.fun
 module ð“¢â‡’ð“ = functor ð“¢â‡’ð“

 -- forget thunkability
 module ForgetThunkability where
  structure : functor-structure ð“’ ð“Ÿ
  prâ‚ structure A = A
  prâ‚‚ structure A B f = prâ‚ f

  axioms : functor-axioms ð“’ ð“Ÿ structure
  prâ‚ axioms _ = refl
  prâ‚‚ axioms _ _ _ _ _ = refl

  fun : functor ð“’ ð“Ÿ
  fun = make structure axioms

 ð“’â‡’ð“Ÿ = ForgetThunkability.fun
 module ð“’â‡’ð“Ÿ = functor ð“’â‡’ð“Ÿ

 module Downshift where
  module str where
   ob : ð“.ob â†’ ð“’.ob
   ob (N , _) = ð““.â‡“ N , ð““.â‡“-positive N

   module _ (M N : ð“.ob) (f : ð“.hom M N) where
    hom-ð“Ÿ : ð“Ÿ.hom (ob M) (ob N)
    hom-ð“Ÿ = ð“Š >> (f >> ð“Œ)

    hom-thunkable : ð““.is-thunkable hom-ð“Ÿ
    hom-thunkable U V g h =
     ((ð“Š >> (f >> ð“Œ)) >> g) >> h ï¼âŸ¨ ap (_>> h) (ð“Š[M]-th _ _ _ _) âŸ©
     (ð“Š >> ((f >> ð“Œ) >> g)) >> h ï¼âŸ¨ ð“Š[M]-th _ _ _ _ âŸ©
     ð“Š >> (((f >> ð“Œ) >> g) >> h) ï¼âŸ¨ ap (ð“Š >>_) lem âŸ©
     ð“Š >> ((f >> ð“Œ) >> (g >> h)) ï¼âŸ¨ ð“Š[M]-th _ _ _ _ â»Â¹ âŸ©
     (ð“Š >> (f >> ð“Œ)) >> (g >> h) âˆŽ
     where

      f-th : ð““.is-thunkable f
      f-th = prâ‚‚ N (prâ‚ M) f

      g-lin : ð““.is-linear g
      g-lin = ð““.â‡“-positive (prâ‚ N) U g

      ð“Š[M]-th : ð““.is-thunkable (ð“Š {prâ‚ M})
      ð“Š[M]-th = prâ‚‚ M (ð““.â‡“ (prâ‚ M)) ð“Š

      lem : ((f >> ð“Œ) >> g) >> h ï¼ (f >> ð“Œ) >> (g >> h)
      lem =
       ((f >> ð“Œ) >> g) >> h ï¼âŸ¨ ap (_>> h) (g-lin _ _ _ _) âŸ©
       (f >> (ð“Œ >> g)) >> h ï¼âŸ¨ f-th _ _ _ _ âŸ©
       f >> ((ð“Œ >> g) >> h) ï¼âŸ¨ ap (f >>_) (ð““.wrap-thunkable _ _ _ _) âŸ©
       f >> (ð“Œ >> (g >> h)) ï¼âŸ¨ f-th _ _ _ _ â»Â¹ âŸ©
       (f >> ð“Œ) >> (g >> h) âˆŽ


    hom : ð“’.hom (ob M) (ob N)
    prâ‚ hom = hom-ð“Ÿ
    prâ‚‚ hom = hom-thunkable

   structure : functor-structure ð“ ð“’
   structure = ob , hom

  module ax where
   preserves-idn : statement-preserves-idn ð“ ð“’ str.structure
   preserves-idn M =
    PositivesAndThunkableMaps.to-hom-ï¼ (str.ob M) (str.ob M) _ _
     (ð“Š >> (ð“.idn M >> ð“Œ) ï¼âŸ¨ ap (ð“Š >>_) (ð““.idn-L _ _ _) âŸ©
      ð“Š >> ð“Œ ï¼âŸ¨ prâ‚‚ ð““.wrap-unwrap-inverse âŸ©
      ð“Ÿ.idn (str.ob M) âˆŽ)

   preserves-seq : statement-preserves-seq ð“ ð“’ str.structure
   preserves-seq M N O f g =
    PositivesAndThunkableMaps.to-hom-ï¼ (str.ob M) (str.ob O) _ _
     (ð“Š >> ((f >> g) >> ð“Œ) ï¼âŸ¨ ap (ð“Š >>_) (f-th _ _ _ _) âŸ©
      ð“Š >> (f >> (g >> ð“Œ)) ï¼âŸ¨ ð“Š[M]-th _ _ _ _ â»Â¹ âŸ©
      (ð“Š >> f) >> (g >> ð“Œ) ï¼âŸ¨ ap (_>> (g >> ð“Œ)) lem1 âŸ©
      ((ð“Š >> (f >> ð“Œ)) >> ð“Š) >> (g >> ð“Œ) ï¼âŸ¨ str.hom-thunkable M N _ _ _ _ _ âŸ©
      (ð“Š >> (f >> ð“Œ)) >> (ð“Š >> (g >> ð“Œ)) âˆŽ)
    where
     f-th : ð““.is-thunkable f
     f-th = prâ‚‚ N (prâ‚ M) f

     ð“Š[M]-th : ð““.is-thunkable (ð“Š {prâ‚ M})
     ð“Š[M]-th = prâ‚‚ M (ð““.â‡“ (prâ‚ M)) ð“Š

     lem1 : (ð“Š >> f) ï¼ (ð“Š >> (f >> ð“Œ)) >> ð“Š
     lem1 =
      ð“Š >> f ï¼âŸ¨ ap (ð“Š >>_) (lem-[-ð“Œ]ð“Š â»Â¹) âŸ©
      ð“Š >> ((f >> ð“Œ) >> ð“Š) ï¼âŸ¨ ð““.unwrap-linear _ _ _ _ â»Â¹ âŸ©
      ((ð“Š >> (f >> ð“Œ)) >> ð“Š) âˆŽ

   axioms : functor-axioms ð“ ð“’ str.structure
   prâ‚ axioms = preserves-idn
   prâ‚‚ axioms = preserves-seq

  fun : functor ð“ ð“’
  fun = make str.structure ax.axioms

 ð“â‡’ð“’ = Downshift.fun
 module ð“â‡’ð“’ = functor ð“â‡’ð“’

 module Upshift where
  module str where
   ob : ð“Ÿ.ob â†’ ð“¢.ob
   ob (A , A-pos) = ð““.â‡‘ A , ð““.â‡‘-negative A

   module _ (A B : ð“Ÿ.ob) (f : ð“Ÿ.hom A B) where
    hom-ð“ : ð“.hom (ob A) (ob B)
    hom-ð“ = ð’» >> (f >> ð’¹)

    hom-linear : ð““.is-linear hom-ð“
    hom-linear U V g h =
     ((h >> g) >> (ð’» >> (f >> ð’¹))) ï¼âŸ¨ hg-th _ _ _ _ â»Â¹ âŸ©
     ((h >> g) >> ð’») >> (f >> ð’¹) ï¼âŸ¨ ap (_>> (f >> ð’¹)) (ð““.force-linear _ _ _ _) âŸ©
     (h >> (g >> ð’»)) >> (f >> ð’¹) ï¼âŸ¨ fð’¹-lin _ _ _ _ âŸ©
     (h >> ((g >> ð’») >> (f >> ð’¹))) ï¼âŸ¨ ap (h >>_) (g-th _ _ _ _) âŸ©
     h >> (g >> (ð’» >> (f >> ð’¹))) âˆŽ
     where
      fð’¹-lin : ð““.is-linear (f >> ð’¹)
      fð’¹-lin = prâ‚‚ A (ð““.â‡‘ (prâ‚ B)) (f >> ð’¹)

      g-th : ð““.is-thunkable g
      g-th = ð““.â‡‘-negative (prâ‚ A) V g

      hg-th : ð““.is-thunkable (h >> g)
      hg-th = ð““.â‡‘-negative (prâ‚ A) U (h >> g)

    hom : ð“¢.hom (ob A) (ob B)
    hom = hom-ð“ , hom-linear

   structure : functor-structure ð“Ÿ ð“¢
   structure = ob , hom

  module ax where
   private
    abstract
     preserves-idn-ð“ : (A : ð“Ÿ.ob) â†’ ð’» {prâ‚ A} >> (ð““.idn _ >> ð’¹) ï¼ ð““.idn _
     preserves-idn-ð“ (A , A-pos) =
      ð’» >> (ð““.idn A >> ð’¹) ï¼âŸ¨ ap (ð’» >>_) (ð““.idn-L _ _ _) âŸ©
      ð’» >> ð’¹ ï¼âŸ¨ prâ‚ ð““.force-delay-inverse âŸ©
      ð““.idn (ð““.â‡‘ A) âˆŽ

     preserves-seq-ð“
      : (A B C : ð“Ÿ.ob)
      â†’ (f : ð“Ÿ.hom A B)
      â†’ (g : ð“Ÿ.hom B C)
      â†’ ð’» >> ((f >> g) >> ð’¹) ï¼ (ð’» >> (f >> ð’¹)) >> (ð’» >> (g >> ð’¹))
     preserves-seq-ð“ (A , A-pos) (B , B-pos) (C , C-pos) f g =
      ð’» >> ((f >> g) >> ð’¹) ï¼âŸ¨ ap (ð’» >>_) (ð’¹-linear _ _ _ _) âŸ©
      ð’» >> (f >> (g >> ð’¹)) ï¼âŸ¨ g-ð’¹-linear _ _ _ _ â»Â¹ âŸ©
      ((ð’» >> f) >> (g >> ð’¹)) ï¼âŸ¨ ap (_>> (g >> ð’¹)) (help1 â»Â¹) âŸ©
      ((ð’» >> (f >> ð’¹)) >> ð’») >> (g >> ð’¹) ï¼âŸ¨ g-ð’¹-linear _ _ _ _ âŸ©
      (ð’» >> (f >> ð’¹)) >> (ð’» >> (g >> ð’¹)) âˆŽ
      where
       help1 : ((ð’» >> (f >> ð’¹)) >> ð’») ï¼ ð’» >> f
       help1 =
        ((ð’» >> (f >> ð’¹)) >> ð’») ï¼âŸ¨ ð““.force-linear _ _ _ _ âŸ©
        (ð’» >> ((f >> ð’¹) >> ð’»)) ï¼âŸ¨ ap (ð’» >>_) lem-[-ð’¹]ð’» âŸ©
        (ð’» >> f) âˆŽ

       g-ð’¹-linear : ð““.is-linear (g >> ð’¹)
       g-ð’¹-linear = B-pos (ð““.â‡‘ C) (g >> ð’¹)

       ð’¹-linear : ð““.is-linear (ð’¹ {C})
       ð’¹-linear = C-pos (ð““.â‡‘ C) ð’¹


     preserves-idn : statement-preserves-idn ð“Ÿ ð“¢ str.structure
     preserves-idn A =
      NegativesAndLinearMaps.to-hom-ï¼ (str.ob A) (str.ob A) _ _
       (preserves-idn-ð“ A)

     preserves-seq : statement-preserves-seq ð“Ÿ ð“¢ str.structure
     preserves-seq A B C f g =
      NegativesAndLinearMaps.to-hom-ï¼ (str.ob A) (str.ob C) _ _
       (preserves-seq-ð“ A B C f g)


   axioms : functor-axioms ð“Ÿ ð“¢ str.structure
   axioms = preserves-idn , preserves-seq

  fun : functor ð“Ÿ ð“¢
  fun = make str.structure ax.axioms

 ð“Ÿâ‡’ð“¢ = Upshift.fun
 module ð“Ÿâ‡’ð“¢ = functor ð“Ÿâ‡’ð“¢

 [â†‘] : functor ð“’ ð“¢
 [â†‘] = composite-functor.fun ð“’â‡’ð“Ÿ ð“Ÿâ‡’ð“¢

 [â†“] : functor ð“¢ ð“’
 [â†“] = composite-functor.fun ð“¢â‡’ð“ ð“â‡’ð“’

 [â‡‘] : functor ð“Ÿ ð“
 [â‡‘] = composite-functor.fun ð“Ÿâ‡’ð“¢ ð“¢â‡’ð“

 [â‡“] : functor ð“ ð“Ÿ
 [â‡“] = composite-functor.fun ð“â‡’ð“’ ð“’â‡’ð“Ÿ

 [â‡‘-â‡“] : functor ð“Ÿ ð“Ÿ
 [â‡‘-â‡“] = composite-functor.fun [â‡‘] [â‡“]

 [â‡“-â‡‘] : functor ð“ ð“
 [â‡“-â‡‘] = composite-functor.fun [â‡“] [â‡‘]

 1[ð“] : functor ð“ ð“
 1[ð“] = identity-functor.fun ð“

 1[ð“Ÿ] : functor ð“Ÿ ð“Ÿ
 1[ð“Ÿ] = identity-functor.fun ð“Ÿ


 module effectful-adjunction where
  open adjunction-of-precategories ð“ ð“Ÿ
  open natural-transformation

  [ð“,ð“] = functor-category.precat ð“ ð“
  module [ð“,ð“] = precategory [ð“,ð“]

  module unit where
   str : transf _ _ 1[ð“] [â‡“-â‡‘]
   str M = ð“Œ >> ð’¹

   abstract
    ax : is-natural _ _ 1[ð“] [â‡“-â‡‘] str
    ax M N f =
     f >> (ð“Œ >> ð’¹ {ð““.â‡“ (prâ‚ N)}) ï¼âŸ¨ ð’¹[â‡“]-linear _ _ _ _ â»Â¹ âŸ©
     (f >> ð“Œ) >> ð’¹ ï¼âŸ¨ ap (_>> ð’¹) lem âŸ©
     ((ð“Œ >> ð’¹) >> (ð’» >> (ð“Š >> (f >> ð“Œ)))) >> ð’¹ ï¼âŸ¨ ð’¹[â‡“]-linear _ _ _ _ âŸ©
     (ð“Œ >> ð’¹) >> ((ð’» >> (ð“Š >> (f >> ð“Œ))) >> ð’¹) ï¼âŸ¨ ap ((ð“Œ >> ð’¹) >>_) (ð’¹[â‡“]-linear _ _ _ _) âŸ©
     (ð“Œ >> ð’¹) >> (ð’» >> ((ð“Š >> (f >> ð“Œ)) >> ð’¹)) âˆŽ

     where
      ð’¹[â‡“]-linear : {Z : _} â†’ ð““.is-linear (ð’¹ {ð““.â‡“ Z})
      ð’¹[â‡“]-linear = ð““.â‡“-positive _ _ _

      lem : (f >> ð“Œ) ï¼ (ð“Œ >> ð’¹) >> (ð’» >> (ð“Š >> (f >> ð“Œ)))
      lem =
       f >> ð“Œ ï¼âŸ¨ lem-ð“Œ[ð“Š-] â»Â¹ âŸ©
       ð“Œ >> (ð“Š >> (f >> ð“Œ)) ï¼âŸ¨ ap (ð“Œ >>_) (lem-ð’¹[ð’»-] â»Â¹) âŸ©
       ð“Œ >> (ð’¹ >> (ð’» >> (ð“Š >> (f >> ð“Œ)))) ï¼âŸ¨ ð““.wrap-thunkable _ _ _ _ â»Â¹ âŸ©
       (ð“Œ >> ð’¹) >> (ð’» >> (ð“Š >> (f >> ð“Œ))) âˆŽ

   unit : nat-transf _ _ 1[ð“] [â‡“-â‡‘]
   unit = make str ax

  module counit where
   str : transf _ _ [â‡‘-â‡“] 1[ð“Ÿ]
   str P = ð“Š >> ð’»

   abstract
    ax : is-natural _ _ [â‡‘-â‡“] 1[ð“Ÿ] str
    ax P Q f =
     (ð“Š >> ((ð’» >> (f >> ð’¹)) >> ð“Œ)) >> (ð“Š >> ð’») ï¼âŸ¨ ð““.force-linear _ _ _ _ â»Â¹ âŸ©
     ((ð“Š >> ((ð’» >> (f >> ð’¹)) >> ð“Œ)) >> ð“Š) >> ð’» ï¼âŸ¨ ap (_>> ð’») lem1 âŸ©
     (ð“Š >> (ð’» >> (f >> ð’¹))) >> ð’» ï¼âŸ¨ ð““.force-linear _ _ _ _ âŸ©
     ð“Š >> ((ð’» >> (f >> ð’¹)) >> ð’») ï¼âŸ¨ ap (ð“Š >>_) lem2 âŸ©
     ð“Š >> (ð’» >> f) ï¼âŸ¨ f-lin _ _ _ _ â»Â¹ âŸ©
     (ð“Š >> ð’») >> f âˆŽ

     where
      f-lin : ð““.is-linear f
      f-lin = prâ‚‚ P (prâ‚ Q) f

      lem1 : (ð“Š >> ((ð’» >> (f >> ð’¹)) >> ð“Œ)) >> ð“Š ï¼ (ð“Š >> (ð’» >> (f >> ð’¹)))
      lem1 =
       (ð“Š >> ((ð’» >> (f >> ð’¹)) >> ð“Œ)) >> ð“Š ï¼âŸ¨ ð““.unwrap-linear _ _ _ _ âŸ©
       ð“Š >> (((ð’» >> (f >> ð’¹)) >> ð“Œ) >> ð“Š) ï¼âŸ¨ ap (ð“Š >>_) lem-[-ð“Œ]ð“Š âŸ©
       ð“Š >> (ð’» >> (f >> ð’¹)) âˆŽ

      lem2 : (ð’» >> (f >> ð’¹)) >> ð’» ï¼ ð’» >> f
      lem2 =
       (ð’» >> (f >> ð’¹)) >> ð’» ï¼âŸ¨ ð““.force-linear _ _ _ _ âŸ©
       ð’» >> ((f >> ð’¹) >> ð’») ï¼âŸ¨ ap (ð’» >>_) lem-[-ð’¹]ð’» âŸ©
       ð’» >> f âˆŽ

   counit : nat-transf _ _ [â‡‘-â‡“] 1[ð“Ÿ]
   counit = make str ax

  str : adjunction-structure [â‡“] [â‡‘]
  prâ‚ str = unit.unit
  prâ‚‚ str = counit.counit

  abstract
   ax : adjunction-axioms [â‡“] [â‡‘] str
   prâ‚ ax =
    to-nat-transf-ï¼ ð“ ð“Ÿ [â‡“] [â‡“]
     (dfunext fe Î» M â†’
      (ð““.idn _ >> (ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ))) >> ((ð““.idn _) >> ((ð“Š >> ð’») >> ð““.idn _))
       ï¼âŸ¨ ap (_>> (ð““.idn _ >> ((ð“Š >> ð’») >> ð““.idn _))) (ð““.idn-L _ _ _) âŸ©
      (ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ)) >> ((ð““.idn _) >> ((ð“Š >> ð’») >> ð““.idn _))
       ï¼âŸ¨ ap ((ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ)) >>_) lem1 âŸ©
      (ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ)) >> (ð“Š >> ð’»)
       ï¼âŸ¨ ð““.force-linear _ _ _ _ â»Â¹ âŸ©
      ((ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ)) >> ð“Š) >> ð’»
       ï¼âŸ¨ ap (_>> ð’») lem0 âŸ©
      ð’¹ >> ð’»
       ï¼âŸ¨ prâ‚‚ ð““.force-delay-inverse âŸ©
      ð““.idn _ âˆŽ)

    where
     lem0 : {A : _} â†’ (ð“Š {A} >> ((ð“Œ >> ð’¹) >> ð“Œ)) >> ð“Š ï¼ ð’¹
     lem0 =
      (ð“Š >> ((ð“Œ >> ð’¹) >> ð“Œ)) >> ð“Š ï¼âŸ¨ ð““.unwrap-linear _ _ _ _ âŸ©
      (ð“Š >> (((ð“Œ >> ð’¹) >> ð“Œ) >> ð“Š)) ï¼âŸ¨ ap (ð“Š >>_) lem-[-ð“Œ]ð“Š âŸ©
      (ð“Š >> (ð“Œ >> ð’¹)) ï¼âŸ¨ lem-ð“Š[ð“Œ-] âŸ©
      ð’¹ âˆŽ

     lem1 : {A B : _} {f : A ð““.âŠ¢ B} â†’ (ð““.idn _ >> (f >> ð““.idn _)) ï¼ f
     lem1 =
      ð““.idn _ >> (_ >> ð““.idn _) ï¼âŸ¨ ð““.idn-L _ _ _ âŸ©
      _ >> ð““.idn _ ï¼âŸ¨ ð““.idn-R _ _ _ âŸ©
      _ âˆŽ

   prâ‚‚ ax =
    to-nat-transf-ï¼ ð“Ÿ ð“ [â‡‘] [â‡‘]
     (dfunext fe Î» P â†’
      (ð““.idn _ >> (ð“Œ >> ð’¹)) >> (ð““.idn _ >> ((ð’» >> ((ð“Š >> ð’») >> ð’¹)) >> ð““.idn _))
       ï¼âŸ¨ ap (_>> (ð““.idn _ >> ((ð’» >> ((ð“Š >> ð’») >> ð’¹)) >> ð““.idn _))) (ð““.idn-L _ _ _) âŸ©
      (ð“Œ >> ð’¹) >> (ð““.idn _ >> ((ð’» >> ((ð“Š >> ð’») >> ð’¹)) >> ð““.idn _))
       ï¼âŸ¨ ap ((ð“Œ >> ð’¹) >>_) (lem0 ((ð“Š >> ð’») >> ð’¹)) âŸ©
      (ð“Œ >> ð’¹) >> (ð’» >> ((ð“Š >> ð’») >> ð’¹))
       ï¼âŸ¨ ð““.wrap-thunkable _ _ _ _ âŸ©
      ð“Œ >> (ð’¹ >> (ð’» >> ((ð“Š >> ð’») >> ð’¹)))
       ï¼âŸ¨ ap (ð“Œ >>_) lem-ð’¹[ð’»-] âŸ©
      ð“Œ >> ((ð“Š >> ð’») >> ð’¹)
       ï¼âŸ¨ ð““.wrap-thunkable _ _ _ _ â»Â¹ âŸ©
      (ð“Œ >> (ð“Š >> ð’»)) >> ð’¹
       ï¼âŸ¨ ap (_>> ð’¹) lem-ð“Œ[ð“Š-] âŸ©
      ð’» >> ð’¹
       ï¼âŸ¨ prâ‚ ð““.force-delay-inverse âŸ©
      ð““.idn _ âˆŽ )
    where
     lem0
      : {U V : _} (f : ð““.â‡“ (ð““.â‡‘ U) ð““.âŠ¢ V)
      â†’ (ð““.idn _ >> ((ð’» >> f) >> ð““.idn _)) ï¼ (ð’» >> f)
     lem0 f =
      ð““.idn _ >> ((ð’» >> f) >> ð““.idn _)
       ï¼âŸ¨ ð““.idn-L _ _ _ âŸ©
      (ð’» >> f) >> ð““.idn _
       ï¼âŸ¨ ð““.idn-R _ _ _ âŸ©
      ð’» >> f âˆŽ

  adj : adjunction [â‡“] [â‡‘]
  adj = make str ax

\end{code}
