Jon Sterling, started 16th Dec 2022

The upshift and downshift, when viewed in terms of the categories obtained from
the duploid, will ultimately form a pair of adjunctions `â†‘âŠ£â†“` and `â‡“âŠ£â‡‘`
respectively:

1. The upshift becomes a *left* adjoint functor `â†‘ : ğ“Ÿ-thunk â†’ ğ“-lin` from the
category of positive types and thunkable maps to the category of negative
objects and linear maps. Its right adjoint is the downshift `â†“ : ğ“-lin â†’
ğ“Ÿ-thunk`.

2. The upshift becomes a *right* adjoint functor `â‡‘ : ğ“Ÿ â†’ ğ“` from the category
of positive types and all maps to the category of negative objects and all
maps. Its left adjoint is the downshift `â‡“ : ğ“ â†’ ğ“Ÿ`.

The category of positive objects and all maps is the Kleisli category for the
monad of the adjunction `â†‘âŠ£â†“`; the category of negative objects and all maps is
the Kleisli category for the comonad of `â†‘âŠ£â†“`. Then the (flipped) adjunction
`â‡“âŠ£â‡‘` is the usual adjunction between the Kleisli categories for the monad and
the comonad respectively.


\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc
import Duploids.Duploid

module Duploids.ShiftFunctors
 (fe : Fun-Ext)
 (pt : propositional-truncations-exist)
 (ğ““ : Duploids.Duploid.duploid fe pt ğ“¤ ğ“¥)
 where

open import UF.Base
open import UF.Retracts
open import UF.Subsingletons

open import Categories.Category fe
open import Categories.Functor fe
open import Categories.NaturalTransformation fe
open import Categories.Adjunction fe
open import Duploids.Preduploid
open Duploids.Duploid fe pt

private module ğ““ = duploid ğ““
open duploid-extras ğ““
open duploid-notation ğ““
open functor-of-precategories

open import Duploids.Categories fe pt ğ““.underlying-preduploid

-- forget linearity
module ForgetLinearity where
 structure : functor-structure ğ“¢ ğ“
 prâ‚ structure A = A
 prâ‚‚ structure A B f = prâ‚ f

 axioms : functor-axioms ğ“¢ ğ“ structure
 prâ‚ axioms _ = refl
 prâ‚‚ axioms _ _ _ _ _ = refl

 fun : functor ğ“¢ ğ“
 fun = make structure axioms

ğ“¢â‡’ğ“ = ForgetLinearity.fun
module ğ“¢â‡’ğ“ = functor ğ“¢â‡’ğ“

-- forget thunkability
module ForgetThunkability where
 structure : functor-structure ğ“’ ğ“Ÿ
 prâ‚ structure A = A
 prâ‚‚ structure A B f = prâ‚ f

 axioms : functor-axioms ğ“’ ğ“Ÿ structure
 prâ‚ axioms _ = refl
 prâ‚‚ axioms _ _ _ _ _ = refl

 fun : functor ğ“’ ğ“Ÿ
 fun = make structure axioms

ğ“’â‡’ğ“Ÿ = ForgetThunkability.fun
module ğ“’â‡’ğ“Ÿ = functor ğ“’â‡’ğ“Ÿ

module Downshift where
 module str where
  ob : ğ“.ob â†’ ğ“’.ob
  ob (N , N-neg) = ğ““.â‡“ N N-neg , ğ““.â‡“-positive N N-neg

  module _ (M N : ğ“.ob) (f : ğ“.hom M N) where
   hom-ğ“Ÿ : ğ“Ÿ.hom (ob M) (ob N)
   hom-ğ“Ÿ = ğ“Š _ >> (f >> ğ“Œ _)

   hom-thunkable : ğ““.is-thunkable hom-ğ“Ÿ
   hom-thunkable U V g h =
    ((ğ“Š _ >> (f >> ğ“Œ _)) >> g) >> h ï¼âŸ¨ ap (_>> h) (ğ“Š[M]-th _ _ _ _) âŸ©
    (ğ“Š _ >> ((f >> ğ“Œ _) >> g)) >> h ï¼âŸ¨ ğ“Š[M]-th _ _ _ _ âŸ©
    ğ“Š _ >> (((f >> ğ“Œ _) >> g) >> h) ï¼âŸ¨ ap (ğ“Š _ >>_) lem âŸ©
    ğ“Š _ >> ((f >> ğ“Œ _) >> (g >> h)) ï¼âŸ¨ ğ“Š[M]-th _ _ _ _ â»Â¹ âŸ©
    (ğ“Š _ >> (f >> ğ“Œ _)) >> (g >> h) âˆ
    where

     f-th : ğ““.is-thunkable f
     f-th = prâ‚‚ N (prâ‚ M) f

     g-lin : ğ““.is-linear g
     g-lin = ğ““.â‡“-positive (prâ‚ N) (prâ‚‚ N) U g

     ğ“Š[M]-th : ğ““.is-thunkable (ğ“Š {prâ‚ M} (prâ‚‚ M))
     ğ“Š[M]-th = prâ‚‚ M (ğ““.â‡“ (prâ‚ M) (prâ‚‚ M)) (ğ“Š _)

     lem : ((f >> ğ“Œ (prâ‚‚ N)) >> g) >> h ï¼ (f >> ğ“Œ (prâ‚‚ N)) >> (g >> h)
     lem =
      ((f >> ğ“Œ _) >> g) >> h ï¼âŸ¨ ap (_>> h) (g-lin _ _ _ _) âŸ©
      (f >> (ğ“Œ _ >> g)) >> h ï¼âŸ¨ f-th _ _ _ _ âŸ©
      f >> ((ğ“Œ _ >> g) >> h) ï¼âŸ¨ ap (f >>_) (ğ““.wrap-thunkable _ _ _ _) âŸ©
      f >> (ğ“Œ _ >> (g >> h)) ï¼âŸ¨ f-th _ _ _ _ â»Â¹ âŸ©
      (f >> ğ“Œ _) >> (g >> h) âˆ


   hom : ğ“’.hom (ob M) (ob N)
   prâ‚ hom = hom-ğ“Ÿ
   prâ‚‚ hom = hom-thunkable

  structure : functor-structure ğ“ ğ“’
  structure = ob , hom

 module ax where
  preserves-idn : statement-preserves-idn ğ“ ğ“’ str.structure
  preserves-idn M =
   PositivesAndThunkableMaps.to-hom-ï¼ (str.ob M) (str.ob M) _ _
    (ğ“Š _ >> (ğ“.idn M >> ğ“Œ _) ï¼âŸ¨ ap (ğ“Š _ >>_) (ğ““.idn-L _ _ _) âŸ©
     ğ“Š _ >> ğ“Œ _ ï¼âŸ¨ prâ‚‚ ğ““.wrap-unwrap-inverse âŸ©
     ğ“Ÿ.idn (str.ob M) âˆ)

  preserves-seq : statement-preserves-seq ğ“ ğ“’ str.structure
  preserves-seq M N O f g =
   PositivesAndThunkableMaps.to-hom-ï¼ (str.ob M) (str.ob O) _ _
    (ğ“Š _ >> ((f >> g) >> ğ“Œ _) ï¼âŸ¨ ap (ğ“Š _ >>_) (f-th _ _ _ _) âŸ©
     ğ“Š _ >> (f >> (g >> ğ“Œ _)) ï¼âŸ¨ ğ“Š[M]-th _ _ _ _ â»Â¹ âŸ©
     (ğ“Š _ >> f) >> (g >> ğ“Œ _) ï¼âŸ¨ ap (_>> (g >> ğ“Œ _)) lem1 âŸ©
     ((ğ“Š _ >> (f >> ğ“Œ _)) >> ğ“Š _) >> (g >> ğ“Œ _) ï¼âŸ¨ str.hom-thunkable M N _ _ _ _ _ âŸ©
     (ğ“Š _ >> (f >> ğ“Œ _)) >> (ğ“Š _ >> (g >> ğ“Œ _)) âˆ)
   where
    f-th : ğ““.is-thunkable f
    f-th = prâ‚‚ N (prâ‚ M) f

    ğ“Š[M]-th : ğ““.is-thunkable (ğ“Š {prâ‚ M} (prâ‚‚ M))
    ğ“Š[M]-th = prâ‚‚ M (ğ““.â‡“ (prâ‚ M) (prâ‚‚ M)) (ğ“Š (prâ‚‚ M))

    lem1 : (ğ“Š (prâ‚‚ M) >> f) ï¼ (ğ“Š (prâ‚‚ M) >> (f >> ğ“Œ (prâ‚‚ N))) >> ğ“Š (prâ‚‚ N)
    lem1 =
     ğ“Š _ >> f ï¼âŸ¨ ap (ğ“Š _ >>_) (lem-[-ğ“Œ]ğ“Š â»Â¹) âŸ©
     ğ“Š _ >> ((f >> ğ“Œ _) >> ğ“Š _) ï¼âŸ¨ ğ““.unwrap-linear _ _ _ _ â»Â¹ âŸ©
     ((ğ“Š _ >> (f >> ğ“Œ _)) >> ğ“Š _) âˆ

  axioms : functor-axioms ğ“ ğ“’ str.structure
  prâ‚ axioms = preserves-idn
  prâ‚‚ axioms = preserves-seq

 fun : functor ğ“ ğ“’
 fun = make str.structure ax.axioms

ğ“â‡’ğ“’ = Downshift.fun
module ğ“â‡’ğ“’ = functor ğ“â‡’ğ“’

module Upshift where
 module str where
  ob : ğ“Ÿ.ob â†’ ğ“¢.ob
  ob (A , A-pos) = ğ““.â‡‘ A A-pos , ğ““.â‡‘-negative A A-pos

  module _ (A B : ğ“Ÿ.ob) (f : ğ“Ÿ.hom A B) where
   hom-ğ“ : ğ“.hom (ob A) (ob B)
   hom-ğ“ = ğ’» _ >> (f >> ğ’¹ _)

   hom-linear : ğ““.is-linear hom-ğ“
   hom-linear U V g h =
    ((h >> g) >> (ğ’» _ >> (f >> ğ’¹ _))) ï¼âŸ¨ hg-th _ _ _ _ â»Â¹ âŸ©
    ((h >> g) >> ğ’» _) >> (f >> ğ’¹ _) ï¼âŸ¨ ap (_>> (f >> ğ’¹ _)) (ğ““.force-linear _ _ _ _) âŸ©
    (h >> (g >> ğ’» _)) >> (f >> ğ’¹ _) ï¼âŸ¨ fğ’¹-lin _ _ _ _ âŸ©
    (h >> ((g >> ğ’» _) >> (f >> ğ’¹ _))) ï¼âŸ¨ ap (h >>_) (g-th _ _ _ _) âŸ©
    h >> (g >> (ğ’» _ >> (f >> ğ’¹ _))) âˆ
    where
     fğ’¹-lin : ğ““.is-linear (f >> ğ’¹ _)
     fğ’¹-lin = prâ‚‚ A (ğ““.â‡‘ (prâ‚ B) (prâ‚‚ B)) (f >> ğ’¹ (prâ‚‚ B))

     g-th : ğ““.is-thunkable g
     g-th = ğ““.â‡‘-negative (prâ‚ A) (prâ‚‚ A) V g

     hg-th : ğ““.is-thunkable (h >> g)
     hg-th = ğ““.â‡‘-negative (prâ‚ A) (prâ‚‚ A) U (h >> g)

   hom : ğ“¢.hom (ob A) (ob B)
   hom = hom-ğ“ , hom-linear

  structure : functor-structure ğ“Ÿ ğ“¢
  structure = ob , hom

 module ax where
  private
   abstract
    preserves-idn-ğ“ : (A : ğ“Ÿ.ob) â†’ ğ’» (prâ‚‚ A) >> (ğ““.idn _ >> ğ’¹ (prâ‚‚ A)) ï¼ ğ““.idn _
    preserves-idn-ğ“ (A , A-pos) =
     ğ’» _ >> (ğ““.idn A >> ğ’¹ _) ï¼âŸ¨ ap (ğ’» _ >>_) (ğ““.idn-L _ _ _) âŸ©
     ğ’» _ >> ğ’¹ _ ï¼âŸ¨ prâ‚ ğ““.force-delay-inverse âŸ©
     ğ““.idn (ğ““.â‡‘ A _) âˆ

    preserves-seq-ğ“
     : (A B C : ğ“Ÿ.ob)
     â†’ (f : ğ“Ÿ.hom A B)
     â†’ (g : ğ“Ÿ.hom B C)
     â†’ ğ’» (prâ‚‚ A) >> ((f >> g) >> ğ’¹ (prâ‚‚ C))
        ï¼ (ğ’» _ >> (f >> ğ’¹ (prâ‚‚ B))) >> (ğ’» (prâ‚‚ B) >> (g >> ğ’¹ (prâ‚‚ C)))
    preserves-seq-ğ“ (A , A-pos) (B , B-pos) (C , C-pos) f g =
     ğ’» A-pos >> ((f >> g) >> ğ’¹ _) ï¼âŸ¨ ap (ğ’» _ >>_) (ğ’¹-linear _ _ _ _) âŸ©
     ğ’» A-pos >> (f >> (g >> ğ’¹ _)) ï¼âŸ¨ g-ğ’¹-linear _ _ _ _ â»Â¹ âŸ©
     ((ğ’» _ >> f) >> (g >> ğ’¹ _)) ï¼âŸ¨ ap (_>> (g >> ğ’¹ _)) (help1 â»Â¹) âŸ©
     ((ğ’» _ >> (f >> ğ’¹ _)) >> ğ’» _) >> (g >> ğ’¹ _) ï¼âŸ¨ g-ğ’¹-linear _ _ _ _ âŸ©
     (ğ’» _ >> (f >> ğ’¹ _)) >> (ğ’» _ >> (g >> ğ’¹ _)) âˆ
     where
      help1 : ((ğ’» A-pos >> (f >> ğ’¹ B-pos)) >> ğ’» B-pos) ï¼ ğ’» A-pos >> f
      help1 =
       ((ğ’» _ >> (f >> ğ’¹ _)) >> ğ’» _) ï¼âŸ¨ ğ““.force-linear _ _ _ _ âŸ©
       (ğ’» _ >> ((f >> ğ’¹ _) >> ğ’» _)) ï¼âŸ¨ ap (ğ’» _ >>_) lem-[-ğ’¹]ğ’» âŸ©
       (ğ’» _ >> f) âˆ

      g-ğ’¹-linear : ğ““.is-linear (g >> ğ’¹ C-pos)
      g-ğ’¹-linear = B-pos (ğ““.â‡‘ C C-pos) (g >> ğ’¹ _)

      ğ’¹-linear : ğ““.is-linear (ğ’¹ C-pos)
      ğ’¹-linear = C-pos (ğ““.â‡‘ C _) (ğ’¹ _)


    preserves-idn : statement-preserves-idn ğ“Ÿ ğ“¢ str.structure
    preserves-idn A =
     NegativesAndLinearMaps.to-hom-ï¼ (str.ob A) (str.ob A) _ _
      (preserves-idn-ğ“ A)

    preserves-seq : statement-preserves-seq ğ“Ÿ ğ“¢ str.structure
    preserves-seq A B C f g =
     NegativesAndLinearMaps.to-hom-ï¼ (str.ob A) (str.ob C) _ _
      (preserves-seq-ğ“ A B C f g)


  axioms : functor-axioms ğ“Ÿ ğ“¢ str.structure
  axioms = preserves-idn , preserves-seq

 fun : functor ğ“Ÿ ğ“¢
 fun = make str.structure ax.axioms

ğ“Ÿâ‡’ğ“¢ = Upshift.fun
module ğ“Ÿâ‡’ğ“¢ = functor ğ“Ÿâ‡’ğ“¢

[â†‘] : functor ğ“’ ğ“¢
[â†‘] = composite-functor.fun ğ“’â‡’ğ“Ÿ ğ“Ÿâ‡’ğ“¢

[â†“] : functor ğ“¢ ğ“’
[â†“] = composite-functor.fun ğ“¢â‡’ğ“ ğ“â‡’ğ“’

[â‡‘] : functor ğ“Ÿ ğ“
[â‡‘] = composite-functor.fun ğ“Ÿâ‡’ğ“¢ ğ“¢â‡’ğ“

[â‡“] : functor ğ“ ğ“Ÿ
[â‡“] = composite-functor.fun ğ“â‡’ğ“’ ğ“’â‡’ğ“Ÿ

\end{code}
