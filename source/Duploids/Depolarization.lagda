Jon Sterling, started 17th Dec 2022

We could consider three forms of depolarization for a deductive system:
1. All objects have positive polarity
2. All objects have negative polarity
3. Either (1) or (2).

It will happen that all three forms of depolarization are equivalent; moreover,
a depolarized deductive system is the same thing as a precategory.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module Duploids.Depolarization (fe : Fun-Ext) where

open import MLTT.Spartan
open import UF.Base
open import UF.Equiv
open import UF.PropTrunc
open import UF.Retracts
open import UF.HLevels
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open import Categories.Category fe
open import Duploids.DeductiveSystem fe

module _ (ğ““ : deductive-system ğ“¤ ğ“¥) where
 module ğ““ = deductive-system ğ““
 open ğ““
 open polarities ğ““

 is-pos-depolarized : ğ“¤ âŠ” ğ“¥ Ì‡
 is-pos-depolarized = (A : ob) â†’ is-positive A

 is-neg-depolarized : ğ“¤ âŠ” ğ“¥ Ì‡
 is-neg-depolarized = (A : ob) â†’ is-negative A

 being-pos-depolarized-is-prop : is-prop is-pos-depolarized
 being-pos-depolarized-is-prop =
  Î -is-prop fe Î» _ â†’
  being-positive-is-prop

 being-neg-depolarized-is-prop : is-prop is-neg-depolarized
 being-neg-depolarized-is-prop =
  Î -is-prop fe Î» _ â†’
  being-negative-is-prop
\end{code}

The positive and negative depolarizations are equivalent.

\begin{code}
 is-pos-depolarized-gives-is-neg-depolarized
  : is-pos-depolarized
  â†’ is-neg-depolarized
 is-pos-depolarized-gives-is-neg-depolarized pos A B f C D g h =
  pos C D h B A g f

 is-neg-depolarized-gives-is-pos-depolarized
  : is-neg-depolarized
  â†’ is-pos-depolarized
 is-neg-depolarized-gives-is-pos-depolarized neg A B f U V g h =
  neg V U h A B g f
\end{code}

A depolarized deductive system enjoys the full associativity axiom and therefore
gives rise to a precategory.

\begin{code}
 module depolarization-and-precategories (H : is-pos-depolarized) where
  depolarization-gives-assoc : category-axiom-statements.statement-assoc ğ““.str
  depolarization-gives-assoc A B C D f g h = H C D h A B g f â»Â¹

  depolarization-gives-precategory-axioms : precategory-axioms ğ““.str
  depolarization-gives-precategory-axioms =
   âŠ¢-is-set ,
   idn-L ,
   idn-R ,
   depolarization-gives-assoc

  precategory-of-depolarized-deductive-system : precategory ğ“¤ ğ“¥
  precategory-of-depolarized-deductive-system =
   make ğ““.str depolarization-gives-precategory-axioms
\end{code}

Conversely, any deductive system enjoying the axioms of a precategory is
depolarized.

\begin{code}
 module _ (ax : precategory-axioms ğ““.str) where
  module ax = precategory-axioms ğ““.str ax

  precategory-gives-pos-depolarized : is-pos-depolarized
  precategory-gives-pos-depolarized A B f U V g h =
   ax.assoc U V A B h g f â»Â¹
\end{code}

For the sake of symmetry, we may considered an equivalent "unbiased" form of
depolarization, which requires propositional truncation.

\begin{code}
 module unbiased-depolarization (pt : propositional-truncations-exist) where
  open PropositionalTruncation pt

  depolarization : ğ“¤ âŠ” ğ“¥ Ì‡
  depolarization = is-pos-depolarized + is-neg-depolarized

  is-depolarized : ğ“¤ âŠ” ğ“¥ Ì‡
  is-depolarized = âˆ¥ depolarization âˆ¥
\end{code}

When a deductive system is depolarized in the unbiased sense, it is both
positively and negatively depolarized. Hence, all notions of depolarization are
equivalent.

\begin{code}
  module _ (H : is-depolarized) where
   is-depolarized-gives-is-pos-depolarized : is-pos-depolarized
   is-depolarized-gives-is-pos-depolarized A B f U V g h =
    âˆ¥âˆ¥-rec (âŠ¢-is-set _ _) case H
    where
     case : depolarization â†’ cut (cut h g) f ï¼ cut h (cut g f)
     case (inl pos) =
      pos A B f U V g h
     case (inr neg) =
      is-neg-depolarized-gives-is-pos-depolarized
       neg
       A B f U V g h

   is-depolarized-gives-is-neg-depolarized : is-neg-depolarized
   is-depolarized-gives-is-neg-depolarized =
    is-pos-depolarized-gives-is-neg-depolarized
     is-depolarized-gives-is-pos-depolarized


depolarized-deductive-system : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
depolarized-deductive-system ğ“¤ ğ“¥ =
 Î£ ğ““ ê‰ deductive-system ğ“¤ ğ“¥ ,
 is-pos-depolarized ğ““

depolarized-deductive-system-to-precategory
 : depolarized-deductive-system ğ“¤ ğ“¥
 â†’ precategory ğ“¤ ğ“¥
depolarized-deductive-system-to-precategory (ğ““ , H) =
 let open depolarization-and-precategories in
 precategory-of-depolarized-deductive-system ğ““ H

precategory-to-depolarized-deductive-system
 : precategory ğ“¤ ğ“¥
 â†’ depolarized-deductive-system ğ“¤ ğ“¥
precategory-to-depolarized-deductive-system ğ“’ =
 ğ““ , precategory-gives-pos-depolarized ğ““ (precategory.ax ğ“’)
 where
  open precategory ğ“’
  open depolarization-and-precategories
  ğ““ : deductive-system _ _
  ğ““ = make (precategory.str ğ“’) (hom-is-set , idn-L , idn-R)

depolarized-deductive-system-to-precategory-is-equiv
 : is-equiv (depolarized-deductive-system-to-precategory {ğ“¤} {ğ“¥})
depolarized-deductive-system-to-precategory-is-equiv = H
 where
  H : is-equiv (depolarized-deductive-system-to-precategory {ğ“¤} {ğ“¥})
  prâ‚ H =
   precategory-to-depolarized-deductive-system , Î» ğ“’ â†’
    equivs-are-lc
     precategory-as-sum.to-sum
     precategory-as-sum.to-sum-is-equiv
     (to-Î£-ï¼ (refl , precategory-axioms-is-prop (precategory.str ğ“’) _ _))
  prâ‚‚ H =
   precategory-to-depolarized-deductive-system ,
   Î» (ğ““ , _) â†’ to-Î£-ï¼ (refl , being-pos-depolarized-is-prop ğ““ _ _)
\end{code}
