Martin Escardo, 20th December 2021

Some thoughts about Dedekind reals.

We generalize the rationals to any type with a prop-valued,
irreflexive relation _<_.

To show that the Dedekind reals agree with their version proposed by Troelstra,
we further assume that _<_ is dense, upper open, and satisfies p â‰¢ q â†’
Â¬(q < p) â†’ p < q (which the type of rationals does).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-PropTrunc
open import UF-FunExt
open import UF-Subsingletons

module Dedekind
        (pt  : propositional-truncations-exist)
        (fe' : FunExt)
        (pe' : PropExt)
        {ð“£  : Universe}
        (â„š   : ð“£ Ì‡ )
        (_<_ : â„š â†’ â„š â†’ ð“£ Ì‡ )
        (order-is-prop-valued : (p q : â„š) â†’ is-prop (p < q))
        (order-is-irrefl      : (q : â„š) â†’ Â¬(q < q))
       where

fe : Fun-Ext
fe {ð“¤} {ð“¥} = fe' ð“¤ ð“¥

pe : Prop-Ext
pe {ð“¤} = pe' ð“¤

open PropositionalTruncation pt

record further-properties-of-â„š-and-its-order : ð“£ Ì‡ where
 constructor
  further
 field
  â„š-is-inhabited  : âˆ¥ â„š âˆ¥
  â„š-is-dense      : (p r : â„š) â†’ p < r â†’ âˆƒ q êž‰ â„š , (p < q) Ã— (q < r)
  transitivity    : (p q r : â„š) â†’ p < q â†’ q < r â†’ p < r
  order-criterion : (p q : â„š) â†’ p â‰¢ q â†’ Â¬(q < p) â†’ p < q
  cotransitivity  : (p q r : â„š) â†’ p < r â†’ (p < q) âˆ¨ (q < r)
  â„š-is-lower-open : (q : â„š) â†’ âˆƒ p êž‰ â„š , (p < q)
  â„š-is-upper-open : (p : â„š) â†’ âˆƒ q êž‰ â„š , (p < q)

open import UF-Powerset
open import UF-Subsingletons-FunExt

ð“£âº = ð“£ âº

is-lower : ð“Ÿ â„š â†’ ð“£ Ì‡
is-lower L = (q : â„š) â†’ q âˆˆ L â†’ (p : â„š) â†’ p < q â†’ p âˆˆ L

is-upper-open : ð“Ÿ â„š â†’ ð“£ Ì‡
is-upper-open A = (p : â„š) â†’ p âˆˆ A â†’ âˆƒ p' êž‰ â„š , ((p < p') Ã— p' âˆˆ A)

is-upper : ð“Ÿ â„š â†’ ð“£ Ì‡
is-upper U = (p : â„š) â†’ p âˆˆ U â†’ (q : â„š) â†’ p < q â†’ q âˆˆ U

is-lower-open : ð“Ÿ â„š â†’ ð“£ Ì‡
is-lower-open A = (q : â„š) â†’ q âˆˆ A â†’ âˆƒ q' êž‰ â„š , ((q' < q) Ã— q' âˆˆ A)

is-lower-real : ð“Ÿ â„š â†’ ð“£ Ì‡
is-lower-real L = is-inhabited L Ã— is-lower L Ã— is-upper-open L

is-upper-real : ð“Ÿ â„š â†’ ð“£ Ì‡
is-upper-real U = is-inhabited U Ã— is-upper U Ã— is-lower-open U

being-lower-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower L)
being-lower-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-upper-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-open L)
being-upper-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-lower-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-real L)
being-lower-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-lower-is-prop L)
                               (being-upper-open-is-prop L)

being-upper-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper L)
being-upper-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-lower-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-open L)
being-lower-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-upper-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-real L)
being-upper-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-upper-is-prop L)
                               (being-lower-open-is-prop L)
\end{code}

The set of lower reals:

\begin{code}

ð•ƒ : ð“£âº  Ì‡
ð•ƒ = Î£ L êž‰ ð“Ÿ â„š , is-lower-real L

ð•ƒ-is-set : is-set ð•ƒ
ð•ƒ-is-set = subsets-of-sets-are-sets (ð“Ÿ â„š) is-lower-real
            (powersets-are-sets'' fe fe pe)
            (Î» {l} â†’ being-lower-real-is-prop l)

\end{code}

The set of upper reals:

\begin{code}

ð•Œ : ð“£âº  Ì‡
ð•Œ = Î£ U êž‰ ð“Ÿ â„š , is-upper-real U

\end{code}

Next we define the set of Dedekind reals after some preparation.

\begin{code}

are-ordered : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“£  Ì‡
are-ordered L U = (p q : â„š) â†’ p âˆˆ L â†’ q âˆˆ U â†’ p < q

are-located : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“£  Ì‡
are-located L U = (p q : â„š) â†’ p < q â†’ p âˆˆ L âˆ¨ q âˆˆ U

being-ordered-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-ordered L U)
being-ordered-is-prop _ _ = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ order-is-prop-valued _ _)

being-located-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-located L U)
being-located-is-prop _ _ = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

technical-lemma : (L L' U U' : ð“Ÿ â„š)
                â†’ is-lower-open U'
                â†’ are-located L  U
                â†’ are-ordered L' U'
                â†’ L  âŠ† L'
                â†’ U' âŠ† U
technical-lemma L L' U U'
       U'-lower-open
       LU-located
       LU'-ordered
       L-is-contained-in-L'
       q
       q-is-in-U'  = Î³
 where
  I : âˆƒ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U'
  I = U'-lower-open q q-is-in-U'

  II : (Î£ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U') â†’ q âˆˆ U
  II (q' , l , i) = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) V III
   where
    III : q' âˆˆ L âˆ¨ q âˆˆ U
    III = LU-located q' q l

    IV : Â¬ (q' âˆˆ L)
    IV j = order-is-irrefl q' b
     where
      a : q' âˆˆ L'
      a = L-is-contained-in-L' q' j

      b : q' < q'
      b = LU'-ordered q' q' a i

    V : (q' âˆˆ L) + (q âˆˆ U) â†’ q âˆˆ U
    V (inl j) = ðŸ˜-elim (IV j)
    V (inr k) = k

  Î³ : q âˆˆ U
  Î³ = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) II I

_upper-section-of_ : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“£ Ì‡
U upper-section-of L = is-lower-open U Ã— are-ordered L U Ã— are-located L U

any-two-upper-sections-are-equal : (L U U' : ð“Ÿ â„š)
                                 â†’ U  upper-section-of L
                                 â†’ U' upper-section-of L
                                 â†’ U â‰¡ U'
any-two-upper-sections-are-equal L U U' (a , b , c) (u , v , w) = Î³
 where
  i : U âŠ† U'
  i = technical-lemma L L U' U a w b (âŠ†-refl' L)

  j : U' âŠ† U
  j = technical-lemma L L U U' u c v (âŠ†-refl' L)

  Î³ : U â‰¡ U'
  Î³ = subset-extensionality'' pe fe fe i j

_is-upper-section-of_ : ð•Œ â†’ ð•ƒ â†’ ð“£ Ì‡
(U , _) is-upper-section-of  (L , _) = are-ordered L U Ã— are-located L U

being-upper-section-is-prop : (l : ð•ƒ) (u : ð•Œ) â†’ is-prop (u is-upper-section-of l)
being-upper-section-is-prop (L , _) (U , _) = Ã—-is-prop
                                               (being-ordered-is-prop L U)
                                               (being-located-is-prop L U)

at-most-one-upper-section : (l : ð•ƒ) (uâ‚€ uâ‚ : ð•Œ)
                          â†’ uâ‚€ is-upper-section-of l
                          â†’ uâ‚ is-upper-section-of l
                          â†’ uâ‚€ â‰¡ uâ‚
at-most-one-upper-section (L , l)
                          uâ‚€@(Uâ‚€ , _ , _ , Uâ‚€-is-lower-open)
                          uâ‚@(Uâ‚ , _ , _ , Uâ‚-is-lower-open)
                          (luâ‚€-ordered , luâ‚€-located)
                          (luâ‚-ordered , luâ‚-located)      = Î³
 where
  Î³ : uâ‚€ â‰¡ uâ‚
  Î³ = to-subtype-â‰¡
        being-upper-real-is-prop
        (any-two-upper-sections-are-equal L Uâ‚€ Uâ‚
            (Uâ‚€-is-lower-open , luâ‚€-ordered , luâ‚€-located)
            (Uâ‚-is-lower-open , luâ‚-ordered , luâ‚-located))

\end{code}

The Dedekind condition for a lower real:

\begin{code}

is-dedekind : ð•ƒ â†’ ð“£âº Ì‡
is-dedekind l = Î£ u êž‰ ð•Œ , (u is-upper-section-of l)

being-dedekind-is-prop : (l : ð•ƒ) â†’ is-prop (is-dedekind l)
being-dedekind-is-prop l (uâ‚€ , pâ‚€) (uâ‚ , pâ‚) = to-subtype-â‰¡
                                                 (being-upper-section-is-prop l)
                                                 (at-most-one-upper-section l uâ‚€ uâ‚ pâ‚€ pâ‚)

\end{code}

We define the the Dedekind reals as a subset of the lower reals:

\begin{code}

â„ : ð“£âº  Ì‡
â„ = Î£ l êž‰ ð•ƒ , is-dedekind l

\end{code}

The following shows that there is some redundancy in the definition of
Dedekind real:

\begin{code}

subset-with-upper-section-is-lower : (L : ð“Ÿ â„š)
                                   â†’ (Î£ U êž‰ ð“Ÿ â„š , U upper-section-of L)
                                   â†’ is-lower L
subset-with-upper-section-is-lower L
  (U , U-lower-open , LU-ordered , LU-located ) = Î³
 where
  Î³ : is-lower L
  Î³ q l p m = âˆ¥âˆ¥-rec (âˆˆ-is-prop L p) b a
   where
    a : p âˆˆ L âˆ¨ q âˆˆ U
    a = LU-located p q m

    b : (p âˆˆ L) + (q âˆˆ U) â†’ p âˆˆ L
    b (inl u) = u
    b (inr v) = ðŸ˜-elim (order-is-irrefl q (LU-ordered q q l v))

\end{code}

The forgetful map of the reals into the lower reals is an embedding
and hence â„ is a set:

\begin{code}

â„-to-ð•ƒ : â„ â†’ ð•ƒ
â„-to-ð•ƒ = prâ‚

open import UF-Embeddings

â„-to-ð•ƒ-is-embedding : is-embedding â„-to-ð•ƒ
â„-to-ð•ƒ-is-embedding = prâ‚-is-embedding being-dedekind-is-prop

â„-is-set : is-set â„
â„-is-set = subsets-of-sets-are-sets ð•ƒ is-dedekind
             ð•ƒ-is-set
             (Î» {l} â†’ being-dedekind-is-prop l)
\end{code}

We now consider an alternative definition of the Dedekind reals
offered by Troelstra.

\begin{code}

is-bounded-above : ð“Ÿ â„š â†’ ð“£ Ì‡
is-bounded-above L = âˆƒ s êž‰ â„š , s âˆ‰ L

being-bounded-above-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-bounded-above L)
being-bounded-above-is-prop L = âˆƒ-is-prop

is-troelstra-located : ð“Ÿ â„š â†’ ð“£ Ì‡
is-troelstra-located L = ((r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L)

being-troelstra-located-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-troelstra-located L)
being-troelstra-located-is-prop L = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

is-troelstra : ð•ƒ â†’ ð“£ Ì‡
is-troelstra (L , _) = is-bounded-above L Ã— is-troelstra-located L

being-troelstra-is-prop : (l : ð•ƒ) â†’ is-prop (is-troelstra l)
being-troelstra-is-prop (L , _) = Ã—-is-prop
                                   (being-bounded-above-is-prop L)
                                   (being-troelstra-located-is-prop L)

\end{code}

The Dedekind and Troelstra conditions are equivalent:

\begin{code}

dedekind-gives-troelstra : (l : ð•ƒ) â†’ is-dedekind l â†’ is-troelstra l
dedekind-gives-troelstra (L , _ , _ , _)
                        ((U , U-is-inhabited , _ , _) , LU-ordered , LU-located) = a , b
 where
  a : (âˆƒ s êž‰ â„š , s âˆ‰ L)
  a = âˆ¥âˆ¥-functor f U-is-inhabited
   where
    f : (Î£ q êž‰ â„š , q âˆˆ U) â†’ Î£ q êž‰ â„š , q âˆ‰ L
    f (q , q-is-in-U) = q , (Î» q-is-in-L â†’ order-is-irrefl q (c q-is-in-L))
     where
      c : q âˆˆ L â†’ q < q
      c q-is-in-L = LU-ordered q q q-is-in-L q-is-in-U

  b : (r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L
  b r s less = âˆ¥âˆ¥-functor f (LU-located r s less)
   where
    f : (r âˆˆ L) + (s âˆˆ U) â†’ (r âˆˆ L) + (s âˆ‰ L)
    f (inl r-is-in-L) = inl r-is-in-L
    f (inr r-is-in-L) = inr (Î» s-is-in-L â†’ order-is-irrefl s (d s-is-in-L))
     where
      d : s âˆˆ L â†’ s < s
      d s-is-in-L = LU-ordered s s s-is-in-L r-is-in-L

\end{code}

For the converse, we need the further assumptions on _<_ mentioned
above:

\begin{code}

open further-properties-of-â„š-and-its-order

troelstra-gives-dedekind : further-properties-of-â„š-and-its-order
                         â†’ (l : ð•ƒ) â†’ is-troelstra l â†’ is-dedekind l
troelstra-gives-dedekind Ï• l@(L , L-is-inhabited , L-is-lower , L-is-upper-open) (a , b) = Î³
 where
  U : ð“Ÿ â„š
  U q = (âˆƒ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) , âˆƒ-is-prop

  U-is-inhabited : is-inhabited U
  U-is-inhabited = âˆ¥âˆ¥-rec (being-inhabited-is-prop U) f a
   where
    f : (Î£ s êž‰ â„š , s âˆ‰ L) â†’ is-inhabited U
    f (s , Î½) = âˆ¥âˆ¥-functor g (â„š-is-upper-open Ï• s)
     where
      g : (Î£ p êž‰ â„š , s < p) â†’ Î£ p êž‰ â„š , p âˆˆ U
      g (p , i) = p , âˆ£ s , i , Î½ âˆ£

  LU-ordered : are-ordered L U
  LU-ordered p q p-is-in-L q-is-in-U = âˆ¥âˆ¥-rec (order-is-prop-valued p q) f q-is-in-U
   where
    f : (Î£ r êž‰ â„š , (r < q) Ã— (r âˆ‰ L)) â†’ p < q
    f (r , i , r-is-not-in-L) = âˆ¥âˆ¥-rec (order-is-prop-valued p q) g (b r q i)
     where
      g : (r âˆˆ L) + (q âˆ‰ L) â†’ p < q
      g (inl r-is-in-L)     = ðŸ˜-elim (r-is-not-in-L r-is-in-L)
      g (inr q-is-not-in-L) = order-criterion Ï• p q I II
       where
        I : p â‰¢ q
        I refl = q-is-not-in-L p-is-in-L

        II : Â¬(q < p)
        II less = q-is-not-in-L (L-is-lower p p-is-in-L q less)

  U-is-upper : is-upper U
  U-is-upper p p-is-in-U q less = âˆ£ p ,
                                   less ,
                                   (Î» p-is-in-L â†’ order-is-irrefl p
                                                   (LU-ordered p p p-is-in-L p-is-in-U)) âˆ£

  U-is-lower-open : is-lower-open U
  U-is-lower-open q q-is-in-U = âˆ¥âˆ¥-rec âˆƒ-is-prop f q-is-in-U
   where
    f : (Î£ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) â†’ âˆƒ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
    f (p , i , p-is-not-in-L) = âˆ¥âˆ¥-functor g (â„š-is-dense Ï• p q i)
     where
      g : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q))
        â†’ Î£ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
      g (p' , j , k) = p' , k , âˆ£ p , j , p-is-not-in-L âˆ£

  LU-located : are-located L U
  LU-located p q less = âˆ¥âˆ¥-rec âˆ¨-is-prop II I
   where
    I : âˆƒ p' êž‰ â„š , (p < p') Ã— (p' < q)
    I = â„š-is-dense Ï• p q less

    II : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q)) â†’ p âˆˆ L âˆ¨ q âˆˆ U
    II (p' , i , j) = âˆ¥âˆ¥-rec âˆ¨-is-prop IV III
     where
      III : p âˆˆ L âˆ¨ p' âˆ‰ L
      III = b p p' i

      IV : (p âˆˆ L) + (p' âˆ‰ L) â†’ p âˆˆ L âˆ¨ q âˆˆ U
      IV (inl p-is-in-L) = âˆ£ inl p-is-in-L âˆ£
      IV (inr p'-is-not-in-L) = âˆ£ inr âˆ£ (p' , j , p'-is-not-in-L) âˆ£ âˆ£

  Î³ : is-dedekind l
  Î³ = (U , (U-is-inhabited , U-is-upper , U-is-lower-open)) , LU-ordered , LU-located


\end{code}

The set of Troelstra reals, again as a subset of the lower reals:

\begin{code}

ð•‹ : ð“£âº Ì‡
ð•‹ = Î£ l êž‰ ð•ƒ , is-troelstra l

\end{code}

Question. Can we prove that â„ = ð•‹ with propositional and functional
extensionality, without univalence? The problem is that the Dedekind
condition and the troelstra condition have different universe levels,
and hence propositional extensionality is not applicable to show that
they are equal, as their equality doesn't even type check. Would
universe lifting help? I haven't thought about this.

\begin{code}

open import UF-Equiv
open import UF-Univalence

dedekind-agrees-with-troelstra : further-properties-of-â„š-and-its-order â†’ â„ â‰ƒ ð•‹
dedekind-agrees-with-troelstra Ï• = Î³
 where
  f : â„ â†’ ð•‹
  f (l , h) = l , dedekind-gives-troelstra l h

  g : ð•‹ â†’ â„
  g (l , k) = l , troelstra-gives-dedekind Ï• l k

  Î³ : â„ â‰ƒ ð•‹
  Î³ = qinveq f (g ,
               (Î» (l , h) â†’ to-subtype-â‰¡ being-dedekind-is-prop refl) ,
               (Î» (l , k) â†’ to-subtype-â‰¡ being-troelstra-is-prop refl))

dedekind-agrees-with-troelstra' : further-properties-of-â„š-and-its-order
                                â†’ is-univalent (ð“£ âº)
                                â†’ â„ â‰¡ ð•‹
dedekind-agrees-with-troelstra' Ï• ua = eqtoid ua â„ ð•‹ (dedekind-agrees-with-troelstra Ï•)

\end{code}

We now consider consequences of excluded middle.

\begin{code}

open import UF-ExcludedMiddle

EM-gives-troelstra-locatedness : EM ð“£ â†’ ((L , _) : ð•ƒ) â†’ is-troelstra-located L
EM-gives-troelstra-locatedness
  em l@(L , L-is-inhabited , L-is-lower , L-is-upper-open) r s less = Î³ Î´
 where
  Î´ : (s âˆˆ L) + (s âˆ‰ L)
  Î´ = em (s âˆˆ L) (âˆˆ-is-prop L s)

  Î³ : type-of Î´ â†’ (r âˆˆ L) âˆ¨ (s âˆ‰ L)
  Î³ (inl s-is-in-L)     = âˆ£ inl (L-is-lower s s-is-in-L r less) âˆ£
  Î³ (inr s-is-not-in-L) = âˆ£ inr s-is-not-in-L âˆ£

\end{code}

The bounded lower reals:

\begin{code}

ð•ƒÎ² : ð“£ âº Ì‡
ð•ƒÎ² = Î£ (L , _) êž‰ ð•ƒ , is-bounded-above L

\end{code}

The boundedness condition only excludes a point at infinity in the
lower reals:

\begin{code}

âˆž : ð“Ÿ â„š
âˆž = Î» q â†’ âŠ¤Î©

âˆž-is-lower-real-but-not-bounded-above : further-properties-of-â„š-and-its-order
                                      â†’ (is-lower-real âˆž) Ã— (Â¬ is-bounded-above âˆž)
âˆž-is-lower-real-but-not-bounded-above Ï• = a , b
 where
  a : is-lower-real âˆž
  a = âˆ¥âˆ¥-rec (being-inhabited-is-prop âˆž) (Î» q â†’ âˆ£ q , * âˆ£) (â„š-is-inhabited Ï•) ,
      (Î» _ _ _ _ â†’ *) ,
      (Î» p * â†’ âˆ¥âˆ¥-rec âˆƒ-is-prop (Î» (q , i) â†’ âˆ£ q , i , * âˆ£) (â„š-is-upper-open Ï• p))

  b : Â¬ is-bounded-above âˆž
  b bounded = âˆ¥âˆ¥-rec ðŸ˜-is-prop (Î» (q , q-is-not-in-âˆž) â†’ q-is-not-in-âˆž *) bounded

\end{code}

In connection with a discussion above, notice that we don't need
univalence for the following, which says that the Troelstra reals
agree with the bounded lower reals if we assume excluded middle:

\begin{code}

ð•‹-and-ð•ƒÎ²-agree-under-EM : EM ð“£ â†’ further-properties-of-â„š-and-its-order â†’ ð•‹ â‰¡ ð•ƒÎ²
ð•‹-and-ð•ƒÎ²-agree-under-EM em Ï• = ap Î£ Î³
 where
  Î´ : is-troelstra âˆ¼ Î» (L , _) â†’ is-bounded-above L
  Î´ l@(L , c) = pe (being-troelstra-is-prop l) (being-bounded-above-is-prop L)
                   prâ‚
                   Î» Î² â†’ Î² , EM-gives-troelstra-locatedness em l

  Î³ : is-troelstra â‰¡ (Î» (L , _) â†’ is-bounded-above L)
  Î³ = dfunext fe Î´

\end{code}

And assuming both univalence and excluded middle, the Dedekind reals
agree with the bounded lower reals:

\begin{code}

â„-and-ð•ƒÎ²-agree-under-EM : EM ð“£
                        â†’ further-properties-of-â„š-and-its-order
                        â†’ is-univalent ð“£âº
                        â†’ â„ â‰¡ ð•ƒÎ²
â„-and-ð•ƒÎ²-agree-under-EM em Ï• ua = dedekind-agrees-with-troelstra' Ï• ua
                                âˆ™ ð•‹-and-ð•ƒÎ²-agree-under-EM em Ï•
\end{code}

We also need further properties of order for embedding the rationals into the reals:

\begin{code}

module rational-reals (Ï• : further-properties-of-â„š-and-its-order) where

 â„š-to-ð•ƒ : â„š â†’ ð•ƒ
 â„š-to-ð•ƒ q = L ,
            â„š-is-lower-open Ï• q ,
            (Î» p i r j â†’ transitivity Ï• r p q j i) ,
            (Î» p â†’  â„š-is-dense Ï• p q)
  where
   L : ð“Ÿ â„š
   L p = (p < q) , order-is-prop-valued p q

 â„š-to-ð•Œ : â„š â†’ ð•Œ
 â„š-to-ð•Œ q = U ,
            â„š-is-upper-open Ï• q ,
            (Î» p i r j â†’ transitivity Ï• q p r i j) ,
            (Î» p i â†’ âˆ¥âˆ¥-functor (Î» (r , j , k) â†’ r , k , j) (â„š-is-dense Ï• q p i))
  where
   U : ð“Ÿ â„š
   U p = (q < p) , order-is-prop-valued q p


 â„š-to-ð•Œ-is-upper-section-of-â„š-to-ð•ƒ : (q : â„š) â†’ (â„š-to-ð•Œ q) is-upper-section-of (â„š-to-ð•ƒ q)
 â„š-to-ð•Œ-is-upper-section-of-â„š-to-ð•ƒ q = (Î» p â†’ transitivity Ï• p q) , (Î» p â†’ cotransitivity Ï• p q)

 â„š-to-ð•ƒ-is-dedekind : (q : â„š) â†’ is-dedekind (â„š-to-ð•ƒ q)
 â„š-to-ð•ƒ-is-dedekind q = â„š-to-ð•Œ q , â„š-to-ð•Œ-is-upper-section-of-â„š-to-ð•ƒ q

 â„š-to-â„ : â„š â†’ â„
 â„š-to-â„ q = â„š-to-ð•ƒ q , â„š-to-ð•ƒ-is-dedekind q

{- TODO.
 â„š-to-â„-is-embedding : is-embedding â„š-to-â„
 â„š-to-â„-is-embedding = {!!}
-}

\end{code}

TODO. Derive a constructive taboo from the assumption that every
bounded lower real is a Dedekind real.

\begin{code}
{-
blah : (A : ð“£ Ì‡ ) â†’ is-prop A â†’ â„š â†’ â„š â†’ ð•ƒ
blah A i pâ‚€ pâ‚ = L , L-is-inhabited , L-is-lower , L-is-upper-open
 where
  L : ð“Ÿ â„š
  L p = (((p < pâ‚€) Ã— (A â†’ p < pâ‚)) ,
        Ã—-is-prop
         (order-is-prop-valued p pâ‚€)
         (Î -is-prop fe (Î» _ â†’ order-is-prop-valued p pâ‚)))


  L-is-inhabited : is-inhabited L
  L-is-inhabited = {!!}

  L-is-lower : is-lower L
  L-is-lower = {!!}

  L-is-upper-open : is-upper-open L
  L-is-upper-open = {!!}
-}
\end{code}

TODO. Define Dedekind completeness and show that â„ is Dedekind complete.
