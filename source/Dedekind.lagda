Martin Escardo, 20th December 2021

Some thoughts about Dedekind reals.

A Dedekind real in constructive type theory is defined as a triple
(L , U , p) where L and U are data, namely given sets of rational
numbers, and p is property of (L , U).

But actually, given a lower Dedekind section L, there is at most one
pair (U , p) such that (L , U , p) is a Dedekind real. Hence the
Dedekind data (U , p) is property of the lower real L rather than
data. A more precise statement of this phenomenon is given below.

We generalize the rationals to any type with a proposition-valued,
irreflexive relation _<_, simply to avoid having to define the
rational numbers. But also it is interesting than nothing other than
a proposition-valued irreflexive relation is needed for the above
discussion.

We also discuss a version of the Dedekind reals proposed by Troelstra.
To show that it agrees with the usual one, we further assume that _<_
is dense, upper open, and satisfies p â‰¢ q â†’ p â‰® q â†’ p < q (which the
type of rationals does).

We also discuss what happens when we assume the principle of
excluded middle.

Here we adopt HoTT/UF as our type-theoretic foundation, which, in
particular, is well-suited to discuss the distinction between data and
property. The univalence axiom is not used anywhere here, but we
mention it in some discussions.

See also the discussion at https://twitter.com/EscardoMartin/status/1473393261012295681

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-Base
open import UF-PropTrunc
open import UF-FunExt
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-Powerset
open import UF-Embeddings
open import UF-Equiv
open import StrictOrder
open import CanonicalMap

module Dedekind
        (pt  : propositional-truncations-exist)
        (fe  : Fun-Ext)
        (pe  : Prop-Ext)
        {ð“¤  : Universe}
        (â„š   : ð“¤ Ì‡ )
        (less-than            : â„š â†’ â„š â†’ ð“¤ Ì‡ )
        (order-is-prop-valued : (p q : â„š) â†’ is-prop (less-than p q))
        (order-is-irrefl      : (q : â„š) â†’ Â¬(less-than q q))
       where

open PropositionalTruncation pt

instance
 strict-order-â„š : Strict-Order â„š â„š
 _<_ {{strict-order-â„š}} = less-than

ð“¤âº = ð“¤ âº

\end{code}

Lower-real conditions:

\begin{code}

is-lower : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower L = (q : â„š) â†’ q âˆˆ L â†’ (p : â„š) â†’ p < q â†’ p âˆˆ L

is-upper-open : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper-open L = (p : â„š) â†’ p âˆˆ L â†’ âˆƒ p' êž‰ â„š , ((p < p') Ã— p' âˆˆ L)

is-lower-real : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower-real L = is-inhabited L Ã— is-lower L Ã— is-upper-open L

\end{code}

Upper-real conditions:

\begin{code}

is-upper : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper U = (p : â„š) â†’ p âˆˆ U â†’ (q : â„š) â†’ p < q â†’ q âˆˆ U

is-lower-open : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower-open U = (q : â„š) â†’ q âˆˆ U â†’ âˆƒ q' êž‰ â„š , ((q' < q) Ã— q' âˆˆ U)

is-upper-real : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper-real U = is-inhabited U Ã— is-upper U Ã— is-lower-open U

\end{code}

The conditions are property:

\begin{code}

being-lower-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower L)
being-lower-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-upper-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-open L)
being-upper-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-lower-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-real L)
being-lower-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-lower-is-prop L)
                               (being-upper-open-is-prop L)

being-upper-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper L)
being-upper-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-lower-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-open L)
being-lower-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-upper-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-real L)
being-upper-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-upper-is-prop L)
                               (being-lower-open-is-prop L)
\end{code}

The sets of lower and upper reals:

\begin{code}

â„á´¸ : ð“¤âº Ì‡
â„á´¸ = Î£ L êž‰ ð“Ÿ â„š , is-lower-real L

â„áµ : ð“¤âº Ì‡
â„áµ = Î£ U êž‰ ð“Ÿ â„š , is-upper-real U

â„á´¸-is-set : is-set â„á´¸
â„á´¸-is-set = subsets-of-sets-are-sets (ð“Ÿ â„š)
             is-lower-real
             (powersets-are-sets'' fe fe pe)
             (Î» {l} â†’ being-lower-real-is-prop l)

â„áµ-is-set : is-set â„áµ
â„áµ-is-set = subsets-of-sets-are-sets (ð“Ÿ â„š)
             is-upper-real
             (powersets-are-sets'' fe fe pe)
             (Î» {l} â†’ being-upper-real-is-prop l)
\end{code}

Next we define the set of Dedekind reals as a subset of the lower
reals, after some preparation.

\begin{code}

are-ordered : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤  Ì‡
are-ordered L U = (p q : â„š) â†’ p âˆˆ L â†’ q âˆˆ U â†’ p < q

are-located : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤  Ì‡
are-located L U = (p q : â„š) â†’ p < q â†’ p âˆˆ L âˆ¨ q âˆˆ U

being-ordered-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-ordered L U)
being-ordered-is-prop _ _ = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ order-is-prop-valued _ _)

being-located-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-located L U)
being-located-is-prop _ _ = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

technical-lemma : (L U L' U' : ð“Ÿ â„š)
                â†’ is-lower-open U'
                â†’ are-located L  U
                â†’ are-ordered L' U'
                â†’ L  âŠ† L'
                â†’ U' âŠ† U
technical-lemma L U L' U'
                U'-lower-open
                LU-located
                LU'-ordered
                L-contained-in-L'
                q
                q-in-U'             = Î³
 where
  I : âˆƒ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U'
  I = U'-lower-open q q-in-U'

  II : (Î£ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U') â†’ q âˆˆ U
  II (q' , l , i) = VI
   where
    III : q' âˆˆ L âˆ¨ q âˆˆ U
    III = LU-located q' q l

    IV : q' âˆ‰ L
    IV j = order-is-irrefl q' b
     where
      a : q' âˆˆ L'
      a = L-contained-in-L' q' j

      b : q' < q'
      b = LU'-ordered q' q' a i

    V : (q' âˆˆ L) + (q âˆˆ U) â†’ q âˆˆ U
    V (inl j) = ðŸ˜-elim (IV j)
    V (inr k) = k

    VI : q âˆˆ U
    VI = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) V III

  Î³ : q âˆˆ U
  Î³ = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) II I


technical-lemma-converse : (L U L' U' : ð“Ÿ â„š)
                         â†’ is-upper-open L
                         â†’ are-located L' U'
                         â†’ are-ordered L  U
                         â†’ U' âŠ† U
                         â†’ L  âŠ† L'
technical-lemma-converse L U L' U'
                L-upper-open
                LU'-located
                LU-ordered
                U'-contained-in-U
                q
                q-in-L             = Î³
 where
  I : âˆƒ q' êž‰ â„š , (q < q') Ã— q' âˆˆ L
  I = L-upper-open q q-in-L

  II : (Î£ q' êž‰ â„š , (q < q') Ã— q' âˆˆ L) â†’ q âˆˆ L'
  II (q' , l , i) = VI
   where
    III : q âˆˆ L' âˆ¨ q' âˆˆ U'
    III = LU'-located q q' l

    IV : q' âˆ‰ U'
    IV j = order-is-irrefl q' b
     where
      a : q' âˆˆ U
      a = U'-contained-in-U q' j

      b : q' < q'
      b = LU-ordered q' q' i a

    V : (q âˆˆ L') + (q' âˆˆ U') â†’ q âˆˆ L'
    V (inl j) = j
    V (inr k) = ðŸ˜-elim (IV k)

    VI : q âˆˆ L'
    VI = âˆ¥âˆ¥-rec (âˆˆ-is-prop L' q) V III

  Î³ : q âˆˆ L'
  Î³ = âˆ¥âˆ¥-rec (âˆˆ-is-prop L' q) II I

\end{code}

The following definition is of an auxiliary character:

\begin{code}

_is-an-upper-section-of_ : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤ Ì‡
U is-an-upper-section-of L = is-lower-open U Ã— are-ordered L U Ã— are-located L U

any-two-upper-sections-are-equal : (L U U' : ð“Ÿ â„š)
                                 â†’ U  is-an-upper-section-of L
                                 â†’ U' is-an-upper-section-of L
                                 â†’ U â‰¡ U'
any-two-upper-sections-are-equal L U U' (a , b , c) (u , v , w) = Î³
 where
  i : U âŠ† U'
  i = technical-lemma L U' L U  a w b (âŠ†-refl' L)

  j : U âŠ‡ U'
  j = technical-lemma L U  L U' u c v (âŠ†-refl' L)

  Î³ : U â‰¡ U'
  Î³ = subset-extensionality'' pe fe fe i j

\end{code}

The following is the version of the definition we are interested in:

\begin{code}

_is-upper-section-of_ : â„áµ â†’ â„á´¸ â†’ ð“¤ Ì‡
(U , _) is-upper-section-of  (L , _) = are-ordered L U Ã— are-located L U

being-upper-section-is-prop : (l : â„á´¸) (u : â„áµ)
                            â†’ is-prop (u is-upper-section-of l)
being-upper-section-is-prop (L , _) (U , _) = Ã—-is-prop
                                               (being-ordered-is-prop L U)
                                               (being-located-is-prop L U)
\end{code}

We use the above auxiliary definition and lemma to establish the following:

\begin{code}

at-most-one-upper-section : (l : â„á´¸) (uâ‚€ uâ‚ : â„áµ)
                          â†’ uâ‚€ is-upper-section-of l
                          â†’ uâ‚ is-upper-section-of l
                          â†’ uâ‚€ â‰¡ uâ‚
at-most-one-upper-section (L , _)
                          uâ‚€@(Uâ‚€ , _ , _ , Uâ‚€-is-lower-open)
                          uâ‚@(Uâ‚ , _ , _ , Uâ‚-is-lower-open)
                          (luâ‚€-ordered , luâ‚€-located)
                          (luâ‚-ordered , luâ‚-located)      = Î³
 where
  Î³ : uâ‚€ â‰¡ uâ‚
  Î³ = to-subtype-â‰¡
        being-upper-real-is-prop
        (any-two-upper-sections-are-equal L Uâ‚€ Uâ‚
            (Uâ‚€-is-lower-open , luâ‚€-ordered , luâ‚€-located)
            (Uâ‚-is-lower-open , luâ‚-ordered , luâ‚-located))
\end{code}

The Dedekind condition for a lower real:

\begin{code}

is-dedekind : â„á´¸ â†’ ð“¤âº Ì‡
is-dedekind l = Î£ u êž‰ â„áµ , (u is-upper-section-of l)

being-dedekind-is-prop : (l : â„á´¸) â†’ is-prop (is-dedekind l)
being-dedekind-is-prop l (uâ‚€ , pâ‚€) (uâ‚ , pâ‚) = to-subtype-â‰¡
                                                 (being-upper-section-is-prop l)
                                                 (at-most-one-upper-section l uâ‚€ uâ‚ pâ‚€ pâ‚)
\end{code}

We define the Dedekind reals as a subset of the lower reals:

\begin{code}

â„ : ð“¤âº Ì‡
â„ = Î£ l êž‰ â„á´¸ , is-dedekind l

\end{code}

The forgetful map of the reals into the lower reals is an embedding
and hence â„ is a set:

\begin{code}

â„-to-â„á´¸ : â„ â†’ â„á´¸
â„-to-â„á´¸ = prâ‚

â„-to-â„á´¸-is-embedding : is-embedding â„-to-â„á´¸
â„-to-â„á´¸-is-embedding = prâ‚-is-embedding being-dedekind-is-prop

â„-is-set : is-set â„
â„-is-set = subsets-of-sets-are-sets â„á´¸
             is-dedekind
             â„á´¸-is-set
             (Î» {l} â†’ being-dedekind-is-prop l)
\end{code}

NB. This won't be a *topological* embedding in topological
models. Because â„ and â„á´¸ are sets, in the sense of HoTT/UF, the
embedding condition merely says that the map is left-cancellable.

We unpack and reorder the definition to emphasize that it amounts to
the usual one:

\begin{code}

is-dedekind-section : ð“Ÿ â„š Ã— ð“Ÿ â„š â†’ ð“¤ Ì‡
is-dedekind-section (L , U) = is-inhabited L Ã— is-lower L Ã— is-upper-open L
                            Ã— is-inhabited U Ã— is-upper U Ã— is-lower-open U
                            Ã— are-ordered L U Ã— are-located L U


NBâ‚ : â„ â‰ƒ (Î£ (L , R) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š , is-dedekind-section (L , R))

NBâ‚ = qinveq (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
              â†’ ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , o , l))

            ((Î» ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , o , l)
              â†’ ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)) ,

             (Î» _ â†’ refl) ,

             (Î» _ â†’ refl))
\end{code}

The following shows that there is some redundancy in the definition of
Dedekind real:

\begin{code}

ordered-located-gives-lower : (L U : ð“Ÿ â„š)
                            â†’ are-ordered L U
                            â†’ are-located L U
                            â†’ is-lower L
ordered-located-gives-lower L U LU-ordered LU-located = Î³
 where
  Î³ : is-lower L
  Î³ q l p m = âˆ¥âˆ¥-rec (âˆˆ-is-prop L p) b a
   where
    a : p âˆˆ L âˆ¨ q âˆˆ U
    a = LU-located p q m

    b : (p âˆˆ L) + (q âˆˆ U) â†’ p âˆˆ L
    b (inl u) = u
    b (inr v) = ðŸ˜-elim (order-is-irrefl q (LU-ordered q q l v))

ordered-located-gives-upper : (L U : ð“Ÿ â„š)
                            â†’ are-ordered L U
                            â†’ are-located L U
                            â†’ is-upper U
ordered-located-gives-upper L U LU-ordered LU-located = Î³
 where
  Î³ : is-upper U
  Î³ q l p m = âˆ¥âˆ¥-rec (âˆˆ-is-prop U p) b a
   where
    a : q âˆˆ L âˆ¨ p âˆˆ U
    a = LU-located q p m

    b : (q âˆˆ L) + (p âˆˆ U) â†’ p âˆˆ U
    b (inl u) = ðŸ˜-elim (order-is-irrefl q (LU-ordered q q u l))
    b (inr v) = v


NBâ‚‚ : â„ â‰ƒ (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
                , is-inhabited L Ã— is-upper-open L
                Ã— is-inhabited U Ã— is-lower-open U
                Ã— are-ordered L U Ã— are-located L U)

NBâ‚‚ = qinveq (Î» ((L , Li , _ , Lo) , (U , Ui , _ , Uo) , o , l)
              â†’ ((L , U) , Li , Lo , Ui , Uo , o , l))

            ((Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
              â†’ ((L , Li , ordered-located-gives-lower L U o l , Lo) ,
                 (U , Ui , ordered-located-gives-upper L U o l , Uo) ,
                 o , l)) ,

             (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
              â†’ to-subtype-â‰¡ being-dedekind-is-prop
                  (to-subtype-â‰¡ being-lower-real-is-prop
                     refl)) ,

             (Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
              â†’ to-subtype-â‰¡ (Î» (L , U) â†’ Ã—â‚†-is-prop
                                           (being-inhabited-is-prop L)
                                           (being-upper-open-is-prop L)
                                           (being-inhabited-is-prop U)
                                           (being-lower-open-is-prop U)
                                           (being-ordered-is-prop L U)
                                           (being-located-is-prop L U))
                  refl))
\end{code}

Sometimes a disjointness condition rather than the order condition is
used in the definition of Dedekind reals.

\begin{code}

disjoint-criterion : (L U : ð“Ÿ â„š)
                   â†’ are-ordered L U
                   â†’ are-disjoint L U
disjoint-criterion L U o p (p-in-L , p-in-U) =
 order-is-irrefl p (o p p p-in-L p-in-U)

\end{code}

From now on we assume the properties of â„š and its order alluded above,
and a few more:

\begin{code}

module _ (â„š-is-dense        : (p r : â„š) â†’ p < r â†’ âˆƒ q êž‰ â„š , (p < q) Ã— (q < r))
         (transitivity      : (p q r : â„š) â†’ p < q â†’ q < r â†’ p < r)
         (order-criterion   : (p q : â„š) â†’ p â‰¢ q â†’ q â‰® p â†’ p < q)
         (cotransitivity    : (p q r : â„š) â†’ p < r â†’ (p < q) âˆ¨ (q < r))
         (tightness         : (p q : â„š) â†’ q â‰® p â†’ p â‰® q â†’ p â‰¡ q)
         (â„š-is-lower-open   : (q : â„š) â†’ âˆƒ p êž‰ â„š , (p < q))
         (â„š-is-upper-open   : (p : â„š) â†’ âˆƒ q êž‰ â„š , (p < q))
         (ðŸŽ Â½ ðŸ             : â„š)
         (ðŸŽ-is-less-than-Â½  : ðŸŽ < Â½)
         (Â½-is-less-than-ðŸ  : Â½ < ðŸ)
       where

 ðŸŽ-is-less-than-ðŸ : ðŸŽ < ðŸ
 ðŸŽ-is-less-than-ðŸ = transitivity ðŸŽ Â½ ðŸ ðŸŽ-is-less-than-Â½ Â½-is-less-than-ðŸ

 equality-criterion : (p q : â„š)
                    â†’ ((r : â„š) â†’ r < p â†’ r < q)
                    â†’ ((r : â„š) â†’ r < q â†’ r < p)
                    â†’ p â‰¡ q
 equality-criterion p q f g = tightness p q (Î» â„“ â†’ order-is-irrefl q (f q â„“))
                                            (Î» â„“ â†’ order-is-irrefl p (g p â„“))

 ordered-criterion : (L U : ð“Ÿ â„š)
                   â†’ is-lower L
                   â†’ are-disjoint L U
                   â†’ are-ordered L U
 ordered-criterion L U L-lower LU-disjoint p q p-in-L q-in-U = Î³
  where
   I : p âˆ‰ U
   I p-in-U = LU-disjoint p (p-in-L , p-in-U)

   II : p â‰¢ q
   II refl = I q-in-U

   III : q â‰® p
   III â„“ = LU-disjoint q (L-lower p p-in-L q â„“ , q-in-U)

   Î³ : p < q
   Î³ = order-criterion p q II III

\end{code}

The following alternative definition of the Dedekind reals is often
found in the literature:

\begin{code}

 NBâ‚ƒ : â„ â‰ƒ (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
                 , is-inhabited L Ã— is-lower L Ã— is-upper-open L
                 Ã— is-inhabited U Ã— is-upper U Ã— is-lower-open U
                 Ã— are-disjoint L U Ã— are-located L U)

 NBâ‚ƒ = qinveq (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
               â†’ ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , disjoint-criterion L U o , l))

             ((Î» ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , d , l)
               â†’ ((L , Li , Ll , Lo) ,
                  (U , Ui , Uu , Uo) ,
                  ordered-criterion L U Ll d , l)) ,

              (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
               â†’ to-subtype-â‰¡ being-dedekind-is-prop
                   (to-subtype-â‰¡ being-lower-real-is-prop
                      refl)) ,

              (Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
               â†’ to-subtype-â‰¡ (Î» (L , U) â†’ Ã—â‚ˆ-is-prop
                                            (being-inhabited-is-prop L)
                                            (being-lower-is-prop L)
                                            (being-upper-open-is-prop L)
                                            (being-inhabited-is-prop U)
                                            (being-upper-is-prop U)
                                            (being-lower-open-is-prop U)
                                            (being-disjoint-is-prop fe L U)
                                            (being-located-is-prop L U))
                   refl))
\end{code}

We now consider an alternative definition of the Dedekind reals
offered by Troelstra.

\begin{code}

 is-bounded-above : ð“Ÿ â„š â†’ ð“¤ Ì‡
 is-bounded-above L = âˆƒ s êž‰ â„š , s âˆ‰ L

 is-located : ð“Ÿ â„š â†’ ð“¤ Ì‡
 is-located L = ((r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L)

 is-troelstra : â„á´¸ â†’ ð“¤ Ì‡
 is-troelstra (L , _) = is-bounded-above L Ã— is-located L

 being-bounded-above-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-bounded-above L)
 being-bounded-above-is-prop L = âˆƒ-is-prop

 being-troelstra-located-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-located L)
 being-troelstra-located-is-prop L = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

 being-troelstra-is-prop : (l : â„á´¸) â†’ is-prop (is-troelstra l)
 being-troelstra-is-prop (L , _) = Ã—-is-prop
                                    (being-bounded-above-is-prop L)
                                    (being-troelstra-located-is-prop L)
\end{code}

The Dedekind and Troelstra conditions are equivalent:

\begin{code}

 dedekind-gives-troelstra : (l : â„á´¸) â†’ is-dedekind l â†’ is-troelstra l
 dedekind-gives-troelstra l@(L , _ , _ , _)
                           ((U , U-inhabited , _ , _) , LU-ordered , LU-located) = Î³
  where
   bounded : (âˆƒ s êž‰ â„š , s âˆ‰ L)
   bounded = âˆ¥âˆ¥-functor f U-inhabited
    where
     f : (Î£ q êž‰ â„š , q âˆˆ U) â†’ Î£ q êž‰ â„š , q âˆ‰ L
     f (q , q-in-U) = q , (Î» q-in-L â†’ order-is-irrefl q (c q-in-L))
      where
       c : q âˆˆ L â†’ q < q
       c q-in-L = LU-ordered q q q-in-L q-in-U

   located : (r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L
   located r s â„“ = âˆ¥âˆ¥-functor f (LU-located r s â„“)
    where
     f : (r âˆˆ L) + (s âˆˆ U) â†’ (r âˆˆ L) + (s âˆ‰ L)
     f (inl r-in-L) = inl r-in-L
     f (inr r-in-L) = inr (Î» s-in-L â†’ order-is-irrefl s (d s-in-L))
      where
       d : s âˆˆ L â†’ s < s
       d s-in-L = LU-ordered s s s-in-L r-in-L

   Î³ : is-troelstra l
   Î³ = bounded , located

\end{code}

A lower Dedekind real may or may not have an upper section. If it
does, it is given by the following candidate.

\begin{code}

 candidate-upper-section : ð“Ÿ â„š â†’ ð“Ÿ â„š
 candidate-upper-section L = Î» q â†’ (âˆƒ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) , âˆƒ-is-prop

 candidate-upper-section-is-lower-open : (L : ð“Ÿ â„š)
                                       â†’ is-lower-open (candidate-upper-section L)
 candidate-upper-section-is-lower-open L q q-in-U = Î³
  where
   f : (Î£ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) â†’ âˆƒ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
   f (p , i , p-not-in-L) = âˆ¥âˆ¥-functor g (â„š-is-dense p q i)
    where
     g : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q))
       â†’ Î£ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
     g (p' , j , k) = p' , k , âˆ£ p , j , p-not-in-L âˆ£

   Î³ : âˆƒ q' êž‰ â„š , ((q' < q) Ã— (q' âˆˆ candidate-upper-section L))
   Î³ = âˆ¥âˆ¥-rec âˆƒ-is-prop f q-in-U

 candidate-upper-section-is-ordered : (L : ð“Ÿ â„š)
                                    â†’ is-lower L
                                    â†’ is-located L
                                    â†’ are-ordered L (candidate-upper-section L)
 candidate-upper-section-is-ordered L L-lower located p q p-in-L q-in-U = Î³
    where
     f : (Î£ r êž‰ â„š , (r < q) Ã— (r âˆ‰ L)) â†’ p < q
     f (r , i , r-not-in-L) = âˆ¥âˆ¥-rec (order-is-prop-valued p q) g (located r q i)
      where
       g : (r âˆˆ L) + (q âˆ‰ L) â†’ p < q
       g (inl r-in-L)     = ðŸ˜-elim (r-not-in-L r-in-L)
       g (inr q-not-in-L) = order-criterion p q I II
        where
         I : p â‰¢ q
         I refl = q-not-in-L p-in-L

         II : q â‰® p
         II â„“ = q-not-in-L (L-lower p p-in-L q â„“)

     Î³ : p < q
     Î³ = âˆ¥âˆ¥-rec (order-is-prop-valued p q) f q-in-U

 candidate-upper-section-is-located : (L : ð“Ÿ â„š)
                                    â†’ is-located L
                                    â†’ are-located L (candidate-upper-section L)
 candidate-upper-section-is-located L located p q â„“ = âˆ¥âˆ¥-rec âˆ¨-is-prop II I
    where
     I : âˆƒ p' êž‰ â„š , (p < p') Ã— (p' < q)
     I = â„š-is-dense p q â„“

     II : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q)) â†’ p âˆˆ L âˆ¨ q âˆˆ candidate-upper-section L
     II (p' , i , j) = âˆ¥âˆ¥-rec âˆ¨-is-prop IV III
      where
       III : p âˆˆ L âˆ¨ p' âˆ‰ L
       III = located p p' i

       IV : (p âˆˆ L) + (p' âˆ‰ L) â†’ p âˆˆ L âˆ¨ q âˆˆ candidate-upper-section L
       IV (inl p-in-L)      = âˆ£ inl p-in-L âˆ£
       IV (inr p'-not-in-L) = âˆ£ inr âˆ£ (p' , j , p'-not-in-L) âˆ£ âˆ£

 candidate-upper-section-is-inhabited : (L : ð“Ÿ â„š)
                                      â†’ is-bounded-above L
                                      â†’ is-inhabited (candidate-upper-section L)
 candidate-upper-section-is-inhabited L bounded =  Î³
    where
     f : (Î£ s êž‰ â„š , s âˆ‰ L) â†’ is-inhabited (candidate-upper-section L)
     f (s , Î½) = âˆ¥âˆ¥-functor g (â„š-is-upper-open s)
      where
       g : (Î£ p êž‰ â„š , s < p) â†’ Î£ p êž‰ â„š , p âˆˆ candidate-upper-section L
       g (p , i) = p , âˆ£ s , i , Î½ âˆ£

     Î³ : is-inhabited (candidate-upper-section L)
     Î³ = âˆ¥âˆ¥-rec (being-inhabited-is-prop (candidate-upper-section L)) f bounded

 candidate-upper-section-is-upper : (L : ð“Ÿ â„š)
                                  â†’ is-lower L
                                  â†’ is-located L
                                  â†’ is-upper (candidate-upper-section L)
 candidate-upper-section-is-upper L lower located p p-in-U q â„“ = Î³
  where
   Î³ : âˆƒ q' êž‰ â„š , (q' < q) Ã— (q' âˆ‰ L)
   Î³ = âˆ£ p ,
        â„“ ,
        (Î» p-in-L â†’ order-is-irrefl p
                     (candidate-upper-section-is-ordered
                       L lower located p p p-in-L p-in-U)) âˆ£
\end{code}

The candidate upper section is the unique candidate in the following
sense:

\begin{code}

 unique-candidate : (L U : ð“Ÿ â„š)
                  â†’ is-dedekind-section (L , U) â†’ U â‰¡ candidate-upper-section L
 unique-candidate L U (Li , Ll , Lo , Ui , Uu , Uo , ordered , located) = Î³
  where
   l : â„á´¸
   l = (L , Li , Ll , Lo)

   u : â„áµ
   u = (U , Ui , Uu , Uo)

   I : is-dedekind l
   I = u , ordered , located

   II : is-located L
   II = prâ‚‚ (dedekind-gives-troelstra l I)

   III : (candidate-upper-section L) is-an-upper-section-of L
   III = candidate-upper-section-is-lower-open L ,
         candidate-upper-section-is-ordered L Ll II ,
         candidate-upper-section-is-located L II

   Î³ : U â‰¡ candidate-upper-section L
   Î³ = any-two-upper-sections-are-equal L U
        (candidate-upper-section L)
        (Uo , ordered , located)
        III

\end{code}

And, as promised, the Troelstra condition implies the Dedekind condition:

\begin{code}

 troelstra-gives-dedekind : (l : â„á´¸) â†’ is-troelstra l â†’ is-dedekind l
 troelstra-gives-dedekind l@(L , L-inhabited , L-lower , L-upper-open)
                            (bounded , located) = Î³
  where
   Î³ : is-dedekind l
   Î³ = ((candidate-upper-section L ,
         (candidate-upper-section-is-inhabited L bounded ,
          candidate-upper-section-is-upper L L-lower located ,
          candidate-upper-section-is-lower-open L)) ,
        candidate-upper-section-is-ordered L L-lower located ,
        candidate-upper-section-is-located L located)

\end{code}

The set of Troelstra reals, again as a subset of the lower reals:

\begin{code}

 â„áµ€ : ð“¤âº Ì‡
 â„áµ€ = Î£ l êž‰ â„á´¸ , is-troelstra l

\end{code}

Question. Can we prove that â„ = â„áµ€ with propositional and functional
extensionality, without univalence? The problem is that the Dedekind
condition and the troelstra condition have different universe levels,
and hence propositional extensionality is not applicable to show that
they are equal, as their equality doesn't even type check. Would
universe lifting help? I haven't thought about this.

\begin{code}

 dedekind-agrees-with-troelstra : â„ â‰ƒ â„áµ€
 dedekind-agrees-with-troelstra = Î³
  where
   f : â„ â†’ â„áµ€
   f (l , h) = l , dedekind-gives-troelstra l h

   g : â„áµ€ â†’ â„
   g (l , k) = l , troelstra-gives-dedekind l k

   Î³ : â„ â‰ƒ â„áµ€
   Î³ = qinveq f (g ,
                (Î» (l , h) â†’ to-subtype-â‰¡ being-dedekind-is-prop refl) ,
                (Î» (l , k) â†’ to-subtype-â‰¡ being-troelstra-is-prop refl))
\end{code}

We now consider consequences of excluded middle. Notice that if A is a
proposition, then so is A + Â¬ A, and thus A + Â¬ A is equivalent to A âˆ¨ Â¬ A.

\begin{code}

 LEM = (A : ð“¤ Ì‡ ) â†’ is-prop A â†’ A + Â¬ A

 LEM-gives-locatedness : LEM â†’ ((L , _) : â„á´¸) â†’ is-located L
 LEM-gives-locatedness
   lem l@(L , L-inhabited , L-lower , L-upper-open) r s â„“ = Î³ Î´
  where
   Î´ : (s âˆˆ L) + (s âˆ‰ L)
   Î´ = lem (s âˆˆ L) (âˆˆ-is-prop L s)

   Î³ : type-of Î´ â†’ (r âˆˆ L) âˆ¨ (s âˆ‰ L)
   Î³ (inl s-in-L)     = âˆ£ inl (L-lower s s-in-L r â„“) âˆ£
   Î³ (inr s-not-in-L) = âˆ£ inr s-not-in-L âˆ£

\end{code}

The bounded lower reals:

\begin{code}

 â„á´®á´¸ : ð“¤âº Ì‡
 â„á´®á´¸ = Î£ (L , _) êž‰ â„á´¸ , is-bounded-above L

\end{code}

The boundedness condition only excludes a point at infinity in the
lower reals:

\begin{code}

 âˆž : ð“Ÿ â„š
 âˆž = Î» q â†’ âŠ¤Î©

 âˆž-is-lower-real : is-lower-real âˆž
 âˆž-is-lower-real = âˆ£ ðŸŽ , * âˆ£ ,
                   (Î» _ _ _ _ â†’ *) ,
                   (Î» p * â†’ âˆ¥âˆ¥-rec
                              âˆƒ-is-prop
                              (Î» (q , i) â†’ âˆ£ q , i , * âˆ£)
                              (â„š-is-upper-open p))

 âˆž-is-not-bounded-above : Â¬ is-bounded-above âˆž
 âˆž-is-not-bounded-above bounded = âˆ¥âˆ¥-rec
                                    ðŸ˜-is-prop
                                    (Î» (q , q-not-in-âˆž) â†’ q-not-in-âˆž *)
                                    bounded
\end{code}

In connection with a discussion above, notice that we don't need
univalence for the following, which says that the Troelstra reals
agree with the bounded lower reals if we assume excluded middle:

\begin{code}

 â„áµ€-and-â„á´®á´¸-agree-under-LEM : LEM â†’ â„áµ€ â‰¡ â„á´®á´¸
 â„áµ€-and-â„á´®á´¸-agree-under-LEM lem = ap Î£ Î³
  where
   Î´ : is-troelstra âˆ¼ Î» (L , _) â†’ is-bounded-above L
   Î´ l@(L , _) = pe (being-troelstra-is-prop l)
                    (being-bounded-above-is-prop L)
                    prâ‚
                    (Î» Î² â†’ Î² , LEM-gives-locatedness lem l)

   Î³ : is-troelstra â‰¡ (Î» (L , _) â†’ is-bounded-above L)
   Î³ = dfunext fe Î´

\end{code}

Therefore, under excluded middle, the Dedekind reals agree with the
bounded lower reals:

\begin{code}

 â„-and-â„á´®á´¸-agree-under-LEM : LEM â†’ â„ â‰ƒ â„á´®á´¸
 â„-and-â„á´®á´¸-agree-under-LEM lem = transport (â„ â‰ƒ_)
                                  (â„áµ€-and-â„á´®á´¸-agree-under-LEM lem)
                                  (dedekind-agrees-with-troelstra)
\end{code}

It follows that bounded lower reals are Dedekind under excluded middle.

\begin{code}

 LEM-gives-all-bounded-lower-reals-are-dedekind : LEM
                                                â†’ ((l , _) : â„á´®á´¸) â†’ is-dedekind l
 LEM-gives-all-bounded-lower-reals-are-dedekind lem (l , bounded) = Î³
  where
   Î³ : is-dedekind l
   Î³ = troelstra-gives-dedekind l (bounded , LEM-gives-locatedness lem l)

\end{code}

And the converse also holds. We use a method of proof suggested
independently by Steve Vickers and Toby Bartels.

\begin{code}

 all-bounded-lower-reals-are-dedekind-gives-LEM : (((l , _) : â„á´®á´¸) â†’ is-dedekind l)
                                                â†’ LEM
 all-bounded-lower-reals-are-dedekind-gives-LEM Î± A A-is-prop = Î³
  where
   L : ð“Ÿ â„š
   L p = ((p < ðŸŽ) âˆ¨ (A Ã— (p < ðŸ))) , âˆ¨-is-prop

   L-inhabited : is-inhabited L
   L-inhabited = âˆ¥âˆ¥-functor h (â„š-is-lower-open ðŸŽ)
    where
     h : (Î£ p êž‰ â„š , p < ðŸŽ) â†’ Î£ p êž‰ â„š , p âˆˆ L
     h (p , â„“) = p , âˆ£ inl â„“ âˆ£

   L-lower : is-lower L
   L-lower p p-in-L p' j = âˆ¥âˆ¥-functor h p-in-L
    where
     h : (p < ðŸŽ) + (A Ã— (p < ðŸ)) â†’ (p' < ðŸŽ) + (A Ã— (p' < ðŸ))
     h (inl â„“)       = inl (transitivity p' p ðŸŽ j â„“)
     h (inr (a , â„“)) = inr (a , transitivity p' p ðŸ j â„“)

   L-upper-open : is-upper-open L
   L-upper-open p p-in-L = âˆ¥âˆ¥-rec âˆƒ-is-prop h p-in-L
    where
     h : (p < ðŸŽ) + (A Ã— (p < ðŸ)) â†’ âˆƒ p' êž‰ â„š , (p < p') Ã— (p' âˆˆ L)
     h (inl â„“) = âˆ¥âˆ¥-functor k (â„š-is-dense p ðŸŽ â„“)
      where
       k : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < ðŸŽ)) â†’ Î£ p' êž‰ â„š , (p < p') Ã— (p' âˆˆ L)
       k (p' , i , j) = p' , i , âˆ£ inl j âˆ£
     h (inr (a , â„“)) = âˆ¥âˆ¥-functor k (â„š-is-dense p ðŸ â„“)
      where
       k : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < ðŸ)) â†’ Î£ p' êž‰ â„š , (p < p') Ã— p' âˆˆ L
       k (p' , i , j) = p' , i , âˆ£ inr (a , j) âˆ£

   l : â„á´¸
   l = (L , L-inhabited , L-lower , L-upper-open)

   l-dedekind-gives-A-decidable : is-dedekind l â†’ A + Â¬ A
   l-dedekind-gives-A-decidable ((U , _ , _) , LU-ordered , LU-located) = Î´
    where
     Î´ : A + Â¬ A
     Î´ = âˆ¥âˆ¥-rec (decidability-of-prop-is-prop fe A-is-prop) h (LU-located ðŸŽ Â½ ðŸŽ-is-less-than-Â½)
      where
       h : (ðŸŽ âˆˆ L) + (Â½ âˆˆ U) â†’ A + Â¬ A
       h (inl ðŸ˜-in-L) = inl (âˆ¥âˆ¥-rec A-is-prop k ðŸ˜-in-L)
        where
         k : (ðŸŽ < ðŸŽ) + (A Ã— (ðŸŽ < ðŸ)) â†’ A
         k (inl â„“)       = ðŸ˜-elim (order-is-irrefl ðŸŽ â„“)
         k (inr (a , _)) = a
       h (inr Â½-in-U) = inr Î½
        where
         Î½ : Â¬ A
         Î½ a = disjoint-criterion L U LU-ordered Â½ (Â½-in-L , Â½-in-U)
          where
           Â½-in-L : Â½ âˆˆ L
           Â½-in-L = âˆ£ inr (a , Â½-is-less-than-ðŸ) âˆ£

   L-bounded-above : is-bounded-above L
   L-bounded-above = âˆ£ ðŸ , (Î» ðŸ-in-L â†’ âˆ¥âˆ¥-rec ðŸ˜-is-prop h ðŸ-in-L) âˆ£
    where
     h : Â¬((ðŸ < ðŸŽ) + (A Ã— (ðŸ < ðŸ)))
     h (inl â„“)       = order-is-irrefl ðŸŽ (transitivity ðŸŽ ðŸ ðŸŽ ðŸŽ-is-less-than-ðŸ â„“)
     h (inr (_ , â„“)) = order-is-irrefl ðŸ â„“

   b : â„á´®á´¸
   b = (l , L-bounded-above)

   Î³ : A + Â¬ A
   Î³ = l-dedekind-gives-A-decidable (Î± b)

\end{code}

The canonical embedding of the rationals into the reals:

\begin{code}

 â„š-to-â„á´¸ : â„š â†’ â„á´¸
 â„š-to-â„á´¸ q = (Î» p â†’ (p < q) , order-is-prop-valued p q) ,
             â„š-is-lower-open q ,
             (Î» p i r j â†’ transitivity r p q j i) ,
             (Î» p â†’  â„š-is-dense p q)

 â„š-to-â„áµ : â„š â†’ â„áµ
 â„š-to-â„áµ q = (Î» p â†’ (q < p) , order-is-prop-valued q p) ,
             â„š-is-upper-open q ,
             (Î» p i r j â†’ transitivity q p r i j) ,
             (Î» p i â†’ âˆ¥âˆ¥-functor (Î» (r , j , k) â†’ r , k , j) (â„š-is-dense q p i))

 â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ : (q : â„š) â†’ (â„š-to-â„áµ q) is-upper-section-of (â„š-to-â„á´¸ q)
 â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ q = (Î» p â†’ transitivity p q) , (Î» p â†’ cotransitivity p q)

 â„š-to-â„á´¸-is-dedekind : (q : â„š) â†’ is-dedekind (â„š-to-â„á´¸ q)
 â„š-to-â„á´¸-is-dedekind q = â„š-to-â„áµ q , â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ q

 â„š-to-â„ : â„š â†’ â„
 â„š-to-â„ q = â„š-to-â„á´¸ q , â„š-to-â„á´¸-is-dedekind q

 â„š-to-â„á´¸-is-embedding : is-embedding â„š-to-â„á´¸
 â„š-to-â„á´¸-is-embedding l (p , a) (q , b) = Î³
  where
   I = â„š-to-â„á´¸ p â‰¡âŸ¨ a âŸ©
       l         â‰¡âŸ¨ b â»Â¹ âŸ©
       â„š-to-â„á´¸ q âˆŽ

   II : (Î» r â†’ (r < p) , _) â‰¡ (Î» r â†’ (r < q) , _)
   II = ap prâ‚ I

   III : (Î» r â†’ r < p) â‰¡ (Î» r â†’ r < q)
   III = ap (Î» f r â†’ prâ‚ (f r)) II

   A : (r : â„š) â†’ r < p â†’ r < q
   A r = idtofun (r < p) (r < q) (happly III r)

   B : (r : â„š) â†’ r < q â†’ r < p
   B r = idtofun (r < q) (r < p) (happly (III â»Â¹) r)

   V : p â‰¡ q
   V = equality-criterion p q A B

   Î³ : (p , a) â‰¡ (q , b)
   Î³ = to-subtype-â‰¡ (Î» _ â†’ â„á´¸-is-set) V

 â„š-to-â„-is-embedding : is-embedding â„š-to-â„
 â„š-to-â„-is-embedding = factor-is-embedding
                        â„š-to-â„
                        â„-to-â„á´¸
                        â„š-to-â„á´¸-is-embedding
                        â„-to-â„á´¸-is-embedding
  where
   notice-that : â„-to-â„á´¸ âˆ˜ â„š-to-â„ â‰¡ â„š-to-â„á´¸
   notice-that = refl

 open import CanonicalMap

 instance
  canonical-map-â„š-â„ : Canonical-Map â„š â„
  Î¹ {{canonical-map-â„š-â„}} = â„š-to-â„

  canonical-map-â„š-â„á´¸ : Canonical-Map â„š â„á´¸
  Î¹ {{canonical-map-â„š-â„á´¸}} = â„š-to-â„á´¸

  canonical-map-â„š-â„áµ : Canonical-Map â„š â„áµ
  Î¹ {{canonical-map-â„š-â„áµ}} = â„š-to-â„áµ


\end{code}

\begin{code}

 lowercut : â„ â†’ ð“Ÿ â„š
 lowercut ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = L

 uppercut : â„ â†’ ð“Ÿ â„š
 uppercut ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = U

 _â„š<â„_ : â„š â†’ â„ â†’ ð“¤ Ì‡
 q â„š<â„ x = q âˆˆ lowercut x

 _â„<â„š_ : â„ â†’ â„š â†’ ð“¤ Ì‡
 x â„<â„š q = q âˆˆ uppercut x

 open import StrictOrder

 instance
  strict-order-â„š-â„ : Strict-Order â„š â„
  _<_ {{strict-order-â„š-â„}} = _â„š<â„_

  strict-order-â„-â„š : Strict-Order â„ â„š
  _<_ {{strict-order-â„-â„š}} = _â„<â„š_

 _â„<â„_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â„<â„ y = âˆƒ q êž‰ â„š , (x < q) Ã— (q < y)

 instance
  strict-order-â„-â„ : Strict-Order â„ â„
  _<_ {{strict-order-â„-â„}} = _â„<â„_

 <-is-prop-valued : (x y : â„) â†’ is-prop (x < y)
 <-is-prop-valued x y = âˆƒ-is-prop

 lowercut-is-inhabited : (x : â„) â†’ âˆƒ p êž‰ â„š , p < x
 lowercut-is-inhabited ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Li

 uppercut-is-inhabited : (x : â„) â†’ âˆƒ q êž‰ â„š , x < q
 uppercut-is-inhabited ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Ui

 lowercut-is-lower : (x : â„) (q : â„š) â†’ q < x â†’ (p : â„š) â†’ p < q â†’ p < x
 lowercut-is-lower ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Ll

 uppercut-is-upper : (x : â„) (p : â„š) â†’ x < p â†’ (q : â„š) â†’ p < q â†’ x < q
 uppercut-is-upper ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Uu

 lowercut-is-upper-open : (x : â„) (p : â„š) â†’ p < x â†’ âˆƒ q êž‰ â„š , (p < q) Ã— (q < x)
 lowercut-is-upper-open ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Lo

 uppercut-is-lower-open : (x : â„) (q : â„š) â†’ x < q â†’ âˆƒ p êž‰ â„š , (p < q) Ã— (x < p)
 uppercut-is-lower-open ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Uo

 cuts-are-ordered : (x : â„) (p q : â„š) â†’ p < x â†’ x < q â†’ p < q
 cuts-are-ordered ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = o

 cuts-are-located : (x : â„) (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (x < q)
 cuts-are-located ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = l

 cuts-are-disjoint : (x : â„) (p : â„š) â†’ p < x â†’ x â‰® p
 cuts-are-disjoint x p l m = disjoint-criterion
                               (lowercut x) (uppercut x)
                               (cuts-are-ordered x)
                               p
                               (l , m)

 lowercut-is-bounded : (x : â„) â†’ âˆƒ p êž‰ â„š , p â‰® x
 lowercut-is-bounded (l , Î´) = prâ‚ (dedekind-gives-troelstra l Î´)

 lowercut-is-located : (x : â„) (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (q â‰® x)
 lowercut-is-located (l , Î´) = prâ‚‚ (dedekind-gives-troelstra l Î´)

 lowercut-lc : (x y : â„) â†’ lowercut x â‰¡ lowercut y â†’ x â‰¡ y
 lowercut-lc x y p = to-subtype-â‰¡ being-dedekind-is-prop
                       (to-subtype-â‰¡ being-lower-real-is-prop p)

 uppercut-lc : (x y : â„) â†’ uppercut x â‰¡ uppercut y â†’ x â‰¡ y
 uppercut-lc x y p = lowercut-lc x y Î³
  where
   I : lowercut x âŠ† lowercut y
   I = technical-lemma-converse (lowercut x) (uppercut x) (lowercut y) (uppercut y)
        (lowercut-is-upper-open x) (cuts-are-located y) (cuts-are-ordered x)
        (transport (_âŠ† uppercut x) p (âŠ†-refl (uppercut x)))

   II : lowercut x âŠ‡ lowercut y
   II = technical-lemma-converse (lowercut y) (uppercut y) (lowercut x) (uppercut x)
         (lowercut-is-upper-open y) (cuts-are-located x) (cuts-are-ordered y)
         (transport (uppercut x âŠ†_) p (âŠ†-refl (uppercut x)))

   Î³ : lowercut x â‰¡ lowercut y
   Î³ = subset-extensionality'' pe fe fe I II

 <-irrefl : (x : â„) â†’ x â‰® x
 <-irrefl x â„“ = Î³
  where
   Î´ : Â¬(Î£ q êž‰ â„š , ((x < q) Ã— (q < x)))
   Î´ (q , a , b) = cuts-are-disjoint x q b a

   Î³ : ðŸ˜
   Î³ = âˆ¥âˆ¥-rec ðŸ˜-is-prop Î´ â„“

 <-trans : (x y z : â„) â†’ x < y â†’ y < z â†’ x < z
 <-trans x y z i j = âˆ¥âˆ¥-functorâ‚‚ f i j
  where
   f : (Î£ p êž‰ â„š , (x < p) Ã— (p < y))
     â†’ (Î£ q êž‰ â„š , (y < q) Ã— (q < z))
     â†’  Î£ r êž‰ â„š , (x < r) Ã— (r < z)
   f (p , i , j) (q , k , l) = p , i , v
    where
     u : p < q
     u = cuts-are-ordered y p q j k

     v : p < z
     v = lowercut-is-lower z q l p u

 <-cotrans-â„š : (p q : â„š) â†’ p < q â†’ (z : â„) â†’ (p < z) âˆ¨ (z < q)
 <-cotrans-â„š p q â„“ z = cuts-are-located z p q â„“

 <-cotrans : (x y : â„) â†’ x < y â†’ (z : â„) â†’ (x < z) âˆ¨ (z < y)
 <-cotrans x y â„“ z = V
  where
   I : (Î£ q êž‰ â„š , ((x < q) Ã— (q < y))) â†’ (x < z) âˆ¨ (z < y)
   I (q , a , b) = âˆ¥âˆ¥-rec âˆ¨-is-prop III II
    where
     II : âˆƒ p êž‰ â„š , (p < q) Ã— (x < p)
     II = uppercut-is-lower-open x q a

     III : (Î£ p êž‰ â„š , (p < q) Ã— (x < p)) â†’ (x < z) âˆ¨ (z < y)
     III (p , c , d) = âˆ¥âˆ¥-functor IV (<-cotrans-â„š p q c z)
       where
        IV : (p < z) + (z < q) â†’ (x < z) + (z < y)
        IV (inl â„“) = inl âˆ£ p , d , â„“ âˆ£
        IV (inr â„“) = inr âˆ£ q , â„“ , b âˆ£

   V : (x < z) âˆ¨ (z < y)
   V = âˆ¥âˆ¥-rec âˆ¨-is-prop I â„“


 _â‰¤_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â‰¤ y = (q : â„š) â†’ q < x â†’ q < y

 â‰¤-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤ y)
 â‰¤-is-prop-valued x y = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆˆ-is-prop (lowercut y) _)

 _â‰¤'_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â‰¤' y = (q : â„š) â†’ y < q â†’ x < q

 â‰¤-gives-â‰¤' : (x y : â„) â†’ x â‰¤ y â†’ x â‰¤' y
 â‰¤-gives-â‰¤' x y â„“ = technical-lemma
                     (lowercut x) (uppercut x)
                     (lowercut y) (uppercut y)
                     (uppercut-is-lower-open y)
                     (cuts-are-located x)
                     (cuts-are-ordered y)
                     â„“

 â‰¤'-gives-â‰¤ : (x y : â„) â†’ x â‰¤' y â†’ x â‰¤ y
 â‰¤'-gives-â‰¤ x y â„“ = technical-lemma-converse
                     (lowercut x) (uppercut x)
                     (lowercut y) (uppercut y)
                     (lowercut-is-upper-open x)
                     (cuts-are-located y)
                     (cuts-are-ordered x)
                     â„“

 not-<-gives-â‰¤ : (x y : â„) â†’ y â‰® x â†’ x â‰¤ y
 not-<-gives-â‰¤ x y Î½ q â„“ = VI
  where
   I : (p : â„š) â†’ p < x â†’ y â‰® p
   I p m l = Î½ âˆ£ p , l , m âˆ£

   II : âˆƒ p êž‰ â„š , (q < p) Ã— (p < x)
   II = lowercut-is-upper-open x q â„“

   III : (Î£ p êž‰ â„š , (q < p) Ã— (p < x)) â†’ q < y
   III (p , i , j) = âˆ¥âˆ¥-rec (âˆˆ-is-prop (lowercut y) q) V IV
    where
     IV : (q < y) âˆ¨ (y < p)
     IV = <-cotrans-â„š q p i y

     V : (q < y) + (y < p) â†’ q < y
     V (inl k) = k
     V (inr l) = ðŸ˜-elim (I p j l)

   VI : q < y
   VI = âˆ¥âˆ¥-rec (âˆˆ-is-prop (lowercut y) q) III II

 â‰¤-gives-not-< : (x y : â„) â†’ x â‰¤ y â†’ y â‰® x
 â‰¤-gives-not-< x y â„“ i = II
  where
   I : Â¬ (Î£ p êž‰ â„š , (y < p) Ã— (p < x))
   I (p , j , k) = cuts-are-disjoint y p (â„“ p k) j

   II : ðŸ˜
   II = âˆ¥âˆ¥-rec ðŸ˜-is-prop I i

 â‰¤-refl : (x : â„) â†’ x â‰¤ x
 â‰¤-refl x q â„“ = â„“

 â‰¤-trans : (x y z : â„) â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
 â‰¤-trans x y z l m p i = m p (l p i)

 â‰¤-antisym : (x y : â„) â†’ x â‰¤ y â†’ y â‰¤ x â†’ x â‰¡ y
 â‰¤-antisym x y l m = lowercut-lc x y Î³
  where
   Î³ : lowercut x â‰¡ lowercut y
   Î³ = subset-extensionality'' pe fe fe l m


 _â™¯_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â™¯ y = (x < y) + (y < x)

 â™¯-is-prop-valued : (x y : â„) â†’ is-prop (x â™¯ y)
 â™¯-is-prop-valued x y = sum-of-contradictory-props
                          (<-is-prop-valued x y) (<-is-prop-valued y x)
                          (Î» i j â†’ <-irrefl x (<-trans x y x i j))

 â™¯-irrefl : (x : â„) â†’ Â¬ (x â™¯ x)
 â™¯-irrefl x (inl â„“) = <-irrefl x â„“
 â™¯-irrefl x (inr â„“) = <-irrefl x â„“

 â™¯-gives-â‰¢ : (x y : â„) â†’ x â™¯ y â†’ x â‰¢ y
 â™¯-gives-â‰¢ x x s refl = â™¯-irrefl x s

 â™¯-sym : (x y : â„) â†’ x â™¯ y â†’ y â™¯ x
 â™¯-sym x y (inl â„“) = inr â„“
 â™¯-sym x y (inr â„“) = inl â„“

 â™¯-cotrans : (x y : â„) â†’ x â™¯ y â†’ (z : â„) â†’ (x â™¯ z) âˆ¨ (y â™¯ z)
 â™¯-cotrans x y (inl â„“) z = âˆ¥âˆ¥-functor
                             (cases (Î» (â„“ : x < z) â†’ inl (inl â„“))
                                    (Î» (â„“ : z < y) â†’ inr (inr â„“)))
                             (<-cotrans x y â„“ z)
 â™¯-cotrans x y (inr â„“) z = âˆ¥âˆ¥-functor
                             (cases (Î» (â„“ : y < z) â†’ inr (inl â„“))
                                    (Î» (â„“ : z < x) â†’ inl (inr â„“)))
                             (<-cotrans y x â„“ z)

 â™¯-tight : (x y : â„) â†’ Â¬ (x â™¯ y) â†’ x â‰¡ y
 â™¯-tight x y Î½ = â‰¤-antisym x y III IV
  where
   I : x â‰® y
   I â„“ = Î½ (inl â„“)

   II : y â‰® x
   II â„“ = Î½ (inr â„“)

   III : x â‰¤ y
   III = not-<-gives-â‰¤ x y II

   IV : y â‰¤ x
   IV = not-<-gives-â‰¤ y x I

 â„-is-Â¬Â¬-separated : (x y : â„) â†’ Â¬Â¬(x â‰¡ y) â†’ x â‰¡ y
 â„-is-Â¬Â¬-separated x y Ï• = â™¯-tight x y (c Ï•)
  where
   c : Â¬Â¬ (x â‰¡ y) â†’ Â¬ (x â™¯ y)
   c = contrapositive (â™¯-gives-â‰¢ x y)

\end{code}
