Martin Escardo, 20th December 2021

Some thoughts about Dedekind reals.

A Dedekind real in constructive type theory is defined as a triple
(L , U , p) where L and U are data, namely given sets of rational
numbers, and p is property of (L , U).

But actually, given a lower Dedekind section L, there is at most one
pair (U , p) such that (L , U , p) is a Dedekind real. Hence the
Dedekind data (U , p) is property of the lower real L rather than
data. A more precise statement of this phenomenon is given below.

We generalize the rationals to any type with a proposition-valued,
irreflexive relation _<_, simply to avoid having to define the
rational numbers. But also it is interesting than nothing other than
a proposition-valued irreflexive relation is needed for the above
discussion.

We also discuss a version of the Dedekind reals proposed by Troelstra.
To show that it agrees with the usual one, we further assume that _<_
is dense, upper open, and satisfies p â‰¢ q â†’ p â‰® q â†’ p < q (which the
type of rationals does).

We also discuss what happens when we assume the principle of
excluded middle.

Here we adopt HoTT/UF as our type-theoretic foundation, which, in
particular, is well-suited to discuss the distinction between data and
property. The univalence axiom is not used anywhere here, but we
mention it in some discussions.

See also the discussion at https://twitter.com/EscardoMartin/status/1473393261012295681

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import CanonicalMapNotation
open import OrderNotation
open import Plus-Properties
open import CompactTypes
open import NaturalsOrder

open import UF-Base
open import UF-Embeddings
open import UF-Equiv
open import UF-FunExt
open import UF-Powerset
open import UF-PropTrunc
open import UF-Size
open import UF-Subsingletons
open import UF-Subsingletons-FunExt

module Dedekind
        (pt  : propositional-truncations-exist)
        (fe  : Fun-Ext)
        (pe  : Prop-Ext)
        {ð“¤  : Universe}
        (â„š   : ð“¤ Ì‡ )
        (_<-â„š-â„š_              : â„š â†’ â„š â†’ ð“¤ Ì‡ )
        (<-â„š-â„š-is-prop-valued : (p q : â„š) â†’ is-prop (p <-â„š-â„š q))
        (<-â„š-â„š-irrefl         : (q : â„š) â†’ Â¬ (q <-â„š-â„š q))
       where

open PropositionalTruncation pt

instance
 strict-order-â„š : Strict-Order â„š â„š
 _<_ {{strict-order-â„š}} = _<-â„š-â„š_

ð“¤âº = ð“¤ âº

\end{code}

Lower-real conditions:

\begin{code}

is-lower : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower L = (q : â„š) â†’ q âˆˆ L â†’ (p : â„š) â†’ p < q â†’ p âˆˆ L

is-upper-open : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper-open L = (p : â„š) â†’ p âˆˆ L â†’ âˆƒ p' êž‰ â„š , ((p < p') Ã— p' âˆˆ L)

is-lower-real : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower-real L = is-inhabited L Ã— is-lower L Ã— is-upper-open L

\end{code}

Upper-real conditions:

\begin{code}

is-upper : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper U = (p : â„š) â†’ p âˆˆ U â†’ (q : â„š) â†’ p < q â†’ q âˆˆ U

is-lower-open : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-lower-open U = (q : â„š) â†’ q âˆˆ U â†’ âˆƒ q' êž‰ â„š , ((q' < q) Ã— q' âˆˆ U)

is-upper-real : ð“Ÿ â„š â†’ ð“¤ Ì‡
is-upper-real U = is-inhabited U Ã— is-upper U Ã— is-lower-open U

\end{code}

The conditions are property:

\begin{code}

being-lower-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower L)
being-lower-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-upper-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-open L)
being-upper-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-lower-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-real L)
being-lower-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-lower-is-prop L)
                               (being-upper-open-is-prop L)

being-upper-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper L)
being-upper-is-prop L = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ âˆˆ-is-prop L _)

being-lower-open-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-lower-open L)
being-lower-open-is-prop L = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆƒ-is-prop)

being-upper-real-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-upper-real L)
being-upper-real-is-prop L = Ã—â‚ƒ-is-prop
                               (being-inhabited-is-prop L)
                               (being-upper-is-prop L)
                               (being-lower-open-is-prop L)
\end{code}

The sets of lower and upper reals:

\begin{code}

â„á´¸ : ð“¤âº Ì‡
â„á´¸ = Î£ L êž‰ ð“Ÿ â„š , is-lower-real L

â„áµ : ð“¤âº Ì‡
â„áµ = Î£ U êž‰ ð“Ÿ â„š , is-upper-real U

â„á´¸-is-set : is-set â„á´¸
â„á´¸-is-set = subsets-of-sets-are-sets (ð“Ÿ â„š)
             is-lower-real
             (powersets-are-sets'' fe fe pe)
             (Î» {l} â†’ being-lower-real-is-prop l)

â„áµ-is-set : is-set â„áµ
â„áµ-is-set = subsets-of-sets-are-sets (ð“Ÿ â„š)
             is-upper-real
             (powersets-are-sets'' fe fe pe)
             (Î» {l} â†’ being-upper-real-is-prop l)

â„á´¸-to-ð“Ÿâ„š : â„á´¸ â†’ ð“Ÿ â„š
â„á´¸-to-ð“Ÿâ„š = prâ‚

â„áµ-to-ð“Ÿâ„š : â„áµ â†’ ð“Ÿ â„š
â„áµ-to-ð“Ÿâ„š = prâ‚

instance
 canonical-map-â„á´¸-to-ð“Ÿâ„š : Canonical-Map â„á´¸ (ð“Ÿ â„š)
 Î¹ {{canonical-map-â„á´¸-to-ð“Ÿâ„š}} = â„á´¸-to-ð“Ÿâ„š

 canonical-map-â„áµ-to-ð“Ÿâ„š : Canonical-Map â„áµ (ð“Ÿ â„š)
 Î¹ {{canonical-map-â„áµ-to-ð“Ÿâ„š}} = â„áµ-to-ð“Ÿâ„š

â„á´¸-to-ð“Ÿâ„š-is-embedding : is-embedding (canonical-map â„á´¸ (ð“Ÿ â„š))
â„á´¸-to-ð“Ÿâ„š-is-embedding = prâ‚-is-embedding being-lower-real-is-prop

â„áµ-to-ð“Ÿâ„š-is-embedding : is-embedding (canonical-map â„áµ (ð“Ÿ â„š))
â„áµ-to-ð“Ÿâ„š-is-embedding = prâ‚-is-embedding being-upper-real-is-prop

\end{code}

Next we define the set of Dedekind reals as a subset of the lower
reals, after some preparation.

\begin{code}

are-ordered : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤  Ì‡
are-ordered L U = (p q : â„š) â†’ p âˆˆ L â†’ q âˆˆ U â†’ p < q

are-located : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤  Ì‡
are-located L U = (p q : â„š) â†’ p < q â†’ p âˆˆ L âˆ¨ q âˆˆ U

being-ordered-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-ordered L U)
being-ordered-is-prop _ _ = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ <-â„š-â„š-is-prop-valued _ _)

being-located-is-prop : (L U : ð“Ÿ â„š) â†’ is-prop (are-located L U)
being-located-is-prop _ _ = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

order-lemma : (L U L' U' : ð“Ÿ â„š)
            â†’ is-lower-open U'
            â†’ are-located L  U
            â†’ are-ordered L' U'
            â†’ L  âŠ† L'
            â†’ U' âŠ† U
order-lemma L U L' U'
            U'-lower-open
            LU-located
            LU'-ordered
            L-contained-in-L'
            q
            q-in-U'             = Î³
 where
  I : âˆƒ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U'
  I = U'-lower-open q q-in-U'

  II : (Î£ q' êž‰ â„š , (q' < q) Ã— q' âˆˆ U') â†’ q âˆˆ U
  II (q' , l , i) = VI
   where
    III : q' âˆˆ L âˆ¨ q âˆˆ U
    III = LU-located q' q l

    IV : q' âˆ‰ L
    IV j = <-â„š-â„š-irrefl q' b
     where
      a : q' âˆˆ L'
      a = L-contained-in-L' q' j

      b : q' < q'
      b = LU'-ordered q' q' a i

    V : (q' âˆˆ L) + (q âˆˆ U) â†’ q âˆˆ U
    V (inl j) = ðŸ˜-elim (IV j)
    V (inr k) = k

    VI : q âˆˆ U
    VI = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) V III

  Î³ : q âˆˆ U
  Î³ = âˆ¥âˆ¥-rec (âˆˆ-is-prop U q) II I


order-lemma-converse : (L U L' U' : ð“Ÿ â„š)
                     â†’ is-upper-open L
                     â†’ are-located L' U'
                     â†’ are-ordered L  U
                     â†’ U' âŠ† U
                     â†’ L  âŠ† L'
order-lemma-converse L U L' U'
                     L-upper-open
                     LU'-located
                     LU-ordered
                     U'-contained-in-U
                     q
                     q-in-L             = Î³
 where
  I : âˆƒ q' êž‰ â„š , (q < q') Ã— q' âˆˆ L
  I = L-upper-open q q-in-L

  II : (Î£ q' êž‰ â„š , (q < q') Ã— q' âˆˆ L) â†’ q âˆˆ L'
  II (q' , l , i) = VI
   where
    III : q âˆˆ L' âˆ¨ q' âˆˆ U'
    III = LU'-located q q' l

    IV : q' âˆ‰ U'
    IV j = <-â„š-â„š-irrefl q' b
     where
      a : q' âˆˆ U
      a = U'-contained-in-U q' j

      b : q' < q'
      b = LU-ordered q' q' i a

    V : (q âˆˆ L') + (q' âˆˆ U') â†’ q âˆˆ L'
    V (inl j) = j
    V (inr k) = ðŸ˜-elim (IV k)

    VI : q âˆˆ L'
    VI = âˆ¥âˆ¥-rec (âˆˆ-is-prop L' q) V III

  Î³ : q âˆˆ L'
  Î³ = âˆ¥âˆ¥-rec (âˆˆ-is-prop L' q) II I

\end{code}

The following definition is of an auxiliary character:

\begin{code}

_is-an-upper-section-of_ : ð“Ÿ â„š â†’ ð“Ÿ â„š â†’ ð“¤ Ì‡
U is-an-upper-section-of L = is-lower-open U Ã— are-ordered L U Ã— are-located L U

any-two-upper-sections-are-equal : (L U U' : ð“Ÿ â„š)
                                 â†’ U  is-an-upper-section-of L
                                 â†’ U' is-an-upper-section-of L
                                 â†’ U â‰¡ U'
any-two-upper-sections-are-equal L U U' (a , b , c) (u , v , w) = Î³
 where
  i : U âŠ† U'
  i = order-lemma L U' L U  a w b (âŠ†-refl' L)

  j : U âŠ‡ U'
  j = order-lemma L U  L U' u c v (âŠ†-refl' L)

  Î³ : U â‰¡ U'
  Î³ = subset-extensionality'' pe fe fe i j

\end{code}

The following is the version of the definition we are interested in:

\begin{code}

_is-upper-section-of_ : â„áµ â†’ â„á´¸ â†’ ð“¤ Ì‡
(U , _) is-upper-section-of  (L , _) = are-ordered L U Ã— are-located L U

being-upper-section-is-prop : (x : â„á´¸) (y : â„áµ)
                            â†’ is-prop (y is-upper-section-of x)
being-upper-section-is-prop (L , _) (U , _) = Ã—-is-prop
                                               (being-ordered-is-prop L U)
                                               (being-located-is-prop L U)
\end{code}

We use the above auxiliary definition and lemma to establish the following:

\begin{code}

at-most-one-upper-section : (l : â„á´¸) (uâ‚€ uâ‚ : â„áµ)
                          â†’ uâ‚€ is-upper-section-of l
                          â†’ uâ‚ is-upper-section-of l
                          â†’ uâ‚€ â‰¡ uâ‚
at-most-one-upper-section (L , _)
                          uâ‚€@(Uâ‚€ , _ , _ , Uâ‚€-is-lower-open)
                          uâ‚@(Uâ‚ , _ , _ , Uâ‚-is-lower-open)
                          (luâ‚€-ordered , luâ‚€-located)
                          (luâ‚-ordered , luâ‚-located)      = Î³
 where
  Î³ : uâ‚€ â‰¡ uâ‚
  Î³ = to-subtype-â‰¡
        being-upper-real-is-prop
        (any-two-upper-sections-are-equal L Uâ‚€ Uâ‚
            (Uâ‚€-is-lower-open , luâ‚€-ordered , luâ‚€-located)
            (Uâ‚-is-lower-open , luâ‚-ordered , luâ‚-located))
\end{code}

The Dedekind condition for a lower real:

\begin{code}

is-dedekind : â„á´¸ â†’ ð“¤âº Ì‡
is-dedekind l = Î£ u êž‰ â„áµ , (u is-upper-section-of l)

being-dedekind-is-prop : (l : â„á´¸) â†’ is-prop (is-dedekind l)
being-dedekind-is-prop l (uâ‚€ , pâ‚€) (uâ‚ , pâ‚) = to-subtype-â‰¡
                                                 (being-upper-section-is-prop l)
                                                 (at-most-one-upper-section l uâ‚€ uâ‚ pâ‚€ pâ‚)
\end{code}

We define the Dedekind reals as a subset of the lower reals:

\begin{code}

â„ : ð“¤âº Ì‡
â„ = Î£ l êž‰ â„á´¸ , is-dedekind l

\end{code}

The forgetful map of the reals into the lower reals is an embedding
and hence â„ is a set:

\begin{code}

â„-to-â„á´¸ : â„ â†’ â„á´¸
â„-to-â„á´¸ = prâ‚

â„-to-â„á´¸-is-embedding : is-embedding â„-to-â„á´¸
â„-to-â„á´¸-is-embedding = prâ‚-is-embedding being-dedekind-is-prop

â„-is-set : is-set â„
â„-is-set = subsets-of-sets-are-sets â„á´¸
             is-dedekind
             â„á´¸-is-set
             (Î» {l} â†’ being-dedekind-is-prop l)

instance
 canonical-map-â„-to-â„á´¸ : Canonical-Map â„ â„á´¸
 Î¹ {{canonical-map-â„-to-â„á´¸}} = â„-to-â„á´¸

\end{code}

NB. This won't be a *topological* embedding in topological
models. Because â„ and â„á´¸ are sets, in the sense of HoTT/UF, the
embedding condition merely says that the map is left-cancellable.

We unpack and reorder the definition to emphasize that it amounts to
the usual one:

\begin{code}

is-dedekind-section : ð“Ÿ â„š Ã— ð“Ÿ â„š â†’ ð“¤ Ì‡
is-dedekind-section (L , U) = is-inhabited L Ã— is-lower L Ã— is-upper-open L
                            Ã— is-inhabited U Ã— is-upper U Ã— is-lower-open U
                            Ã— are-ordered L U Ã— are-located L U


NBâ‚ : â„ â‰ƒ (Î£ (L , R) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š , is-dedekind-section (L , R))

NBâ‚ = qinveq (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
              â†’ ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , o , l))

            ((Î» ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , o , l)
              â†’ ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)) ,

             (Î» _ â†’ refl) ,

             (Î» _ â†’ refl))
\end{code}

The following shows that there is some redundancy in the definition of
Dedekind real:

\begin{code}

ordered-located-gives-lower : (L U : ð“Ÿ â„š)
                            â†’ are-ordered L U
                            â†’ are-located L U
                            â†’ is-lower L
ordered-located-gives-lower L U LU-ordered LU-located = Î³
 where
  Î³ : is-lower L
  Î³ q l p m = âˆ¥âˆ¥-rec (âˆˆ-is-prop L p) b a
   where
    a : p âˆˆ L âˆ¨ q âˆˆ U
    a = LU-located p q m

    b : (p âˆˆ L) + (q âˆˆ U) â†’ p âˆˆ L
    b (inl u) = u
    b (inr v) = ðŸ˜-elim (<-â„š-â„š-irrefl q (LU-ordered q q l v))

ordered-located-gives-upper : (L U : ð“Ÿ â„š)
                            â†’ are-ordered L U
                            â†’ are-located L U
                            â†’ is-upper U
ordered-located-gives-upper L U LU-ordered LU-located = Î³
 where
  Î³ : is-upper U
  Î³ q l p m = âˆ¥âˆ¥-rec (âˆˆ-is-prop U p) b a
   where
    a : q âˆˆ L âˆ¨ p âˆˆ U
    a = LU-located q p m

    b : (q âˆˆ L) + (p âˆˆ U) â†’ p âˆˆ U
    b (inl u) = ðŸ˜-elim (<-â„š-â„š-irrefl q (LU-ordered q q u l))
    b (inr v) = v


NBâ‚‚ : â„ â‰ƒ (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
                , is-inhabited L Ã— is-upper-open L
                Ã— is-inhabited U Ã— is-lower-open U
                Ã— are-ordered L U Ã— are-located L U)

NBâ‚‚ = qinveq (Î» ((L , Li , _ , Lo) , (U , Ui , _ , Uo) , o , l)
              â†’ ((L , U) , Li , Lo , Ui , Uo , o , l))

            ((Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
              â†’ ((L , Li , ordered-located-gives-lower L U o l , Lo) ,
                 (U , Ui , ordered-located-gives-upper L U o l , Uo) ,
                 o , l)) ,

             (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
              â†’ to-subtype-â‰¡ being-dedekind-is-prop
                  (to-subtype-â‰¡ being-lower-real-is-prop
                     refl)) ,

             (Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
              â†’ to-subtype-â‰¡ (Î» (L , U) â†’ Ã—â‚†-is-prop
                                           (being-inhabited-is-prop L)
                                           (being-upper-open-is-prop L)
                                           (being-inhabited-is-prop U)
                                           (being-lower-open-is-prop U)
                                           (being-ordered-is-prop L U)
                                           (being-located-is-prop L U))
                  refl))
\end{code}

Sometimes a disjointness condition rather than the order condition is
used in the definition of Dedekind reals.

\begin{code}

disjoint-criterion : (L U : ð“Ÿ â„š)
                   â†’ are-ordered L U
                   â†’ are-disjoint L U
disjoint-criterion L U o p (p-in-L , p-in-U) = <-â„š-â„š-irrefl p (o p p p-in-L p-in-U)

\end{code}

From now on we assume the properties of â„š and its order alluded above,
and a few more:

\begin{code}

module _ (â„š-density         : (p r : â„š) â†’ p < r â†’ Î£ q êž‰ â„š , (p < q) Ã— (q < r))
         (â„š-transitivity    : (p q r : â„š) â†’ p < q â†’ q < r â†’ p < r)
         (â„š-order-criterion : (p q : â„š) â†’ q â‰® p â†’ p â‰¢ q â†’ p < q)
         (â„š-cotransitivity  : (p q r : â„š) â†’ p < r â†’ (p < q) âˆ¨ (q < r))
         (â„š-tightness       : (p q : â„š) â†’ q â‰® p â†’ p â‰® q â†’ p â‰¡ q)
         (â„š-is-lower-open   : (q : â„š) â†’ âˆƒ p êž‰ â„š , (p < q))
         (â„š-is-upper-open   : (p : â„š) â†’ âˆƒ q êž‰ â„š , (p < q))
         (ðŸŽ Â½ ðŸ             : â„š)
         (ðŸŽ-is-less-than-Â½  : ðŸŽ < Â½)
         (Â½-is-less-than-ðŸ  : Â½ < ðŸ)
       where

 ðŸŽ-is-less-than-ðŸ : ðŸŽ < ðŸ
 ðŸŽ-is-less-than-ðŸ = â„š-transitivity ðŸŽ Â½ ðŸ ðŸŽ-is-less-than-Â½ Â½-is-less-than-ðŸ

 instance
  order-â„š-â„š : Order â„š â„š
  _â‰¤_ {{order-â„š-â„š}} p q = (r : â„š) â†’ r < p â†’ r < q

 â„š-â‰¤-antisym : (p q : â„š) â†’ p â‰¤ q â†’ q â‰¤ p â†’ p â‰¡ q
 â„š-â‰¤-antisym p q i j = â„š-tightness p q (Î» â„“ â†’ <-â„š-â„š-irrefl q (i q â„“))
                                       (Î» â„“ â†’ <-â„š-â„š-irrefl p (j p â„“))

 <-or-â‰¡-gives-â‰¤-on-â„š : (p q : â„š) â†’ (p < q) + (p â‰¡ q) â†’ p â‰¤ q
 <-or-â‰¡-gives-â‰¤-on-â„š p q (inl â„“)    r m = â„š-transitivity r p q m â„“
 <-or-â‰¡-gives-â‰¤-on-â„š p q (inr refl) r â„“ = â„“

 â„š-trichotomy = (p q : â„š) â†’ (p < q) + (p â‰¡ q) + (p > q)

 â‰¤-on-â„š-gives-â‰¡-or-< : â„š-trichotomy
                     â†’ (p q : â„š) â†’ p â‰¤ q â†’ (p < q) + (p â‰¡ q)
 â‰¤-on-â„š-gives-â‰¡-or-< Ï„ p q â„“ = Î³ (Ï„ p q)
  where
   I : q â‰® p
   I m = <-â„š-â„š-irrefl q (â„“ q m)

   Î³ : (p < q) + (p â‰¡ q) + (p > q) â†’ (p < q) + (p â‰¡ q)
   Î³ (inl i)       = inl i
   Î³ (inr (inl e)) = inr e
   Î³ (inr (inr j)) = ðŸ˜-elim (I j)

 ordered-criterion : (L U : ð“Ÿ â„š)
                   â†’ is-lower L
                   â†’ are-disjoint L U
                   â†’ are-ordered L U
 ordered-criterion L U L-lower LU-disjoint p q p-in-L q-in-U = Î³
  where
   I : p âˆ‰ U
   I p-in-U = LU-disjoint p (p-in-L , p-in-U)

   II : p â‰¢ q
   II refl = I q-in-U

   III : q â‰® p
   III â„“ = LU-disjoint q (L-lower p p-in-L q â„“ , q-in-U)

   Î³ : p < q
   Î³ = â„š-order-criterion p q III II

\end{code}

The following alternative definition of the Dedekind reals is often
found in the literature:

\begin{code}

 NBâ‚ƒ : â„ â‰ƒ (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
                 , is-inhabited L Ã— is-lower L Ã— is-upper-open L
                 Ã— is-inhabited U Ã— is-upper U Ã— is-lower-open U
                 Ã— are-disjoint L U Ã— are-located L U)

 NBâ‚ƒ = qinveq (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
               â†’ ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , disjoint-criterion L U o , l))

             ((Î» ((L , U) , Li , Ll , Lo , Ui , Uu , Uo , d , l)
               â†’ ((L , Li , Ll , Lo) ,
                  (U , Ui , Uu , Uo) ,
                  ordered-criterion L U Ll d , l)) ,

              (Î» ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l)
               â†’ to-subtype-â‰¡ being-dedekind-is-prop
                   (to-subtype-â‰¡ being-lower-real-is-prop
                      refl)) ,

              (Î» ((L , U) , Li , Lo , Ui , Uo , o , l)
               â†’ to-subtype-â‰¡ (Î» (L , U) â†’ Ã—â‚ˆ-is-prop
                                            (being-inhabited-is-prop L)
                                            (being-lower-is-prop L)
                                            (being-upper-open-is-prop L)
                                            (being-inhabited-is-prop U)
                                            (being-upper-is-prop U)
                                            (being-lower-open-is-prop U)
                                            (being-disjoint-is-prop fe L U)
                                            (being-located-is-prop L U))
                   refl))
\end{code}

We now consider an alternative definition of the Dedekind reals
offered by Troelstra.

\begin{code}

 is-bounded-above : ð“Ÿ â„š â†’ ð“¤ Ì‡
 is-bounded-above L = âˆƒ s êž‰ â„š , s âˆ‰ L

 is-located : ð“Ÿ â„š â†’ ð“¤ Ì‡
 is-located L = ((r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L)

 is-troelstra : â„á´¸ â†’ ð“¤ Ì‡
 is-troelstra (L , _) = is-bounded-above L Ã— is-located L

 being-bounded-above-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-bounded-above L)
 being-bounded-above-is-prop L = âˆƒ-is-prop

 being-troelstra-located-is-prop : (L : ð“Ÿ â„š) â†’ is-prop (is-located L)
 being-troelstra-located-is-prop L = Î â‚ƒ-is-prop fe (Î» _ _ _ â†’ âˆ¨-is-prop)

 being-troelstra-is-prop : (l : â„á´¸) â†’ is-prop (is-troelstra l)
 being-troelstra-is-prop (L , _) = Ã—-is-prop
                                    (being-bounded-above-is-prop L)
                                    (being-troelstra-located-is-prop L)
\end{code}

The Dedekind and Troelstra conditions are equivalent:

\begin{code}

 dedekind-gives-troelstra : (l : â„á´¸) â†’ is-dedekind l â†’ is-troelstra l
 dedekind-gives-troelstra l@(L , _ , _ , _)
                           ((U , U-inhabited , _ , _) , LU-ordered , LU-located) = Î³
  where
   bounded : (âˆƒ s êž‰ â„š , s âˆ‰ L)
   bounded = âˆ¥âˆ¥-functor f U-inhabited
    where
     f : (Î£ q êž‰ â„š , q âˆˆ U) â†’ Î£ q êž‰ â„š , q âˆ‰ L
     f (q , q-in-U) = q , (Î» q-in-L â†’ <-â„š-â„š-irrefl q (c q-in-L))
      where
       c : q âˆˆ L â†’ q < q
       c q-in-L = LU-ordered q q q-in-L q-in-U

   located : (r s : â„š) â†’ r < s â†’ r âˆˆ L âˆ¨ s âˆ‰ L
   located r s â„“ = âˆ¥âˆ¥-functor f (LU-located r s â„“)
    where
     f : (r âˆˆ L) + (s âˆˆ U) â†’ (r âˆˆ L) + (s âˆ‰ L)
     f (inl r-in-L) = inl r-in-L
     f (inr r-in-L) = inr (Î» s-in-L â†’ <-â„š-â„š-irrefl s (d s-in-L))
      where
       d : s âˆˆ L â†’ s < s
       d s-in-L = LU-ordered s s s-in-L r-in-L

   Î³ : is-troelstra l
   Î³ = bounded , located

\end{code}

A lower Dedekind real may or may not have an upper section. If it
does, it is given by the following candidate.

\begin{code}

 candidate-upper-section : ð“Ÿ â„š â†’ ð“Ÿ â„š
 candidate-upper-section L = Î» q â†’ (âˆƒ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) , âˆƒ-is-prop

 candidate-upper-section-is-lower-open : (L : ð“Ÿ â„š)
                                       â†’ is-lower-open (candidate-upper-section L)
 candidate-upper-section-is-lower-open L q q-in-U = Î³
  where
   f : (Î£ p êž‰ â„š , (p < q) Ã— (p âˆ‰ L)) â†’ âˆƒ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
   f (p , i , p-not-in-L) = g (â„š-density p q i)
    where
     g : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q))
       â†’  âˆƒ p' êž‰ â„š , (p' < q) Ã— (âˆƒ p êž‰ â„š , (p < p') Ã— (p âˆ‰ L))
     g (p' , j , k) = âˆ£ p' , k , âˆ£ p , j , p-not-in-L âˆ£ âˆ£

   Î³ : âˆƒ q' êž‰ â„š , ((q' < q) Ã— (q' âˆˆ candidate-upper-section L))
   Î³ = âˆ¥âˆ¥-rec âˆƒ-is-prop f q-in-U

 candidate-upper-section-is-ordered : (L : ð“Ÿ â„š)
                                    â†’ is-lower L
                                    â†’ is-located L
                                    â†’ are-ordered L (candidate-upper-section L)
 candidate-upper-section-is-ordered L L-lower located p q p-in-L q-in-U = Î³
    where
     f : (Î£ r êž‰ â„š , (r < q) Ã— (r âˆ‰ L)) â†’ p < q
     f (r , i , r-not-in-L) = âˆ¥âˆ¥-rec (<-â„š-â„š-is-prop-valued p q) g (located r q i)
      where
       g : (r âˆˆ L) + (q âˆ‰ L) â†’ p < q
       g (inl r-in-L)     = ðŸ˜-elim (r-not-in-L r-in-L)
       g (inr q-not-in-L) = â„š-order-criterion p q II I
        where
         I : p â‰¢ q
         I refl = q-not-in-L p-in-L

         II : q â‰® p
         II â„“ = q-not-in-L (L-lower p p-in-L q â„“)

     Î³ : p < q
     Î³ = âˆ¥âˆ¥-rec (<-â„š-â„š-is-prop-valued p q) f q-in-U

 candidate-upper-section-is-located : (L : ð“Ÿ â„š)
                                    â†’ is-located L
                                    â†’ are-located L (candidate-upper-section L)
 candidate-upper-section-is-located L located p q â„“ = âˆ¥âˆ¥-rec âˆ¨-is-prop II I
    where
     I : âˆƒ p' êž‰ â„š , (p < p') Ã— (p' < q)
     I = âˆ£ â„š-density p q â„“ âˆ£

     II : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < q)) â†’ p âˆˆ L âˆ¨ q âˆˆ candidate-upper-section L
     II (p' , i , j) = âˆ¥âˆ¥-rec âˆ¨-is-prop IV III
      where
       III : p âˆˆ L âˆ¨ p' âˆ‰ L
       III = located p p' i

       IV : (p âˆˆ L) + (p' âˆ‰ L) â†’ p âˆˆ L âˆ¨ q âˆˆ candidate-upper-section L
       IV (inl p-in-L)      = âˆ£ inl p-in-L âˆ£
       IV (inr p'-not-in-L) = âˆ£ inr âˆ£ (p' , j , p'-not-in-L) âˆ£ âˆ£

 candidate-upper-section-is-inhabited : (L : ð“Ÿ â„š)
                                      â†’ is-bounded-above L
                                      â†’ is-inhabited (candidate-upper-section L)
 candidate-upper-section-is-inhabited L bounded =  Î³
    where
     f : (Î£ s êž‰ â„š , s âˆ‰ L) â†’ is-inhabited (candidate-upper-section L)
     f (s , Î½) = âˆ¥âˆ¥-functor g (â„š-is-upper-open s)
      where
       g : (Î£ p êž‰ â„š , s < p) â†’ Î£ p êž‰ â„š , p âˆˆ candidate-upper-section L
       g (p , i) = p , âˆ£ s , i , Î½ âˆ£

     Î³ : is-inhabited (candidate-upper-section L)
     Î³ = âˆ¥âˆ¥-rec (being-inhabited-is-prop (candidate-upper-section L)) f bounded

 candidate-upper-section-is-upper : (L : ð“Ÿ â„š)
                                  â†’ is-lower L
                                  â†’ is-located L
                                  â†’ is-upper (candidate-upper-section L)
 candidate-upper-section-is-upper L lower located p p-in-U q â„“ = Î³
  where
   Î³ : âˆƒ q' êž‰ â„š , (q' < q) Ã— (q' âˆ‰ L)
   Î³ = âˆ£ p ,
        â„“ ,
        (Î» p-in-L â†’ <-â„š-â„š-irrefl p
                     (candidate-upper-section-is-ordered
                       L lower located p p p-in-L p-in-U)) âˆ£
\end{code}

The candidate upper section is the unique candidate in the following
sense:

\begin{code}

 unique-candidate : (L U : ð“Ÿ â„š)
                  â†’ is-dedekind-section (L , U) â†’ U â‰¡ candidate-upper-section L
 unique-candidate L U (Li , Ll , Lo , Ui , Uu , Uo , ordered , located) = Î³
  where
   l : â„á´¸
   l = (L , Li , Ll , Lo)

   u : â„áµ
   u = (U , Ui , Uu , Uo)

   I : is-dedekind l
   I = u , ordered , located

   II : is-located L
   II = prâ‚‚ (dedekind-gives-troelstra l I)

   III : (candidate-upper-section L) is-an-upper-section-of L
   III = candidate-upper-section-is-lower-open L ,
         candidate-upper-section-is-ordered L Ll II ,
         candidate-upper-section-is-located L II

   Î³ : U â‰¡ candidate-upper-section L
   Î³ = any-two-upper-sections-are-equal L U
        (candidate-upper-section L)
        (Uo , ordered , located)
        III

\end{code}

And, as promised, the Troelstra condition implies the Dedekind condition:

\begin{code}

 troelstra-gives-dedekind : (l : â„á´¸) â†’ is-troelstra l â†’ is-dedekind l
 troelstra-gives-dedekind l@(L , L-inhabited , L-lower , L-upper-open)
                            (bounded , located) = Î³
  where
   Î³ : is-dedekind l
   Î³ = ((candidate-upper-section L ,
         (candidate-upper-section-is-inhabited L bounded ,
          candidate-upper-section-is-upper L L-lower located ,
          candidate-upper-section-is-lower-open L)) ,
        candidate-upper-section-is-ordered L L-lower located ,
        candidate-upper-section-is-located L located)

\end{code}

The set of Troelstra reals, again as a subset of the lower reals:

\begin{code}

 â„áµ€ : ð“¤âº Ì‡
 â„áµ€ = Î£ l êž‰ â„á´¸ , is-troelstra l

\end{code}

Question. Can we prove that â„ = â„áµ€ with propositional and functional
extensionality, without univalence? The problem is that the Dedekind
condition and the troelstra condition have different universe levels,
and hence propositional extensionality is not applicable to show that
they are equal, as their equality doesn't even type check. Would
universe lifting help? I haven't thought about this.

\begin{code}

 dedekind-agrees-with-troelstra : â„ â‰ƒ â„áµ€
 dedekind-agrees-with-troelstra = Î³
  where
   f : â„ â†’ â„áµ€
   f (l , h) = l , dedekind-gives-troelstra l h

   g : â„áµ€ â†’ â„
   g (l , k) = l , troelstra-gives-dedekind l k

   Î³ : â„ â‰ƒ â„áµ€
   Î³ = qinveq f (g ,
                (Î» (l , h) â†’ to-subtype-â‰¡ being-dedekind-is-prop refl) ,
                (Î» (l , k) â†’ to-subtype-â‰¡ being-troelstra-is-prop refl))
\end{code}

We now consider consequences of excluded middle. Notice that if A is a
proposition, then so is A + Â¬ A, and thus A + Â¬ A is equivalent to A âˆ¨ Â¬ A.

\begin{code}

 LEM = (A : ð“¤ Ì‡ ) â†’ is-prop A â†’ A + Â¬ A

 LEM-gives-locatedness : LEM â†’ ((L , _) : â„á´¸) â†’ is-located L
 LEM-gives-locatedness
   lem l@(L , L-inhabited , L-lower , L-upper-open) r s â„“ = Î³ Î´
  where
   Î´ : (s âˆˆ L) + (s âˆ‰ L)
   Î´ = lem (s âˆˆ L) (âˆˆ-is-prop L s)

   Î³ : type-of Î´ â†’ (r âˆˆ L) âˆ¨ (s âˆ‰ L)
   Î³ (inl s-in-L)     = âˆ£ inl (L-lower s s-in-L r â„“) âˆ£
   Î³ (inr s-not-in-L) = âˆ£ inr s-not-in-L âˆ£

\end{code}

The bounded lower reals:

\begin{code}

 â„á´®á´¸ : ð“¤âº Ì‡
 â„á´®á´¸ = Î£ (L , _) êž‰ â„á´¸ , is-bounded-above L

\end{code}

The boundedness condition only excludes a point at infinity in the
lower reals:

\begin{code}

 infty : ð“Ÿ â„š
 infty = Î» q â†’ âŠ¤Î©

 infty-is-lower-real : is-lower-real infty
 infty-is-lower-real = âˆ£ ðŸŽ , â‹† âˆ£ ,
                   (Î» _ _ _ _ â†’ â‹†) ,
                   (Î» p â‹† â†’ âˆ¥âˆ¥-rec
                              âˆƒ-is-prop
                              (Î» (q , i) â†’ âˆ£ q , i , â‹† âˆ£)
                              (â„š-is-upper-open p))

 infty-is-not-bounded-above : Â¬ is-bounded-above infty
 infty-is-not-bounded-above bounded = âˆ¥âˆ¥-rec
                                        ðŸ˜-is-prop
                                        (Î» (q , q-not-in-infty) â†’ q-not-in-infty â‹†)
                                        bounded
\end{code}

In connection with a discussion above, notice that we don't need
univalence for the following, which says that the Troelstra reals
agree with the bounded lower reals if we assume excluded middle:

\begin{code}

 â„áµ€-and-â„á´®á´¸-agree-under-LEM : LEM â†’ â„áµ€ â‰¡ â„á´®á´¸
 â„áµ€-and-â„á´®á´¸-agree-under-LEM lem = ap Î£ Î³
  where
   Î´ : is-troelstra âˆ¼ Î» (L , _) â†’ is-bounded-above L
   Î´ l@(L , _) = pe (being-troelstra-is-prop l)
                    (being-bounded-above-is-prop L)
                    prâ‚
                    (Î» Î² â†’ Î² , LEM-gives-locatedness lem l)

   Î³ : is-troelstra â‰¡ (Î» (L , _) â†’ is-bounded-above L)
   Î³ = dfunext fe Î´

\end{code}

Therefore, under excluded middle, the Dedekind reals agree with the
bounded lower reals:

\begin{code}

 â„-and-â„á´®á´¸-agree-under-LEM : LEM â†’ â„ â‰ƒ â„á´®á´¸
 â„-and-â„á´®á´¸-agree-under-LEM lem = transport (â„ â‰ƒ_)
                                  (â„áµ€-and-â„á´®á´¸-agree-under-LEM lem)
                                  (dedekind-agrees-with-troelstra)
\end{code}

It follows that bounded lower reals are Dedekind under excluded middle.

\begin{code}

 LEM-gives-all-bounded-lower-reals-are-dedekind : LEM
                                                â†’ ((l , _) : â„á´®á´¸) â†’ is-dedekind l
 LEM-gives-all-bounded-lower-reals-are-dedekind lem (l , bounded) = Î³
  where
   Î³ : is-dedekind l
   Î³ = troelstra-gives-dedekind l (bounded , LEM-gives-locatedness lem l)

\end{code}

And the converse also holds. We use a method of proof suggested
independently by Steve Vickers and Toby Bartels.

\begin{code}

 all-bounded-lower-reals-are-dedekind-gives-LEM : (((l , _) : â„á´®á´¸) â†’ is-dedekind l)
                                                â†’ LEM
 all-bounded-lower-reals-are-dedekind-gives-LEM Î± A A-is-prop = Î³
  where
   L : ð“Ÿ â„š
   L p = ((p < ðŸŽ) âˆ¨ (A Ã— (p < ðŸ))) , âˆ¨-is-prop

   L-inhabited : is-inhabited L
   L-inhabited = âˆ¥âˆ¥-functor h (â„š-is-lower-open ðŸŽ)
    where
     h : (Î£ p êž‰ â„š , p < ðŸŽ) â†’ Î£ p êž‰ â„š , p âˆˆ L
     h (p , â„“) = p , âˆ£ inl â„“ âˆ£

   L-lower : is-lower L
   L-lower p p-in-L p' j = âˆ¥âˆ¥-functor h p-in-L
    where
     h : (p < ðŸŽ) + (A Ã— (p < ðŸ)) â†’ (p' < ðŸŽ) + (A Ã— (p' < ðŸ))
     h (inl â„“)       = inl (â„š-transitivity p' p ðŸŽ j â„“)
     h (inr (a , â„“)) = inr (a , â„š-transitivity p' p ðŸ j â„“)

   L-upper-open : is-upper-open L
   L-upper-open p p-in-L = âˆ¥âˆ¥-rec âˆƒ-is-prop h p-in-L
    where
     h : (p < ðŸŽ) + (A Ã— (p < ðŸ)) â†’ âˆƒ p' êž‰ â„š , (p < p') Ã— (p' âˆˆ L)
     h (inl â„“) = k (â„š-density p ðŸŽ â„“)
      where
       k : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < ðŸŽ)) â†’ âˆƒ p' êž‰ â„š , (p < p') Ã— (p' âˆˆ L)
       k (p' , i , j) = âˆ£ p' , i , âˆ£ inl j âˆ£ âˆ£
     h (inr (a , â„“)) = k (â„š-density p ðŸ â„“)
      where
       k : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < ðŸ)) â†’ âˆƒ p' êž‰ â„š , (p < p') Ã— p' âˆˆ L
       k (p' , i , j) = âˆ£ p' , i , âˆ£ inr (a , j) âˆ£ âˆ£

   l : â„á´¸
   l = (L , L-inhabited , L-lower , L-upper-open)

   l-dedekind-gives-A-decidable : is-dedekind l â†’ A + Â¬ A
   l-dedekind-gives-A-decidable ((U , _ , _) , LU-ordered , LU-located) = Î´
    where
     Î´ : A + Â¬ A
     Î´ = âˆ¥âˆ¥-rec (decidability-of-prop-is-prop fe A-is-prop) h (LU-located ðŸŽ Â½ ðŸŽ-is-less-than-Â½)
      where
       h : (ðŸŽ âˆˆ L) + (Â½ âˆˆ U) â†’ A + Â¬ A
       h (inl ðŸ˜-in-L) = inl (âˆ¥âˆ¥-rec A-is-prop k ðŸ˜-in-L)
        where
         k : (ðŸŽ < ðŸŽ) + (A Ã— (ðŸŽ < ðŸ)) â†’ A
         k (inl â„“)       = ðŸ˜-elim (<-â„š-â„š-irrefl ðŸŽ â„“)
         k (inr (a , _)) = a
       h (inr Â½-in-U) = inr Î½
        where
         Î½ : Â¬ A
         Î½ a = disjoint-criterion L U LU-ordered Â½ (Â½-in-L , Â½-in-U)
          where
           Â½-in-L : Â½ âˆˆ L
           Â½-in-L = âˆ£ inr (a , Â½-is-less-than-ðŸ) âˆ£

   L-bounded-above : is-bounded-above L
   L-bounded-above = âˆ£ ðŸ , (Î» ðŸ-in-L â†’ âˆ¥âˆ¥-rec ðŸ˜-is-prop h ðŸ-in-L) âˆ£
    where
     h : Â¬((ðŸ < ðŸŽ) + (A Ã— (ðŸ < ðŸ)))
     h (inl â„“)       = <-â„š-â„š-irrefl ðŸŽ (â„š-transitivity ðŸŽ ðŸ ðŸŽ ðŸŽ-is-less-than-ðŸ â„“)
     h (inr (_ , â„“)) = <-â„š-â„š-irrefl ðŸ â„“

   b : â„á´®á´¸
   b = (l , L-bounded-above)

   Î³ : A + Â¬ A
   Î³ = l-dedekind-gives-A-decidable (Î± b)

\end{code}

The canonical embedding of the rationals into the reals:

\begin{code}

 â„š-to-â„á´¸ : â„š â†’ â„á´¸
 â„š-to-â„á´¸ q = (Î» p â†’ (p < q) , <-â„š-â„š-is-prop-valued p q) ,
             â„š-is-lower-open q ,
             (Î» p i r j â†’ â„š-transitivity r p q j i) ,
             (Î» p i â†’ âˆ£ â„š-density p q i âˆ£)

 â„š-to-â„áµ : â„š â†’ â„áµ
 â„š-to-â„áµ q = (Î» p â†’ (q < p) , <-â„š-â„š-is-prop-valued q p) ,
             â„š-is-upper-open q ,
             (Î» p i r j â†’ â„š-transitivity q p r i j) ,
             (Î» p i â†’ âˆ£(Î» (r , j , k) â†’ r , k , j) (â„š-density q p i)âˆ£)

 â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ : (q : â„š) â†’ (â„š-to-â„áµ q) is-upper-section-of (â„š-to-â„á´¸ q)
 â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ q = (Î» p â†’ â„š-transitivity p q) ,
                                         (Î» p â†’ â„š-cotransitivity p q)

 â„š-to-â„á´¸-is-dedekind : (q : â„š) â†’ is-dedekind (â„š-to-â„á´¸ q)
 â„š-to-â„á´¸-is-dedekind q = â„š-to-â„áµ q , â„š-to-â„áµ-is-upper-section-of-â„š-to-â„á´¸ q

 â„š-to-â„ : â„š â†’ â„
 â„š-to-â„ q = â„š-to-â„á´¸ q , â„š-to-â„á´¸-is-dedekind q

 â„š-to-â„á´¸-is-embedding : is-embedding â„š-to-â„á´¸
 â„š-to-â„á´¸-is-embedding l (p , a) (q , b) = Î³
  where
   I = â„š-to-â„á´¸ p â‰¡âŸ¨ a âŸ©
       l         â‰¡âŸ¨ b â»Â¹ âŸ©
       â„š-to-â„á´¸ q âˆŽ

   II : (Î» r â†’ (r < p) , _) â‰¡ (Î» r â†’ (r < q) , _)
   II = ap prâ‚ I

   III : (Î» r â†’ r < p) â‰¡ (Î» r â†’ r < q)
   III = ap (Î» f r â†’ prâ‚ (f r)) II

   A : (r : â„š) â†’ r < p â†’ r < q
   A r = idtofun (r < p) (r < q) (happly III r)

   B : (r : â„š) â†’ r < q â†’ r < p
   B r = idtofun (r < q) (r < p) (happly (III â»Â¹) r)

   V : p â‰¡ q
   V =  â„š-â‰¤-antisym p q A B

   Î³ : (p , a) â‰¡ (q , b)
   Î³ = to-subtype-â‰¡ (Î» _ â†’ â„á´¸-is-set) V

 instance
  canonical-map-â„š-to-â„ : Canonical-Map â„š â„
  Î¹ {{canonical-map-â„š-to-â„}} = â„š-to-â„

 â„š-to-â„-is-embedding : is-embedding (canonical-map â„š â„)
 â„š-to-â„-is-embedding = factor-is-embedding
                        â„š-to-â„
                        â„-to-â„á´¸
                        â„š-to-â„á´¸-is-embedding
                        â„-to-â„á´¸-is-embedding
  where
   notice-that : â„-to-â„á´¸ âˆ˜ â„š-to-â„ â‰¡ â„š-to-â„á´¸
   notice-that = refl

 is-rational : â„ â†’ ð“¤âº Ì‡
 is-rational x = Î£ q êž‰ â„š , Î¹ q â‰¡ x

 being-rational-is-prop : (x : â„) â†’ is-prop (is-rational x)
 being-rational-is-prop = â„š-to-â„-is-embedding

\end{code}

We could also define

 instance
  canonical-map-â„š-to-â„á´¸ : Canonical-Map â„š â„á´¸
  Î¹ {{canonical-map-â„š-to-â„á´¸}} = â„š-to-â„á´¸

  canonical-map-â„š-to-â„áµ : Canonical-Map â„š â„áµ
  Î¹ {{canonical-map-â„š-to-â„áµ}} = â„š-to-â„áµ

but this would give us trouble with unsolved constraints.

We now consider order and apartness on real numbers.

\begin{code}

 lowercut : â„ â†’ ð“Ÿ â„š
 lowercut ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = L

 uppercut : â„ â†’ ð“Ÿ â„š
 uppercut ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = U

 instance
  strict-order-â„š-â„ : Strict-Order â„š â„
  _<_ {{strict-order-â„š-â„}} p x = p âˆˆ lowercut x

  strict-order-â„-â„š : Strict-Order â„ â„š
  _<_ {{strict-order-â„-â„š}} x q = q âˆˆ uppercut x

  strict-order-â„-â„ : Strict-Order â„ â„
  _<_ {{strict-order-â„-â„}} x y = âˆƒ q êž‰ â„š , (x < q) Ã— (q < y)

 <-â„š-â„-is-prop-valued : (p : â„š) (x : â„) â†’ is-prop (p < x)
 <-â„š-â„-is-prop-valued p x = âˆˆ-is-prop (lowercut x) p

 <-â„-â„š-is-prop-valued : (x : â„) (q : â„š) â†’ is-prop (x < q)
 <-â„-â„š-is-prop-valued x q = âˆˆ-is-prop (uppercut x) q

 <-â„-â„-is-prop-valued : (x y : â„) â†’ is-prop (x < y)
 <-â„-â„-is-prop-valued x y = âˆƒ-is-prop

\end{code}

We now name all the projections out of â„. We first give their types
and then define them, for the sake of clarity.

\begin{code}

 lowercut-is-inhabited  : (x : â„) â†’ âˆƒ p êž‰ â„š , p < x
 uppercut-is-inhabited  : (x : â„) â†’ âˆƒ q êž‰ â„š , x < q
 lowercut-is-lower      : (x : â„) (q : â„š) â†’ q < x â†’ (p : â„š) â†’ p < q â†’ p < x
 uppercut-is-upper      : (x : â„) (p : â„š) â†’ x < p â†’ (q : â„š) â†’ p < q â†’ x < q
 lowercut-is-upper-open : (x : â„) (p : â„š) â†’ p < x â†’ âˆƒ q êž‰ â„š , (p < q) Ã— (q < x)
 uppercut-is-lower-open : (x : â„) (q : â„š) â†’ x < q â†’ âˆƒ p êž‰ â„š , (p < q) Ã— (x < p)
 cuts-are-ordered       : (x : â„) (p q : â„š) â†’ p < x â†’ x < q â†’ p < q
 cuts-are-located       : (x : â„) (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (x < q)
 cuts-are-disjoint      : (x : â„) (p : â„š) â†’ p < x â†’ x â‰® p
 lowercut-is-bounded    : (x : â„) â†’ âˆƒ p êž‰ â„š , p â‰® x
 lowercut-is-located    : (x : â„) (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (q â‰® x)

 lowercut-is-inhabited  ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Li
 uppercut-is-inhabited  ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Ui
 lowercut-is-lower      ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Ll
 uppercut-is-upper      ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Uu
 lowercut-is-upper-open ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Lo
 uppercut-is-lower-open ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = Uo
 cuts-are-ordered       ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = o
 cuts-are-located       ((L , Li , Ll , Lo) , (U , Ui , Uu , Uo) , o , l) = l

 cuts-are-disjoint x p l m = disjoint-criterion
                               (lowercut x) (uppercut x)
                               (cuts-are-ordered x)
                               p
                               (l , m)

 lowercut-is-bounded (l , Î´) = prâ‚ (dedekind-gives-troelstra l Î´)
 lowercut-is-located (l , Î´) = prâ‚‚ (dedekind-gives-troelstra l Î´)

\end{code}

The lower and upper cut projections are left-cancellable (and hence
embeddings, as the types under consideration are all sets).

\begin{code}

 lowercut-lc : (x y : â„) â†’ lowercut x â‰¡ lowercut y â†’ x â‰¡ y
 lowercut-lc x y e = to-subtype-â‰¡ being-dedekind-is-prop
                       (to-subtype-â‰¡ being-lower-real-is-prop e)

 uppercut-lc : (x y : â„) â†’ uppercut x â‰¡ uppercut y â†’ x â‰¡ y
 uppercut-lc x y p = lowercut-lc x y III
  where
   I : lowercut x âŠ† lowercut y
   I = order-lemma-converse (lowercut x) (uppercut x) (lowercut y) (uppercut y)
        (lowercut-is-upper-open x) (cuts-are-located y) (cuts-are-ordered x)
        (transport (_âŠ† uppercut x) p (âŠ†-refl (uppercut x)))

   II : lowercut x âŠ‡ lowercut y
   II = order-lemma-converse (lowercut y) (uppercut y) (lowercut x) (uppercut x)
         (lowercut-is-upper-open y) (cuts-are-located x) (cuts-are-ordered y)
         (transport (uppercut x âŠ†_) p (âŠ†-refl (uppercut x)))

   III : lowercut x â‰¡ lowercut y
   III = subset-extensionality'' pe fe fe I II

\end{code}

We now develop the basic properties of the _<_ order.

\begin{code}

 <-irrefl : (x : â„) â†’ x â‰® x
 <-irrefl x â„“ = Î³
  where
   Î´ : Â¬ (Î£ q êž‰ â„š , ((x < q) Ã— (q < x)))
   Î´ (q , a , b) = cuts-are-disjoint x q b a

   Î³ : ðŸ˜
   Î³ = âˆ¥âˆ¥-rec ðŸ˜-is-prop Î´ â„“

 <-â„-â„-trans : (x y z : â„) â†’ x < y â†’ y < z â†’ x < z
 <-â„-â„-trans x y z i j = âˆ¥âˆ¥-functorâ‚‚ f i j
  where
   f : (Î£ p êž‰ â„š , (x < p) Ã— (p < y))
     â†’ (Î£ q êž‰ â„š , (y < q) Ã— (q < z))
     â†’  Î£ r êž‰ â„š , (x < r) Ã— (r < z)
   f (p , i , j) (q , k , l) = p , i , v
    where
     u : p < q
     u = cuts-are-ordered y p q j k

     v : p < z
     v = lowercut-is-lower z q l p u

 <-cotrans-â„š : (p q : â„š) â†’ p < q â†’ (z : â„) â†’ (p < z) âˆ¨ (z < q)
 <-cotrans-â„š p q â„“ z = cuts-are-located z p q â„“

 <-cotrans : (x y : â„) â†’ x < y â†’ (z : â„) â†’ (x < z) âˆ¨ (z < y)
 <-cotrans x y â„“ z = V
  where
   I : (Î£ q êž‰ â„š , ((x < q) Ã— (q < y))) â†’ (x < z) âˆ¨ (z < y)
   I (q , a , b) = âˆ¥âˆ¥-rec âˆ¨-is-prop III II
    where
     II : âˆƒ p êž‰ â„š , (p < q) Ã— (x < p)
     II = uppercut-is-lower-open x q a

     III : (Î£ p êž‰ â„š , (p < q) Ã— (x < p)) â†’ (x < z) âˆ¨ (z < y)
     III (p , c , d) = âˆ¥âˆ¥-functor IV (<-cotrans-â„š p q c z)
       where
        IV : (p < z) + (z < q) â†’ (x < z) + (z < y)
        IV (inl â„“) = inl âˆ£ p , d , â„“ âˆ£
        IV (inr â„“) = inr âˆ£ q , â„“ , b âˆ£

   V : (x < z) âˆ¨ (z < y)
   V = âˆ¥âˆ¥-rec âˆ¨-is-prop I â„“

\end{code}

There are a number of equivalent ways to define the _â‰¤_ order on â„. We
give four for now, and three more later.

\begin{code}

 _â‰¤â‚€_ _â‰¤â‚_ _â‰¤â‚‚_ _â‰¤â‚ƒ_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â‰¤â‚€ y  = (p : â„š) â†’ p < x â†’ p < y
 x â‰¤â‚ y  = (q : â„š) â†’ y < q â†’ x < q
 x â‰¤â‚‚ y  = y â‰® x
 x â‰¤â‚ƒ y  = (p q : â„š) â†’ p < x â†’ y < q â†’ p < q

\end{code}

Definition (3) has the advantage that it is applicable when x is a
lower real and y is an upper real. See the interval domain below. But
we adopted the first definition for reals before we realized that. It
doesn't matter much, because we can switch between all the definitions
in the case of the reals.

\begin{code}

 â‰¤â‚€-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚€ y)
 â‰¤â‚-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚ y)
 â‰¤â‚‚-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚‚ y)
 â‰¤â‚ƒ-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚ƒ y)

 â‰¤â‚€-is-prop-valued x y = Î â‚‚-is-prop fe (Î» _ _ â†’ <-â„š-â„-is-prop-valued _ y)
 â‰¤â‚-is-prop-valued x y = Î â‚‚-is-prop fe (Î» _ _ â†’ <-â„-â„š-is-prop-valued x _)
 â‰¤â‚‚-is-prop-valued x y = negations-are-props fe
 â‰¤â‚ƒ-is-prop-valued x y = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ <-â„š-â„š-is-prop-valued _ _)

 instance
  order-â„-â„ : Order â„ â„
  _â‰¤_ {{order-â„-â„}} = _â‰¤â‚€_

 â‰¤-gives-â‰¤â‚ : (x y : â„) â†’ x â‰¤ y â†’ x â‰¤â‚ y
 â‰¤-gives-â‰¤â‚ x y â„“ = order-lemma
                     (lowercut x) (uppercut x)
                     (lowercut y) (uppercut y)
                     (uppercut-is-lower-open y)
                     (cuts-are-located x)
                     (cuts-are-ordered y)
                     â„“

 â‰¤â‚-gives-â‰¤ : (x y : â„) â†’ x â‰¤â‚ y â†’ x â‰¤ y
 â‰¤â‚-gives-â‰¤ x y â„“ = order-lemma-converse
                     (lowercut x) (uppercut x)
                     (lowercut y) (uppercut y)
                     (lowercut-is-upper-open x)
                     (cuts-are-located y)
                     (cuts-are-ordered x)
                     â„“

 â‰¤â‚‚-gives-â‰¤ : (x y : â„) â†’ x â‰¤â‚‚ y â†’ x â‰¤ y
 â‰¤â‚‚-gives-â‰¤ x y Î½ q â„“ = VI
  where
   I : (p : â„š) â†’ p < x â†’ y â‰® p
   I p m l = Î½ âˆ£ p , l , m âˆ£

   II : âˆƒ p êž‰ â„š , (q < p) Ã— (p < x)
   II = lowercut-is-upper-open x q â„“

   III : (Î£ p êž‰ â„š , (q < p) Ã— (p < x)) â†’ q < y
   III (p , i , j) = âˆ¥âˆ¥-rec (<-â„š-â„-is-prop-valued q y) V IV
    where
     IV : (q < y) âˆ¨ (y < p)
     IV = <-cotrans-â„š q p i y

     V : (q < y) + (y < p) â†’ q < y
     V (inl k) = k
     V (inr l) = ðŸ˜-elim (I p j l)

   VI : q < y
   VI = âˆ¥âˆ¥-rec (<-â„š-â„-is-prop-valued q y) III II

 â‰¤-gives-â‰¤â‚‚ : (x y : â„) â†’ x â‰¤ y â†’ x â‰¤â‚‚ y
 â‰¤-gives-â‰¤â‚‚ x y â„“ i = II
  where
   I : Â¬ (Î£ p êž‰ â„š , (y < p) Ã— (p < x))
   I (p , j , k) = cuts-are-disjoint y p (â„“ p k) j

   II : ðŸ˜
   II = âˆ¥âˆ¥-rec ðŸ˜-is-prop I i

 â‰¤â‚ƒ-gives-â‰¤ : (x y : â„) â†’ x â‰¤â‚ƒ y â†’ x â‰¤ y
 â‰¤â‚ƒ-gives-â‰¤ x y l = III
  where
   I : Â¬ (Î£ p êž‰ â„š , (y < p) Ã— (p < x))
   I (p , i , j) = <-â„š-â„š-irrefl p (l p p j i)

   II : y â‰® x
   II m = âˆ¥âˆ¥-rec ðŸ˜-is-prop I m

   III : x â‰¤ y
   III = â‰¤â‚‚-gives-â‰¤ x y II

 â‰¤-gives-â‰¤â‚ƒ : (x y : â„) â†’ x â‰¤ y â†’ x â‰¤â‚ƒ y
 â‰¤-gives-â‰¤â‚ƒ x y l p q i j = II
  where
   I : p < y
   I = l p i

   II : p < q
   II = cuts-are-ordered y p q I j

 â‰¤-â„-refl : (x : â„) â†’ x â‰¤ x
 â‰¤-â„-refl x q â„“ = â„“

 â‰¤-â„-â„-trans : (x y z : â„) â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
 â‰¤-â„-â„-trans x y z l m p i = m p (l p i)

 â‰¤-â„-â„-antisym : (x y : â„) â†’ x â‰¤ y â†’ y â‰¤ x â†’ x â‰¡ y
 â‰¤-â„-â„-antisym x y l m = lowercut-lc x y Î³
  where
   Î³ : lowercut x â‰¡ lowercut y
   Î³ = subset-extensionality'' pe fe fe l m

\end{code}

The type â„ is large, in the sense that it lives in ð“¤âº rather than ð“¤,
but it is locally small, in the sense that each identity type x â‰¡ y
with x,y:â„, which also lives in ð“¤âº, has copy in the universe ð“¤, namely
the type (x â‰¤ y) Ã— (y â‰¤ x).

\begin{code}

 â„-is-locally-small : is-locally-small â„
 â„-is-locally-small x y = Î³
  where
   f : (x â‰¤ y) Ã— (y â‰¤ x) â†’ x â‰¡ y
   f = uncurry (â‰¤-â„-â„-antisym x y)

   g : x â‰¡ y â†’ (x â‰¤ y) Ã— (y â‰¤ x)
   g refl = â‰¤-â„-refl x , â‰¤-â„-refl x

   e : ((x â‰¤ y) Ã— (y â‰¤ x)) â‰ƒ (x â‰¡ y)
   e = qinveq
        f
        (g ,
         (Î» a â†’ Ã—-is-prop (â‰¤â‚€-is-prop-valued x y) (â‰¤â‚€-is-prop-valued y x) (g (f a)) a) ,
         (Î» b â†’ â„-is-set (f (g b)) b))

   Î³ : (x â‰¡ y) has-size ð“¤
   Î³ = ((x â‰¤ y) Ã— (y â‰¤ x)) , e

\end{code}

Relationship between the orders of â„š and â„:

\begin{code}

 â„š-to-â„-preserves-< : (p q : â„š) â†’ p < q â†’ Î¹ p < Î¹ q
 â„š-to-â„-preserves-< p q l = âˆ£ â„š-density p q l âˆ£

 â„š-to-â„-reflects-< : (p q : â„š) â†’ Î¹ p < Î¹ q â†’ p < q
 â„š-to-â„-reflects-< p q = âˆ¥âˆ¥-rec
                           (<-â„š-â„š-is-prop-valued p q)
                           (Î» (r , i , j) â†’ â„š-transitivity p r q i j)

 â‰¤-on-â„š-agrees-with-â‰¤-on-â„ : (p q : â„š) â†’ (p â‰¤ q) â‰¡ (Î¹ p â‰¤ Î¹ q)
 â‰¤-on-â„š-agrees-with-â‰¤-on-â„ p q = refl

 â‰¤-on-â„š-is-prop-valued : (p q : â„š) â†’ is-prop (Î¹ p â‰¤ Î¹ q)
 â‰¤-on-â„š-is-prop-valued p q = â‰¤â‚€-is-prop-valued (Î¹ p) (Î¹ q)

 â„š-to-â„-preserves-â‰¤ : (p q : â„š) â†’ p â‰¤ q â†’ Î¹ p â‰¤ Î¹ q
 â„š-to-â„-preserves-â‰¤ p q = id

 â„š-to-â„-reflects-â‰¤ : (p q : â„š) â†’ Î¹ p â‰¤ Î¹ q â†’ p â‰¤ q
 â„š-to-â„-reflects-â‰¤ p q = id

 â„š-to-â„-left : (p : â„š) (x : â„) â†’ p < x â†’ Î¹ p < x
 â„š-to-â„-left p x = lowercut-is-upper-open x p

 â„š-to-â„-left-converse : (p : â„š) (x : â„) â†’ Î¹ p < x â†’ p < x
 â„š-to-â„-left-converse p x = âˆ¥âˆ¥-rec
                              (<-â„š-â„-is-prop-valued p x)
                              (Î» (q , m , o) â†’ lowercut-is-lower x q o p m)

 â„š-to-â„-right : (x : â„) (q : â„š) â†’ x < q â†’ x < Î¹ q
 â„š-to-â„-right x q l = âˆ¥âˆ¥-functor (Î» (p , m , o) â†’ p , o , m)
                                (uppercut-is-lower-open x q l)

 â„š-to-â„-right-converse : (x : â„) (q : â„š) â†’ x < Î¹ q â†’ x < q
 â„š-to-â„-right-converse x q = âˆ¥âˆ¥-rec
                               (<-â„-â„š-is-prop-valued x q)
                               (Î» (p , m , o) â†’ uppercut-is-upper x p m q o)
\end{code}

The promised three more ways to define _â‰¤_ on â„:

\begin{code}

 _â‰¤â‚€â‚_ _â‰¤â‚â‚_ _â‰¤â‚ƒâ‚_ : â„ â†’ â„ â†’ ð“¤âº Ì‡
 x â‰¤â‚€â‚ y = (z : â„) â†’ z < x â†’ z < y
 x â‰¤â‚â‚ y = (z : â„) â†’ y < z â†’ x < z
 x â‰¤â‚ƒâ‚ y = (z t : â„) â†’ z < x â†’ y < t â†’ z < t

 â‰¤â‚€â‚-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚€â‚ y)
 â‰¤â‚â‚-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚â‚ y)
 â‰¤â‚ƒâ‚-is-prop-valued : (x y : â„) â†’ is-prop (x â‰¤â‚ƒâ‚ y)

 â‰¤â‚€â‚-is-prop-valued x y = Î â‚‚-is-prop fe (Î» z _ â†’ <-â„-â„-is-prop-valued z y)
 â‰¤â‚â‚-is-prop-valued x y = Î â‚‚-is-prop fe (Î» z _ â†’ <-â„-â„-is-prop-valued x z)
 â‰¤â‚ƒâ‚-is-prop-valued x y = Î â‚„-is-prop fe (Î» z t _ _ â†’ <-â„-â„-is-prop-valued z t)

 â‰¤â‚€-gives-â‰¤â‚€â‚ : (x y : â„) â†’ x â‰¤â‚€ y â†’ x â‰¤â‚€â‚ y
 â‰¤â‚€-gives-â‰¤â‚€â‚ x y l z = âˆ¥âˆ¥-functor f
  where
   f : (Î£ p êž‰ â„š , (z < p) Ã— (p < x))
     â†’ (Î£ p êž‰ â„š , (z < p) Ã— (p < y))
   f (p , u , v) = p , u , l p v

 â‰¤â‚€â‚-gives-â‰¤â‚€ : (x y : â„) â†’ x â‰¤â‚€â‚ y â†’ x â‰¤â‚€ y
 â‰¤â‚€â‚-gives-â‰¤â‚€ x y l p m = II
  where
   I : Î¹ p < y
   I = l (Î¹ p) (â„š-to-â„-left p x m)

   II : p < y
   II = â„š-to-â„-left-converse p y I

 â‰¤â‚-gives-â‰¤â‚â‚ : (x y : â„) â†’ x â‰¤â‚ y â†’ x â‰¤â‚â‚ y
 â‰¤â‚-gives-â‰¤â‚â‚ x y l z = âˆ¥âˆ¥-functor f
  where
   f : (Î£ p êž‰ â„š , (y < p) Ã— (p < z))
     â†’ (Î£ p êž‰ â„š , (x < p) Ã— (p < z))
   f (p , u , v) = p , l p u , v

 â‰¤â‚â‚-gives-â‰¤â‚ : (x y : â„) â†’ x â‰¤â‚â‚ y â†’ x â‰¤â‚ y
 â‰¤â‚â‚-gives-â‰¤â‚ x y l p m = II
  where
   I : x < Î¹ p
   I = l (Î¹ p) (â„š-to-â„-right y p m)

   II : x < p
   II = â„š-to-â„-right-converse x p I

 â‰¤â‚ƒâ‚-gives-â‰¤â‚ƒ : (x y : â„) â†’ x â‰¤â‚ƒâ‚ y â†’ x â‰¤â‚ƒ y
 â‰¤â‚ƒâ‚-gives-â‰¤â‚ƒ x y l p q m o = â„š-to-â„-reflects-< p q Î³
  where
   Î³ : Î¹ p < Î¹ q
   Î³ = l (Î¹ p) (Î¹ q) (â„š-to-â„-left p x m) (â„š-to-â„-right y q o)

 â‰¤â‚ƒ-gives-â‰¤â‚ƒâ‚ : (x y : â„) â†’ x â‰¤â‚ƒ y â†’ x â‰¤â‚ƒâ‚ y
 â‰¤â‚ƒ-gives-â‰¤â‚ƒâ‚ x y l z t m o = âˆ¥âˆ¥-functorâ‚‚ f m o
  where
   f : (Î£ p êž‰ â„š , (z < p) Ã— (p < x))
     â†’ (Î£ q êž‰ â„š , (y < q) Ã— (q < t))
     â†’ (Î£ p êž‰ â„š , (z < p) Ã— (p < t))
   f (p , i , j) (q , u , v) = p , i , II
    where
     I : p < q
     I = l p q j u

     II : p < t
     II = lowercut-is-lower t q v p I

\end{code}

Relationship between _<_ and _â‰¤_ on â„:

\begin{code}

 <-gives-â‰¤' : (x y : â„) â†’ x < y â†’ x â‰¤ y
 <-gives-â‰¤' x y l = â‰¤â‚€â‚-gives-â‰¤â‚€ x y f
  where
   f : (z : â„) â†’ z < x â†’ z < y
   f z m = <-â„-â„-trans z x y m l

 <-â‰¤-trans : (x y z : â„) â†’ x < y â†’ y â‰¤ z â†’ x < z
 <-â‰¤-trans x y z l m = â‰¤â‚€-gives-â‰¤â‚€â‚ y z m x l

 â‰¤-<-â„-â„-trans : (x y z : â„) â†’ x â‰¤ y â†’ y < z â†’ x < z
 â‰¤-<-â„-â„-trans x y z l m = â‰¤â‚-gives-â‰¤â‚â‚ x y (â‰¤-gives-â‰¤â‚ x y l) z m

\end{code}

Apartness of real numbers and its basic properties:

\begin{code}

 _â™¯_ : â„ â†’ â„ â†’ ð“¤ Ì‡
 x â™¯ y = (x < y) + (y < x)

 â™¯-is-prop-valued : (x y : â„) â†’ is-prop (x â™¯ y)
 â™¯-is-prop-valued x y = sum-of-contradictory-props
                          (<-â„-â„-is-prop-valued x y)
                          (<-â„-â„-is-prop-valued y x)
                          (Î» i j â†’ <-irrefl x (<-â„-â„-trans x y x i j))

 â™¯-irrefl : (x : â„) â†’ Â¬ (x â™¯ x)
 â™¯-irrefl x (inl â„“) = <-irrefl x â„“
 â™¯-irrefl x (inr â„“) = <-irrefl x â„“

 â™¯-gives-â‰¢ : (x y : â„) â†’ x â™¯ y â†’ x â‰¢ y
 â™¯-gives-â‰¢ x x s refl = â™¯-irrefl x s

 â™¯-sym : (x y : â„) â†’ x â™¯ y â†’ y â™¯ x
 â™¯-sym x y (inl â„“) = inr â„“
 â™¯-sym x y (inr â„“) = inl â„“

 â™¯-cotrans : (x y : â„) â†’ x â™¯ y â†’ (z : â„) â†’ (x â™¯ z) âˆ¨ (y â™¯ z)
 â™¯-cotrans x y (inl â„“) z = âˆ¥âˆ¥-functor
                             (cases (Î» (â„“ : x < z) â†’ inl (inl â„“))
                                    (Î» (â„“ : z < y) â†’ inr (inr â„“)))
                             (<-cotrans x y â„“ z)
 â™¯-cotrans x y (inr â„“) z = âˆ¥âˆ¥-functor
                             (cases (Î» (â„“ : y < z) â†’ inr (inl â„“))
                                    (Î» (â„“ : z < x) â†’ inl (inr â„“)))
                             (<-cotrans y x â„“ z)

 â™¯-tight : (x y : â„) â†’ Â¬ (x â™¯ y) â†’ x â‰¡ y
 â™¯-tight x y Î½ = â‰¤-â„-â„-antisym x y III IV
  where
   I : x â‰® y
   I â„“ = Î½ (inl â„“)

   II : y â‰® x
   II â„“ = Î½ (inr â„“)

   III : x â‰¤ y
   III = â‰¤â‚‚-gives-â‰¤ x y II

   IV : y â‰¤ x
   IV = â‰¤â‚‚-gives-â‰¤ y x I

 â„-is-Â¬Â¬-separated : (x y : â„) â†’ Â¬Â¬ (x â‰¡ y) â†’ x â‰¡ y
 â„-is-Â¬Â¬-separated x y Ï• = â™¯-tight x y (c Ï•)
  where
   c : Â¬Â¬ (x â‰¡ y) â†’ Â¬ (x â™¯ y)
   c = contrapositive (â™¯-gives-â‰¢ x y)

 â„-order-criterion : (x y : â„) â†’ x â‰¤ y â†’ x â™¯ y â†’ x < y
 â„-order-criterion x y â„“ (inl m) = m
 â„-order-criterion x y â„“ (inr m) = ðŸ˜-elim (â‰¤-gives-â‰¤â‚‚ x y â„“ m)

 is-irrational : â„ â†’ ð“¤âº Ì‡
 is-irrational x = Â¬ (Î£ q êž‰ â„š , Î¹ q â‰¡ x)

 is-strongly-irrational : â„ â†’ ð“¤ Ì‡
 is-strongly-irrational x = (q : â„š) â†’ Î¹ q â™¯ x

 being-irrational-is-prop : (x : â„) â†’ is-prop (is-irrational x)
 being-irrational-is-prop x = negations-are-props fe

 being-strongly-irrational-is-prop : (x : â„) â†’ is-prop (is-strongly-irrational x)
 being-strongly-irrational-is-prop x = Î -is-prop fe (Î» q â†’ â™¯-is-prop-valued (Î¹ q) x)

\end{code}

We now consider the existence of least upper bounds of bounded
families x : ð¼ â†’ â„ with ð¼ inhabited.

A sufficient condition, given by Bishop, is that

  (p q : â„š) â†’ p < q â†’ (âˆƒ i êž‰ ð¼ , p < x i)
                    âˆ¨ (Î  i êž‰ ð¼ , x i < q)

We observe that the weaker condition

  (p q : â„š) â†’ p < q â†’  (âˆƒ i êž‰ ð¼ , p < x i)
                    âˆ¨ Â¬(âˆƒ i êž‰ ð¼ , q < x i)

suffices.

If we define (p < x) = (âˆƒ i êž‰ ð¼ , p < x i), then this weaker sufficient
condition reads

  (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (q â‰® x)

so that we see that it is analogous to Troelstra's locatedness
condition discussed above.

In the following, we write x â‰¤ y to mean that the real number y is an
upper bound of the family x.

\begin{code}

 module _ {ð¼ : ð“¤ Ì‡ } where

  F = ð¼ â†’ â„

  instance
   order-F-â„ : Order F â„
   _â‰¤_ {{order-F-â„}} x y = (i : ð¼) â†’ x i â‰¤ y

  â‰¤-F-â„-is-prop-valued : (x : F) (y : â„)
                           â†’ is-prop (x â‰¤ y)
  â‰¤-F-â„-is-prop-valued x y = Î -is-prop fe (Î» i â†’ â‰¤â‚€-is-prop-valued (x i) y)

  _has-lub_ : F â†’ â„ â†’ ð“¤âº Ì‡
  x has-lub y = (x â‰¤ y) Ã— ((z : â„) â†’ x â‰¤ z â†’ y â‰¤ z)

  _has-a-lub : F â†’ ð“¤âº Ì‡
  x has-a-lub = Î£ y êž‰ â„ , (x has-lub y)

  having-lub-is-prop : (x : F) (y : â„)
                     â†’ is-prop (x has-lub y)
  having-lub-is-prop x y = Ã—-is-prop
                             (â‰¤-F-â„-is-prop-valued x y)
                             (Î â‚‚-is-prop fe (Î» z _ â†’ â‰¤â‚€-is-prop-valued y z))

  having-a-lub-is-prop : (x : F) â†’ is-prop (x has-a-lub)
  having-a-lub-is-prop x (y , a , b) (y' , a' , b') = Î³
   where
    I : y â‰¡ y'
    I = â‰¤-â„-â„-antisym y y' (b y' a') (b' y a)

    Î³ : (y , a , b) â‰¡ (y' , a' , b')
    Î³ = to-subtype-â‰¡ (having-lub-is-prop x) I

  instance
   strict-order-â„š-F : Strict-Order â„š F
   _<_ {{strict-order-â„š-F}} p x = âˆƒ i êž‰ ð¼ , p < x i

  strict-order-â„š-F-is-prop : (p : â„š) (x : F) â†’ is-prop (p < x)
  strict-order-â„š-F-is-prop p x = âˆƒ-is-prop

  strict-order-â„š-F-observation : (p : â„š) (x : F)
                               â†’ (p â‰® x) â‡” (x â‰¤ Î¹ p)
  strict-order-â„š-F-observation p x = f , g
   where
    f : p â‰® x â†’ x â‰¤ Î¹ p
    f Î½ i = I
     where
      I : (q : â„š) â†’ q < x i â†’ q < p
      I q l = â„š-order-criterion q p II III
       where
        II : p â‰® q
        II m = Î½ âˆ£ i , lowercut-is-lower (x i) q l p m âˆ£

        III : q â‰¢ p
        III refl = Î½ âˆ£ i , l âˆ£

    g : x â‰¤ Î¹ p â†’ p â‰® x
    g l = âˆ¥âˆ¥-rec ðŸ˜-is-prop I
     where
      I : Â¬ (Î£ i êž‰ ð¼ , p < x i)
      I (i , m) = <-â„š-â„š-irrefl p (l i p m)

  is-upper-bounded : F â†’ ð“¤âº Ì‡
  is-upper-bounded x = âˆƒ y êž‰ â„ , (x â‰¤ y)

  is-located-family : F â†’ ð“¤ Ì‡
  is-located-family x = (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (q â‰® x)

  lub-sufficient-conditions : F â†’ ð“¤âº Ì‡
  lub-sufficient-conditions x = âˆ¥ ð¼ âˆ¥
                              Ã— is-upper-bounded x
                              Ã— is-located-family x

  lub : (x : F) â†’ lub-sufficient-conditions x â†’ x has-a-lub
  lub x (ð¼-inhabited , x-bounded , x-located) = y , a , b
   where
    L : ð“Ÿ â„š
    L p = (p < x) , strict-order-â„š-F-is-prop p x

    L-inhabited : âˆƒ p êž‰ â„š , p < x
    L-inhabited = âˆ¥âˆ¥-rec âˆƒ-is-prop I ð¼-inhabited
     where
      I : ð¼ â†’ âˆƒ p êž‰ â„š , âˆƒ i êž‰ ð¼ , p < x i
      I i = III II
       where
        II : Î£ i êž‰ ð¼ , âˆƒ p êž‰ â„š , p < x i
        II = i , lowercut-is-inhabited (x i)

        III : type-of II â†’ âˆƒ p êž‰ â„š , âˆƒ i êž‰ ð¼ , p < x i
        III (i , s) = âˆ¥âˆ¥-functor IV s
         where
          IV : (Î£ p êž‰ â„š , p < x i) â†’ Î£ p êž‰ â„š , âˆƒ i êž‰ ð¼ , p < x i
          IV (p , l) = p , âˆ£ i , l âˆ£

    L-lower : (q : â„š) â†’ q < x â†’ (p : â„š) â†’ p < q â†’ p < x
    L-lower q l p m = âˆ¥âˆ¥-functor (Î» (i , k) â†’ i , lowercut-is-lower (x i) q k p m) l

    L-upper-open : (p : â„š) â†’ p < x â†’ âˆƒ p' êž‰ â„š , ((p < p') Ã— (p' < x))
    L-upper-open p = âˆ¥âˆ¥-rec âˆƒ-is-prop f
     where
      f : (Î£ i êž‰ ð¼ , p < x i) â†’ âˆƒ p' êž‰ â„š , ((p < p') Ã— (p' < x))
      f (i , l) = âˆ¥âˆ¥-functor g (lowercut-is-upper-open (x i) p l)
       where
        g : (Î£ p' êž‰ â„š , (p < p') Ã— (p' < x i)) â†’ Î£ p' êž‰ â„š , ((p < p') Ã— (p' < x))
        g (p' , m , o) = p' , m , âˆ£ i , o âˆ£

    yá´¸ : â„á´¸
    yá´¸ = (L , L-inhabited , L-lower , L-upper-open)

    L-bounded-above : âˆƒ q êž‰ â„š , q â‰® x
    L-bounded-above = âˆ¥âˆ¥-rec âˆƒ-is-prop I x-bounded
     where
      I : (Î£ Î² êž‰ â„ , x â‰¤ Î²) â†’ âˆƒ q êž‰ â„š , q â‰® x
      I (Î² , l) = âˆ¥âˆ¥-functor II (uppercut-is-inhabited Î²)
       where
        II : (Î£ q êž‰ â„š , Î² < q) â†’ Î£ q êž‰ â„š , q â‰® x
        II (q , m) = q , âˆ¥âˆ¥-rec ðŸ˜-is-prop III
         where
          III : Â¬ (Î£ i êž‰ ð¼ , q < x i)
          III (i , o) = <-â„š-â„š-irrefl q (cuts-are-ordered Î² q q (l i q o) m)

    L-located : (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (q â‰® x)
    L-located = x-located

    Ï„ : is-troelstra yá´¸
    Ï„ = L-bounded-above , L-located

    y : â„
    y = (yá´¸ , troelstra-gives-dedekind yá´¸ Ï„)

    a : x â‰¤ y
    a i p l = âˆ£ i , l âˆ£

    b : (z : â„) â†’ x â‰¤ z â†’ y â‰¤ z
    b z l p = âˆ¥âˆ¥-rec (<-â„š-â„-is-prop-valued p z) f
     where
      f : (Î£ i êž‰ ð¼ , p < x i) â†’ p < z
      f (i , m) = l i p m

  instance
   strict-order-F-â„š : Strict-Order F â„š
   _<_ {{strict-order-F-â„š}} x q = (i : ð¼) â†’ x i < q

  <-F-â„š-is-prop-valued : (q : â„š) (x : F) â†’ is-prop (x < q)
  <-F-â„š-is-prop-valued q x = Î -is-prop fe (Î» i â†’ <-â„-â„š-is-prop-valued (x i) q)

  is-bishop-located : F â†’ ð“¤ Ì‡
  is-bishop-located x = (p q : â„š) â†’ p < q â†’ (p < x) âˆ¨ (x < q)

  bishop-located-families-are-located : (x : F)
                                      â†’ is-bishop-located x
                                      â†’ is-located-family x
  bishop-located-families-are-located x located p q l = IV

   where
    I : x < q â†’ q â‰® x
    I m = âˆ¥âˆ¥-rec ðŸ˜-is-prop II
     where
      II : Â¬ (Î£ i êž‰ ð¼ , q < x i)
      II (i , o) = <-â„š-â„š-irrefl q (cuts-are-ordered (x i) q q o (m i))

    III : (p < x) + (x < q) â†’ (p < x) + (q â‰® x)
    III (inl l) = inl l
    III (inr m) = inr (I m)

    IV : (p < x) âˆ¨ (q â‰® x)
    IV = âˆ¥âˆ¥-functor III (located p q l)

\end{code}

The partial reals, or interval domain, arise from dropping the
locatedness condition from the Dedekind reals.

\begin{code}

 instance
  strict-order-â„š-â„á´¸ : Strict-Order â„š â„á´¸
  _<_ {{strict-order-â„š-â„á´¸}} p (L , _) = p âˆˆ L

  strict-order-â„áµ-â„š : Strict-Order â„áµ â„š
  _<_ {{strict-order-â„áµ-â„š}} (U , _) p = p âˆˆ U

 instance
  order-â„á´¸-â„áµ : Order â„á´¸ â„áµ
  _â‰¤_ {{order-â„á´¸-â„áµ}} x y = (p q : â„š) â†’ p < x â†’ y < q â†’ p < q

 ð“¡ : ð“¤âº Ì‡
 ð“¡ = Î£ (x , y) êž‰ â„á´¸ Ã— â„áµ , (x â‰¤ y)

 ð“¡-is-set : is-set ð“¡
 ð“¡-is-set = subsets-of-sets-are-sets (â„á´¸ Ã— â„áµ) (Î» (x , y) â†’ x â‰¤ y)
              (Ã—-is-set â„á´¸-is-set â„áµ-is-set)
              (Î â‚„-is-prop fe (Î» _ _ _ _ â†’ <-â„š-â„š-is-prop-valued _ _))

 NBâ‚„ : ð“¡ â‰ƒ (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
                  , (is-inhabited L Ã— is-lower L Ã— is-upper-open L)
                  Ã— (is-inhabited U Ã— is-upper U Ã— is-lower-open U)
                  Ã— are-ordered L U)

 NBâ‚„ = qinveq (Î» (((L , Li , Ll , Lo) , (U , Ui , Uu , Uo)) , o)
               â†’ (L , U) , (Li , Ll , Lo) , ((Ui , Uu , Uo) , o))
             ((Î» ((L , U) , (Li , Ll , Lo) , ((Ui , Uu , Uo) , o))
               â†’ (((L , Li , Ll , Lo) , (U , Ui , Uu , Uo)) , o)) ,
              (Î» _ â†’ refl) ,
              (Î» _ â†’ refl))

 â„-to-ð“¡ : â„ â†’ ð“¡
 â„-to-ð“¡ (x , y , o , _) = (x , y) , o

 instance
  canonical-map-â„-to-ð“¡ : Canonical-Map â„ ð“¡
  Î¹ {{canonical-map-â„-to-ð“¡}} = â„-to-ð“¡

  order-â„á´¸-â„á´¸ : Order â„á´¸ â„á´¸
  _â‰¤_ {{order-â„á´¸-â„á´¸}} x y = (p : â„š) â†’ p < x â†’ p < y

  order-â„áµ-â„áµ : Order â„áµ â„áµ
  _â‰¤_ {{order-â„áµ-â„áµ}} x y = (p : â„š) â†’ y < p â†’ x < p

  square-order-ð“¡-ð“¡ : Square-Order ð“¡ ð“¡
  _âŠ‘_ {{square-order-ð“¡-ð“¡}} ((x , y) , _) ((x' , y') , _) = (x â‰¤ x') Ã— (y' â‰¤ y)

 â„-to-ð“¡-is-embedding : is-embedding (canonical-map â„ ð“¡)
 â„-to-ð“¡-is-embedding ((x , y) , o) ((x , y , o , l) , refl) ((x , y , o , m) , refl) = Î³
  where
   Î´ : l â‰¡ m
   Î´ = being-located-is-prop (Î¹ x) (Î¹ y) l m

   Î³ : ((x , y , o , l) , refl) â‰¡ ((x , y , o , m) , refl)
   Î³ = ap (Î» - â†’ (x , y , o , -) , refl) Î´

\end{code}

Notice that this is reverse inclusion of intervals: wider intervals
are lower in the square order.

If we drop the inhabitation conditions, the endpoints can be Â±âˆž:

\begin{code}

 ð“¡âˆž = (Î£ (L , U) êž‰ ð“Ÿ â„š Ã— ð“Ÿ â„š
             , (is-lower L Ã— is-upper-open L)
             Ã— (is-upper U Ã— is-lower-open U)
             Ã— are-ordered L U)

 ð“¡-to-ð“¡âˆž : ð“¡ â†’ ð“¡âˆž
 ð“¡-to-ð“¡âˆž (((L , _ , Ll , Lo) , (U , _ , Uu , Uo)) , o) = (L , U) , (Ll , Lo) , (Uu , Uo) , o

 âŠ¥ð“¡ : ð“¡âˆž
 âŠ¥ð“¡ = (âˆ… , âˆ…) , ((Î» _ ()) , (Î» _ ())) , ((Î» _ ()) , (Î» _ ())) , (Î» p q ())

 instance
  canonical-map-ð“¡-to-ð“¡âˆž : Canonical-Map ð“¡ ð“¡âˆž
  Î¹ {{canonical-map-ð“¡-to-ð“¡âˆž}} = ð“¡-to-ð“¡âˆž

 ð“¡-to-ð“¡âˆž-is-embedding : is-embedding (canonical-map ð“¡ ð“¡âˆž)
 ð“¡-to-ð“¡âˆž-is-embedding ((L , U) , (Ll , Lo) , (Uu , Uo) , o)
                        ((((L , i , Ll , Lo) , U , k , Uu , Uo) , o) , refl)
                        ((((L , j , Ll , Lo) , U , l , Uu , Uo) , o) , refl)
   = (((L , i , Ll , Lo) , U , k , Uu , Uo) , o) , refl â‰¡âŸ¨ I âŸ©
     (((L , j , Ll , Lo) , U , l , Uu , Uo) , o) , refl âˆŽ
  where
   I = apâ‚‚ (Î» i k â†’ (((L , i , Ll , Lo) , U , k , Uu , Uo) , o) , refl)
           (being-inhabited-is-prop L i j)
           (being-inhabited-is-prop U k l)

\end{code}

The notion of a locator for a real number was studied by my student
Auke Booij in his PhD thesis.

\begin{code}

 locator : â„ â†’ ð“¤ Ì‡
 locator x = (p q : â„š) â†’ p < q â†’ (p < x) + (x < q)

\end{code}

We also consider the following notion of locator for families:

\begin{code}

 bishop-locator : {ð¼ : ð“¤ Ì‡ } â†’ (ð¼ â†’ â„) â†’ ð“¤ Ì‡
 bishop-locator {ð¼} x = (p q : â„š)
                      â†’ p < q
                      â†’ (Î£ i êž‰ ð¼ , p < x i)
                      + (Î  i êž‰ ð¼ , x i < q)

 pointwise-locator-gives-bishop-locator : (ð¼ : ð“¤ Ì‡ ) (x : ð¼ â†’ â„)
                                        â†’ compactâˆ™ ð¼
                                        â†’ ((i : ð¼) â†’ locator (x i))
                                        â†’ bishop-locator x
 pointwise-locator-gives-bishop-locator ð¼ x Îº â„“ p q l = Î³
  where
   Î³ : (Î£ i êž‰ ð¼ , p < x i) + (Î  i êž‰ ð¼ , x i < q)
   Î³ = compact-gives-Î£+Î  ð¼
        (Î» i â†’ p < x i ) (Î» i â†’ x i < q)
        (compactâˆ™-gives-compact Îº)
        (Î» i â†’ â„“ i p q l)

 lub-with-locators : (ð¼ : ð“¤ Ì‡ ) (x : ð¼ â†’ â„)
                   â†’ compactâˆ™ ð¼
                   â†’ is-upper-bounded x
                   â†’ ((i : ð¼) â†’ locator (x i))
                   â†’ Î£ y êž‰ â„ , (x has-lub y) Ã— locator y
 lub-with-locators ð¼ x Îº Î² â„“ = Î³
  where
   h : âˆ¥ ð¼ âˆ¥
   h = âˆ£ compactâˆ™-gives-pointed Îº âˆ£

   I : bishop-locator x
   I = pointwise-locator-gives-bishop-locator ð¼ x Îº â„“

   II : (p q : â„š) â†’ p < q â†’ ((Î£ i êž‰ ð¼ , p < x i) + (Î  i êž‰ ð¼ , x i < q)) â†’ (p < x) âˆ¨ (x < q)
   II p q l (inl (i , m)) = âˆ£ inl âˆ£ i , m âˆ£ âˆ£
   II p q l (inr Ï•)       = âˆ£ inr Ï• âˆ£

   III : is-bishop-located x
   III p q l = II p q l (I p q l)

   IV : x has-a-lub
   IV = lub x (h , Î² , bishop-located-families-are-located x III)

   y : â„
   y = prâ‚ IV

   V : x has-lub y
   V = prâ‚‚ IV

   VI : (p q : â„š) â†’ p < q â†’ (p < y) + (y < q)
   VI p q l = Î´ (â„š-density p q l)
    where
     Î´ : (Î£ q' êž‰ â„š , (p < q') Ã— (q' < q)) â†’ (p < y) + (y < q)
     Î´ (q' , i , j) = VII (I p q' i)
      where
       VII : ((Î£ i êž‰ ð¼ , p < x i) + (Î  i êž‰ ð¼ , x i < q')) â†’ (p < y) + (y < q)
       VII (inl (o , m)) = inl âˆ£ o , m âˆ£
       VII (inr Ï•)       = inr IX
        where
         VIII : q' â‰® y
         VIII = âˆ¥âˆ¥-rec ðŸ˜-is-prop (Î» (i , o) â†’ <-â„š-â„š-irrefl q' (cuts-are-ordered (x i) q' q' o (Ï• i)))

         IX : âˆƒ q' êž‰ â„š , (q' < q) Ã— q' â‰® y
         IX = âˆ£ q' , j , VIII âˆ£

   Î³ : Î£ y êž‰ â„ , (x has-lub y) Ã— locator y
   Î³ = (y , V , VI)

\end{code}

Limits of sequences, but using the topological, rather than metric, structure of the reals.

\begin{code}

 â¦…_,_â¦† : â„š â†’ â„š â†’ (â„ â†’ Î© ð“¤)
 â¦… p , q â¦† = Î» x â†’ ((p < x) Ã— (x < q)) , Ã—-is-prop
                                         (<-â„š-â„-is-prop-valued p x)
                                         (<-â„-â„š-is-prop-valued x q)

 _has-limit_ : (â„• â†’ â„) â†’ â„ â†’ ð“¤ Ì‡
 x has-limit xâˆž = (p q : â„š)
                 â†’ xâˆž âˆˆ â¦… p , q â¦†
                 â†’ âˆƒ n êž‰ â„• , ((k : â„•) â†’ k â‰¥ n â†’ x k âˆˆ â¦… p , q â¦†)

 open import GenericConvergentSequence

 is-continuous-â„•âˆž-â„ : (â„•âˆž â†’ â„) â†’ ð“¤ Ì‡
 is-continuous-â„•âˆž-â„ x = (ð“ƒ : â„•âˆž) (p q : â„š)
                      â†’ x ð“ƒ âˆˆ â¦… p , q â¦†
                      â†’ âˆƒ ð“€ êž‰ â„•âˆž
                            , (ð“€ â‰º ð“ƒ)
                            Ã— ((ð’¾ : â„•âˆž) â†’ ð’¾ â‰½ ð“€ â†’ x ð’¾ âˆˆ â¦… p , q â¦†)

\end{code}

Some (overlapping) problems:

\begin{code}

 Problemâ‚ = (x : â„• â†’ â„) (xâˆž : â„)
          â†’ x has-limit xâˆž
          â†’ Î£ xÌ‚ êž‰ (â„•âˆž â†’ â„)
                 , ((n : â„•) â†’ xÌ‚ (Î¹ n) â‰¡ x n)
                 Ã— (xÌ‚ âˆž â‰¡ xâˆž)

 Problemâ‚‚ = (x : â„• â†’ â„) (xâˆž : â„)
          â†’ ((n : â„•) â†’ locator (x n))
          â†’ locator xâˆž
          â†’ x has-limit xâˆž
          â†’ Î£ xÌ‚ êž‰ (â„•âˆž â†’ â„)
                 , ((n : â„•) â†’ xÌ‚ (Î¹ n) â‰¡ x n)
                 Ã— (xÌ‚ âˆž â‰¡ xâˆž)
                 Ã— ((ð“ƒ : â„•âˆž) â†’ locator (xÌ‚ ð“ƒ))

 Problemâ‚ƒ = (x : â„•âˆž â†’ â„)
          â†’ (x âˆ˜ Î¹) has-limit (x âˆž)
          â†’ ((n : â„•) â†’ locator (x (Î¹ n)))
          â†’ locator (x âˆž)

 Problemâ‚„ = Î£ A êž‰ (â„ â†’ Î© ð“¤) , (Î£ x êž‰ â„ , x âˆˆ A) â‰ƒ â„•âˆž

 Problemâ‚… = Î£ A êž‰ (â„ â†’ Î© ð“¤)
                , ((Î£ x êž‰ â„ , x âˆˆ A) â‰ƒ â„•âˆž)
                Ã— ((x : â„) â†’ x âˆˆ A â†’ locator x)

 Problemâ‚† = (A : â„ â†’ Î© ð“¤)
          â†’ ((Î£ x êž‰ â„ , x âˆˆ A) â‰ƒ â„•âˆž)
          â†’ (x : â„)
          â†’ x âˆˆ A
          â†’ locator x

\end{code}

Should some of the above âˆƒ be Î£ and/or vice-versa?
