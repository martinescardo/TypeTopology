Martin Escardo, 17th December 2022.

Proof that in HoTT/UF the axiom of choice implies that every set can
be well-ordered, written in Agda, similar to the one in the HoTT book,
and to one of the two original proofs by Zermelo in set theory.

Converse added 22nd December 2022, but is already available, by Tom de
Jong since July 2021 in the module WellOrderingTaboo, which also shows
that, under excluded middle, the classical and inductive definitions
of ordinals agree.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan
open import NotionsOfDecidability.Decidable
open import Ordinals.Arithmetic
open import Ordinals.Equivalence
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying
open import UF.Base
open import UF.Choice
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.ExcludedMiddle
open import UF.FunExt
open import UF.Logic
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Univalence

\end{code}

We work in a Spartan Martin-LÃ¶f type theory and assume that the
univalence axiom holds and that propositional truncations exist:

\begin{code}

module Ordinals.WellOrderingPrinciple
        (ua : Univalence)
        (pt : propositional-truncations-exist)
       where

open import Ordinals.BuraliForti ua
open import Ordinals.OrdinalOfOrdinals ua

\end{code}

We then derive function extensionality and propositional
extensionality from univalence:

\begin{code}

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

 pe : Prop-Ext
 pe = Univalence-gives-Prop-Ext ua

 pe' : PropExt
 pe' ğ“¤ = pe {ğ“¤}

open import Ordinals.WellOrderingTaboo fe' pe

open InductiveWellOrder pt
open PropositionalTruncation pt
open UF.Choice.ExcludedMiddle pt fe
open UF.Choice.choice-functions pt pe' fe
open UF.Choice.Univalent-Choice fe pt

\end{code}

We now state the main theorem of this file, and its converse, where
the axiom of choice is formulated as in the HoTT book.

\begin{code}

every-set-can-be-well-ordered = {ğ“¤ : Universe} {X : ğ“¤ Ì‡ }
                              â†’ is-set X
                              â†’ âˆƒ _â‰º_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _â‰º_)

choice-gives-well-ordering : Axiom-of-Choice
                           â†’ every-set-can-be-well-ordered

well-ordering-gives-choice : every-set-can-be-well-ordered
                           â†’ Axiom-of-Choice

\end{code}

We first prove that, under excluded middle, if a set has a given
choice function, then it can be equipped with a well ordering. Later
we will derive excluded middle from choice in order to apply this to
prove the main theorem.

\begin{code}

open import Ordinals.WellOrderTransport fe
open import UF.ImageAndSurjection pt

open UF.Powerset.inhabited-subsets pt
open UF.Logic.AllCombinators pt fe'

choice-function-gives-well-ordering :

        Excluded-Middle
      â†’ {X : ğ“¤ Ì‡ }
      â†’ is-set X
      â†’ (Î£ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A))
      â†’ Î£ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)

choice-function-gives-well-ordering {ğ“¤} em {X} X-is-set (Îµ , Îµ-behaviour) = W
 where

\end{code}

We first define a function f : Ordinal ğ“¤ â†’ X by transfinite recursion
as follows:

\begin{code}

   Ï• : (Î± : Ordinal ğ“¤) â†’ (âŸ¨ Î± âŸ© â†’ X) â†’ X
   Ï• Î± s = Îµ â… x ê‰ X âˆ£ â±¯ a ê‰ âŸ¨ Î± âŸ© , s a â‰¢ x â†

   f : Ordinal ğ“¤ â†’ X
   f = transfinite-recursion-on-OO X Ï•

\end{code}

For an ordinal Î± and a point a in the underlying set âŸ¨ Î± âŸ© of Î±, we
denote by Î± â†“ a the down set of a in Î±. This is an ordinal on its own,
and, by univalence, every ordinal Î² â‰º Î± is of the form Î± â†“ a. Using
this construction, we define a subset A Î± of X for each ordinal Î±, and
with this we can specify the recursive behaviour of f as follows:

\begin{code}

   A : Ordinal ğ“¤ â†’ ğ“Ÿ X
   A Î± = â… x ê‰ X âˆ£ â±¯ a ê‰ âŸ¨ Î± âŸ© , f (Î± â†“ a) â‰¢ x â†

   f-behaviour : (Î± : Ordinal ğ“¤) â†’ f Î± ï¼ Îµ (A Î±)
   f-behaviour = transfinite-recursion-on-OO-behaviour X Ï•

\end{code}

The following properties of f should be self-explanatory:

\begin{code}

   f-lemma : (Î± : Ordinal ğ“¤) â†’ is-inhabited (A Î±) â†’ (Î² : Ordinal ğ“¤) â†’ Î² âŠ² Î± â†’ f Î± â‰  f Î²
   f-lemma Î± i Î² (a , refl) p = III
    where
     I = Îµ (A Î±)   ï¼âŸ¨ (f-behaviour Î±)â»Â¹ âŸ©
         f Î±       ï¼âŸ¨ p âŸ©
         f (Î± â†“ a) âˆ


     II : f (Î± â†“ a) âˆˆ A Î±
     II = transport (_âˆˆ A Î±) I (Îµ-behaviour (A Î±) i)

     III : ğŸ˜
     III = II a refl

   f-is-conditionally-1-1 : (Î± Î² : Ordinal ğ“¤)
                          â†’ is-inhabited (A Î±)
                          â†’ is-inhabited (A Î²)
                          â†’ Î± â‰  Î²
                          â†’ f Î± â‰  f Î²
   f-is-conditionally-1-1 Î± Î² i j Î½ = I (trichotomy _âŠ²_ fe' em âŠ²-is-well-order Î± Î²)
    where
     I : (Î± âŠ² Î²) + (Î± ï¼ Î²) + (Î² âŠ² Î±) â†’ f Î± â‰  f Î²
     I (inl l)       = â‰ -sym (f-lemma Î² j Î± l)
     I (inr (inl p)) = ğŸ˜-elim (Î½ p)
     I (inr (inr m)) = f-lemma Î± i Î² m

   f-is-conditionally-lc : (Î± Î² : Ordinal ğ“¤)
                         â†’ is-inhabited (A Î±)
                         â†’ is-inhabited (A Î²)
                         â†’ f Î± ï¼ f Î²
                         â†’ Î± ï¼ Î²
   f-is-conditionally-lc Î± Î² i j p =
     Â¬Â¬-elim
       (em (Î± ï¼ Î²) (the-type-of-ordinals-is-a-set (ua ğ“¤) fe'))
       (Î» (Î½ : Î± â‰  Î²) â†’ f-is-conditionally-1-1 Î± Î² i j Î½ p)

\end{code}

A crucial property of the family A Î± of subsets of X is that it is
eventually empty. We first prove that it is somewhere empty by
contradiction, using the fact that the type of ordinals is large (by
the Burali-Forti argument). We need to use propositional resizing for
this purpose, which follows from excluded middle, which we are
assuming.

\begin{code}

   A-is-somewhere-empty : âˆƒ Î± ê‰ Ordinal ğ“¤ , is-empty-subset (A Î±)
   A-is-somewhere-empty = III
    where
     I : Â¬ ((Î± : Ordinal ğ“¤) â†’ is-inhabited (A Î±))
     I Ï• = absurd
      where
       f-lc : left-cancellable f
       f-lc {Î±} {Î²} = f-is-conditionally-lc Î± Î² (Ï• Î±) (Ï• Î²)

       f-is-embedding : is-embedding f
       f-is-embedding = lc-maps-into-sets-are-embeddings f f-lc X-is-set

       X' : ğ“¤ âº Ì‡
       X' = image f

       f' : Ordinal ğ“¤ â†’ X'
       f' = corestriction f

       f'-is-equiv : is-equiv f'
       f'-is-equiv = corestriction-of-embedding-is-equivalence f f-is-embedding

       B : X â†’ ğ“¤ âº Ì‡
       B x = x âˆˆimage f

       B-is-prop : (x : X) â†’ is-prop (B x)
       B-is-prop x = being-in-the-image-is-prop x f

       Ï : Propositional-Resizing
       Ï = EM-gives-PR em

       C : X â†’ ğ“¤ Ì‡
       C x = resize Ï (B x) (B-is-prop x)

       Ï„ : Nat C B
       Ï„ x = from-resize Ï (B x) (B-is-prop x)

       Ï„-is-equiv : (x : X) â†’ is-equiv (Ï„ x)
       Ï„-is-equiv x = from-resize-is-equiv Ï (B x) (B-is-prop x)

       X'' : ğ“¤ Ì‡
       X'' = Î£ x ê‰ X , C x

       e = X''       â‰ƒâŸ¨ NatÎ£ Ï„ , NatÎ£-is-equiv C B Ï„ Ï„-is-equiv âŸ©
           X'        â‰ƒâŸ¨ â‰ƒ-sym (f' , f'-is-equiv) âŸ©
           Ordinal ğ“¤ â– 

       the-type-of-ordinals-is-small : is-small (Ordinal ğ“¤)
       the-type-of-ordinals-is-small = X'' , e

       absurd : ğŸ˜
       absurd = the-type-of-ordinals-is-large the-type-of-ordinals-is-small

     II : âˆƒ Î± ê‰ Ordinal ğ“¤ , Â¬ is-inhabited (A Î±)
     II = not-Î -implies-âˆƒ-not pt em (Î» x â†’ being-inhabited-is-prop (A x)) I

     III : âˆƒ Î± ê‰ Ordinal ğ“¤ , is-empty-subset (A Î±)
     III = Natâˆƒ (Î» Î± â†’ non-inhabited-subsets-are-empty (A Î±)) II

\end{code}

It follows from the above and excluded middle that there is a least
such Î±, which we will call Î±â‚€:

\begin{code}

   A-is-eventually-empty : Î£ Î±â‚€ ê‰ Ordinal ğ“¤
                                , is-empty-subset (A Î±â‚€)
                                Ã— ((Î± : Ordinal ğ“¤) â†’ is-empty-subset (A Î±) â†’ Î±â‚€ â‰¼ Î±)
   A-is-eventually-empty = nonempty-has-minimal _âŠ²_ fe' em pt âŠ²-is-well-order _
                            (Î» Î± â†’ being-empty-subset-is-prop fe' (A Î±))
                            A-is-somewhere-empty

   Î±â‚€ : Ordinal ğ“¤
   eâ‚€ : is-empty-subset (A Î±â‚€)
   mâ‚€ : (Î± : Ordinal ğ“¤) â†’ is-empty-subset (A Î±) â†’ Î±â‚€ â‰¼ Î±

   Î±â‚€ = prâ‚ A-is-eventually-empty
   eâ‚€ = prâ‚ (prâ‚‚ A-is-eventually-empty)
   mâ‚€ = prâ‚‚ (prâ‚‚ A-is-eventually-empty)

   nâ‚€ : (Î± : Ordinal ğ“¤) â†’ Î± âŠ² Î±â‚€ â†’ is-inhabited (A Î±)
   nâ‚€ Î± l = non-empty-subsets-are-inhabited em
             (A Î±)
             (contrapositive (mâ‚€ Î±) (Î» (m : Î±â‚€ â‰¼ Î±) â†’ irrefl (OO ğ“¤) Î± (m Î± l)))

\end{code}

We now restrict f to Î±â‚€ as follows, and show that the resulting map is
a surjection and an injection, and hence an equivalence, and we use
this to transport the well-ordering of Î±â‚€ to X to establish the
desired result:

\begin{code}

   fâ‚€ : âŸ¨ Î±â‚€ âŸ© â†’ X
   fâ‚€ a = f (Î±â‚€ â†“ a)

   fâ‚€-is-surjection : is-surjection fâ‚€
   fâ‚€-is-surjection x = not-Î -not-implies-âˆƒ pt em (eâ‚€ x)

   fâ‚€-lc : left-cancellable fâ‚€
   fâ‚€-lc {a} {b} p = II
    where
     Ia : is-inhabited (A (Î±â‚€ â†“ a))
     Ia = nâ‚€ (Î±â‚€ â†“ a) (a , refl)

     Ib : is-inhabited (A (Î±â‚€ â†“ b))
     Ib = nâ‚€ (Î±â‚€ â†“ b) (b , refl)

     I : (Î±â‚€ â†“ a) ï¼ (Î±â‚€ â†“ b)
     I = f-is-conditionally-lc (Î±â‚€ â†“ a) (Î±â‚€ â†“ b) Ia Ib p

     II : a ï¼ b
     II = â†“-lc Î±â‚€ a b I

   fâ‚€-is-embedding : is-embedding fâ‚€
   fâ‚€-is-embedding = lc-maps-into-sets-are-embeddings fâ‚€ fâ‚€-lc X-is-set

   fâ‚€-is-equiv : is-equiv fâ‚€
   fâ‚€-is-equiv = surjective-embeddings-are-equivs fâ‚€ fâ‚€-is-embedding fâ‚€-is-surjection

   structure-equiv : OrdinalStructure âŸ¨ Î±â‚€ âŸ© â‰ƒ OrdinalStructure X
   structure-equiv = transport-ordinal-structure (ua ğ“¤) âŸ¨ Î±â‚€ âŸ© X (fâ‚€ , fâ‚€-is-equiv)

\end{code}

And our desired results follows directly from this:

\begin{code}

   W : Î£ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
   W = âŒœ structure-equiv âŒ (structure Î±â‚€)

\end{code}

Using this we can prove the main theorem stated above, and restated
below, as follows. We first obtain a choice function conditionally to
the inhabitedness of X from the axiom of choice, and also the
principle of excluded middle. We then use excluded middle to check
whether X is inhabited. If it is, we apply the above lemma. Otherwise
it is empty and hence clearly well-ordered.

\begin{code}

choice-gives-well-ordering = restatement
 where
  restatement : Axiom-of-Choice
              â†’ {ğ“¤ : Universe} {X : ğ“¤ Ì‡ }
              â†’ is-set X
              â†’ âˆƒ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
  restatement ac {ğ“¤} {X} X-is-set = III
   where
    choice-function : âˆ¥ X âˆ¥ â†’ âˆƒ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
    choice-function = Choice-gives-Choiceâ‚„ ac X X-is-set

    em : Excluded-Middle
    em = Choice-gives-Excluded-Middle pe' ac

    I : âˆ¥ X âˆ¥ â†’ âˆƒ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
    I s = âˆ¥âˆ¥-functor
            (choice-function-gives-well-ordering em X-is-set)
            (choice-function s)

    II : Â¬ âˆ¥ X âˆ¥ â†’ âˆƒ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
    II Î½ = âˆ£ structure (prop-ordinal fe X (empty-types-are-props (Î½ âˆ˜ âˆ£_âˆ£))) âˆ£

    III : âˆƒ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
    III = cases I II (em âˆ¥ X âˆ¥ âˆ¥âˆ¥-is-prop)

\end{code}

We now prove the converse of the main theorem.

\begin{code}

well-ordering-gives-choice-function :

        Excluded-Middle
      â†’ {X : ğ“¤ Ì‡ }
      â†’ is-set X
      â†’ Î£ _<_ ê‰ (X â†’ X â†’ ğ“¤ Ì‡ ), (is-well-order _<_)
      â†’ (Î£ Îµ ê‰ (ğ“Ÿâº X â†’ X) , ((ğ“ : ğ“Ÿâº X) â†’ Îµ ğ“ âˆˆâº ğ“))

well-ordering-gives-choice-function {ğ“¤} em {X} X-is-set (_<_ , w) = Îµ , Îµ-behaviour
 where
  Î¼ : (ğ“ : ğ“Ÿâº X) â†’ Î£ xâ‚€ ê‰ X , (xâ‚€ âˆˆâº ğ“) Ã— _
  Î¼ (A , i) = nonempty-has-minimal _<_ fe' em pt w (_âˆˆ A) (âˆˆ-is-prop A) i

  Îµ : ğ“Ÿâº X â†’ X
  Îµ ğ“ = prâ‚ (Î¼ ğ“)

  Îµ-behaviour : (ğ“ : ğ“Ÿâº X) â†’ Îµ ğ“ âˆˆâº ğ“
  Îµ-behaviour ğ“ = prâ‚ (prâ‚‚ (Î¼ ğ“))

well-ordering-gives-choice = restatement
 where
  restatement : every-set-can-be-well-ordered â†’ Axiom-of-Choice
  restatement w {ğ“¤} {ğ“¥} = II
   where
    em : Excluded-Middle
    em = inductive-well-order-on-every-set-gives-excluded-middle (Î» _ â†’ w)

    I : ACâ‚ƒ {ğ“¤ âŠ” ğ“¥}
    I X X-is-set = âˆ¥âˆ¥-functor
                    (well-ordering-gives-choice-function em X-is-set)
                    (w X-is-set)

    II : AC {ğ“¤} {ğ“¥}
    II = ACâ‚ƒ-gives-AC I

\end{code}
