Martin Escardo, 23 December 2020.

We discuss how to transport well-orders along equivalences. This cannot
be done with univalence when the types live in different universes.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt

module Ordinals.WellOrderTransport (fe : FunExt) where

open import Ordinals.Equivalence
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying
open import Ordinals.WellOrderArithmetic
open import UF.Base
open import UF.Equiv
open import UF.Retracts
open import UF.Subsingletons
open import UF.Univalence

\end{code}

Univalence makes the following trivial:

\begin{code}

transport-ordinal-structure : is-univalent ğ“¤
                            â†’ (X Y : ğ“¤ Ì‡ )
                            â†’ X â‰ƒ Y
                            â†’ OrdinalStructure X â‰ƒ OrdinalStructure Y
transport-ordinal-structure ua X Y = Î³
 where
  Î´ : X ï¼ Y â†’ OrdinalStructure X ï¼ OrdinalStructure Y
  Î´ = ap OrdinalStructure

  Î³ : X â‰ƒ Y â†’ OrdinalStructure X â‰ƒ OrdinalStructure Y
  Î³ e = idtoeq (OrdinalStructure X) (OrdinalStructure Y) (Î´ (eqtoid ua X Y e))

\end{code}

The above can be done without univalence.

We could hope to get, more generally,

                               (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ )
                             â†’ X â‰ƒ Y
                             â†’ OrdinalStructure X â‰ƒ OrdinalStructure Y.

But this not possible, not even assuming univalence.

The reason is that it is not possible to transport an order
_<_ : X â†’ X â†’ ğ“¤ to an order _â‰º_ : Y â†’ Y â†’ ğ“¥ along a given equivalence
X â‰ƒ Y without propositional resizing, which we prefer not to assume.

However, if a particular order is resizable we can perform the
transport, although univalence won't help, which is what we do in this
file.

\begin{code}

module order-transfer-lemmaâ‚
         (X : ğ“¤ Ì‡ )
         (Y : ğ“¥ Ì‡ )
         (ğ•— : X â‰ƒ Y)
       where

  private
   f : X â†’ Y
   f = âŒœ ğ•— âŒ

   fâ»Â¹ : Y â†’ X
   fâ»Â¹ = inverse f (âŒœâŒ-is-equiv ğ•—)

   Î· : fâ»Â¹ âˆ˜ f âˆ¼ id
   Î· = inverses-are-retractions f (âŒœâŒ-is-equiv ğ•—)

   Îµ : f âˆ˜ fâ»Â¹ âˆ¼ id
   Îµ = inverses-are-sections f (âŒœâŒ-is-equiv ğ•—)

\end{code}

The point is that the derived relation has values in the universe ğ“¤,
but we would need it to have values in the universe ğ“¥. If the relation
is proposition-valued and propositional resizing holds, then this is
not a problem, but we prefer not to assume propositional resizing.

So we assume that the order relation on X already has values in ğ“¥,
and, as it turns out, this will be enough for our intended application
of this file.

So we make one further assumption and a definition:

\begin{code}

  module _ (_<_ : X â†’ X â†’ ğ“¥ Ì‡ ) where
    private
       _â‰º_ : Y â†’ Y â†’ ğ“¥ Ì‡
       y â‰º y' = fâ»Â¹ y < fâ»Â¹ y'

    order = _â‰º_

    order-preservationâ†’ : (x x' : X) â†’ x < x' â†’ f x â‰º f x'
    order-preservationâ†’ x x' = transportâ‚‚ _<_ ((Î· x)â»Â¹) ((Î· x')â»Â¹)

    order-preservationâ† : (y y' : Y) â†’ y â‰º y' â†’ fâ»Â¹ y < fâ»Â¹ y'
    order-preservationâ† y y' = id

\end{code}

Then our objective will be to prove the following:

\begin{code}

    transport-well-order : is-well-order _<_ â†” is-well-order _â‰º_

\end{code}

This is easy but painful, and will need a number of routine steps.

But notice that

\begin{code}

    private

      NB-< : type-of (is-well-order _<_) ï¼ ğ“¤ âŠ” ğ“¥ Ì‡
      NB-< = refl

      NB-â‰º : type-of (is-well-order _â‰º_) ï¼ ğ“¥ Ì‡
      NB-â‰º = refl

\end{code}

So we can't assert that the types (is-well-order _<_) and
(is-well-order _â‰º_) are equal.

However, we can easily establish their equivalence:

\begin{code}

    transport-well-order-â‰ƒ : (is-well-order _<_) â‰ƒ (is-well-order _â‰º_)
    transport-well-order-â‰ƒ = logically-equivalent-props-are-equivalent
                              (being-well-order-is-prop (_<_) fe)
                              (being-well-order-is-prop (_â‰º_) fe)
                              (lr-implication transport-well-order)
                              (rl-implication transport-well-order)

\end{code}

And now we provide all steps needed to establish transport-well-order.

\begin{code}

    is-prop-valuedâ†’ : is-prop-valued _<_ â†’ is-prop-valued _â‰º_
    is-prop-valuedâ†’ j y y' = j (fâ»Â¹ y) (fâ»Â¹ y')

    is-prop-valuedâ† : is-prop-valued _â‰º_ â†’ is-prop-valued _<_
    is-prop-valuedâ† i x x' = Î³
     where
      Î´ : is-prop (fâ»Â¹ (f x) < fâ»Â¹ (f x'))
      Î´ = i (f x) (f x')

      Î³ : is-prop (x < x')
      Î³ = transportâ‚‚ (Î» x x' â†’ is-prop (x < x')) (Î· x) (Î· x') Î´

    is-well-foundedâ†’ : is-well-founded _<_ â†’ is-well-founded _â‰º_
    is-well-foundedâ†’ = retract-well-founded _<_ _â‰º_ f fâ»Â¹ Îµ Î³
     where
      Î³ : (x : X) (y : Y) â†’ y â‰º f x â†’ fâ»Â¹ y < x
      Î³ x y = transport ( fâ»Â¹ y <_) (Î· x)

    is-well-foundedâ† : is-well-founded _â‰º_ â†’ is-well-founded _<_
    is-well-foundedâ† = retract-well-founded _â‰º_ _<_ fâ»Â¹ f Î· Î³
     where
      Î³ : (y : Y) (x : X) â†’ x < fâ»Â¹ y â†’ f x â‰º y
      Î³ y x = transport (_< fâ»Â¹ y) ((Î· x)â»Â¹)

    is-extensionalâ†’ : is-extensional _<_ â†’ is-extensional _â‰º_
    is-extensionalâ†’ e y y' Ï• Î³ = p
     where
      I : (x : X) â†’ x < fâ»Â¹ y â†’ x < fâ»Â¹ y'
      I x l = transport (_< fâ»Â¹ y') (Î· x) a
       where
        a : fâ»Â¹ (f x) < fâ»Â¹ y'
        a = Ï• (f x) (transport (_< fâ»Â¹ y) ((Î· x)â»Â¹) l)

      II : (x : X) â†’ x < fâ»Â¹ y' â†’ x < fâ»Â¹ y
      II x l = transport (_< fâ»Â¹ y) (Î· x) b
       where
        b : fâ»Â¹ (f x) < fâ»Â¹ y
        b = Î³ (f x) (transport (_< fâ»Â¹ y') ((Î· x)â»Â¹) l)

      q : fâ»Â¹ y ï¼ fâ»Â¹ y'
      q = e (fâ»Â¹ y) (fâ»Â¹ y') I II

      p : y ï¼ y'
      p = sections-are-lc fâ»Â¹ (f , Îµ) q

    is-extensionalâ† : is-extensional _â‰º_ â†’ is-extensional _<_
    is-extensionalâ† e x x' Ï• Î³ = p
     where
      I : (y : Y) â†’ fâ»Â¹ y < fâ»Â¹ (f x) â†’ fâ»Â¹ y < fâ»Â¹ (f x')
      I y l = transport (fâ»Â¹ y <_) ((Î· x')â»Â¹) a
       where
        a : fâ»Â¹ y < x'
        a = Ï• (fâ»Â¹ y) (transport (fâ»Â¹ y <_) (Î· x) l)

      II : (y : Y) â†’ fâ»Â¹ y < fâ»Â¹ (f x') â†’ fâ»Â¹ y < fâ»Â¹ (f x)
      II y l = transport (fâ»Â¹ y <_) ((Î· x)â»Â¹) b
       where
        b : fâ»Â¹ y < x
        b = Î³ (fâ»Â¹ y) (transport (fâ»Â¹ y <_) (Î· x') l)

      q : f x ï¼ f x'
      q = e (f x) (f x') I II

      p : x ï¼ x'
      p = sections-are-lc f (fâ»Â¹ , Î·) q

    is-transitiveâ†’ : is-transitive _<_ â†’ is-transitive _â‰º_
    is-transitiveâ†’ t x y z = t (fâ»Â¹ x) (fâ»Â¹ y) (fâ»Â¹ z)

    is-transitiveâ† : is-transitive _â‰º_ â†’ is-transitive _<_
    is-transitiveâ† t x y z = II
     where
      I : fâ»Â¹ (f x) < fâ»Â¹ (f y) â†’ fâ»Â¹ (f y) < fâ»Â¹ (f z) â†’ fâ»Â¹ (f x) < fâ»Â¹ (f z)
      I = t (f x) (f y) (f z)

      II : x < y â†’ y < z â†’ x < z
      II = transportâ‚ƒ (Î» a b c â†’ a < b â†’ b < c â†’ a < c) (Î· x) (Î· y) (Î· z) I

\end{code}

Putting all this together, we get the desired result:

\begin{code}

    well-orderâ†’ : is-well-order _<_ â†’ is-well-order _â‰º_
    well-orderâ†’ (p , w , e , t) = is-prop-valuedâ†’ p ,
                                  is-well-foundedâ†’ w ,
                                  is-extensionalâ†’ e ,
                                  is-transitiveâ†’ t

    well-orderâ† : is-well-order _â‰º_ â†’ is-well-order _<_
    well-orderâ† (p , w , e , t) = is-prop-valuedâ† p ,
                                  is-well-foundedâ† w ,
                                  is-extensionalâ† e ,
                                  is-transitiveâ† t

    transport-well-order = well-orderâ†’ , well-orderâ†

\end{code}

So we see how much work univalence is performing behind the scenes,
when it is available, as in the construction
transport-ordinal-structure.

\begin{code}

module order-transfer-lemmaâ‚‚
         (X   : ğ“¤ Ì‡ )
         (_<_ : X â†’ X â†’ ğ“¥ Ì‡ )
         (_â‰º_ : X â†’ X â†’ ğ“¦ Ì‡ )
         (ğ•— : (x y : X) â†’ (x < y) â‰ƒ (x â‰º y))
       where

    private
      f : {x y : X} â†’ x < y â†’ x â‰º y
      f {x} {y} = âŒœ ğ•— x y âŒ

      fâ»Â¹ : {x y : X} â†’ x â‰º y â†’ x < y
      fâ»Â¹ {x} {y} = inverse (f {x} {y}) (âŒœâŒ-is-equiv (ğ•— x y))

    is-prop-valuedâ†’ : is-prop-valued _<_ â†’ is-prop-valued _â‰º_
    is-prop-valuedâ†’ i x y = equiv-to-prop (â‰ƒ-sym (ğ•— x y)) (i x y)

    is-well-foundedâ†’ : is-well-founded _<_ â†’ is-well-founded _â‰º_
    is-well-foundedâ†’ = retract-well-founded _<_ _â‰º_ id id
                        (Î» x â†’ refl) (Î» x y â†’ fâ»Â¹ {y} {x})

    is-extensionalâ†’ : is-extensional _<_ â†’ is-extensional _â‰º_
    is-extensionalâ†’ e x y Ï• Î³ = p
     where
      I : (u : X) â†’ u < x â†’ u < y
      I u l = fâ»Â¹ (Ï• u (f l))

      II : (u : X) â†’ u < y â†’ u < x
      II u l = fâ»Â¹ (Î³ u (f l))

      p : x ï¼ y
      p = e x y I II

    is-transitiveâ†’ : is-transitive _<_ â†’ is-transitive _â‰º_
    is-transitiveâ†’ t x y z l m = f (t x y z (fâ»Â¹ l) (fâ»Â¹ m))

module order-transfer-lemmaâ‚ƒ
         (X   : ğ“¤ Ì‡ )
         (_<_ : X â†’ X â†’ ğ“¤ Ì‡ )
         (_â‰º_ : X â†’ X â†’ ğ“¥ Ì‡ )
         (ğ•— : (x y : X) â†’ (x < y) â‰ƒ (x â‰º y))
       where

    well-orderâ†’ : is-well-order _<_ â†’ is-well-order _â‰º_
    well-orderâ†’ (p , w , e , t) = is-prop-valuedâ†’ p ,
                                  is-well-foundedâ†’ w ,
                                  is-extensionalâ†’ e ,
                                  is-transitiveâ†’ t
     where
      open order-transfer-lemmaâ‚‚ X _<_ _â‰º_ ğ•—

    well-orderâ† : is-well-order _â‰º_ â†’ is-well-order _<_
    well-orderâ† (p , w , e , t) = is-prop-valuedâ†’ p ,
                                  is-well-foundedâ†’ w ,
                                  is-extensionalâ†’ e ,
                                  is-transitiveâ†’ t
     where
      open order-transfer-lemmaâ‚‚ X _â‰º_ _<_ (Î» x y â†’ â‰ƒ-sym (ğ•— x y))

    transport-well-order : is-well-order _<_ â†” is-well-order _â‰º_
    transport-well-order = well-orderâ†’ , well-orderâ†

    transport-well-order-â‰ƒ : (is-well-order _<_) â‰ƒ (is-well-order _â‰º_)
    transport-well-order-â‰ƒ = logically-equivalent-props-are-equivalent
                               (being-well-order-is-prop (_<_) fe)
                               (being-well-order-is-prop (_â‰º_) fe)
                               (lr-implication transport-well-order)
                               (rl-implication transport-well-order)
\end{code}

We can transport structures of ordinals with resizable order:

\begin{code}

resizable-order : Ordinal ğ“¤ â†’ (ğ“¥ : Universe) â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
resizable-order Î± ğ“¥ = Î£ _<_ ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î± âŸ© â†’ ğ“¥ Ì‡ ) ,
                              ((x y : âŸ¨ Î± âŸ©) â†’ (x â‰ºâŸ¨ Î± âŸ© y) â‰ƒ (x < y))


transfer-structure : (X : ğ“¤ Ì‡ ) (Î± : Ordinal ğ“¥)
                   â†’ X â‰ƒ âŸ¨ Î± âŸ©
                   â†’ resizable-order Î± ğ“¤
                   â†’ Î£ s ê‰ OrdinalStructure X , (X , s) â‰ƒâ‚’ Î±
transfer-structure {ğ“¤} {ğ“¥} X Î± ğ•— (_<_ , <-is-equivalent-to-â‰º) = Î³
 where
  f : X â†’ âŸ¨ Î± âŸ©
  f = âŒœ ğ•— âŒ

  fâ»Â¹ : âŸ¨ Î± âŸ© â†’ X
  fâ»Â¹ = inverse f (âŒœâŒ-is-equiv ğ•—)

  Îµ : f âˆ˜ fâ»Â¹ âˆ¼ id
  Îµ = inverses-are-sections f (âŒœâŒ-is-equiv ğ•—)

  wâ» : is-well-order _<_
  wâ» = order-transfer-lemmaâ‚ƒ.well-orderâ†’ âŸ¨ Î± âŸ© (underlying-order Î±) _<_
                               <-is-equivalent-to-â‰º (is-well-ordered Î±)

  _â‰º_ : X â†’ X â†’ ğ“¤ Ì‡
  x â‰º y = f x < f y

  w : is-well-order _â‰º_
  w = order-transfer-lemmaâ‚.well-orderâ†’ âŸ¨ Î± âŸ© X (â‰ƒ-sym ğ•—) _<_ wâ»

  fâ»Â¹-preserves-order : (a b : âŸ¨ Î± âŸ©) â†’ a â‰ºâŸ¨ Î± âŸ© b â†’ fâ»Â¹ a â‰º fâ»Â¹ b
  fâ»Â¹-preserves-order a b l = II
   where
    I : a < b
    I = âŒœ <-is-equivalent-to-â‰º a b âŒ l

    II : f (fâ»Â¹ a) < f (fâ»Â¹ b)
    II = transportâ‚‚ _<_ ((Îµ a)â»Â¹) ((Îµ b)â»Â¹) I

  f-preserves-order : (x y : X) â†’ x â‰º y â†’ f x â‰ºâŸ¨ Î± âŸ© f y
  f-preserves-order x y = âŒœ <-is-equivalent-to-â‰º (f x) (f y) âŒâ»Â¹

  e : (X , _â‰º_ , w) â‰ƒâ‚’ Î±
  e = (f , f-preserves-order , âŒœâŒ-is-equiv ğ•— , fâ»Â¹-preserves-order)

  Î³ : Î£ s ê‰ OrdinalStructure X , (X , s) â‰ƒâ‚’ Î±
  Î³ = ((_â‰º_ , w) , e)

\end{code}
