Tom de Jong, Nicolai Kraus, Fredrik Nordvall Forsberg, Chuangjie Xu,
December 2024

We record various properties of the abstract and concrete constructions of
ordinal exponentiation using transport and the equivalence proved in
Ordinals.Exponentiation.RelatingConstructions.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.Univalence
open import UF.PropTrunc
open import UF.Size

module Ordinals.Exponentiation.PropertiesViaTransport
       (ua : Univalence)
       (pt : propositional-truncations-exist)
       (sr : Set-Replacement pt)
       where

open import UF.FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import MLTT.List
open import MLTT.Spartan

open import UF.Base
open import UF.DiscreteAndSeparated

open import Ordinals.Arithmetic fe
open import Ordinals.Notions
open import Ordinals.OrdinalOfOrdinals ua
open import Ordinals.Type
open import Ordinals.Underlying

open import Ordinals.Exponentiation.DecreasingList ua pt
open import Ordinals.Exponentiation.RelatingConstructions ua pt sr
open import Ordinals.Exponentiation.Specification ua pt sr
open import Ordinals.Exponentiation.Supremum ua pt sr
open import Ordinals.Exponentiation.TrichotomousLeastElement ua pt

open import DiscreteGraphicMonoids.ListsWithoutRepetitions fe'
             using (List-is-discrete)
open import TypeTopology.SigmaDiscreteAndTotallySeparated
             using (Ã—-is-discrete)

\end{code}

The exponentiation constructions inherit decidability properties from Î± and Î².

\begin{code}

expá´¸-preserves-discreteness : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                            â†’ is-discrete âŸ¨ Î± âŸ©
                            â†’ is-discrete âŸ¨ Î² âŸ©
                            â†’ is-discrete âŸ¨ expá´¸[ğŸ™+ Î± ] Î² âŸ©
expá´¸-preserves-discreteness Î± Î² Î±-is-disc Î²-is-disc l@(xs , _) l'@(ys , _) =
 III II
  where
   I : is-discrete (âŸ¨ Î± âŸ© Ã— âŸ¨ Î² âŸ©)
   I = Ã—-is-discrete Î±-is-disc Î²-is-disc

   II : is-decidable (xs ï¼ ys)
   II = List-is-discrete â¦ƒ discrete-gives-discrete' I â¦„ xs ys

   III : is-decidable (xs ï¼ ys) â†’ is-decidable (l ï¼ l')
   III (inl  eq) = inl (to-DecrListâ‚‚-ï¼ Î± Î² eq)
   III (inr neq) = inr (Î» p â†’ neq (ap (DecrListâ‚‚-list Î± Î²) p))

exponentiationá´¸-preserves-discreteness : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                         (h : has-trichotomous-least-element Î±)
                                       â†’ is-discrete âŸ¨ Î± âŸ©
                                       â†’ is-discrete âŸ¨ Î² âŸ©
                                       â†’ is-discrete âŸ¨ exponentiationá´¸ Î± h Î² âŸ©
exponentiationá´¸-preserves-discreteness Î± Î² h@(âŠ¥ , _) Î±-is-discrete Î²-is-discrete =
 expá´¸-preserves-discreteness (Î± âº[ h ]) Î² Î±âº-is-discrete Î²-is-discrete
  where
   Î±âº-is-discrete : is-discrete âŸ¨ Î± âº[ h ] âŸ©
   Î±âº-is-discrete = subtype-is-discrete
                     (Prop-valuedness Î± âŠ¥)
                     Î±-is-discrete

^â‚’-preserves-discreteness-for-base-with-trichotomous-least-element
 : (Î± Î² : Ordinal ğ“¤)
 â†’ has-trichotomous-least-element Î±
 â†’ is-discrete âŸ¨ Î± âŸ©
 â†’ is-discrete âŸ¨ Î² âŸ©
 â†’ is-discrete âŸ¨ Î± ^â‚’ Î² âŸ©
^â‚’-preserves-discreteness-for-base-with-trichotomous-least-element
 Î± Î² h Î±-disc Î²-disc =
  transport (Î» - â†’ is-discrete âŸ¨ - âŸ©)
            (exponentiation-constructions-agree Î± Î² h)
            (exponentiationá´¸-preserves-discreteness Î± Î² h Î±-disc Î²-disc)

expá´¸-preserves-trichotomy : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                          â†’ is-trichotomous Î±
                          â†’ is-trichotomous Î²
                          â†’ is-trichotomous (expá´¸[ğŸ™+ Î± ] Î²)
expá´¸-preserves-trichotomy Î± Î² tri-Î± tri-Î² l@(xs , _) l'@(ys , _) =
 Îº (tri xs ys)
 where
  tri : (xs ys : List âŸ¨  Î± Ã—â‚’ Î² âŸ©)
      â†’ (xs â‰ºâŸ¨List (Î± Ã—â‚’ Î²) âŸ© ys) + (xs ï¼ ys) + (ys â‰ºâŸ¨List (Î± Ã—â‚’ Î²) âŸ© xs)
  tri [] [] = inr (inl refl)
  tri [] (x âˆ· ys) = inl []-lex
  tri (x âˆ· xs) [] = inr (inr []-lex)
  tri ((a , b) âˆ· xs) ((a' , b') âˆ· ys) =
   Ï• (Ã—â‚’-is-trichotomous Î± Î² tri-Î± tri-Î² (a , b) (a' , b')) (tri xs ys)
   where
    Ï• : in-trichotomy (underlying-order (Î± Ã—â‚’ Î²)) (a , b) (a' , b')
      â†’ in-trichotomy (lex-for-ordinal (Î± Ã—â‚’Â Î²)) xs ys
      â†’ in-trichotomy (lex-for-ordinal (Î± Ã—â‚’Â Î²))
                      ((a , b) âˆ· xs)
                      ((a' , b') âˆ· ys)
    Ï• (inl p)       _              = inl (head-lex p)
    Ï• (inr (inl r)) (inl ps)       = inl (tail-lex r ps)
    Ï• (inr (inl r)) (inr (inl rs)) = inr (inl (apâ‚‚ _âˆ·_ r rs))
    Ï• (inr (inl r)) (inr (inr qs)) = inr (inr (tail-lex (r â»Â¹) qs))
    Ï• (inr (inr q)) _              = inr (inr (head-lex q))

  Îº : (xs â‰ºâŸ¨List (Î± Ã—â‚’ Î²) âŸ© ys) + (xs ï¼ ys) + (ys â‰ºâŸ¨List (Î± Ã—â‚’ Î²) âŸ© xs)
    â†’ (l â‰ºâŸ¨ expá´¸[ğŸ™+ Î± ] Î² âŸ© l') + (l ï¼ l') + (l' â‰ºâŸ¨ expá´¸[ğŸ™+ Î± ] Î² âŸ© l)
  Îº (inl p) = inl p
  Îº (inr (inl e)) = inr (inl (to-DecrListâ‚‚-ï¼ Î± Î² e))
  Îº (inr (inr q)) = inr (inr q)

private
 tri-least : (Î± : Ordinal ğ“¤)
           â†’ has-least Î±
           â†’ is-trichotomous Î±
           â†’ has-trichotomous-least-element Î±
 tri-least Î± (âŠ¥ , âŠ¥-is-least) t =
  âŠ¥ ,
  is-trichotomous-and-least-implies-is-trichotomous-least Î± âŠ¥ (t âŠ¥) âŠ¥-is-least

exponentiationá´¸-preserves-trichotomy
 : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
 â†’ (h : has-least Î±)
 â†’ (t : is-trichotomous Î±)
 â†’ is-trichotomous Î²
 â†’ is-trichotomous (exponentiationá´¸ Î± (tri-least Î± h t) Î²)
exponentiationá´¸-preserves-trichotomy Î± Î² h tri-Î± tri-Î² =
 expá´¸-preserves-trichotomy (Î± âº[ h' ]) Î² tri-Î±âº tri-Î²
  where
   h' : has-trichotomous-least-element Î±
   h' = tri-least Î± h tri-Î±
   tri-Î±âº : is-trichotomous (Î± âº[ h' ])
   tri-Î±âº (x , p) (y , q) = Îº (tri-Î± x y)
    where
     Îº : in-trichotomy (underlying-order Î±) x y
       â†’ in-trichotomy (underlying-order (Î± âº[ h' ])) (x , p) (y , q)
     Îº (inl l)       = inl l
     Îº (inr (inl e)) = inr (inl (to-âº-ï¼ Î± h' e))
     Îº (inr (inr k)) = inr (inr k)

^â‚’-preserves-trichotomy : (Î± Î² : Ordinal ğ“¤)
                        â†’ has-least Î±
                        â†’ is-trichotomous Î±
                        â†’ is-trichotomous Î²
                        â†’ is-trichotomous (Î± ^â‚’ Î²)
^â‚’-preserves-trichotomy
 Î± Î² (âŠ¥ , p) tri-Î± tri-Î² =
  transport is-trichotomous
   (exponentiation-constructions-agree Î± Î² h)
   (exponentiationá´¸-preserves-trichotomy Î± Î² (âŠ¥ , p) tri-Î± tri-Î²)
   where
    h : has-trichotomous-least-element Î±
    h = âŠ¥ ,
        is-trichotomous-and-least-implies-is-trichotomous-least Î± âŠ¥ (tri-Î± âŠ¥) p

\end{code}

Added 11 April 2025.

If the base ordinal has a trichotomous least element, then so does Î± ^â‚’ Î².

\begin{code}

^â‚’-has-trichotomous-least-element : (Î± Î² : Ordinal ğ“¤)
                                  â†’ has-trichotomous-least-element Î±
                                  â†’ has-trichotomous-least-element (Î± ^â‚’ Î²)
^â‚’-has-trichotomous-least-element Î± Î² h =
 transport
  has-trichotomous-least-element
  (exponentiation-constructions-agree Î± Î² h)
  (exponentiationá´¸-has-trichotomous-least-element Î± h Î²)

\end{code}

Since the abstract construction satisfies the ordinal specifications, so does
the concrete construction.

\begin{code}

module _
        (Î± : Ordinal ğ“¤)
        (h : has-trichotomous-least-element Î±)
       where

 exponentiationá´¸-satisfies-zero-specification
  : exp-specification-zero Î± (exponentiationá´¸ Î± h)
 exponentiationá´¸-satisfies-zero-specification =
  transportâ»Â¹ (exp-specification-zero Î±)
              (dfunext fe' (Î» Î² â†’ exponentiation-constructions-agree Î± Î² h))
              (^â‚’-satisfies-zero-specification Î±)

 exponentiationá´¸-satisfies-succ-specification
  : exp-specification-succ Î± (exponentiationá´¸ Î± h)
 exponentiationá´¸-satisfies-succ-specification =
  transportâ»Â¹ (exp-specification-succ Î±)
              (dfunext fe' (Î» Î² â†’ exponentiation-constructions-agree Î± Î² h))
              (^â‚’-satisfies-succ-specification Î±
                (trichotomous-least-element-gives-ğŸ™â‚’-âŠ´ Î± h))

 exponentiationá´¸-satisfies-sup-specification
  : exp-specification-sup Î± (exponentiationá´¸ Î± h)
 exponentiationá´¸-satisfies-sup-specification =
  transportâ»Â¹ (exp-specification-sup Î±)
              (dfunext fe' (Î» Î² â†’ exponentiation-constructions-agree Î± Î² h))
              (^â‚’-satisfies-sup-specification Î±)

\end{code}

Further properties whose direct proofs would require combinatorics of decreasing
lists can also be derived via transport.

\begin{code}

 exponentiationá´¸-monotone-in-exponent :
  (Î² Î³ : Ordinal ğ“¤) â†’ Î² âŠ´ Î³ â†’ exponentiationá´¸ Î± h Î² âŠ´ exponentiationá´¸ Î± h Î³
 exponentiationá´¸-monotone-in-exponent Î² Î³ l =
  transportâ‚‚ _âŠ´_
   ((exponentiation-constructions-agree Î± Î² h) â»Â¹)
   ((exponentiation-constructions-agree Î± Î³ h) â»Â¹)
   (^â‚’-monotone-in-exponent Î± Î² Î³ l)

 ğŸ™â‚’-neutral-exponentiationá´¸ : exponentiationá´¸ Î± h ğŸ™â‚’ ï¼ Î±
 ğŸ™â‚’-neutral-exponentiationá´¸ =
  transportâ»Â¹
   (_ï¼ Î±)
   (exponentiation-constructions-agree Î± ğŸ™â‚’ h)
   (ğŸ™â‚’-neutral-^â‚’ Î± (trichotomous-least-element-gives-ğŸ™â‚’-âŠ´ Î± h))

 exponentiationá´¸-by-ğŸšâ‚’-is-Ã—â‚’ : exponentiationá´¸ Î± h ğŸšâ‚’ ï¼ Î± Ã—â‚’ Î±
 exponentiationá´¸-by-ğŸšâ‚’-is-Ã—â‚’ =
  transportâ»Â¹
   (_ï¼ Î± Ã—â‚’ Î±)
   (exponentiation-constructions-agree Î± ğŸšâ‚’ h)
   (^â‚’-ğŸšâ‚’-is-Ã—â‚’ Î± (trichotomous-least-element-gives-ğŸ™â‚’-âŠ´ Î± h))

 exponentiationá´¸-by-+â‚’
  : (Î² Î³ : Ordinal ğ“¤)
  â†’ exponentiationá´¸ Î± h (Î² +â‚’ Î³)
    ï¼ exponentiationá´¸ Î± h Î² Ã—â‚’ exponentiationá´¸ Î± h Î³
 exponentiationá´¸-by-+â‚’ Î² Î³ =
  transportâ‚‚
   _ï¼_
    (exponentiation-constructions-agree Î± (Î² +â‚’ Î³) h â»Â¹)
    (apâ‚‚ _Ã—â‚’_
         ((exponentiation-constructions-agree Î± Î² h) â»Â¹)
         ((exponentiation-constructions-agree Î± Î³ h) â»Â¹))
    (^â‚’-by-+â‚’ Î± Î² Î³)

 module _
         (Î² Î³ : Ordinal ğ“¤)
        where

  private
   h' : has-trichotomous-least-element (exponentiationá´¸ Î± h Î²)
   h' = exponentiationá´¸-has-trichotomous-least-element Î± h Î²

  exponentiationá´¸-by-Ã—â‚’
   : exponentiationá´¸ Î± h (Î² Ã—â‚’ Î³) ï¼ exponentiationá´¸ (exponentiationá´¸ Î± h Î²) h' Î³
  exponentiationá´¸-by-Ã—â‚’ =
   transportâ‚‚
    _ï¼_
     (exponentiation-constructions-agree Î± (Î² Ã—â‚’ Î³) h â»Â¹)
     ((exponentiation-constructions-agree (exponentiationá´¸ Î± h Î²) Î³ h'
        âˆ™ ap (_^â‚’ Î³) (exponentiation-constructions-agree Î± Î² h)) â»Â¹)
     (^â‚’-by-Ã—â‚’ Î± Î² Î³)

 exponentiationá´¸-order-preserving-in-exponent
  : (Î² Î³ : Ordinal ğ“¤)
  â†’ ğŸ™â‚’ âŠ² Î±
  â†’ Î² âŠ² Î³ â†’ exponentiationá´¸ Î± h Î² âŠ² exponentiationá´¸ Î± h Î³
 exponentiationá´¸-order-preserving-in-exponent Î² Î³ l k =
  transportâ‚‚
   _âŠ²_
   (exponentiation-constructions-agree Î± Î² h â»Â¹)
   (exponentiation-constructions-agree Î± Î³ h â»Â¹)
   (^â‚’-order-preserving-in-exponent Î± Î² Î³ l k)

\end{code}
