Tom de Jong, Nicolai Kraus, Fredrik Nordvall Forsberg, Chuangjie Xu,
26 November 2024.

\begin{code}

{-# OPTIONS --safe --without-K --no-exact-split --lossy-unification #-}

open import UF.Univalence
open import UF.PropTrunc
open import UF.Size

module Ordinals.Exponentiation.DecreasingListAlternative
       (ua : Univalence)
       (pt : propositional-truncations-exist)
       (sr : Set-Replacement pt)
       where

open import UF.Base
open import UF.ClassicalLogic
open import UF.Equiv
open import UF.FunExt
open import UF.Sets
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.ImageAndSurjection pt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

 pe : PropExt
 pe = Univalence-gives-PropExt ua

open import MLTT.Plus-Properties
open import MLTT.Spartan
open import MLTT.Sigma
open import MLTT.List

open import Ordinals.Arithmetic fe
open import Ordinals.AdditionProperties ua
open import Ordinals.Equivalence
open import Ordinals.Maps
open import Ordinals.MultiplicationProperties ua
open import Ordinals.Notions
open import Ordinals.OrdinalOfOrdinals ua
open import Ordinals.Type
open import Ordinals.Underlying
open import Ordinals.WellOrderingTaboo
open import Ordinals.OrdinalOfOrdinalsSuprema ua

open import Ordinals.Exponentiation.DecreasingList ua pt sr

open PropositionalTruncation pt

open suprema pt sr

\end{code}

Detachable least element

\begin{code}

has-a-detachable-least-element : Ordinal ğ“¤ â†’ ğ“¤ Ì‡
has-a-detachable-least-element Î± = Î£ aâ‚€ ê‰ âŸ¨ Î± âŸ© , ((x : âŸ¨ Î± âŸ©) â†’ is-decidable (x ï¼ aâ‚€)) Ã—
                                                  ((x : âŸ¨ Î± âŸ©) â†’ Â¬ (x ï¼ aâ‚€) â†’ aâ‚€ â‰ºâŸ¨ Î± âŸ© x)

has-a-detachable-least-element' : Ordinal ğ“¤ â†’ ğ“¤ Ì‡
has-a-detachable-least-element' Î± = Î£ aâ‚€ ê‰ âŸ¨ Î± âŸ© , ((x : âŸ¨ Î± âŸ©) â†’ is-decidable (x ï¼ aâ‚€)) Ã—
                                                   is-minimal Î± aâ‚€
                                                -- ((x : âŸ¨ Î± âŸ©) â†’ Â¬ (x â‰ºâŸ¨ Î± âŸ© aâ‚€))

has-a-detachable-least-element'' : Ordinal ğ“¤ â†’ ğ“¤ Ì‡
has-a-detachable-least-element'' Î± = (x : âŸ¨ Î± âŸ©)
                                   â†’ is-decidable ((y : âŸ¨ Î± âŸ©) â†’ Â¬ (y ï¼ x) â†’ x â‰ºâŸ¨ Î± âŸ© y)

has-a-detachable-least-element''' : Ordinal ğ“¤ â†’ ğ“¤ Ì‡
has-a-detachable-least-element''' Î± = (x : âŸ¨ Î± âŸ©) â†’ is-decidable (is-minimal Î± x)


positive-sub-oridnal : (Î± : Ordinal ğ“¤) â†’ has-a-detachable-least-element Î± â†’ Ordinal ğ“¤
positive-sub-oridnal Î± (aâ‚€ , aâ‚€-dec , aâ‚€-least) =
  âŸ¨Î±'âŸ© , _<'_ , <'-propvalued , <'-wellfounded , <'-extensional , <'-transitive
 where
  âŸ¨Î±'âŸ© = Î£ a ê‰ âŸ¨ Î± âŸ© , aâ‚€ â‰ºâŸ¨ Î± âŸ© a

  _<'_ : âŸ¨Î±'âŸ© â†’ âŸ¨Î±'âŸ© â†’ _
  _<'_ = subtype-order Î± (Î» - â†’ aâ‚€ â‰ºâŸ¨ Î± âŸ© -)

  <'-propvalued : is-prop-valued _<'_
  <'-propvalued = subtype-order-propositional Î± (Î» - â†’ aâ‚€ â‰ºâŸ¨ Î± âŸ© -)

  <'-wellfounded : is-well-founded _<'_
  <'-wellfounded = subtype-order-wellfounded Î± (Î» - â†’ aâ‚€ â‰ºâŸ¨ Î± âŸ© -)

  <'-extensional : is-extensional _<'_
  <'-extensional (x , p) (y , q) f g = to-subtype-ï¼ (Î» x â†’ Prop-valuedness Î± aâ‚€ x)
                                                     (Extensionality Î± x y u v)
   where
    u : (z : âŸ¨ Î± âŸ©) â†’ z â‰ºâŸ¨ Î± âŸ© x â†’ z â‰ºâŸ¨ Î± âŸ© y
    u z r with aâ‚€-dec z
    ... | inl refl = q
    ... | inr s = f (z , aâ‚€-least z s) r
    v : (z : âŸ¨ Î± âŸ©) â†’ z â‰ºâŸ¨ Î± âŸ© y â†’ z â‰ºâŸ¨ Î± âŸ© x
    v z r with aâ‚€-dec z
    ... | inl refl = p
    ... | inr s = g (z , aâ‚€-least z s) r

  <'-transitive : is-transitive _<'_
  <'-transitive = subtype-order-transitive Î± (Î» - â†’ aâ‚€ â‰ºâŸ¨ Î± âŸ© -)


_âº[_] : (Î± : Ordinal ğ“¤) â†’ has-a-detachable-least-element Î± â†’ Ordinal ğ“¤
Î± âº[ dâŠ¥ ] = positive-sub-oridnal Î± dâŠ¥


has-a-detachable-least-element-lemma : (Î± : Ordinal ğ“¤) (dâŠ¥ : has-a-detachable-least-element Î±)
    â†’ Î± ï¼ ğŸ™â‚’ +â‚’ (Î± âº[ dâŠ¥ ])
has-a-detachable-least-element-lemma Î± dâŠ¥@(aâ‚€ , aâ‚€-dec , aâ‚€-least) = eq
 where
  Î±' = Î± âº[ dâŠ¥ ]

  f' : (x : âŸ¨ Î± âŸ©) â†’ is-decidable (x ï¼ aâ‚€) â†’ ğŸ™ + âŸ¨ Î±' âŸ©
  f' x (inl _) = inl â‹†
  f' x (inr q) = inr (x , aâ‚€-least x q)

  f : âŸ¨ Î± âŸ© â†’ ğŸ™ + âŸ¨ Î±' âŸ©
  f x = f' x (aâ‚€-dec x)

  g : ğŸ™ + âŸ¨ Î±' âŸ© â†’ âŸ¨ Î± âŸ©
  g (inl â‹†) = aâ‚€
  g (inr (x , _)) = x

  f-equiv : is-order-equiv Î± (ğŸ™â‚’ +â‚’ Î±') f
  f-equiv = f-order-preserving , (qinvs-are-equivs f (g , Î· , Ïµ)) , g-order-preserving
   where
    f'-order-preserving : (x y : âŸ¨ Î± âŸ©)
                        â†’ (dx : is-decidable (x ï¼ aâ‚€))
                        â†’ (dy : is-decidable (y ï¼ aâ‚€))
                        â†’ x â‰ºâŸ¨ Î± âŸ© y â†’ f' x dx â‰ºâŸ¨ ğŸ™â‚’ +â‚’ Î±' âŸ© f' y dy
    f'-order-preserving .aâ‚€ .aâ‚€ (inl refl) (inl refl) r = ğŸ˜-elim (irrefl Î± aâ‚€ r)
    f'-order-preserving .aâ‚€ y (inl refl) (inr q) r = â‹†
    f'-order-preserving x .aâ‚€ (inr p) (inl refl) r = ğŸ˜-elim (irrefl Î± x x<x)
     where
      x<x : x â‰ºâŸ¨ Î± âŸ© x
      x<x = Transitivity Î± x aâ‚€ x r (aâ‚€-least x p)
    f'-order-preserving x y (inr p) (inr q) r = r
    f-order-preserving : is-order-preserving Î± (ğŸ™â‚’ +â‚’ Î±') f
    f-order-preserving x y = f'-order-preserving x y (aâ‚€-dec x) (aâ‚€-dec y)
    g-order-preserving : is-order-preserving (ğŸ™â‚’ +â‚’ Î±') Î± g
    g-order-preserving (inl â‹†) (inr (y , p)) q = p
    g-order-preserving (inr x) (inr (y , p)) q = q
    Î·' : (x : âŸ¨ Î± âŸ©) â†’ (d : is-decidable (x ï¼ aâ‚€)) â†’ g (f' x d) ï¼ x
    Î·' .aâ‚€ (inl refl) = refl
    Î·' x (inr p) = refl
    Î· : (x : âŸ¨ Î± âŸ©) â†’ g (f x) ï¼ x
    Î· x = Î·' x (aâ‚€-dec x)
    Ïµ' : (y : ğŸ™ + âŸ¨ Î±' âŸ©) â†’ (d : is-decidable (g y ï¼ aâ‚€)) â†’ f' (g y) d ï¼ y
    Ïµ' (inl â‹†) (inl e) = refl
    Ïµ' (inl â‹†) (inr q) = ğŸ˜-elim (q refl)
    Ïµ' (inr (.aâ‚€ , p)) (inl refl) = ğŸ˜-elim (irrefl Î± aâ‚€ p)
    Ïµ' (inr (x , p)) (inr q) = ap inr (to-subtype-ï¼ ((Î» x â†’ Prop-valuedness Î± aâ‚€ x)) refl)
    Ïµ : (y : ğŸ™ + âŸ¨ Î±' âŸ©) â†’ f (g y) ï¼ y
    Ïµ y = Ïµ' y (aâ‚€-dec (g y))

  eq : Î± ï¼ ğŸ™â‚’ +â‚’ Î±'
  eq = eqtoidâ‚’ (ua _) fe' Î± (ğŸ™â‚’ +â‚’ Î±') (f , f-equiv)

\end{code}

Exponentiation whose base is an ordinal with a detachable least element

\begin{code}

exp : (Î± : Ordinal ğ“¤) â†’ has-a-detachable-least-element Î± â†’ Ordinal ğ“¥ â†’ Ordinal (ğ“¤ âŠ” ğ“¥)
exp Î± dâŠ¥ Î² = [ğŸ™+ (Î± âº[ dâŠ¥ ]) ]^ Î²

exp-dle-0-spec : (Î± : Ordinal ğ“¤) (dâŠ¥ : has-a-detachable-least-element Î±)
    â†’ exp Î± _ (ğŸ˜â‚’ {ğ“¥}) ï¼ ğŸ™â‚’
exp-dle-0-spec Î± dâŠ¥ = exp-0-spec (Î± âº[ dâŠ¥ ])

exp-dle-succ-spec : (Î± : Ordinal ğ“¤) (dâŠ¥ : has-a-detachable-least-element Î±)
    â†’ (Î² : Ordinal ğ“¤)
    â†’ exp Î± _ (Î² +â‚’ ğŸ™â‚’) ï¼ exp Î± _ Î² Ã—â‚’ Î±
exp-dle-succ-spec Î± dâŠ¥ Î² = goal
 where
  fact : exp Î± _ (Î² +â‚’ ğŸ™â‚’) ï¼ exp Î± _ Î² Ã—â‚’ (ğŸ™â‚’ +â‚’ (Î± âº[ dâŠ¥ ]))
  fact = exp-succ-spec (Î± âº[ dâŠ¥ ]) Î²
  eq : Î± ï¼ ğŸ™â‚’ +â‚’ (Î± âº[ dâŠ¥ ])
  eq = has-a-detachable-least-element-lemma Î± dâŠ¥
  goal : exp Î± _ (Î² +â‚’ ğŸ™â‚’) ï¼ exp Î± _ Î² Ã—â‚’ Î±
  goal = transport (Î» x â†’ exp Î± dâŠ¥ (Î² +â‚’ ğŸ™â‚’) ï¼ exp Î± dâŠ¥ Î² Ã—â‚’ x) (eq â»Â¹) fact

exp-dle-sup-spec : (Î± : Ordinal ğ“¤) (dâŠ¥ : has-a-detachable-least-element Î±)
    â†’ {I : ğ“¤ Ì‡} â†’ âˆ¥ I âˆ¥ â†’ (Î² : I â†’ Ordinal ğ“¤)
    â†’ sup (Î» i â†’ exp Î± _ (Î² i)) ï¼ exp Î± _ (sup Î²)
exp-dle-sup-spec Î± dâŠ¥ = exp-sup-spec (Î± âº[ dâŠ¥ ])

\end{code}
