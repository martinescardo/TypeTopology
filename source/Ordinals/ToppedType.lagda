Martin Escardo, 29 June 2018

The subtype Ordinalsáµ€ of ordinals with a top element.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import UF.FunExt

module Ordinals.ToppedType
        (fe : FunExt)
       where

open import MLTT.Spartan
open import Notation.UnderlyingType
open import Ordinals.Notions
open import Ordinals.Type

open import UF.Base
open import UF.Subsingletons

\end{code}

To get closure under sums constructively, we need to restrict to
particular kinds of ordinals. Having a top element is a simple
sufficient condition, which holds in the applications we have in mind
(for compact ordinals).  Classically, the topped ordinals are the
successor ordinals. Constructively, â„•âˆ is an example of an ordinal
with a top element which is not a successor ordinal, as its top
element is not isolated.

\begin{code}

Ordinaláµ€ : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
Ordinaláµ€ ğ“¤ = Î£ Î± ê‰ Ordinal ğ“¤ , has-top (underlying-order Î±)

instance
 underlying-type-of-topped-ordinal : Underlying-Type (Ordinaláµ€ ğ“¤) (ğ“¤ Ì‡)
 âŸ¨_âŸ© {{underlying-type-of-topped-ordinal}} (Î± , _) = âŸ¨ Î± âŸ©


underlying-type-is-setáµ€ : FunExt
                        â†’ (Î² : Ordinaláµ€ ğ“¤)
                        â†’ is-set âŸ¨ Î² âŸ©
underlying-type-is-setáµ€ fe (Î± , t) = underlying-type-is-set fe Î±

\end{code}

Topped ordinals are ranged over by Ï„,Ï….

\begin{code}

tunderlying-order : (Ï„ : Ordinaláµ€ ğ“¤) â†’ âŸ¨ Ï„ âŸ© â†’ âŸ¨ Ï„ âŸ© â†’ ğ“¤ Ì‡
tunderlying-order ((X , _<_ , o) , t) = _<_

syntax tunderlying-order Ï„ x y = x â‰ºâŸ¨ Ï„ âŸ© y

tunderlying-rorder : (Ï„ : Ordinaláµ€ ğ“¤) â†’ âŸ¨ Ï„ âŸ© â†’ âŸ¨ Ï„ âŸ© â†’ ğ“¤ Ì‡
tunderlying-rorder Ï„ x y = Â¬ (y â‰ºâŸ¨ Ï„ âŸ© x)

syntax tunderlying-rorder Ï„ x y = x â‰¼âŸ¨ Ï„ âŸ© y

â‰¼-prop-valued : (Ï„ : Ordinaláµ€ ğ“¤) (x y : âŸ¨ Ï„ âŸ©) â†’ is-prop (x â‰¼âŸ¨ Ï„ âŸ© y)
â‰¼-prop-valued {ğ“¤} Ï„ x y l m = dfunext (fe ğ“¤ ğ“¤â‚€) (Î» x â†’ ğŸ˜-elim (m x))

topped : (Ï„ : Ordinaláµ€ ğ“¤) â†’ has-top (tunderlying-order Ï„)
topped (Î± , t) = t

top : (Ï„ : Ordinaláµ€ ğ“¤) â†’ âŸ¨ Ï„ âŸ©
top (Î± , (x , i)) = x

top-is-top : (Ï„ : Ordinaláµ€ ğ“¤) â†’ is-top (tunderlying-order Ï„) (top Ï„)
top-is-top (Î± , (x , i)) = i

tis-well-ordered : (Ï„ : Ordinaláµ€ ğ“¤) â†’ is-well-order (tunderlying-order Ï„)
tis-well-ordered ((X , _<_ , o) , t) = o

open import TypeTopology.InfProperty

has-infs-of-complemented-subsets : Ordinaláµ€ ğ“¤ â†’ ğ“¤ Ì‡
has-infs-of-complemented-subsets Î± = has-inf (Î» x y â†’ x â‰¼âŸ¨ Î± âŸ© y)

\end{code}
