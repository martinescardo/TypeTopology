Fredrik Nordvall Forsberg, 13 November 2023.
In collaboration with Tom de Jong, Nicolai Kraus and Chuangjie Xu.

Minor updates 9 and 11 September, and 1 November 2024.

We prove several properties of ordinal multiplication, including that it
preserves suprema of ordinals and that it enjoys a left-cancellation property.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.Univalence

module Ordinals.MultiplicationProperties
       (ua : Univalence)
       where

open import UF.Base
open import UF.Equiv
open import UF.FunExt
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.ClassicalLogic

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import MLTT.Spartan
open import MLTT.Plus-Properties

open import Ordinals.Arithmetic fe
open import Ordinals.Equivalence
open import Ordinals.Maps
open import Ordinals.OrdinalOfOrdinals ua
open import Ordinals.Type
open import Ordinals.Underlying
open import Ordinals.AdditionProperties ua

Ã—â‚’-ğŸ˜â‚’-right : (Î± : Ordinal ğ“¤) â†’ Î± Ã—â‚’ ğŸ˜â‚’ {ğ“¥} ï¼ ğŸ˜â‚’
Ã—â‚’-ğŸ˜â‚’-right Î± = âŠ´-antisym _ _
                 (to-âŠ´ (Î± Ã—â‚’ ğŸ˜â‚’) ğŸ˜â‚’ (Î» (a , b) â†’ ğŸ˜-elim b))
                 (ğŸ˜â‚’-least-âŠ´ (Î± Ã—â‚’ ğŸ˜â‚’))

Ã—â‚’-ğŸ˜â‚’-left : (Î± : Ordinal ğ“¤) â†’ ğŸ˜â‚’ {ğ“¥} Ã—â‚’ Î± ï¼ ğŸ˜â‚’
Ã—â‚’-ğŸ˜â‚’-left Î± = âŠ´-antisym _ _
                (to-âŠ´ (ğŸ˜â‚’ Ã—â‚’ Î±) ğŸ˜â‚’ (Î» (b , a) â†’ ğŸ˜-elim b))
                (ğŸ˜â‚’-least-âŠ´ (ğŸ˜â‚’ Ã—â‚’ Î±))

ğŸ™â‚’-left-neutral-Ã—â‚’ : (Î± : Ordinal ğ“¤) â†’ ğŸ™â‚’ {ğ“¤} Ã—â‚’ Î± ï¼ Î±
ğŸ™â‚’-left-neutral-Ã—â‚’ {ğ“¤} Î± = eqtoidâ‚’ (ua _) fe' _ _
                            (f , f-order-preserving ,
                             f-is-equiv , g-order-preserving)
 where
  f : ğŸ™ Ã— âŸ¨ Î± âŸ© â†’ âŸ¨ Î± âŸ©
  f = prâ‚‚

  g : âŸ¨ Î± âŸ© â†’ ğŸ™ Ã— âŸ¨ Î± âŸ©
  g = ( â‹† ,_)

  f-order-preserving : is-order-preserving (ğŸ™â‚’ {ğ“¤} Ã—â‚’ Î±) Î± f
  f-order-preserving x y (inl p) = p

  f-is-equiv : is-equiv f
  f-is-equiv = qinvs-are-equivs f (g , (Î» _ â†’ refl) , (Î» _ â†’ refl))

  g-order-preserving : is-order-preserving Î± (ğŸ™â‚’ {ğ“¤} Ã—â‚’ Î±) g
  g-order-preserving x y p = inl p

ğŸ™â‚’-right-neutral-Ã—â‚’ : (Î± : Ordinal ğ“¤) â†’ Î± Ã—â‚’ ğŸ™â‚’ {ğ“¤} ï¼ Î±
ğŸ™â‚’-right-neutral-Ã—â‚’ {ğ“¤} Î± = eqtoidâ‚’ (ua _) fe' _ _
                             (f , f-order-preserving ,
                              f-is-equiv , g-order-preserving)
 where
  f : âŸ¨ Î± âŸ© Ã— ğŸ™ â†’ âŸ¨ Î± âŸ©
  f = prâ‚

  g : âŸ¨ Î± âŸ© â†’ âŸ¨ Î± âŸ© Ã— ğŸ™
  g = (_, â‹† )

  f-order-preserving : is-order-preserving (Î± Ã—â‚’ ğŸ™â‚’ {ğ“¤}) Î± f
  f-order-preserving x y (inr (refl , p)) = p

  f-is-equiv : is-equiv f
  f-is-equiv = qinvs-are-equivs f (g , (Î» _ â†’ refl) , (Î» _ â†’ refl))

  g-order-preserving : is-order-preserving Î± (Î± Ã—â‚’ ğŸ™â‚’ {ğ“¤}) g
  g-order-preserving x y p = inr (refl , p)

\end{code}

Because we use --lossy-unification to speed up typechecking we have to
explicitly mention the universes in the lemma below; using them as variables (as
usual) results in a unification error.

\begin{code}

Ã—â‚’-assoc : {ğ“¤ ğ“¥ ğ“¦ : Universe}
           (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (Î³ : Ordinal ğ“¦)
         â†’ (Î± Ã—â‚’ Î²) Ã—â‚’ Î³ ï¼ Î± Ã—â‚’ (Î² Ã—â‚’ Î³)
Ã—â‚’-assoc Î± Î² Î³ =
 eqtoidâ‚’ (ua _) fe' ((Î±  Ã—â‚’ Î²) Ã—â‚’ Î³) (Î±  Ã—â‚’ (Î² Ã—â‚’ Î³))
  (f , order-preserving-reflecting-equivs-are-order-equivs
   ((Î±  Ã—â‚’ Î²) Ã—â‚’ Î³) (Î±  Ã—â‚’ (Î² Ã—â‚’ Î³))
   f f-equiv f-preserves-order f-reflects-order)
  where
   f : âŸ¨ (Î± Ã—â‚’ Î²) Ã—â‚’ Î³ âŸ© â†’ âŸ¨ Î± Ã—â‚’ (Î² Ã—â‚’ Î³) âŸ©
   f ((a , b) , c) = (a , (b , c))

   g : âŸ¨ Î± Ã—â‚’ (Î² Ã—â‚’ Î³) âŸ© â†’ âŸ¨ (Î± Ã—â‚’ Î²) Ã—â‚’ Î³ âŸ©
   g (a , (b , c)) = ((a , b) , c)

   f-equiv : is-equiv f
   f-equiv = qinvs-are-equivs f (g , (Î» x â†’ refl) , (Î» x â†’ refl))

   f-preserves-order : is-order-preserving  ((Î±  Ã—â‚’ Î²) Ã—â‚’ Î³) (Î±  Ã—â‚’ (Î² Ã—â‚’ Î³)) f
   f-preserves-order _ _ (inl p) = inl (inl p)
   f-preserves-order _ _ (inr (r , inl p)) = inl (inr (r , p))
   f-preserves-order _ _ (inr (r , inr (u , q))) = inr (to-Ã—-ï¼ u r , q)

   f-reflects-order : is-order-reflecting ((Î±  Ã—â‚’ Î²) Ã—â‚’ Î³) (Î±  Ã—â‚’ (Î² Ã—â‚’ Î³)) f
   f-reflects-order _ _ (inl (inl p)) = inl p
   f-reflects-order _ _ (inl (inr (r , q))) = inr (r , (inl q))
   f-reflects-order _ _ (inr (refl , q)) = inr (refl , (inr (refl , q)))

\end{code}

The lemma below is as general as possible in terms of universe parameters
because addition requires its arguments to come from the same universe, at least
at present.

\begin{code}

Ã—â‚’-distributes-+â‚’-right : (Î± : Ordinal ğ“¤) (Î² Î³ : Ordinal ğ“¥)
                        â†’ Î± Ã—â‚’ (Î² +â‚’ Î³) ï¼ (Î± Ã—â‚’ Î²) +â‚’ (Î± Ã—â‚’ Î³)
Ã—â‚’-distributes-+â‚’-right Î± Î² Î³ = eqtoidâ‚’ (ua _) fe' _ _
                                 (f , f-order-preserving ,
                                  f-is-equiv , g-order-preserving)
 where
  f : âŸ¨ Î± Ã—â‚’ (Î² +â‚’ Î³) âŸ© â†’ âŸ¨ (Î± Ã—â‚’ Î²) +â‚’ (Î± Ã—â‚’ Î³) âŸ©
  f (a , inl b) = inl (a , b)
  f (a , inr c) = inr (a , c)

  g : âŸ¨ (Î± Ã—â‚’ Î²) +â‚’ (Î± Ã—â‚’ Î³) âŸ© â†’ âŸ¨ Î± Ã—â‚’ (Î² +â‚’ Î³) âŸ©
  g (inl (a , b)) = a , inl b
  g (inr (a , c)) = a , inr c

  f-order-preserving : is-order-preserving _ _ f
  f-order-preserving (a , inl b) (a' , inl b') (inl p) = inl p
  f-order-preserving (a , inl b) (a' , inr c') (inl p) = â‹†
  f-order-preserving (a , inr c) (a' , inr c') (inl p) = inl p
  f-order-preserving (a , inl b) (a' , inl .b) (inr (refl , q)) = inr (refl , q)
  f-order-preserving (a , inr c) (a' , inr .c) (inr (refl , q)) = inr (refl , q)

  f-is-equiv : is-equiv f
  f-is-equiv = qinvs-are-equivs f (g , Î· , Îµ)
   where
    Î· : g âˆ˜ f âˆ¼ id
    Î· (a , inl b) = refl
    Î· (a , inr c) = refl

    Îµ : f âˆ˜ g âˆ¼ id
    Îµ (inl (a , b)) = refl
    Îµ (inr (a , c)) = refl

  g-order-preserving : is-order-preserving _ _ g
  g-order-preserving (inl (a , b)) (inl (a' , b')) (inl p) = inl p
  g-order-preserving (inl (a , b)) (inl (a' , .b)) (inr (refl , q)) =
   inr (refl , q)
  g-order-preserving (inl (a , b)) (inr (a' , c')) p = inl â‹†
  g-order-preserving (inr (a , c)) (inr (a' , c')) (inl p) = inl p
  g-order-preserving (inr (a , c)) (inr (a' , c')) (inr (refl , q)) =
   inr (refl , q)

\end{code}

The following characterizes the initial segments of a product and is rather
useful when working with simulations between products.

\begin{code}

Ã—â‚’-â†“ : (Î± Î² : Ordinal ğ“¤)
     â†’ {a : âŸ¨ Î± âŸ©} {b : âŸ¨ Î² âŸ©}
     â†’ (Î± Ã—â‚’ Î²) â†“ (a , b) ï¼ (Î± Ã—â‚’ (Î² â†“ b)) +â‚’ (Î± â†“ a)
Ã—â‚’-â†“ Î± Î² {a} {b} = eqtoidâ‚’ (ua _) fe' _ _ (f , f-order-preserving ,
                                           f-is-equiv , g-order-preserving)
 where
  f : âŸ¨ (Î± Ã—â‚’ Î²) â†“ (a , b) âŸ© â†’ âŸ¨ (Î± Ã—â‚’ (Î² â†“ b)) +â‚’ (Î± â†“ a) âŸ©
  f ((x , y) , inl p) = inl (x , (y , p))
  f ((x , y) , inr (r , q)) = inr (x , q)

  g : âŸ¨ (Î± Ã—â‚’ (Î² â†“ b)) +â‚’ (Î± â†“ a) âŸ© â†’ âŸ¨ (Î± Ã—â‚’ Î²) â†“ (a , b) âŸ©
  g (inl (x , y , p)) = (x , y) , inl p
  g (inr (x , q)) = (x , b) , inr (refl , q)

  f-order-preserving : is-order-preserving _ _ f
  f-order-preserving ((x , y) , inl p) ((x' , y') , inl p') (inl l) = inl l
  f-order-preserving ((x , y) , inl p) ((x' , _)  , inl p') (inr (refl , l)) =
   inr ((ap (y ,_) (Prop-valuedness Î² _ _ p p')) , l)
  f-order-preserving ((x , y) , inl p) ((x' , y') , inr (r' , q')) l = â‹†
  f-order-preserving ((x , y) , inr (refl , q)) ((x' , y') , inl p') (inl l) =
   ğŸ˜-elim (irrefl Î² y (Transitivity Î² _ _ _ l p'))
  f-order-preserving ((x , y) , inr (refl , q))
                     ((x' , _)  , inl p') (inr (refl , l)) = ğŸ˜-elim
                                                              (irrefl Î² y p')
  f-order-preserving ((x , y) , inr (refl , q))
                     ((x' , _)  , inr (refl , q')) (inl l) = ğŸ˜-elim
                                                              (irrefl Î² y l)
  f-order-preserving ((x , y) , inr (refl , q))
                     ((x' , _)  , inr (refl , q')) (inr (_ , l)) = l

  f-is-equiv : is-equiv f
  f-is-equiv = qinvs-are-equivs f (g , Î· , Îµ)
   where
    Î· : g âˆ˜ f âˆ¼ id
    Î· ((x , y) , inl p) = refl
    Î· ((x , y) , inr (refl , q)) = refl

    Îµ : f âˆ˜ g âˆ¼ id
    Îµ (inl (x , y)) = refl
    Îµ (inr x) = refl

  g-order-preserving : is-order-preserving _ _ g
  g-order-preserving (inl (x , y , p)) (inl (x' , y' , p')) (inl l) = inl l
  g-order-preserving (inl (x , y , p)) (inl (x' , y' , p')) (inr (refl , l)) =
   inr (refl , l)
  g-order-preserving (inl (x , y , p)) (inr (x' , q')) _ = inl p
  g-order-preserving (inr (x , q))     (inr (x' , q')) l = inr (refl , l)

\end{code}

We now prove several useful facts about (bounded) simulations between products.

\begin{code}

Ã—â‚’-increasing-on-right : (Î± Î² Î³ : Ordinal ğ“¤)
                       â†’ ğŸ˜â‚’ âŠ² Î±
                       â†’ Î² âŠ² Î³
                       â†’ (Î± Ã—â‚’ Î²) âŠ² (Î± Ã—â‚’ Î³)
Ã—â‚’-increasing-on-right Î± Î² Î³ (a , p) (c , q) = (a , c) , I
 where
  I = Î± Ã—â‚’ Î²                    ï¼âŸ¨ ğŸ˜â‚’-right-neutral (Î± Ã—â‚’ Î²) â»Â¹ âŸ©
      (Î± Ã—â‚’ Î²) +â‚’ ğŸ˜â‚’            ï¼âŸ¨ apâ‚‚ (Î» -â‚ -â‚‚ â†’ (Î± Ã—â‚’ -â‚) +â‚’ -â‚‚) q p âŸ©
      (Î± Ã—â‚’ (Î³ â†“ c)) +â‚’ (Î± â†“ a) ï¼âŸ¨ Ã—â‚’-â†“ Î± Î³ â»Â¹ âŸ©
      (Î± Ã—â‚’ Î³) â†“ (a , c)        âˆ

Ã—â‚’-right-monotone-âŠ´ : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (Î³ : Ordinal ğ“¦)
                    â†’ Î² âŠ´ Î³
                    â†’ (Î± Ã—â‚’ Î²) âŠ´ (Î± Ã—â‚’ Î³)
Ã—â‚’-right-monotone-âŠ´ Î± Î² Î³ (g , sim-g) = f , f-initial-segment ,
                                            f-order-preserving
 where
   f : âŸ¨ Î± Ã—â‚’ Î² âŸ© â†’ âŸ¨ Î± Ã—â‚’ Î³ âŸ©
   f (a , b) = a , g b

   f-initial-segment : is-initial-segment (Î± Ã—â‚’ Î²) (Î± Ã—â‚’ Î³) f
   f-initial-segment (a , b) (a' , c') (inl l) = (a' , b') , inl k , ap (a' ,_) q
    where
     I : Î£ b' ê‰ âŸ¨ Î² âŸ© , b' â‰ºâŸ¨ Î² âŸ© b Ã— (g b' ï¼ c')
     I = simulations-are-initial-segments Î² Î³ g sim-g b c' l
     b' = prâ‚ I
     k = prâ‚ (prâ‚‚ I)
     q = prâ‚‚ (prâ‚‚ I)
   f-initial-segment (a , b) (a' , c') (inr (refl , q)) =
    (a' , b) , inr (refl , q) , refl

   f-order-preserving : is-order-preserving (Î± Ã—â‚’ Î²) (Î± Ã—â‚’ Î³) f
   f-order-preserving (a , b) (a' , b') (inl p) =
    inl (simulations-are-order-preserving Î² Î³ g sim-g b b' p)
   f-order-preserving (a , b) (a' , b') (inr (refl , q)) = inr (refl , q)

Ã—â‚’-â‰¼-left : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
            {a a' : âŸ¨ Î± âŸ©} {b : âŸ¨ Î² âŸ©}
          â†’ a â‰¼âŸ¨ Î± âŸ© a'
          â†’ (a , b) â‰¼âŸ¨ Î± Ã—â‚’ Î² âŸ© (a' , b)
Ã—â‚’-â‰¼-left Î± Î² p (aâ‚€ , bâ‚€) (inl r) = inl r
Ã—â‚’-â‰¼-left Î± Î² p (aâ‚€ , bâ‚€) (inr (eq , r)) = inr (eq , p aâ‚€ r)

\end{code}

Multiplication satisfies the expected recursive equations (which
classically define ordinal multiplication): zero is fixed by multiplication
(this is Ã—â‚’-ğŸ˜â‚’-right above), multiplication for successors is repeated addition
and multiplication preserves suprema.

\begin{code}

Ã—â‚’-successor : (Î± Î² : Ordinal ğ“¤) â†’ Î± Ã—â‚’ (Î² +â‚’ ğŸ™â‚’) ï¼ (Î± Ã—â‚’ Î²) +â‚’ Î±
Ã—â‚’-successor Î± Î² =
  Î± Ã—â‚’ (Î² +â‚’ ğŸ™â‚’)          ï¼âŸ¨ Ã—â‚’-distributes-+â‚’-right Î± Î² ğŸ™â‚’ âŸ©
  ((Î± Ã—â‚’ Î²) +â‚’ (Î± Ã—â‚’ ğŸ™â‚’)) ï¼âŸ¨ ap ((Î± Ã—â‚’ Î²) +â‚’_) (ğŸ™â‚’-right-neutral-Ã—â‚’ Î±)  âŸ©
  (Î± Ã—â‚’ Î²) +â‚’ Î±           âˆ

open import UF.PropTrunc
open import UF.Size

module _ (pt : propositional-truncations-exist)
         (sr : Set-Replacement pt)
       where

 open import Ordinals.OrdinalOfOrdinalsSuprema ua
 open suprema pt sr
 open PropositionalTruncation pt

 Ã—â‚’-preserves-suprema : (Î± : Ordinal ğ“¤) {I : ğ“¤ Ì‡ } (Î² : I â†’ Ordinal ğ“¤)
                      â†’ Î± Ã—â‚’ sup Î² ï¼ sup (Î» i â†’ Î± Ã—â‚’ Î² i)
 Ã—â‚’-preserves-suprema {ğ“¤} Î± {I} Î² = âŠ´-antisym (Î± Ã—â‚’ sup Î²) (sup (Î» i â†’ Î± Ã—â‚’ Î² i)) â¦…1â¦† â¦…2â¦†
  where
   â¦…2â¦† : sup (Î» i â†’ Î± Ã—â‚’ Î² i) âŠ´ (Î± Ã—â‚’ sup Î²)
   â¦…2â¦† = sup-is-lower-bound-of-upper-bounds (Î» i â†’ Î± Ã—â‚’ Î² i) (Î± Ã—â‚’ sup Î²)
          (Î» i â†’ Ã—â‚’-right-monotone-âŠ´ Î± (Î² i) (sup Î²) (sup-is-upper-bound Î² i))

   â¦…1â¦† : (Î± Ã—â‚’ sup Î²) âŠ´ sup (Î» i â†’ Î± Ã—â‚’ Î² i)
   â¦…1â¦† = â‰¼-gives-âŠ´ (Î± Ã—â‚’ sup Î²) (sup (Î» i â†’ Î± Ã—â‚’ Î² i)) â¦…1â¦†-I
    where
     â¦…1â¦†-I : (Î³ : Ordinal ğ“¤) â†’ Î³ âŠ² (Î± Ã—â‚’ sup Î²) â†’ Î³ âŠ² sup (Î» i â†’ Î± Ã—â‚’ Î² i)
     â¦…1â¦†-I _ ((a , y) , refl) = â¦…1â¦†-III
      where
       â¦…1â¦†-II : (Î£ i ê‰ I , Î£ b ê‰ âŸ¨ Î² i âŸ© , sup Î² â†“ y ï¼ (Î² i) â†“ b)
              â†’ ((Î± Ã—â‚’ sup Î²) â†“ (a , y)) âŠ² sup (Î» j â†’ Î± Ã—â‚’ Î² j)
       â¦…1â¦†-II (i , b , e) = Ïƒ (a , b) , eq
        where
         Ïƒ : âŸ¨ Î± Ã—â‚’ Î² i âŸ© â†’ âŸ¨ sup (Î» j â†’ Î± Ã—â‚’ Î² j) âŸ©
         Ïƒ = [ Î± Ã—â‚’ Î² i , sup (Î» j â†’ Î± Ã—â‚’ Î² j) ]âŸ¨ sup-is-upper-bound _ i âŸ©

         eq = (Î± Ã—â‚’ sup Î²) â†“ (a , y)           ï¼âŸ¨ Ã—â‚’-â†“ Î± (sup Î²) âŸ©
              (Î± Ã—â‚’ (sup Î² â†“ y)) +â‚’ (Î± â†“ a)    ï¼âŸ¨ eqâ‚ âŸ©
              (Î± Ã—â‚’ (Î² i â†“ b)) +â‚’ (Î± â†“ a)      ï¼âŸ¨ Ã—â‚’-â†“ Î± (Î² i) â»Â¹ âŸ©
              (Î± Ã—â‚’ Î² i) â†“ (a , b)             ï¼âŸ¨ eqâ‚‚ âŸ©
              sup (Î» j â†’ Î± Ã—â‚’ Î² j) â†“ Ïƒ (a , b) âˆ
          where
           eqâ‚ = ap (Î» - â†’ ((Î± Ã—â‚’ -) +â‚’ (Î± â†“ a))) e
           eqâ‚‚ = (initial-segment-of-sup-at-component
                  (Î» j â†’ Î± Ã—â‚’ Î² j) i (a , b)) â»Â¹

       â¦…1â¦†-III : ((Î± Ã—â‚’ sup Î²) â†“ (a , y)) âŠ² sup (Î» i â†’ Î± Ã—â‚’ Î² i)
       â¦…1â¦†-III = âˆ¥âˆ¥-rec (âŠ²-is-prop-valued _ _) â¦…1â¦†-II
                  (initial-segment-of-sup-is-initial-segment-of-some-component
                    Î² y)

\end{code}

11 September 2024, added by Tom de Jong following a question by Martin Escardo.

The equations for successor and suprema uniquely specify the multiplication
operation even though they are not constructively sufficient to define it.

\begin{code}

 private
  successor-equation : (Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤) â†’ ğ“¤ âº Ì‡
  successor-equation {ğ“¤} _âŠ—_ =
   (Î± Î² : Ordinal ğ“¤) â†’ Î± âŠ— (Î² +â‚’ ğŸ™â‚’) ï¼ (Î± âŠ— Î²) +â‚’ Î±

  suprema-equation : (Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤) â†’ ğ“¤ âº Ì‡
  suprema-equation {ğ“¤} _âŠ—_ =
   (Î± : Ordinal ğ“¤) (I : ğ“¤ Ì‡ ) (Î² : I â†’ Ordinal ğ“¤)
    â†’ Î± âŠ— (sup Î²) ï¼ sup (Î» i â†’ Î± âŠ— Î² i)

  recursive-equation : (Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤) â†’ ğ“¤ âº Ì‡
  recursive-equation {ğ“¤} _âŠ—_ =
   (Î± Î² : Ordinal ğ“¤) â†’ Î± âŠ— Î² ï¼ sup (Î» b â†’ (Î± âŠ— (Î² â†“ b)) +â‚’ Î±)

  successor-and-suprema-equations-give-recursive-equation
   : (_âŠ—_ : Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤)
   â†’ successor-equation _âŠ—_
   â†’ suprema-equation _âŠ—_
   â†’ recursive-equation _âŠ—_
  successor-and-suprema-equations-give-recursive-equation
   _âŠ—_ âŠ—-succ âŠ—-sup Î± Î² = Î± âŠ— Î²                           ï¼âŸ¨ I   âŸ©
                          (Î± âŠ— sup (Î» b â†’ (Î² â†“ b) +â‚’ ğŸ™â‚’)) ï¼âŸ¨ II  âŸ©
                          sup (Î» b â†’ Î± âŠ— ((Î² â†“ b) +â‚’ ğŸ™â‚’)) ï¼âŸ¨ III âŸ©
                          sup (Î» b â†’ (Î± âŠ— (Î² â†“ b)) +â‚’ Î±)  âˆ
    where
     I   = ap (Î± âŠ—_) (supremum-of-successors-of-initial-segments pt sr Î²)
     II  = âŠ—-sup Î± âŸ¨ Î² âŸ© (Î» b â†’ (Î² â†“ b) +â‚’ ğŸ™â‚’)
     III = ap sup (dfunext fe' (Î» b â†’ âŠ—-succ Î± (Î² â†“ b)))

 Ã—â‚’-recursive-equation : recursive-equation {ğ“¤} _Ã—â‚’_
 Ã—â‚’-recursive-equation =
  successor-and-suprema-equations-give-recursive-equation
    _Ã—â‚’_ Ã—â‚’-successor (Î» Î± _ Î² â†’ Ã—â‚’-preserves-suprema Î± Î²)

 Ã—â‚’-is-uniquely-specified'
  : (_âŠ—_ : Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤)
  â†’ recursive-equation _âŠ—_
  â†’ (Î± Î² : Ordinal ğ“¤) â†’ Î± âŠ— Î² ï¼ Î± Ã—â‚’ Î²
 Ã—â‚’-is-uniquely-specified' {ğ“¤} _âŠ—_ âŠ—-rec Î± =
  transfinite-induction-on-OO (Î» - â†’ (Î± âŠ— -) ï¼ (Î± Ã—â‚’ -)) I
   where
    I : (Î² : Ordinal ğ“¤)
      â†’ ((b : âŸ¨ Î² âŸ©) â†’ (Î± âŠ— (Î² â†“ b)) ï¼ (Î± Ã—â‚’ (Î² â†“ b)))
      â†’ (Î± âŠ— Î²) ï¼ (Î± Ã—â‚’ Î²)
    I Î² IH = Î± âŠ— Î²                            ï¼âŸ¨ II  âŸ©
             sup (Î» b â†’ (Î± âŠ— (Î² â†“ b)) +â‚’ Î±)   ï¼âŸ¨ III âŸ©
             sup (Î» b â†’ (Î± Ã—â‚’ (Î² â†“ b)) +â‚’ Î±)  ï¼âŸ¨ IV  âŸ©
             Î± Ã—â‚’ Î²                           âˆ
     where
      II  = âŠ—-rec Î± Î²
      III = ap sup (dfunext fe' (Î» b â†’ ap (_+â‚’ Î±) (IH b)))
      IV  = Ã—â‚’-recursive-equation Î± Î² â»Â¹

 Ã—â‚’-is-uniquely-specified
  : âˆƒ! _âŠ—_ ê‰ (Ordinal ğ“¤ â†’ Ordinal ğ“¤ â†’ Ordinal ğ“¤) ,
     (successor-equation _âŠ—_) Ã— (suprema-equation _âŠ—_)
 Ã—â‚’-is-uniquely-specified {ğ“¤} =
  (_Ã—â‚’_ , (Ã—â‚’-successor , (Î» Î± _ Î² â†’ Ã—â‚’-preserves-suprema Î± Î²))) ,
  (Î» (_âŠ—_ , âŠ—-succ , âŠ—-sup) â†’
   to-subtype-ï¼
    (Î» F â†’ Ã—-is-prop (Î â‚‚-is-prop fe'
                       (Î» _ _ â†’ underlying-type-is-set fe (OO ğ“¤)))
                     (Î â‚ƒ-is-prop fe'
                       (Î» _ _ _ â†’ underlying-type-is-set fe (OO ğ“¤))))
    (dfunext fe'
      (Î» Î± â†’ dfunext fe'
       (Î» Î² â†’
        (Ã—â‚’-is-uniquely-specified' _âŠ—_
          (successor-and-suprema-equations-give-recursive-equation
            _âŠ—_ âŠ—-succ âŠ—-sup)
        Î± Î²) â»Â¹))))

\end{code}

The above should be contrasted to the situation for addition where we do not
know how to prove such a result since only *inhabited* suprema are preserved by
addition.
However, a classically equivalent, but constructively stronger, reformulation of
the equations for addition does uniquely specify, and lead to a definition of
ordinal addition, cf. the comment in Ordinals.AdditionProperties.


Added 17 September 2024 by Fredrik Nordvall Forsberg:

Multiplication being monotone in the left argument is a constructive taboo.

Addition 22 November 2024: monotonicity in the left argument is
equivalent to Excluded Middle.

\begin{code}

Ã—â‚’-minimal : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (aâ‚€ : âŸ¨ Î± âŸ©) (bâ‚€ : âŸ¨ Î² âŸ©)
           â†’ is-least Î± aâ‚€ â†’ is-least Î² bâ‚€ â†’ is-minimal (Î± Ã—â‚’ Î²) (aâ‚€ , bâ‚€)
Ã—â‚’-minimal Î± Î² aâ‚€ bâ‚€ aâ‚€-least bâ‚€-least (a , b) (inl l)
 = irrefl Î² b (bâ‚€-least b b l)
Ã—â‚’-minimal Î± Î² aâ‚€ bâ‚€ aâ‚€-least bâ‚€-least (a , b) (inr (refl , l))
 = irrefl Î± a (aâ‚€-least a a l)

Ã—â‚’-least : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (aâ‚€ : âŸ¨ Î± âŸ©) (bâ‚€ : âŸ¨ Î² âŸ©)
         â†’ is-least Î± aâ‚€ â†’ is-least Î² bâ‚€ â†’ is-least (Î± Ã—â‚’ Î²) (aâ‚€ , bâ‚€)
Ã—â‚’-least Î± Î²  aâ‚€ bâ‚€ aâ‚€-least bâ‚€-least =
 minimal-is-least (Î± Ã—â‚’ Î²) (aâ‚€ , bâ‚€) (Ã—â‚’-minimal Î± Î² aâ‚€ bâ‚€ aâ‚€-least bâ‚€-least)

Ã—â‚’-left-monotonicity-implies-EM
  : ((Î± Î² : Ordinal ğ“¤) (Î³ : Ordinal ğ“¥) â†’ Î± âŠ´ Î² â†’ Î± Ã—â‚’ Î³ âŠ´ Î² Ã—â‚’ Î³)
  â†’ EM ğ“¤
Ã—â‚’-left-monotonicity-implies-EM hyp P isprop-P = III (f (â‹† , inr â‹†)) refl
 where
  Î± = ğŸ™â‚’
  Pâ‚’ = prop-ordinal P isprop-P
  Î² = ğŸ™â‚’ +â‚’ Pâ‚’
  Î³ = ğŸšâ‚’

  I : Î± âŠ´ Î²
  I = â‰¼-gives-âŠ´ Î± Î²
       (transport
         (_â‰¼ Î²)
         (ğŸ˜â‚’-right-neutral ğŸ™â‚’)
         (+â‚’-right-monotone ğŸ™â‚’ ğŸ˜â‚’ Pâ‚’
         (ğŸ˜â‚’-least Pâ‚’)))

  II : (Î± Ã—â‚’ Î³) âŠ´ (Î² Ã—â‚’ Î³)
  II = hyp Î± Î² Î³ I

  f = prâ‚ II
  f-sim = prâ‚‚ II

  f-preserves-least : f (â‹† , inl â‹†) ï¼ (inl â‹† , inl â‹†)
  f-preserves-least = simulations-preserve-least (Î± Ã—â‚’ Î³) (Î² Ã—â‚’ Î³)
                        (â‹† , inl â‹†)
                        (inl â‹† , inl â‹†)
                        f f-sim
                        (Ã—â‚’-least Î± Î³ â‹† (inl â‹†)
                          â‹†-least
                          (left-preserves-least ğŸ™â‚’ ğŸ™â‚’ â‹† â‹†-least))
                        (Ã—â‚’-least Î² Î³ (inl â‹†) (inl â‹†)
                         (left-preserves-least ğŸ™â‚’ Pâ‚’ â‹† â‹†-least)
                         (left-preserves-least ğŸ™â‚’ ğŸ™â‚’ â‹† â‹†-least))
   where
    â‹†-least : is-least (ğŸ™â‚’ {ğ“¤}) â‹†
    â‹†-least â‹† â‹† = ğŸ˜-elim

  III : (x : âŸ¨ Î² Ã—â‚’ Î³ âŸ©) â†’ f (â‹† , inr â‹†) ï¼ x â†’ P + Â¬ P
  III (inl â‹† , inl â‹†) r = ğŸ˜-elim (+disjoint' IIIâ‚‚)
   where
    IIIâ‚ = f (â‹† , inr â‹†)   ï¼âŸ¨ r âŸ©
           (inl â‹† , inl â‹†) ï¼âŸ¨ f-preserves-least â»Â¹ âŸ©
           f (â‹† , inl â‹†)   âˆ
    IIIâ‚‚ : inr â‹† ï¼ inl â‹†
    IIIâ‚‚ = ap prâ‚‚ (simulations-are-lc _ _ f f-sim IIIâ‚)
  III (inl â‹† , inr â‹†) r = inr (Î» p â†’ ğŸ˜-elim (+disjoint (IIIâ‚† p)))
   where
    IIIâ‚ƒ : (p : P)
         â†’ Î£ x ê‰ âŸ¨ ğŸ™â‚’ Ã—â‚’ ğŸšâ‚’ âŸ© ,
             (x â‰ºâŸ¨ ğŸ™â‚’ Ã—â‚’ ğŸšâ‚’ âŸ© (â‹† , inr â‹†)) Ã— (f x ï¼ (inr p , inl â‹†))
    IIIâ‚ƒ p = simulations-are-initial-segments (Î± Ã—â‚’ Î³) (Î² Ã—â‚’ Î³) f f-sim
               (â‹† , inr â‹†) (inr p , inl â‹†)
               (transportâ»Â¹ (Î» - â†’ (inr p , inl â‹†) â‰ºâŸ¨ Î² Ã—â‚’ Î³ âŸ© - ) r (inl â‹†))
    IIIâ‚„ : (p : P)
         â†’ Î£ x ê‰ âŸ¨ ğŸ™â‚’ Ã—â‚’ ğŸšâ‚’ âŸ© ,
             (x â‰ºâŸ¨ ğŸ™â‚’ Ã—â‚’ ğŸšâ‚’ âŸ© (â‹† , inr â‹†)) Ã— (f x ï¼ (inr p , inl â‹†))
         â†’ f (â‹† , inl â‹†) ï¼ (inr p , inl â‹†)
    IIIâ‚„ p ((â‹† , inl â‹†) , l , q) = q
    IIIâ‚„ p ((â‹† , inr â‹†) , l , q) = ğŸ˜-elim (irrefl (ğŸ™â‚’ Ã—â‚’ ğŸšâ‚’) (â‹† , inr â‹†) l)

    IIIâ‚… : (p : P) â†’ (inl â‹† , inl â‹†) ï¼ (inr p , inl â‹†)
    IIIâ‚… p = (inl â‹† , inl â‹†) ï¼âŸ¨ f-preserves-least â»Â¹ âŸ©
             f (â‹† , inl â‹†)   ï¼âŸ¨ IIIâ‚„ p (IIIâ‚ƒ p) âŸ©
             (inr p , inl â‹†) âˆ

    IIIâ‚† : (p : P) â†’ inl â‹† ï¼ inr p
    IIIâ‚† p = ap prâ‚ (IIIâ‚… p)

  III (inr p , c) r = inl p

EM-implies-Ã—â‚’-left-monotonicity
 : EM (ğ“¤ âŠ” ğ“¥)
 â†’ (Î± Î² : Ordinal ğ“¤) (Î³ : Ordinal ğ“¥) â†’ Î± âŠ´ Î² â†’ (Î± Ã—â‚’ Î³) âŠ´ (Î² Ã—â‚’ Î³)
EM-implies-Ã—â‚’-left-monotonicity em Î± Î² Î³ (g , g-sim)
 = â‰¼-gives-âŠ´ (Î± Ã—â‚’ Î³) (Î² Ã—â‚’ Î³)
             (EM-implies-order-preserving-gives-â‰¼ em (Î± Ã—â‚’ Î³)
                                                     (Î² Ã—â‚’ Î³)
                                                     (f , f-order-preserving))
  where
   f : âŸ¨  Î± Ã—â‚’ Î³ âŸ© â†’ âŸ¨ Î² Ã—â‚’ Î³ âŸ©
   f (a , c) = (g a , c)

   f-order-preserving : is-order-preserving (Î± Ã—â‚’ Î³) (Î² Ã—â‚’ Î³) f
   f-order-preserving (a , c) (a' , c') (inl l) = inl l
   f-order-preserving (a , c) (a' , c) (inr (refl , l))
    = inr (refl , simulations-are-order-preserving Î± Î² g g-sim a a' l)

EM-implies-induced-âŠ´-on-Ã—â‚’ : EM ğ“¤
                           â†’ (Î± Î² Î³ Î´ : Ordinal ğ“¤)
                           â†’ Î± âŠ´ Î³ â†’ Î² âŠ´ Î´ â†’ (Î± Ã—â‚’ Î²) âŠ´ (Î³ Ã—â‚’ Î´)
EM-implies-induced-âŠ´-on-Ã—â‚’ em Î± Î² Î³ Î´ ğ•— ğ•˜ =
 âŠ´-trans (Î± Ã—â‚’ Î²) (Î± Ã—â‚’ Î´) (Î³ Ã—â‚’ Î´)
         (Ã—â‚’-right-monotone-âŠ´ Î± Î² Î´ ğ•˜)
         (EM-implies-Ã—â‚’-left-monotonicity em Î± Î³ Î´ ğ•—)

\end{code}

To prove that multiplication is left cancellable, we require the following
technical lemma: if Î± > ğŸ˜, then every simulation from Î± Ã—â‚’ Î² to Î± Ã—â‚’ Î³ + Î± â†“ aâ‚
firstly never hits the second summand, and secondly, in the first component, it
decomposes as the identity on the first component and a function Î² â†’ Î³ on the
second component, viz. one that is independent of the first component. The
inspiration for this lemma is the lemma simulation-product-decomposition below,
which only deals with simulations f : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ (ie, with Î± â†“ aâ‚ = ğŸ˜â‚’ in
the context of the current lemma). However the more general statement seems to
be necessary for proving left cancellability with respect to âŠ´, rather than
just with respect to ï¼.

\begin{code}

simulation-product-decomposition-generalised
 : (Î± : Ordinal ğ“¤)
 â†’ ğŸ˜â‚’ âŠ² Î±
 â†’ (Î² Î³ : Ordinal ğ“¤)
 â†’ (aâ‚ : âŸ¨ Î± âŸ©)
 â†’ ((f , _) : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
 â†’ Î£ g ê‰ (âŸ¨ Î² âŸ© â†’ âŸ¨ Î³ âŸ©) , ((a : âŸ¨ Î± âŸ©) (b : âŸ¨ Î² âŸ©) â†’ f (a , b) ï¼ inl (a , g b))
simulation-product-decomposition-generalised {ğ“¤} Î± (aâ‚€ , aâ‚€-least) = II
 where
  P : Ordinal ğ“¤ â†’ ğ“¤ âº Ì‡
  P Î² =   (Î³ : Ordinal ğ“¤) (aâ‚ : âŸ¨ Î± âŸ©)
          ((f , _) : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
        â†’ (b : âŸ¨ Î² âŸ©) â†’ Î£ c ê‰ âŸ¨ Î³ âŸ© , ((a : âŸ¨ Î± âŸ©) â†’ f (a , b) ï¼ inl (a , c))

  Pâ‚€ : Ordinal ğ“¤ â†’ ğ“¤ âº Ì‡
  Pâ‚€ Î² =   (aâ‚ : âŸ¨ Î± âŸ©) (Î³ : Ordinal ğ“¤)
           ((f , _) : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
           (b : âŸ¨ Î² âŸ©)
           (x : âŸ¨ (Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚) âŸ©)
         â†’ f (aâ‚€ , b) ï¼ x
         â†’ Î£ c ê‰ âŸ¨ Î³ âŸ© , f (aâ‚€ , b) ï¼ inl (aâ‚€ , c)

  I : (Î² Î³ : Ordinal ğ“¤) (aâ‚ : âŸ¨ Î± âŸ©)
      ((f , _) : Î± Ã—â‚’ Î² âŠ´ (Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚))
      (b : âŸ¨ Î² âŸ©)
    â†’ Î± Ã—â‚’ (Î² â†“ b) ï¼ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (aâ‚€ , b)
  I Î² Î³ aâ‚ ğ•—@(f , f-sim) b =
   Î± Ã—â‚’ (Î² â†“ b)                      ï¼âŸ¨ Iâ‚ âŸ©
   Î± Ã—â‚’ (Î² â†“ b) +â‚’ (Î± â†“ aâ‚€)          ï¼âŸ¨ Ã—â‚’-â†“ Î± Î² â»Â¹ âŸ©
   Î± Ã—â‚’ Î² â†“ (aâ‚€ , b)                 ï¼âŸ¨ Iâ‚‚ âŸ©
   Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (aâ‚€ , b)   âˆ
    where
     Iâ‚ = ğŸ˜â‚’-right-neutral
             (Î± Ã—â‚’ (Î² â†“ b)) â»Â¹ âˆ™ ap (Î± Ã—â‚’ (Î² â†“ b) +â‚’_)
             aâ‚€-least
     Iâ‚‚ = simulations-preserve-â†“ _ _ ğ•— (aâ‚€ , b)

  ğ•˜â‚ : (Î² : Ordinal ğ“¤)
     â†’ ((b : âŸ¨ Î² âŸ©) â†’ P (Î² â†“ b))
     â†’ Pâ‚€ Î²
  ğ•˜â‚ Î² IH aâ‚ Î³ ğ•—@(f , _) b (inl (a' , c)) e = c , III
    where
     eq = Î± Ã—â‚’ (Î² â†“ b)                      ï¼âŸ¨ I Î² Î³ aâ‚ ğ•— b âŸ©
          Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (aâ‚€ , b)   ï¼âŸ¨ ap ((Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚)) â†“_) e âŸ©
          Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ inl (a' , c) ï¼âŸ¨ +â‚’-â†“-left (a' , c) â»Â¹ âŸ©
          Î± Ã—â‚’ Î³ â†“ (a' , c)                 ï¼âŸ¨ Ã—â‚’-â†“ Î± Î³ âŸ©
          Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ a')          âˆ

     ğ•—' :  Î± Ã—â‚’ (Î² â†“ b) âŠ´ Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ a')
     f' = Idtofunâ‚’ eq
     ğ•—' = f' , Idtofunâ‚’-is-simulation eq

     ğ•—'â»Â¹ : Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ a') âŠ´ Î± Ã—â‚’ (Î² â†“ b)
     f'â»Â¹ = Idtofunâ‚’ (eq â»Â¹)
     ğ•—'â»Â¹ = f'â»Â¹ , Idtofunâ‚’-is-simulation (eq â»Â¹)

     II : a' ï¼ aâ‚€
     II = Extensionality Î± a' aâ‚€
           (Î» x l â†’ ğŸ˜-elim (IIâ‚ x l))
           (Î» x l â†’ ğŸ˜-elim (transportâ»Â¹ âŸ¨_âŸ© aâ‚€-least (x , l)))
      where
       IIâ‚ : (x : âŸ¨ Î± âŸ©) â†’ Â¬ (x â‰ºâŸ¨ Î± âŸ© a')
       IIâ‚ x l = +disjoint IIâ‚‚
        where
         y : âŸ¨ Î± Ã—â‚’ (Î² â†“ b) âŸ©
         y = f'â»Â¹ (inr (x , l))
         yâ‚ = prâ‚ y
         yâ‚‚ = prâ‚‚ y

         z : âŸ¨ Î³ â†“ c âŸ©
         z = prâ‚ (IH b (Î³ â†“ c) a' ğ•—' yâ‚‚)

         IIâ‚‚ = inl (yâ‚ , z)            ï¼âŸ¨ prâ‚‚ (IH b (Î³ â†“ c) a' ğ•—' yâ‚‚) yâ‚ â»Â¹ âŸ©
               f' (f'â»Â¹ (inr (x , l))) ï¼âŸ¨ Idtofunâ‚’-retraction eq (inr (x , l)) âŸ©
               inr (x , l)             âˆ

     III = f (aâ‚€ , b)   ï¼âŸ¨ e âŸ©
           inl (a' , c) ï¼âŸ¨ ap (Î» - â†’ inl (- , c)) II âŸ©
           inl (aâ‚€ , c) âˆ

  ğ•˜â‚ Î² _ aâ‚ Î³ ğ•—@(f , f-sim) b (inr (x , p)) e = ğŸ˜-elim (Î½ (aâ‚ , refl))
   where
    eq-I = Î± Ã—â‚’ (Î² â†“ b)                     ï¼âŸ¨ I Î² Î³ aâ‚ ğ•— b âŸ©
           Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (aâ‚€ , b)  ï¼âŸ¨ ap ((Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚)) â†“_) e âŸ©
           Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ inr (x , p) ï¼âŸ¨ +â‚’-â†“-right (x , p) â»Â¹ âŸ©
           Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚ â†“ (x , p))     ï¼âŸ¨ eq-Iâ‚ âŸ©
           Î± Ã—â‚’ Î³  +â‚’ (Î± â†“ x)               âˆ
     where
      eq-Iâ‚ = ap ((Î± Ã—â‚’ Î³) +â‚’_) (iterated-â†“  Î± aâ‚ x p)

    eq-II = Î± Ã—â‚’ Î³ +â‚’ ((Î± â†“ x) +â‚’ Î±) ï¼âŸ¨ +â‚’-assoc (Î± Ã—â‚’ Î³) (Î± â†“ x) Î± â»Â¹ âŸ©
            Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ x) +â‚’ Î±   ï¼âŸ¨ ap (_+â‚’ Î±) eq-I â»Â¹ âŸ©
            Î± Ã—â‚’ (Î² â†“ b) +â‚’ Î±        ï¼âŸ¨ Ã—â‚’-successor Î± (Î² â†“ b) â»Â¹ âŸ©
            Î± Ã—â‚’ ((Î² â†“ b) +â‚’ ğŸ™â‚’)      âˆ

    ineq-I : Î± Ã—â‚’ Î³ +â‚’ ((Î± â†“ x) +â‚’ Î±) âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚)
    ineq-I = transportâ»Â¹
              (Î» - â†’ - âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
              eq-II
              (âŠ´-trans
               (Î± Ã—â‚’ ((Î² â†“ b) +â‚’ ğŸ™â‚’))
               (Î± Ã—â‚’ Î²)
               (Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
               (Ã—â‚’-right-monotone-âŠ´ Î± ((Î² â†“ b) +â‚’ ğŸ™â‚’) Î²
                 (upper-bound-of-successors-of-initial-segments Î² b))
               ğ•—)

    ineq-II : (Î± â†“ x) +â‚’ Î± âŠ´ Î± â†“ aâ‚
    ineq-II = +â‚’-left-reflects-âŠ´ (Î± Ã—â‚’ Î³) ((Î± â†“ x) +â‚’ Î±) (Î± â†“ aâ‚) ineq-I

    h : âŸ¨ Î± âŸ© â†’ âŸ¨ Î± â†“ aâ‚ âŸ©
    h a = [ (Î± â†“ x) +â‚’ Î± ,  Î± â†“ aâ‚ ]âŸ¨ ineq-II âŸ© (inr a)

    h-order-preserving : is-order-preserving Î± (Î± â†“ aâ‚) h
    h-order-preserving a a' l =
     simulations-are-order-preserving
      ((Î± â†“ x) +â‚’ Î±)
      (Î± â†“ aâ‚)
      [ (Î± â†“ x) +â‚’ Î± ,  Î± â†“ aâ‚ ]âŸ¨ ineq-II âŸ©
      ([ (Î± â†“ x) +â‚’ Î± ,  Î± â†“ aâ‚ ]âŸ¨ ineq-II âŸ©-is-simulation)
      (inr a) (inr a') l

    Î½ : Â¬ (Î± â†“ aâ‚ âŠ² Î±)
    Î½ = order-preserving-gives-not-âŠ² Î± (Î± â†“ aâ‚)
         (h , h-order-preserving)

  ğ•˜â‚‚ : (Î² : Ordinal ğ“¤)
     â†’ ((b : âŸ¨ Î² âŸ©) â†’ P (Î² â†“ b))
     â†’ P Î²
  ğ•˜â‚‚ Î² IH Î³ aâ‚ ğ•—@(f , f-sim) b = c , c-satisfies-equation
   where
    c : âŸ¨ Î³ âŸ©
    c = prâ‚ (ğ•˜â‚ Î² IH aâ‚ Î³ ğ•— b (f (aâ‚€ , b)) refl)

    c-spec : f (aâ‚€ , b) ï¼ inl (aâ‚€ , c)
    c-spec = prâ‚‚ (ğ•˜â‚ Î² IH aâ‚ Î³ ğ•— b (f (aâ‚€ , b)) refl)

    c-satisfies-equation : (a : âŸ¨ Î± âŸ©) â†’ f (a , b) ï¼ inl (a , c)
    c-satisfies-equation a = â†“-lc (Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚)) _ _ eq-II
     where
      eq-I = Î± Ã—â‚’ (Î² â†“ b)                      ï¼âŸ¨ I Î² Î³ aâ‚ ğ•— b âŸ©
             Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (aâ‚€ , b)   ï¼âŸ¨ eq-Iâ‚ âŸ©
             Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ inl (aâ‚€ , c) ï¼âŸ¨ +â‚’-â†“-left (aâ‚€ , c) â»Â¹ âŸ©
             Î± Ã—â‚’ Î³ â†“ (aâ‚€ , c)                 ï¼âŸ¨ Ã—â‚’-â†“ Î± Î³ âŸ©
             Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ aâ‚€)          ï¼âŸ¨ eq-Iâ‚‚ âŸ©
             Î± Ã—â‚’ (Î³ â†“ c)                      âˆ
       where
        eq-Iâ‚ = ap (((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)) â†“_) c-spec
        eq-Iâ‚‚ = ap ((Î± Ã—â‚’ (Î³ â†“ c)) +â‚’_) aâ‚€-least â»Â¹
                âˆ™ ğŸ˜â‚’-right-neutral (Î± Ã—â‚’ (Î³ â†“ c))

      eq-II = Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ f (a , b)   ï¼âŸ¨ eq-IIâ‚ âŸ©
              Î± Ã—â‚’ Î² â†“ (a , b)                 ï¼âŸ¨ Ã—â‚’-â†“ Î± Î² âŸ©
              Î± Ã—â‚’ (Î² â†“ b) +â‚’ (Î± â†“ a)          ï¼âŸ¨ ap (_+â‚’ (Î± â†“ a)) eq-I âŸ©
              Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ a)          ï¼âŸ¨ Ã—â‚’-â†“ Î± Î³ â»Â¹ âŸ©
              Î± Ã—â‚’ Î³ â†“ (a , c)                 ï¼âŸ¨ +â‚’-â†“-left (a , c) âŸ©
              Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚) â†“ inl (a , c) âˆ
       where
        eq-IIâ‚ = (simulations-preserve-â†“ _ _ ğ•— (a , b)) â»Â¹

  ğ•˜ : (Î² : Ordinal ğ“¤) â†’ P Î²
  ğ•˜ = transfinite-induction-on-OO P ğ•˜â‚‚

  II : (Î² Î³ : Ordinal ğ“¤)
     â†’ (aâ‚ : âŸ¨ Î± âŸ©)
     â†’ ((f , _) : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ aâ‚))
     â†’ Î£ g ê‰ (âŸ¨ Î² âŸ© â†’ âŸ¨ Î³ âŸ©)
           , ((a : âŸ¨ Î± âŸ©) (b : âŸ¨ Î² âŸ©) â†’ f (a , b) ï¼ inl (a , g b))
  II Î² Î³ aâ‚ ğ•— = (Î»   b â†’ prâ‚ (ğ•˜ Î² Î³ aâ‚ ğ•— b)) ,
                (Î» a b â†’ prâ‚‚ (ğ•˜ Î² Î³ aâ‚ ğ•— b) a)

Ã—â‚’-left-cancellable-âŠ´-generalised
 : (Î± Î² Î³ : Ordinal ğ“¤) (aâ‚ : âŸ¨ Î± âŸ©)
 â†’ ğŸ˜â‚’ âŠ² Î±
 â†’ Î± Ã—â‚’ Î² âŠ´ (Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)
 â†’ Î² âŠ´ Î³
Ã—â‚’-left-cancellable-âŠ´-generalised Î± Î² Î³ aâ‚ p@(aâ‚€ , aâ‚€-least) ğ•— =
 (g , g-is-initial-segment , g-is-order-preserving)
 where
  f = [ Î± Ã—â‚’ Î² , (Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚) ]âŸ¨ ğ•— âŸ©
  f-sim = [ Î± Ã—â‚’ Î² , (Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚) ]âŸ¨ ğ•— âŸ©-is-simulation

  g : âŸ¨ Î² âŸ© â†’ âŸ¨ Î³ âŸ©
  g = prâ‚ (simulation-product-decomposition-generalised Î± p Î² Î³ aâ‚ ğ•—)

  g-property :  (a : âŸ¨ Î± âŸ©)(b : âŸ¨ Î² âŸ©) â†’ f (a , b) ï¼ inl (a , g b)
  g-property = prâ‚‚ (simulation-product-decomposition-generalised Î± p Î² Î³ aâ‚ ğ•—)

  g-is-initial-segment : is-initial-segment Î² Î³ g
  g-is-initial-segment b c l = b' , k' k , e'
   where
    l' : inl (aâ‚€ , c) â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)) âŸ© inl (aâ‚€ , g b)
    l' = inl l

    l'' : inl (aâ‚€ , c) â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)) âŸ© f (aâ‚€ , b)
    l'' = transportâ»Â¹ (Î» - â†’ inl (aâ‚€ , c) â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚))âŸ© -)
                      (g-property aâ‚€ b)
                      l'

    x : Î£ y ê‰ âŸ¨ Î± Ã—â‚’ Î² âŸ© , (y â‰ºâŸ¨ Î± Ã—â‚’ Î² âŸ© (aâ‚€ , b)) Ã— (f y ï¼ (inl (aâ‚€ , c)))
    x = simulations-are-initial-segments _ _ f f-sim (aâ‚€ , b) (inl (aâ‚€ , c)) l''
    a' = prâ‚ (prâ‚ x)
    b' = prâ‚‚ (prâ‚ x)
    k = prâ‚ (prâ‚‚ x)
    e = prâ‚‚ (prâ‚‚ x)

    k' : (a' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î² âŸ© (aâ‚€ , b) â†’ b' â‰ºâŸ¨ Î² âŸ© b
    k' (inl p) = p
    k' (inr (r , q)) = ğŸ˜-elim (transportâ»Â¹ âŸ¨_âŸ© aâ‚€-least (a' , q))

    e' : g b' ï¼ c
    e' = ap prâ‚‚ (inl-lc (g-property a' b' â»Â¹ âˆ™ e))

  g-is-order-preserving : is-order-preserving Î² Î³ g
  g-is-order-preserving b b' l = III II
   where
    I : f (aâ‚€ , b) â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)) âŸ© f (aâ‚€ , b')
    I = simulations-are-order-preserving _ _ f f-sim (aâ‚€ , b) (aâ‚€ , b') (inl l)

    II : inl (aâ‚€ , g b) â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚)) âŸ© inl (aâ‚€ , g b')
    II = transportâ‚‚ (Î» x y â†’ x â‰ºâŸ¨ ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ aâ‚))âŸ© y)
                     (g-property aâ‚€ b)
                     (g-property aâ‚€ b')
                     I

    III : (aâ‚€ , g b) â‰ºâŸ¨ (Î± Ã—â‚’ Î³) âŸ© (aâ‚€ , g b') â†’ g b â‰ºâŸ¨ Î³ âŸ© g b'
    III (inl p) = p
    III (inr (r , q)) = ğŸ˜-elim (irrefl Î± aâ‚€ q)

Ã—â‚’-left-cancellable-âŠ´ : (Î± Î² Î³ : Ordinal ğ“¤)
                      â†’ ğŸ˜â‚’ âŠ² Î±
                      â†’ (Î± Ã—â‚’ Î²) âŠ´ (Î± Ã—â‚’ Î³)
                      â†’ Î² âŠ´ Î³
Ã—â‚’-left-cancellable-âŠ´ Î± Î² Î³ p@(aâ‚€ , aâ‚€-least) ğ•— =
  Ã—â‚’-left-cancellable-âŠ´-generalised Î± Î² Î³ aâ‚€ p
   (transport (Î» - â†’ (Î± Ã—â‚’ Î²) âŠ´ -)
              (ğŸ˜â‚’-right-neutral (Î± Ã—â‚’ Î³) â»Â¹ âˆ™ ap ((Î± Ã—â‚’ Î³) +â‚’_) aâ‚€-least) ğ•—)

simulation-product-decomposition
 : (Î± : Ordinal ğ“¤) (Î² Î³ : Ordinal ğ“¥)
   ((aâ‚€ , aâ‚€-least) : ğŸ˜â‚’ âŠ² Î±)
   ((f , _) : (Î± Ã—â‚’ Î²) âŠ´ (Î± Ã—â‚’ Î³))
 â†’ (a : âŸ¨ Î± âŸ©) (b : âŸ¨ Î² âŸ©) â†’ f (a , b) ï¼ (a , prâ‚‚ (f (aâ‚€ , b)))
simulation-product-decomposition {ğ“¤} {ğ“¥} Î± Î² Î³ (aâ‚€ , aâ‚€-least)
                                 (f , sim@(init-seg , order-pres)) a b = I
 where
  f' : âŸ¨ Î± Ã—â‚’ Î² âŸ© â†’ âŸ¨ Î± Ã—â‚’ Î³ âŸ©
  f' (a , b) = (a , prâ‚‚ (f (aâ‚€ , b)))

  P : âŸ¨ Î± Ã—â‚’ Î² âŸ© â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P (a , b) = (f (a , b)) ï¼ f' (a , b)

  I : P (a , b)
  I = Transfinite-induction (Î± Ã—â‚’ Î²) P II (a , b)
   where
    II : (x : âŸ¨ Î± Ã—â‚’ Î² âŸ©)
       â†’ ((y : âŸ¨ Î± Ã—â‚’ Î² âŸ©) â†’ y â‰ºâŸ¨ Î± Ã—â‚’ Î² âŸ© x â†’ P y)
       â†’ P x
    II (a , b) IH = Extensionality (Î± Ã—â‚’ Î³) (f (a , b)) (f' (a , b)) III IV
     where
      III : (u : âŸ¨ Î± Ã—â‚’ Î³ âŸ©) â†’ u â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f (a , b) â†’ u â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f' (a , b)
      III (a' , c') p = transport (Î» - â†’ - â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f' (a , b)) IIIâ‚‚ (IIIâ‚ƒ p')
       where
        IIIâ‚ : Î£ (a'' , b') ê‰ âŸ¨ Î± Ã—â‚’ Î² âŸ© , (a'' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î² âŸ© (a , b)
                                         Ã— (f (a'' , b') ï¼ a' , c')
        IIIâ‚ = init-seg (a , b) (a' , c') p
        a'' = prâ‚ (prâ‚ IIIâ‚)
        b' = prâ‚‚ (prâ‚ IIIâ‚)
        p' = prâ‚ (prâ‚‚ IIIâ‚)
        eq : f (a'' , b') ï¼ (a' , c')
        eq = prâ‚‚ (prâ‚‚ IIIâ‚)

        IIIâ‚‚ : f' (a'' , b') ï¼ (a' , c')
        IIIâ‚‚ = IH (a'' , b') p' â»Â¹ âˆ™ eq

        IIIâ‚ƒ : (a'' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î² âŸ© (a , b)
             â†’ f' (a'' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f' (a , b)
        IIIâ‚ƒ (inl q) = h (order-pres (aâ‚€' , b') (aâ‚€ , b) (inl q))
         where
          aâ‚€' : âŸ¨ Î± âŸ©
          aâ‚€' = prâ‚ (f (aâ‚€ , b))

          ih : (f (aâ‚€' , b')) ï¼ f' (aâ‚€' , b')
          ih = IH (aâ‚€' , b') (inl q)

          h : f  (aâ‚€' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f  (aâ‚€ , b)
            â†’ f' (a'' , b') â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f' (a , b)
          h (inl r) = inl (transport (Î» - â†’ - â‰ºâŸ¨ Î³ âŸ© prâ‚‚ (f (aâ‚€ , b)))
                                     (ap prâ‚‚ ih) r)
          h (inr (_ , r)) = ğŸ˜-elim (irrefl Î± aâ‚€' (transport (Î» - â†’ - â‰ºâŸ¨ Î± âŸ© aâ‚€')
                                                            (ap prâ‚ ih) r))
        IIIâ‚ƒ (inr (e , q)) = inr (ap (Î» - â†’ prâ‚‚ (f (aâ‚€ , -))) e , q)

      IV : (u : âŸ¨ Î± Ã—â‚’ Î³ âŸ©) â†’ u â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f' (a , b) â†’ u â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f  (a , b)
      IV (a' , c') (inl p) = lâ‚‚ (a' , c') (inl p)
       where
        lâ‚ : aâ‚€ â‰¼âŸ¨ Î± âŸ© a
        lâ‚ x p = ğŸ˜-elim (transport âŸ¨_âŸ© (aâ‚€-least â»Â¹) (x , p))
        lâ‚‚ : f (aâ‚€ , b) â‰¼âŸ¨ Î± Ã—â‚’ Î³ âŸ© f (a , b)
        lâ‚‚ = simulations-are-monotone _ _
              f sim (aâ‚€ , b) (a , b) (Ã—â‚’-â‰¼-left Î± Î² lâ‚)
      IV (a' , c') (inr (r , q)) =
       transport (Î» - â†’ - â‰ºâŸ¨ Î± Ã—â‚’ Î³ âŸ© f  (a , b)) eq
                 (order-pres (a' , b) (a , b) (inr (refl , q)))
        where
         eq = f  (a' , b)             ï¼âŸ¨ IH (a' , b) (inr (refl , q)) âŸ©
              f' (a' , b)             ï¼âŸ¨ refl âŸ©
              (a' , prâ‚‚ (f (aâ‚€ , b))) ï¼âŸ¨ ap (a' ,_) (r â»Â¹) âŸ©
              (a' , c')               âˆ
\end{code}

The following result states that multiplication for ordinals can be cancelled on
the left. Interestingly, Andrew Swan [Swa18] proved that the corresponding
result for sets is not provable constructively already for Î± = ğŸš: there are
toposes where the statement

  ğŸš Ã— X â‰ƒ ğŸš Ã— Y â†’ X â‰ƒ Y

is not true for certain objects X and Y in the topos.

[Swa18] Andrew Swan
        On Dividing by Two in Constructive Mathematics
        2018
        https://arxiv.org/abs/1804.04490

\begin{code}

Ã—â‚’-left-cancellable : (Î± Î² Î³ : Ordinal ğ“¤)
                    â†’ ğŸ˜â‚’ âŠ² Î±
                    â†’ (Î± Ã—â‚’ Î²) ï¼ (Î± Ã—â‚’ Î³)
                    â†’ Î² ï¼ Î³
Ã—â‚’-left-cancellable {ğ“¤ = ğ“¤} Î± Î² Î³ p e = âŠ´-antisym Î² Î³ (f Î² Î³ e) (f Î³ Î² (e â»Â¹))
 where
  f : (Î² Î³ : Ordinal ğ“¤) â†’ (Î± Ã—â‚’ Î²) ï¼ (Î± Ã—â‚’ Î³) â†’ Î² âŠ´ Î³
  f Î² Î³ e = Ã—â‚’-left-cancellable-âŠ´ Î± Î² Î³ p (â‰ƒâ‚’-to-âŠ´ (Î± Ã—â‚’ Î²) (Î± Ã—â‚’ Î³)
                                                   (idtoeqâ‚’ (Î± Ã—â‚’ Î²) (Î± Ã—â‚’ Î³) e))

\end{code}

Using similar techniques, we can also prove that multiplication is
left cancellable with respect to âŠ².

\begin{code}

simulation-product-decomposition-leftover-empty
 : (Î± Î² Î³ : Ordinal ğ“¤)
 â†’ ğŸ˜â‚’ âŠ² Î±
 â†’ (a : âŸ¨ Î± âŸ©)
 â†’ (Î± Ã—â‚’ Î²) ï¼ (Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ a))
 â†’ (Î± Ã—â‚’ Î²) ï¼ (Î± Ã—â‚’ Î³)
simulation-product-decomposition-leftover-empty Î± Î² Î³ (aâ‚€ , p) a e = eq
 where
  a-least : (x : âŸ¨ Î± âŸ©) â†’ Â¬ (x â‰ºâŸ¨ Î± âŸ© a)
  a-least x l = +disjoint (inr-is-inl â»Â¹)
   where
    ğ•— : Î± Ã—â‚’ Î² âŠ´ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ a)
    ğ•— = â‰ƒâ‚’-to-âŠ´ _ _ (idtoeqâ‚’ _ _ e)
    f = [ (Î± Ã—â‚’ Î²) , ((Î± Ã—â‚’ Î³) +â‚’ (Î± â†“ a)) ]âŸ¨ ğ•— âŸ©

    ğ•—â»Â¹ : Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ a) âŠ´ Î± Ã—â‚’ Î²
    ğ•—â»Â¹ = â‰ƒâ‚’-to-âŠ´ _ _ (idtoeqâ‚’ _ _ (e â»Â¹))
    fâ»Â¹ = [ Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ a) , Î± Ã—â‚’ Î² ]âŸ¨ ğ•—â»Â¹ âŸ©

    f-decomposition : Î£ g ê‰ (âŸ¨ Î² âŸ© â†’ âŸ¨ Î³ âŸ©) ,
                        ((a : âŸ¨ Î± âŸ©) (b : âŸ¨ Î² âŸ©) â†’ f (a , b) ï¼ inl (a , g b) )
    f-decomposition =
      simulation-product-decomposition-generalised Î± (aâ‚€ , p) Î² Î³ a ğ•—
    g = prâ‚ f-decomposition

    inr-is-inl = (inr (x , l))         ï¼âŸ¨ equiv _ _ e (inr (x , l)) âŸ©
                 f (fâ»Â¹ (inr (x , l))) ï¼âŸ¨ prâ‚‚ f-decomposition x' y' âŸ©
                 inl (x' , g y')       âˆ
     where
      x' = prâ‚ (fâ»Â¹ (inr (x , l)))
      y' = prâ‚‚ (fâ»Â¹ (inr (x , l)))
      equiv : (Î± Î² : Ordinal ğ“¤) â†’ (eq : Î± ï¼ Î²) (x : âŸ¨ Î² âŸ©)
            â†’ x ï¼ [ Î± , Î² ]âŸ¨ â‰ƒâ‚’-to-âŠ´ Î± Î² (idtoeqâ‚’ Î± Î² eq) âŸ©
                     ([ Î² , Î± ]âŸ¨ â‰ƒâ‚’-to-âŠ´ Î² Î± (idtoeqâ‚’ Î² Î± (eq â»Â¹)) âŸ© x)
      equiv Î± Î² refl x = refl


  a-is-aâ‚€ : a ï¼ aâ‚€
  a-is-aâ‚€ = Extensionality Î± a aâ‚€ (Î» x l â†’ ğŸ˜-elim (a-least x l))
                                  (Î» x l â†’ ğŸ˜-elim (transportâ»Â¹ âŸ¨_âŸ© p (x , l)))

  eq = Î± Ã—â‚’ Î²            ï¼âŸ¨ e âŸ©
       Î± Ã—â‚’ Î³ +â‚’ (Î± â†“ a) ï¼âŸ¨ ap ((Î± Ã—â‚’ Î³) +â‚’_) (ap (Î± â†“_) a-is-aâ‚€ âˆ™ p â»Â¹) âŸ©
       Î± Ã—â‚’ Î³ +â‚’ ğŸ˜â‚’      ï¼âŸ¨ ğŸ˜â‚’-right-neutral (Î± Ã—â‚’ Î³) âŸ©
       Î± Ã—â‚’ Î³            âˆ

Ã—â‚’-left-cancellable-âŠ² : (Î± Î² Î³ : Ordinal ğ“¤)
                      â†’ ğŸ˜â‚’ âŠ² Î±
                      â†’ Î± Ã—â‚’ Î² âŠ² Î± Ã—â‚’ Î³
                      â†’ Î² âŠ² Î³
Ã—â‚’-left-cancellable-âŠ² Î± Î² Î³ Î±-positive ((a , c) , p) = c , III
 where
  I : Î± Ã—â‚’ Î² ï¼ Î± Ã—â‚’ (Î³ â†“ c) +â‚’ (Î± â†“ a)
  I = p âˆ™ Ã—â‚’-â†“ Î± Î³

  II : Î± Ã—â‚’ Î² ï¼ Î± Ã—â‚’ (Î³ â†“ c)
  II = simulation-product-decomposition-leftover-empty Î± Î² (Î³ â†“ c) Î±-positive a I

  III : Î² ï¼ (Î³ â†“ c)
  III = Ã—â‚’-left-cancellable Î± Î² (Î³ â†“ c) Î±-positive II

\end{code}