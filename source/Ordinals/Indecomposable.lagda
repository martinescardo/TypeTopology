Martin Escardo, 25th August 2022,
written down in Agda 27th August 2022 while travelling back from
Thierry Coquand's 60th birthday celebration

The type of ordinals is decomposable as a disjoint union of two
pointed types if and only weak excluded middle holds.

Equivalently, there is a function f : Ordinal ğ“¤ â†’ ğŸš such that f Î± ï¼ 0 and f Î² = 1 for some ordinals Î± and Î² if and only if weak excluded middle holds (every negated proposition is decidable, which is equivalent to De Morgan's Law). https://www.cs.bham.ac.uk/~mhe/TypeTopology/Ordinals.Indecomposable.html

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UF.Univalence

module Ordinals.Indecomposable (ua : Univalence) where

open import UF.Base
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Equiv
open import UF.UA-FunExt
open import UF.FunExt
open import UF.ExcludedMiddle
open import UF.Size

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

 pe : PropExt
 pe = Univalence-gives-PropExt ua

open import MLTT.Spartan
open import MLTT.Two-Properties

open import Ordinals.Type
open import Ordinals.OrdinalOfOrdinals ua
open import Ordinals.Arithmetic fe

\end{code}

A type X is decomposable if there are designated pointed types Xâ‚€ and
Xâ‚ with X â‰ƒ Xâ‚€ + Xâ‚. Equivalently, X is decomposable if there is a
designated non-constant function f : X â†’ ğŸš, in the strong sense that
there are designated xâ‚€ and xâ‚ : X that are mapped to respectively â‚€
and â‚ by f.

\begin{code}

decomposable : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
decomposable X = Î£ xâ‚€ ê‰ X , Î£ xâ‚ ê‰ X , Î£ f ê‰ (X â†’ ğŸš) , (f xâ‚€ ï¼ â‚€) Ã— (f xâ‚ ï¼ â‚)

decomposableâ‚ : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
decomposableâ‚ {ğ“¤} X = Î£ Xâ‚€ ê‰ ğ“¤ Ì‡ , Î£ Xâ‚ ê‰ ğ“¤ Ì‡ , Xâ‚€ Ã— Xâ‚ Ã— (X â‰ƒ Xâ‚€ + Xâ‚)

\end{code}

TODO. decomposable X â‰ƒ decomposableâ‚ X. Is this already proved
somewhere in TypeTopology? This equivalence was already used in a
publication with coathors.

\begin{code}

WEM-gives-decomposability-of-two-pointed-types : WEM ğ“¤
                                               â†’ (X : ğ“¤ Ì‡ )
                                               â†’ has-two-distinct-points X
                                               â†’ decomposable X
WEM-gives-decomposability-of-two-pointed-types wem X ((xâ‚€ , xâ‚) , d) = Î³
 where
  g : (x : X) â†’ Â¬ (x â‰  xâ‚€) + Â¬Â¬ (x â‰  xâ‚€) â†’ ğŸš
  g x (inl _) = â‚€
  g x (inr _) = â‚

  h : (x : X) â†’ Â¬ (x â‰  xâ‚€) + Â¬Â¬ (x â‰  xâ‚€)
  h x = wem (x â‰  xâ‚€) (negations-are-props fe')

  f : X â†’ ğŸš
  f x = g x (h x)

  fâ‚€ : (Î´ : Â¬ (xâ‚€ â‰  xâ‚€) + Â¬Â¬ (xâ‚€ â‰  xâ‚€)) â†’ g xâ‚€ Î´ ï¼ â‚€
  fâ‚€ (inl _) = refl
  fâ‚€ (inr u) = ğŸ˜-elim (three-negations-imply-one u refl)

  eâ‚€ : f xâ‚€ ï¼ â‚€
  eâ‚€ = fâ‚€ (h xâ‚€)

  fâ‚ : (Î´ : Â¬ (xâ‚ â‰  xâ‚€) + Â¬Â¬ (xâ‚ â‰  xâ‚€)) â†’ g xâ‚ Î´ ï¼ â‚
  fâ‚ (inl Ï•) = ğŸ˜-elim (Ï• (â‰ -sym d))
  fâ‚ (inr _) = refl

  eâ‚ : f xâ‚ ï¼ â‚
  eâ‚ = fâ‚ (h xâ‚)

  Î³ : decomposable X
  Î³ = xâ‚€ , xâ‚ , f , eâ‚€ , eâ‚

WEM-gives-decomposability-of-ordinals-typeâº : WEM (ğ“¤ âº) â†’ decomposable (Ordinal ğ“¤)
WEM-gives-decomposability-of-ordinals-typeâº {ğ“¤} wem =
 WEM-gives-decomposability-of-two-pointed-types wem (Ordinal ğ“¤)
  ((ğŸ™â‚’ , ğŸ˜â‚’) , (Î» (e : ğŸ™â‚’ ï¼ ğŸ˜â‚’) â†’ ğŸ˜-elim (idtofun ğŸ™ ğŸ˜ (ap âŸ¨_âŸ© e) â‹†)))

\end{code}

We can strengthen this to WEM ğ“¤ â†’ decomposable (Ordinal ğ“¤ Ì‡) using
the fact that Ordinal ğ“¤ Ì‡ is locally small.

\begin{code}

WEM-gives-decomposability-of-two-pointed-typesâº : WEM ğ“¤
                                               â†’ (X : ğ“¤ âº Ì‡ )
                                               â†’ is-locally-small X
                                               â†’ has-two-distinct-points X
                                               â†’ decomposable X
WEM-gives-decomposability-of-two-pointed-typesâº {ğ“¤} wem X l ((xâ‚€ , xâ‚) , d) = Î³
 where
  g : (x : X) â†’ Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) â†’ ğŸš
  g x (inl _) = â‚€
  g x (inr _) = â‚

  h : (x : X) â†’ Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€)
  h x = wem (x â‰ âŸ¦ l âŸ§ xâ‚€) (negations-are-props fe')

  f : X â†’ ğŸš
  f x = g x (h x)

  fâ‚€ : (Î´ : Â¬ (xâ‚€ â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (xâ‚€ â‰ âŸ¦ l âŸ§ xâ‚€)) â†’ g xâ‚€ Î´ ï¼ â‚€
  fâ‚€ (inl _) = refl
  fâ‚€ (inr u) = ğŸ˜-elim (three-negations-imply-one u âŸ¦ l âŸ§-refl)

  eâ‚€ : f xâ‚€ ï¼ â‚€
  eâ‚€ = fâ‚€ (h xâ‚€)

  fâ‚ : (Î´ : Â¬ (xâ‚ â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (xâ‚ â‰ âŸ¦ l âŸ§ xâ‚€)) â†’ g xâ‚ Î´ ï¼ â‚
  fâ‚ (inl Ï•) = ğŸ˜-elim (Ï• (â‰ -gives-â‰ âŸ¦ l âŸ§ (â‰ -sym d)))
  fâ‚ (inr _) = refl

  eâ‚ : f xâ‚ ï¼ â‚
  eâ‚ = fâ‚ (h xâ‚)

  Î³ : decomposable X
  Î³ = xâ‚€ , xâ‚ , f , eâ‚€ , eâ‚

WEM-gives-decomposability-of-ordinals-type : WEM ğ“¤ â†’ decomposable (Ordinal ğ“¤)
WEM-gives-decomposability-of-ordinals-type {ğ“¤} wem =
 WEM-gives-decomposability-of-two-pointed-typesâº wem (Ordinal ğ“¤)
  the-type-of-ordinals-is-locally-small
  ((ğŸ™â‚’ , ğŸ˜â‚’) , (Î» (e : ğŸ™â‚’ ï¼ ğŸ˜â‚’) â†’ ğŸ˜-elim (idtofun ğŸ™ ğŸ˜ (ap âŸ¨_âŸ© e) â‹†)))

\end{code}

For the converse, we use the following notion, where Î© ğ“¤ is the type
of truth values, or propositions, in the universe ğ“¤.

\begin{code}

Î©-Path : {X : ğ“¤ Ì‡ } (ğ“¥ : Universe) â†’ X â†’ X â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
Î©-Path {ğ“¤} {X} ğ“¥ x y = Î£ f ê‰ (Î© ğ“¥ â†’ X) , (f âŠ¥Î© ï¼ x) Ã— (f âŠ¤Î© ï¼ y)

has-Î©-paths : (ğ“¥ : Universe) â†’ ğ“¤ Ì‡  â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
has-Î©-paths ğ“¥ X = (x y : X) â†’ Î©-Path ğ“¥ x y

type-of-ordinals-has-Î©-paths : has-Î©-paths ğ“¤ (Ordinal ğ“¤)
type-of-ordinals-has-Î©-paths {ğ“¤} Î± Î² = f , Î³âŠ¥ , Î³âŠ¤
 where
  f : Î© ğ“¤ â†’ Ordinal ğ“¤
  f p = (Î©-to-ordinal (not fe' p) Ã—â‚’ Î±) +â‚’ (Î©-to-ordinal p Ã—â‚’ Î²)

  Î³âŠ¥ : f âŠ¥Î© ï¼ Î±
  Î³âŠ¥ = eqtoidâ‚’ (f âŠ¥Î©) Î± (u , o , e , p)
   where
    u : âŸ¨ f âŠ¥Î© âŸ© â†’ âŸ¨ Î± âŸ©
    u (inl (x , a)) = a

    o : is-order-preserving (f âŠ¥Î©) Î± u
    o (inl (x , a)) (inl (x , b)) (inr (refl , l)) = l

    v : âŸ¨ Î± âŸ© â†’ âŸ¨ f âŠ¥Î© âŸ©
    v a = inl (ğŸ˜-elim , a)

    vu : v âˆ˜ u âˆ¼ id
    vu (inl (x , a)) = ap inl (to-Ã—-ï¼ (dfunext fe' (Î» z â†’ ğŸ˜-elim z)) refl)

    uv : u âˆ˜ v âˆ¼ id
    uv a = refl

    e : is-equiv u
    e = qinvs-are-equivs u (v , vu , uv)

    p : is-order-preserving Î± (f âŠ¥Î©) v
    p a b l = inr (refl , l)

  Î³âŠ¤ : f âŠ¤Î© ï¼ Î²
  Î³âŠ¤ = eqtoidâ‚’ (f âŠ¤Î©) Î² (u , o , e , p)
   where
    u : âŸ¨ f âŠ¤Î© âŸ© â†’ âŸ¨ Î² âŸ©
    u (inl (f , _)) = ğŸ˜-elim (f â‹†)
    u (inr (â‹† , b)) = b

    o : is-order-preserving (f âŠ¤Î©) Î² u
    o (inl (f , _)) y l = ğŸ˜-elim (f â‹†)
    o (inr (â‹† , _)) (inr (â‹† , _)) (inr (_ , l)) = l

    v : âŸ¨ Î² âŸ© â†’ âŸ¨ f âŠ¤Î© âŸ©
    v b = inr (â‹† , b)

    vu : v âˆ˜ u âˆ¼ id
    vu (inl (f , _)) = ğŸ˜-elim (f â‹†)
    vu (inr (â‹† , b)) = refl

    uv : u âˆ˜ v âˆ¼ id
    uv b = refl

    e : is-equiv u
    e = qinvs-are-equivs u (v , vu , uv)

    p : is-order-preserving Î² (f âŠ¤Î©) v
    p b c l = inr (refl , l)

non-constant-map-Î©-to-ğŸš-gives-WEM :
        (Î£ f ê‰ (Î© ğ“¤ â†’ ğŸš) , Î£ pâ‚€ ê‰ Î© ğ“¤ , Î£ pâ‚ ê‰ Î© ğ“¤ , (f pâ‚€ ï¼ â‚€) Ã— (f pâ‚ ï¼ â‚))
      â†’ WEM ğ“¤
non-constant-map-Î©-to-ğŸš-gives-WEM {ğ“¤} (f , pâ‚€@(Pâ‚€ , iâ‚€) , pâ‚@(Pâ‚ , iâ‚) , eâ‚€ , eâ‚) = IV
 where
  g : Î© ğ“¤ â†’ Î© ğ“¤
  g (Q , j) = ((Pâ‚€ Ã— Q) + (Pâ‚ Ã— Â¬ Q)) , k
   where
    k : is-prop ((Pâ‚€ Ã— Q) + (Pâ‚ Ã— Â¬ Q))
    k (inl (a , b)) (inl (u , v)) = ap inl (to-Ã—-ï¼ (iâ‚€ a u) (j b v))
    k (inl (a , b)) (inr (u , v)) = ğŸ˜-elim (v b)
    k (inr (a , b)) (inl (u , v)) = ğŸ˜-elim (b v)
    k (inr (a , b)) (inr (u , v)) = ap inr (to-Ã—-ï¼
                                             (iâ‚ a u)
                                             (negations-are-props fe' b v))

  Iâ‚€ : (q : Î© ğ“¤) â†’ q holds â†’ f (g q) ï¼ â‚€
  Iâ‚€ q h = II
   where
    I : g q ï¼ pâ‚€
    I = to-subtype-ï¼
          (Î» _ â†’ being-prop-is-prop fe')
          (univalence-gives-propext (ua ğ“¤) (prâ‚‚ (g q)) iâ‚€
            (cases prâ‚ (Î» (_ , n) â†’ ğŸ˜-elim (n h)))
            (Î» x â†’ inl (x , h)))

    II = f (g q) ï¼âŸ¨ ap f I âŸ©
         f pâ‚€    ï¼âŸ¨ eâ‚€ âŸ©
         â‚€       âˆ

  Iâ‚ : (q : Î© ğ“¤) â†’ Â¬ (q holds) â†’ f (g q) ï¼ â‚
  Iâ‚ q n = II
   where
    I : g q ï¼ pâ‚
    I = to-subtype-ï¼
          (Î» _ â†’ being-prop-is-prop fe')
          (univalence-gives-propext (ua ğ“¤) (prâ‚‚ (g q)) iâ‚
          (cases (Î» (_ , h) â†’ ğŸ˜-elim (n h)) prâ‚)
          (Î» x â†’ inr (x , n)))

    II = f (g q) ï¼âŸ¨ ap f I âŸ©
         f pâ‚    ï¼âŸ¨ eâ‚ âŸ©
         â‚       âˆ

  IIIâ‚€ : (q : Î© ğ“¤) â†’ f (g q) ï¼ â‚€ â†’ Â¬ (q holds) + Â¬Â¬ (q holds)
  IIIâ‚€ q e = inr (contrapositive (Iâ‚ q) (equal-â‚€-different-from-â‚ e))

  IIIâ‚ : (q : Î© ğ“¤) â†’ f (g q) ï¼ â‚ â†’ Â¬ (q holds) + Â¬Â¬ (q holds)
  IIIâ‚ q e = inl (contrapositive (Iâ‚€ q) (equal-â‚-different-from-â‚€ e))

  IV : (Q : ğ“¤  Ì‡) â†’ is-prop Q â†’ Â¬ Q + Â¬Â¬ Q
  IV Q j = ğŸš-equality-cases (IIIâ‚€ (Q , j)) (IIIâ‚ (Q , j))


decomposable-type-with-Î©-paths-gives-WEM : {X : ğ“¤ Ì‡ }
                                         â†’ decomposable X
                                         â†’ has-Î©-paths ğ“¥ X
                                         â†’ WEM ğ“¥
decomposable-type-with-Î©-paths-gives-WEM {ğ“¤} {ğ“¥} {X} (xâ‚€ , xâ‚ , f , eâ‚€ , eâ‚) c = Î³
 where
  g : Î© ğ“¥ â†’ X
  g = prâ‚ (c xâ‚€ xâ‚)

  gp : (g âŠ¥Î© ï¼ xâ‚€) Ã— (g âŠ¤Î© ï¼ xâ‚)
  gp = prâ‚‚ (c xâ‚€ xâ‚)

  Iâ‚€ = f (g âŠ¥Î©) ï¼âŸ¨ ap f (prâ‚ gp) âŸ©
       f xâ‚€     ï¼âŸ¨ eâ‚€ âŸ©
       â‚€        âˆ

  Iâ‚ = f (g âŠ¤Î©) ï¼âŸ¨ ap f (prâ‚‚ gp) âŸ©
       f xâ‚     ï¼âŸ¨ eâ‚ âŸ©
       â‚        âˆ

  Î³ : WEM ğ“¥
  Î³ = non-constant-map-Î©-to-ğŸš-gives-WEM (f âˆ˜ g , âŠ¥Î© , âŠ¤Î© , Iâ‚€ , Iâ‚)

decomposability-of-ordinals-type-gives-WEM : decomposable (Ordinal ğ“¤) â†’ WEM ğ“¤
decomposability-of-ordinals-type-gives-WEM d =
 decomposable-type-with-Î©-paths-gives-WEM d type-of-ordinals-has-Î©-paths

Ordinal-decomposable-iff-WEM : decomposable (Ordinal ğ“¤) â‡” WEM ğ“¤
Ordinal-decomposable-iff-WEM = decomposability-of-ordinals-type-gives-WEM ,
                               WEM-gives-decomposability-of-ordinals-type

\end{code}

TODO. Because WEM ğ“¤ is a proposition, it follows that
âˆ¥ decomposable (Ordinal ğ“¤) âˆ¥ â‡” WEM ğ“¤, and hence also
âˆ¥ decomposable (Ordinal ğ“¤) âˆ¥ â†’ decomposable (Ordinal ğ“¤).
