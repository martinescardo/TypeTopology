Martin Escardo, 25th August 2022,
written down in Agda 27th August 2022 while travelling back from
Thierry Coquand's 60th birthday celebration.

The type of ordinals is decomposable as a disjoint union of two
pointed types if and only if weak excluded middle holds (every negated
proposition is decidable, which is equivalent to De Morgan's Law).

Equivalently, there is a function f : Ordinal ğ“¤ â†’ ğŸš such that f Î± ï¼ 0
and f Î² = 1 for some ordinals Î± and Î² if and only if weak excluded
middle holds.

In other words, the type Ordinal ğ“¤ has no non-trivial decidable
property unless weak excluded middle holds.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness #-}

open import UF.Univalence

module Ordinals.Indecomposable (ua : Univalence) where

open import MLTT.Spartan
open import MLTT.Two-Properties

open import UF.Base
open import UF.Classifiers
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.EquivalenceExamples
open import UF.ExcludedMiddle
open import UF.FunExt
open import UF.PropTrunc
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

â‡_ : Î© ğ“¤ â†’ Î© ğ“¤
â‡_ = not fe'

open import Ordinals.Arithmetic fe
open import Ordinals.Equivalence
open import Ordinals.Maps
open import Ordinals.OrdinalOfOrdinals ua
open import Ordinals.Type
open import Ordinals.Underlying

\end{code}

A type X is decomposable if there are pointed types Xâ‚€ and Xâ‚ with
X â‰ƒ Xâ‚€ + Xâ‚. Equivalently, X is decomposable if there is a
non-constant function f : X â†’ ğŸš, in the strong sense that there are xâ‚€
and xâ‚ in X that are mapped to respectively â‚€ and â‚ by f.

We first work with the type of all decompositions, in the above two
equivalent manifestations, and later we consider decomposability
defined as its propositional truncation.

\begin{code}

decomposition : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
decomposition X = Î£ f ê‰ (X â†’ ğŸš) , (Î£ xâ‚€ ê‰ X , f xâ‚€ ï¼ â‚€) Ã— (Î£ xâ‚ ê‰ X , f xâ‚ ï¼ â‚)

decomposition' : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
decomposition' {ğ“¤} X = Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , (Y â‚€ + Y â‚ â‰ƒ X) Ã— Y â‚€ Ã— Y â‚

\end{code}

The above two decomposition types are equivalent:

\begin{code}

decomposition-lemma : (X : ğ“¤ Ì‡ )
                    â†’ (Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , (Y â‚€ + Y â‚ â‰ƒ X))
                    â‰ƒ (X â†’ ğŸš)
decomposition-lemma {ğ“¤} X =
 (Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , (Y â‚€ + Y â‚ â‰ƒ X))       â‰ƒâŸ¨ I âŸ©
 (Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , ((Î£ n ê‰ ğŸš , Y n) â‰ƒ X)) â‰ƒâŸ¨ II âŸ©
 (X â†’ ğŸš)                                    â– 
 where
  I  = Î£-cong (Î» Y â†’ â‰ƒ-cong-left fe (â‰ƒ-sym alternative-+))
  II = Î£-fibers-â‰ƒ (ua ğ“¤) fe'

decompositions-agree : (X : ğ“¤ Ì‡ ) â†’ decomposition X â‰ƒ decomposition' X
decompositions-agree {ğ“¤} X =
 (Î£ f ê‰ (X â†’ ğŸš) , fiber f â‚€ Ã— fiber f â‚)                        â‰ƒâŸ¨ I âŸ©
 (Î£ (Y , _) ê‰ (Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , (Y â‚€ + Y â‚ â‰ƒ X)) , Y â‚€ Ã— Y â‚)  â‰ƒâŸ¨ II âŸ©
 (Î£ Y ê‰ (ğŸš â†’ ğ“¤ Ì‡ ) , (Y â‚€ + Y â‚ â‰ƒ X) Ã— Y â‚€ Ã— Y â‚)                â– 
 where
  I  = Î£-change-of-variable-â‰ƒ _ (â‰ƒ-sym (decomposition-lemma X))
  II = Î£-assoc

WEM-gives-decomposition-of-two-pointed-types : WEM ğ“¤
                                             â†’ (X : ğ“¤ Ì‡ )
                                             â†’ has-two-distinct-points X
                                             â†’ decomposition X
WEM-gives-decomposition-of-two-pointed-types wem X ((xâ‚€ , xâ‚) , d) = Î³
 where
  g : (x : X) â†’ Â¬ (x â‰  xâ‚€) + Â¬Â¬ (x â‰  xâ‚€) â†’ ğŸš
  g x (inl _) = â‚€
  g x (inr _) = â‚

  h : (x : X) â†’ Â¬ (x â‰  xâ‚€) + Â¬Â¬ (x â‰  xâ‚€)
  h x = wem (x â‰  xâ‚€) (negations-are-props fe')

  f : X â†’ ğŸš
  f x = g x (h x)


  gâ‚€ : (Î´ : Â¬ (xâ‚€ â‰  xâ‚€) + Â¬Â¬ (xâ‚€ â‰  xâ‚€)) â†’ g xâ‚€ Î´ ï¼ â‚€
  gâ‚€ (inl _) = refl
  gâ‚€ (inr u) = ğŸ˜-elim (three-negations-imply-one u refl)

  eâ‚€ : f xâ‚€ ï¼ â‚€
  eâ‚€ = gâ‚€ (h xâ‚€)

  gâ‚ : (Î´ : Â¬ (xâ‚ â‰  xâ‚€) + Â¬Â¬ (xâ‚ â‰  xâ‚€)) â†’ g xâ‚ Î´ ï¼ â‚
  gâ‚ (inl Ï•) = ğŸ˜-elim (Ï• (â‰ -sym d))
  gâ‚ (inr _) = refl

  eâ‚ : f xâ‚ ï¼ â‚
  eâ‚ = gâ‚ (h xâ‚)

  Î³ : decomposition X
  Î³ = f , (xâ‚€ , eâ‚€) , (xâ‚ , eâ‚)

WEM-gives-decomposition-of-ordinals-typeâº : WEM (ğ“¤ âº) â†’ decomposition (Ordinal ğ“¤)
WEM-gives-decomposition-of-ordinals-typeâº {ğ“¤} wem =
 WEM-gives-decomposition-of-two-pointed-types wem (Ordinal ğ“¤)
  ((ğŸ™â‚’ , ğŸ˜â‚’) , (Î» (e : ğŸ™â‚’ ï¼ ğŸ˜â‚’) â†’ ğŸ˜-elim (idtofun ğŸ™ ğŸ˜ (ap âŸ¨_âŸ© e) â‹†)))

\end{code}

We can strengthen this to WEM ğ“¤ â†’ decomposition (Ordinal ğ“¤) using
the fact that the type Ordinal ğ“¤ Ì‡ is locally small.

\begin{code}

WEM-gives-decomposition-of-two-pointed-typesâº : WEM ğ“¤
                                              â†’ (X : ğ“¤ âº Ì‡ )
                                              â†’ is-locally-small X
                                              â†’ has-two-distinct-points X
                                              â†’ decomposition X
WEM-gives-decomposition-of-two-pointed-typesâº {ğ“¤} wem X l ((xâ‚€ , xâ‚) , d) = Î³
 where
  g : (x : X) â†’ Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) â†’ ğŸš
  g x (inl _) = â‚€
  g x (inr _) = â‚

  h : (x : X) â†’ Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (x â‰ âŸ¦ l âŸ§ xâ‚€)
  h x = wem (x â‰ âŸ¦ l âŸ§ xâ‚€) (negations-are-props fe')

  f : X â†’ ğŸš
  f x = g x (h x)

  gâ‚€ : (Î´ : Â¬ (xâ‚€ â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (xâ‚€ â‰ âŸ¦ l âŸ§ xâ‚€)) â†’ g xâ‚€ Î´ ï¼ â‚€
  gâ‚€ (inl _) = refl
  gâ‚€ (inr u) = ğŸ˜-elim (three-negations-imply-one u âŸ¦ l âŸ§-refl)

  eâ‚€ : f xâ‚€ ï¼ â‚€
  eâ‚€ = gâ‚€ (h xâ‚€)

  gâ‚ : (Î´ : Â¬ (xâ‚ â‰ âŸ¦ l âŸ§ xâ‚€) + Â¬Â¬ (xâ‚ â‰ âŸ¦ l âŸ§ xâ‚€)) â†’ g xâ‚ Î´ ï¼ â‚
  gâ‚ (inl Ï•) = ğŸ˜-elim (Ï• (â‰ -gives-â‰ âŸ¦ l âŸ§ (â‰ -sym d)))
  gâ‚ (inr _) = refl

  eâ‚ : f xâ‚ ï¼ â‚
  eâ‚ = gâ‚ (h xâ‚)

  Î³ : decomposition X
  Î³ = f , (xâ‚€ , eâ‚€) , (xâ‚ , eâ‚)

WEM-gives-decomposition-of-ordinals-type : WEM ğ“¤ â†’ decomposition (Ordinal ğ“¤)
WEM-gives-decomposition-of-ordinals-type {ğ“¤} wem =
 WEM-gives-decomposition-of-two-pointed-typesâº wem (Ordinal ğ“¤)
  (the-type-of-ordinals-is-locally-small (ua ğ“¤) fe')
  ((ğŸ™â‚’ , ğŸ˜â‚’) , (Î» (e : ğŸ™â‚’ ï¼ ğŸ˜â‚’) â†’ ğŸ˜-elim (idtofun ğŸ™ ğŸ˜ (ap âŸ¨_âŸ© e) â‹†)))

\end{code}

For the converse, we use the following notion, where Î© ğ“¤ is the type
of truth values, or propositions, in the universe ğ“¤. An Î©-path from x
to y in a type X is a function f ê‰ Î© ğ“¥ â†’ X that maps false to x and
true to y. We collect all such functions in a type Î©-Path ğ“¥ x y.

\begin{code}

Î©-Path : {X : ğ“¤ Ì‡ } (ğ“¥ : Universe) â†’ X â†’ X â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
Î©-Path {ğ“¤} {X} ğ“¥ x y = Î£ f ê‰ (Î© ğ“¥ â†’ X) , (f âŠ¥Î© ï¼ x) Ã— (f âŠ¤Î© ï¼ y)

\end{code}

The ordinals in any universe have Î©-paths between any two points.

\begin{code}

has-Î©-paths : (ğ“¥ : Universe) â†’ ğ“¤ Ì‡  â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
has-Î©-paths ğ“¥ X = (x y : X) â†’ Î©-Path ğ“¥ x y

type-of-ordinals-has-Î©-paths : has-Î©-paths ğ“¤ (Ordinal ğ“¤)
type-of-ordinals-has-Î©-paths {ğ“¤} Î± Î² = f , Î³âŠ¥ , Î³âŠ¤
 where
  f : Î© ğ“¤ â†’ Ordinal ğ“¤
  f p = (Î©-to-ordinal (â‡ p) Ã—â‚’ Î±) +â‚’ (Î©-to-ordinal p Ã—â‚’ Î²)

  Î³âŠ¥ : f âŠ¥Î© ï¼ Î±
  Î³âŠ¥ = eqtoidâ‚’ (ua ğ“¤) fe' (f âŠ¥Î©) Î± (u , o , e , p)
   where
    u : âŸ¨ f âŠ¥Î© âŸ© â†’ âŸ¨ Î± âŸ©
    u (inl (x , a)) = a

    o : is-order-preserving (f âŠ¥Î©) Î± u
    o (inl (x , a)) (inl (x , b)) (inr (refl , l)) = l

    v : âŸ¨ Î± âŸ© â†’ âŸ¨ f âŠ¥Î© âŸ©
    v a = inl (ğŸ˜-elim , a)

    vu : v âˆ˜ u âˆ¼ id
    vu (inl (x , a)) = ap inl (to-Ã—-ï¼ (dfunext fe' (Î» z â†’ ğŸ˜-elim z)) refl)

    uv : u âˆ˜ v âˆ¼ id
    uv a = refl

    e : is-equiv u
    e = qinvs-are-equivs u (v , vu , uv)

    p : is-order-preserving Î± (f âŠ¥Î©) v
    p a b l = inr (refl , l)

  Î³âŠ¤ : f âŠ¤Î© ï¼ Î²
  Î³âŠ¤ = eqtoidâ‚’ (ua ğ“¤) fe' (f âŠ¤Î©) Î² (u , o , e , p)
   where
    u : âŸ¨ f âŠ¤Î© âŸ© â†’ âŸ¨ Î² âŸ©
    u (inl (f , _)) = ğŸ˜-elim (f â‹†)
    u (inr (â‹† , b)) = b

    o : is-order-preserving (f âŠ¤Î©) Î² u
    o (inl (f , _)) y l = ğŸ˜-elim (f â‹†)
    o (inr (â‹† , _)) (inr (â‹† , _)) (inr (_ , l)) = l

    v : âŸ¨ Î² âŸ© â†’ âŸ¨ f âŠ¤Î© âŸ©
    v b = inr (â‹† , b)

    vu : v âˆ˜ u âˆ¼ id
    vu (inl (f , _)) = ğŸ˜-elim (f â‹†)
    vu (inr (â‹† , b)) = refl

    uv : u âˆ˜ v âˆ¼ id
    uv b = refl

    e : is-equiv u
    e = qinvs-are-equivs u (v , vu , uv)

    p : is-order-preserving Î² (f âŠ¤Î©) v
    p b c l = inr (refl , l)

decomposition-of-Î©-gives-WEM : decomposition (Î© ğ“¤) â†’ WEM ğ“¤
decomposition-of-Î©-gives-WEM {ğ“¤} (f , (pâ‚€@(Pâ‚€ , iâ‚€) , eâ‚€) , (pâ‚@(Pâ‚ , iâ‚) , eâ‚)) = IV
 where
  g : Î© ğ“¤ â†’ Î© ğ“¤
  g (Q , j) = ((Pâ‚€ Ã— Q) + (Pâ‚ Ã— Â¬ Q)) , k
   where
    k : is-prop ((Pâ‚€ Ã— Q) + (Pâ‚ Ã— Â¬ Q))
    k (inl (a , b)) (inl (u , v)) = ap inl (to-Ã—-ï¼ (iâ‚€ a u) (j b v))
    k (inl (a , b)) (inr (u , v)) = ğŸ˜-elim (v b)
    k (inr (a , b)) (inl (u , v)) = ğŸ˜-elim (b v)
    k (inr (a , b)) (inr (u , v)) = ap inr (to-Ã—-ï¼
                                             (iâ‚ a u)
                                             (negations-are-props fe' b v))

  Iâ‚€ : (q : Î© ğ“¤) â†’ q holds â†’ f (g q) ï¼ â‚€
  Iâ‚€ q h = II
   where
    I : g q ï¼ pâ‚€
    I = to-subtype-ï¼
          (Î» _ â†’ being-prop-is-prop fe')
          (univalence-gives-propext (ua ğ“¤) (prâ‚‚ (g q)) iâ‚€
            (cases prâ‚ (Î» (_ , n) â†’ ğŸ˜-elim (n h)))
            (Î» x â†’ inl (x , h)))

    II = f (g q) ï¼âŸ¨ ap f I âŸ©
         f pâ‚€    ï¼âŸ¨ eâ‚€ âŸ©
         â‚€       âˆ

  Iâ‚ : (q : Î© ğ“¤) â†’ Â¬ (q holds) â†’ f (g q) ï¼ â‚
  Iâ‚ q n = II
   where
    I : g q ï¼ pâ‚
    I = to-subtype-ï¼
          (Î» _ â†’ being-prop-is-prop fe')
          (univalence-gives-propext (ua ğ“¤) (prâ‚‚ (g q)) iâ‚
          (cases (Î» (_ , h) â†’ ğŸ˜-elim (n h)) prâ‚)
          (Î» x â†’ inr (x , n)))

    II = f (g q) ï¼âŸ¨ ap f I âŸ©
         f pâ‚    ï¼âŸ¨ eâ‚ âŸ©
         â‚       âˆ

  IIIâ‚€ : (q : Î© ğ“¤) â†’ f (g q) ï¼ â‚€ â†’ Â¬ (q holds) + Â¬Â¬ (q holds)
  IIIâ‚€ q e = inr (contrapositive (Iâ‚ q) (equal-â‚€-different-from-â‚ e))

  IIIâ‚ : (q : Î© ğ“¤) â†’ f (g q) ï¼ â‚ â†’ Â¬ (q holds) + Â¬Â¬ (q holds)
  IIIâ‚ q e = inl (contrapositive (Iâ‚€ q) (equal-â‚-different-from-â‚€ e))

  IV : (Q : ğ“¤  Ì‡ )â†’ is-prop Q â†’ Â¬ Q + Â¬Â¬ Q
  IV Q j = ğŸš-equality-cases (IIIâ‚€ (Q , j)) (IIIâ‚ (Q , j))

decomposition-of-type-with-Î©-paths-gives-WEM : {X : ğ“¤ Ì‡ }
                                             â†’ decomposition X
                                             â†’ has-Î©-paths ğ“¥ X
                                             â†’ WEM ğ“¥
decomposition-of-type-with-Î©-paths-gives-WEM {ğ“¤} {ğ“¥} {X} (f , (xâ‚€ , eâ‚€) , (xâ‚ , eâ‚)) c = Î³
 where
  g : Î© ğ“¥ â†’ X
  g = prâ‚ (c xâ‚€ xâ‚)

  gp : (g âŠ¥Î© ï¼ xâ‚€) Ã— (g âŠ¤Î© ï¼ xâ‚)
  gp = prâ‚‚ (c xâ‚€ xâ‚)

  Iâ‚€ = f (g âŠ¥Î©) ï¼âŸ¨ ap f (prâ‚ gp) âŸ©
       f xâ‚€     ï¼âŸ¨ eâ‚€ âŸ©
       â‚€        âˆ

  Iâ‚ = f (g âŠ¤Î©) ï¼âŸ¨ ap f (prâ‚‚ gp) âŸ©
       f xâ‚     ï¼âŸ¨ eâ‚ âŸ©
       â‚        âˆ

  Î³ : WEM ğ“¥
  Î³ = decomposition-of-Î©-gives-WEM (f âˆ˜ g , (âŠ¥Î© , Iâ‚€) , (âŠ¤Î© , Iâ‚))

decomposition-of-ordinals-type-gives-WEM : decomposition (Ordinal ğ“¤) â†’ WEM ğ“¤
decomposition-of-ordinals-type-gives-WEM d =
 decomposition-of-type-with-Î©-paths-gives-WEM d type-of-ordinals-has-Î©-paths

Ordinal-decomposition-iff-WEM : decomposition (Ordinal ğ“¤) â‡” WEM ğ“¤
Ordinal-decomposition-iff-WEM = decomposition-of-ordinals-type-gives-WEM ,
                                WEM-gives-decomposition-of-ordinals-type

\end{code}

We now assume that propositional truncations exist to define
decomposability as the truncation of the type of decompositions. It is
a corollary of the above development that the decomposability of the
type of ordinals gives a specific decomposition.

\begin{code}

module _ (pt : propositional-truncations-exist) where

 open propositional-truncations-exist pt public

 decomposable : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
 decomposable X = âˆ¥ decomposition X âˆ¥

 Ordinal-decomposable-iff-WEM : decomposable (Ordinal ğ“¤) â‡” WEM ğ“¤
 Ordinal-decomposable-iff-WEM =
  âˆ¥âˆ¥-rec (WEM-is-prop fe) decomposition-of-ordinals-type-gives-WEM ,
  (Î» wem â†’ âˆ£ WEM-gives-decomposition-of-ordinals-type wem âˆ£)

 decomposability-gives-decomposition : decomposable (Ordinal ğ“¤) â†’ decomposition (Ordinal ğ“¤)
 decomposability-gives-decomposition {ğ“¤} Î´ = WEM-gives-decomposition-of-ordinals-type
                                               (lr-implication Ordinal-decomposable-iff-WEM Î´)

\end{code}

Notice that the formulation of this doesn't refer to WEM, but its
proof uses WEM, which follows from the hypothesis. Even though
decomposable (Ordinal ğ“¤) and WEM are property, we get data out of
them - if we are given a proof of decomposability.


Added 9th September 2022 by Tom de Jong.

After a discussion with MartÃ­n on 8th September 2022, we noticed that the
decomposability theorem can be generalised from Ord ğ“¤ to any locally small
ğ“¤-sup-lattice with two distinct points. (This is indeed a generalisation because
Ord ğ“¤ is a locally small ğ“¤-sup-lattice, at least in the presence of small set
quotients or set replacement, see Ordinals.OrdinalOfOrdinalsSuprema.)

One direction is still given by the lemma above:
  WEM-gives-decomposition-of-two-pointed-typesâº :
      WEM ğ“¤
    â†’ (X : ğ“¤ âº Ì‡ )
    â†’ is-locally-small X
    â†’ has-two-distinct-points X
    â†’ decomposition X

[NB. Predicatively, nontrivial ğ“¤-sup-lattices necessarily have large
     carriers [dJE21,dJE22], so that the simpler lemma

     WEM-gives-decomposition-of-two-pointed-types :
         WEM ğ“¤
       â†’ (X : ğ“¤ Ì‡ )
       â†’ has-two-distinct-points X
       â†’ decomposition X

     is not sufficient.]

For the other we use

  decomposition-of-type-with-Î©-paths-gives-WEM :
      {X : ğ“¤ Ì‡ }
    â†’ decomposition X
    â†’ has-Î©-paths ğ“¥ X
    â†’ WEM ğ“¥

The point is that every ğ“¤-sup-lattice X has Î©ğ“¤-paths, because given x y : X, we
can define f : Î© ğ“¤ â†’ X by mapping a proposition P to the join of the family

  Î´ : ğŸ™ + P â†’ X
  Î´(inl â‹†) = x;
  Î´(inr p) = y.

The family Î´ also plays a key role in [dJE21,dJE22] although we have the
restriction that x âŠ‘ y in those papers, because we consider a broader collection
of posets there that includes the ğ“¤-sup-lattices, but also ğ“¤-bounded-complete
posets and ğ“¤-directed complete posets.

References
----------

[dJE21] Tom de Jong and MartÃ­n HÃ¶tzel EscardÃ³.
        â€˜Predicative Aspects of Order Theory in Univalent Foundationsâ€™.
        In: 6th International Conference on Formal Structures for Computation and
        Deduction (FSCD 2021). Ed. by Naoki Kobayashi. Vol. 195.
        Leibniz International Proceedings in Informatics (LIPIcs).
        Schloss Dagstuhlâ€“Leibniz-Zentrum fÃ¼r Informatik, 2021, 8:1â€“8:18.
        doi: 10.4230/LIPIcs.FSCD.2021.8.
[dJE22] Tom de Jong and MartÃ­n HÃ¶tzel EscardÃ³.
        â€˜On Small Types in Univalent Foundationsâ€™. Sept. 2022.
        arXiv: 2111.00482 [cs.LO]. Revised and expanded version of [dJE21b].
        Accepted pending minor revision to a special issue of Logical Methods in
        Computer Science on selected papers from FSCD 2021.
