Martin Escardo, August 2018

Various maps of ordinals, including equivalences.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.Univalence

module Ordinals.Maps where

open import MLTT.Spartan
open import Notation.CanonicalMap
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Yoneda

\end{code}

Maps of ordinals. A simulation gives a notion of embedding of
ordinals, making them into a poset, as proved below.

\begin{code}

is-order-preserving
 is-monotone
 is-order-reflecting
 is-order-embedding
 is-initial-segment
 is-simulation       : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡

is-order-preserving Î± Î² f = (x y : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© y â†’ f x â‰ºâŸ¨ Î² âŸ© f y

is-monotone         Î± Î² f = (x y : âŸ¨ Î± âŸ©) â†’ x â‰¼âŸ¨ Î± âŸ© y â†’ f x â‰¼âŸ¨ Î² âŸ© f y

is-order-reflecting Î± Î² f = (x y : âŸ¨ Î± âŸ©) â†’ f x â‰ºâŸ¨ Î² âŸ© f y â†’ x â‰ºâŸ¨ Î± âŸ© y

is-order-embedding  Î± Î² f = is-order-preserving Î± Î² f Ã— is-order-reflecting Î± Î² f

is-initial-segment  Î± Î² f = (x : âŸ¨ Î± âŸ©) (y : âŸ¨ Î² âŸ©)
                          â†’ y â‰ºâŸ¨ Î² âŸ© f x
                          â†’ Î£ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y)

is-simulation       Î± Î² f = is-initial-segment Î± Î² f Ã— is-order-preserving Î± Î² f

simulations-are-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                 â†’ is-simulation Î± Î² f
                                 â†’ is-order-preserving Î± Î² f
simulations-are-order-preserving Î± Î² f (i , p) = p

simulations-are-initial-segments : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                 â†’ is-simulation Î± Î² f
                                 â†’ is-initial-segment Î± Î² f
simulations-are-initial-segments Î± Î² f (i , p) = i

being-order-preserving-is-prop : Fun-Ext
                               â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                 (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                               â†’ is-prop (is-order-preserving Î± Î² f)
being-order-preserving-is-prop fe Î± Î² f =
 Î â‚ƒ-is-prop fe (Î» x y l â†’ Prop-valuedness Î² (f x) (f y))

being-order-reflecting-is-prop : Fun-Ext
                               â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                 (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                               â†’ is-prop (is-order-reflecting Î± Î² f)
being-order-reflecting-is-prop fe Î± Î² f =
  Î â‚ƒ-is-prop fe (Î» x y l â†’ Prop-valuedness Î± x y)

being-order-embedding-is-prop : Fun-Ext
                              â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                              â†’ is-prop (is-order-embedding Î± Î² f)
being-order-embedding-is-prop fe Î± Î² f =
 Ã—-is-prop
  (being-order-preserving-is-prop fe Î± Î² f)
  (being-order-reflecting-is-prop fe Î± Î² f)

order-reflecting-gives-inverse-order-preserving :
   (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
 â†’ (e : is-equiv f)
 â†’ is-order-reflecting Î± Î² f
 â†’ is-order-preserving Î² Î± (inverse f e)
order-reflecting-gives-inverse-order-preserving Î± Î² f e r x y l = m
 where
  g : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  g = inverse f e

  l' : f (g x) â‰ºâŸ¨ Î² âŸ© f (g y)
  l' = transportâ‚‚ (Î» x y â†’ x â‰ºâŸ¨ Î² âŸ© y)
        ((inverses-are-sections f e x)â»Â¹)
        ((inverses-are-sections f e y)â»Â¹) l

  s : f (g x) â‰ºâŸ¨ Î² âŸ© f (g y) â†’ g x â‰ºâŸ¨ Î± âŸ© g y
  s = r (g x) (g y)

  m : g x â‰ºâŸ¨ Î± âŸ© g y
  m = s l'

inverse-order-reflecting-gives-order-preserving :
   (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
   (e : is-equiv f)
 â†’ is-order-preserving Î² Î± (inverse f e)
 â†’ is-order-reflecting Î± Î² f
inverse-order-reflecting-gives-order-preserving Î± Î² f e q x y l = r
 where
  g : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  g = inverse f e

  s : g (f x) â‰ºâŸ¨ Î± âŸ© g (f y)
  s = q (f x) (f y) l

  r : x â‰ºâŸ¨ Î± âŸ© y
  r = transportâ‚‚ (Î» x y â†’ x â‰ºâŸ¨ Î± âŸ© y)
       (inverses-are-retractions f e x)
       (inverses-are-retractions f e y) s

simulations-are-lc : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                     (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                   â†’ is-simulation Î± Î² f
                   â†’ left-cancellable f
simulations-are-lc Î± Î² f (i , p) = Î³
 where
  Ï† : âˆ€ x y
    â†’ is-accessible (underlying-order Î±) x
    â†’ is-accessible (underlying-order Î±) y
    â†’ f x ï¼ f y
    â†’ x ï¼ y
  Ï† x y (acc s) (acc t) r = Extensionality Î± x y g h
   where
    g : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© x â†’ u â‰ºâŸ¨ Î± âŸ© y
    g u l = d
     where
      a : f u â‰ºâŸ¨ Î² âŸ© f y
      a = transport (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) r (p u x l)

      b : Î£ v ê‰ âŸ¨ Î± âŸ© , (v â‰ºâŸ¨ Î± âŸ© y) Ã— (f v ï¼ f u)
      b = i y (f u) a

      c : u ï¼ prâ‚ b
      c = Ï† u (prâ‚ b) (s u l) (t (prâ‚ b) (prâ‚ (prâ‚‚ b))) ((prâ‚‚ (prâ‚‚ b))â»Â¹)

      d : u â‰ºâŸ¨ Î± âŸ© y
      d = transport (Î» - â†’ - â‰ºâŸ¨ Î± âŸ© y) (c â»Â¹) (prâ‚ (prâ‚‚ b))

    h : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© y â†’ u â‰ºâŸ¨ Î± âŸ© x
    h u l = d
     where
      a : f u â‰ºâŸ¨ Î² âŸ© f x
      a = transport (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) (r â»Â¹) (p u y l)

      b : Î£ v ê‰ âŸ¨ Î± âŸ© , (v â‰ºâŸ¨ Î± âŸ© x) Ã— (f v ï¼ f u)
      b = i x (f u) a

      c : prâ‚ b ï¼ u
      c = Ï† (prâ‚ b) u (s (prâ‚ b) (prâ‚ (prâ‚‚ b))) (t u l) (prâ‚‚ (prâ‚‚ b))

      d : u â‰ºâŸ¨ Î± âŸ© x
      d = transport (Î» - â†’ - â‰ºâŸ¨ Î± âŸ© x) c (prâ‚ (prâ‚‚ b))

  Î³ : left-cancellable f
  Î³ {x} {y} = Ï† x y (Well-foundedness Î± x) (Well-foundedness Î± y)

simulations-are-embeddings : FunExt
                           â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                             (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                           â†’ is-simulation Î± Î² f
                           â†’ is-embedding f
simulations-are-embeddings fe Î± Î² f s = lc-maps-into-sets-are-embeddings f
                                         (simulations-are-lc Î± Î² f s)
                                         (underlying-type-is-set fe Î²)

being-initial-segment-is-prop : Fun-Ext
                              â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                              â†’ is-order-preserving Î± Î² f
                              â†’ is-prop (is-initial-segment Î± Î² f)
being-initial-segment-is-prop fe Î± Î² f p = prop-criterion Î³
  where
   Î³ : is-initial-segment Î± Î² f â†’ is-prop (is-initial-segment Î± Î² f)
   Î³ i = Î â‚ƒ-is-prop fe (Î» x z l â†’ Ï† x z l)
    where
     Ï† : âˆ€ x y
       â†’ y â‰ºâŸ¨ Î² âŸ© f x
       â†’ is-prop (Î£ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y))
     Ï† x y l (x' , (m , r)) (x'' , (m' , r')) = to-Î£-ï¼ (a , b)
      where
       c : f x' ï¼ f x''
       c = r âˆ™ r' â»Â¹

       j : is-simulation Î± Î² f
       j = (i , p)

       a : x' ï¼ x''
       a = simulations-are-lc Î± Î² f j c

       k : is-prop ((x'' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x'' ï¼ y))
       k = Ã—-is-prop
            (Prop-valuedness Î± x'' x)
            (underlying-type-is-set (Î» _ _ â†’ fe) Î²)

       b : transport (Î» - â†’  (- â‰ºâŸ¨ Î± âŸ© x) Ã— (f - ï¼ y)) a (m , r) ï¼ m' , r'
       b = k _ _

being-simulation-is-prop : Fun-Ext
                         â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                           (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                         â†’ is-prop (is-simulation Î± Î² f)
being-simulation-is-prop fe Î± Î² f =
 Ã—-prop-criterion
  (being-initial-segment-is-prop fe Î± Î² f ,
   (Î» _ â†’ being-order-preserving-is-prop fe Î± Î² f))

lc-initial-segments-are-order-reflecting : (Î± Î² : Ordinal ğ“¤)
                                           (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                         â†’ is-initial-segment Î± Î² f
                                         â†’ left-cancellable f
                                         â†’ is-order-reflecting Î± Î² f
lc-initial-segments-are-order-reflecting Î± Î² f i c x y l = m
 where
  a : Î£ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© y) Ã— (f x' ï¼ f x)
  a = i y (f x) l

  m : x â‰ºâŸ¨ Î± âŸ© y
  m = transport (Î» - â†’ - â‰ºâŸ¨ Î± âŸ© y) (c (prâ‚‚ (prâ‚‚ a))) (prâ‚ (prâ‚‚ a))

simulations-are-order-reflecting : (Î± Î² : Ordinal ğ“¤)
                                   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                 â†’ is-simulation Î± Î² f
                                 â†’ is-order-reflecting Î± Î² f
simulations-are-order-reflecting Î± Î² f (i , p) =
 lc-initial-segments-are-order-reflecting Î± Î² f i
  (simulations-are-lc Î± Î² f (i , p))

order-embeddings-are-lc : (Î± Î² : Ordinal ğ“¤) (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                        â†’ is-order-embedding Î± Î² f
                        â†’ left-cancellable f
order-embeddings-are-lc Î± Î² f (p , r) {x} {y} s = Î³
 where
  a : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© x â†’ u â‰ºâŸ¨ Î± âŸ© y
  a u l = r u y j
   where
    i : f u â‰ºâŸ¨ Î² âŸ© f x
    i = p u x l

    j : f u â‰ºâŸ¨ Î² âŸ© f y
    j = transport (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) s i

  b : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© y â†’ u â‰ºâŸ¨ Î± âŸ© x
  b u l = r u x j
   where
    i : f u â‰ºâŸ¨ Î² âŸ© f y
    i = p u y l

    j : f u â‰ºâŸ¨ Î² âŸ© f x

    j = transportâ»Â¹ (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) s i


  Î³ : x ï¼ y
  Î³ = Extensionality Î± x y a b

order-embedings-are-embeddings : FunExt
                               â†’ (Î± Î² : Ordinal ğ“¤)
                                 (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                               â†’ is-order-embedding Î± Î² f
                               â†’ is-embedding f
order-embedings-are-embeddings fe Î± Î² f (p , r) =
  lc-maps-into-sets-are-embeddings f
   (order-embeddings-are-lc Î± Î² f (p , r))
   (underlying-type-is-set fe Î²)

simulations-are-monotone : (Î± Î² : Ordinal ğ“¤)
                           (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                         â†’ is-simulation Î± Î² f
                         â†’ is-monotone Î± Î² f
simulations-are-monotone Î± Î² f (i , p) = Ï†
 where
  Ï† : (x y : âŸ¨ Î± âŸ©)
    â†’ ((z : âŸ¨ Î± âŸ©) â†’ z â‰ºâŸ¨ Î± âŸ© x â†’ z â‰ºâŸ¨ Î± âŸ© y)
    â†’ (t : âŸ¨ Î² âŸ©) â†’ t â‰ºâŸ¨ Î² âŸ© f x â†’ t â‰ºâŸ¨ Î² âŸ© f y
  Ï† x y Ïˆ t l = transport (Î» - â†’ - â‰ºâŸ¨ Î² âŸ© f y) b d
   where
    z : âŸ¨ Î± âŸ©
    z = prâ‚ (i x t l)

    a : z â‰ºâŸ¨ Î± âŸ© x
    a = prâ‚ (prâ‚‚ (i x t l))

    b : f z ï¼ t
    b = prâ‚‚ (prâ‚‚ (i x t l))

    c : z â‰ºâŸ¨ Î± âŸ© y
    c = Ïˆ z a

    d : f z â‰ºâŸ¨ Î² âŸ© f y
    d = p z y c

at-most-one-simulation : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                         (f f' : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                       â†’ is-simulation Î± Î² f
                       â†’ is-simulation Î± Î² f'
                       â†’ f âˆ¼ f'
at-most-one-simulation Î± Î² f f' (i , p) (i' , p') x = Î³
 where
  Ï† : âˆ€ x
    â†’ is-accessible (underlying-order Î±) x
    â†’ f x ï¼ f' x
  Ï† x (acc u) = Extensionality Î² (f x) (f' x) a b
   where
    IH : âˆ€ y â†’ y â‰ºâŸ¨ Î± âŸ© x â†’ f y ï¼ f' y
    IH y l = Ï† y (u y l)

    a : (z : âŸ¨ Î² âŸ©) â†’ z â‰ºâŸ¨ Î² âŸ© f x â†’ z â‰ºâŸ¨ Î² âŸ© f' x
    a z l = transport (Î» - â†’ - â‰ºâŸ¨ Î² âŸ© f' x) t m
     where
      s : Î£ y ê‰ âŸ¨ Î± âŸ© , (y â‰ºâŸ¨ Î± âŸ© x) Ã— (f y ï¼ z)
      s = i x z l

      y : âŸ¨ Î± âŸ©
      y = prâ‚ s

      m : f' y â‰ºâŸ¨ Î² âŸ© f' x
      m = p' y x (prâ‚ (prâ‚‚ s))

      t : f' y ï¼ z
      t = f' y  ï¼âŸ¨ (IH y (prâ‚ (prâ‚‚ s)))â»Â¹ âŸ©
          f y   ï¼âŸ¨ prâ‚‚ (prâ‚‚ s) âŸ©
          z     âˆ

    b : (z : âŸ¨ Î² âŸ©) â†’ z â‰ºâŸ¨ Î² âŸ© f' x â†’ z â‰ºâŸ¨ Î² âŸ© f x
    b z l = transport (Î» - â†’ - â‰ºâŸ¨ Î² âŸ© f x) t m
     where
      s : Î£ y ê‰ âŸ¨ Î± âŸ© , (y â‰ºâŸ¨ Î± âŸ© x) Ã— (f' y ï¼ z)
      s = i' x z l

      y : âŸ¨ Î± âŸ©
      y = prâ‚ s

      m : f y â‰ºâŸ¨ Î² âŸ© f x
      m = p y x (prâ‚ (prâ‚‚ s))

      t : f y ï¼ z
      t = f y  ï¼âŸ¨ IH y (prâ‚ (prâ‚‚ s)) âŸ©
          f' y ï¼âŸ¨ prâ‚‚ (prâ‚‚ s) âŸ©
          z    âˆ

  Î³ : f x ï¼ f' x
  Î³ = Ï† x (Well-foundedness Î± x)

\end{code}

Added 29th March 2022.

Simulations preserve least elements.

\begin{code}

initial-segments-preserve-least :
   (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
   (x : âŸ¨ Î± âŸ©) (y : âŸ¨ Î² âŸ©)
   (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
 â†’ is-initial-segment Î± Î² f
 â†’ is-least Î± x
 â†’ is-least Î² y
 â†’ f x ï¼ y
initial-segments-preserve-least Î± Î² x y f i m n = c
 where
  a : f x â‰¼âŸ¨ Î² âŸ© y
  a u l = IV
   where
    x' : âŸ¨ Î± âŸ©
    x' = prâ‚ (i x u l)

    I : x' â‰ºâŸ¨ Î± âŸ© x
    I = prâ‚ (prâ‚‚ (i x u l))

    II : x â‰¼âŸ¨ Î± âŸ© x'
    II = m x'

    III : x' â‰ºâŸ¨ Î± âŸ© x'
    III = II x' I

    IV : u â‰ºâŸ¨ Î² âŸ© y
    IV = ğŸ˜-elim (irrefl Î± x' III)

  b : y â‰¼âŸ¨ Î² âŸ© f x
  b = n (f x)

  c : f x ï¼ y
  c = Antisymmetry Î² (f x) y a b

simulations-preserve-least : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                             (x : âŸ¨ Î± âŸ©) (y : âŸ¨ Î² âŸ©)
                             (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                           â†’ is-simulation Î± Î² f
                           â†’ is-least Î± x
                           â†’ is-least Î² y
                           â†’ f x ï¼ y
simulations-preserve-least Î± Î² x y f (i , _) =
 initial-segments-preserve-least Î± Î² x y f i

\end{code}

Added in March 2022 by Tom de Jong:

Notice that we defined "is-initial-segment" using Î£ (rather than âˆƒ).
This is fine, because if f is a simulation from Î± to Î², then for
every x : âŸ¨ Î± âŸ© and y : âŸ¨ Î² âŸ© with y â‰ºâŸ¨ Î² âŸ© f x, the type
(Î£ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y)) is a proposition. It
follows (see the proof above) that being a simulation is property.

However, for some purposes, notably for constructing suprema of
ordinals in OrdinalSupOfOrdinals.lagda, it is useful to formulate the
notion of initial segment and the notion of simulation using âˆƒ, rather
than Î£.

Using the techniques that were used above to prove that being a simulation is
property, we show the definition of simulation with âˆƒ to be equivalent to the
original one.

\begin{code}

open import UF.PropTrunc

module _ (pt : propositional-truncations-exist)
         (fe : FunExt)
       where

 open PropositionalTruncation pt

 is-initial-segment' : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-initial-segment' Î± Î² f = (x : âŸ¨ Î± âŸ©) (y : âŸ¨ Î² âŸ©)
                           â†’ y â‰ºâŸ¨ Î² âŸ© f x
                           â†’ âˆƒ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y)

 is-simulation' : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-simulation' Î± Î² f = is-initial-segment' Î± Î² f Ã— is-order-preserving Î± Î² f

 simulations-are-lc' : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                       (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                     â†’ is-simulation' Î± Î² f
                     â†’ left-cancellable f
 simulations-are-lc' Î± Î² f (i , p) = Î³
  where
   Ï† : âˆ€ x y
     â†’ is-accessible (underlying-order Î±) x
     â†’ is-accessible (underlying-order Î±) y
     â†’ f x ï¼ f y
     â†’ x ï¼ y
   Ï† x y (acc s) (acc t) r = Extensionality Î± x y g h
    where
     g : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© x â†’ u â‰ºâŸ¨ Î± âŸ© y
     g u l = âˆ¥âˆ¥-rec (Prop-valuedness Î± u y) b (i y (f u) a)
      where
       a : f u â‰ºâŸ¨ Î² âŸ© f y
       a = transport (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) r (p u x l)

       b : (Î£ v ê‰ âŸ¨ Î± âŸ© , (v â‰ºâŸ¨ Î± âŸ© y) Ã— (f v ï¼ f u))
         â†’ u â‰ºâŸ¨ Î± âŸ© y
       b (v , k , e) = transport (Î» - â†’ - â‰ºâŸ¨Â Î± âŸ© y) (c â»Â¹) k
        where
         c : u ï¼ v
         c = Ï† u v (s u l) (t v k) (e â»Â¹)

     h : (u : âŸ¨ Î± âŸ©) â†’ u â‰ºâŸ¨ Î± âŸ© y â†’ u â‰ºâŸ¨ Î± âŸ© x
     h u l = âˆ¥âˆ¥-rec (Prop-valuedness Î± u x) b (i x (f u) a)
      where
       a : f u â‰ºâŸ¨ Î² âŸ© f x
       a = transport (Î» - â†’ f u â‰ºâŸ¨ Î² âŸ© -) (r â»Â¹) (p u y l)

       b : (Î£ v ê‰ âŸ¨ Î± âŸ© , (v â‰ºâŸ¨ Î± âŸ© x) Ã— (f v ï¼ f u))
         â†’ u â‰ºâŸ¨ Î± âŸ© x
       b (v , k , e) = transport (Î» - â†’ - â‰ºâŸ¨ Î± âŸ© x) c k
        where
         c : v ï¼ u
         c = Ï† v u (s v k) (t u l) e

   Î³ : left-cancellable f
   Î³ {x} {y} = Ï† x y (Well-foundedness Î± x) (Well-foundedness Î± y)

 simulation-prime : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                    (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                  â†’ is-simulation Î± Î² f
                  â†’ is-simulation' Î± Î² f
 simulation-prime Î± Î² f (i , p) = (j , p)
  where
   j : is-initial-segment' Î± Î² f
   j x y l = âˆ£ i x y l âˆ£

 simulation-unprime : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                      (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                    â†’ is-simulation' Î± Î² f
                    â†’ is-simulation Î± Î² f
 simulation-unprime Î± Î² f (i , p) = (j , p)
  where
   j : is-initial-segment Î± Î² f
   j x y l = âˆ¥âˆ¥-rec prp id (i x y l)
    where
     prp : is-prop (Î£ x' ê‰ âŸ¨ Î± âŸ© , (x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y))
     prp (z , l , e) (z' , l' , e') = to-subtype-ï¼ â¦…1â¦† â¦…2â¦†
      where
       â¦…1â¦† : (x' : âŸ¨ Î± âŸ©) â†’ is-prop ((x' â‰ºâŸ¨ Î± âŸ© x) Ã— (f x' ï¼ y))
       â¦…1â¦† x' = Ã—-is-prop (Prop-valuedness Î± x' x) (underlying-type-is-set fe Î²)

       â¦…2â¦† : z ï¼ z'
       â¦…2â¦† = simulations-are-lc' Î± Î² f (i , p) (e âˆ™ e' â»Â¹)
\end{code}
