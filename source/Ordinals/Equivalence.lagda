Martin Escardo, 29 June 2018, 26th February 2023

Equivalence of ordinals.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import MLTT.Spartan
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.PreSIP-Examples
open import UF.PreUnivalence
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Univalence

module Ordinals.Equivalence where

\end{code}

Characterization of equality of ordinals using the structure identity
principle:

\begin{code}

Ordinal-ï¼ : FunExt
          â†’ is-univalent ğ“¤
          â†’ (Î± Î² : Ordinal ğ“¤)
          â†’ (Î± ï¼ Î²)
          â‰ƒ (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                 , is-equiv f
                 Ã— ((Î» x x' â†’ x â‰ºâŸ¨ Î± âŸ© x') ï¼ (Î» x x' â†’ f x â‰ºâŸ¨ Î² âŸ© f x')))
Ordinal-ï¼ {ğ“¤} fe = generalized-metric-space.characterization-of-M-ï¼ (ğ“¤ Ì‡ )
                     (Î» _ â†’ is-well-order)
                     (Î» X _<_ â†’ being-well-order-is-prop _<_ fe)
 where
  open import UF.SIP-Examples

\end{code}

Often it is convenient to work with the following alternative notion _â‰ƒâ‚’_
of ordinal equivalence, which we take as the official one:

\begin{code}

is-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                    â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-order-preserving Î± Î² f = (x y : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© y â†’ f x â‰ºâŸ¨ Î² âŸ© f y

being-order-preserving-is-prop : Fun-Ext
                               â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                 (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                               â†’ is-prop (is-order-preserving Î± Î² f)
being-order-preserving-is-prop fe Î± Î² f =
 Î â‚ƒ-is-prop fe (Î» x y l â†’ Prop-valuedness Î² (f x) (f y))

is-order-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-order-equiv Î± Î² f = is-order-preserving Î± Î² f
                     Ã— (Î£ e ê‰ is-equiv f , is-order-preserving Î² Î± (inverse f e))

order-equivs-are-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                    {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                  â†’ is-order-equiv Î± Î² f
                                  â†’ is-order-preserving Î± Î² f
order-equivs-are-order-preserving Î± Î² = prâ‚


order-equivs-are-equivs : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                          {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                        â†’ (i : is-order-equiv Î± Î² f)
                        â†’ is-equiv f
order-equivs-are-equivs Î± Î² = prâ‚ âˆ˜ prâ‚‚

_â‰ƒâ‚’_ : Ordinal ğ“¤ â†’ Ordinal ğ“¥ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î± â‰ƒâ‚’ Î² = Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) , is-order-equiv Î± Î² f

\end{code}

See the module OrdinalOfOrdinals for a proof that Î± â‰ƒâ‚’ Î² is
canonically equivalent to Î± ï¼ Î². (For historical reasons, that proof
doesn't use the structure identity principle.)

\begin{code}

â‰ƒâ‚’-refl : (Î± : Ordinal ğ“¤) â†’ Î± â‰ƒâ‚’ Î±
â‰ƒâ‚’-refl Î± = id , (Î» x y â†’ id) , id-is-equiv âŸ¨ Î± âŸ© , (Î» x y â†’ id)

â‰ƒâ‚’-sym : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥ )
       â†’ Î± â‰ƒâ‚’ Î² â†’ Î² â‰ƒâ‚’ Î±
â‰ƒâ‚’-sym Î± Î² (f , p , e , q) = inverse f e , q , inverses-are-equivs f e , p

â‰ƒâ‚’-trans : âˆ€ {ğ“¤} {ğ“¥} {ğ“¦} (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥ ) (Î³ : Ordinal ğ“¦)
         â†’ Î± â‰ƒâ‚’ Î² â†’ Î² â‰ƒâ‚’ Î³ â†’ Î± â‰ƒâ‚’ Î³
â‰ƒâ‚’-trans Î± Î² Î³ (f , p , e , q) (f' , p' , e' , q') =
  f' âˆ˜ f ,
  (Î» x y l â†’ p' (f x) (f y) (p x y l)) ,
  âˆ˜-is-equiv e e' ,
  (Î» x y l â†’ q (inverse f' e' x) (inverse f' e' y) (q' x y l))

â‰ƒâ‚’-to-fun : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ Î± â‰ƒâ‚’ Î² â†’ âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©
â‰ƒâ‚’-to-fun Î± Î² = prâ‚

â‰ƒâ‚’-to-fun-is-order-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                         â†’ is-order-equiv Î± Î² (â‰ƒâ‚’-to-fun Î± Î² e)
â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² = prâ‚‚

â‰ƒâ‚’-to-fun-is-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                   â†’ is-equiv (â‰ƒâ‚’-to-fun Î± Î² e)
â‰ƒâ‚’-to-fun-is-equiv Î± Î² e = order-equivs-are-equivs Î± Î² (â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² e)

inverses-of-order-equivs-are-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                              â†’ (i : is-order-equiv Î± Î² f)
                                              â†’ is-order-preserving Î² Î±
                                                  (inverse f (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-preserving Î± Î² = prâ‚‚ âˆ˜ prâ‚‚

is-order-reflecting : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                    â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-order-reflecting Î± Î² f = (x y : âŸ¨ Î± âŸ©) â†’ f x â‰ºâŸ¨ Î² âŸ© f y â†’ x â‰ºâŸ¨ Î± âŸ© y

being-order-reflecting-is-prop : Fun-Ext
                               â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                 (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                               â†’ is-prop (is-order-reflecting Î± Î² f)
being-order-reflecting-is-prop fe Î± Î² f =
 Î â‚ƒ-is-prop fe (Î» x y l â†’ Prop-valuedness Î± x y)

order-reflecting-gives-inverse-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                  (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                                â†’ (e : is-equiv f)
                                                â†’ is-order-reflecting Î± Î² f
                                                â†’ is-order-preserving Î² Î± (inverse f e)
order-reflecting-gives-inverse-order-preserving Î± Î² f e r x y l = m
 where
  g : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  g = inverse f e

  l' : f (g x) â‰ºâŸ¨ Î² âŸ© f (g y)
  l' = transportâ‚‚ (Î» x y â†’ x â‰ºâŸ¨ Î² âŸ© y)
        ((inverses-are-sections f e x)â»Â¹)
        ((inverses-are-sections f e y)â»Â¹) l

  s : f (g x) â‰ºâŸ¨ Î² âŸ© f (g y) â†’ g x â‰ºâŸ¨ Î± âŸ© g y
  s = r (g x) (g y)

  m : g x â‰ºâŸ¨ Î± âŸ© g y
  m = s l'

inverse-order-reflecting-gives-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                  (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                                  (e : is-equiv f)
                                                â†’ is-order-preserving Î² Î± (inverse f e)
                                                â†’ is-order-reflecting Î± Î² f
inverse-order-reflecting-gives-order-preserving Î± Î² f e q x y l = r
 where
  g : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  g = inverse f e

  s : g (f x) â‰ºâŸ¨ Î± âŸ© g (f y)
  s = q (f x) (f y) l

  r : x â‰ºâŸ¨ Î± âŸ© y
  r = transportâ‚‚ (Î» x y â†’ x â‰ºâŸ¨ Î± âŸ© y)
       (inverses-are-retractions f e x)
       (inverses-are-retractions f e y) s

order-preserving-reflecting-equivs-are-order-equivs : (Î± : Ordinal ğ“¤)
                                                      (Î² : Ordinal ğ“¥)
                                                      (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                                    â†’ is-equiv f
                                                    â†’ is-order-preserving Î± Î² f
                                                    â†’ is-order-reflecting Î± Î² f
                                                    â†’ is-order-equiv Î± Î² f
order-preserving-reflecting-equivs-are-order-equivs Î± Î² f e p r =
 p , e , order-reflecting-gives-inverse-order-preserving Î± Î² f e r


order-equivs-are-order-reflecting : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                    (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                  â†’ is-order-equiv Î± Î² f
                                  â†’ is-order-reflecting Î± Î² f
order-equivs-are-order-reflecting Î± Î² f (_ , e , q) =
 inverse-order-reflecting-gives-order-preserving Î± Î² f e q

inverses-of-order-equivs-are-order-reflecting : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                              â†’ (i : is-order-equiv Î± Î² f)
                                              â†’ is-order-reflecting Î² Î±
                                                  (inverse f (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-reflecting Î± Î² {f} (p , e , q) =
 order-equivs-are-order-reflecting Î² Î±
  (inverse f e)
  (q , inverses-are-equivs f e , p)

inverses-of-order-equivs-are-order-equivs : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                            {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                          â†’ (i : is-order-equiv Î± Î² f)
                                          â†’ is-order-equiv Î² Î±
                                              (inverse f (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-equivs Î± Î² {f} (p , e , q) =
 (q , inverses-are-equivs f e , p)

â‰ƒâ‚’-to-funâ»Â¹ : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ Î± â‰ƒâ‚’ Î² â†’ âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
â‰ƒâ‚’-to-funâ»Â¹ Î± Î² e = inverse (â‰ƒâ‚’-to-fun Î± Î² e)
                      (order-equivs-are-equivs Î± Î²
                        (â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² e))

â‰ƒâ‚’-to-funâ»Â¹-is-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                     â†’ is-equiv (â‰ƒâ‚’-to-funâ»Â¹ Î± Î² e)
â‰ƒâ‚’-to-funâ»Â¹-is-equiv Î± Î² e = inverses-are-equivs (â‰ƒâ‚’-to-fun Î± Î² e)
                                (â‰ƒâ‚’-to-fun-is-equiv Î± Î² e)

order-equivs-preserve-largest : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                              â†’ (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                              â†’ is-order-equiv Î± Î² f
                              â†’ (x : âŸ¨ Î± âŸ©)
                              â†’ is-largest Î± x
                              â†’ is-largest Î² (f x)
order-equivs-preserve-largest Î± Î² f (o , e , p) x â„“ = Î´
 where
  fâ»Â¹ : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  fâ»Â¹ = inverse f e

  Î´ : (y : âŸ¨ Î² âŸ©) â†’ y â‰¼âŸ¨ Î² âŸ© f x
  Î´ y t l = IV
   where
    I : fâ»Â¹ t â‰ºâŸ¨ Î± âŸ© fâ»Â¹ y
    I = p t y l

    II : fâ»Â¹ t â‰ºâŸ¨ Î± âŸ© x
    II = â„“ (fâ»Â¹ y) (fâ»Â¹ t) I

    III : f (fâ»Â¹ t) â‰ºâŸ¨ Î² âŸ© f x
    III = o (fâ»Â¹ t) x II

    IV : t â‰ºâŸ¨ Î² âŸ© f x
    IV = transport (Î» - â†’ - â‰ºâŸ¨ Î² âŸ© f x) (inverses-are-sections f e t) III

\end{code}

Added 25th Feb 2023. Alternative definition of ordinal equivalence

\begin{code}

_â‰ƒâ‚_ : Ordinal ğ“¤ â†’ Ordinal ğ“¥ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î± â‰ƒâ‚ Î² = Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
             , is-equiv f
             Ã— ((x x' : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© x' â‡” f x â‰ºâŸ¨ Î² âŸ© f x')

â‰ƒâ‚-coincides-with-â‰ƒâ‚’ : FunExt
                     â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                     â†’ (Î± â‰ƒâ‚ Î²) â‰ƒ (Î± â‰ƒâ‚’ Î²)
â‰ƒâ‚-coincides-with-â‰ƒâ‚’ fe Î± Î² =
 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , is-equiv f
      Ã— ((x x' : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© x' â‡” f x â‰ºâŸ¨ Î² âŸ© f x'))     â‰ƒâŸ¨ I âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , is-equiv f
      Ã— (is-order-preserving Î± Î² f)
      Ã— (is-order-reflecting Î± Î² f))                          â‰ƒâŸ¨ II âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , (Î£ e ê‰ is-equiv f
             , (is-order-preserving Î± Î² f)
             Ã— (is-order-preserving Î² Î± (inverse f e))))      â‰ƒâŸ¨ III âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , (is-order-preserving Î± Î² f)
      Ã— (Î£ e ê‰ is-equiv f
             , (is-order-preserving Î² Î± (inverse f e))))      â– 
  where
   I  = Î£-cong (Î» f â†’ Ã—-cong (â‰ƒ-refl _) Î Ã—-distrâ‚‚)
   II = Î£-cong (Î» f â†’ Î£-cong (Î» e â†’ Ã—-cong (â‰ƒ-refl _) (b f e)))
    where
     fe' = FunExt-to-Fun-Ext fe
     b = Î» f e â†’ logically-equivalent-props-are-equivalent
                  (being-order-reflecting-is-prop fe' Î± Î² f)
                  (being-order-preserving-is-prop fe' Î² Î± (inverse f e))
                  (order-reflecting-gives-inverse-order-preserving Î± Î² f e)
                  (inverse-order-reflecting-gives-order-preserving Î± Î² f e)
   III = Î£-cong (Î» f â†’ Î£-flip)

\end{code}

If we only assume preunivalence, meaning that idtoeq is an embedding
(rather than an equivalence), which is implied by each of univalence
and the K axiom, we get that idtoeqâ‚’ is an embedding (rather than an
equivalence). This was suggested to me by Peter Lumbsdaine in August
2022.

\begin{code}

idtoeqâ‚’-is-embedding : is-preunivalent ğ“¤
                     â†’ FunExt
                     â†’ PropExt
                     â†’ (Î± Î² : Ordinal ğ“¤)
                     â†’ (Î± ï¼ Î²) â†ª (Î± â‰ƒâ‚’ Î²)
idtoeqâ‚’-is-embedding {ğ“¤} pua fe pe Î± Î² = II
 where
  open relational-space
        (Î» (X : ğ“¤ Ì‡ ) (_<_ : X â†’ X â†’ ğ“¤ Ì‡) â†’ is-well-order _<_)
        (Î» (X : ğ“¤ Ì‡ ) (_<_ : X â†’ X â†’ ğ“¤ Ì‡) â†’ being-well-order-is-prop _<_ fe)
        (Î» {X R} w {x} {y} â†’ prop-valuedness R w x y)

  I : (Î± ï¼ Î²) â†ª (Î± â‰…â‚‚ Î²)
  I = M-embeddingâ‚‚ pua (fe _ _) (pe _) Î± Î²

  II : (Î± ï¼ Î²) â†ª (Î± â‰ƒâ‚’ Î²)
  II = â‰ƒ-gives-â†ª (â‰ƒâ‚-coincides-with-â‰ƒâ‚’ fe Î± Î²)
     âˆ˜â†ª I

\end{code}
