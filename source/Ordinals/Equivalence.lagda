Martin Escardo, 29 June 2018, 26th February 2023

Equivalence of ordinals.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness --auto-inline #-}

open import MLTT.Spartan
open import Ordinals.Maps
open import Ordinals.Notions
open import Ordinals.Type
open import Ordinals.Underlying
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.PreSIP-Examples
open import UF.PreUnivalence
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Univalence
open import UF.Yoneda

module Ordinals.Equivalence where

\end{code}

Characterization of equality of ordinals using the structure identity
principle:

\begin{code}

Ordinal-ï¼ : FunExt
          â†’ is-univalent ğ“¤
          â†’ (Î± Î² : Ordinal ğ“¤)
          â†’ (Î± ï¼ Î²)
          â‰ƒ (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                 , is-equiv f
                 Ã— ((Î» x x' â†’ x â‰ºâŸ¨ Î± âŸ© x') ï¼ (Î» x x' â†’ f x â‰ºâŸ¨ Î² âŸ© f x')))
Ordinal-ï¼ {ğ“¤} fe = generalized-metric-space.characterization-of-M-ï¼ (ğ“¤ Ì‡ )
                     (Î» _ â†’ is-well-order)
                     (Î» X _<_ â†’ being-well-order-is-prop _<_ fe)
 where
  open import UF.SIP-Examples

\end{code}

Often it is convenient to work with the following alternative notion _â‰ƒâ‚’_
of ordinal equivalence, which we take as the official one:

\begin{code}

is-order-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-order-equiv Î± Î² f = is-order-preserving Î± Î² f
                     Ã— (Î£ e ê‰ is-equiv f , is-order-preserving Î² Î± (inverse f e))

order-equivs-are-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                    {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                  â†’ is-order-equiv Î± Î² f
                                  â†’ is-order-preserving Î± Î² f
order-equivs-are-order-preserving Î± Î² = prâ‚


order-equivs-are-equivs : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                          {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                        â†’ (i : is-order-equiv Î± Î² f)
                        â†’ is-equiv f
order-equivs-are-equivs Î± Î² = prâ‚ âˆ˜ prâ‚‚

inverses-of-order-equivs-are-order-preserving : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                              â†’ (i : is-order-equiv Î± Î² f)
                                              â†’ is-order-preserving Î² Î±
                                                  (inverse f
                                                   (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-preserving Î± Î² = prâ‚‚ âˆ˜ prâ‚‚

being-order-equiv-is-prop : Fun-Ext
                          â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                            (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                          â†’ is-prop (is-order-equiv Î± Î² f)
being-order-equiv-is-prop fe Î± Î² f = Ã—-is-prop
                                      (being-order-preserving-is-prop fe Î± Î² f)
                                      (Î£-is-prop
                                        (being-equiv-is-prop (Î» _ _ â†’ fe) f)
                                        (Î» e â†’ being-order-preserving-is-prop fe Î² Î±
                                                  (inverse f e)))

\end{code}

Our official definition of ordinal equivalence (see below for
equivalent definitions):

\begin{code}

_â‰ƒâ‚’_ : Ordinal ğ“¤ â†’ Ordinal ğ“¥ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î± â‰ƒâ‚’ Î² = Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©) , is-order-equiv Î± Î² f

â‰ƒâ‚’-refl : (Î± : Ordinal ğ“¤) â†’ Î± â‰ƒâ‚’ Î±
â‰ƒâ‚’-refl Î± = id , (Î» x y â†’ id) , id-is-equiv âŸ¨ Î± âŸ© , (Î» x y â†’ id)

idtoeqâ‚’ : (Î± Î² : Ordinal ğ“¤) â†’ Î± ï¼ Î² â†’ Î± â‰ƒâ‚’ Î²
idtoeqâ‚’ Î± Î± refl = â‰ƒâ‚’-refl Î±

eqtoidâ‚’ : is-univalent ğ“¤
        â†’ Fun-Ext
        â†’ (Î± Î² : Ordinal ğ“¤) â†’ Î± â‰ƒâ‚’ Î² â†’ Î± ï¼ Î²
eqtoidâ‚’ {ğ“¤} ua fe Î± Î² (f , p , e , q) = Î³
 where
  abstract
   A : (Y : ğ“¤ Ì‡ ) â†’ âŸ¨ Î± âŸ© â‰ƒ Y â†’ ğ“¤ âº Ì‡
   A Y e = (Ïƒ : OrdinalStructure Y)
         â†’ is-order-preserving Î± (Y , Ïƒ) âŒœ e âŒ
         â†’ is-order-preserving (Y , Ïƒ) Î± âŒœ e âŒâ»Â¹
         â†’ Î± ï¼ (Y , Ïƒ)

   a : A âŸ¨ Î± âŸ© (â‰ƒ-refl âŸ¨ Î± âŸ©)
   a Ïƒ Ï† Ïˆ = g
    where
     b : (x x' : âŸ¨ Î± âŸ©) â†’ (x â‰ºâŸ¨ Î± âŸ© x') ï¼ (x â‰ºâŸ¨ âŸ¨ Î± âŸ© , Ïƒ âŸ© x')
     b x x' = univalence-gives-propext ua
               (Prop-valuedness Î± x x')
               (Prop-valuedness (âŸ¨ Î± âŸ© , Ïƒ) x x')
               (Ï† x x')
               (Ïˆ x x')

     c : underlying-order Î± ï¼ underlying-order (âŸ¨ Î± âŸ© , Ïƒ)
     c = dfunext fe (Î» x â†’ dfunext fe (b x))

     d : structure Î± ï¼ Ïƒ
     d = prâ‚-lc (Î» {_<_} â†’ being-well-order-is-prop _<_ (Î» _ _ â†’ fe)) c

     g : Î± ï¼ (âŸ¨ Î± âŸ© , Ïƒ)
     g = to-Î£-ï¼' d

   Î³ : Î± ï¼ Î²
   Î³ = JEq ua âŸ¨ Î± âŸ© A a âŸ¨ Î² âŸ© (f , e) (structure Î²) p q

\end{code}

For historical reasons, the above proof doesn't use the structure
identity principle.

\begin{code}

â‰ƒâ‚’-sym : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥ )
       â†’ Î± â‰ƒâ‚’ Î² â†’ Î² â‰ƒâ‚’ Î±
â‰ƒâ‚’-sym Î± Î² (f , p , e , q) = inverse f e , q , inverses-are-equivs f e , p

â‰ƒâ‚’-trans : âˆ€ {ğ“¤} {ğ“¥} {ğ“¦} (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥ ) (Î³ : Ordinal ğ“¦)
         â†’ Î± â‰ƒâ‚’ Î² â†’ Î² â‰ƒâ‚’ Î³ â†’ Î± â‰ƒâ‚’ Î³
â‰ƒâ‚’-trans Î± Î² Î³ (f , p , e , q) (f' , p' , e' , q') =
  f' âˆ˜ f ,
  (Î» x y l â†’ p' (f x) (f y) (p x y l)) ,
  âˆ˜-is-equiv e e' ,
  (Î» x y l â†’ q (inverse f' e' x) (inverse f' e' y) (q' x y l))

order-equivs-are-simulations : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                               (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                             â†’ is-order-equiv Î± Î² f
                             â†’ is-simulation Î± Î² f
order-equivs-are-simulations Î± Î² f (p , e , q) = h (equivs-are-qinvs f e) q , p
 where
  h : (d : qinv f)
    â†’ is-order-preserving Î² Î± (prâ‚ d)
    â†’ is-initial-segment Î± Î² f
  h (g , Îµ , Î·) q x y l = g y , r , Î· y
   where
    m : g y â‰ºâŸ¨ Î± âŸ© g (f x)
    m = q y (f x) l

    r : g y â‰ºâŸ¨ Î± âŸ© x
    r = transport (Î» - â†’ g y â‰ºâŸ¨ Î± âŸ© -) (Îµ x) m

â‰ƒâ‚’-to-fun : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ Î± â‰ƒâ‚’ Î² â†’ âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©
â‰ƒâ‚’-to-fun Î± Î² = prâ‚

â‰ƒâ‚’-to-fun-is-order-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                         â†’ is-order-equiv Î± Î² (â‰ƒâ‚’-to-fun Î± Î² e)
â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² = prâ‚‚

â‰ƒâ‚’-to-fun-is-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                   â†’ is-equiv (â‰ƒâ‚’-to-fun Î± Î² e)
â‰ƒâ‚’-to-fun-is-equiv Î± Î² e = order-equivs-are-equivs Î± Î² (â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² e)

order-preserving-reflecting-equivs-are-order-equivs : (Î± : Ordinal ğ“¤)
                                                      (Î² : Ordinal ğ“¥)
                                                      (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                                    â†’ is-equiv f
                                                    â†’ is-order-preserving Î± Î² f
                                                    â†’ is-order-reflecting Î± Î² f
                                                    â†’ is-order-equiv Î± Î² f
order-preserving-reflecting-equivs-are-order-equivs Î± Î² f e p r =
 p , e , order-reflecting-gives-inverse-order-preserving Î± Î² f e r


order-equivs-are-order-reflecting : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                    (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                                  â†’ is-order-equiv Î± Î² f
                                  â†’ is-order-reflecting Î± Î² f
order-equivs-are-order-reflecting Î± Î² f (_ , e , q) =
 inverse-order-reflecting-gives-order-preserving Î± Î² f e q

inverses-of-order-equivs-are-order-reflecting : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                                {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                              â†’ (i : is-order-equiv Î± Î² f)
                                              â†’ is-order-reflecting Î² Î±
                                                  (inverse f (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-reflecting Î± Î² {f} (p , e , q) =
 order-equivs-are-order-reflecting Î² Î±
  (inverse f e)
  (q , inverses-are-equivs f e , p)

inverses-of-order-equivs-are-order-equivs : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                                            {f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©}
                                          â†’ (i : is-order-equiv Î± Î² f)
                                          â†’ is-order-equiv Î² Î±
                                              (inverse f (order-equivs-are-equivs Î± Î² i))
inverses-of-order-equivs-are-order-equivs Î± Î² {f} (p , e , q) =
 (q , inverses-are-equivs f e , p)

â‰ƒâ‚’-to-funâ»Â¹ : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) â†’ Î± â‰ƒâ‚’ Î² â†’ âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
â‰ƒâ‚’-to-funâ»Â¹ Î± Î² e = inverse (â‰ƒâ‚’-to-fun Î± Î² e)
                      (order-equivs-are-equivs Î± Î²
                        (â‰ƒâ‚’-to-fun-is-order-equiv Î± Î² e))

â‰ƒâ‚’-to-funâ»Â¹-is-equiv : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥) (e : Î± â‰ƒâ‚’ Î²)
                     â†’ is-equiv (â‰ƒâ‚’-to-funâ»Â¹ Î± Î² e)
â‰ƒâ‚’-to-funâ»Â¹-is-equiv Î± Î² e = inverses-are-equivs (â‰ƒâ‚’-to-fun Î± Î² e)
                                (â‰ƒâ‚’-to-fun-is-equiv Î± Î² e)

â‰ƒâ‚’-gives-â‰ƒ : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
           â†’ Î± â‰ƒâ‚’ Î² â†’ âŸ¨ Î± âŸ© â‰ƒ âŸ¨ Î² âŸ©
â‰ƒâ‚’-gives-â‰ƒ Î± Î² (f , p , e , q) = (f , e)

â‰ƒâ‚’-is-prop-valued : Fun-Ext
                  â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                  â†’ is-prop (Î± â‰ƒâ‚’ Î²)
â‰ƒâ‚’-is-prop-valued fe Î± Î² (f , p , e , q) (f' , p' , e' , q')  = Î³
  where
   r : f âˆ¼ f'
   r = at-most-one-simulation Î± Î² f f'
        (order-equivs-are-simulations Î± Î² f  (p  , e ,  q ))
        (order-equivs-are-simulations Î± Î² f' (p' , e' , q'))

   Î³ : (f , p , e , q) ï¼ (f' , p' , e' , q')
   Î³ = to-subtype-ï¼
        (being-order-equiv-is-prop fe Î± Î²)
        (dfunext fe r)

UAâ‚’ : is-univalent ğ“¤
    â†’ Fun-Ext
    â†’ (Î± Î² : Ordinal ğ“¤) â†’ is-equiv (idtoeqâ‚’ Î± Î²)
UAâ‚’ {ğ“¤} ua fe Î± = nats-with-sections-are-equivs Î±
                   (idtoeqâ‚’ Î±)
                   (Î» Î² â†’ (eqtoidâ‚’ ua fe Î± Î² , Î· Î²))
 where
  Î· : (Î² : Ordinal ğ“¤) (e : Î± â‰ƒâ‚’ Î²)
    â†’ idtoeqâ‚’ Î± Î² (eqtoidâ‚’ ua fe Î± Î² e) ï¼ e
  Î· Î² e = â‰ƒâ‚’-is-prop-valued fe Î± Î² (idtoeqâ‚’ Î± Î² (eqtoidâ‚’ ua fe Î± Î² e)) e

the-type-of-ordinals-is-a-set : is-univalent ğ“¤
                              â†’ Fun-Ext
                              â†’ is-set (Ordinal ğ“¤)
the-type-of-ordinals-is-a-set ua fe {Î±} {Î²} = equiv-to-prop
                                               (idtoeqâ‚’ Î± Î² , UAâ‚’ ua fe Î± Î²)
                                               (â‰ƒâ‚’-is-prop-valued fe Î± Î²)

UAâ‚’-â‰ƒ : is-univalent ğ“¤
      â†’ Fun-Ext
      â†’ (Î± Î² : Ordinal ğ“¤) â†’ (Î± ï¼ Î²) â‰ƒ (Î± â‰ƒâ‚’ Î²)
UAâ‚’-â‰ƒ ua fe Î± Î² = idtoeqâ‚’ Î± Î² , UAâ‚’ ua fe Î± Î²

the-type-of-ordinals-is-locally-small : is-univalent ğ“¤
                                      â†’ Fun-Ext
                                      â†’ is-locally-small (Ordinal ğ“¤)
the-type-of-ordinals-is-locally-small ua fe Î± Î² =
 (Î± â‰ƒâ‚’ Î²) , â‰ƒ-sym (UAâ‚’-â‰ƒ ua fe Î± Î²)

\end{code}

One of the many applications of the univalence axiom is to manufacture
examples of types that are not sets. Here we have instead used it to
prove that a certain type is a set. But see below for a proof that
uses a weaker assumption.

\begin{code}

order-equivs-preserve-largest : (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                              â†’ (f : âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
                              â†’ is-order-equiv Î± Î² f
                              â†’ (x : âŸ¨ Î± âŸ©)
                              â†’ is-largest Î± x
                              â†’ is-largest Î² (f x)
order-equivs-preserve-largest Î± Î² f (o , e , p) x â„“ = Î´
 where
  fâ»Â¹ : âŸ¨ Î² âŸ© â†’ âŸ¨ Î± âŸ©
  fâ»Â¹ = inverse f e

  Î´ : (y : âŸ¨ Î² âŸ©) â†’ y â‰¼âŸ¨ Î² âŸ© f x
  Î´ y t l = IV
   where
    I : fâ»Â¹ t â‰ºâŸ¨ Î± âŸ© fâ»Â¹ y
    I = p t y l

    II : fâ»Â¹ t â‰ºâŸ¨ Î± âŸ© x
    II = â„“ (fâ»Â¹ y) (fâ»Â¹ t) I

    III : f (fâ»Â¹ t) â‰ºâŸ¨ Î² âŸ© f x
    III = o (fâ»Â¹ t) x II

    IV : t â‰ºâŸ¨ Î² âŸ© f x
    IV = transport (Î» - â†’ - â‰ºâŸ¨ Î² âŸ© f x) (inverses-are-sections f e t) III

\end{code}

Added 25th Feb 2023. Alternative definition of ordinal equivalence

\begin{code}

_â‰ƒâ‚_ : Ordinal ğ“¤ â†’ Ordinal ğ“¥ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Î± â‰ƒâ‚ Î² = Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
             , is-equiv f
             Ã— ((x x' : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© x' â‡” f x â‰ºâŸ¨ Î² âŸ© f x')

â‰ƒâ‚-coincides-with-â‰ƒâ‚’ : FunExt
                     â†’ (Î± : Ordinal ğ“¤) (Î² : Ordinal ğ“¥)
                     â†’ (Î± â‰ƒâ‚ Î²) â‰ƒ (Î± â‰ƒâ‚’ Î²)
â‰ƒâ‚-coincides-with-â‰ƒâ‚’ fe Î± Î² =
 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , is-equiv f
      Ã— ((x x' : âŸ¨ Î± âŸ©) â†’ x â‰ºâŸ¨ Î± âŸ© x' â‡” f x â‰ºâŸ¨ Î² âŸ© f x')) â‰ƒâŸ¨ I âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , is-equiv f
      Ã— (is-order-preserving Î± Î² f)
      Ã— (is-order-reflecting Î± Î² f))                      â‰ƒâŸ¨ II âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , (Î£ e ê‰ is-equiv f
             , (is-order-preserving Î± Î² f)
             Ã— (is-order-preserving Î² Î± (inv f e))))      â‰ƒâŸ¨ III âŸ©

 (Î£ f ê‰ (âŸ¨ Î± âŸ© â†’ âŸ¨ Î² âŸ©)
      , (is-order-preserving Î± Î² f)
      Ã— (Î£ e ê‰ is-equiv f
             , (is-order-preserving Î² Î± (inv f e))))      â– 
  where
   inv = inverse
   I   = Î£-cong (Î» f â†’ Ã—-cong (â‰ƒ-refl _) Î Ã—-distrâ‚‚)
   II  = Î£-cong (Î» f â†’ Î£-cong (Î» e â†’ Ã—-cong (â‰ƒ-refl _) (b f e)))
    where
     b = Î» f e
       â†’ logically-equivalent-props-are-equivalent
          (being-order-reflecting-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥) Î± Î² f)
          (being-order-preserving-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥) Î² Î± (inv f e))
          (order-reflecting-gives-inverse-order-preserving Î± Î² f e)
          (inverse-order-reflecting-gives-order-preserving Î± Î² f e)
   III = Î£-cong (Î» f â†’ Î£-flip)

\end{code}

If we only assume preunivalence, meaning that idtoeq is an embedding
(rather than an equivalence), which is implied by each of univalence
and the K axiom, we get that idtoeqâ‚’ is an embedding (rather than an
equivalence). This was suggested to me by Peter Lumsdaine in August
2022.

\begin{code}

idtoeqâ‚’-embedding : is-preunivalent ğ“¤
                  â†’ FunExt
                  â†’ (Î± Î² : Ordinal ğ“¤)
                  â†’ (Î± ï¼ Î²) â†ª (Î± â‰ƒâ‚’ Î²)
idtoeqâ‚’-embedding {ğ“¤} pua fe Î± Î² = II
 where
  open relational-space {ğ“¤} {ğ“¤} {ğ“¤}
        (Î» (X : ğ“¤ Ì‡ ) (_<_ : X â†’ X â†’ ğ“¤ Ì‡ ) â†’ is-well-order _<_)
        (Î» (X : ğ“¤ Ì‡ ) (_<_ : X â†’ X â†’ ğ“¤ Ì‡ ) â†’ being-well-order-is-prop _<_ fe)
        (Î» {X R} w {x} {y} â†’ prop-valuedness R w x y)

  I : (Î± ï¼ Î²) â†ª (Î± â‰…â‚‚ Î²)
  I = M-embeddingâ‚‚-bis pua pua (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥) Î± Î²

  II : (Î± ï¼ Î²) â†ª (Î± â‰ƒâ‚’ Î²)
  II = â‰ƒ-gives-â†ª (â‰ƒâ‚-coincides-with-â‰ƒâ‚’ fe Î± Î²) âˆ˜â†ª I

Ordinal-is-set-under-preunivalence : is-preunivalent ğ“¤
                                   â†’ FunExt
                                   â†’ is-set (Ordinal ğ“¤)
Ordinal-is-set-under-preunivalence {ğ“¤} pua fe {Î±} {Î²} =
 subtypes-of-props-are-props
  âŒŠ idtoeqâ‚’-embedding pua fe Î± Î² âŒ‹
  âŒŠ idtoeqâ‚’-embedding pua fe Î± Î² âŒ‹-is-embedding
  (â‰ƒâ‚’-is-prop-valued (fe _ _) Î± Î²)

\end{code}

NB. The above idtoeqâ‚’-embedding is constructed by a non-trivial
procedure using preunivalence and function extensionality as
assumptions, and so we may wonder whether it really is idtoeqâ‚’. It
isn't on the nose, but it is pointwise equal to it on the nose:

\begin{code}

idtoeqâ‚’-embedding-really-is-idtoeqâ‚’ : (pua : is-preunivalent ğ“¤)
                                      (fe : FunExt)
                                      (Î± Î² : Ordinal ğ“¤)
                                    â†’  âŒŠ idtoeqâ‚’-embedding pua fe Î± Î² âŒ‹
                                    âˆ¼ idtoeqâ‚’ Î± Î²
idtoeqâ‚’-embedding-really-is-idtoeqâ‚’ pua fe Î± Î² refl = refl

\end{code}

And so equal:

\begin{code}

idtoeqâ‚’-embedding-really-is-idtoeqâ‚’' : (pua : is-preunivalent ğ“¤)
                                       (fe : FunExt)
                                       (Î± Î² : Ordinal ğ“¤)
                                     â†’  âŒŠ idtoeqâ‚’-embedding pua fe Î± Î² âŒ‹
                                     ï¼ idtoeqâ‚’ Î± Î²
idtoeqâ‚’-embedding-really-is-idtoeqâ‚’' pua fe Î± Î² =
 dfunext (fe _ _) (idtoeqâ‚’-embedding-really-is-idtoeqâ‚’ pua fe Î± Î²)

\end{code}

Added 26 March 2023 by Tom de Jong, following a discussion with Nicolai Kraus on
24 March 2023.

We show that having an identification of the ordinals (ğŸš , â‚€ â‰º â‚) and
(ğŸš , â‚ â‰º â‚€) contradicts the K-axiom.

It follows that pre-univalence cannot be sufficient to show that the simulation
ordering on the type of ordinals is antisymmetric: The ordinals (ğŸš , â‚€ â‰º â‚) and
(ğŸš , â‚ â‰º â‚€) are equivalent, while pre-univalence is derivable from the K-axiom.

  Î± ï¼ Î² ----> âŸ¨ Î± âŸ© ï¼ âŸ¨ Î² âŸ©
    |                |
    |                |
    v                v
  Î± â‰ƒâ‚’ Î² ----> âŸ¨ Î± âŸ© â‰ƒ âŸ¨ Î² âŸ©


\begin{code}

private

 idtoeqâ‚’-naturality : (Î± Î² : Ordinal ğ“¤) â†’ (p : Î± ï¼ Î²)
                    â†’ idtoeq âŸ¨ Î± âŸ© âŸ¨ Î² âŸ© (ap âŸ¨_âŸ© p)
                    ï¼ â‰ƒâ‚’-gives-â‰ƒ Î± Î² (idtoeqâ‚’ Î± Î² p)
 idtoeqâ‚’-naturality Î± Î² refl = refl

 ğŸšâ‚’ : Ordinal ğ“¤â‚€
 ğŸšâ‚’ = ğŸš , (_â‰º_ , p , w , e , t)
  where
   _â‰º_ : ğŸš â†’ ğŸš â†’ ğ“¤â‚€ Ì‡
   â‚€ â‰º â‚€ = ğŸ˜
   â‚€ â‰º â‚ = ğŸ™
   â‚ â‰º â‚€ = ğŸ˜
   â‚ â‰º â‚ = ğŸ˜
   p : is-prop-valued _â‰º_
   p â‚€ â‚€ = ğŸ˜-is-prop
   p â‚€ â‚ = ğŸ™-is-prop
   p â‚ â‚€ = ğŸ˜-is-prop
   p â‚ â‚ = ğŸ˜-is-prop
   w : is-well-founded _â‰º_
   w â‚€ = step a
    where
     a : (y : ğŸš) â†’ y â‰º â‚€ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = ğŸ˜-elim l
   w â‚ = step a
    where
     a : (y : ğŸš) â†’ y â‰º â‚ â†’ is-accessible _â‰º_ y
     a â‚€ l = w â‚€
     a â‚ l = ğŸ˜-elim l
   e : is-extensional _â‰º_
   e â‚€ â‚€ u v = refl
   e â‚€ â‚ u v = ğŸ˜-elim (v â‚€ â‹†)
   e â‚ â‚€ u v = ğŸ˜-elim (u â‚€ â‹†)
   e â‚ â‚ u v = refl
   t : is-transitive _â‰º_
   t â‚€ â‚€ â‚€ k l = l
   t â‚€ â‚ â‚€ k l = l
   t â‚ â‚€ â‚€ k l = l
   t â‚ â‚ â‚€ k l = l
   t â‚€ â‚€ â‚ k l = l
   t â‚€ â‚ â‚ k l = k
   t â‚ â‚€ â‚ k l = k
   t â‚ â‚ â‚ k l = l

 ğŸšâ‚’' : Ordinal ğ“¤â‚€
 ğŸšâ‚’' = ğŸš , (_â‰º_ , p , w , e , t)
  where
   _â‰º_ : ğŸš â†’ ğŸš â†’ ğ“¤â‚€ Ì‡
   â‚€ â‰º â‚€ = ğŸ˜
   â‚€ â‰º â‚ = ğŸ˜
   â‚ â‰º â‚€ = ğŸ™
   â‚ â‰º â‚ = ğŸ˜
   p : is-prop-valued _â‰º_
   p â‚€ â‚€ = ğŸ˜-is-prop
   p â‚€ â‚ = ğŸ˜-is-prop
   p â‚ â‚€ = ğŸ™-is-prop
   p â‚ â‚ = ğŸ˜-is-prop
   w : is-well-founded _â‰º_
   w â‚€ = step a
    where
     a : (y : ğŸš) â†’ y â‰º â‚€ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = w â‚
   w â‚ = step a
    where
     a : (y : ğŸš) â†’ y â‰º â‚ â†’ is-accessible _â‰º_ y
     a â‚€ l = ğŸ˜-elim l
     a â‚ l = ğŸ˜-elim l
   e : is-extensional _â‰º_
   e â‚€ â‚€ u v = refl
   e â‚€ â‚ u v = ğŸ˜-elim (u â‚ â‹†)
   e â‚ â‚€ u v = ğŸ˜-elim (v â‚ â‹†)
   e â‚ â‚ u v = refl
   t : is-transitive _â‰º_
   t â‚€ â‚€ â‚€ k l = l
   t â‚€ â‚ â‚€ k l = k
   t â‚ â‚€ â‚€ k l = â‹†
   t â‚ â‚ â‚€ k l = l
   t â‚€ â‚€ â‚ k l = l
   t â‚€ â‚ â‚ k l = l
   t â‚ â‚€ â‚ k l = l
   t â‚ â‚ â‚ k l = l

 open import MLTT.Two-Properties

 ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’' : ğŸšâ‚’ â‰ƒâ‚’ ğŸšâ‚’'
 ğŸšâ‚’-â‰ƒâ‚’-ğŸšâ‚’' = f , f-preserves-order , f-is-equiv , f-preserves-order'
  where
   f : ğŸš â†’ ğŸš
   f = complement
   f-preserves-order : is-order-preserving ğŸšâ‚’ ğŸšâ‚’' f
   f-preserves-order â‚€ â‚ l = l
   f-preserves-order â‚€ â‚€ l = ğŸ˜-elim l
   f-preserves-order â‚ â‚€ l = ğŸ˜-elim l
   f-preserves-order â‚ â‚ l = ğŸ˜-elim l
   f-is-equiv : is-equiv f
   f-is-equiv = qinvs-are-equivs f (f , complement-involutive , complement-involutive)
   f-preserves-order' : is-order-preserving ğŸšâ‚’' ğŸšâ‚’ f
   f-preserves-order' â‚€ â‚€ l = ğŸ˜-elim l
   f-preserves-order' â‚€ â‚ l = ğŸ˜-elim l
   f-preserves-order' â‚ â‚€ l = l
   f-preserves-order' â‚ â‚ l = ğŸ˜-elim l

 complement-is-the-only-ordinal-equivalence-of-ğŸš : (e : ğŸšâ‚’ â‰ƒâ‚’ ğŸšâ‚’')
                                                 â†’ â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e âˆ¼ complement
 complement-is-the-only-ordinal-equivalence-of-ğŸš e â‚€ = different-from-â‚€-equal-â‚ h
  where
   f : ğŸš â†’ ğŸš
   f = â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e
   h : â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e â‚€ â‰  â‚€
   h p = l' (f â‚) (order-equivs-are-order-preserving ğŸšâ‚’ ğŸšâ‚’' (â‰ƒâ‚’-to-fun-is-order-equiv ğŸšâ‚’ ğŸšâ‚’' e) â‚€ â‚ â‹†)
    where
     l : (b : ğŸš) â†’ Â¬ (â‚€ â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)
     l â‚€ l = ğŸ˜-elim l
     l â‚ l = ğŸ˜-elim l
     l' : (b : ğŸš) â†’ Â¬ (f â‚€ â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)
     l' b = idtofun _ _ (ap (Î» - â†’ Â¬ (- â‰ºâŸ¨ ğŸšâ‚’' âŸ© b)) (p â»Â¹)) (l b)
 complement-is-the-only-ordinal-equivalence-of-ğŸš e â‚ = different-from-â‚-equal-â‚€ h
  where
   f : ğŸš â†’ ğŸš
   f = â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e
   h : â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' e â‚ â‰  â‚
   h p = l' (f â‚€) (order-equivs-are-order-preserving ğŸšâ‚’ ğŸšâ‚’' (â‰ƒâ‚’-to-fun-is-order-equiv ğŸšâ‚’ ğŸšâ‚’' e) â‚€ â‚ â‹†)
    where
     l : (b : ğŸš) â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© â‚)
     l â‚€ l = ğŸ˜-elim l
     l â‚ l = ğŸ˜-elim l
     l' : (b : ğŸš) â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© f â‚)
     l' b = idtofun _ _ (ap (Î» - â†’ Â¬ (b â‰ºâŸ¨ ğŸšâ‚’' âŸ© -)) (p â»Â¹)) (l b)

 identification-of-ğŸšâ‚’-and-ğŸšâ‚’'-contradicts-K : ğŸšâ‚’ ï¼ ğŸšâ‚’' â†’ Â¬ K-axiom ğ“¤â‚
 identification-of-ğŸšâ‚’-and-ğŸšâ‚’'-contradicts-K pâ‚’ K = p-is-not-refl (K (ğ“¤â‚€ Ì‡  ) p refl)
  where
   p : ğŸš ï¼ ğŸš
   p = ap âŸ¨_âŸ© pâ‚’
   f : ğŸš â‰ƒ ğŸš
   f = idtoeq ğŸš ğŸš p
   p-is-not-refl : Â¬ (p ï¼ refl)
   p-is-not-refl e = zero-is-not-one (â‚€                     ï¼âŸ¨ refl âŸ©
                                      âŒœ idtoeq ğŸš ğŸš refl âŒ â‚€ ï¼âŸ¨ ap (Î» - â†’ âŒœ idtoeq ğŸš ğŸš - âŒ â‚€) (e â»Â¹) âŸ©
                                      âŒœ f âŒ â‚€               ï¼âŸ¨ ap (Î» - â†’ âŒœ - âŒ â‚€) (idtoeqâ‚’-naturality ğŸšâ‚’ ğŸšâ‚’' pâ‚’) âŸ©
                                      âŒœ â‰ƒâ‚’-gives-â‰ƒ ğŸšâ‚’ ğŸšâ‚’' (idtoeqâ‚’ ğŸšâ‚’ ğŸšâ‚’' pâ‚’) âŒ â‚€ ï¼âŸ¨ refl âŸ©
                                      â‰ƒâ‚’-to-fun ğŸšâ‚’ ğŸšâ‚’' (idtoeqâ‚’ ğŸšâ‚’ ğŸšâ‚’' pâ‚’) â‚€ ï¼âŸ¨ complement-is-the-only-ordinal-equivalence-of-ğŸš (idtoeqâ‚’ ğŸšâ‚’ ğŸšâ‚’' pâ‚’) â‚€ âŸ©
                                      â‚                     âˆ)

\end{code}
