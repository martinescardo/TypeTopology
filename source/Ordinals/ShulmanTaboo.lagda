Martin Escardo, 4th October 2018

We worked with ordinals with top in order to be able to construct the
sum of an ordinal-indexed family of ordinals, with the lexicographic
order. Here we justify that some such restriction is needed.

This implements the following email communication:

On 22/07/18 06:17, Michael Shulman wrote:
> Ah, I see.  In fact I think "every subset of an ordinal is an
> ordinal" is equivalent to LEM as follows: consider the ordinal Prop,
> with (P < Q) = (~P /\ Q) as before, and consider the subset A of all
> P such that ~~P is true.  Then False \notin A, so given any Q \in A,
> we cannot have any P \in A with P < Q.  Thus the hypothesis of
> extensionality for A is vacuous, so to say that A is extensional is
> to say that it is a prop.  But True \in A, so this is to say
> that ~~P -> (P = True), i.e. DNE holds, hence LEM.
>
> On Fri, Jul 20, 2018 at 3:42 PM, Martin Escardo <m.escardo@cs.bham.ac.uk> wrote:
>> On 20/07/18 23:31, Michael Shulman wrote:
>>> Do you know of a model in which preservation of extensionality
>>> definitely fails (or a proof of a taboo from it)?
>>
>> No. But here is an idea that made me to give up trying to prove
>> extensionality of the lexicographic order on Sigma.
>>
>> Any prop is an ordinal in a unique way (with the empty order).
>>
>> Now suppose that X is an ordinal, and consider P:X->U prop
>> valued.  Then the lexicographic order on the sum Sigma (x:X),P (x) is
>> nothing but a subset of X. While classically the subset will remain
>> extensional (for if we have x and y not equal, then either x<y or
>> y<x, and hence they will not have the same lower set),
>> constructively this seems very dubious.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.FunExt
open import UF.Subsingletons

module Ordinals.ShulmanTaboo
       (fe : FunExt)
       (pe : propext ğ“¤â‚€)
       where

open import Ordinals.Type
open import Ordinals.OrdinalOfTruthValues fe ğ“¤â‚€ pe
open import Ordinals.Notions
open import Ordinals.Underlying

open import UF.Base
open import UF.ExcludedMiddle
open import UF.SubtypeClassifier
open import UF.Subsingletons-FunExt

feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
feâ‚€ = fe ğ“¤â‚€ ğ“¤â‚€

\end{code}

The type of truth values is Î©, following topos-theoretic notation, and
the ordinal of truth values, ordered by p < q iff p ï¼ âŠ¥ and q ï¼ âŠ¤, is
denoted by Î©â‚’ (the subscript is the letter "o", for "ordinal", and not
the number zero). This is parametrized by an arbitrary universe, which
in this module is instantiated to ğ“¤â‚€.

\begin{code}

X : ğ“¤â‚ Ì‡
X = Î£ p ê‰ âŸ¨ Î©â‚’ âŸ© , Â¬ (p ï¼ âŠ¥)

recall-that : is-extensional (underlying-order Î©â‚’)
recall-that = Extensionality Î©â‚’

\end{code}

However, the extensionality of the restriction of the underlying order
of the ordinal Î©â‚’ of truth values to X is a constructive taboo (it
implies excluded middle), as shown by Shulman in the message quoted
above:

\begin{code}

_â‰º_ : X â†’ X â†’ ğ“¤â‚ Ì‡
(p , _) â‰º (q , _) = p â‰ºâŸ¨ Î©â‚’ âŸ© q

shulmans-taboo : is-extensional _â‰º_ â†’ EM ğ“¤â‚€
shulmans-taboo e = DNE-gives-EM feâ‚€ Î´
 where
  i : is-prop X
  i x y = e x y f g
   where
    f : (z : X) â†’ z â‰º x â†’ z â‰º y
    f (p , Ï†) (a , _) = ğŸ˜-elim (Ï† a)

    g : (z : X) â†’ z â‰º y â†’ z â‰º x
    g (q , Ïˆ) (b , _) = ğŸ˜-elim (Ïˆ b)

  Î´ : (P : ğ“¤â‚€ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ P
  Î´ P j Ï† = Idtofun s Ï†
   where
    p q : X
    p = (Â¬Â¬ P , negations-are-props feâ‚€) ,
        (Î» r â†’ Idtofun (ap prâ‚ r) Ï†)
    q = (P , j) ,
        (Î» r â†’ Ï† (Idtofun (ap prâ‚ r)))

    r : p ï¼ q
    r = i p q

    s : Â¬Â¬ P ï¼ P
    s = ap (prâ‚ âˆ˜ prâ‚) r

\end{code}

TODO: Use this to show that if the sum of any ordinal-indexed family
of ordinals is an ordinal under the lexicographic ordering, then
excluded middle holds, as explained in the message quoted
above. (Routine, tedious.)
