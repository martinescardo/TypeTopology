Jon Sterling, started 27th Sep 2022

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module Modal.Subuniverse where

open import MLTT.Spartan
open import UF.Subsingletons
open import UF.Base
open import UF.FunExt
open import UF.Equiv
open import UF.Retracts
open import UF.Univalence

subuniverse
 : (ğ“¤ ğ“¥ : Universe)
 â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
subuniverse ğ“¤ ğ“¥ =
 Î£ P ê‰ (ğ“¤ Ì‡ â†’ ğ“¥ Ì‡) ,
 Î  A ê‰ ğ“¤ Ì‡ ,
 is-prop (P A)

subuniverse-contains
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¥ Ì‡
subuniverse-contains P A =
 prâ‚ P A

subuniverse-member
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-member P =
 Î£ (subuniverse-contains P)

reflection-candidate
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
reflection-candidate P A =
 Î£ A' ê‰ subuniverse-member P ,
 (A â†’ prâ‚ A')

is-reflection
 : (P : subuniverse ğ“¤ ğ“¥)
 â†’ (A : ğ“¤ Ì‡)
 â†’ reflection-candidate P A
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
is-reflection P A (A' , Î·) =
 (B : _)
 â†’ subuniverse-contains P B
 â†’ is-equiv Î» (f : prâ‚ A' â†’ B) â†’ f âˆ˜ Î·

subuniverse-reflects
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-reflects {ğ“¤ = ğ“¤} P A =
 Î£ A' ê‰ reflection-candidate P A ,
 is-reflection P A A'

subuniverse-is-reflective
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-is-reflective P =
 Î  (subuniverse-reflects P)


subuniverse-is-replete
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-is-replete {ğ“¤ = ğ“¤} P =
 (A B : ğ“¤ Ì‡)
 â†’ A â‰ƒ B
 â†’ subuniverse-contains P B
 â†’ subuniverse-contains P A

univalence-implies-subuniverse-is-replete
 : (ua : is-univalent ğ“¤)
 â†’ (P : subuniverse ğ“¤ ğ“¥)
 â†’ subuniverse-is-replete P
univalence-implies-subuniverse-is-replete ua P A B e =
 transportâ»Â¹
  (subuniverse-contains P)
  (eqtoid ua A B e)

module ReflectiveSubuniverse (P : subuniverse ğ“¤ ğ“¥) (P-is-reflective : subuniverse-is-reflective P) where
 reflection : (A : ğ“¤ Ì‡) â†’ reflection-candidate P A
 reflection A = prâ‚ (P-is-reflective A)

 â—‹-packed : (A : ğ“¤ Ì‡) â†’ subuniverse-member P
 â—‹-packed A = prâ‚ (reflection A)

 â—‹ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
 â—‹ A = prâ‚ (â—‹-packed A)

 subuniverse-contains-reflection : (A : ğ“¤ Ì‡) â†’ subuniverse-contains P (â—‹ A)
 subuniverse-contains-reflection A = prâ‚‚ (â—‹-packed A)

 Î· : (A : ğ“¤ Ì‡) â†’ A â†’ â—‹ A
 Î· A = prâ‚‚ (reflection A)

 âˆ˜Î· : {ğ“¥ : _} (A : ğ“¤ Ì‡) (B : ğ“¥ Ì‡) â†’ (â—‹ A â†’ B) â†’ A â†’ B
 âˆ˜Î· A B = _âˆ˜ (Î· A)

 âˆ˜Î·-is-equiv
  : {A : ğ“¤ Ì‡} {B : ğ“¤ Ì‡}
  â†’ subuniverse-contains P B
  â†’ is-equiv (âˆ˜Î· A B)
 âˆ˜Î·-is-equiv B-in-P =
  prâ‚‚ (P-is-reflective _) _ B-in-P

 â—‹-rec
  : (A B : ğ“¤ Ì‡)
  â†’ (B-in-P : subuniverse-contains P B)
  â†’ (A â†’ B)
  â†’ (â—‹ A â†’ B)
 â—‹-rec A B B-in-P =
  inverse _ (âˆ˜Î·-is-equiv B-in-P)

 â—‹-rec-compute
  : (A B : ğ“¤ Ì‡)
  â†’ (B-in-P : subuniverse-contains P B)
  â†’ (f : A â†’ B)
  â†’ (x : A)
  â†’ â—‹-rec A B B-in-P f (Î· A x) ï¼ f x
 â—‹-rec-compute A B B-in-P f =
  happly (inverses-are-sections _ (âˆ˜Î·-is-equiv B-in-P) f)

 â—‹-rec-ext
  : (A B : ğ“¤ Ì‡)
  â†’ (B-in-P : subuniverse-contains P B)
  â†’ (f g : â—‹ A â†’ B)
  â†’ (f âˆ˜ Î· A) ï¼ (g âˆ˜ Î· A)
  â†’ f ï¼ g
 â—‹-rec-ext A B B-in-P f g fgÎ· =
  let H = inverses-are-retractions _ (âˆ˜Î·-is-equiv B-in-P) in
  f ï¼âŸ¨ H f â»Â¹ âŸ©
  â—‹-rec A B B-in-P (f âˆ˜ Î· A) ï¼âŸ¨ ap (â—‹-rec A B B-in-P) fgÎ· âŸ©
  â—‹-rec A B B-in-P (g âˆ˜ Î· A) ï¼âŸ¨ H g âŸ©
  g âˆ

 Î·-is-section-implies-has-section
  : (fe : funext ğ“¤ ğ“¤)
  â†’ (A : ğ“¤ Ì‡)
  â†’ is-section (Î· A)
  â†’ has-section (Î· A)
 prâ‚ (Î·-is-section-implies-has-section fe A Î·-is-section) = prâ‚ Î·-is-section
 prâ‚‚ (Î·-is-section-implies-has-section fe A Î·-is-section) =
  happly
   (â—‹-rec-ext A (â—‹ A) (subuniverse-contains-reflection A) _ _
     (dfunext fe Î» x â†’
      Î· A (prâ‚ Î·-is-section (Î· A x)) ï¼âŸ¨ ap (Î· A) (prâ‚‚ Î·-is-section x) âŸ©
      Î· A x âˆ))

 Î·-is-equiv-implies-subuniverse-contains
  : (P-is-replete : subuniverse-is-replete P)
  â†’ (A : ğ“¤ Ì‡)
  â†’ is-equiv (Î· A)
  â†’ subuniverse-contains P A
 Î·-is-equiv-implies-subuniverse-contains P-is-replete A Î·-is-equiv =
  P-is-replete _ _
   (Î· A , Î·-is-equiv)
   (subuniverse-contains-reflection A)

 reflective-subuniverse-closed-under-retracts
  : (fe : funext ğ“¤ ğ“¤)
  â†’ (P-is-replete : subuniverse-is-replete P)
  â†’ (E B : ğ“¤ Ì‡)
  â†’ retract B of E
  â†’ subuniverse-contains P E
  â†’ subuniverse-contains P B
 reflective-subuniverse-closed-under-retracts fe P-is-replete E B B-retract-of-E E-in-P =
   Î·-is-equiv-implies-subuniverse-contains P-is-replete B
    (Î·-is-section-implies-has-section fe B Î·-is-section ,
     Î·-is-section)
   where
    h : â—‹ B â†’ E
    h = â—‹-rec B E E-in-P (section B-retract-of-E)

    Îµ : â—‹ B â†’ B
    Îµ = retraction B-retract-of-E âˆ˜ h

    Î·-is-section : is-section (Î· B)
    prâ‚ Î·-is-section = Îµ
    prâ‚‚ Î·-is-section x =
     Îµ (Î· B x) ï¼âŸ¨ ap (retraction B-retract-of-E) (â—‹-rec-compute B E E-in-P (section B-retract-of-E) x) âŸ©
     retraction B-retract-of-E (section B-retract-of-E x) ï¼âŸ¨ retract-condition B-retract-of-E x âŸ©
     x âˆ

 reflective-subuniverse-closed-under-products
  : (fe : funext ğ“¤ ğ“¤)
  â†’ (P-is-replete : subuniverse-is-replete P)
  â†’ (A : ğ“¤ Ì‡)
  â†’ (B : A â†’ ğ“¤ Ì‡)
  â†’ (B-in-P : Î  x ê‰ A , subuniverse-contains P (B x))
  â†’ subuniverse-contains P (Î  B)
 reflective-subuniverse-closed-under-products fe P-is-replete A B B-in-P =
  reflective-subuniverse-closed-under-retracts fe P-is-replete _ _ ret (subuniverse-contains-reflection (Î  B))
  where
   h : (x : A) â†’ â—‹ (Î  B) â†’ B x
   h x = â—‹-rec (Î  B) (B x) (B-in-P x) (Î» f â†’ f x)

   ret : retract Î  B of â—‹ (Î  B)
   prâ‚ ret f x = h x f
   prâ‚ (prâ‚‚ ret) = Î· (Î  B)
   prâ‚‚ (prâ‚‚ ret) f =
    dfunext fe Î» x â†’
    â—‹-rec-compute (Î  B) (B x) (B-in-P x) (Î» g â†’ g x) f

\end{code}
