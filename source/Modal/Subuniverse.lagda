Jon Sterling, started 27th Sep 2022
Andrew Swan, 7th Februrary 2024, definition of Î£-closed subuniverse added

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Modal.Subuniverse where

open import MLTT.Spartan
open import UF.Subsingletons
open import UF.Base
open import UF.FunExt
open import UF.Equiv
open import UF.Univalence

subuniverse
 : (ğ“¤ ğ“¥ : Universe)
 â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
subuniverse ğ“¤ ğ“¥ =
 Î£ P ê‰ (ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ ),
 Î  A ê‰ ğ“¤ Ì‡ ,
 is-prop (P A)

subuniverse-contains
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¥ Ì‡
subuniverse-contains P A =
 prâ‚ P A

subuniverse-member
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-member P =
 Î£ (subuniverse-contains P)

reflection-candidate
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
reflection-candidate P A =
 Î£ A' ê‰ subuniverse-member P ,
 (A â†’ prâ‚ A')

is-reflection
 : (P : subuniverse ğ“¤ ğ“¥)
 â†’ (A : ğ“¤ Ì‡)
 â†’ reflection-candidate P A
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
is-reflection P A (A' , Î·) =
 (B : _)
 â†’ subuniverse-contains P B
 â†’ is-equiv Î» (f : prâ‚ A' â†’ B) â†’ f âˆ˜ Î·

subuniverse-reflects
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ Ì‡
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-reflects P A =
 Î£ A' ê‰ reflection-candidate P A ,
 is-reflection P A A'

subuniverse-is-reflective
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-is-reflective P =
 Î  (subuniverse-reflects P)

subuniverse-is-replete
 : subuniverse ğ“¤ ğ“¥
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-is-replete P =
 (A B : _)
 â†’ A â‰ƒ B
 â†’ subuniverse-contains P B
 â†’ subuniverse-contains P A

univalence-implies-subuniverse-is-replete
 : (ua : is-univalent ğ“¤)
 â†’ (P : subuniverse ğ“¤ ğ“¥)
 â†’ subuniverse-is-replete P
univalence-implies-subuniverse-is-replete ua P A B e =
 transportâ»Â¹
  (subuniverse-contains P)
  (eqtoid ua A B e)

subuniverse-is-sigma-closed
 : (P : subuniverse ğ“¤ ğ“¥)
 â†’ ğ“¤ âº âŠ” ğ“¥  Ì‡
subuniverse-is-sigma-closed P =
 (A : _) â†’
 (B : A â†’ _) â†’
 prâ‚ P A â†’
 ((a : A) â†’ prâ‚ P (B a)) â†’
 prâ‚ P (Î£ B)
\end{code}
