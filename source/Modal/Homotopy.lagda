Jon Sterling, started 5th Oct 2022

This file contains some lemmas about precomposing embeddings onto homotopies.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Modal.Homotopy where

open import MLTT.Spartan
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.FunExt

homotopy-precomp
  : {ğ“¤ ğ“¥ ğ“¦ : Universe}
  â†’ {U : ğ“¤ Ì‡ } {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ }
  â†’ (f g : X â†’ Y)
  â†’ (i : U â†’ X)
  â†’ f âˆ¼ g
  â†’ f âˆ˜ i âˆ¼ g âˆ˜ i
homotopy-precomp f g i h =
 h âˆ˜ i

\end{code}

The following is a slight generalization of Lemma 5.1.18 of Egbert Rijke's
doctoral dissertation.

\begin{code}
homotopy-precomp-by-embedding-is-equiv
 : {ğ“¤ ğ“¥ ğ“¦ : Universe}
 â†’ (fe0 : funext ğ“¥ ğ“¦)
 â†’ (fe1 : funext ğ“¤ ğ“¦)
 â†’ (fe2 : funext (ğ“¥ âŠ” ğ“¦) (ğ“¤ âŠ” ğ“¦))
 â†’ (fe3 : funext (ğ“¥ âŠ” ğ“¦) (ğ“¥ âŠ” ğ“¦))
 â†’ {U : ğ“¤ Ì‡ } {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ }
 â†’ (f g : X â†’ Y)
 â†’ (i : U â†’ X)
 â†’ (precomp-i-is-emb : is-embedding Î» (- : X â†’ Y) â†’ - âˆ˜ i)
 â†’ is-equiv (homotopy-precomp f g i)
homotopy-precomp-by-embedding-is-equiv fe0 fe1 fe2 fe3 f g i precomp-i-is-emb =
 transport is-equiv composite-is-precomp (eqtofun- composite)

 where
  composite : f âˆ¼ g â‰ƒ (f âˆ˜ i âˆ¼ g âˆ˜ i)
  composite =
   â‰ƒ-sym (â‰ƒ-funext fe0 f g)
    â— (ap (_âˆ˜ i) , embedding-gives-embedding' _ precomp-i-is-emb _ _)
    â— â‰ƒ-funext fe1 (f âˆ˜ i) (g âˆ˜ i)

  composite-is-precomp : eqtofun composite ï¼ homotopy-precomp f g i
  composite-is-precomp =
   dfunext fe2 Î» h â†’
   eqtofun composite h ï¼âŸ¨ ap happly (aux h) âŸ©
   happly (dfunext fe1 (h âˆ˜ i)) ï¼âŸ¨ happly-funext fe1 _ _ (h âˆ˜ i) âŸ©
   homotopy-precomp f g i h âˆ

   where
    aux : (h : f âˆ¼ g) â†’ ap (_âˆ˜ i) (inverse _ (fe0 f g) h) ï¼ dfunext fe1 (h âˆ˜ i)
    aux h =
     ap (_âˆ˜ i) (inverse (happly' f g) (fe0 f g) h)
      ï¼âŸ¨ ap (Î» - â†’ ap (_âˆ˜ i) (- h)) (inverse-happly-is-dfunext fe0 fe3 f g) âŸ©
     ap (_âˆ˜ i) (dfunext fe0 h)
      ï¼âŸ¨ ap-precomp-funext _ _ i h fe0 fe1 âŸ©
     dfunext fe1 (h âˆ˜ i) âˆ

\end{code}
