Tom de Jong & Martin Escardo, 20 May 2019.

 * Directed complete posets.
 * Continuous maps.
 * Examples and constructions in DcpoConstructions

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-Base
open import UF-PropTrunc
open import UF-Subsingletons
open import UF-FunExt
open import UF-PropTrunc

module Dcpo
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (ğ“¥ : Universe) -- where the index type for directed completeness lives
       where

open PropositionalTruncation pt

open import UF-Subsingletons
open import UF-Subsingletons-FunExt

module _ {ğ“¤ ğ“£ : Universe}
         {D : ğ“¤ Ì‡ }
         (_âŠ‘_ : D â†’ D â†’ ğ“£ Ì‡ )
       where

 is-prop-valued : ğ“¤ âŠ” ğ“£ Ì‡
 is-prop-valued = (x y : D) â†’ is-prop (x âŠ‘ y)

 is-reflexive : ğ“¤ âŠ” ğ“£ Ì‡
 is-reflexive = (x : D) â†’ x âŠ‘ x

 is-transitive : ğ“¤ âŠ” ğ“£ Ì‡
 is-transitive = (x y z : D) â†’ x âŠ‘ y â†’ y âŠ‘ z â†’ x âŠ‘ z

 is-antisymmetric : ğ“¤ âŠ” ğ“£ Ì‡
 is-antisymmetric = (x y : D) â†’ x âŠ‘ y â†’ y âŠ‘ x â†’ x â‰¡ y

 is-least : D â†’ ğ“¤ âŠ” ğ“£ Ì‡
 is-least x = âˆ€ (y : D) â†’ x âŠ‘ y

 has-least : ğ“¤ âŠ” ğ“£ Ì‡
 has-least = Î£ x ê‰ D , is-least x

 is-upperbound : {I : ğ“¥ Ì‡ } (u : D) (Î± : I â†’ D) â†’ ğ“¥ âŠ” ğ“£ Ì‡
 is-upperbound u Î± = (i : domain Î±) â†’ Î± i âŠ‘ u

 is-sup : {I : ğ“¥ Ì‡ } â†’ D â†’ (I â†’ D) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“£ Ì‡
 is-sup s Î± = (is-upperbound s Î±)
            Ã— ((u : D) â†’ is-upperbound u Î± â†’ s âŠ‘ u)

 is-sup-gives-is-upperbound : {I : ğ“¥ Ì‡ } {s : D} {Î± : I â†’ D}
                            â†’ is-sup s Î±
                            â†’ is-upperbound s Î±
 is-sup-gives-is-upperbound i = prâ‚ i

 is-sup-gives-is-lowerbound-of-upperbounds : {I : ğ“¥ Ì‡ } {s : D} {Î± : I â†’ D}
                                           â†’ is-sup s Î±
                                           â†’ (u : D)
                                           â†’ is-upperbound u Î± â†’ s âŠ‘ u
 is-sup-gives-is-lowerbound-of-upperbounds i = prâ‚‚ i

 has-sup : {I : ğ“¥ Ì‡ } â†’ (I â†’ D) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“£ Ì‡
 has-sup Î± = Î£ s ê‰ D , is-sup s Î±

 the-sup : {I : ğ“¥ Ì‡ } {Î± : I â†’ D} â†’ has-sup Î± â†’ D
 the-sup (s , i) = s

 sup-property : {I : ğ“¥ Ì‡ } {Î± : I â†’ D} (h : has-sup Î±) â†’ is-sup (the-sup h) Î±
 sup-property (s , i) = i

 is-directed : {I : ğ“¥ Ì‡ } â†’ (I â†’ D) â†’ ğ“¥ âŠ” ğ“£ Ì‡
 is-directed {I} Î± = âˆ¥ I âˆ¥ Ã— ((i j : I) â†’ âˆƒ k ê‰ I , (Î± i âŠ‘ Î± k) Ã— (Î± j âŠ‘ Î± k))

 is-directed-gives-inhabited : {I : ğ“¥ Ì‡ } (Î± : I â†’ D) â†’ is-directed Î± â†’ âˆ¥ I âˆ¥
 is-directed-gives-inhabited Î± = prâ‚

 is-directed-order : {I : ğ“¥ Ì‡ } (Î± : I â†’ D) â†’ is-directed Î±
                   â†’ (i j : I) â†’ âˆƒ k ê‰ I , (Î± i âŠ‘ Î± k) Ã— (Î± j âŠ‘ Î± k)
 is-directed-order Î± = prâ‚‚

 being-directed-is-prop : {I : ğ“¥ Ì‡ } (Î± : I â†’ D) â†’ is-prop (is-directed Î±)
 being-directed-is-prop Î± = Ã—-is-prop âˆ¥âˆ¥-is-prop
                            (Î -is-prop fe
                               (Î» i â†’ Î -is-prop fe
                                       (Î» j â†’ âˆ¥âˆ¥-is-prop )))

 is-directed-complete : ğ“¤ âŠ” (ğ“¥ âº) âŠ” ğ“£  Ì‡
 is-directed-complete = (I : ğ“¥ Ì‡ ) (Î± : I â†’ D) â†’ is-directed Î± â†’ has-sup Î±

 dcpo-axioms : ğ“¤ âŠ” (ğ“¥ âº) âŠ” ğ“£ Ì‡
 dcpo-axioms = is-set D
             Ã— is-prop-valued
             Ã— is-reflexive
             Ã— is-transitive
             Ã— is-antisymmetric
             Ã— is-directed-complete

 is-sup-is-prop : dcpo-axioms â†’ {I : ğ“¥ Ì‡ } (d : D) (Î± : I â†’ D)
                  â†’ is-prop (is-sup d Î±)
 is-sup-is-prop (s , p , r , t , a , c) {I} d Î± = Î³
  where
   Î³ : is-prop (is-sup d Î±)
   Î³ = Ã—-is-prop (Î -is-prop fe (Î» (i : I) â†’ p (Î± i) d))
                 (Î -is-prop fe (Î» (x : D) â†’ Î -is-prop fe (Î» l â†’ p d x)))

 having-sup-is-prop : dcpo-axioms â†’ {I : ğ“¥ Ì‡ } (Î± : I â†’ D)
                      â†’ is-prop (has-sup Î±)
 having-sup-is-prop (s , p , r , t , a , c) {I} Î± = Î³
  where
   Î³ : is-prop (has-sup Î±)
   Î³ (j , (u , l)) (j' , (u' , l')) =
     to-Î£-â‰¡ (q , is-sup-is-prop (s , p , r , t , a , c) j' Î± _ _)
    where
     q : j â‰¡ j'
     q = a j j' (l j' u') (l' j u)

 being-directed-complete-is-prop : dcpo-axioms â†’ is-prop is-directed-complete
 being-directed-complete-is-prop a =
  Î -is-prop fe
   (Î» I â†’ Î -is-prop fe
             (Î» Î± â†’ Î -is-prop fe (Î» d â†’ having-sup-is-prop a Î±)))

 dcpo-axioms-is-prop : is-prop dcpo-axioms
 dcpo-axioms-is-prop = prop-criterion Î³
  where
   Î³ : dcpo-axioms â†’ is-prop dcpo-axioms
   Î³ (s , p , r , t , a , c) =
    Ã—-is-prop  (being-set-is-prop fe)
    (Ã—-is-prop (Î -is-prop fe
                 (Î» (x : D) â†’ Î -is-prop fe
                                (Î» (y : D) â†’ being-prop-is-prop fe)))
    (Ã—-is-prop (Î -is-prop fe (Î» (x : D) â†’ p x x))
    (Ã—-is-prop (Î -is-prop fe
                 (Î» (x : D) â†’ Î -is-prop fe
                               (Î» (y : D) â†’ Î -is-prop fe
                                             (Î» (z : D) â†’ Î -is-prop fe
                                                           (Î» (l : x âŠ‘ y) â†’ Î -is-prop fe
                                                                             (Î» (m : y âŠ‘ z) â†’ p x z))))))
    (Ã—-is-prop (Î -is-prop fe
                 (Î» (x : D) â†’ Î -is-prop fe
                               (Î» y â†’ Î -is-prop fe
                                       (Î» (l : x âŠ‘ y) â†’ Î -is-prop fe
                                                         Î» (m : y âŠ‘ x) â†’ s))))
              (being-directed-complete-is-prop (s , p , r , t , a , c))))))

module _ {ğ“¤ ğ“£ : Universe} where

 DCPO-structure : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” (ğ“¥ âº) âŠ” (ğ“£ âº) Ì‡
 DCPO-structure D = Î£ _âŠ‘_ ê‰ (D â†’ D â†’ ğ“£ Ì‡ ), (dcpo-axioms {ğ“¤} {ğ“£} _âŠ‘_)

 DCPO : (ğ“¤ âº) âŠ” (ğ“¥ âº) âŠ” (ğ“£ âº) Ì‡
 DCPO = Î£ D ê‰ ğ“¤ Ì‡ , DCPO-structure D

 âŸ¨_âŸ© : DCPO â†’ ğ“¤ Ì‡
 âŸ¨ D , _âŠ‘_ , d âŸ© = D

 underlying-order : (ğ““ : DCPO) â†’ âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ““ âŸ© â†’ ğ“£ Ì‡
 underlying-order (D , _âŠ‘_ , d) = _âŠ‘_

 syntax underlying-order ğ““ x y = x âŠ‘âŸ¨ ğ““ âŸ© y

 DCPOâŠ¥ : (ğ“¥ âº) âŠ” (ğ“¤ âº) âŠ” (ğ“£ âº) Ì‡
 DCPOâŠ¥ = Î£ ğ““ ê‰ DCPO , has-least (underlying-order ğ““)

 âŸª_âŸ« : DCPOâŠ¥ â†’ DCPO
 âŸª ğ““ , x , p âŸ«  = ğ““

 the-least : (ğ““ : DCPOâŠ¥) â†’ âŸ¨ âŸª ğ““ âŸ« âŸ©
 the-least (ğ““ , x , p) = x

 least-property : (ğ““ : DCPOâŠ¥) â†’ is-least (underlying-order âŸª ğ““ âŸ«) (the-least ğ““)
 least-property (ğ““ , x , p) = p

 axioms-of-dcpo : (ğ““ : DCPO) â†’ dcpo-axioms (underlying-order ğ““)
 axioms-of-dcpo (D , _âŠ‘_ , d) = d

 sethood : (ğ““ : DCPO) â†’ is-set âŸ¨ ğ““ âŸ©
 sethood (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = s

 prop-valuedness : (ğ““ : DCPO) â†’ is-prop-valued (underlying-order ğ““ )
 prop-valuedness (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = p

 reflexivity : (ğ““ : DCPO) â†’ is-reflexive (underlying-order ğ““)
 reflexivity (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = r

 transitivity : (ğ““ : DCPO) â†’ is-transitive (underlying-order ğ““)
 transitivity (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = t

 antisymmetry : (ğ““ : DCPO) â†’ is-antisymmetric (underlying-order ğ““)
 antisymmetry (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = a

 directed-completeness : (ğ““ : DCPO) â†’ is-directed-complete (underlying-order ğ““)
 directed-completeness (D , _âŠ‘_ , (s  , p  , r  , t  , a  , c )) = c

 is-Directed : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©) â†’ ğ“¥ âŠ” ğ“£ Ì‡
 is-Directed ğ““ Î± = is-directed (underlying-order ğ““) Î±

 is-Directed-gives-inhabited : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ Î± â†’ âˆ¥ I âˆ¥
 is-Directed-gives-inhabited ğ““ Î± = prâ‚

 is-Directed-order : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } (Î± : I â†’ âŸ¨ ğ““ âŸ©) â†’ is-Directed ğ““ Î±
                   â†’ (i j : I) â†’ âˆƒ k ê‰ I , (Î± i âŠ‘âŸ¨ ğ““ âŸ© Î± k) Ã— (Î± j âŠ‘âŸ¨ ğ““ âŸ© Î± k)
 is-Directed-order ğ““ Î± = prâ‚‚

 âˆ : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} â†’ is-Directed ğ““ Î± â†’ âŸ¨ ğ““ âŸ©
 âˆ ğ““ {I} {Î±} Î´ = prâ‚ (directed-completeness ğ““ I Î± Î´)

 âˆ-is-sup : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} (Î´ : is-Directed ğ““ Î±)
          â†’ ((i : I) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© âˆ ğ““ Î´)
          Ã— ((u : âŸ¨ ğ““ âŸ©) â†’ ((i : I) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© u) â†’ âˆ ğ““ Î´ âŠ‘âŸ¨ ğ““ âŸ© u)
 âˆ-is-sup ğ““ {I} {Î±} Î´ = prâ‚‚ (directed-completeness ğ““ I Î± Î´)

 âˆ-is-upperbound : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} (Î´ : is-Directed ğ““ Î±)
                 â†’ ((i : I) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© âˆ ğ““ Î´)
 âˆ-is-upperbound ğ““ Î´ = prâ‚ (âˆ-is-sup ğ““ Î´)

 âˆ-is-lowerbound-of-upperbounds : (ğ““ : DCPO) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©} (Î´ : is-Directed ğ““ Î±)
                                â†’ ((u : âŸ¨ ğ““ âŸ©) â†’ ((i : I) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© u) â†’ âˆ ğ““ Î´ âŠ‘âŸ¨ ğ““ âŸ© u)
 âˆ-is-lowerbound-of-upperbounds ğ““ Î´ = prâ‚‚ (âˆ-is-sup ğ““ Î´)

\end{code}

We introduce pretty syntax for chain reasoning with inequalities.
(Cf. â‰¡âŸ¨_âŸ© and âˆ in Id.lagda, â‰ƒâŸ¨_âŸ© and â–  in UF-Equiv.lagda)

For example, given (a b c d : âŸ¨ ğ““ âŸ©) and
u : a âŠ‘âŸ¨ ğ““ âŸ© b
v : b âŠ‘âŸ¨ ğ““ âŸ© c
w : c âŠ‘âŸ¨ ğ““ âŸ© d

this will allow us to write

z : a âŠ‘âŸ¨ ğ““ âŸ© d
z = a âŠ‘âŸ¨ ğ““ âŸ©[ u ]
    b âŠ‘âŸ¨ ğ““ âŸ©[ v ]
    c âŠ‘âŸ¨ ğ““ âŸ©[ w ]
    d âˆâŸ¨ ğ““ âŸ©

rather than the hard(er) to read

z : a âŠ‘âŸ¨ ğ““ âŸ© d
z = transitivity ğ““ a c d z' w
 where
  z' : a âŠ‘âŸ¨ ğ““ âŸ© c
  z' = transitivity ğ““ a b c u v

\begin{code}

 transitivity' : (ğ““ : DCPO) (x : âŸ¨ ğ““ âŸ©) {y z : âŸ¨ ğ““ âŸ©}
               â†’ x âŠ‘âŸ¨ ğ““ âŸ© y â†’ y âŠ‘âŸ¨ ğ““ âŸ© z â†’ x âŠ‘âŸ¨ ğ““ âŸ© z
 transitivity' ğ““ x {y} {z} u v = transitivity ğ““ x y z u v

 syntax transitivity' ğ““ x u v = x âŠ‘âŸ¨ ğ““ âŸ©[ u ] v
 infixr 0 transitivity'

 syntax reflexivity ğ““ x = x âˆâŸ¨ ğ““ âŸ©
 infix 1 reflexivity

\end{code}

Next, we define continuous maps between dcpos.

\begin{code}

is-monotone : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'}) â†’ (âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©) â†’ ğ“¤ âŠ” ğ“£ âŠ” ğ“£' Ì‡
is-monotone ğ““ ğ“” f = (x y : âŸ¨ ğ““ âŸ©) â†’ x âŠ‘âŸ¨ ğ““ âŸ© y â†’ f x âŠ‘âŸ¨ ğ“” âŸ© f y

is-continuous : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
              â†’ (âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©)
              â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ âŠ” ğ“¤' âŠ” ğ“£' Ì‡
is-continuous ğ““ ğ“” f = (I : ğ“¥ Ì‡ ) (Î± : I â†’ âŸ¨ ğ““ âŸ©) (Î´ : is-Directed ğ““ Î±)
                     â†’ is-sup (underlying-order ğ“”) (f (âˆ ğ““ Î´)) (f âˆ˜ Î±)

being-continuous-is-prop : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'}) (f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©)
                           â†’ is-prop (is-continuous ğ““ ğ“” f)
being-continuous-is-prop ğ““ ğ“” f =
   Î -is-prop fe
    (Î» I â†’ Î -is-prop fe
            (Î» Î± â†’ Î -is-prop fe
                     (Î» Î´ â†’ is-sup-is-prop (underlying-order ğ“”)
                            (axioms-of-dcpo ğ“”) (f (âˆ ğ““ Î´)) (f âˆ˜ Î±))))

DCPO[_,_] : DCPO {ğ“¤} {ğ“£} â†’ DCPO {ğ“¤'} {ğ“£'} â†’ ğ“¥ âº âŠ” ğ“¤ âŠ” ğ“£ âŠ” ğ“¤' âŠ” ğ“£' Ì‡
DCPO[ ğ““ , ğ“” ] = Î£ f ê‰ (âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©), is-continuous ğ““ ğ“” f

DCPOâŠ¥[_,_] : DCPOâŠ¥ {ğ“¤} {ğ“£} â†’ DCPOâŠ¥ {ğ“¤'} {ğ“£'} â†’ (ğ“¥ âº) âŠ” ğ“¤ âŠ” ğ“£ âŠ” ğ“¤' âŠ” ğ“£' Ì‡
DCPOâŠ¥[ ğ““ , ğ“” ] = DCPO[ âŸª ğ““ âŸ« , âŸª ğ“” âŸ« ]

underlying-function : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                    â†’ DCPO[ ğ““ , ğ“” ] â†’ âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©
underlying-function ğ““ ğ“” (f , _) = f

continuity-of-function : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'}) (f : DCPO[ ğ““ , ğ“” ])
                       â†’ is-continuous ğ““ ğ“” (underlying-function ğ““ ğ“” f)
continuity-of-function ğ““ ğ“” (_ , c) = c

continuous-functions-are-monotone : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                                    (f : DCPO[ ğ““ , ğ“” ])
                                  â†’ is-monotone ğ““ ğ“” (underlying-function ğ““ ğ“” f)
continuous-functions-are-monotone ğ““ ğ“” (f , cts) x y l = Î³
  where
   Î± : ğŸ™ {ğ“¥} + ğŸ™ {ğ“¥} â†’ âŸ¨ ğ““ âŸ©
   Î± (inl *) = x
   Î± (inr *) = y
   Î´ : is-Directed ğ““ Î±
   Î´ = (âˆ£ inl * âˆ£ , Îµ)
    where
     Îµ : (i j : ğŸ™ + ğŸ™) â†’ âˆƒ (\k â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© Î± k Ã— Î± j âŠ‘âŸ¨ ğ““ âŸ© Î± k)
     Îµ (inl *) (inl *) = âˆ£ inr * , l , l âˆ£
     Îµ (inl *) (inr *) = âˆ£ inr * , l , reflexivity ğ““ y âˆ£
     Îµ (inr *) (inl *) = âˆ£ inr * , reflexivity ğ““ y , l âˆ£
     Îµ (inr *) (inr *) = âˆ£ inr * , reflexivity ğ““ y , reflexivity ğ““ y âˆ£
   a : y â‰¡ âˆ ğ““ Î´
   a = antisymmetry ğ““ y (âˆ ğ““ Î´)
           (âˆ-is-upperbound ğ““ Î´ (inr *))
           (âˆ-is-lowerbound-of-upperbounds ğ““ Î´ y h)
    where
     h : (i : ğŸ™ + ğŸ™) â†’ Î± i âŠ‘âŸ¨ ğ““ âŸ© y
     h (inl *) = l
     h (inr *) = reflexivity ğ““ y
   b : is-sup (underlying-order ğ“”) (f y) (f âˆ˜ Î±)
   b = transport (Î» - â†’ is-sup (underlying-order ğ“”) - (f âˆ˜ Î±)) (ap f (a â»Â¹))
       (cts (ğŸ™ + ğŸ™) Î± Î´)
   Î³ : f x âŠ‘âŸ¨ ğ“” âŸ© f y
   Î³ = is-sup-gives-is-upperbound (underlying-order ğ“”) b (inl *)

constant-functions-are-continuous : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                                    (e : âŸ¨ ğ“” âŸ©) â†’ is-continuous ğ““ ğ“” (Î» d â†’ e)
constant-functions-are-continuous ğ““ ğ“” e I Î± Î´ = u , v where
 u : (i : I) â†’ e âŠ‘âŸ¨ ğ“” âŸ© e
 u i = reflexivity ğ“” e
 v : (y : âŸ¨ ğ“” âŸ©) â†’ ((i : I) â†’ e âŠ‘âŸ¨ ğ“” âŸ© y) â†’ e âŠ‘âŸ¨ ğ“” âŸ© y
 v y l  = âˆ¥âˆ¥-rec (prop-valuedness ğ“” e y) (Î» (i : I) â†’ l i)
          (is-directed-gives-inhabited (underlying-order ğ““) Î± Î´)

image-is-directed : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                    (f : DCPO[ ğ““ , ğ“” ]) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©}
                  â†’ is-Directed ğ““ Î±
                  â†’ is-Directed ğ“” ((underlying-function ğ““ ğ“” f) âˆ˜ Î±)
image-is-directed ğ““ ğ“” (f , c) {I} {Î±} Î´ =
 (is-Directed-gives-inhabited ğ““ Î± Î´) , Î³
  where
   Î³ : (i j : I)
     â†’ âˆƒ k ê‰ I , f (Î± i) âŠ‘âŸ¨ ğ“” âŸ© f (Î± k) Ã— f (Î± j) âŠ‘âŸ¨ ğ“” âŸ© f (Î± k)
   Î³ i j = âˆ¥âˆ¥-functor h (is-Directed-order ğ““ Î± Î´ i j)
    where
     h : (Î£ k ê‰ I , (Î± i) âŠ‘âŸ¨ ğ““ âŸ© (Î± k) Ã— (Î± j) âŠ‘âŸ¨ ğ““ âŸ© (Î± k))
       â†’ Î£ k ê‰ I , f (Î± i) âŠ‘âŸ¨ ğ“” âŸ© f (Î± k) Ã— f (Î± j) âŠ‘âŸ¨ ğ“” âŸ© f (Î± k)
     h (k , l , m) =
      k , (continuous-functions-are-monotone ğ““ ğ“” (f , c) (Î± i) (Î± k) l ,
      (continuous-functions-are-monotone ğ““ ğ“” (f , c) (Î± j) (Î± k) m))

continuous-function-âˆ-â‰¡ : (ğ““ : DCPO {ğ“¤} {ğ“£}) (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                          (f : DCPO[ ğ““ , ğ“” ]) {I : ğ“¥ Ì‡ } {Î± : I â†’ âŸ¨ ğ““ âŸ©}
                          (Î´ : is-Directed ğ““ Î±)
                        â†’ (underlying-function ğ““ ğ“” f) (âˆ ğ““ Î´) â‰¡
                          âˆ ğ“” (image-is-directed ğ““ ğ“” f Î´)
continuous-function-âˆ-â‰¡ ğ““ ğ“” (f , c) {I} {Î±} Î´ =
 antisymmetry ğ“” (f (âˆ ğ““ Î´)) (âˆ ğ“” (image-is-directed ğ““ ğ“” (f , c) Î´)) a b
  where
   s : is-sup (underlying-order ğ“”) (f (âˆ ğ““ Î´)) (f âˆ˜ Î±)
   s = c I Î± Î´
   Îµ : is-Directed ğ“” (f âˆ˜ Î±)
   Îµ = image-is-directed ğ““ ğ“” (f , c) Î´
   a : f (âˆ ğ““ Î´) âŠ‘âŸ¨ ğ“” âŸ© âˆ ğ“” (image-is-directed ğ““ ğ“” (f , c) Î´)
   a = is-sup-gives-is-lowerbound-of-upperbounds (underlying-order ğ“”) s
       (âˆ ğ“” (image-is-directed ğ““ ğ“” (f , c) Î´))
       (âˆ-is-upperbound ğ“” Îµ)
   b : âˆ ğ“” Îµ  âŠ‘âŸ¨ ğ“” âŸ© f (âˆ ğ““ Î´)
   b = âˆ-is-lowerbound-of-upperbounds ğ“” Îµ (f (âˆ ğ““ Î´))
       (is-sup-gives-is-upperbound (underlying-order ğ“”) s)

\end{code}
