Martin Escardo 17th December 2018. (This has a connection with injectivity.)

We have a look at the algebras of the lifting monad.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

module Lifting.Algebras
        (ğ“£ : Universe)
       where

open import UF.Base
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Univalence

open import Lifting.IdentityViaSIP ğ“£
open import Lifting.Lifting ğ“£
open import Lifting.Monad ğ“£

\end{code}

An element of ğ“› (ğ“› X) amounts to a family of partial elements of X
indexed by a proposition:

\begin{code}

double-ğ“›-charac : (X : ğ“¤ Ì‡ )
                â†’ ğ“› (ğ“› X) â‰ƒ (Î£ P ê‰ ğ“£ Ì‡
                                 , (Î£ Q ê‰ (P â†’ ğ“£ Ì‡ )
                                        , ((p : P) â†’ Q p â†’ X)
                                        Ã— ((p : P) â†’ is-prop (Q p)))
                                 Ã— is-prop P)
double-ğ“›-charac X = Î£-cong (Î» P â†’ Ã—-cong (Î³ X P) (â‰ƒ-refl (is-prop P)))
 where
  Î³ : (X : ğ“¤ Ì‡ ) (P : ğ“£ Ì‡ )
    â†’ (P â†’ ğ“› X) â‰ƒ (Î£ Q ê‰ (P â†’ ğ“£ Ì‡ ), ((p : P) â†’ Q p â†’ X) Ã— ((p : P) â†’ is-prop (Q p)))
  Î³ X P = (P â†’ Î£ Q ê‰ ğ“£ Ì‡ , (Q â†’ X) Ã— is-prop Q)                                 â‰ƒâŸ¨ I âŸ©
          (Î£ Q ê‰ (P â†’ ğ“£ Ì‡ ), ((p : P) â†’ ((Q p â†’ X) Ã— is-prop (Q p))))           â‰ƒâŸ¨ II âŸ©
          (Î£ Q ê‰ (P â†’ ğ“£ Ì‡ ), ((p : P) â†’ Q p â†’ X) Ã— ((p : P) â†’ is-prop (Q p)))   â– 
           where
            I  = Î Î£-distr-â‰ƒ
            II = Î£-cong (Î» Q â†’ â†’Ã—)
\end{code}

The usual definition of algebra of a monad and construction of free
algebras:

\begin{code}

ğ“›-algebra : ğ“¤ Ì‡ â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
ğ“›-algebra X = Î£ s ê‰ (ğ“› X â†’ X) , (s âˆ˜ Î· âˆ¼ id) Ã— (s âˆ˜ Î¼ âˆ¼ s âˆ˜ ğ“›Ì‡ s)

free-ğ“›-algebra : is-univalent ğ“£ â†’ (X : ğ“¤ Ì‡ ) â†’ ğ“›-algebra (ğ“› X)
free-ğ“›-algebra ua X = Î¼ , ğ“›-unit-leftâˆ¼ ua , ğ“›-assocâˆ¼ ua

\end{code}

We can describe algebras in terms of "join" operations subject to two
laws:

\begin{code}

joinop : ğ“¤ Ì‡ â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
joinop X = {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ X) â†’ X

\end{code}

The intuitive idea is that a "join" operation on X consists of, for
each proposition P, a map (P â†’ X) â†’ X that "puts together" the
elements of a family f : P â†’ X to get an element âˆ f of X.

Unfortunately, we won't be able to write simply âˆ f in Agda notation,
as the witness that P is a proposition can almost never be
automatically inferred and hence has to be written explicitly.

To characterize algebras, the join operations have two satisfy the
following two laws:

\begin{code}

ğ“›-alg-Lawâ‚€ : {X : ğ“¤ Ì‡ } â†’ joinop X â†’ ğ“¤ Ì‡
ğ“›-alg-Lawâ‚€ {ğ“¤} {X} âˆ = (x : X) â†’ âˆ ğŸ™-is-prop (Î» (p : ğŸ™) â†’ x) ï¼ x

ğ“›-alg-Lawâ‚ : {X : ğ“¤ Ì‡ } â†’ joinop X â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
ğ“›-alg-Lawâ‚ {ğ“¤} {X} âˆ = (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ )
                        (i : is-prop P) (j : (p : P) â†’ is-prop (Q p))
                        (f : Î£ Q â†’ X)
                      â†’ âˆ (Î£-is-prop i j) f ï¼ âˆ i (Î» p â†’ âˆ (j p) (Î» q â†’ f (p , q)))

\end{code}

Omitting the witnesses of proposition-hood, the above two laws can be
written in more standard mathematical notation as follows:

    âˆ  x = x
   p:ğŸ™

    âˆ          f r  =  âˆ   âˆ     f (p , q)
  r : Î£ {P} Q         p:P q:Q(p)


\begin{code}

ğ“›-alg : ğ“¤ Ì‡ â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
ğ“›-alg X = Î£ âˆ ê‰ joinop X , ğ“›-alg-Lawâ‚€ âˆ Ã— ğ“›-alg-Lawâ‚ âˆ

\end{code}

Before proving that we have an equivalence

  ğ“›-algebra X â‰ƒ ğ“›-alg X,

we characterize the algebra morphisms in terms of joins (unfortunately
overloading is not available):

\begin{code}

â‹ : {X : ğ“¤ Ì‡ } â†’ (ğ“› X â†’ X) â†’ joinop X
â‹ s {P} i f = s (P , f , i)

âˆÌ‡ : {X : ğ“¤ Ì‡ } â†’ ğ“›-algebra X â†’ joinop X
âˆÌ‡ (s , _) = â‹ s

âˆ : {X : ğ“¤ Ì‡ } â†’ ğ“›-alg X â†’ joinop X
âˆ (âˆ , Îº , Î¹) = âˆ

lawâ‚€ : {X : ğ“¤ Ì‡ } (a : ğ“›-alg X) â†’ ğ“›-alg-Lawâ‚€ (âˆ a)
lawâ‚€ (âˆ , Îº , Î¹) = Îº

lawâ‚ : {X : ğ“¤ Ì‡ } (a : ğ“›-alg X) â†’ ğ“›-alg-Lawâ‚ (âˆ a)
lawâ‚ (âˆ , Îº , Î¹) = Î¹


\end{code}

The algebra morphisms are the maps that preserve joins. Omitting the
first argument of â‹, the following says that the morphisms are the
maps h : X â†’ Y with

  h (â‹ f) ï¼ â‹ h (f p)
            p:P

for all f:Pâ†’X.

\begin{code}

ğ“›-morphism-charac : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                    (s : ğ“› X â†’ X) (t : ğ“› Y â†’ Y)
                    (h : X â†’ Y)

                  â†’ (h âˆ˜ s âˆ¼ t âˆ˜ ğ“›Ì‡ h)
                  â‰ƒ ({P : ğ“£ Ì‡ } (i : is-prop P) (f : P â†’ X)
                       â†’ h (â‹ s i f) ï¼ â‹ t i (Î» p â†’ h (f p)))
ğ“›-morphism-charac s t h = qinveq (Î» H {P} i f â†’ H (P , f , i))
                                 ((Î» {Ï€ (P , f , i) â†’ Ï€ {P} i f}) ,
                                 (Î» _ â†’ refl) ,
                                 (Î» _ â†’ refl))

\end{code}

We name the other two projections of ğ“›-alg:

\begin{code}

ğ“›-alg-const : {X : ğ“¤ Ì‡ } (A : ğ“›-alg X) â†’ (x : X) â†’ âˆ A ğŸ™-is-prop (Î» (p : ğŸ™) â†’ x) ï¼ x
ğ“›-alg-const (âˆ , Îº , Î¹) = Îº

ğ“›-alg-iterated : {X : ğ“¤ Ì‡ } (A : ğ“›-alg X)
                 (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ )
                 (i : is-prop P) (j : (p : P) â†’ is-prop (Q p))
                 (f : Î£ Q â†’ X)
               â†’ âˆ A (Î£-is-prop i j) f ï¼ âˆ A i (Î» p â†’ âˆ A (j p) (Î» q â†’ f (p , q)))
ğ“›-alg-iterated (âˆ , Îº , Î¹) = Î¹

\end{code}

We could write a proof of the following characterization of the
ğ“›-algebras by composing equivalences as above, but it seems more
direct, and just as clear, to write a direct proof, by construction of
the equivalence and its inverse. This also emphasizes that the
equivalence is definitional, in the sense that the two required
equations hold definitionally.

\begin{code}

ğ“›-algebra-gives-alg : {X : ğ“¤ Ì‡ } â†’ ğ“›-algebra X â†’ ğ“›-alg X
ğ“›-algebra-gives-alg (s , unit , assoc) =
  â‹ s ,
  unit ,
  (Î» P Q i j f â†’ assoc (P , (Î» p â†’ Q p , (Î» q â†’ f (p , q)) , j p) , i))

ğ“›-alg-gives-algebra : {X : ğ“¤ Ì‡ } â†’ ğ“›-alg X â†’ ğ“›-algebra X
ğ“›-alg-gives-algebra {ğ“¤} {X} (âˆ , unit , Î¹) = s , unit , assoc
 where
  s : ğ“› X â†’ X
  s (P , f , i) = âˆ i f

  assoc : s âˆ˜ Î¼ âˆ¼ s âˆ˜ ğ“›Ì‡ s
  assoc (P , g , i) = Î¹ P (prâ‚ âˆ˜ g) i
                       (Î» p â†’ prâ‚‚ (prâ‚‚ (g p)))
                       (Î» r â†’ prâ‚ (prâ‚‚ (g (prâ‚ r))) (prâ‚‚ r))

ğ“›-alg-charac : {X : ğ“¤ Ì‡ } â†’ ğ“›-algebra X â‰ƒ ğ“›-alg X
ğ“›-alg-charac = qinveq
                ğ“›-algebra-gives-alg
                (ğ“›-alg-gives-algebra , ((Î» _ â†’ refl) , (Î» _ â†’ refl)))
\end{code}

We now consider an equivalent of ğ“›-alg-Lawâ‚€ (which is useful e.g. for
type injectivity purposes).

\begin{code}

ğ“›-alg-Lawâ‚€' : {X : ğ“¤ Ì‡ } â†’ joinop X â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
ğ“›-alg-Lawâ‚€' {ğ“¤} {X} âˆ = (P : ğ“£ Ì‡ )
                        (i : is-prop P)
                        (f : P â†’ X)
                        (p : P)
                      â†’ âˆ i f ï¼ f p

ğ“›-alg-Lawâ‚€-givesâ‚€' : propext ğ“£
                   â†’ funext ğ“£ ğ“£
                   â†’ funext ğ“£ ğ“¤
                   â†’ {X : ğ“¤ Ì‡ }
                     (âˆ : joinop X)
                   â†’ ğ“›-alg-Lawâ‚€ âˆ
                   â†’ ğ“›-alg-Lawâ‚€' âˆ
ğ“›-alg-Lawâ‚€-givesâ‚€' pe fe fe' {X} âˆ Îº P i f p = Î³
 where
  r : f ï¼ Î» (_ : P) â†’ f p
  r = dfunext fe' (Î» p' â†’ ap f (i p' p))

  s : P ï¼ ğŸ™ â†’ âˆ {P} i f ï¼ âˆ {ğŸ™} ğŸ™-is-prop (Î» (_ : ğŸ™) â†’ f p)
  s refl = apâ‚‚ âˆ (being-prop-is-prop fe i ğŸ™-is-prop) r

  t : P ï¼ ğŸ™
  t = pe i ğŸ™-is-prop unique-to-ğŸ™ (Î» _ â†’ p)

  Î³ = âˆ {P} i f                   ï¼âŸ¨ s t âŸ©
      âˆ ğŸ™-is-prop (f âˆ˜ (Î» _ â†’ p)) ï¼âŸ¨ Îº (f p) âŸ©
      f p                         âˆ

ğ“›-alg-Lawâ‚€'-givesâ‚€ : {X : ğ“¤ Ì‡ }
                     (âˆ : joinop X)
                    â†’ ğ“›-alg-Lawâ‚€' âˆ
                    â†’ ğ“›-alg-Lawâ‚€ âˆ
ğ“›-alg-Lawâ‚€'-givesâ‚€ {ğ“¤} {X} âˆ Ï† x = Ï† ğŸ™ ğŸ™-is-prop (Î» _ â†’ x) â‹†

\end{code}

We now consider a non-dependent version of ğ“›-alg-Lawâ‚, and show that it is
equivalent to ğ“›-alg-Lawâ‚:

\begin{code}

ğ“›-alg-Lawâ‚' : {X : ğ“¤ Ì‡ } â†’ joinop X â†’ ğ“£ âº âŠ” ğ“¤ Ì‡
ğ“›-alg-Lawâ‚' {ğ“¤} {X} âˆ = (P Q : ğ“£ Ì‡ )
                        (i : is-prop P)
                        (j : is-prop Q)
                        (f : P Ã— Q â†’ X)
                           â†’ âˆ (Ã—-is-prop i j) f
                           ï¼ âˆ i (Î» p â†’ âˆ j (Î» q â†’ f (p , q)))

\end{code}

The difference with ğ“›-alg-Lawâ‚ is that the family f has type P Ã— Q â†’ X
rather than Î£ {P} Q â†’ X, and so the modified, logically equivalent law
amounts to

    âˆ   âˆ   f (p , q) =   âˆ        f r
   p:P q:Q              r : P Ã— Q

One direction of the logical equivalence is trivial:

\begin{code}

ğ“›-alg-Lawâ‚-givesâ‚' : {X : ğ“¤ Ì‡ } (âˆ : joinop X)
                   â†’ ğ“›-alg-Lawâ‚ âˆ â†’ ğ“›-alg-Lawâ‚' âˆ
ğ“›-alg-Lawâ‚-givesâ‚' {ğ“¤} {X} âˆ a P Q i j = a P (Î» _ â†’ Q) i (Î» p â†’ j)

\end{code}

To establish the converse we need the following lemma for joins, which
is interesting on its own right,

  âˆ  f p ï¼ âˆ  f (k q),
 p:P      q:Q

and also gives self-distributivity of joins:

  âˆ   âˆ  f (p , q) =   âˆ   âˆ  f (p , q)
 p:P q:Q              q:Q p:P


\begin{code}

change-of-variables-in-join : {X : ğ“¤ Ì‡ } (âˆ : joinop X)
                              (P : ğ“£ Ì‡ ) (i : is-prop P)
                              (Q : ğ“£ Ì‡ ) (j : is-prop Q)
                              (h : P â†’ Q) (k : Q â†’ P)
                              (f : P â†’ X)
                            â†’ is-univalent ğ“£
                            â†’ âˆ i f ï¼ âˆ j (f âˆ˜ k)

change-of-variables-in-join âˆ P i Q j h k f ua = Î³
 where
  cd : (r : Q ï¼ P) â†’ âˆ i f ï¼ âˆ j (f âˆ˜ Idtofun r)
  cd refl = ap (Î» - â†’ âˆ - f) (being-prop-is-prop (univalence-gives-funext ua) i j)

  e : Q â‰ƒ P
  e = qinveq k (h , ((Î» q â†’ j (h (k q)) q) , Î» p â†’ i (k (h p)) p))

  a : Idtofun (eqtoid ua Q P e) ï¼ k
  a = ap âŒœ_âŒ (idtoeq'-eqtoid ua Q P e)

  Î³ : âˆ i f ï¼ âˆ j (f âˆ˜ k)
  Î³ = cd (eqtoid ua Q P e) âˆ™ ap (Î» - â†’ âˆ j (f âˆ˜ -)) a

ğ“›-alg-self-distr : {X : ğ“¤ Ì‡ } (âˆ : joinop X)
                   (P : ğ“£ Ì‡ ) (i : is-prop P)
                   (Q : ğ“£ Ì‡ ) (j : is-prop Q)
                 â†’ is-univalent ğ“£
                 â†’ ğ“›-alg-Lawâ‚' âˆ
                 â†’ (f : P Ã— Q â†’ X)
                      â†’ âˆ i (Î» p â†’ âˆ j (Î» q â†’ f (p , q)))
                      ï¼ âˆ j (Î» q â†’ âˆ i (Î» p â†’ f (p , q)))

ğ“›-alg-self-distr âˆ P i Q j ua lâ‚' f =
 âˆ i (Î» p â†’ âˆ j (Î» q â†’ f (p , q)))                     ï¼âŸ¨ a âŸ©
 âˆ (Î£-is-prop i (Î» p â†’ j)) f                           ï¼âŸ¨ b âŸ©
 âˆ (Î£-is-prop j (Î» p â†’ i)) (f âˆ˜ (Î» t â†’ prâ‚‚ t , prâ‚ t)) ï¼âŸ¨ c âŸ©
 âˆ j (Î» q â†’ âˆ i (Î» p â†’ f (p , q)))                     âˆ
  where
   a = (lâ‚' P Q i j f)â»Â¹
   b = change-of-variables-in-join
        âˆ
        (P Ã— Q)
        (Î£-is-prop i (Î» p â†’ j))
        (Q Ã— P)
        (Î£-is-prop j (Î» p â†’ i))
        (Î» t â†’ prâ‚‚ t , prâ‚ t) (Î» t â†’ prâ‚‚ t , prâ‚ t) f ua
   c = lâ‚' Q P j i (Î» t â†’ f (prâ‚‚ t , prâ‚ t))

\end{code}

Using this we can prove the other direction of the logical equivalence
claimed above:

\begin{code}

ğ“›-alg-Lawâ‚'-givesâ‚ : {X : ğ“¤ Ì‡ } (âˆ : joinop X)
                    â†’ is-univalent ğ“£
                    â†’ funext ğ“£ ğ“¤
                    â†’ ğ“›-alg-Lawâ‚' âˆ
                    â†’ ğ“›-alg-Lawâ‚ âˆ
ğ“›-alg-Lawâ‚'-givesâ‚ {ğ“¤} {X} âˆ ua fe a P Q i j f = Î³
 where
  h : (p : P) â†’ Q p â†’ Î£ Q
  h p q = (p , q)

  k : (p : P) â†’ Î£ Q â†’ Q p
  k p (p' , q) = transport Q (i p' p) q

  f' : P Ã— Î£ Q â†’ X
  f' (p , p' , q) = f (p , k p (p' , q))

  k' : Î£ Q â†’ P Ã— Î£ Q
  k' (p , q) = p , p , q

  H : f' âˆ˜ k' âˆ¼ f
  H (p , q) = ap (Î» - â†’ f (p , -)) (j p _ _)

  Î³ = âˆ {Î£ Q} (Î£-is-prop i j) f                                         ï¼âŸ¨ b âŸ©
      âˆ {Î£ Q} (Î£-is-prop i j) (f' âˆ˜ k')                                 ï¼âŸ¨ c â»Â¹ âŸ©
      âˆ {P Ã— Î£ Q} (Ã—-is-prop i (Î£-is-prop i j)) f'                      ï¼âŸ¨ d âŸ©
      âˆ {P} i (Î» p â†’ âˆ {Î£ Q} (Î£-is-prop i j) ((Î» Ïƒ â†’ f (p , Ïƒ)) âˆ˜ k p)) ï¼âŸ¨ e âŸ©
      âˆ {P} i (Î» p â†’ âˆ {Q p} (j p) (Î» q â†’ f (p , q)))                   âˆ
   where
    b = (ap (âˆ {Î£ Q} (Î£-is-prop i j)) (dfunext fe H))â»Â¹
    c = change-of-variables-in-join
         âˆ
         (P Ã— Î£ Q)
         (Ã—-is-prop i (Î£-is-prop i j))
         (Î£ Q)
         (Î£-is-prop i j) prâ‚‚ k' f' ua
    d = a P (Î£ Q) i (Î£-is-prop i j) (Î» z â†’ f (prâ‚ z , k (prâ‚ z) (prâ‚‚ z)))
    e = (ap (âˆ {P} i)
          (dfunext fe (Î» p â†’ change-of-variables-in-join
                              âˆ
                              (Q p)
                              (j p)
                              (Î£ Q) (Î£-is-prop i j)
                              (h p) (k p) (Î» Ïƒ â†’ f (p , Ïƒ)) ua)))â»Â¹
\end{code}

The algebras form an exponential ideal with the pointwise
operations. More generally:

\begin{code}

Î -is-alg : funext ğ“¤ ğ“¥
         â†’ {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ )
         â†’ ((x : X) â†’ ğ“›-alg (A x)) â†’ ğ“›-alg (Î  A)
Î -is-alg {ğ“¤} {ğ“¥} fe {X} A Î± = âˆÂ· , lâ‚€ , lâ‚
 where
  âˆÂ· : {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ Î  A) â†’ Î  A
  âˆÂ· i f x = âˆ (Î± x) i (Î» p â†’ f p x)

  lâ‚€ : (Ï† : Î  A) â†’ âˆÂ· ğŸ™-is-prop (Î» p â†’ Ï†) ï¼ Ï†
  lâ‚€ Ï† = dfunext fe (Î» x â†’ lawâ‚€ (Î± x) (Ï† x))

  lâ‚ : (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ )
       (i : is-prop P) (j : (p : P) â†’ is-prop (Q p))
       (f : Î£ Q â†’ Î  A)
      â†’ âˆÂ· (Î£-is-prop i j) f
      ï¼ âˆÂ· i (Î» p â†’ âˆÂ· (j p) (Î» q â†’ f (p , q)))
  lâ‚ P Q i j f = dfunext fe (Î» x â†’ lawâ‚ (Î± x) P Q i j (Î» Ïƒ â†’ f Ïƒ x))

\end{code}

This is the case for any monad of a certain kind, but the way we
proved this above using our characterizations of the algebras applies
only to our monad.

The following examples are crucial for injectivity. They say that the
universe is an algebra in at least two ways, with âˆ = Î£ and âˆ = Î 
respectively.

\begin{code}

universe-is-algebra-Î£ : is-univalent ğ“£ â†’ ğ“›-alg (ğ“£ Ì‡ )
universe-is-algebra-Î£ ua = sum , k , Î¹
 where
  sum : {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ ğ“£ Ì‡ ) â†’ ğ“£ Ì‡
  sum {P} i = Î£

  k : (X : ğ“£ Ì‡ ) â†’ Î£ (Î» p â†’ X) ï¼ X
  k X = eqtoid ua (ğŸ™ Ã— X) X ğŸ™-lneutral

  Î¹ : (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ ) (i : is-prop P)
      (j : (p : P) â†’ is-prop (Q p)) (f : Î£ Q â†’ ğ“£ Ì‡ )
    â†’ Î£ f ï¼ Î£ (Î» p â†’ Î£ (Î» q â†’ f (p , q)))
  Î¹ P Q i j f = eqtoid ua _ _ Î£-assoc

universe-is-algebra-Î  : is-univalent ğ“£ â†’ ğ“›-alg (ğ“£ Ì‡ )
universe-is-algebra-Î  ua = prod , k , Î¹
 where
  fe : funext ğ“£ ğ“£
  fe = univalence-gives-funext ua

  prod : {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ ğ“£ Ì‡ ) â†’ ğ“£ Ì‡
  prod {P} i = Î 

  k : (X : ğ“£ Ì‡ ) â†’ Î  (Î» p â†’ X) ï¼ X
  k X = eqtoid ua (ğŸ™ â†’ X) X (â‰ƒ-sym (ğŸ™â†’ (univalence-gives-funext ua)))

  Î¹ : (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ ) (i : is-prop P)
      (j : (p : P) â†’ is-prop (Q p)) (f : Î£ Q â†’ ğ“£ Ì‡ )
    â†’ Î  f ï¼ Î  (Î» p â†’ Î  (Î» q â†’ f (p , q)))
  Î¹ P Q i j f = eqtoid ua _ _ (curry-uncurry' fe fe)

\end{code}
