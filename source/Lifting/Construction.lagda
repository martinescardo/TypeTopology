Martin Escardo 25th October 2018.

The type of partial elements of a type (or lifting).

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

module Lifting.Construction (ğ“£ : Universe) where

open import UF.Subsingletons
open import UF.SubtypeClassifier hiding (âŠ¥)

ğ“› : ğ“¤ Ì‡ â†’ ğ“£ âº âŠ”  ğ“¤ Ì‡
ğ“› X = Î£ P ê‰ ğ“£ Ì‡ , (P â†’ X) Ã— is-prop P

is-defined : {X : ğ“¤ Ì‡ } â†’ ğ“› X â†’ ğ“£ Ì‡
is-defined (P , Ï† , i) = P

is-def : {X : ğ“¤ Ì‡ } â†’ ğ“› X â†’ Î© ğ“£
is-def (P , Ï† , i) = (P , i)

being-defined-is-prop : {X : ğ“¤ Ì‡ } (l : ğ“›  X) â†’ is-prop (is-defined l)
being-defined-is-prop (P , Ï† , i) = i

value : {X : ğ“¤ Ì‡ } (l : ğ“›  X) â†’ is-defined l â†’ X
value (P , Ï† , i) = Ï†

\end{code}

The "total" elements of ğ“› X:

\begin{code}

Î· : {X : ğ“¤ Ì‡ } â†’ X â†’ ğ“› X
Î· x = ğŸ™ , (Î» _ â†’ x) , ğŸ™-is-prop

\end{code}

Its "undefined" element:

\begin{code}

âŠ¥ : {X : ğ“¤ Ì‡ } â†’ ğ“› X
âŠ¥ = ğŸ˜ , unique-from-ğŸ˜ , ğŸ˜-is-prop

\end{code}

Added 7th November 2025. I don't know why we didn't work with the
following more natural definition.

\begin{code}

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier-Properties

ğ“›' : ğ“¤ Ì‡ â†’ ğ“£ âº âŠ”  ğ“¤ Ì‡
ğ“›' X = Î£ p ê‰ Î© ğ“£ , (p holds â†’ X)

ğ“›-is-equiv-to-ğ“›' : {X : ğ“¤ Ì‡ } â†’ ğ“› X â‰ƒ ğ“›' X
ğ“›-is-equiv-to-ğ“›' {ğ“¤} {X} =
 (Î£ P ê‰ ğ“£ Ì‡ , (P â†’ X) Ã— is-prop P) â‰ƒâŸ¨ Î£-cong (Î» P â†’ Ã—-comm)  âŸ©
 (Î£ P ê‰ ğ“£ Ì‡ , is-prop P Ã— (P â†’ X)) â‰ƒâŸ¨ â‰ƒ-sym Î£-assoc âŸ©
 (Î£ p ê‰ Î© ğ“£ , (p holds â†’ X))     â– 

\end{code}

With this representation, it is easy to prove that ğ“› X is a set if X is.

\begin{code}

open import UF.FunExt

ğ“›-is-set : funext ğ“£ ğ“£
         â†’ funext ğ“£ ğ“¤
         â†’ propext ğ“£
         â†’ {X : ğ“¤ Ì‡ } â†’ is-set X â†’ is-set (ğ“› X)
ğ“›-is-set fe fe' pe X-is-set = equiv-to-set
                                ğ“›-is-equiv-to-ğ“›'
                                (Î£-is-set
                                  (Î©-is-set fe pe)
                                  (Î» p â†’ Î -is-set fe' Î» _ â†’ X-is-set))

\end{code}
