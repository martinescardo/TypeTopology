Martin Escardo, 6th September 2025.

We construct two distinct ğ“›-algebra structures on the subtype
classifier Î©, with structure maps given by Î£ (the free algebra) and Î ,
where ğ“› is the lifting monad, also known as the partial map classifier
monad.

This is just an adaptation of the fact that Î£ and Î  are ğ“›-structure
maps on the universe, already proved in the file Lifting.Algebras,
which uses univalence.

We could prove that Î£ and Î  are structure maps on Î© by showing that a
subtype of an algebra closed under the structure map is itself an
algebra, and apply this to the subtype Î© of the universe. However, we
want a proof that doesn't use univalence, so that it makes sense in
the internal language of a 1-topos. We use propositional and
functional extensionality instead, which are validated in any 1-topos.

So notice that not even classically do we have "every algebra is a
free algebra". What we do have classically (i.e. in boolean toposes)
is that the underlying set of any algebra is isomorphic to the
underlying set of a free algebra, that is, it is isomorphic to ğ“› X
for some X.

Question. In an arbitrary 1-topos, is it the case that the underlying
set of any algebra is isomorphic to ğ“› X for some X?

I very much doubt that this would be the case.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.Subsingletons

module Lifting.TwoAlgebrasOnOmega
        (ğ“£ : Universe)
        (fe : Fun-Ext)
        (pe : propext ğ“£)
       where

open import Lifting.Algebras ğ“£
open import UF.Base
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier

private
 sum : {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ Î© ğ“£) â†’ Î© ğ“£
 sum {P} i f = (Î£ p ê‰ P , f p holds) ,
               (Î£-is-prop i (Î» p â†’ holds-is-prop (f p)))

Î£-algebra-on-Î© : ğ“›-alg (Î© ğ“£)
Î£-algebra-on-Î© = sum , k , Î¹
 where
  k : (P : Î© ğ“£) â†’ sum ğŸ™-is-prop (Î» (_ : ğŸ™) â†’ P) ï¼ P
  k P = Î©-extensionality' pe fe ğŸ™-lneutral

  Î¹ : (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ ) (i : is-prop P)
      (j : (p : P) â†’ is-prop (Q p)) (f : Î£ Q â†’ Î© ğ“£)
    â†’ sum (Î£-is-prop i j) f
    ï¼ sum i (Î» p â†’ sum (j p) (Î» q â†’ f (p , q)))
  Î¹ P Q i j f = Î©-extensionality' pe fe Î£-assoc

private
 prod : {P : ğ“£ Ì‡ } â†’ is-prop P â†’ (P â†’ Î© ğ“£) â†’ Î© ğ“£
 prod {P} i f = (Î  p ê‰ P , f p holds) ,
                 Î -is-prop fe (Î» p â†’ holds-is-prop (f p))

Î -algebra-on-Î© : ğ“›-alg (Î© ğ“£)
Î -algebra-on-Î© = prod , k , Î¹
 where
  k : (P : Î© ğ“£) â†’ prod ğŸ™-is-prop (Î» (_ : ğŸ™) â†’ P) ï¼ P
  k P = Î©-extensionality' pe fe (â‰ƒ-sym (ğŸ™â†’ fe))

  Î¹ : (P : ğ“£ Ì‡ ) (Q : P â†’ ğ“£ Ì‡ ) (i : is-prop P)
      (j : (p : P) â†’ is-prop (Q p)) (f : Î£ Q â†’ Î© ğ“£)
    â†’ prod (Î£-is-prop i j) f
     ï¼ prod i (Î» p â†’ prod (j p) (Î» q â†’ f (p , q)))
  Î¹ P Q i j f = Î©-extensionality' pe fe (curry-uncurry' fe fe)

Î£-and-Î -disagree
 : Â¬ (  {P : ğ“£ Ì‡ }
        (i : is-prop P)
        (f : P â†’ Î© ğ“£)
      â†’ (Î£ p ê‰ P , f p holds) ï¼ (Î  p ê‰ P , f p holds))
Î£-and-Î -disagree a
 = II
 where
  I = ğŸ˜       â‰ƒâŸ¨ Ã—ğŸ˜ âŸ©
      ğŸ˜ Ã— ğŸ˜   â‰ƒâŸ¨ idtoeq _ _ (a ğŸ˜-is-prop Î» _ â†’ (ğŸ˜ , ğŸ˜-is-prop)) âŸ©
      (ğŸ˜ â†’ ğŸ˜) â‰ƒâŸ¨ â‰ƒ-sym (ğŸ˜â†’ fe) âŸ©
      ğŸ™ {ğ“¤â‚€}  â– 

  II : ğŸ˜
  II = âŒœ I âŒâ»Â¹ â‹†

Î£-and-Î -algebra-on-Î©-disagree : Î£-algebra-on-Î© â‰  Î -algebra-on-Î©
Î£-and-Î -algebra-on-Î©-disagree e = Î£-and-Î -disagree V
  where
   I : (Î» {P} â†’ sum {P}) ï¼ prod
   I = ap prâ‚ e

   II : (P : ğ“£ Ì‡ ) â†’ sum {P} ï¼ prod {P}
   II = implicit-happly I

   III : (P : ğ“£ Ì‡ ) (i : is-prop P) â†’ sum {P} i ï¼ prod {P} i
   III P = happly (II P)

   IV : (P : ğ“£ Ì‡ ) (i : is-prop P) (f : P â†’ Î© ğ“£) â†’ sum {P} i f ï¼ prod {P} i f
   IV P i = happly (III P i)

   V : {P : ğ“£ Ì‡ }
       (i : is-prop P)
       (f : P â†’ Î© ğ“£)
     â†’ (Î£ p ê‰ P , f p holds) ï¼ (Î  p ê‰ P , f p holds)
   V {P} i f = ap prâ‚ (IV P i f)

\end{code}
