Martin Escardo, 2nd December 2025.

In any 1-topos, powers of Î© are free lifting algebras. This result
seems to new new.

The same argument directly generalizes to show that products of free
algebras are free, and this generalization is included in the file
ProductsOfFreeAlgebrasAreFree in the parent directory.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.Subsingletons
open import UF.PropTrunc

module Lifting.PowersOfOmegaAreFreeAlgebras
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (ğ“£  : Universe)
        (X  : ğ“£ Ì‡ )
       where

open import Lifting.Construction ğ“£
open import Lifting.Algebras ğ“£
open import Lifting.Identity ğ“£
open import Lifting.TwoAlgebrasOnOmega ğ“£ fe pe
open import UF.Embeddings
open import UF.Equiv
open import UF.Logic
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier-Properties
open import UF.SubtypeClassifier renaming (Î© to Î©-of-universe)

private
 Î© : ğ“£ âº Ì‡
 Î© = Î©-of-universe ğ“£

 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

open PropositionalTruncation pt
open Conjunction

\end{code}

Our objective is to show that the algebra Î©Ë£ is free. We know it is
an algebra because it is a product of copies of the (free) algebra Î©.

\begin{code}

Î©Ë£ : ğ“£ âº Ì‡
Î©Ë£ = X â†’ Î©

\end{code}

We let Ï€ range over Î©Ë£.

\begin{code}

Î©Ë£-is-set : is-set Î©Ë£
Î©Ë£-is-set = Î -is-set fe (Î» (_ : X) â†’ Î©-is-set fe pe)

Î©Ë£-ğ“›-alg : ğ“›-alg Î©Ë£
Î©Ë£-ğ“›-alg = Î -is-alg fe (Î» (_ : X) â†’ Î©) (Î» (_ : X) â†’ Î£-alg-on-Î©)

âˆ : extension-op Î©Ë£
âˆ = ğ“›-alg-structure-map Î©Ë£-ğ“›-alg

\end{code}

We now consider a notion of positivity for elements of Î©Ë£ (which
agrees with Anders Kock's notion of positivity for this particular
algebra, but we don't need to know this here).

\begin{code}

is-pos : Î©Ë£ â†’ ğ“£ Ì‡
is-pos Ï€ = âˆƒ x ê‰ X , Ï€ x holds

being-pos-is-prop : (Ï€ : Î©Ë£) â†’ is-prop (is-pos Ï€)
being-pos-is-prop Ï€ = âˆƒ-is-prop

is-Pos : Î©Ë£ â†’ Î©
is-Pos Ï€ = is-pos Ï€ , being-pos-is-prop Ï€

\end{code}

We will show that Î©Ë£ is freely generated by its set G of positive
elements, with insertion of generators the embedding Î¹ of G into Î©Ë£.

\begin{code}

G : ğ“£ âº Ì‡
G = Î£ Ï€ ê‰ Î©Ë£ , is-pos Ï€

G-is-set : is-set G
G-is-set = Î£-is-set Î©Ë£-is-set (Î» (Ï€ : Î©Ë£) â†’ props-are-sets (being-pos-is-prop Ï€))

Î¹ : G â†’ Î©Ë£
Î¹ = prâ‚

Î¹-is-pos : (g : G) â†’ is-pos (Î¹ g)
Î¹-is-pos = prâ‚‚

Î¹-is-embedding : is-embedding Î¹
Î¹-is-embedding = prâ‚-is-embedding being-pos-is-prop

\end{code}

Our first step is to show that Î©Ë£ is equivalent to ğ“› G, the canonical
algebra freely generated by G with insertion of generators Î· : G â†’ ğ“› G.

\begin{code}

open free-algebras-in-the-category-of-sets pe fe G G-is-set

ğ“›G : ğ“›-alg (ğ“› G)
ğ“›G = canonical-free-algebra

h : ğ“› G â†’ Î©Ë£
h = ğ“›-extension Î©Ë£-is-set Î©Ë£-ğ“›-alg Î¹

h-explicitly : (l@(P , Ï† , i) : ğ“› G)
             â†’ h l ï¼ Î» x â†’ âˆ‘ (Î» (p : P) â†’ Î¹ (Ï† p) x)
h-explicitly l = by-definition

h-is-hom : is-hom ğ“›G Î©Ë£-ğ“›-alg h
h-is-hom = ğ“›-extension-is-hom Î©Ë£-is-set Î©Ë£-ğ“›-alg Î¹

h-extends-Î¹ : h âˆ˜ Î· âˆ¼ Î¹
h-extends-Î¹ = ğ“›-extension-extends Î©Ë£-is-set Î©Ë£-ğ“›-alg Î¹

\end{code}

Our aim is to fill this diagram with a homomorphism hâ»Â¹ inverting h so
that Î©Ë£ is isomorphic to ğ“› G as a lifting algebra:

       Î·
  G â”€â”€â”€â”€â”€â”€â”€â”€â†’ ğ“› G
   â•²         â”‚  â†‘
    â•²        â”‚  â”‚
     â•²       â”‚  â”‚
    Î¹ â•²    h â”‚  â”‚ hâ»Â¹
       â•²     â”‚  â”‚
        â•²    â”‚  â”‚
         â•²   â”‚  â”‚
          â•²  â†“  â”‚
           â˜  Î©Ë£.

The only insight in this file is the definition of hâ»Â¹. Everything
else is just hard work.

\begin{code}

hâ»Â¹ : Î©Ë£ â†’ ğ“› G
hâ»Â¹ Ï€ = is-pos Ï€ , (Î» (i : is-pos Ï€) â†’ Ï€ , i) , being-pos-is-prop Ï€

hâ»Â¹-is-section : h âˆ˜ hâ»Â¹ âˆ¼ id
hâ»Â¹-is-section Ï€ =
 h (hâ»Â¹ Ï€)                          ï¼âŸ¨ h-explicitly (hâ»Â¹ Ï€) âŸ©
 (Î» x â†’ âˆ‘ (Î» (_ : is-pos Ï€) â†’ Ï€ x)) ï¼âŸ¨by-definitionâŸ©
 (Î» x â†’ is-Pos Ï€ âˆ§ Ï€ x)             ï¼âŸ¨ I âŸ©
 (Î» x â†’ Ï€ x)                        ï¼âŸ¨by-definitionâŸ©
 Ï€                                  âˆ
  where
   I = dfunext fe (Î» x â†’ Î©-extensionality pe fe
                          prâ‚‚
                          (Î» (d : Ï€ x holds) â†’ âˆ£ x , d âˆ£ , d))

\end{code}

To show that hâ»Â¹ is also a retraction, and that it is a homomorphism,
we will use the following two definitional equalities tacitly.

\begin{code}

module _
        (l@(P , Ï† , i) : ğ“› G)
        (Ï† : P â†’ Î©Ë£)
       where

 NBâ‚€ : is-defined (hâ»Â¹ (âˆ i Ï†)) ï¼ (âˆƒ x ê‰ X , Î£ p ê‰ P , Ï† p x holds)
 NBâ‚€ = by-definition

 NBâ‚ : is-defined (â¨† i (hâ»Â¹ âˆ˜ Ï†)) ï¼ (Î£ p ê‰ P , âˆƒ x ê‰ X , Ï† p x holds)
 NBâ‚ = by-definition

hâ»Â¹-is-retraction : hâ»Â¹ âˆ˜ h âˆ¼ id
hâ»Â¹-is-retraction l@(P , Ï† , i) = V
 where
  I : (âˆƒ x ê‰ X , Î£ p ê‰ P , Î¹ (Ï† p) x holds) â†’ P
  I = âˆ¥âˆ¥-rec i (Î» (x , p , d) â†’ p)

  II : P â†’ âˆƒ x ê‰ X , Î£ p ê‰ P , Î¹ (Ï† p) x holds
  II p = âˆ¥âˆ¥-rec âˆƒ-is-prop (Î» (x , d) â†’ âˆ£ x , p , d âˆ£) e
   where
    e : âˆƒ x ê‰ X , Î¹ (Ï† p) x holds
    e = Î¹-is-pos (Ï† p)

  III : {e : âˆƒ x ê‰ X , Î£ p ê‰ P , Î¹ (Ï† p) x holds}
      â†’ (Î» x â†’ âˆ‘ (Î» (p : P) â†’ Î¹ (Ï† p) x)) ï¼ Î¹ (Ï† (I e))
  III {e} = dfunext fe (Î» x â†’ to-subtype-ï¼ (Î» _ â†’ being-prop-is-prop fe) (Iâ‚€ x))
   where
    Iâ‚€ : (x : X) â†’ (Î£ p ê‰ P , Î¹ (Ï† p) x holds) ï¼ (Î¹ (Ï† (I e)) x holds)
    Iâ‚€ x = pe (Î£-is-prop i (Î» p â†’ holds-is-prop (Î¹ (Ï† p) x)))
              (holds-is-prop (Î¹ (Ï† (I e)) x))
              (Î» (p , d) â†’ transport (Î» - â†’ Î¹ (Ï† -) x holds) (i p (I e)) d)
              (Î» (d : Î¹ (Ï† (I e)) x holds) â†’ I e , d)

  IV : value (hâ»Â¹ (h l)) âˆ¼ (Î» x â†’ Ï† (I x))
  IV e = to-subtype-ï¼ being-pos-is-prop (III {e})

  V : hâ»Â¹ (h l) ï¼ l
  V = from-â‹ pe fe fe ((I , II) , IV)

\end{code}

So Î©Ë£ is equivalent to the underlying type of a free algebra.

\begin{code}

Î©Ë£-is-ğ“›G : Î©Ë£ â‰ƒ ğ“› G
Î©Ë£-is-ğ“›G = qinveq hâ»Â¹ (h , hâ»Â¹-is-section , hâ»Â¹-is-retraction)

\end{code}

The equivalence is an algebra homomorphism.

\begin{code}

hâ»Â¹-is-hom : is-hom Î©Ë£-ğ“›-alg ğ“›G hâ»Â¹
hâ»Â¹-is-hom P i Ï† = IV
 where
  I : (âˆƒ x ê‰ X , Î£ p ê‰ P , Ï† p x holds) â†’ (Î£ p ê‰ P , âˆƒ x ê‰ X , Ï† p x holds)
  I = âˆ¥âˆ¥-rec (Î£-is-prop i Î» _ â†’ âˆƒ-is-prop) (Î» (x , p , d) â†’ p , âˆ£ x , d âˆ£)

  II : (Î£ p ê‰ P , âˆƒ x ê‰ X , Ï† p x holds) â†’ (âˆƒ x ê‰ X , Î£ p ê‰ P , Ï† p x holds)
  II (p , e) = âˆ¥âˆ¥-functor (Î» (x , d) â†’ x , p , d) e

  III : value (hâ»Â¹ (âˆ i Ï†)) âˆ¼ (Î» x â†’ value (â¨† i (hâ»Â¹ âˆ˜ Ï†)) (I x))
  III e = IIIâ‚
   where
    p : P
    p = prâ‚ (I e)

    IIIâ‚€ : âˆ i Ï† ï¼ Ï† p
    IIIâ‚€ = ğ“›-alg-Lawâ‚€-givesâ‚€' pe fe fe âˆ (ğ“›-alg-lawâ‚€ Î©Ë£-ğ“›-alg) P i Ï† p

    IIIâ‚ : (âˆ i Ï† , e) ï¼ (Ï† p , prâ‚‚ (I e))
    IIIâ‚ = to-subtype-ï¼ being-pos-is-prop IIIâ‚€

  IV : hâ»Â¹ (âˆ i Ï†) ï¼ â¨† i (hâ»Â¹ âˆ˜ Ï†)
  IV = from-â‹ pe fe fe ((I , II) , III)

\end{code}

This shows that Î©Ë£ equipped with the algebra structure Î©Ë£-ğ“›-alg is
isomorphic to the free algebra ğ“› G in the category of algebras, and
hence is itself free.

\begin{code}

Î©Ë£-is-free-ğ“›-alg : is-free-ğ“›-alg Î©Ë£-ğ“›-alg G Î¹
Î©Ë£-is-free-ğ“›-alg = ğ“›-alg-isomorphic-to-free-ğ“›-alg-is-itself-free pe fe
                    Î©Ë£-is-set
                    G
                    G-is-set
                    Î¹
                    Î©Ë£-ğ“›-alg
                    hâ»Â¹
                    hâ»Â¹-is-section
                    hâ»Â¹-is-retraction
                    hâ»Â¹-is-hom
\end{code}
