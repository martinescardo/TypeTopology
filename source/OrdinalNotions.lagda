Martin Escardo, April 2013.

Adapted to this development June 2018, with further additions.

Ordinals like in the HoTT book and variations.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import DiscreteAndSeparated

open import UF-Base
open import UF-Subsingletons
open import UF-FunExt
open import UF-Subsingletons-FunExt
open import UF-ExcludedMiddle

module OrdinalNotions
        {ğ“¤ ğ“¥ : Universe}
        {X : ğ“¤ Ì‡ }
        (_<_ : X â†’ X â†’ ğ“¥ Ì‡ )
       where

is-prop-valued : ğ“¤ âŠ” ğ“¥ Ì‡
is-prop-valued = (x y : X) â†’ is-prop (x < y)

data is-accessible : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡ where
 next : (x : X) â†’ ((y : X) â†’ y < x â†’ is-accessible y) â†’ is-accessible x

accessible-induction : (P : (x : X) â†’ is-accessible x â†’ ğ“¦ Ì‡ )
                     â†’ ((x : X) (Ïƒ : (y : X) â†’ y < x â†’ is-accessible y)
                         â†’ ((y : X) (l : y < x) â†’ P y (Ïƒ y l))
                         â†’ P x (next x Ïƒ))
                     â†’ (x : X) (a : is-accessible x) â†’ P x a
accessible-induction P f = h
  where
   h : (x : X) (a : is-accessible x) â†’ P x a
   h x (next x Ïƒ) = f x Ïƒ (Î» y l â†’ h y (Ïƒ y l))

prev : (x : X)
     â†’ is-accessible x
     â†’ (y : X) â†’ y < x â†’ is-accessible y
prev = accessible-induction
        (Î» x _ â†’ (y : X) â†’ y < x â†’ is-accessible y)
        (Î» x Ïƒ f â†’ Ïƒ)

prev-behaviour : (x : X) (a : is-accessible x)
               â†’ next x (prev x a) â‰¡ a
prev-behaviour = accessible-induction _ (Î» _ _ _ â†’ refl)

prev-behaviour' : (x : X) (Ïƒ : (y : X) â†’ y < x â†’ is-accessible y)
                â†’ prev x (next x Ïƒ) â‰¡ Ïƒ
prev-behaviour' x Ïƒ = refl

transfinite-induction' :  (P : X â†’ ğ“¦ Ì‡ )
                       â†’ ((x : X) â†’ ((y : X) â†’ y < x â†’ P y) â†’ P x)
                       â†’ (x : X) â†’ is-accessible x â†’ P x
transfinite-induction' P f = accessible-induction
                              (Î» x _ â†’ P x)
                              (Î» x _ â†’ f x)

is-well-founded : ğ“¤ âŠ” ğ“¥ Ì‡
is-well-founded = (x : X) â†’ is-accessible x

Well-founded : ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦  âº Ì‡
Well-founded {ğ“¦} = (P : X â†’ ğ“¦ Ì‡ )
                 â†’ ((x : X) â†’ ((y : X) â†’ y < x â†’ P y) â†’ P x)
                 â†’ (x : X) â†’ P x

transfinite-induction : is-well-founded â†’ âˆ€ {ğ“¦} â†’ Well-founded {ğ“¦}
transfinite-induction w P f x = transfinite-induction' P f x (w x)

transfinite-induction-converse : Well-founded {ğ“¤ âŠ” ğ“¥} â†’ is-well-founded
transfinite-induction-converse Ï† = Ï† is-accessible next

transfinite-recursion : is-well-founded
                      â†’ âˆ€ {ğ“¦} {Y : ğ“¦ Ì‡ }
                      â†’ ((x : X) â†’ ((y : X) â†’ y < x â†’ Y) â†’ Y)
                      â†’ X â†’ Y
transfinite-recursion w {ğ“¦} {Y} = transfinite-induction w (Î» x â†’ Y)

is-transitive : ğ“¤ âŠ” ğ“¥ Ì‡
is-transitive = (x y z : X) â†’ x < y â†’ y < z â†’ x < z

private
  _â‰¼_ : X â†’ X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  x â‰¼ y = âˆ€ u â†’ u < x â†’ u < y

extensional-po = _â‰¼_

extensional-po-is-prop-valued : FunExt
                              â†’ is-prop-valued
                              â†’ (x y : X) â†’ is-prop (x â‰¼ y)
extensional-po-is-prop-valued fe isp x y =
  Î â‚‚-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥) (Î» u l â†’ isp u y)

â‰¼-refl : {x : X} â†’ x â‰¼ x
â‰¼-refl u l = l

â‰¼-trans : {x y z : X} â†’ x â‰¼ y â†’ y â‰¼ z â†’ x â‰¼ z
â‰¼-trans f g u l = g u (f u l)

is-extensional : ğ“¤ âŠ” ğ“¥ Ì‡
is-extensional = (x y : X) â†’ x â‰¼ y â†’ y â‰¼ x â†’ x â‰¡ y

is-extensional' : ğ“¤ âŠ” ğ“¥ Ì‡
is-extensional' = (x y : X) â†’ ((u : X) â†’ (u < x) â‡” (u < y)) â†’ x â‰¡ y

extensional-gives-extensional' : is-extensional â†’ is-extensional'
extensional-gives-extensional' e x y f = e x y
                                          (Î» u l â†’ prâ‚ (f u) l)
                                          (Î» u l â†’ prâ‚‚ (f u) l)

extensional'-gives-extensional : is-extensional' â†’ is-extensional
extensional'-gives-extensional e' x y g h = e' x y (Î» u â†’ (g u , h u))

\end{code}

The HoTT Book additionally requires that the underlying type is a set
in the following definition, but this actually follows from the
extensionality condition (see below):

\begin{code}

is-well-order : ğ“¤ âŠ” ğ“¥ Ì‡
is-well-order = is-prop-valued
              Ã— is-well-founded
              Ã— is-extensional
              Ã— is-transitive

prop-valuedness : is-well-order â†’ is-prop-valued
prop-valuedness (p , w , e , t) = p

well-foundedness : is-well-order â†’ is-well-founded
well-foundedness (p , w , e , t) = w

extensionality : is-well-order â†’ is-extensional
extensionality (p , w , e , t) = e

transitivity : is-well-order â†’ is-transitive
transitivity (p , w , e , t) = t

accessibility-is-prop : FunExt
                      â†’ (x : X) â†’ is-prop (is-accessible x)
accessibility-is-prop fe = accessible-induction P Ï†
 where
  P : (x : X) â†’ is-accessible x â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x a = (b : is-accessible x) â†’ a â‰¡ b

  Ï† : (x : X) (Ïƒ : (y : X) â†’ y < x â†’ is-accessible y)
    â†’ ((y : X) (l : y < x) (a : is-accessible y) â†’ Ïƒ y l â‰¡ a)
    â†’ (b : is-accessible x) â†’ next x Ïƒ â‰¡ b
  Ï† x Ïƒ IH b = next x Ïƒ â‰¡âŸ¨ i âŸ©
               next x Ï„ â‰¡âŸ¨ prev-behaviour x b âŸ©
               b        âˆ
   where
    Ï„ : (y : X) â†’ y < x â†’ is-accessible y
    Ï„ = prev x b

    h :  (y : X) (l : y < x) â†’ Ïƒ y l â‰¡ Ï„ y l
    h y l = IH y l (Ï„ y l)

    i = ap (next x)
           (dfunext (fe ğ“¤ (ğ“¤ âŠ” ğ“¥)) (Î» y â†’ dfunext (fe ğ“¥ (ğ“¤ âŠ” ğ“¥)) (h y)))

well-foundedness-is-prop : FunExt â†’ is-prop is-well-founded
well-foundedness-is-prop fe = Î -is-prop (fe ğ“¤ (ğ“¤ âŠ” ğ“¥))
                               (accessibility-is-prop fe)

extensionally-ordered-types-are-sets : FunExt
                                     â†’ is-prop-valued
                                     â†’ is-extensional
                                     â†’ is-set X
extensionally-ordered-types-are-sets fe isp e = Î³
 where
  f : {x y :  X} â†’ x â‰¡ y â†’ x â‰¡ y
  f {x} {y} p = e x y (transport (x â‰¼_) p (â‰¼-refl {x}))
                      (transport (_â‰¼ x) p (â‰¼-refl {x}))

  ec : {x y : X} {l l' : x â‰¼ y} {m m' : y â‰¼ x} â†’ e x y l m â‰¡ e x y l' m'
  ec {x} {y} {l} {l'} {m} {m'} = apâ‚‚ (e x y)
                                     (extensional-po-is-prop-valued fe isp x y l l')
                                     (extensional-po-is-prop-valued fe isp y x m m')

  Îº : {x y : X} â†’ wconstant (f {x} {y})
  Îº p q = ec

  Î³ : is-set X
  Î³ = Id-collapsibles-are-sets (f , Îº)

well-ordered-types-are-sets : FunExt â†’ is-well-order â†’ is-set X
well-ordered-types-are-sets fe (p , w , e , t) =
 extensionally-ordered-types-are-sets fe p e

extensionality-is-prop : FunExt â†’ is-prop-valued â†’ is-prop is-extensional
extensionality-is-prop fe isp e e' =
 dfunext (fe ğ“¤ (ğ“¤ âŠ” ğ“¥))
   (Î» x â†’ dfunext (fe ğ“¤ (ğ“¤ âŠ” ğ“¥))
           (Î» y â†’ Î â‚‚-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                    (Î» l m â†’ extensionally-ordered-types-are-sets fe isp e)
                    (e x y)
                    (e' x y)))

transitivity-is-prop : FunExt â†’ is-prop-valued â†’ is-prop is-transitive
transitivity-is-prop fe isp = Î â‚…-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                               (Î» x y z l m â†’ isp x z)

being-well-order-is-prop : FunExt â†’ is-prop is-well-order
being-well-order-is-prop fe = prop-criterion Î³
 where
  Î³ : is-well-order â†’ is-prop is-well-order
  Î³ o = Ã—â‚„-is-prop (Î â‚‚-is-prop ((Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥))
                      (Î» x y â†’ being-prop-is-prop (fe ğ“¥ ğ“¥)))
                   (well-foundedness-is-prop fe)
                   (extensionality-is-prop fe (prop-valuedness o))
                   (transitivity-is-prop fe (prop-valuedness o))

private
 _â‰¾_ : X â†’ X â†’ ğ“¥ Ì‡
 x â‰¾ y = Â¬ (y < x)

â‰¾-is-prop-valued : funext ğ“¥ ğ“¤â‚€ â†’ is-prop-valued â†’ (x y : X) â†’ is-prop (x â‰¾ y)
â‰¾-is-prop-valued fe p x y = negations-are-props fe

<-gives-â‰¾  : (x : X)
           â†’ is-accessible x
           â†’ (y : X) â†’ y < x â†’ y â‰¾ x
<-gives-â‰¾ = transfinite-induction'
                     (Î» x â†’ (y : X) â†’ y < x â†’ y â‰¾ x)
                     (Î» x f y l m â†’ f y l x m l)

â‰¾-refl : (x : X) â†’ is-accessible x â†’ x â‰¾ x
â‰¾-refl x a l = <-gives-â‰¾ x a x l l

irreflexive : (x : X) â†’ is-accessible x â†’ Â¬ (x < x)
irreflexive = â‰¾-refl

<-gives-â‰¢ : is-well-founded
          â†’ (x y : X) â†’ x < y â†’ x â‰¢ y
<-gives-â‰¢ w x y l p = irreflexive y (w y) (transport (_< y) p l)

<-gives-â‰¼ : is-transitive â†’ {x y : X} â†’ x < y â†’ x â‰¼ y
<-gives-â‰¼ t {x} {y} l u m = t u x y m l

â‰¼-coarser-than-â‰¾ : (y : X) â†’ is-accessible y â†’ (x : X) â†’ x â‰¼ y â†’ x â‰¾ y
â‰¼-coarser-than-â‰¾ y a x f l = â‰¾-refl y a (f y l)

is-top : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-top x = (y : X) â†’ y â‰¾ x

is-top' : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-top' x = (y : X) â†’ y â‰¼ x

is-top'-gives-is-top : is-well-founded â†’ (x : X) â†’ is-top' x â†’ is-top x
is-top'-gives-is-top w x i y = â‰¼-coarser-than-â‰¾ x (w x) y (i y)

has-top : ğ“¤ âŠ” ğ“¥ Ì‡
has-top = Î£ x ê‰ X , is-top x

no-minimal-is-empty : is-well-founded
                     â†’ âˆ€ {ğ“¦} (A : X â†’ ğ“¦ Ì‡ )
                     â†’ ((x : X) â†’ A x â†’ is-nonempty (Î£ y ê‰ X , (y < x) Ã— A y))
                     â†’ is-empty (Î£ A)
no-minimal-is-empty w A s (x , p) = Î³
 where
  g : (x : X) â†’ is-accessible x â†’ Â¬ (A x)
  g x (next x Ïƒ) Î½ = Î´
   where
    h : Â¬Â¬ (Î£ y ê‰ X , (y < x) Ã— A y)
    h = s x Î½

    IH : (y : X) â†’ y < x â†’ Â¬ (A y)
    IH y l = g y (Ïƒ y l)

    k : Â¬ (Î£ y ê‰ X , (y < x) Ã— A y)
    k (y , l , a) = IH y l a

    Î´ : ğŸ˜
    Î´ = h k

  f : ((x : X) â†’ A x â†’ Â¬Â¬ (Î£ y ê‰ X , (y < x) Ã— A y))
    â†’ (x : X) â†’ Â¬ (A x)
  f s x p = g x (w x) p

  Î³ : ğŸ˜
  Î³ = f s x p

no-minimal-is-empty-weaker-version : is-well-founded
                                   â†’ âˆ€ {ğ“¦} (A : X â†’ ğ“¦ Ì‡ )
                                   â†’ ((x : X) â†’ A x â†’ Î£ y ê‰ X , (y < x) Ã— A y)
                                   â†’ is-empty (Î£ A)
no-minimal-is-empty-weaker-version w A s =
  no-minimal-is-empty w A (Î» x a â†’ double-negation-intro (s x a))

\end{code}

The remainder of this file is not needed anywhere else (at least at
the time of writing, namely 11th January 2021).

\begin{code}

is-trichotomous-element : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-trichotomous-element x = (y : X) â†’ (x < y) + (x â‰¡ y) + (y < x)

is-trichotomous-order : ğ“¤ âŠ” ğ“¥ Ì‡
is-trichotomous-order = (x : X) â†’ is-trichotomous-element x

\end{code}

Not all ordinals are trichotomous, in the absence of excluded middle
or even just LPO, because â„•âˆ is not discrete unless LPO holds, but its
natural order is well-founded, and types with well-founded trichotomous
relations are discrete (have decidable equality):

\begin{code}

trichotomous-gives-discrete : is-well-founded
                            â†’ is-trichotomous-order
                            â†’ is-discrete X
trichotomous-gives-discrete w t x y = f (t x y)
 where
  f : (x < y) + (x â‰¡ y) + (y < x) â†’ (x â‰¡ y) + (x â‰¢ y)
  f (inl l)       = inr (<-gives-â‰¢ w x y l)
  f (inr (inl p)) = inl p
  f (inr (inr l)) = inr (â‰¢-sym (<-gives-â‰¢ w y x l))

\end{code}

The following proof that excluded middle gives trichotomy, added 11th
Jan 2021, is the same as that in the HoTT book, except that we use
negation instead of the assumption of existence of propositional
truncations to get a proposition to which we can apply excluded
middle.  But notice that, under excluded middle and function
extensionality, double negation is the same thing as propositional
truncation. Notice also we additionally need function extensionality
as an assumption (to know that the negation of a type is a
proposition).

\begin{code}

trichotomy : funext (ğ“¤ âŠ” ğ“¥) ğ“¤â‚€
           â†’ excluded-middle (ğ“¤ âŠ” ğ“¥)
           â†’ is-well-order
           â†’ is-trichotomous-order
trichotomy fe em (p , w , e , t) = Î³
 where
  P : X â†’ X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x y = (x < y) + (x â‰¡ y) + (y < x)

  Î³ : (x y : X) â†’ P x y
  Î³ = transfinite-induction w (Î» x â†’ âˆ€ y â†’ P x y) Ï•
   where
    Ï• : (x : X)
      â†’ ((x' : X) â†’ x' < x â†’ (y : X) â†’ P x' y)
      â†’ (y : X) â†’ P x y
    Ï• x IH-x = transfinite-induction w (Î» y â†’ P x y) Ïˆ
     where
      Ïˆ : (y : X)
        â†’ ((y' : X) â†’ y' < y â†’ P x y')
        â†’ P x y
      Ïˆ y IH-y = Î´
       where
        A = Î£ x' ê‰ X , (x' < x) Ã— ((y < x') + (x' â‰¡ y))

        Â¬Â¬A-gives-P : Â¬Â¬ A â†’ P x y
        Â¬Â¬A-gives-P = b
         where
          a : A â†’ y < x
          a (x' , l , inl m) = t y x' x m l
          a (x' , l , inr p) = transport (_< x) p l

          b : Â¬Â¬ A â†’ (x < y) + (x â‰¡ y) + (y < x)
          b = inr âˆ˜ inr âˆ˜ EM-gives-DNE (lower-EM ğ“¤ em) (y < x) (p y x) âˆ˜ Â¬Â¬-functor a

        Â¬A-gives-â‰¼ : Â¬ A â†’ x â‰¼ y
        Â¬A-gives-â‰¼ Î½ x' l = d
         where
          a : Â¬ ((y < x') + (x' â‰¡ y))
          a f = Î½ (x' , l , f)

          b : P x' y
          b = IH-x x' l y

          c : Â¬ ((y < x') + (x' â‰¡ y)) â†’ P x' y â†’ x' < y
          c g (inl i)         = i
          c g (inr (inl ii))  = ğŸ˜-elim (g (inr ii))
          c g (inr (inr iii)) = ğŸ˜-elim (g (inl iii))

          d : x' < y
          d = c a b

        B = Î£ y' ê‰ X , (y' < y) Ã— ((x < y') + (x â‰¡ y'))

        Â¬Â¬B-gives-P : Â¬Â¬ B â†’ P x y
        Â¬Â¬B-gives-P = b
         where
          a : B â†’ x < y
          a (y' , l , inl m) = t x y' y m l
          a (y' , l , inr p) = transport (_< y) (p â»Â¹) l

          b : Â¬Â¬ B â†’ (x < y) + (x â‰¡ y) + (y < x)
          b = inl âˆ˜ EM-gives-DNE (lower-EM ğ“¤ em) (x < y) (p x y) âˆ˜ Â¬Â¬-functor a

        Â¬B-gives-â‰¼ : Â¬ B â†’ y â‰¼ x
        Â¬B-gives-â‰¼ Î½ y' l = d
         where
          a : Â¬ ((x < y') + (x â‰¡ y'))
          a f = Î½ (y' , l , f)

          b : P x y'
          b = IH-y y' l

          c : Â¬ ((x < y') + (x â‰¡ y')) â†’ P x y' â†’ y' < x
          c g (inl i)         = ğŸ˜-elim (g (inl i))
          c g (inr (inl ii))  = ğŸ˜-elim (g (inr ii))
          c g (inr (inr iii)) = iii

          d : y' < x
          d = c a b

        Â¬A-and-Â¬B-give-P : Â¬ A â†’ Â¬ B â†’ P x y
        Â¬A-and-Â¬B-give-P Î½ Î½' = b
         where
          a : Â¬ A â†’ Â¬ B â†’ x â‰¡ y
          a Î½ Î½' = e x y (Â¬A-gives-â‰¼ Î½) (Â¬B-gives-â‰¼ Î½')

          b : (x < y) + (x â‰¡ y) + (y < x)
          b = inr (inl (a Î½ Î½'))

        Î´ : P x y
        Î´ = Cases (em (Â¬ A) (negations-are-props fe))
             (Î» (Î½ : Â¬ A)
                   â†’ Cases (em (Â¬ B) (negations-are-props fe))
                      (Â¬A-and-Â¬B-give-P Î½)
                      Â¬Â¬B-gives-P)
             Â¬Â¬A-gives-P


\end{code}

Because we assume function extensionality and excluded middle in this
annonymous submodule, propositional truncations are available, and it
amounts to double negation.

\begin{code}

module _
        (fe : Fun-Ext)
        (em : Excluded-Middle)
       where

 open import UF-PropTrunc
 open PropositionalTruncation (fem-proptrunc (Î» ğ“¤ ğ“¥ â†’ fe {ğ“¤} {ğ“¥}) em)

 nonempty-has-minimal : is-well-order
                      â†’ (A : X â†’ ğ“¦ Ì‡ )
                      â†’ ((x : X) â†’ is-prop (A x))
                      â†’ âˆƒ x ê‰ X , A x
                      â†’ Î£ x ê‰ X , A x Ã— ((y : X) â†’ A y â†’ x â‰¾ y)
 nonempty-has-minimal {ğ“¦} W A A-is-prop-valued f = Î³
  where
   B : ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ Ì‡
   B = Î£ x ê‰ X , A x Ã— ((y : X) â†’ A y â†’ x â‰¾ y)

   g : Â¬ ((x : X) â†’ A x â†’ âˆƒ y ê‰ X , (y < x) Ã— A y)
   g = contrapositive (no-minimal-is-empty (well-foundedness W) A) f

   h : âˆƒ x ê‰ X , Â¬ (A x â†’ âˆƒ y ê‰ X , (y < x) Ã— A y)
   h = not-Î -implies-not-not-Î£
        (Î» x â†’ EM-gives-DNE em
                (A x â†’ âˆƒ y ê‰ X , (y < x) Ã— A y)
                (Î â‚‚-is-prop fe (Î» _ _ â†’ ğŸ˜-is-prop)))
        g

   Ï• : (x : X)
     â†’ Â¬ (A x â†’ âˆƒ y ê‰ X , (y < x) Ã— A y)
     â†’ A x Ã— ((y : X) â†’ A y â†’ x â‰¾ y)
   Ï• x Ïˆ = EM-gives-DNE em (A x)
             (A-is-prop-valued x)
             ((Î» Î½ â†’ Ïˆ (Î» a _ â†’ Î½ a))) ,
           Î» y a l â†’ Ïˆ (Î» _ Î½ â†’ Î½ (y , l , a))

   Î´ : Â¬Â¬ B
   Î´ = Â¬Â¬-functor (Î» (x , f) â†’ x , Ï• x f) h

   j : (x : X) â†’ is-prop ((y : X) â†’ A y â†’ x â‰¾ y)
   j x = Î â‚ƒ-is-prop fe (Î» x a l â†’ ğŸ˜-is-prop)

   i : (x : X) â†’ is-prop (A x Ã— ((y : X) â†’ A y â†’ x â‰¾ y))
   i x = Ã—-is-prop (A-is-prop-valued x) (j x)

   B-is-prop : is-prop B
   B-is-prop (x , a , f) (x' , a' , f') = to-subtype-â‰¡ i q
    where
     q : x â‰¡ x'
     q = k (trichotomy fe em W x x')
      where
       k : (x < x') + (x â‰¡ x') + (x' < x) â†’ x â‰¡ x'
       k (inl l)       = ğŸ˜-elim (f' x a l)
       k (inr (inl p)) = p
       k (inr (inr l)) = ğŸ˜-elim (f x' a' l)

   Î³ : B
   Î³ = EM-gives-DNE em B B-is-prop Î´

\end{code}

When do we get x â‰¾ y â†’ x â‰¼ y (say for ordinals)? When do we get
cotransitivity? Jean S. Josef observed that cotransitivity gives x â‰¾ y
â†’ x â‰¼ y if _<_ is an order. But cotransitivity alone is enough.

Or consider the truncated version of the following, if _<_ is
proposition valued.

\begin{code}

cotransitive : ğ“¤ âŠ” ğ“¥ Ì‡
cotransitive = (x y z : X) â†’ x < y â†’ (x < z) + (z < y)

cotransitive-â‰¾-gives-â‰¼ : cotransitive â†’ (x y : X) â†’ x â‰¾ y â†’ x â‰¼ y
cotransitive-â‰¾-gives-â‰¼ c x y n u l = Î³ (c u x y l)
 where
  Î³ : (u < y) + (y < x) â†’ u < y
  Î³ (inl l) = l
  Î³ (inr l) = ğŸ˜-elim (n l)

\end{code}

This is the end of the submodule with the assumption of excluded
middle.

Originally, in 2011 (see my JSL publication), we needed to work with
the following weakening of well-foundedness (transfinite induction for
detachable subsets), but as of Summer 2018, it is not needed any
longer as we are able to show that our compact ordinals are
well-founded in the standard, stronger, sense.

\begin{code}

is-well-foundedâ‚‚ : ğ“¤ âŠ” ğ“¥ Ì‡
is-well-foundedâ‚‚ = (p : X â†’ ğŸš) â†’ ((x : X) â†’ ((y : X) â†’ y < x â†’ p y â‰¡ â‚) â†’ p x â‰¡ â‚)
                               â†’ (x : X) â†’ p x â‰¡ â‚

well-founded-Wellfoundedâ‚‚ : is-well-founded â†’ is-well-foundedâ‚‚
well-founded-Wellfoundedâ‚‚ w p = transfinite-induction w (Î» x â†’ p x â‰¡ â‚)

open import UF-Miscelanea

being-well-foundedâ‚‚-is-prop : FunExt â†’ is-prop is-well-foundedâ‚‚
being-well-foundedâ‚‚-is-prop fe = Î â‚ƒ-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                                   (Î» p s x â†’ ğŸš-is-set)

is-well-orderâ‚‚ : ğ“¤ âŠ” ğ“¥ Ì‡
is-well-orderâ‚‚ = is-prop-valued Ã— is-well-foundedâ‚‚ Ã— is-extensional Ã— is-transitive

is-well-order-gives-is-well-orderâ‚‚ : is-well-order â†’ is-well-orderâ‚‚
is-well-order-gives-is-well-orderâ‚‚ (p , w , e , t) = p , (well-founded-Wellfoundedâ‚‚ w) , e , t

being-well-orderâ‚‚-is-prop : FunExt â†’ is-prop-valued â†’ is-prop is-well-orderâ‚‚
being-well-orderâ‚‚-is-prop fe isp = Ã—â‚„-is-prop
                                     (Î â‚‚-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                                        (Î» x y â†’ being-prop-is-prop (fe ğ“¥ ğ“¥)))
                                     (being-well-foundedâ‚‚-is-prop fe)
                                     (extensionality-is-prop fe isp)
                                     (transitivity-is-prop fe isp)
\end{code}

Experimental ideas. We don't truncate the Î£, at least not for the
moment, so x <â‚‚ y won't be a proposition (i.e. subsingleton) in
general. However, given ğŸš-order-separation, this is logically
equivalent to a proposition (has split support).

\begin{code}

open import Two-Properties

_â‰ºâ‚‚_ : X â†’ X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
x â‰ºâ‚‚ y = Î£ p ê‰ (X â†’ ğŸš) , (p x <â‚‚ p y)
                       Ã— ((u v : X) â†’ (u < v â†’ p u â‰¤â‚‚ p v)
                                    Ã— (p u <â‚‚ p v â†’ u < v))

â‰ºâ‚‚-courser-than-< : (x y : X) â†’ x â‰ºâ‚‚ y â†’ x < y
â‰ºâ‚‚-courser-than-< x y (p , l , Ï†) = prâ‚‚ (Ï† x y) l

ğŸš-order-separated : ğ“¤ âŠ” ğ“¥ Ì‡
ğŸš-order-separated = (x y : X) â†’ x < y â†’ x â‰ºâ‚‚ y

ğŸš-order-separated-gives-cotransitive : ğŸš-order-separated â†’ cotransitive
ğŸš-order-separated-gives-cotransitive s x y z l = g (s x y l)
 where
  g : (Î£ p ê‰ (X â†’ ğŸš) , (p x <â‚‚ p y)
                     Ã— ((u v : X) â†’ (u < v â†’ p u â‰¤â‚‚ p v)
                                  Ã— (p u <â‚‚ p v â†’ u < v)))
    â†’ (x < z) + (z < y)
  g (p , m , Ï•) = Cases (ğŸš-is-discrete (p z) â‚€)
                   (Î» (t : p z â‰¡ â‚€)
                            â†’  inr (prâ‚‚ (Ï• z y) (Lemma[aâ‰¡â‚€â†’b<câ†’a<c] t m)))
                   (Î» (t : Â¬ (p z â‰¡ â‚€))
                            â†’ inl (prâ‚‚ (Ï• x z) (Lemma[a<bâ†’câ‰¢â‚€â†’a<c] m t)))
\end{code}

It seems that this is not going to be useful, because although â„•âˆ
satisfies this property, the property doesn't seem to be preserved by
the lexicographic order (think about this).
