Martin Escardo, 12th September 2018.

We define arithmetic operations on â„•âˆ by corecursion as defined in the
module CoNaturals. The lack of pattern matching on Zero and Succ and
of some definitional equalities make some proofs lengthier than they
would be if we had used a built-in coinductive definition of â„•âˆ.

We use the final coalgebra property of â„•âˆ for the functor ğŸ™ + (-) to
both construct the functions and prove their properties (including
idempotency, commutativity and associativity of the minimum
operation).

NB. There are shorter constructions with more direct proofs of the
minimum function, e.g. take the pointwise minimum in ğŸš (see the
function max in the module GenericconvergentSequence), but this module
serves as a good illustration of reasoning with the final coalgebra
property to both construct functions and prove their properties.

This file will grow on demand. The first operation we needed (for
codistances) is minimum.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt

module CoNaturalsArithmetic (fe : âˆ€ U V â†’ funext U V) where

open import Two
open import GenericConvergentSequence
open import CoNaturals fe
open import UF-Base

feâ‚€ : funext Uâ‚€ Uâ‚€
feâ‚€ = fe Uâ‚€ Uâ‚€

\end{code}

We consider a ğŸ™ + (-) coalgebra Îº on â„•âˆ Ã— â„•âˆ so that min is the unique
homomorphism to the final coalgebra PRED : â„•âˆ â†’ ğŸ™ + â„•âˆ on â„•âˆ.

\begin{code}

private
 Îº-min : â„•âˆ Ã— â„•âˆ â†’ ğŸ™ {Uâ‚€} + â„•âˆ Ã— â„•âˆ
 Îº-min (u , v) = ğŸš-Cases (positivity u)
                  (inl *)
                  (ğŸš-Cases (positivity v)
                    (inl *)
                    (inr (Pred u , Pred v)))

min : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
min = â„•âˆ-corec Îº-min

\end{code}

The defining equations of min thus are:

\begin{code}

min-eqâ‚€ : âˆ€ v   â†’ min (Zero , v) â‰¡ Zero
min-eqâ‚ : âˆ€ u   â†’ min (Succ u , Zero) â‰¡ Zero
min-eqâ‚‚ : âˆ€ u v â†’ min (Succ u , Succ v) â‰¡ Succ (min (u , v))

min-eqâ‚€ = Î» v   â†’ Coalg-morphism-Zero Îº-min (Zero , v) * refl
min-eqâ‚ = Î» u   â†’ Coalg-morphism-Zero Îº-min (Succ u , Zero) * refl
min-eqâ‚‚ = Î» u v â†’ Coalg-morphism-Succ Îº-min (Succ u , Succ v) (u , v) refl

\end{code}

Maximum (another version is defined in GenericConvergentSequence):

\begin{code}

private
 Îº-max : â„•âˆ Ã— â„•âˆ â†’ ğŸ™ {Uâ‚€} + â„•âˆ Ã— â„•âˆ
 Îº-max (u , v) = ğŸš-Cases (positivity u)
                   (ğŸš-Cases (positivity v)
                      (inl *)
                      (inr (Zero , Pred v)))
                   (ğŸš-Cases (positivity v)
                      (inr (Pred u , Zero))
                      (inr (Pred u , (Pred v))))

max' : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
max' = â„•âˆ-corec Îº-max

max-eqâ‚€ :         max' (Zero , Zero) â‰¡ Zero
max-eqâ‚ : âˆ€ v   â†’ max' (Zero , Succ v) â‰¡ Succ (max' (Zero , v))
max-eqâ‚‚ : âˆ€ u   â†’ max' (Succ u , Zero) â‰¡ Succ (max' (u , Zero))
max-eqâ‚ƒ : âˆ€ u v â†’ max' (Succ u , Succ v) â‰¡ Succ (max' (u , v))

max-eqâ‚€ =         Coalg-morphism-Zero Îº-max (Zero , Zero) * refl
max-eqâ‚ = Î» v   â†’ Coalg-morphism-Succ Îº-max (Zero , Succ v) (Zero , v) refl
max-eqâ‚‚ = Î» u   â†’ Coalg-morphism-Succ Îº-max (Succ u , Zero) (u , Zero) refl
max-eqâ‚ƒ = Î» u v â†’ Coalg-morphism-Succ Îº-max (Succ u , Succ v) (u , v) refl

\end{code}

We now prove properties of the minimum function using the
final-coalgebra property.

Using the equations min-eqâ‚€ and min-eqâ‚‚, we have that the function Î» u
â†’ min (u , u) is an algebra homomorphism from PRED to PRED and hence
is equal to the identity function:

\begin{code}

min-idempotent : âˆ€ u â†’ min (u , u) â‰¡ u
min-idempotent u = ap (Î» - â†’ - u) h-is-corec
 where
  h : â„•âˆ â†’ â„•âˆ
  h u = min (u , u)
  h-homomorphism : is-homomorphism PRED h
  h-homomorphism = dfunext feâ‚€ (Î» u â†’ Ï† (Zero+Succ feâ‚€ u))
   where
    Ï† : {u : â„•âˆ} â†’ (u â‰¡ Zero) + (Î£ \(w : â„•âˆ) â†’ u â‰¡ Succ w) â†’ PRED (h u) â‰¡ ğŸ™+ h (PRED u)
    Ï† (inl refl) =
      PRED (min (Zero , Zero))     â‰¡âŸ¨ ap PRED (min-eqâ‚€ Zero) âŸ©
      PRED Zero                    â‰¡âŸ¨ refl âŸ©
      ğŸ™+ h (PRED Zero)             âˆ
    Ï† (inr (w , refl)) =
      PRED (min (Succ w , Succ w)) â‰¡âŸ¨ ap PRED (min-eqâ‚‚ w w) âŸ©
      PRED (Succ (min (w , w)))    â‰¡âŸ¨ refl âŸ©
      ğŸ™+ h (PRED (Succ w))         âˆ
  h-is-corec : h â‰¡ id
  h-is-corec = homomorphism-uniqueness PRED h id h-homomorphism id-homomorphism

\end{code}

(Notice that the above argument actually shows that any function
f : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ that satisfies f (Zero , Zero) â‰¡ Zero and
f (Succ w , Succ w) = Succ (f w) is idempotent, as would be the cases
of the maximum function)

Similarly, to prove that min is commutative, we show that the function
Î» (u , v) â†’ min (v , u) satisfies the same "defining equations" as the
function min.

The following equation is derived from the above equations min-eqâ‚€ and
min-eqâ‚ by cases on whether u is Zero or a Succ(Pred u).

\begin{code}

eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ : (h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ)
                    â†’ (âˆ€ v â†’ h (Zero , v) â‰¡ Zero)
                    â†’ (âˆ€ u â†’ h (Succ u , Zero) â‰¡ Zero)
                    â†’ (âˆ€ u â†’ h (u , Zero) â‰¡ Zero)
eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ h eqâ‚€ eqâ‚ u = Î³ (Zero+Succ feâ‚€ u)
 where
  Î³ : (u â‰¡ Zero) + (Î£ \(w : â„•âˆ) â†’ u â‰¡ Succ w) â†’ h (u , Zero) â‰¡ Zero
  Î³ (inl refl)       = h (Zero , Zero)   â‰¡âŸ¨ eqâ‚€ Zero âŸ© Zero âˆ
  Î³ (inr (w , refl)) = h (Succ w , Zero) â‰¡âŸ¨ eqâ‚ w âŸ©    Zero âˆ

min-eqâ‚ƒ : âˆ€ u â†’ min (u , Zero) â‰¡ Zero
min-eqâ‚ƒ = eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ min min-eqâ‚€ min-eqâ‚

\end{code}

Conversely, if a function satisfies the above equations, then it is a
coalgebra homomorphism and hence is equal to â„•âˆ-corec Îº-min.

\begin{code}

equations-characterize-homomorphisms :
    (h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ)
  â†’ (âˆ€ v   â†’ h (Zero , v) â‰¡ Zero)
  â†’ (âˆ€ u   â†’ h (Succ u , Zero) â‰¡ Zero)
  â†’ (âˆ€ u v â†’ h (Succ u , Succ v) â‰¡ Succ (h (u , v)))
  â†’ is-homomorphism Îº-min h
equations-characterize-homomorphisms h eqâ‚€ eqâ‚ eqâ‚‚ = dfunext feâ‚€ Î³
  where
   Î³ : (w : â„•âˆ Ã— â„•âˆ) â†’ PRED (h w) â‰¡ ğŸ™+ h (Îº-min w)
   Î³ (u , v) = Ï† (Zero+Succ feâ‚€ u) (Zero+Succ feâ‚€ v)
    where
     Ï† : (u â‰¡ Zero) + (Î£ \(w : â„•âˆ) â†’ u â‰¡ Succ w)
       â†’ (v â‰¡ Zero) + (Î£ \(t : â„•âˆ) â†’ v â‰¡ Succ t)
       â†’ PRED (h (u , v)) â‰¡ ğŸ™+ h (Îº-min (u , v))
     Ï† (inl refl) _  =
       PRED (h (Zero , v))            â‰¡âŸ¨ ap PRED (eqâ‚€ v) âŸ©
       PRED Zero                      â‰¡âŸ¨ refl âŸ©
       ğŸ™+ h (Îº-min (Zero , v))        âˆ
     Ï† (inr (w , refl)) (inl refl) =
       PRED (h (Succ w , Zero))       â‰¡âŸ¨ ap PRED (eqâ‚ w) âŸ©
       PRED Zero                      â‰¡âŸ¨ refl âŸ©
       ğŸ™+ h (Îº-min (Succ w , Zero))   âˆ
     Ï† (inr (w , refl)) (inr (t , refl)) =
       PRED (h (Succ w , Succ t))     â‰¡âŸ¨ ap PRED (eqâ‚‚ w t) âŸ©
       PRED (Succ (h (w , t)))        â‰¡âŸ¨ refl âŸ©
       ğŸ™+ h (Îº-min (Succ w , Succ t)) âˆ

\end{code}

To prove that min is commutative, we show that the following function
h is also a coalgebra homomorphism and hence equal to â„•âˆ-corec p:

\begin{code}

min-commutative : âˆ€ u v â†’ min (u , v) â‰¡ min (v , u)
min-commutative u v = h (v , u)               â‰¡âŸ¨ ap (Î» - â†’ - (v , u)) h-is-corec âŸ©
                      â„•âˆ-corec Îº-min (v , u) âˆ
 where
  h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
  h (u , v) = min (v , u)
  h-homomorphism : is-homomorphism Îº-min h
  h-homomorphism = equations-characterize-homomorphisms h h-eqâ‚€ h-eqâ‚ h-eqâ‚‚
   where
    h-eqâ‚€ : (v : â„•âˆ) â†’ min (v , Zero) â‰¡ Zero
    h-eqâ‚€ v = min-eqâ‚ƒ v
    h-eqâ‚ : (u : â„•âˆ) â†’ min (Zero , Succ u) â‰¡ Zero
    h-eqâ‚ u = min-eqâ‚€ (Succ u)
    h-eqâ‚‚ : (u v : â„•âˆ) â†’ min (Succ v , Succ u) â‰¡ Succ (min (v , u))
    h-eqâ‚‚ u v = min-eqâ‚‚ v u
  h-is-corec : h â‰¡ â„•âˆ-corec Îº-min
  h-is-corec = homomorphism-uniqueness Îº-min h (â„•âˆ-corec Îº-min)
                h-homomorphism (â„•âˆ-corec-homomorphism Îº-min)

\end{code}

Similarly, to prove that min is associative, we show that the two functions

   Î» (u , v , w) â†’ min (u , min (v , w))
   Î» (u , v , w) â†’ min (min (u , v) , w)

are homormorphisms from the same coalgebra on â„•âˆ Ã— â„•âˆ Ã— â„•âˆ to the
final coalgebra PRED.

\begin{code}

min-assoc : (u v w : â„•âˆ) â†’ min (u , min (v , w)) â‰¡ min (min (u , v) , w)
min-assoc u v w = ap (Î» - â†’ - (u , v , w)) p
 where
  f g : â„•âˆ Ã— â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
  f (u , v , w) = min (u , min (v , w))
  g (u , v , w) = min (min (u , v) , w)
  k : â„•âˆ Ã— â„•âˆ Ã— â„•âˆ â†’ ğŸ™ + â„•âˆ Ã— â„•âˆ Ã— â„•âˆ
  k (u , v , w) = ğŸš-Cases (positivity u)
                   (inl *)
                   (ğŸš-Cases (positivity v)
                     (inl *)
                     (ğŸš-Cases (positivity w)
                       (inl *)
                       (inr (Pred u , Pred v , Pred w))))
  f-homomorphism : is-homomorphism k f
  f-homomorphism = dfunext feâ‚€ Î³
   where
    Î³ : (z : â„•âˆ Ã— â„•âˆ Ã— â„•âˆ) â†’ PRED (f z) â‰¡ ğŸ™+ f (k z)
    Î³ (u , v , w) = Ï† (Zero+Succ feâ‚€ u) (Zero+Succ feâ‚€ v) (Zero+Succ feâ‚€ w)
     where
      Ï† : (u â‰¡ Zero) + (Î£ \(x : â„•âˆ) â†’ u â‰¡ Succ x)
       â†’ (v â‰¡ Zero) + (Î£ \(y : â„•âˆ) â†’ v â‰¡ Succ y)
       â†’ (w â‰¡ Zero) + (Î£ \(z : â„•âˆ) â†’ w â‰¡ Succ z)
       â†’ PRED (f (u , v , w)) â‰¡ ğŸ™+ f (k (u , v , w))
      Ï† (inl refl) _ _ = ap PRED (min-eqâ‚€ (min (v , w)))
      Ï† (inr (x , refl)) (inl refl) _ =
        PRED (min (Succ x , min (Zero , w))) â‰¡âŸ¨ ap (Î» - â†’ PRED (min (Succ x , -))) (min-eqâ‚€ w) âŸ©
        PRED (min (Succ x , Zero))           â‰¡âŸ¨ ap PRED (min-eqâ‚ƒ u) âŸ©
        PRED Zero                            â‰¡âŸ¨ ap PRED (min-eqâ‚ƒ u) âŸ©
        ğŸ™+ f (k (Succ x , Zero , w))         âˆ
      Ï† (inr (x , refl)) (inr (y , refl)) (inl refl) =
        PRED (min (Succ x , min (Succ y , Zero))) â‰¡âŸ¨ ap (Î» - â†’ PRED (min (Succ x , -))) (min-eqâ‚ƒ (Succ y)) âŸ©
        PRED (min (Succ x , Zero))                â‰¡âŸ¨ ap PRED (min-eqâ‚ƒ (Succ x)) âŸ©
        ğŸ™+ f (k (Succ x , Succ y , Zero))         âˆ
      Ï† (inr (x , refl)) (inr (y , refl)) (inr (z , refl)) =
        PRED (min (Succ x , min (Succ y , Succ z))) â‰¡âŸ¨ ap (Î» - â†’ PRED (min (Succ x , -))) (min-eqâ‚‚ y z) âŸ©
        PRED (min (Succ x , Succ (min (y , z))))    â‰¡âŸ¨ ap PRED (min-eqâ‚‚ x (min (y , z))) âŸ©
        ğŸ™+ f (k (Succ x , Succ y , Succ z))         âˆ
  g-homomorphism : is-homomorphism k g
  g-homomorphism = dfunext feâ‚€ Î³
   where
    Î³ : (z : â„•âˆ Ã— â„•âˆ Ã— â„•âˆ) â†’ PRED (g z) â‰¡ ğŸ™+ g (k z)
    Î³ (u , v , w) = Ï† (Zero+Succ feâ‚€ u) (Zero+Succ feâ‚€ v) (Zero+Succ feâ‚€ w)
     where
      Ï† : (u â‰¡ Zero) + (Î£ \(x : â„•âˆ) â†’ u â‰¡ Succ x)
       â†’ (v â‰¡ Zero) + (Î£ \(y : â„•âˆ) â†’ v â‰¡ Succ y)
       â†’ (w â‰¡ Zero) + (Î£ \(z : â„•âˆ) â†’ w â‰¡ Succ z)
       â†’ PRED (g (u , v , w)) â‰¡ ğŸ™+ g (k (u , v , w))
      Ï† (inl refl) _ _ = ap PRED (min-eqâ‚€ (min (v , w)))
      Ï† (inr (x , refl)) (inl refl) _ =
        PRED (min (min (Succ x , Zero) , w)) â‰¡âŸ¨ ap (Î» - â†’ PRED (min (- , w))) (min-eqâ‚ƒ (Succ x)) âŸ©
        PRED (min (Zero , w)) â‰¡âŸ¨ ap PRED (min-eqâ‚€ w) âŸ©
        PRED Zero â‰¡âŸ¨ refl âŸ©
        ğŸ™+ g (k (Succ x , Zero , w)) âˆ
      Ï† (inr (x , refl)) (inr (y , refl)) (inl refl) =
        PRED (min (min (Succ x , Succ y) , Zero)) â‰¡âŸ¨ ap PRED (min-eqâ‚ƒ (min (Succ x , Succ y))) âŸ©
        PRED Zero â‰¡âŸ¨ refl âŸ©
        ğŸ™+ g (k (Succ x , Succ y , Zero)) âˆ
      Ï† (inr (x , refl)) (inr (y , refl)) (inr (z , refl)) =
        PRED (min (min (Succ x , Succ y) , Succ z)) â‰¡âŸ¨ ap (Î» - â†’ PRED (min (- , Succ z))) (min-eqâ‚‚ x y) âŸ©
        PRED (min (Succ (min (x , y)) , Succ z)) â‰¡âŸ¨ ap PRED (min-eqâ‚‚ (min (x , y)) z) âŸ©
        PRED (Succ (min (min (x , y) , z))) â‰¡âŸ¨ refl âŸ©
        ğŸ™+ g (k (Succ x , Succ y , Succ z)) âˆ
  p : f â‰¡ g
  p = homomorphism-uniqueness k f g f-homomorphism g-homomorphism

\end{code}

The following two facts invert the equations that characterize min:

\begin{code}

min-Zero : âˆ€ u v   â†’ min (u , v) â‰¡ Zero â†’ (u â‰¡ Zero) + (v â‰¡ Zero)
min-Succ : âˆ€ u v x â†’ min (u , v) â‰¡ Succ x â†’ (u â‰¡ Succ (Pred u))
                                          Ã— (v â‰¡ Succ (Pred v))
                                          Ã— (x â‰¡ min (Pred u , Pred v))

\end{code}

And here are their constructions:

\begin{code}

min-Zero u v r = h (Zero+Succ feâ‚€ u) (Zero+Succ feâ‚€ v)
 where
  h : (u â‰¡ Zero) + (Î£ \(w : â„•âˆ) â†’ u â‰¡ Succ w) â†’ (v â‰¡ Zero) + (Î£ \(t : â„•âˆ) â†’ v â‰¡ Succ t) â†’ _
  h (inl refl) _ = inl refl
  h (inr (w , refl)) (inl refl) = inr refl
  h (inr (w , refl)) (inr (t , refl)) = ğŸ˜-elim (Zero-not-Succ (r â»Â¹ âˆ™ min-eqâ‚‚ w t))


min-Succ u v x r = h (Zero+Succ feâ‚€ u) (Zero+Succ feâ‚€ v)
 where
  h : (u â‰¡ Zero) + (Î£ \(w : â„•âˆ) â†’ u â‰¡ Succ w) â†’ (v â‰¡ Zero) + (Î£ \(t : â„•âˆ) â†’ v â‰¡ Succ t) â†’ _
  h (inl refl) _ =
    ğŸ˜-elim (Zero-not-Succ (Zero           â‰¡âŸ¨ (min-eqâ‚€ v)â»Â¹ âŸ©
                           min (Zero , v) â‰¡âŸ¨ r âŸ©
                           Succ x         âˆ))
  h (inr (w , refl)) (inl refl) =
    ğŸ˜-elim (Zero-not-Succ (Zero           â‰¡âŸ¨ (min-eqâ‚ƒ u)â»Â¹ âŸ©
                           min (u , v)    â‰¡âŸ¨ r âŸ©
                           Succ x         âˆ))
  h (inr (w , refl)) (inr (t , refl)) = refl , refl ,
    Succ-lc (Succ x                       â‰¡âŸ¨ r â»Â¹ âŸ©
             min (Succ w , Succ t)        â‰¡âŸ¨ min-eqâ‚‚ w t âŸ©
             Succ (min (w , t))           âˆ)

\end{code}

Relation of min with â‰¼.

\begin{code}

{-
min-â‰¼-l : (u v : â„•âˆ) â†’ min (u , v) â‰¼ u
min-â‰¼-l u = Î³ (Zero-or-Succ feâ‚€ u)
 where
  Î³ : (u â‰¡ Zero) + (u â‰¡ Succ (Pred u)) â†’ (v : â„•âˆ) â†’ min (u , v) â‰¼ u
  Î³ (inl refl) v n p = transport (Î» - â†’ n âŠ -) (min-eqâ‚€ v) p
  Î³ (inr q) v zero p = ap positivity q
  Î³ (inr q) v (succ n) p = Ï† (Zero-or-Succ feâ‚€ v)
   where
    Ï† : (v â‰¡ Zero) + (v â‰¡ Succ (Pred v)) â†’ incl u (succ n) â‰¡ â‚
    Ï† (inl refl) = ğŸ˜-elim (zero-is-not-one t)
     where
      t : â‚€ â‰¡ â‚
      t = transport (Î» - â†’ incl - (succ n) â‰¡ â‚) (min-eqâ‚ƒ u) p
    Ï† (inr r) = {!!}
     where
      IH : {!!}
      IH = {!!}

min-glb : (u v w : â„•âˆ) â†’ u â‰¼ v â†’ u â‰¼ w â†’ u â‰¼ min (v , w)
min-glb u v w = {!!}
-}

\end{code}
