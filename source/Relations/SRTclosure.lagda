Martin Escardo, 3 February 2021.

â‹† Symmetric closure of a relation.

â‹† Iteration of a relation.

â‹† Reflexive-transitive closure of a relation.

â‹† Symmetric-reflexive-transitive closure of a relation.

â‹† propositional, symmetric-reflexive-transitive closure of a relation.

â‹† A special kind of Church-Rosser property.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (_^_)

module Relations.SRTclosure where

open import UF.Subsingletons
open import UF.PropTrunc

open import Naturals.Addition renaming (_+_ to right-addition)

\end{code}

We work with addition defined by induction on the left argument:

\begin{code}

_âˆ”_ : â„• â†’ â„• â†’ â„•
m âˆ” n = right-addition n m

_âŠ‘_ : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ (X â†’ X â†’ ğ“¦ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ Ì‡
R âŠ‘ S = âˆ€ x y â†’ R x y â†’ S x y

is-prop-valued-rel is-equiv-rel : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-prop-valued-rel A = âˆ€ x y â†’ is-prop (A x y)
is-equiv-rel       A = is-prop-valued-rel A
                     Ã— reflexive A
                     Ã— symmetric A
                     Ã— transitive A
\end{code}

The symmetric closure of a relation A:

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (A : X â†’ X â†’ ğ“¥ Ì‡ )
       where

 s-closure : X â†’ X â†’ ğ“¥ Ì‡
 s-closure x y = A x y + A y x

 s-symmetric : symmetric s-closure
 s-symmetric x y (inl a) = inr a
 s-symmetric x y (inr a) = inl a

 s-extension : A âŠ‘ s-closure
 s-extension x y = inl

 s-induction : (R : X â†’ X â†’ ğ“¦ Ì‡ )
             â†’ symmetric R
             â†’ A âŠ‘ R
             â†’ s-closure âŠ‘ R
 s-induction R s A-included-in-R x y (inl a) = A-included-in-R x y a
 s-induction R s A-included-in-R x y (inr a) = s y x (A-included-in-R y x a)

\end{code}

To define the reflexive-transitive closure, we first consider the
iteration of a relation B:

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (B : X â†’ X â†’ ğ“¤ Ì‡ )
       where

 iteration : â„• â†’ X â†’ X â†’ ğ“¤ Ì‡
 iteration 0        x y = x ï¼ y
 iteration (succ n) x y = Î£ z ê‰ X , B x z Ã— iteration n z y

 iteration-reflexive : (x : X) â†’ iteration 0 x x
 iteration-reflexive x = refl

 iteration-transitive' : (n : â„•) (x y z : X) â†’ iteration n x y â†’ B y z â†’ iteration (succ n) x z
 iteration-transitive' 0        x x z refl        b  = z , b , refl
 iteration-transitive' (succ n) x y z (t , b , c) b' = t , b , iteration-transitive' n t y z c b'

 iteration-transitive'-converse : (n : â„•) (x z : X)
                                â†’ iteration (succ n) x z
                                â†’ Î£ y ê‰ X , iteration n x y Ã— B y z
 iteration-transitive'-converse 0        x z (z , b , refl)       = x , refl , b
 iteration-transitive'-converse (succ n) x z (y , b , t , b' , i) = Î³
  where
   IH : Î£ u ê‰ X , iteration n y u Ã— B u z
   IH = iteration-transitive'-converse n y z (t , b' , i)

   u : X
   u = prâ‚ IH

   i' : iteration n y u
   i' = prâ‚ (prâ‚‚ IH)

   b'' : B u z
   b'' = prâ‚‚ (prâ‚‚ IH)

   Î³ : Î£ u ê‰ X , iteration (succ n) x u Ã— B u z
   Î³ = u , (y , b , i') , b''

 iteration-symmetric : symmetric B â†’ (m : â„•) â†’ symmetric (iteration m)
 iteration-symmetric sym 0        x x refl        = refl
 iteration-symmetric sym (succ m) x y (z , b , c) = Î³
   where
    c' : iteration m y z
    c' = iteration-symmetric sym m z y c

    Î³ : iteration (succ m) y x
    Î³ = iteration-transitive' m y z x c' (sym x z b)

 iteration-transitive : (m n : â„•) (x y z : X) â†’ iteration m x y â†’ iteration n y z â†’ iteration (m âˆ” n) x z
 iteration-transitive 0        n x x z refl        c' = c'
 iteration-transitive (succ m) n x y z (t , b , c) c' = t , b , iteration-transitive m n t y z c c'

\end{code}

This is regarding the above anonymous module but needs to be outside it:

\begin{code}

private
 _^_ : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¤ Ì‡ ) â†’ â„• â†’ (X â†’ X â†’ ğ“¤ Ì‡ )
 _^_ = iteration

\end{code}

The reflexive-transitive closure of a relation B:

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (B : X â†’ X â†’ ğ“¤ Ì‡ )
       where

 rt-closure : X â†’ X â†’ ğ“¤ Ì‡
 rt-closure x y = Î£ n ê‰ â„• , (B ^ n) x y

 rt-reflexive : reflexive rt-closure
 rt-reflexive x = 0 , refl

 rt-symmetric : symmetric B â†’ symmetric rt-closure
 rt-symmetric s x y (m , c) = m , iteration-symmetric B s m x y c

 rt-transitive : transitive rt-closure
 rt-transitive x y z (m , c) (m' , c') = (m âˆ” m') , iteration-transitive B m m' x y z c c'

 rt-extension : B âŠ‘ rt-closure
 rt-extension x y b = 1 , y , b , refl

 rt-induction : (R : X â†’ X â†’ ğ“¥ Ì‡ )
              â†’ reflexive R
              â†’ transitive R
              â†’ B âŠ‘ R
              â†’ rt-closure âŠ‘ R
 rt-induction R r t B-included-in-R = Î³
  where
   Î³ : (x y : X) â†’ rt-closure x y â†’ R x y
   Î³ x x (0      , refl)      = r x
   Î³ x y (succ n , z , b , c) = t x z y (B-included-in-R x z b) (Î³ z y (n , c))

\end{code}

By combining the symmetric closure with the reflective-transitive
closure, we get the symmetric-reflexive-transitive-closure:

\begin{code}

module _ {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         (A : X â†’ X â†’ ğ“¤ Ì‡ )
       where

 srt-closure : X â†’ X â†’ ğ“¤ Ì‡
 srt-closure = rt-closure (s-closure A)

 srt-symmetric : symmetric srt-closure
 srt-symmetric = rt-symmetric (s-closure A) (s-symmetric A)

 srt-reflexive : reflexive srt-closure
 srt-reflexive = rt-reflexive (s-closure A)

 srt-transitive : transitive srt-closure
 srt-transitive = rt-transitive (s-closure A)

 srt-extension'' : s-closure A âŠ‘ srt-closure
 srt-extension'' = rt-extension (s-closure A)

 srt-extension' : A âŠ‘ s-closure A
 srt-extension' = s-extension A

 srt-extension : A âŠ‘ srt-closure
 srt-extension x y = srt-extension'' x y âˆ˜ srt-extension' x y

 rt-gives-srt : (x y : X) â†’ rt-closure A x y â†’ srt-closure x y
 rt-gives-srt x y (m , i) = g m x y i
  where
   f : (n : â„•) (x y : X) â†’ iteration A n x y â†’ iteration (s-closure A) n x y
   f 0        x x refl        = refl
   f (succ n) x y (z , e , i) = z , inl e , (f n z y i)

   g : (n : â„•) (x y : X) â†’ iteration A n x y â†’ srt-closure x y
   g 0        x x refl        = srt-reflexive x
   g (succ n) x y (z , e , i) = succ n , z , inl e , f n z y i

 srt-induction : (R : X â†’ X â†’ ğ“¥ Ì‡ )
               â†’ symmetric R
               â†’ reflexive R
               â†’ transitive R
               â†’ A âŠ‘ R
               â†’ srt-closure âŠ‘ R
 srt-induction R s r t A-included-in-R x y = Î³
  where
   Î´ : s-closure A âŠ‘ R
   Î´ = s-induction A R s A-included-in-R

   Î³ : srt-closure x y â†’ R x y
   Î³ = rt-induction (s-closure A) R r t Î´ x y

\end{code}

The proposition-valued, symmetric-reflexive-transitive closure of a
relation A:

\begin{code}

module psrt
        (pt : propositional-truncations-exist)
        {ğ“¤ : Universe}
        {X : ğ“¤ Ì‡ }
        (A : X â†’ X â†’ ğ“¤ Ì‡ )
       where

 open PropositionalTruncation pt

 psrt-closure : X â†’ X â†’ ğ“¤ Ì‡
 psrt-closure x y = âˆ¥ srt-closure A x y âˆ¥

 psrt-is-prop-valued : (x y : X) â†’ is-prop (psrt-closure x y)
 psrt-is-prop-valued x y = âˆ¥âˆ¥-is-prop

 psrt-symmetric : symmetric psrt-closure
 psrt-symmetric x y = âˆ¥âˆ¥-functor (srt-symmetric A x y)

 psrt-reflexive : reflexive psrt-closure
 psrt-reflexive x = âˆ£ srt-reflexive A x âˆ£

 psrt-transitive : transitive psrt-closure
 psrt-transitive x y z = âˆ¥âˆ¥-functorâ‚‚ (srt-transitive A x y z)

 psrt-extension : A âŠ‘ psrt-closure
 psrt-extension x y a = âˆ¥âˆ¥-functor (srt-extension A x y) âˆ£ a âˆ£

 psrt-induction : (R : X â†’ X â†’ ğ“¥ Ì‡ )
                â†’ ((x y : X) â†’ is-prop (R x y))
                â†’ reflexive R
                â†’ symmetric R
                â†’ transitive R
                â†’ A âŠ‘ R
                â†’ psrt-closure âŠ‘ R
 psrt-induction R p r s t A-included-in-R x y =
  âˆ¥âˆ¥-rec (p x y) (srt-induction A R s r t A-included-in-R x y)

 psrt-is-equiv-rel : is-equiv-rel psrt-closure
 psrt-is-equiv-rel = psrt-is-prop-valued ,
                     psrt-reflexive ,
                     psrt-symmetric ,
                     psrt-transitive
\end{code}

Any proposition-valued relation that is logically equivalent to an
equivalence relation is itself an equivalence relation. Unfortunately,
we cannot use univalence to perform this transport as the types live
in different universes.

\begin{code}

is-equiv-rel-transport : {X : ğ“¤ Ì‡ }
                         (A : X â†’ X â†’ ğ“¥ Ì‡ )
                         (B : X â†’ X â†’ ğ“¦ Ì‡ )
                       â†’ is-prop-valued-rel B
                       â†’ ((x y : X) â†’ A x y â†” B x y)
                       â†’ is-equiv-rel A
                       â†’ is-equiv-rel B
is-equiv-rel-transport {X} A B p' e (p , r , s , t) = (p' , r' , s' , t')
 where
  r' : reflexive B
  r' x = lr-implication (e x x) (r x)

  s' : symmetric B
  s' x y b = lr-implication (e y x) (s x y (rl-implication (e x y) b))

  t' : transitive B
  t' x y z b b' = lr-implication (e x z)
                    (t x y z (rl-implication (e x y) b)
                             (rl-implication (e y z) b'))
\end{code}
