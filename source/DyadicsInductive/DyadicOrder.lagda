Tom de Jong, 3 - 5 March 2020

As suggested by Martin Escardo.

The (usual) order on the dyadic rationals

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import DyadicsInductive.Dyadics
open import UF.Subsingletons

\end{code}

We inductively define an order â‰º on ğ”» and prove that it is transitive and
linear.

We also show that is is "dense" and "has no endpoints", but formulated using Î£
rather than âˆƒ. Although the proper formulation would use âˆƒ (to ensure that being
dense and having no endpoints is property, rather than structure), we still
prove the Î£-versions for two reasons:
- we can easily prove them and derive the âˆƒ-versions from them;
- so that this module does not depend on propositional truncation.

\begin{code}

module DyadicsInductive.DyadicOrder where

\end{code}

We defined â‰º by using the translation (from ğ”» to (-1,1)) as set out in Dyadic.

\begin{code}

_â‰º_ : ğ”» â†’ ğ”» â†’ ğ“¤â‚€ Ì‡
middle  â‰º middle  = ğŸ˜
middle  â‰º left y  = ğŸ˜
middle  â‰º right y = ğŸ™
left x  â‰º middle  = ğŸ™
left x  â‰º left y  = x â‰º y
left x  â‰º right y = ğŸ™
right x â‰º middle  = ğŸ˜
right x â‰º left y  = ğŸ˜
right x â‰º right y = x â‰º y

\end{code}

Monotonicity of left and right holds by definition (so we will never call these
lemmas), but we record them here for clarity.

\begin{code}

left-monotone : {x y : ğ”»} â†’ x â‰º y â†’ left x â‰º left y
left-monotone = id

right-monotone : {x y : ğ”»} â†’ x â‰º y â†’ right x â‰º right y
right-monotone = id

\end{code}

\begin{code}

â‰º-is-prop-valued : (x y : ğ”») â†’ is-prop (x â‰º y)
â‰º-is-prop-valued middle    middle    = ğŸ˜-is-prop
â‰º-is-prop-valued middle    (left y)  = ğŸ˜-is-prop
â‰º-is-prop-valued middle    (right y) = ğŸ™-is-prop
â‰º-is-prop-valued (left x)  middle    = ğŸ™-is-prop
â‰º-is-prop-valued (left x)  (left y)  = â‰º-is-prop-valued x y
â‰º-is-prop-valued (left x)  (right y) = ğŸ™-is-prop
â‰º-is-prop-valued (right x) middle    = ğŸ˜-is-prop
â‰º-is-prop-valued (right x) (left y)  = ğŸ˜-is-prop
â‰º-is-prop-valued (right x) (right y) = â‰º-is-prop-valued x y

â‰º-is-transitive : (x y z : ğ”») â†’ x â‰º y â†’ y â‰º z â†’ x â‰º z
â‰º-is-transitive middle middle z = ğŸ˜-induction
â‰º-is-transitive middle (left y) middle = ğŸ˜-induction
â‰º-is-transitive middle (left y) (left z) = ğŸ˜-induction
â‰º-is-transitive middle (left y) (right z) = ğŸ˜-induction
â‰º-is-transitive middle (right y) middle _ = id
â‰º-is-transitive middle (right y) (left z) _ = id
â‰º-is-transitive middle (right y) (right z) _ _ = â‹†
â‰º-is-transitive (left x) middle middle _ _ = â‹†
â‰º-is-transitive (left x) middle (left z) _ = ğŸ˜-induction
â‰º-is-transitive (left x) middle (right z) _ = id
â‰º-is-transitive (left x) (left y) middle _ = id
â‰º-is-transitive (left x) (left y) (left z) = â‰º-is-transitive x y z
â‰º-is-transitive (left x) (left y) (right z) _ = id
â‰º-is-transitive (left x) (right y) middle _ _ = â‹†
â‰º-is-transitive (left x) (right y) (left z) _ = ğŸ˜-induction
â‰º-is-transitive (left x) (right y) (right z) _ _ = â‹†
â‰º-is-transitive (right x) middle z = ğŸ˜-induction
â‰º-is-transitive (right x) (left y) z = ğŸ˜-induction
â‰º-is-transitive (right x) (right y) middle _ = id
â‰º-is-transitive (right x) (right y) (left z) _ = id
â‰º-is-transitive (right x) (right y) (right z) = â‰º-is-transitive x y z

â‰º-is-linear : (x y : ğ”») â†’ x â‰  y â†’ x â‰º y + y â‰º x
â‰º-is-linear middle middle p = ğŸ˜-induction (p refl)
â‰º-is-linear middle (left y) _ = inr â‹†
â‰º-is-linear middle (right y) _ = inl â‹†
â‰º-is-linear (left x) middle _ = inl â‹†
â‰º-is-linear (left x) (left y) lxâ‰ ly = â‰º-is-linear x y xâ‰ y
 where
  xâ‰ y : x â‰  y
  xâ‰ y = contrapositive (ap left) lxâ‰ ly
â‰º-is-linear (left x) (right y) _ = inl â‹†
â‰º-is-linear (right x) middle _ = inr â‹†
â‰º-is-linear (right x) (left y) _ = inr â‹†
â‰º-is-linear (right x) (right y) rxâ‰ ry = â‰º-is-linear x y xâ‰ y
 where
  xâ‰ y : x â‰  y
  xâ‰ y = contrapositive (ap right) rxâ‰ ry

\end{code}

Discreteness of ğ”» and linearity of â‰º imply that â‰º is trichotomous, i.e. for
every x y : ğ”» , x â‰º y or x ï¼ y or y â‰º x holds. The lemmas after
â‰º-is-trichotomous show that exactly one of these is the case, as witnessed by
trichotomy-is-a-singleton.

\begin{code}

â‰º-is-trichotomous : (x y : ğ”») â†’ x â‰º y + (x ï¼ y) + (y â‰º x)
â‰º-is-trichotomous x y = cases a b (ğ”»-is-discrete x y)
 where
  a : x ï¼ y â†’ (x â‰º y) + (x ï¼ y) + (y â‰º x)
  a = inr âˆ˜ inl
  b : (x â‰  y) â†’ (x â‰º y) + (x ï¼ y) + (y â‰º x)
  b n = cases c d (â‰º-is-linear x y n)
   where
    c : x â‰º y â†’ (x â‰º y) + (x ï¼ y) + (y â‰º x)
    c = inl
    d : y â‰º x â†’ (x â‰º y) + (x ï¼ y) + (y â‰º x)
    d = inr âˆ˜ inr

â‰º-to-â‰  : {x y : ğ”»} â†’ x â‰º y â†’ x â‰  y
â‰º-to-â‰  {middle}  {middle}      = ğŸ˜-induction
â‰º-to-â‰  {middle}  {left y}      = ğŸ˜-induction
â‰º-to-â‰  {middle}  {right y} _   = middle-is-not-right
â‰º-to-â‰  {left x}  {middle}  _   = (Î» p â†’ middle-is-not-left (p â»Â¹))
â‰º-to-â‰  {left x}  {left y}  xâ‰ºy = contrapositive left-lc (â‰º-to-â‰  xâ‰ºy)
â‰º-to-â‰  {left x}  {right y} _   = left-is-not-right
â‰º-to-â‰  {right x} {middle}      = ğŸ˜-induction
â‰º-to-â‰  {right x} {left y}      = ğŸ˜-induction
â‰º-to-â‰  {right x} {right y} xâ‰ºy = contrapositive right-lc (â‰º-to-â‰  xâ‰ºy)

â‰º-to-â‰ ' : {x y : ğ”»} â†’ y â‰º x â†’ x â‰  y
â‰º-to-â‰ ' l e = â‰º-to-â‰  l (e â»Â¹)

ï¼-to-Â¬â‰º : {x y : ğ”»} â†’ x ï¼ y â†’ Â¬ (x â‰º y)
ï¼-to-Â¬â‰º e l = â‰º-to-â‰  l e

ï¼-to-Â¬â‰º' : {x y : ğ”»} â†’ x ï¼ y â†’ Â¬ (y â‰º x)
ï¼-to-Â¬â‰º' e l = â‰º-to-â‰  l (e â»Â¹)

â‰º-to-Â¬â‰º : (x y : ğ”») â†’ x â‰º y â†’ Â¬ (y â‰º x)
â‰º-to-Â¬â‰º middle    middle      = ğŸ˜-induction
â‰º-to-Â¬â‰º middle    (left y)    = ğŸ˜-induction
â‰º-to-Â¬â‰º middle    (right y) _ = id
â‰º-to-Â¬â‰º (left x)  middle    _ = id
â‰º-to-Â¬â‰º (left x)  (left y)    = â‰º-to-Â¬â‰º x y
â‰º-to-Â¬â‰º (left x)  (right y) _ = id
â‰º-to-Â¬â‰º (right x) middle      = ğŸ˜-induction
â‰º-to-Â¬â‰º (right x) (left y)    = ğŸ˜-induction
â‰º-to-Â¬â‰º (right x) (right y)   = â‰º-to-Â¬â‰º x y

trichotomy-is-a-singleton : {x y : ğ”»} â†’ is-singleton (x â‰º y + (x ï¼ y) + y â‰º x)
trichotomy-is-a-singleton {x} {y} =
 pointed-props-are-singletons (â‰º-is-trichotomous x y) Î³
  where
   Î³ : is-prop (x â‰º y + (x ï¼ y) + y â‰º x)
   Î³ = +-is-prop (â‰º-is-prop-valued x y) h g
    where
     h : is-prop ((x ï¼ y) + y â‰º x)
     h = +-is-prop ğ”»-is-set (â‰º-is-prop-valued y x) ï¼-to-Â¬â‰º'
     g : x â‰º y â†’ Â¬ ((x ï¼ y) + y â‰º x)
     g l = cases a b
      where
       a : x â‰  y
       a = â‰º-to-â‰  l
       b : Â¬ (y â‰º x)
       b = â‰º-to-Â¬â‰º x y l

\end{code}

Next, we prove that â‰º has no endpoints and is dense (although formulated with Î£,
as explained at the top of this file).

\begin{code}

left-â‰º : (x : ğ”») â†’ left x â‰º x
left-â‰º middle    = â‹†
left-â‰º (left x)  = left-â‰º x
left-â‰º (right x) = â‹†

â‰º-right : (x : ğ”») â†’ x â‰º right x
â‰º-right middle    = â‹†
â‰º-right (left x)  = â‹†
â‰º-right (right x) = â‰º-right x

â‰º-has-no-left-endpoint-Î£ : (x : ğ”») â†’ Î£ y ê‰ ğ”» , y â‰º x
â‰º-has-no-left-endpoint-Î£ x = left x , left-â‰º x

â‰º-has-no-right-endpoint-Î£ : (x : ğ”») â†’ Î£ y ê‰ ğ”» , x â‰º y
â‰º-has-no-right-endpoint-Î£ x = right x , â‰º-right x

â‰º-is-dense-Î£ : (x y : ğ”») â†’ x â‰º y â†’ Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y
â‰º-is-dense-Î£ middle (right y) _ = right (left y) , â‹† , left-â‰º y
â‰º-is-dense-Î£ (left x) middle _ = left (right x) , â‰º-right x , â‹†
â‰º-is-dense-Î£ (left x) (left y) xâ‰ºy = Î³ (â‰º-is-dense-Î£ x y xâ‰ºy)
 where
  Î³ : (Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , left x â‰º z Ã— z â‰º left y
  Î³ (z , xâ‰ºz , zâ‰ºy) = left z , xâ‰ºz , zâ‰ºy
â‰º-is-dense-Î£ (left x) (right y) _ = middle , â‹† , â‹†
â‰º-is-dense-Î£ (right x) middle = ğŸ˜-induction
â‰º-is-dense-Î£ (right x) (left y) = ğŸ˜-induction
â‰º-is-dense-Î£ (right x) (right y) l = Î³ (â‰º-is-dense-Î£ x y l)
 where
  Î³ : (Î£ z ê‰ ğ”» , x â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , right x â‰º z Ã— z â‰º right y
  Î³ (z , m , n) = right z , m , n

\end{code}

Binary interpolation is a generalisation of density, which can, in our case, be
proved from density using trichotomy of â‰º.

We will need this property to construct the (rounded) ideal completion of
(ğ”» , â‰º).

\begin{code}

â‰º-interpolationâ‚‚-Î£ : (xâ‚ xâ‚‚ y : ğ”») â†’ xâ‚ â‰º y â†’ xâ‚‚ â‰º y
                   â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
â‰º-interpolationâ‚‚-Î£ xâ‚ xâ‚‚ y lâ‚ lâ‚‚ = casesâ‚ƒ a b c (â‰º-is-trichotomous xâ‚ xâ‚‚)
 where
  a : xâ‚ â‰º xâ‚‚ â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
  a k = Î³ (â‰º-is-dense-Î£ xâ‚‚ y lâ‚‚)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚‚ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
    Î³ (z , m , n) = z , â‰º-is-transitive xâ‚ xâ‚‚ z k m , m , n
  b : xâ‚ ï¼ xâ‚‚ â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
  b refl = Î³ (â‰º-is-dense-Î£ xâ‚ y lâ‚)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
    Î³ (z , m , n) = z , m , m , n
  c : xâ‚‚ â‰º xâ‚ â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
  c k = Î³ (â‰º-is-dense-Î£ xâ‚ y lâ‚)
   where
    Î³ : (Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— z â‰º y) â†’ Î£ z ê‰ ğ”» , xâ‚ â‰º z Ã— xâ‚‚ â‰º z Ã— z â‰º y
    Î³ (z , m , n) = z , m , â‰º-is-transitive xâ‚‚ xâ‚ z k m , n

\end{code}
