Tom de Jong, 3 - 5 March 2020

As suggested by Martin Escardo.

Dyadic rationals (https://en.wikipedia.org/wiki/Dyadic_rational)

\begin{code}

{-# OPTIONS --safe --without-K #-}

module DyadicsInductive.Dyadics where

open import MLTT.Spartan
open import MLTT.Unit-Properties
open import UF.DiscreteAndSeparated
open import UF.Sets
open import UF.Subsingletons

\end{code}

We inductively construct dyadic rationals in (-1,1), as follows.
Start with the point 0 in the middle (represented by middle below).
Define two functions (represented by left and right below):

l : (-1,1) â†’ (-1,1)
l x = (x-1)/2

r : (-1,1) â†’ (-1,1)
r x = (x+1)/2

Some values (ordered) to give an impression:

                   0                  -- just 0
        -1/2                1/2       -- l 0 = -1/2 and r 0 = 1/2
   -3/4      -1/4      1/4       3/4  -- l (l 0), l (r 0), r (l 0) and r (r 0)

In this module we just define the type and prove that it has decidable
equality. The order on ğ”» is defined in the separate module Dyadic-Order.

\begin{code}

data ğ”» : ğ“¤â‚€ Ì‡ where
  middle : ğ”»
  left   : ğ”» â†’ ğ”»
  right  : ğ”» â†’ ğ”»

\end{code}

Using the well-known encode-decode method (cf. Section 2.13 of the HoTT book),
we can show that right and left are injective and that ğ”» is discrete (i.e. it
has decidable equality).

By Hedberg's Theorem, ğ”» is a set.

\begin{code}

middle-is-not-left : {x : ğ”»} â†’ middle â‰  left x
middle-is-not-left p = ğŸ™-is-not-ğŸ˜ (ap f p)
 where
  f : ğ”» â†’ ğ“¤â‚€ Ì‡
  f middle    = ğŸ™
  f (left _)  = ğŸ˜
  f (right _) = ğŸ˜

middle-is-not-right : {x : ğ”»} â†’ middle â‰  right x
middle-is-not-right p = ğŸ™-is-not-ğŸ˜ (ap f p)
 where
  f : ğ”» â†’ ğ“¤â‚€ Ì‡
  f middle    = ğŸ™
  f (left _)  = ğŸ˜
  f (right _) = ğŸ˜

left-is-not-right : {x y : ğ”»} â†’ left x â‰  right y
left-is-not-right p = ğŸ™-is-not-ğŸ˜ (ap f p)
 where
  f : ğ”» â†’ ğ“¤â‚€ Ì‡
  f middle    = ğŸ™
  f (left x)  = ğŸ™
  f (right x) = ğŸ˜

left-lc : {x y : ğ”»} â†’ left x ï¼ left y â†’ x ï¼ y
left-lc = ap f
 where
  f : ğ”» â†’ ğ”»
  f middle    = middle
  f (left x)  = x
  f (right x) = right x

right-lc : {x y : ğ”»} â†’ right x ï¼ right y â†’ x ï¼ y
right-lc = ap f
 where
  f : ğ”» â†’ ğ”»
  f middle    = middle
  f (left x)  = left x
  f (right x) = x

ğ”»-is-discrete : is-discrete ğ”»
ğ”»-is-discrete middle    middle    = inl refl
ğ”»-is-discrete middle    (left y)  = inr middle-is-not-left
ğ”»-is-discrete middle    (right y) = inr middle-is-not-right
ğ”»-is-discrete (left x)  middle    = inr (Î» p â†’ middle-is-not-left (p â»Â¹))
ğ”»-is-discrete (left x)  (left y)  = cases a b (ğ”»-is-discrete x y)
 where
  a : x ï¼ y â†’ is-decidable (left x ï¼ left y)
  a = inl âˆ˜ ap left
  b : Â¬ (x ï¼ y) â†’ is-decidable (left x ï¼ left y)
  b = inr âˆ˜ contrapositive left-lc
ğ”»-is-discrete (left x)  (right y) = inr left-is-not-right
ğ”»-is-discrete (right x) middle    = inr (Î» p â†’ middle-is-not-right (p â»Â¹))
ğ”»-is-discrete (right x) (left y)  = inr (Î» p â†’ left-is-not-right (p â»Â¹))
ğ”»-is-discrete (right x) (right y) = cases a b (ğ”»-is-discrete x y)
 where
  a : x ï¼ y â†’ is-decidable (right x ï¼ right y)
  a = inl âˆ˜ ap right
  b : Â¬ (x ï¼ y) â†’ is-decidable (right x ï¼ right y)
  b = inr âˆ˜ contrapositive right-lc

ğ”»-is-set : is-set ğ”»
ğ”»-is-set = discrete-types-are-sets ğ”»-is-discrete

\end{code}
