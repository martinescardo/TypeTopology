Andrew Sneap, 17 February 2022

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan renaming (_+_ to _âˆ”_)

open import Integers.Exponentiation
open import Integers.Multiplication
open import Integers.Order
open import Integers.Parity
open import Integers.Type
open import MLTT.Plus-Properties
open import Naturals.Addition
open import Naturals.Division
open import Naturals.Exponentiation
open import Naturals.HCF
open import Naturals.Multiplication renaming (_*_ to _â„•*_)
open import Naturals.Order
open import Naturals.Parity
open import Naturals.Properties
open import Notation.Order
open import Rationals.Fractions hiding (_â‰ˆ_ ; â‰ˆ-sym ; â‰ˆ-trans ; â‰ˆ-refl)
open import Rationals.Multiplication renaming (_*_ to _â„š*_)
open import Rationals.Type
open import TypeTopology.SigmaDiscreteAndTotallySeparated
open import UF.Base hiding (_â‰ˆ_)
open import UF.DiscreteAndSeparated
open import UF.Sets
open import UF.Subsingletons

module Dyadics.Type where

\end{code}

We will define the dyadics as a sigma type. Hence, we begin by stating
the type of the property which defines a dyadic. The condition is that
either the denominator is zero, or the denominator is greater than
zero, but the numerator is odd. This type contains "simplified"
dyadics.

By properties of order, naturals, integers it follows that the dyadics
are a set.

\begin{code}

is-â„¤[1/2] : (z : â„¤) (n : â„•) â†’ ğ“¤â‚€ Ì‡
is-â„¤[1/2] z n = (n ï¼ 0) âˆ” (n > 0 Ã— â„¤odd z)

is-â„¤[1/2]-is-prop : (z : â„¤) (n : â„•) â†’ is-prop (is-â„¤[1/2] z n)
is-â„¤[1/2]-is-prop z n = +-is-prop â„•-is-set II I
 where
  I : n ï¼ 0 â†’ Â¬ (0 < n Ã— â„¤odd z)
  I nï¼0 (0<n , odd-z) = not-less-than-itself 0 (transport (0 <_) nï¼0 0<n)

  II : is-prop (0 < n Ã— â„¤odd z)
  II = Ã—-is-prop (<-is-prop-valued 0 n) (â„¤odd-is-prop z)

is-â„¤[1/2]-is-discrete : ((z , n) : â„¤ Ã— â„•) â†’ is-discrete (is-â„¤[1/2] z n)
is-â„¤[1/2]-is-discrete (z , n) = props-are-discrete (is-â„¤[1/2]-is-prop z n)

â„¤[1/2] : ğ“¤â‚€ Ì‡
â„¤[1/2] = Î£ (z , n) ê‰ â„¤ Ã— â„• , is-â„¤[1/2] z n

â„¤[1/2]-is-discrete : is-discrete â„¤[1/2]
â„¤[1/2]-is-discrete = Î£-is-discrete I is-â„¤[1/2]-is-discrete
 where
  I : is-discrete (â„¤ Ã— â„•)
  I = Ã—-is-discrete â„¤-is-discrete â„•-is-discrete

â„¤[1/2]-is-set : is-set â„¤[1/2]
â„¤[1/2]-is-set = discrete-types-are-sets â„¤[1/2]-is-discrete

0â„¤[1/2] : â„¤[1/2]
0â„¤[1/2] = (pos 0 , 0) , (inl refl)

1â„¤[1/2] : â„¤[1/2]
1â„¤[1/2] = (pos 1 , 0) , (inl refl)

1/2â„¤[1/2] : â„¤[1/2]
1/2â„¤[1/2] = (pos 1 , 1) , (inr (â‹† , â‹†))

\end{code}

To define operations on dyadics, we need to consider how to normalise
dyadics into their simplified forms. For example, multiplication of
dyadics using standard rational multiplication gives
numerator/denominator combinations which are not always in lowest
terms. Hence, we must factor our operations through a "normalisation"
function, similarly to our approach to standard rationals.

Due to this normalisation, we introduce an equivalence relation, and
prove that equivalent dyadics are equal. In order to prove properties
of dyadic operations, we will prove that dyadics are equivalent.

\begin{code}

normalise-pos-lemma : (z : â„¤) (n : â„•) â†’ â„¤[1/2]
normalise-pos-lemma z 0        = (z , 0) , (inl refl)
normalise-pos-lemma z (succ n) =
 Cases (â„¤even-or-odd z) case-even case-odd
 where
  case-even : â„¤even z â†’ â„¤[1/2]
  case-even ez = (Î» (k , e) â†’ normalise-pos-lemma k n) divide-by-two
   where
    divide-by-two : Î£ k ê‰ â„¤ , z ï¼ pos 2 * k
    divide-by-two = â„¤even-is-multiple-of-two z ez

  case-odd : â„¤odd z â†’ â„¤[1/2]
  case-odd oz = (z , succ n) , inr (â‹† , oz)

normalise-pos : â„¤ Ã— â„• â†’ â„¤[1/2]
normalise-pos (z , n) = normalise-pos-lemma z n

dnum : â„¤[1/2] â†’ â„¤
dnum ((p , _) , _) = p

dden : â„¤[1/2] â†’ â„•
dden ((_ , a) , _) = a

\end{code}

We can now retrieve properties of a normalised dyadic rational by
pattern matching and evaluating cases. This requires a number of
lemmas, including finding the numerators and denominators for specific
values of p, and the inductive step of dividing through by a factor of
2.

\begin{code}

normalise-pos-odd-num : ((p , a) : â„¤ Ã— â„•) â†’ â„¤odd p
                                          â†’ dnum (normalise-pos (p , a)) ï¼ p
normalise-pos-odd-num (p , 0)      odd-p = refl
normalise-pos-odd-num (p , succ a) odd-p = equality-cases (â„¤even-or-odd p) I II
 where
  I : (ep : â„¤even p) â†’ â„¤even-or-odd p ï¼ inl ep
                     â†’ dnum (normalise-pos (p , succ a)) ï¼ p
  I ep _ = ğŸ˜-elim (â„¤even-not-odd p ep odd-p)

  II : (op : â„¤odd p) â†’ â„¤even-or-odd p ï¼ inr op
                     â†’ dnum (normalise-pos (p , succ a)) ï¼ p
  II op e = ap dnum (Cases-equality-r _ _ (â„¤even-or-odd p) op e)

normalise-pos-odd-denom : ((p , a) : â„¤ Ã— â„•) â†’ â„¤odd p
                                            â†’ dden (normalise-pos (p , a)) ï¼ a
normalise-pos-odd-denom (p , 0)      odd-p = refl
normalise-pos-odd-denom (p , succ a) odd-p = equality-cases (â„¤even-or-odd p) I II
 where
  I : (ep : â„¤even p) â†’ â„¤even-or-odd p ï¼ inl ep
                      â†’ dden (normalise-pos (p , succ a)) ï¼ succ a
  I ep e = ğŸ˜-elim (â„¤even-not-odd p ep odd-p)

  II : (op : â„¤odd p) â†’ â„¤even-or-odd p ï¼ inr op
                      â†’ dden (normalise-pos (p , succ a)) ï¼ succ a
  II op e = ap dden (Cases-equality-r _ _ (â„¤even-or-odd p) op e)

normalise-pos-even-prev : (p : â„¤) (a : â„•)
                        â†’ (ep : â„¤even p)
                        â†’ ((p/2 , _) : Î£ p/2 ê‰ â„¤ , p ï¼ pos 2 * p/2)
                        â†’ normalise-pos (p/2 , a) ï¼ normalise-pos (p , succ a)
normalise-pos-even-prev p a ep (p/2 , e) = equality-cases (â„¤even-or-odd p) I II
 where
  I : (even-p : â„¤even p)
    â†’ â„¤even-or-odd p ï¼ inl even-p
    â†’ normalise-pos (p/2 , a) ï¼ normalise-pos (p , succ a)
  I even-p eâ‚‚
   = normalise-pos (p/2 , a)        ï¼âŸ¨ refl âŸ©
     normalise-pos-lemma p/2 a      ï¼âŸ¨ i âŸ©
     normalise-pos-lemma p/2' a     ï¼âŸ¨ ii âŸ©
     normalise-pos-lemma p (succ a) ï¼âŸ¨ refl âŸ©
     normalise-pos (p , succ a)     âˆ
   where
    p/2' : â„¤
    p/2' = prâ‚ (â„¤even-is-multiple-of-two p even-p)

    eâ‚ƒ : p ï¼ pos 2 * p/2'
    eâ‚ƒ = prâ‚‚ (â„¤even-is-multiple-of-two p even-p)

    eâ‚„ : pos 2 * p/2 ï¼ pos 2 * p/2'
    eâ‚„ = pos 2 * p/2 ï¼âŸ¨ e â»Â¹ âŸ©
         p           ï¼âŸ¨ eâ‚ƒ âŸ©
         pos 2 * p/2' âˆ

    halfs-of-p-equal : p/2 ï¼ p/2'
    halfs-of-p-equal = â„¤-mult-left-cancellable p/2 p/2' (pos 2) id eâ‚„

    i : normalise-pos-lemma p/2 a ï¼ normalise-pos-lemma p/2' a
    i = ap (Î» - â†’ normalise-pos-lemma - a) halfs-of-p-equal

    ii : normalise-pos-lemma p/2' a ï¼ normalise-pos-lemma p (succ a)
    ii = Cases-equality-l _ _ (â„¤even-or-odd p) even-p eâ‚‚ â»Â¹

  II : (op : â„¤odd p) â†’ â„¤even-or-odd p ï¼ inr op
                     â†’ normalise-pos (p/2 , a) ï¼ normalise-pos (p , succ a)
  II op = ğŸ˜-elim (â„¤even-not-odd p ep op)

normalise-pos-info' : (p : â„¤) â†’ (a : â„•)
                    â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , a)) * pos (2^ k))
                    Ã— (a ï¼ dden (normalise-pos (p , a)) + k)
normalise-pos-info' p 0      = 0 , refl , refl
normalise-pos-info' p  (succ a) = equality-cases (â„¤even-or-odd p) I II
 where
  I : (ep : â„¤even p)
    â†’ â„¤even-or-odd p ï¼ inl ep
    â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , succ a)) * pos (2^ k))
              Ã— (succ a ï¼ dden (normalise-pos (p , succ a)) + k)
  I ep e = Î³â‚ (â„¤even-is-multiple-of-two p ep)
   where
    Î³â‚ : Î£ p/2 ê‰ â„¤ , p ï¼ pos 2 * p/2
       â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , succ a)) * pos (2^ k))
                 Ã— (succ a ï¼ dden (normalise-pos (p , succ a)) + k)
    Î³â‚ (p/2 , eâ‚‚) = Î³â‚‚ (normalise-pos-info' p/2 a)
     where
      Î³â‚‚ : (Î£ k' ê‰ â„• , (p/2 ï¼ dnum (normalise-pos (p/2 , a)) * pos (2^ k')) Ã—
                         (a ï¼ dden (normalise-pos (p/2 , a)) + k'))

          â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , succ a)) * pos (2^ k))
                    Ã— (succ a ï¼ dden (normalise-pos (p , succ a)) + k)
      Î³â‚‚ (k' , eâ‚ƒ , eâ‚„) = (succ k') , Î± , Î²
       where
        k'' = pos (2^ k')
        Î± : p ï¼ dnum (normalise-pos (p , succ a)) * pos (2^ (succ k'))
        Î± = p                                                      ï¼âŸ¨ eâ‚‚  âŸ©
            pos 2 * p/2                                            ï¼âŸ¨ i   âŸ©
            pos 2 * (dnum (normalise-pos (p/2 , a)) * k'')         ï¼âŸ¨ ii  âŸ©
            pos 2 * dnum (normalise-pos (p/2 , a)) * k''           ï¼âŸ¨ iii âŸ©
            dnum (normalise-pos (p/2 , a)) * pos 2 * k''           ï¼âŸ¨ iv  âŸ©
            dnum (normalise-pos (p/2 , a)) * (pos 2 * k'')         ï¼âŸ¨ v   âŸ©
            dnum (normalise-pos (p/2 , a)) * pos (2^ (succ k'))    ï¼âŸ¨ vi  âŸ©
            dnum (normalise-pos (p , succ a)) * pos (2^ (succ k')) âˆ
         where
          i   = ap (pos 2 *_) eâ‚ƒ
          ii  = â„¤*-assoc (pos 2) (dnum (normalise-pos (p/2 , a))) k'' â»Â¹
          iii = ap (_* k'') (â„¤*-comm (pos 2) (dnum (normalise-pos (p/2 , a))))
          iv  = â„¤*-assoc (dnum (normalise-pos (p/2 , a))) (pos 2) k''
          v   = ap (dnum (normalise-pos (p/2 , a)) *_)
                 (pos-multiplication-equiv-to-â„• 2 (2^ k'))
          vi  = ap (Î» - â†’ dnum - * pos (2^ (succ k')))
                 (normalise-pos-even-prev p a ep (p/2 , eâ‚‚))

        Î² : succ a ï¼ dden (normalise-pos (p , succ a)) + succ k'
        Î² = succ a                                       ï¼âŸ¨ i    âŸ©
             succ (dden (normalise-pos (p/2 , a)) + k')  ï¼âŸ¨ refl âŸ©
             dden (normalise-pos (p/2 , a)) + succ k'    ï¼âŸ¨ ii   âŸ©
             dden (normalise-pos (p , succ a)) + succ k' âˆ
         where
          i = ap succ eâ‚„
          ii = ap (Î» - â†’ dden - + succ k')
                (normalise-pos-even-prev p a ep (p/2 , eâ‚‚))

  II : (op : â„¤odd p)
    â†’ â„¤even-or-odd p ï¼ inr op
    â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , succ a)) * pos (2^ k))
              Ã— (succ a ï¼ dden (normalise-pos (p , succ a)) + k)
  II op e = 0 , i , ii
   where
    i : p ï¼ dnum (normalise-pos (p , succ a))
    i = normalise-pos-odd-num (p , succ a) op â»Â¹

    ii : succ a ï¼ dden (normalise-pos (p , succ a))
    ii = normalise-pos-odd-denom (p , succ a) op â»Â¹

\end{code}

This function finds the k value for which (2^k) is a common factor of
a dyadic rational. It is proved with it's arguments seperated in the
above function, to satisy Agda's termination checker.

\begin{code}

normalise-pos-info : ((p , a) : â„¤ Ã— â„•)
                   â†’ Î£ k ê‰ â„• , (p ï¼ dnum (normalise-pos (p , a)) * pos (2^ k))
                             Ã— (a ï¼ dden (normalise-pos (p , a)) + k)
normalise-pos-info (p , a) = normalise-pos-info' p a

\end{code}

We also defined a normalisation process for when an operation results
in a negative on the denominator (i.e 2^ (- k)) for some (k : â„•). This
is not needed for the standard field operations but may be useful for
more exotic dyadic rational functions.

\begin{code}

normalise-neg-lemma : (z : â„¤) (n : â„•) â†’ â„¤[1/2]
normalise-neg-lemma z 0        = (z * pos 2 , 0) , (inl refl)
normalise-neg-lemma z (succ n) = normalise-neg-lemma (z * pos 2) n

normalise-neg : â„¤ Ã— â„• â†’ â„¤[1/2]
normalise-neg (z , n) = normalise-neg-lemma z n

normalise : â„¤ Ã— â„¤ â†’ â„¤[1/2]
normalise (z , pos n)     = normalise-pos (z , n)
normalise (z , negsucc n) = normalise-neg (z , n)

from-â„¤[1/2] : â„¤[1/2] â†’ â„¤ Ã— â„•
from-â„¤[1/2] = prâ‚

\end{code}

We define two equivalence relations. The first is by considering an
equivalence on a numerator / denominator pair only, without the proof
that they are simplified. The second defines an equivalence on two
dyadic rationals, and is defined in terms of the first.

Sometimes we have two dyadics rationals of the form (p , Î±) (q , Î²),
and we want to prove equality using an equivalence p â‰ˆ' q. In other
cases we may have dyadic rationals of the form (normalise-pos p)
(normalise-pos q), and we want to prove equality using the equivalence
using p â‰ˆ' q.

Usually, the first case isn't fruitful, and instead we prove that
 (p , Î±) â‰ˆ normalise-pos p,
 (q , Î²) â‰ˆ normalise-pos q.

Given an operation _âŠ™_, we then show that
 (p , Î±) âŠ™ (q , Î²) ï¼ (normalise-pos p) âŠ™ (normalise-pos q)
                   ï¼ normalise-pos (p âŠ™' q)
for some operation _âŠ™'_ defined on (â„¤ Ã— â„•) Ã— (â„¤ Ã— â„•).

All of this machinery, as well as the usual equivalence laws are
defined below.

\begin{code}

_â‰ˆ'_ : (p q : â„¤ Ã— â„•) â†’ ğ“¤â‚€ Ì‡
(x , n) â‰ˆ' (y , m) = x * pos (2^ m) ï¼ y * pos (2^ n)

â‰ˆ'-sym : (p q : â„¤ Ã— â„•) â†’ p â‰ˆ' q â†’ q â‰ˆ' p
â‰ˆ'-sym p q e = e â»Â¹

â‰ˆ'-trans : (p q r : â„¤ Ã— â„•) â†’ p â‰ˆ' q â†’ q â‰ˆ' r â†’ p â‰ˆ' r
â‰ˆ'-trans (x , n) (y , m) (z , p) eâ‚ eâ‚‚ = Î³
 where
  p' m' n' : â„¤
  p' = pos (2^ p)
  m' = pos (2^ m)
  n' = pos (2^ n)

  I : x * p' * m' ï¼ z * n' * m'
  I = x * p' * m' ï¼âŸ¨ â„¤-mult-rearrangement x p' m' âŸ©
      x * m' * p' ï¼âŸ¨ ap (_* p') eâ‚                âŸ©
      y * n' * p' ï¼âŸ¨ â„¤-mult-rearrangement y n' p' âŸ©
      y * p' * n' ï¼âŸ¨ ap (_* n') eâ‚‚                âŸ©
      z * m' * n' ï¼âŸ¨ â„¤-mult-rearrangement z m' n' âŸ©
      z * n' * m' âˆ

  VI : not-zero m'
  VI = exponents-not-zero' m

  Î³ : x * p' ï¼ z * n'
  Î³ = â„¤-mult-right-cancellable (x * p') (z * n') m' VI I

â‰ˆ'-refl : (p : â„¤ Ã— â„•) â†’ p â‰ˆ' p
â‰ˆ'-refl p = refl

_â‰ˆ_ : (p q : â„¤[1/2]) â†’ ğ“¤â‚€ Ì‡
(p , _) â‰ˆ (q , _) = p â‰ˆ' q

infix 0 _â‰ˆ_

â‰ˆ-sym : (x y : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ x
â‰ˆ-sym (p , _) (q , _) e = â‰ˆ'-sym p q e

â‰ˆ-trans : (x y z : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ z â†’ x â‰ˆ z
â‰ˆ-trans (p , _) (q , _) (r , _) eâ‚ eâ‚‚ = â‰ˆ'-trans p q r eâ‚ eâ‚‚

â‰ˆ-refl : (p : â„¤[1/2]) â†’ p â‰ˆ p
â‰ˆ-refl (p , _) = â‰ˆ'-refl p

â‰ˆ'-to-ï¼-0 : ((x , m) (y , n) : â„¤ Ã— â„•)
              â†’ (x , m) â‰ˆ' (y , n)
              â†’ m ï¼ 0
              â†’ n ï¼ 0
              â†’ (x , m) ï¼ (y , n)
â‰ˆ'-to-ï¼-0 (x , m) (y , n) e mï¼0 nï¼0 = to-Ã—-ï¼ I (mï¼0 âˆ™ nï¼0 â»Â¹)
 where
  I : x ï¼ y
  I = x              ï¼âŸ¨ refl                                  âŸ©
      x * pos (2^ 0) ï¼âŸ¨ ap (Î» z â†’ x * (pos (2^ z))) (nï¼0 â»Â¹) âŸ©
      x * pos (2^ n) ï¼âŸ¨ e                                     âŸ©
      y * pos (2^ m) ï¼âŸ¨ ap (Î» z â†’ y * (pos (2^ z))) mï¼0      âŸ©
      y * pos (2^ 0) ï¼âŸ¨ refl                                  âŸ©
      y              âˆ

â‰ˆ'-lt-consequence : ((x , m) (y , n) : â„¤ Ã— â„•)
                  â†’ (x , m) â‰ˆ' (y , n) â†’ m ï¼ 0 â†’ Â¬ (n > 0 Ã— â„¤odd y)
â‰ˆ'-lt-consequence (x , m) (y , 0)      e mï¼0 (n>0 , oy) = ğŸ˜-elim n>0
â‰ˆ'-lt-consequence (x , m) (y , succ n) e mï¼0 (n>0 , oy) = Î³
 where
  I : x * pos (2^ (succ n)) ï¼ y
  I = x * pos (2^ (succ n)) ï¼âŸ¨ e âŸ©
      y * pos (2^ m)        ï¼âŸ¨ ap (Î» - â†’ y * pos (2^ -)) mï¼0 âŸ©
      y * pos (2^ 0)        ï¼âŸ¨ refl âŸ©
      y âˆ

  II : â„¤even (x * pos (2^ (succ n)))
  II = â„¤times-even-is-even' x (pos (2^ (succ n))) (2-exponents-even n)

  Î³ : ğŸ˜
  Î³ = â„¤odd-not-even y oy (transport â„¤even I II)

â‰ˆ'-reduce  : (x y : â„¤) (n : â„•)
           â†’ (x , 1) â‰ˆ' (y , succ (succ n))
           â†’ (x , 0) â‰ˆ' (y , succ n)
â‰ˆ'-reduce  x y n e
 = â„¤-mult-right-cancellable (x * n') (y * pos (2^ 0)) (pos 2) id I
 where
  n' = pos (2^ (succ n))
  I : x * n' * pos 2 ï¼ y * pos (2^ 0) * pos 2
  I = x * n' * pos 2                  ï¼âŸ¨ i    âŸ©
      x * (n' * pos 2)                ï¼âŸ¨ ii   âŸ©
      x * pos (2^ (succ n) â„•* 2)      ï¼âŸ¨ iii  âŸ©
      x * pos (2^ (succ (succ n)))    ï¼âŸ¨ e    âŸ©
      y * pos (2^ 1)                  ï¼âŸ¨ iv   âŸ©
      y * (pos 2 * pos 1)             ï¼âŸ¨ refl âŸ©
      y * pos (2^ 0) * pos 2          âˆ

   where
    i   = â„¤*-assoc x n' (pos 2)
    ii  = ap (x *_) (pos-multiplication-equiv-to-â„• (2^ (succ n)) 2)
    iii = ap (Î» - â†’ x * pos -) (mult-commutativity (2^ (succ n)) 2)
    iv  = ap (y *_) (pos-multiplication-equiv-to-â„• 2 1) â»Â¹

â‰ˆ'-to-ï¼' : (x : â„¤) (m : â„•) (y : â„¤) (n : â„•)
          â†’ (x , m) â‰ˆ' (y , n)
          â†’ m > 0 Ã— â„¤odd x
          â†’ n > 0 Ã— â„¤odd y
          â†’ (x , m) ï¼ (y , n)
â‰ˆ'-to-ï¼' x m y 0 e (m>0 , ox) (n>0 , on)        = ğŸ˜-elim n>0
â‰ˆ'-to-ï¼' x 0 y (succ n) e (m>0 , ox) (n>0 , on) = ğŸ˜-elim m>0
â‰ˆ'-to-ï¼' x 1 y 1 e (m>0 , ox) (n>0 , on)
 = to-Ã—-ï¼ (â„¤-mult-right-cancellable x y (pos (2^ 1)) id e) refl
â‰ˆ'-to-ï¼' x 1 y (succ (succ n)) e (m>0 , ox) (n>0 , on)
 = ğŸ˜-elim i
  where
   ii : x * pos (2^ (succ n)) ï¼ y * pos (2^ 0)
   ii = â‰ˆ'-reduce x y n e
   i : ğŸ˜
   i = â‰ˆ'-lt-consequence (x , 0) (y , succ n) ii refl (â‹† , on)
â‰ˆ'-to-ï¼' x (succ (succ m)) y 1 e (m>0 , ox) (n>0 , on)
 = ğŸ˜-elim i
  where
   ii : (y , 0) â‰ˆ' (x , succ m)
   ii = â‰ˆ'-reduce y x m (e â»Â¹)
   i : ğŸ˜
   i = â‰ˆ'-lt-consequence (y , 0) (x , succ m) ii refl (â‹† , ox)
â‰ˆ'-to-ï¼' x  (succ (succ m)) y  (succ (succ n)) e (m>0 , ox) (n>0 , on)
 = III (from-Ã—-ï¼' (â‰ˆ'-to-ï¼' x (succ m) y (succ n) II (â‹† , ox) (â‹† , on)))
  where
   n' = pos (2^ (succ n))
   m' = pos (2^ (succ m))
   I : x * n' * pos 2 ï¼ y * m' * pos 2
   I = x * n' * pos 2               ï¼âŸ¨ i   âŸ©
       x * (n' * pos 2)             ï¼âŸ¨ ii  âŸ©
       x * pos (2^ (succ n) â„•* 2)   ï¼âŸ¨ iii âŸ©
       x * pos (2^ (succ (succ n))) ï¼âŸ¨ e   âŸ©
       y * pos (2^ (succ (succ m))) ï¼âŸ¨ iv  âŸ©
       y * pos (2^ (succ m) â„•* 2)   ï¼âŸ¨ v   âŸ©
       y * (m' * pos 2)             ï¼âŸ¨ vi  âŸ©
       y * m' * pos 2               âˆ
    where
     i   = â„¤*-assoc x (n') (pos 2)
     ii  = ap (x *_) (pos-multiplication-equiv-to-â„• (2^ (succ n)) 2)
     iii = ap (Î» - â†’ x * pos -) (mult-commutativity (2^ (succ n)) 2)
     iv  = ap (Î» - â†’ y * pos -) (mult-commutativity 2 (2^ (succ m)))
     v   = ap (y *_) (pos-multiplication-equiv-to-â„• (2^ (succ m)) 2 â»Â¹)
     vi  = â„¤*-assoc y m' (pos 2) â»Â¹

   II : x * n' ï¼ y * m'
   II = â„¤-mult-right-cancellable (x * n') (y * m') (pos 2) id I

   III : (x ï¼ y) Ã— (succ m ï¼ succ n) â†’ x , succ (succ m) ï¼ y , succ (succ n)
   III (xï¼y , mï¼n) = to-Ã—-ï¼ xï¼y (ap succ mï¼n)

â‰ˆ'-to-ï¼'' : ((x , m) (y , n) : â„¤ Ã— â„•)
           â†’ (x , m) â‰ˆ' (y , n) â†’ m > 0 Ã— â„¤odd x
           â†’ n > 0 Ã— â„¤odd y
           â†’ (x , m) ï¼ (y , n)
â‰ˆ'-to-ï¼'' (x , m) (y , n) e p q = â‰ˆ'-to-ï¼' x m y n e p q

â‰ˆ-to-ï¼-lemma : ((x , m) (y , n) : â„¤ Ã— â„•)
              â†’ (x , m) â‰ˆ' (y , n)
              â†’ is-â„¤[1/2] x m
              â†’ is-â„¤[1/2] y n
              â†’ (x , m) ï¼ (y , n)
â‰ˆ-to-ï¼-lemma x y e (inl p) (inl q) = â‰ˆ'-to-ï¼-0 x y e p q
â‰ˆ-to-ï¼-lemma x y e (inl p) (inr q) = ğŸ˜-elim (â‰ˆ'-lt-consequence x y e p q)
â‰ˆ-to-ï¼-lemma x y e (inr p) (inl q) = ğŸ˜-elim (â‰ˆ'-lt-consequence y x (e â»Â¹) q p)
â‰ˆ-to-ï¼-lemma x y e (inr p) (inr q) = â‰ˆ'-to-ï¼'' x y e p q

â‰ˆ-to-ï¼ : (x y : â„¤[1/2]) â†’ x â‰ˆ y â†’ x ï¼ y
â‰ˆ-to-ï¼ ((x , n) , p) ((y , m) , q) eq = to-subtype-ï¼ I II
 where
  I : ((x , n) : â„¤ Ã— â„•) â†’ is-prop (is-â„¤[1/2] x n)
  I (x , n) = is-â„¤[1/2]-is-prop x n

  II : x , n ï¼ y , m
  II = â‰ˆ-to-ï¼-lemma (x , n) (y , m) eq p q

ï¼-to-â‰ˆ : (x y : â„¤[1/2]) â†’ x ï¼ y â†’ x â‰ˆ y
ï¼-to-â‰ˆ ((x , a) , Î±) ((y , b) , Î²) e = Î³
 where
  Î³â‚ : x ï¼ y
  Î³â‚ = ap (prâ‚ âˆ˜ prâ‚) e
  Î³â‚‚ : b ï¼ a
  Î³â‚‚ = ap (prâ‚‚ âˆ˜ prâ‚) (e â»Â¹)
  Î³ : ((x , a) , Î±) â‰ˆ ((y , b) , Î²)
  Î³ = x * pos (2^ b) ï¼âŸ¨ ap (_* pos (2^ b)) Î³â‚ âŸ©
      y * pos (2^ b) ï¼âŸ¨ ap (Î» - â†’ y * pos (2^ -)) Î³â‚‚ âŸ©
      y * pos (2^ a) âˆ

â„¤[1/2]-to-normalise-pos : ((p , e) : â„¤[1/2]) â†’ (p , e) ï¼ normalise-pos p
â„¤[1/2]-to-normalise-pos ((x , 0) , inl nï¼0)
 = to-subtype-ï¼ (Î» (x , n) â†’ is-â„¤[1/2]-is-prop x n) refl
â„¤[1/2]-to-normalise-pos ((x , (succ n)) , inl nï¼0)
 = ğŸ˜-elim (positive-not-zero n nï¼0)
â„¤[1/2]-to-normalise-pos ((x , 0) , inr (0<0 , oz))
 = ğŸ˜-elim (not-less-than-itself 0 0<0)
â„¤[1/2]-to-normalise-pos ((x , succ n) , inr (0<n , oz)) = ap f e
 where
  e : inr oz ï¼ â„¤even-or-odd x
  e = â„¤even-or-odd-is-prop x (inr oz) (â„¤even-or-odd x)

  f : â„¤even x âˆ” â„¤odd x â†’ â„¤[1/2]
  f = dep-cases case-even case-odd
   where
    case-even : â„¤even x â†’ â„¤[1/2]
    case-even ez = normalise-pos-lemma x/2 n
     where
      x/2 = prâ‚ (â„¤even-is-multiple-of-two x ez)
    case-odd : â„¤odd x â†’ â„¤[1/2]
    case-odd oz = (x , succ n) , inr (â‹† , oz)

â„¤[1/2]-from-normalise-pos : (z : â„¤) (n : â„•)
                          â†’ Î£ q ê‰ â„¤[1/2] , q ï¼ normalise-pos (z , n)
â„¤[1/2]-from-normalise-pos z n = (normalise-pos (z , n)) , refl

â‰ˆ'-normalise-pos : (p : â„¤ Ã— â„•) â†’ p â‰ˆ' from-â„¤[1/2] (normalise-pos p)
â‰ˆ'-normalise-pos (p , a) = Î³ (normalise-pos-info (p , a))
 where
  p' : â„¤
  p' = dnum (normalise-pos (p , a))

  a' : â„•
  a' = dden (normalise-pos (p , a))

  Î³ : Î£ k ê‰ â„• , (p ï¼ p' * pos (2^ k))
              Ã— (a ï¼ a' + k)
    â†’ (p , a) â‰ˆ' (p' , a')
  Î³ (k , eâ‚ , eâ‚‚) = p * pos (2^ a')                 ï¼âŸ¨ i   âŸ©
                    p' * pos (2^ k) * pos (2^ a')   ï¼âŸ¨ ii  âŸ©
                    p' * (pos (2^ k) * pos (2^ a')) ï¼âŸ¨ iii âŸ©
                    p' * pos (2^ k â„•* 2^ a')        ï¼âŸ¨ iv  âŸ©
                    p' * pos (2^ (k + a'))          ï¼âŸ¨ v   âŸ©
                    p' * pos (2^ (a' + k))          ï¼âŸ¨ vi  âŸ©
                    p' * pos (2^ a) âˆ
   where
    i   = ap (_* pos (2^ a')) eâ‚
    ii  = â„¤*-assoc p' (pos (2^ k)) (pos (2^ a'))
    iii = ap (p' *_) (pos-multiplication-equiv-to-â„• (2^ k) (2^ a'))
    iv  = ap (Î» - â†’ p' * pos -) (prod-of-powers 2 k a')
    v   = ap (Î» - â†’ p' * pos (2^ -)) (addition-commutativity k a')
    vi  = ap (Î» - â†’ p' * pos (2^ -)) (eâ‚‚ â»Â¹)

â‰ˆ-normalise-pos : ((z , Î±) : â„¤[1/2]) â†’ (z , Î±) â‰ˆ normalise-pos z
â‰ˆ-normalise-pos (z , Î±)
 = ï¼-to-â‰ˆ (z , Î±) (normalise-pos z) (â„¤[1/2]-to-normalise-pos (z , Î±))

â‰ˆ-ap : (f : â„¤[1/2] â†’ â„¤[1/2]) (x y : â„¤[1/2]) â†’ x â‰ˆ y â†’ f x â‰ˆ f y
â‰ˆ-ap f x y e = ï¼-to-â‰ˆ (f x) (f y) (ap f (â‰ˆ-to-ï¼ x y e))

â‰ˆ-transport : (A : â„¤[1/2] â†’ ğ“¤ Ì‡ ){x y : â„¤[1/2]} â†’ x â‰ˆ y â†’ A x â†’ A y
â‰ˆ-transport A {x} {y} e = transport A (â‰ˆ-to-ï¼ x y e)

â‰ˆ'-to-ï¼ : (p q : â„¤ Ã— â„•) â†’ p â‰ˆ' q â†’ normalise-pos p ï¼ normalise-pos q
â‰ˆ'-to-ï¼ p q e = â‰ˆ-to-ï¼ (normalise-pos p) (normalise-pos q) Î³
 where
  I : from-â„¤[1/2] (normalise-pos p) â‰ˆ' p
  I = (â‰ˆ'-normalise-pos p) â»Â¹

  II : q â‰ˆ' from-â„¤[1/2] (normalise-pos q)
  II = â‰ˆ'-normalise-pos q

  III : from-â„¤[1/2] (normalise-pos p) â‰ˆ' q
  III = â‰ˆ'-trans (from-â„¤[1/2] (normalise-pos p)) p q I e

  Î³ : from-â„¤[1/2] (normalise-pos p) â‰ˆ' from-â„¤[1/2] (normalise-pos q)
  Î³ = â‰ˆ'-trans
      (from-â„¤[1/2] (normalise-pos p))
      q
      (from-â„¤[1/2] (normalise-pos q))
      III II

â„¤[1/2]-numerator-zero-is-zero' : (a : â„•) â†’ normalise-pos (pos 0 , a) ï¼ 0â„¤[1/2]
â„¤[1/2]-numerator-zero-is-zero' 0        = refl
â„¤[1/2]-numerator-zero-is-zero' (succ a) = I â»Â¹ âˆ™ IH
 where
  IH : normalise-pos (pos 0 , a) ï¼ 0â„¤[1/2]
  IH = â„¤[1/2]-numerator-zero-is-zero' a

  I : normalise-pos (pos 0 , a) ï¼ normalise-pos (pos 0 , succ a)
  I = normalise-pos-even-prev (pos 0) a â‹† (pos 0 , refl)

â„¤[1/2]-numerator-zero-is-zero : ((x , a) : â„¤ Ã— â„•)
                              â†’ x ï¼ pos 0
                              â†’ normalise-pos (x , a) ï¼ 0â„¤[1/2]
â„¤[1/2]-numerator-zero-is-zero (pos 0 , a) e = â„¤[1/2]-numerator-zero-is-zero' a
â„¤[1/2]-numerator-zero-is-zero (pos (succ x) , a) e
 = ğŸ˜-elim (pos-succ-not-zero x e)
â„¤[1/2]-numerator-zero-is-zero (negsucc x , a) e = ğŸ˜-elim (negsucc-not-pos e)

\end{code}

The following proofs relate dyadic rationals to rationals.

\begin{code}

â„¤[1/2]-lt-lemma : (x : â„¤) (n : â„•)
                â†’ â„¤odd x
                â†’ is-in-lowest-terms (x , pred (2^ (succ n)))
â„¤[1/2]-lt-lemma x n ox = coprime-to-coprime' _ _ Î³â‚„
 where
  n' = 2^ (succ n)

  Î³â‚ : 1 âˆ£ abs x
  Î³â‚ = 1-divides-all (abs x)

  Î³â‚‚ : 1 âˆ£ succ (pred n')
  Î³â‚‚ = 1-divides-all (succ (pred n'))

  Î³â‚ƒ : (d : â„•) â†’ is-common-divisor d (abs x) (succ (pred n')) â†’ d âˆ£ 1
  Î³â‚ƒ d icd-d = III II
   where
    i : succ (pred n') ï¼ n'
    i = succ-pred' n' (exponents-not-zero (succ n))

    II : is-common-divisor d (abs x) n'
    II = transport (Î» - â†’ is-common-divisor d (abs x) -) i icd-d

    III : is-common-divisor d (abs x) n' â†’ d âˆ£ 1
    III (d|x , d|n') = odd-power-of-two-coprime d (abs x) (succ n) ox d|x d|n'

  Î³â‚„ : is-in-lowest-terms' (x , pred (2^ (succ n)))
  Î³â‚„ = (Î³â‚ , Î³â‚‚) , Î³â‚ƒ

â„¤[1/2]-to-â„š : â„¤[1/2] â†’ â„š
â„¤[1/2]-to-â„š ((x , n)      , inl nï¼0)       = (x , 0) , (denom-zero-lt x)
â„¤[1/2]-to-â„š ((x , 0)      , inr (0<n , ox)) = ğŸ˜-elim 0<n
â„¤[1/2]-to-â„š ((x , succ n) , inr (0<n , ox)) = (x , pred (2^ (succ n))) , I
 where
  I : is-in-lowest-terms (x , pred (2^ (succ n)))
  I = â„¤[1/2]-lt-lemma x n ox

\end{code}

Boilerplate transitivity proofs.

\begin{code}

â‰ˆ-transâ‚‚ : (x y z a : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ z
                              â†’ z â‰ˆ a
                              â†’ x â‰ˆ a
â‰ˆ-transâ‚‚ x y z a p q r = â‰ˆ-trans x y a p (â‰ˆ-trans y z a q r)

â‰ˆ-transâ‚ƒ : (x y z a b : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ z
                                â†’ z â‰ˆ a â†’ a â‰ˆ b
                                â†’ x â‰ˆ b
â‰ˆ-transâ‚ƒ x y z a b p q r s = â‰ˆ-transâ‚‚ x y z b p q (â‰ˆ-trans z a b r s)

â‰ˆ-transâ‚„ : (x y z a b c : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ z
                                  â†’ z â‰ˆ a â†’ a â‰ˆ b
                                  â†’ b â‰ˆ c
                                  â†’ x â‰ˆ c
â‰ˆ-transâ‚„ x y z a b c p q r s t = â‰ˆ-transâ‚ƒ x y z a c p q r (â‰ˆ-trans a b c s t)

â‰ˆ-transâ‚… : (x y z a b c d : â„¤[1/2]) â†’ x â‰ˆ y â†’ y â‰ˆ z
                                    â†’ z â‰ˆ a â†’ a â‰ˆ b
                                    â†’ b â‰ˆ c â†’ c â‰ˆ d
                                    â†’ x â‰ˆ d
â‰ˆ-transâ‚… x y z a b c d p q r s t u =
 â‰ˆ-transâ‚„ x y z a b d p q r s (â‰ˆ-trans b c d t u)

\end{code}
