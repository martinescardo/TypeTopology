Martin Escardo, 03 February 2021.

Symmetric, reflexive, transitive closure of a relation. Also of a
relation with rank.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT

module SRTclosure
       (ğ“¤ : Universe)
       (X : ğ“¤ Ì‡ )
       (A : X â†’ X â†’ ğ“¤ Ì‡ )
       where

open import UF-Subsingletons
open import UF-Base
open import UF-Retracts
open import UF-Equiv
open import UF-UniverseEmbedding
open import UF-UA-FunExt
open import UF-FunExt
open import UF-PropTrunc

B : X â†’ X â†’ ğ“¤ Ì‡
B x y = A x y + A y x

B-symmetric : (x y : X) â†’ B x y â†’ B y x
B-symmetric x y (inl a) = inr a
B-symmetric x y (inr a) = inl a

A-included-in-B : (x y : X) â†’ A x y â†’ B x y
A-included-in-B x y = inl

B-induction : (R : X â†’ X â†’ ğ“¥ Ì‡ )
            â†’ Symmetric R
            â†’ ((x y : X) â†’ A x y â†’ R x y)
            â†’ ((x y : X) â†’ B x y â†’ R x y)
B-induction R s A-included-in-R x y (inl a) = A-included-in-R x y a
B-induction R s A-included-in-R x y (inr a) = s y x (A-included-in-R y x a)

C : â„• â†’ X â†’ X â†’ ğ“¤ Ì‡
C zero     x y = x â‰¡ y
C (succ n) x y = Î£ z ê‰ X , B x z Ã— C n z y

_âˆ”_ : â„• â†’ â„• â†’ â„•
zero   âˆ” n = n
succ m âˆ” n = succ (m âˆ” n)

C-reflexive : (x : X) â†’ C 0 x x
C-reflexive x = refl

CB-transitive : (n : â„•) (x y z : X) â†’ C n x y â†’ B y z â†’ C (succ n) x z
CB-transitive zero     x x z refl        b  = z , b , refl
CB-transitive (succ n) x y z (t , b , c) b' = t , b , CB-transitive n t y z c b'

C-symmetric : (m : â„•) (x y : X) â†’ C m x y â†’ C m y x
C-symmetric zero     x x refl        = refl
C-symmetric (succ m) x y (z , b , c) = Î³
  where
   c' : C m y z
   c' = C-symmetric m z y c

   Î³ : C (succ m) y x
   Î³ = CB-transitive m y z x c' (B-symmetric x z b)

C-transitive : (m n : â„•) (x y z : X) â†’ C m x y â†’ C n y z â†’ C (m âˆ” n) x z
C-transitive zero     n x x z refl        c' = c'
C-transitive (succ m) n x y z (t , b , c) c' = t , b , C-transitive m n t y z c c'

D : X â†’ X â†’ ğ“¤ Ì‡
D x y = Î£ n ê‰ â„• , C n x y

D-reflexive : Reflexive D
D-reflexive x = 0 , refl

D-symmetric : Symmetric D
D-symmetric x y (m , c) = m , C-symmetric m x y c

D-transitive : Transitive D
D-transitive x y z (m , c) (m' , c') = (m âˆ” m') , C-transitive m m' x y z c c'

B-included-in-D : (x y : X) â†’ B x y â†’ D x y
B-included-in-D x y b = 1 , y , b , refl

A-included-in-D : (x y : X) â†’ A x y â†’ D x y
A-included-in-D x y a = B-included-in-D x y (A-included-in-B x y a)

D-induction : (R : X â†’ X â†’ ğ“¥ Ì‡)
            â†’ Reflexive R
            â†’ Symmetric R
            â†’ Transitive R
            â†’ ((x y : X) â†’ A x y â†’ R x y)
            â†’ ((x y : X) â†’ D x y â†’ R x y)
D-induction R r s t A-included-in-R = D-included-in-R
 where
  D-included-in-R : (x y : X) â†’ D x y â†’ R x y
  D-included-in-R x x (zero , refl) = r x
  D-included-in-R x y (succ n , z , b , c) = t x z y (B-induction R s A-included-in-R x z b)
                                                     (D-included-in-R z y (n , c))

module _ (pt : propositional-truncations-exist)
         (A-is-prop-valued : (x y : X) â†’ is-prop (A x y))
       where

 open PropositionalTruncation pt

 E : X â†’ X â†’ ğ“¤ Ì‡
 E x y = âˆ¥ D x y âˆ¥

 E-is-prop-valued : (x y : X) â†’ is-prop (E x y)
 E-is-prop-valued x y = âˆ¥âˆ¥-is-prop

 E-reflexive : Reflexive E
 E-reflexive x = âˆ£ D-reflexive x âˆ£

 E-symmetric : Symmetric E
 E-symmetric x y = âˆ¥âˆ¥-functor (D-symmetric x y)

 E-transitive : Transitive E
 E-transitive x y z = âˆ¥âˆ¥-functorâ‚‚ (D-transitive x y z)

 A-included-in-E : (x y : X) â†’ A x y â†’ E x y
 A-included-in-E x y a = âˆ¥âˆ¥-functor (A-included-in-D x y) âˆ£ a âˆ£

 E-induction : (R : X â†’ X â†’ ğ“¥ Ì‡)
             â†’ Reflexive R
             â†’ Symmetric R
             â†’ Transitive R
             â†’ ((x y : X) â†’ is-prop (R x y))
             â†’ ((x y : X) â†’ A x y â†’ R x y)
             â†’ ((x y : X) â†’ E x y â†’ R x y)
 E-induction R r s t R-is-prop-valued A-included-in-R x y =
  âˆ¥âˆ¥-rec (R-is-prop-valued x y) (D-induction R r s t A-included-in-R x y)
