---
title:        Subobjects in Synthetic Topology
author:       Martin Trucchi
date-started: 2024-05-28
---

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.Base
open import UF.FunExt
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.SubtypeClassifier
open import SyntheticTopology.SierpinskiObject

module SyntheticTopology.SubObjects
        (ğ“¤ ğ“¥ : Universe)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (ğ•Š : Generalized-Sierpinski-Object fe pe pt ğ“¤ ğ“¥)
        where

open import SyntheticTopology.Compactness ğ“¤ ğ“¥ fe pe pt ğ•Š
open import SyntheticTopology.Discreteness ğ“¤ ğ“¥ fe pe pt ğ•Š
open import SyntheticTopology.Overtness ğ“¤ ğ“¥ fe pe pt ğ•Š
open import SyntheticTopology.SetCombinators ğ“¤ fe pe pt
open import SyntheticTopology.SierpinskiAxioms ğ“¤ ğ“¥ fe pe pt ğ•Š
open import UF.ImageAndSurjection pt
open import UF.Logic

open AllCombinators pt fe
open PropositionalTruncation pt hiding (_âˆ¨_)
open Sierpinski-notations fe pe pt ğ•Š

\end{code}

We define notions involving sub-objects (sub-overtness, sub-compactness...)
defined in [2].
We also prove some lemmas that are in [1] and [2].

Because of predicativity, we have to use definitions 2. of 7.2 and 8.2
for subcompactness and subovertness in [2].

\begin{code}

module subdefinitions (ğ’³ : hSet ğ“¤) where
 private
  X = underlying-set ğ’³
  set-X = prâ‚‚ ğ’³

 is-subcompact : (U : ğ“Ÿ X) â†’ Î© (ğ“¤ âº âŠ” ğ“¥)
 is-subcompact U =
  â±¯ (P , open-P) ê‰ ğ“ ğ’³
   , is-open-proposition (â±¯ x ê‰ X , (x âˆˆâ‚š U) â‡’ x âˆˆâ‚š P)

 is-subcompact' : (U : ğ“Ÿ X) â†’ Î© (ğ“¤ âº âŠ” ğ“¥)
 is-subcompact' U =
  â±¯ (P , open-P) ê‰ ğ“ ğ’³
   , is-open-proposition (â±¯ (x , Ux) ê‰ (ğ•‹ U) , x âˆˆâ‚š P)

 is-subovert : (U : ğ“Ÿ X) â†’ Î© (ğ“¤ âº âŠ” ğ“¥)
 is-subovert U =
  â±¯ (P , open-P) ê‰ ğ“ ğ’³ , is-open-proposition (Æâ‚š (x , Ux) ê‰ (ğ•‹ U) , x âˆˆâ‚š P)

\end{code}

First, we can prove that the two notions of subcompactness are equivalent.

\begin{code}

 sub-gives-sub' : (U : ğ“Ÿ X) â†’ is-subcompact U holds â†’ is-subcompact' U holds
 sub-gives-sub' U sub-U (P , open-P) =
  â‡”-open (â±¯ x ê‰ X , x âˆˆâ‚š U â‡’ x âˆˆâ‚š P) (â±¯ (x , Ux) ê‰ (ğ•‹ U) , x âˆˆâ‚š P)
          ((Î» hyp (x , Ux) â†’ hyp x Ux) , Î» hyp x Ux â†’ hyp (x , Ux))
          (sub-U ((Î» x â†’ x âˆˆâ‚š P) , open-P))


 sub'-gives-sub : (U : ğ“Ÿ X) â†’ is-subcompact' U holds â†’ is-subcompact U holds
 sub'-gives-sub U sub'-U (P , open-P) =
  â‡”-open (â±¯ (x , Ux) ê‰ (ğ•‹ U) , x âˆˆâ‚š P) (â±¯ x ê‰ X , x âˆˆâ‚š U â‡’ x âˆˆâ‚š P)
          ((Î» hyp x Ux â†’ hyp (x , Ux)) , (Î» hyp (x , Ux) â†’ hyp x Ux))
          (sub'-U ((Î» x â†’ x âˆˆâ‚š P) , open-P))

\end{code}

Then we prove some lemmas.

\begin{code}

 subovert-of-discrete-is-open : (U : ğ“Ÿ X)
                              â†’ is-subovert U holds
                              â†’ is-discrete ğ’³ holds
                              â†’ is-intrinsically-open ğ’³ U holds

 subovert-of-discrete-is-open U subovert-U discrete-X x =
  â‡”-open (Æâ‚š (y , Uy) ê‰ (ğ•‹ U) , (x ï¼â‚š y)) (x âˆˆâ‚š U) (pâ‚ , pâ‚‚) â€ 
   where
    open Equality set-X

    pâ‚ : ((Æâ‚š (y , Uy) ê‰ (ğ•‹ U) , (x ï¼â‚š y)) â‡’ x âˆˆâ‚š U) holds
    pâ‚ ex-y = âˆ¥âˆ¥-rec (holds-is-prop (x âˆˆâ‚š U))
                     (Î» ((y , Uy) , y-eq-x) â†’ transport (_holds âˆ˜ U)
                                                        (y-eq-x â»Â¹)
                                                        Uy)
                     ex-y

    pâ‚‚ : (x âˆˆâ‚š U â‡’ Æâ‚š (y , Uy) ê‰ (ğ•‹ U) , (x ï¼â‚š y)) holds
    pâ‚‚ Ux = âˆ£ (x , Ux) , refl âˆ£

    â€  : is-open-proposition (Æâ‚š (y , Uy) ê‰ (ğ•‹ U) , (x ï¼â‚š y)) holds
    â€  = subovert-U ((Î» y â†’ x ï¼â‚š y) , Î» y â†’ discrete-X (x , y))


 subovert-inter-open-subovert : closed-under-binary-meets holds
                              â†’ (â±¯ A ê‰ (ğ“Ÿ X) ,
                                  â±¯ (U , _) ê‰ ğ“ ğ’³ ,
                                   is-subovert A
                                    â‡’ is-subovert (A âˆ© U)) holds

 subovert-inter-open-subovert cl-âˆ§ A (U , open-U) subovert-A (V , open-V) =
  â‡”-open T-A
         T-Aâˆ©U
         (pâ‚ , pâ‚‚)
         (subovert-A (U âˆ© V , Î» x â†’ cl-âˆ§ (U x) (V x) ((open-U x) , (open-V x))))
   where
    T-A : Î© ğ“¤
    T-A = Æâ‚š (x , Ax) ê‰ (ğ•‹ A) , (x âˆˆâ‚š (U âˆ© V))

    T-Aâˆ©U : Î© ğ“¤
    T-Aâˆ©U = Æâ‚š (x , A-U-x) ê‰ (ğ•‹ (A âˆ© U)) , (x âˆˆâ‚š V)

    pâ‚ : (T-A â‡’ T-Aâˆ©U) holds
    pâ‚ ex-U-V = âˆ¥âˆ¥-rec (holds-is-prop T-Aâˆ©U)
                       (Î» ((x , Ax) , Ux , Vx) â†’ âˆ£ (x , Ax , Ux) , Vx  âˆ£)
                       ex-U-V

    pâ‚‚ : (T-Aâˆ©U â‡’ T-A) holds
    pâ‚‚ ex-V = âˆ¥âˆ¥-rec (holds-is-prop T-A)
                     (Î» ((x , Ax , Ux) , Vx) â†’ âˆ£ (x , Ax) , Ux , Vx  âˆ£)
                     ex-V

 open-subset-of-overt-is-subovert : closed-under-binary-meets holds
                                  â†’ (â±¯ (U , _) ê‰ ğ“ ğ’³ ,
                                     is-overt ğ’³ â‡’ is-subovert U) holds

 open-subset-of-overt-is-subovert cl-âˆ§ (U , open-U) overt-X (V , open-V) =
  â‡”-open (Æâ‚š x ê‰ X , x âˆˆâ‚š (U âˆ© V))
         (Æâ‚š (x , Ux) ê‰ (ğ•‹ U) , (x âˆˆâ‚š V))
         ((Î» ex-Uâˆ©V â†’ âˆ¥âˆ¥-rec (holds-is-prop (Æâ‚š (x , Ux) ê‰ (ğ•‹ U) , (x âˆˆâ‚š V)))
                             (Î» (x , Ux , Vx) â†’ âˆ£ (x , Ux) , Vx âˆ£)
                             ex-Uâˆ©V) ,
           (Î» ex-V â†’ âˆ¥âˆ¥-rec (holds-is-prop (Æâ‚š x ê‰ X , (x âˆˆâ‚š (U âˆ© V))))
                             (Î» ((x , Ux) , Vx) â†’ âˆ£ x , Ux , Vx âˆ£)
                             ex-V))
         (overt-X ((U âˆ© V) , (Î» x â†’ cl-âˆ§ (x âˆˆâ‚š U)
                                         (x âˆˆâ‚š V)
                                         ((open-U x) , (open-V x)))))

\end{code}

We have some lemmas that states the consistency of "sub" definitions
related to "plain" ones.

\begin{code}

 compact-iff-subcompact-in-self
  : ((is-compact ğ’³ â‡” (is-subcompact full))) holds

 compact-iff-subcompact-in-self =
  compact-gives-subcompact , subcompact-gives-compact

  where
    compact-gives-subcompact :
     (is-compact ğ’³ â‡’ is-subcompact full) holds
    compact-gives-subcompact compact-X (U , open-U) =
     â‡”-open (â±¯ x ê‰ X , x âˆˆâ‚š U)
            (â±¯ x ê‰ X , âŠ¤ â‡’ U x)
            ((Î» hyp x _ â†’ hyp x) , (Î» hyp x â†’ hyp x âŠ¤-holds))
            (compact-X (U , open-U))

    subcompact-gives-compact :
     ( is-subcompact full â‡’ is-compact ğ’³) holds
    subcompact-gives-compact = Î» subcompact-X (U , open-U) â†’
     â‡”-open (â±¯ x ê‰ X , âŠ¤ â‡’ x âˆˆâ‚š U)
            (â±¯ x ê‰ X , x âˆˆâ‚š U)
            ((Î» hyp x â†’ hyp x âŠ¤-holds) , (Î» hyp x _ â†’ hyp x))
            (subcompact-X ((Î» z â†’ z âˆˆâ‚š U) , open-U))


 overt-iff-subovert-in-self
  : ((is-overt ğ’³ â‡” (is-subovert full))) holds

 overt-iff-subovert-in-self =
  overt-gives-subovert , subovert-gives-overt
   where
    pâ‚ : (U : ğ“Ÿ X)
       â†’ ((Æâ‚š x ê‰ X , x âˆˆâ‚š U) â‡’ (Æâ‚š (x , true) ê‰ (ğ•‹ full) , x âˆˆâ‚š U)) holds
    pâ‚ U ex-U = âˆ¥âˆ¥-rec (holds-is-prop (Æâ‚š (x , true) ê‰ (ğ•‹ full) , x âˆˆâ‚š U))
                       (Î» (x , Ux) â†’ âˆ£ (x , âŠ¤-holds) , Ux âˆ£)
                       ex-U

    pâ‚‚ : (U : ğ“Ÿ X)
       â†’ ((Æâ‚š (x , true) ê‰ (ğ•‹ full) , x âˆˆâ‚š U) â‡’ (Æâ‚š x ê‰ X , x âˆˆâ‚š U)) holds
    pâ‚‚ U ex-full = âˆ¥âˆ¥-rec (holds-is-prop (Æâ‚š x ê‰ X , x âˆˆâ‚š U))
                          (Î» ((x , true) , Ux) â†’ âˆ£ x , Ux âˆ£)
                          ex-full

    overt-gives-subovert :
     (is-overt ğ’³ â‡’ is-subovert full) holds

    overt-gives-subovert overt-X (U , open-U) =
     â‡”-open (Æâ‚š x ê‰ X , x âˆˆâ‚š U)
            (Æâ‚š (x , true) ê‰ (ğ•‹ full) , U x)
            (pâ‚ U , pâ‚‚ U)
            (overt-X (U , open-U))

    subovert-gives-overt :
     ( is-subovert full â‡’ is-overt ğ’³) holds

    subovert-gives-overt = Î» subovert-X (U , open-U) â†’
      â‡”-open (Æâ‚š (x , true) ê‰ (ğ•‹ full) , U x)
             (Æâ‚š x ê‰ X , x âˆˆâ‚š U)
             (pâ‚‚ U , pâ‚ U)
             (subovert-X (U , open-U))
\end{code}

\section{References}

- [1]: Davorin LesnÌŒik. *Synthetic Topology and Constructive Metric Spaces*.

  PhD Thesis, 2010

  https://doi.org/10.48550/arXiv.2104.10399

- [2]: MartÃ­n EscardÃ³. *Topology via higher-order intuitionistic logic*

  Unpublished notes, Pittsburgh, 2004

  https://www.cs.bham.ac.uk/~mhe/papers/pittsburgh.pdf
