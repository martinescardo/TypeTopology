---
title:        Compactness in Synthetic Topology
author:       Martin Trucchi
date-started: 2024-05-28
---

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import SyntheticTopology.SierpinskiObject
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.FunExt
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier

module SyntheticTopology.Compactness
        (ğ“¤  ğ“¥ : Universe)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (ğ•Š : Generalized-Sierpinski-Object fe pe pt ğ“¤ ğ“¥)
        where

open import UF.ImageAndSurjection pt
open import UF.Logic
open import SyntheticTopology.SetCombinators ğ“¤ fe pe pt

open AllCombinators pt fe
open PropositionalTruncation pt hiding (_âˆ¨_)
open Sierpinski-notations fe pe pt ğ•Š

\end{code}

We here start to investigate a notion of compactness defined in [1] and [2].

A type `X` is called `compact` if its universal quantification on
`intrinsically-open` predicates is an open proposition.

\begin{code}

module _ (ğ’³ : hSet ğ“¤) where
 private
  X = underlying-set ğ’³

 is-compact : Î© (ğ“¤ âº âŠ” ğ“¥)
 is-compact =
  â±¯ (P , open-P) ê‰ ğ“ ğ’³ , is-open-proposition (â±¯ x ê‰ X , x âˆˆâ‚š P)

\end{code}

The type `ğŸ™` is compact i.e. the empty product is compact, regardless of the
Sierpinski Object.

\begin{code}

ğŸ™-is-compact : is-compact ğŸ™â‚› holds
ğŸ™-is-compact (P , open-P) =
 â‡”-open (â‹† âˆˆâ‚š P) (â±¯ x ê‰ ğŸ™ , x âˆˆâ‚š P) eq (open-P â‹†)
  where
   eq : (â‹† âˆˆâ‚š P â‡” (â±¯ x ê‰ ğŸ™ , x âˆˆâ‚š P)) holds
   eq = (Î» pstar  x â†’ pstar) , (Î» f â†’ f â‹†)

\end{code}

Binary products of compact types are compact.

\begin{code}

module _ (ğ’³ ğ’´ : hSet ğ“¤) where
 private
  X = underlying-set ğ’³
  Y = underlying-set ğ’´

 Ã—-is-compact : is-compact ğ’³ holds
              â†’ is-compact ğ’´ holds
              â†’ is-compact (ğ’³ Ã—â‚› ğ’´)  holds

 Ã—-is-compact compact-X compact-Y (P , open-P) =
  â‡”-open chained-forall
         tuple-forall
         (pâ‚ , pâ‚‚)
         â€ 
   where
    tuple-forall : Î© ğ“¤
    tuple-forall = â±¯ z ê‰ (X Ã— Y) , z âˆˆâ‚š P

    chained-forall : Î© ğ“¤
    chained-forall = â±¯ x ê‰ X , (â±¯ y ê‰ Y , (x , y) âˆˆâ‚š P)

    pâ‚ : (chained-forall â‡’ tuple-forall) holds
    pâ‚ = Î» Qxy z â†’ Qxy (prâ‚ z) (prâ‚‚ z)

    pâ‚‚ : (tuple-forall â‡’ chained-forall) holds
    pâ‚‚ = Î» Qz x' y' â†’ Qz (x' , y')

    prop-y : ğ“Ÿ X
    prop-y x = â±¯ y ê‰ Y , (x , y) âˆˆâ‚š P

    prop-y-open : (x : X) â†’ is-open-proposition (prop-y x) holds
    prop-y-open x = compact-Y ((Î» y â†’ (x , y) âˆˆâ‚š P) , Î» y â†’ open-P (x , y))

    â€  : is-open-proposition chained-forall holds
    â€  = compact-X ((Î» x â†’ prop-y x) , prop-y-open)

\end{code}

Images of compact types by surjective functions are compact.

\begin{code}

 image-of-compact : (f : X â†’ Y)
                  â†’ is-surjection f
                  â†’ is-compact ğ’³ holds
                  â†’ is-compact ğ’´ holds
 image-of-compact f sf compact-X (P , open-P) =
  â‡”-open pre-image-forall image-forall (pâ‚ , pâ‚‚) â€ 
   where
    pre-image-forall : Î© ğ“¤
    pre-image-forall = â±¯ x ê‰ X , (f x) âˆˆâ‚š P

    image-forall : Î© ğ“¤
    image-forall = â±¯ y ê‰ Y , y âˆˆâ‚š P

    pâ‚ : (pre-image-forall â‡’ image-forall) holds
    pâ‚ pX y = surjection-induction f
                                   sf
                                   (_holds âˆ˜ P)
                                   (Î» y â†’ holds-is-prop (P y))
                                   pX
                                   y

    pâ‚‚ : (image-forall â‡’ pre-image-forall) holds
    pâ‚‚ pY x = pY (f x)

    â€  : is-open-proposition pre-image-forall holds
    â€  = compact-X (P âˆ˜ f , open-P âˆ˜ f)

\end{code}

This also works for any function, in which case we prove that `image f` is
compact.

We have to get out of the module defining `ğ’³` and `ğ’´` in order to apply the
previous lemma.

\begin{code}

image-of-compact' : ((X , sX) : hSet ğ“¤)
                  â†’ ((Y , sY) : hSet ğ“¤)
                  â†’ (f : X â†’ Y)
                  â†’ is-compact (X , sX) holds
                  â†’ is-compact (imageâ‚› (X , sX) (Y , sY) f) holds

image-of-compact' (X , sX) (Y , sY) f compact-X =
 image-of-compact (X , sX)
                  (imageâ‚› (X , sX) (Y , sY) f)
                  (corestriction f)
                  (corestrictions-are-surjections f)
                  compact-X

\end{code}

\section{References}

- [1]: Davorin LesnÌŒik. *Synthetic Topology and Constructive Metric Spaces*.

  PhD Thesis, 2010

  https://doi.org/10.48550/arXiv.2104.10399

- [2]: MartÃ­n EscardÃ³. *Topology via higher-order intuitionistic logic*

  Unpublished notes, Pittsburgh, 2004

  https://www.cs.bham.ac.uk/~mhe/papers/pittsburgh.pdf
