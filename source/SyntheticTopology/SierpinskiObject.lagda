---
title:          Definition of Sierpinski object synthetic topology
authors:        ["Ayberk Tosun", "Martin Trucchi"]
date-started:   2024-05-02
date-completed: 2024-05-31
dates-updated:  [2024-05-28, 2024-06-05 , 2024-06-07]
---

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier

module SyntheticTopology.SierpinskiObject
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist) where

open import UF.Classifiers
open import UF.Embeddings
open import UF.Equiv
open import UF.ImageAndSurjection pt
open import UF.Logic
open import UF.Subsingletons-FunExt
open import UF.Univalence
open import UF.UniverseEmbedding

open AllCombinators pt fe
open PropositionalTruncation pt hiding (_âˆ¨_)

\end{code}

What is a SierpiÅ„ski object? In MartÃ­n EscardÃ³Â´s unpublished note [2],
a SierpiÅ„ski object is defined in the setting of a topos as a subobject of the
subobject classifier. This is also given in Definition 2.4 of Davorin Lesnik's
thesis [1], who took this unpublished note as a starting point for his PhD
thesis.

The purpose of this development is to develop these notions in the context of
HoTT/UF, where we look at subtypes of the subtype classifier. Because we work
predicatively, however, the definition of the notion of SierpiÅ„ski object is not
that straightforward in our setting.

In the impredicative setting of topos theory, one works with the topos of ğ“¤-sets
over some universe ğ“¤, and the SierpiÅ„ski object in that setting would translate
to:

\begin{code}

Sierpinski-Objectâ‚€ : (ğ“¤ : Universe) â†’ ğ“¤ âº  Ì‡
Sierpinski-Objectâ‚€ ğ“¤ = Subtype' ğ“¤ (Î© ğ“¤)

\end{code}

However, many dominances that we are interested in do not fit this definition in
a predicative setting. For example, the largest possible dominance of `Î© ğ“¤` is
ğ“¤âº-set rather than a ğ“¤-set. Accordingly, we generalize the universes in the
notion of SierpiÅ„ski object as follows:

\begin{code}

Generalized-Sierpinski-Object : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥) âº  Ì‡
Generalized-Sierpinski-Object ğ“¤ ğ“¥ = Î© ğ“¤ â†’ Î© ğ“¥

\end{code}

We think that in most cases, our dominances will be of the form `Î© ğ“¤ â†’ Î© (ğ“¤ âº)`,
but because we can prove things in this general setting, we choose to work with
the generalized definition instead.

We define some notation to refer to components of a SierpiÅ„ski object.

In the module below, we assume the existence of a SierpiÅ„ski object `ğ•Š` and
define some notions _synthetically_, following the work of MartÃ­n EscardÃ³ (and
Davorin LeÅ¡nik).

\begin{code}

module Sierpinski-notations (ğ•Š : Generalized-Sierpinski-Object ğ“¤ ğ“¥) where

\end{code}

The propositions in `Î©` that fall in the subset `ğ•Š` are called _open
propositions_. We introduce suggestive terminology accordingly.

The type of open proposition is noted Î©â‚’

\begin{code}

 is-open-proposition : Î© ğ“¤ â†’ Î© ğ“¥
 is-open-proposition = ğ•Š

 Î©â‚’ : ğ“¤ âº âŠ” ğ“¥  Ì‡
 Î©â‚’ = Î£ p ê‰ Î© ğ“¤ , is-open-proposition p holds

\end{code}

Here, we only work with sets.

We defined some combinators for the sake of notational simplicity in the module
SetCombinators.

To define this and some related notions, we work in a module parameterized by an
hSet `ğ’³`. We adopt the convention of using calligraphic letters `ğ’³`, `ğ’´`, ...
for inhabitants of the type `hSet`.

\begin{code}

 module _ (ğ’³ : hSet ğ“¤) where

\end{code}

We denote by `X` the underlying set of `ğ’³`.

\begin{code}

  private
   X = underlying-set ğ’³

\end{code}

A subset of a set is said to be `intrinsically open` if it is a predicate
defined by open propositions.

\begin{code}

  is-intrinsically-open : ğ“Ÿ X â†’ Î© (ğ“¤ âŠ” ğ“¥)
  is-intrinsically-open P = â±¯ x ê‰ X , is-open-proposition (x âˆˆâ‚š P)

\end{code}

For convenience, we write down the subtype of open propositions (= subset) of a
set `X`

\begin{code}

  ğ“ : ğ“¤ âº âŠ” ğ“¥  Ì‡
  ğ“ = Î£ P ê‰ ğ“Ÿ X , is-intrinsically-open P holds

  underlying-prop : ğ“ â†’ ğ“Ÿ X
  underlying-prop = prâ‚

\end{code}

We also prove the following convenient lemma.

\begin{code}

 â‡”-open
  : (p q : Î© ğ“¤)
  â†’ ((p â‡” q) â‡’ is-open-proposition p â‡’ is-open-proposition q) holds
 â‡”-open p q = â‡”-transport pe p q (_holds âˆ˜ is-open-proposition)

\end{code}

\section{References}

- [1]: Davorin LesnÌŒik. *Synthetic Topology and Constructive Metric Spaces*.

  PhD Thesis, 2010

  https://doi.org/10.48550/arXiv.2104.10399

- [2]: MartÃ­n EscardÃ³. *Topology via higher-order intuitionistic logic*

  Unpublished notes, Pittsburgh, 2004

  https://www.cs.bham.ac.uk/~mhe/papers/pittsburgh.pdf
