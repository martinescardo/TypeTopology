Tom de Jong, May 2019.
Major additions January 2020.

We construct the exponential (pointed) dcpos (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) and (ğ““ âŸ¹áµˆá¶œáµ–áµ’âŠ¥ ğ“”) for
(pointed) dcpos ğ““ and ğ“”.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT hiding (J)
open import UF-FunExt
open import UF-PropTrunc

module DcpoExponential
        (pt : propositional-truncations-exist)
        (fe : âˆ€ {ğ“¤ ğ“¥} â†’ funext ğ“¤ ğ“¥)
        (ğ“¥ : Universe)
       where

open PropositionalTruncation pt

open import UF-Subsingletons
open import UF-Subsingletons-FunExt

open import Dcpo pt fe ğ“¥
open import DcpoMiscelanea pt fe ğ“¥

open import Poset fe

module _ (ğ““ : DCPO {ğ“¤} {ğ“£})
         (ğ“” : DCPO {ğ“¤'} {ğ“£'})
       where

 _hom-âŠ‘_ : DCPO[ ğ““ , ğ“” ] â†’ DCPO[ ğ““ , ğ“” ] â†’ ğ“¤ âŠ” ğ“£' Ì‡
 (f , _) hom-âŠ‘ (g , _) = âˆ€ d â†’ f d âŠ‘âŸ¨ ğ“” âŸ© g d

 pointwise-family : {I : ğ“¥ Ì‡} (Î± : I â†’ DCPO[ ğ““ , ğ“” ]) â†’ âŸ¨ ğ““ âŸ© â†’ I â†’ âŸ¨ ğ“” âŸ©
 pointwise-family Î± d i = underlying-function ğ““ ğ“” (Î± i) d

 pointwise-family-is-directed : {I : ğ“¥ Ì‡} (Î± : I â†’ DCPO[ ğ““ , ğ“” ])
                                (Î´ : is-directed _hom-âŠ‘_ Î±)
                                (d : âŸ¨ ğ““ âŸ©)
                              â†’ is-directed (underlying-order ğ“”)
                                 (pointwise-family Î± d)
 pointwise-family-is-directed {I} Î± Î´ d = a , b
  where
   a : âˆ¥ I âˆ¥
   a = inhabited-if-directed _hom-âŠ‘_ Î± Î´
   b : is-semidirected (underlying-order ğ“”) (pointwise-family Î± d)
   b i j = do
    (k , l , m) â† semidirected-if-directed _hom-âŠ‘_ Î± Î´ i j
    âˆ£ k , l d , m d âˆ£

 continuous-functions-sup : {I : ğ“¥ Ì‡} (Î± : I â†’ DCPO[ ğ““ , ğ“” ])
                          â†’ is-directed _hom-âŠ‘_ Î± â†’ DCPO[ ğ““ , ğ“” ]
 continuous-functions-sup {I} Î± Î´ = f , c
  where
   Îµ : (d : âŸ¨ ğ““ âŸ©) â†’ is-directed (underlying-order ğ“”) (pointwise-family Î± d)
   Îµ = pointwise-family-is-directed Î± Î´
   f : âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©
   f d = âˆ ğ“” (Îµ d)
   c : is-continuous ğ““ ğ“” f
   c J Î² Ï† = ub , lb-of-ubs
    where
     ub : (j : J) â†’ f (Î² j) âŠ‘âŸ¨ ğ“” âŸ© f (âˆ ğ““ Ï†)
     ub j = f (Î² j)         âŠ‘âŸ¨ ğ“” âŸ©[ reflexivity ğ“” (f (Î² j)) ]
            âˆ ğ“” (Îµ (Î² j))   âŠ‘âŸ¨ ğ“” âŸ©[ âˆ-is-monotone ğ“” (Îµ (Î² j)) (Îµ (âˆ ğ““ Ï†)) h ]
            âˆ ğ“” (Îµ (âˆ ğ““ Ï†)) âŠ‘âŸ¨ ğ“” âŸ©[ reflexivity ğ“” (f (âˆ ğ““ Ï†)) ]
            f (âˆ ğ““ Ï†)       âˆâŸ¨ ğ“” âŸ©
      where
       h : (i : I) â†’ [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (Î² j) âŠ‘âŸ¨ ğ“” âŸ© [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (âˆ ğ““ Ï†)
       h i = monotone-if-continuous ğ““ ğ“” (Î± i) (Î² j) (âˆ ğ““ Ï†)
              (âˆ-is-upperbound ğ““ Ï† j)
     lb-of-ubs : is-lowerbound-of-upperbounds (underlying-order ğ“”) (f (âˆ ğ““ Ï†))
                  (f âˆ˜ Î²)
     lb-of-ubs e l = f (âˆ ğ““ Ï†)       âŠ‘âŸ¨ ğ“” âŸ©[ reflexivity ğ“” (f (âˆ ğ““ Ï†)) ]
                     âˆ ğ“” (Îµ (âˆ ğ““ Ï†)) âŠ‘âŸ¨ ğ“” âŸ©[ u ]
                     e               âˆâŸ¨ ğ“” âŸ©
      where
       u = âˆ-is-lowerbound-of-upperbounds ğ“” (Îµ (âˆ ğ““ Ï†)) e v
        where
         v : (i : I) â†’ [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (âˆ ğ““ Ï†) âŠ‘âŸ¨ ğ“” âŸ© e
         v i = [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (âˆ ğ““ Ï†)             âŠ‘âŸ¨ ğ“” âŸ©[ lâ‚ ]
               âˆ ğ“” (image-is-directed' ğ““ ğ“” (Î± i) Ï†) âŠ‘âŸ¨ ğ“” âŸ©[ lâ‚‚ ]
               e                                    âˆâŸ¨ ğ“” âŸ©
          where
           lâ‚ = continuous-âˆ-âŠ‘ ğ““ ğ“” (Î± i) Ï†
           lâ‚‚ = âˆ-is-lowerbound-of-upperbounds ğ“”
                 (image-is-directed' ğ““ ğ“” (Î± i) Ï†) e w
            where
             w : (j : J) â†’ [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (Î² j) âŠ‘âŸ¨ ğ“” âŸ© e
             w j = [ ğ““ , ğ“” ]âŸ¨ Î± i âŸ© (Î² j) âŠ‘âŸ¨ ğ“” âŸ©[ âˆ-is-upperbound ğ“” (Îµ (Î² j)) i ]
                   âˆ ğ“” (Îµ (Î² j))          âŠ‘âŸ¨ ğ“” âŸ©[ reflexivity ğ“” (f (Î² j)) ]
                   f (Î² j)                âŠ‘âŸ¨ ğ“” âŸ©[ l j ]
                   e                      âˆâŸ¨ ğ“” âŸ©

infixr 20 _âŸ¹áµˆá¶œáµ–áµ’_

_âŸ¹áµˆá¶œáµ–áµ’_ : DCPO {ğ“¤} {ğ“£} â†’ DCPO {ğ“¤'} {ğ“£'}
        â†’ DCPO {(ğ“¥ âº) âŠ” ğ“¤ âŠ” ğ“£ âŠ” ğ“¤' âŠ” ğ“£'} {ğ“¤ âŠ” ğ“£'}
ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“” = DCPO[ ğ““ , ğ“” ] , _âŠ‘_ , pa , dc
 where
  _âŠ‘_ = ğ““ hom-âŠ‘ ğ“”
  pa : PosetAxioms.poset-axioms _âŠ‘_
  pa = s , p , r , t , a
   where
    open PosetAxioms _âŠ‘_
    s : is-set DCPO[ ğ““ , ğ“” ]
    s = subsets-of-sets-are-sets (âŸ¨ ğ““ âŸ© â†’ âŸ¨ ğ“” âŸ©) (is-continuous ğ““ ğ“”)
         (Î -is-set fe (Î» x â†’ sethood ğ“”))
         (Î» {f} â†’ being-continuous-is-prop ğ““ ğ“” f)
    p : (f g : DCPO[ ğ““ , ğ“” ]) â†’ is-prop (f âŠ‘ g)
    p (f , _) (g , _) = Î -is-prop fe (Î» x â†’ prop-valuedness ğ“” (f x) (g x))
    r : (f : DCPO[ ğ““ , ğ“” ]) â†’ f âŠ‘ f
    r (f , _) x = reflexivity ğ“” (f x)
    t : (f g h : DCPO[ ğ““ , ğ“” ]) â†’ f âŠ‘ g â†’ g âŠ‘ h â†’ f âŠ‘ h
    t (f , _) (g , _) (h , _) l m x = transitivity ğ“” (f x) (g x) (h x)
                                      (l x) (m x)
    a : (f g : DCPO[ ğ““ , ğ“” ]) â†’ f âŠ‘ g â†’ g âŠ‘ f â†’ f â‰¡ g
    a f g l m = to-continuous-function-â‰¡ ğ““ ğ“”
                 (Î» x â†’ antisymmetry ğ“” ([ ğ““ , ğ“” ]âŸ¨ f âŸ© x) ([ ğ““ , ğ“” ]âŸ¨ g âŸ© x)
                  (l x) (m x))
  dc : is-directed-complete _âŠ‘_
  dc I Î± Î´ = (continuous-functions-sup ğ““ ğ“” Î± Î´) , u , v
   where
    u : (i : I) â†’ Î± i âŠ‘ continuous-functions-sup ğ““ ğ“” Î± Î´
    u i d = âˆ-is-upperbound ğ“” (pointwise-family-is-directed ğ““ ğ“” Î± Î´ d) i
    v : (g : DCPO[ ğ““ , ğ“” ])
      â†’ ((i : I) â†’ Î± i âŠ‘ g)
      â†’ continuous-functions-sup ğ““ ğ“” Î± Î´ âŠ‘ g
    v (g , _) l d = âˆ-is-lowerbound-of-upperbounds ğ“”
                     (pointwise-family-is-directed ğ““ ğ“” Î± Î´ d)
                     (g d) (Î» (i : I) â†’ l i d)

infixr 20 _âŸ¹áµˆá¶œáµ–áµ’âŠ¥_

_âŸ¹áµˆá¶œáµ–áµ’âŠ¥_ : DCPOâŠ¥ {ğ“¤} {ğ“£} â†’ DCPOâŠ¥ {ğ“¤'} {ğ“£'}
         â†’ DCPOâŠ¥ {(ğ“¥ âº) âŠ” ğ“¤ âŠ” ğ“£ âŠ” ğ“¤' âŠ” ğ“£'} {ğ“¤ âŠ” ğ“£'}
ğ““ âŸ¹áµˆá¶œáµ–áµ’âŠ¥ ğ“” = (ğ““ â») âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â») , h
 where
  h : has-least (underlying-order ((ğ““ â») âŸ¹áµˆá¶œáµ–áµ’ (ğ“” â»)))
  h = ((Î» _ â†’ âŠ¥ ğ“”) ,
      constant-functions-are-continuous (ğ““ â») (ğ“” â») (âŠ¥ ğ“”)) ,
      (Î» g d â†’ âŠ¥-is-least ğ“” (underlying-function (ğ““ â») (ğ“” â») g d))

\end{code}

Now that we have constructed exponentials, we can state and prove additional
continuity results regarding composition of continuous functions.

(These results are used in constructing Scott's Dâˆ in DcpoDinfinity.lagda.)

\begin{code}

DCPO-âˆ˜-is-monotoneâ‚ : (ğ““ : DCPO {ğ“¤} {ğ“£})
                      (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                      (ğ“”' : DCPO {ğ“¦} {ğ“¦'})
                      (f : DCPO[ ğ““ , ğ“” ])
                    â†’ is-monotone (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (DCPO-âˆ˜ ğ““ ğ“” ğ“”' f)
DCPO-âˆ˜-is-monotoneâ‚ ğ““ ğ“” ğ“”' (f , cf) g h l x = l (f x)

DCPO-âˆ˜-is-monotoneâ‚‚ : (ğ““ : DCPO {ğ“¤} {ğ“£})
                      (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                      (ğ“”' : DCPO {ğ“¦} {ğ“¦'})
                      (g : DCPO[ ğ“” , ğ“”' ])
                    â†’ is-monotone (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
                       (Î» f â†’ DCPO-âˆ˜ ğ““ ğ“” ğ“”' f g)
DCPO-âˆ˜-is-monotoneâ‚‚ ğ““ ğ“” ğ“”' g (f , cf) (h , ch) l x =
 monotone-if-continuous ğ“” ğ“”' g (f x) (h x) (l x)

DCPO-âˆ˜-is-continuousâ‚ : (ğ““ : DCPO {ğ“¤} {ğ“£})
                        (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                        (ğ“”' : DCPO {ğ“¦} {ğ“¦'})
                        (f : DCPO[ ğ““ , ğ“” ])
                      â†’ is-continuous (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (DCPO-âˆ˜ ğ““ ğ“” ğ“”' f)
DCPO-âˆ˜-is-continuousâ‚ ğ““ ğ“” ğ“”' f I Î± Î´ =
 transport (Î» - â†’ is-sup (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”')) - (DCPO-âˆ˜ ğ““ ğ“” ğ“”' f âˆ˜ Î±))
  (Î³ â»Â¹) (âˆ-is-sup (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î²} Îµ)
  where
     Î² : I â†’ âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”' âŸ©
     Î² i = DCPO-âˆ˜ ğ““ ğ“” ğ“”' f (Î± i)
     Îµ : is-Directed (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') Î²
     Îµ = image-is-directed (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {DCPO-âˆ˜ ğ““ ğ“” ğ“”' f}
          (DCPO-âˆ˜-is-monotoneâ‚ ğ““ ğ“” ğ“”' f) {I} {Î±} Î´
     Î³ : DCPO-âˆ˜ ğ““ ğ“” ğ“”' f (âˆ (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î±} Î´) â‰¡ âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î²} Îµ
     Î³ = to-continuous-function-â‰¡ ğ““ ğ“”' Ïˆ
      where
       Ïˆ : (x : âŸ¨ ğ““ âŸ©)
         â†’ [ ğ“” , ğ“”' ]âŸ¨ (âˆ (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î±} Î´) âŸ© ([ ğ““ , ğ“” ]âŸ¨ f âŸ© x)
         â‰¡ âˆ ğ“”' (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x)
       Ïˆ x = [ ğ“” , ğ“”' ]âŸ¨ (âˆ (ğ“” âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î±} Î´) âŸ© ([ ğ““ , ğ“” ]âŸ¨ f âŸ© x) â‰¡âŸ¨ eâ‚ âŸ©
             âˆ ğ“”' Îµ'                                                     â‰¡âŸ¨ eâ‚‚ âŸ©
             âˆ ğ“”' (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x) âˆ
        where
         Îµ' : is-Directed ğ“”' (pointwise-family ğ“” ğ“”' Î± ([ ğ““ , ğ“” ]âŸ¨ f âŸ© x))
         Îµ' = pointwise-family-is-directed ğ“” ğ“”' Î± Î´ ([ ğ““ , ğ“” ]âŸ¨ f âŸ© x)
         eâ‚ = refl
         eâ‚‚ = âˆ-independent-of-directedness-witness ğ“”' Îµ'
               (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x)

DCPO-âˆ˜-is-continuousâ‚‚ : (ğ““ : DCPO {ğ“¤} {ğ“£})
                        (ğ“” : DCPO {ğ“¤'} {ğ“£'})
                        (ğ“”' : DCPO {ğ“¦} {ğ“¦'})
                        (g : DCPO[ ğ“” , ğ“”' ])
                      â†’ is-continuous (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”')
                         (Î» f â†’ DCPO-âˆ˜ ğ““ ğ“” ğ“”' f g)
DCPO-âˆ˜-is-continuousâ‚‚ ğ““ ğ“” ğ“”' g I Î± Î´ =
 transport
  (Î» - â†’ is-sup (underlying-order (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”')) - ((Î» f â†’ DCPO-âˆ˜ ğ““ ğ“” ğ“”' f g) âˆ˜ Î±))
  (Î³ â»Â¹) (âˆ-is-sup (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î²} Îµ)
   where
    Î² : I â†’ âŸ¨ ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”' âŸ©
    Î² i = DCPO-âˆ˜ ğ““ ğ“” ğ“”' (Î± i) g
    Îµ : is-Directed (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') Î²
    Îµ = image-is-directed (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {Î» f â†’ DCPO-âˆ˜ ğ““ ğ“” ğ“”' f g}
         (DCPO-âˆ˜-is-monotoneâ‚‚ ğ““ ğ“” ğ“”' g) {I} {Î±} Î´
    Î³ : DCPO-âˆ˜ ğ““ ğ“” ğ“”' (âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) {I} {Î±} Î´) g â‰¡ âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”') {I} {Î²} Îµ
    Î³ = to-continuous-function-â‰¡ ğ““ ğ“”' Ïˆ
     where
      Ïˆ : (x : âŸ¨ ğ““ âŸ©)
        â†’ [ ğ“” , ğ“”' ]âŸ¨ g âŸ© ([ ğ““ , ğ“” ]âŸ¨ âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) {I} {Î±} Î´ âŸ© x)
        â‰¡ âˆ ğ“”' (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x)
      Ïˆ x = [ ğ“” , ğ“”' ]âŸ¨ g âŸ© ([ ğ““ , ğ“” ]âŸ¨ âˆ (ğ““ âŸ¹áµˆá¶œáµ–áµ’ ğ“”) {I} {Î±} Î´ âŸ© x) â‰¡âŸ¨ refl âŸ©
            [ ğ“” , ğ“”' ]âŸ¨ g âŸ© (âˆ ğ“” Îµ')                                 â‰¡âŸ¨ eâ‚ âŸ©
            âˆ ğ“”' Îµ''                                                 â‰¡âŸ¨ eâ‚‚ âŸ©
            âˆ ğ“”' (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x)           âˆ
       where
        Îµ' : is-Directed ğ“” (pointwise-family ğ““ ğ“” Î± x)
        Îµ' = pointwise-family-is-directed ğ““ ğ“” Î± Î´ x
        Îµ'' : is-Directed ğ“”' ([ ğ“” , ğ“”' ]âŸ¨ g âŸ© âˆ˜ pointwise-family ğ““ ğ“” Î± x)
        Îµ'' = image-is-directed' ğ“” ğ“”' g Îµ'
        eâ‚ = continuous-âˆ-â‰¡ ğ“” ğ“”' g Îµ'
        eâ‚‚ = âˆ-independent-of-directedness-witness ğ“”' Îµ''
              (pointwise-family-is-directed ğ““ ğ“”' Î² Îµ x)

DCPO-âˆ˜â‚ƒ-is-continuousâ‚‚ : {ğ“¦â‚ ğ“£â‚ ğ“¦â‚‚ ğ“£â‚‚ ğ“¦â‚ƒ ğ“£â‚ƒ ğ“¦â‚„ ğ“£â‚„ : Universe}
                         (ğ““â‚ : DCPO {ğ“¦â‚} {ğ“£â‚}) (ğ““â‚‚ : DCPO {ğ“¦â‚‚} {ğ“£â‚‚})
                         (ğ““â‚ƒ : DCPO {ğ“¦â‚ƒ} {ğ“£â‚ƒ}) (ğ““â‚„ : DCPO {ğ“¦â‚„} {ğ“£â‚„})
                         (f : DCPO[ ğ““â‚ , ğ““â‚‚ ]) (h : DCPO[ ğ““â‚ƒ , ğ““â‚„ ])
                       â†’ is-continuous (ğ““â‚‚ âŸ¹áµˆá¶œáµ–áµ’ ğ““â‚ƒ) (ğ““â‚ âŸ¹áµˆá¶œáµ–áµ’ ğ““â‚„)
                          (Î» g â†’ DCPO-âˆ˜â‚ƒ ğ““â‚ ğ““â‚‚ ğ““â‚ƒ ğ““â‚„ f g h)
DCPO-âˆ˜â‚ƒ-is-continuousâ‚‚ ğ““â‚ ğ““â‚‚ ğ““â‚ƒ ğ““â‚„ f h =
 âˆ˜-is-continuous (ğ““â‚‚ âŸ¹áµˆá¶œáµ–áµ’ ğ““â‚ƒ) (ğ““â‚‚ âŸ¹áµˆá¶œáµ–áµ’ ğ““â‚„) (ğ““â‚ âŸ¹áµˆá¶œáµ–áµ’ ğ““â‚„)
  (Î» g â†’ DCPO-âˆ˜ ğ““â‚‚ ğ““â‚ƒ ğ““â‚„ g h) (DCPO-âˆ˜ ğ““â‚ ğ““â‚‚ ğ““â‚„ f)
  (DCPO-âˆ˜-is-continuousâ‚‚ ğ““â‚‚ ğ““â‚ƒ ğ““â‚„ h) (DCPO-âˆ˜-is-continuousâ‚ ğ““â‚ ğ““â‚‚ ğ““â‚„ f)

\end{code}
