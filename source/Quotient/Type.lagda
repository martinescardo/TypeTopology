Tom de Jong, 4 & 5 April 2022.

Quotients. Much of this material is moved from or abstracted from the
earlier 2018 module Quotient.Large by Martin Escardo.

\begin{code}

{-# OPTIONS --safe --without-K --exact-split #-}

module Quotient.Type where

open import MLTT.Spartan

open import UF.Base hiding (_â‰ˆ_)
open import UF.Equiv
open import UF.FunExt
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier
open import UF.SubtypeClassifier-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

is-prop-valued is-equiv-relation : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-prop-valued    _â‰ˆ_ = âˆ€ x y â†’ is-prop (x â‰ˆ y)
is-equiv-relation _â‰ˆ_ = is-prop-valued _â‰ˆ_
                      Ã— reflexive      _â‰ˆ_
                      Ã— symmetric      _â‰ˆ_
                      Ã— transitive     _â‰ˆ_

EqRel : {ğ“¤ ğ“¥ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
EqRel {ğ“¤} {ğ“¥} X = Î£ R ê‰ (X â†’ X â†’ ğ“¥ Ì‡ ) , is-equiv-relation R

_â‰ˆ[_]_ : {X : ğ“¤ Ì‡ } â†’ X â†’ EqRel X â†’ X â†’ ğ“¥ Ì‡
x â‰ˆ[ _â‰ˆ_ , _ ] y = x â‰ˆ y

identifies-related-points : {X : ğ“¤ Ì‡ } (â‰ˆ : EqRel {ğ“¤} {ğ“¥} X) {Y : ğ“¦ Ì‡ }
                          â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ Ì‡
identifies-related-points (_â‰ˆ_ , _) f = âˆ€ {x x'} â†’ x â‰ˆ x' â†’ f x ï¼ f x'

\end{code}

To account for the module Quotient.Large, and, at the same time, usual
(small) quotients, we introduce a parametric definion of existence of
quotients. For small quotients we take F = id, and for large quotients
we take F = _âº (see below).

\begin{code}

record general-set-quotients-exist (F : Universe â†’ Universe) : ğ“¤Ï‰ where
 field
  _/_ : {ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ ) â†’ EqRel {ğ“¤} {ğ“¥} X â†’ ğ“¤ âŠ” F ğ“¥ Ì‡
  Î·/ : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X) â†’ X â†’ X / â‰‹
  Î·/-identifies-related-points : {ğ“¤ ğ“¥ : Universe}
                                 {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X)
                               â†’ identifies-related-points â‰‹ (Î·/ â‰‹)
  /-is-set : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X) â†’ is-set (X / â‰‹)
  /-universality : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X)
                   {ğ“¦ : Universe} {Y : ğ“¦ Ì‡ }
                 â†’ is-set Y
                 â†’ (f : X â†’ Y)
                 â†’ identifies-related-points â‰‹ f
                 â†’ âˆƒ! fÌ… ê‰ (X / â‰‹ â†’ Y) , fÌ… âˆ˜ Î·/ â‰‹ âˆ¼ f

\end{code}

Added 22 August 2022.
The induction principle follows from the universal property.

\begin{code}

 /-induction : {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X)
               {P : X / â‰‹ â†’ ğ“¦ Ì‡ }
             â†’ ((x' : X / â‰‹) â†’ is-prop (P x'))
             â†’ ((x : X) â†’ P (Î·/ â‰‹ x)) â†’ (y : X / â‰‹) â†’ P y
 /-induction {X = X} â‰‹ {P} P-is-prop-valued Ï y =
  transport P (happly fÌ…-section-of-prâ‚ y) (prâ‚‚ (fÌ… y))
   where
    f : X â†’ Î£ P
    f x = (Î·/ â‰‹ x , Ï x)
    f-identifies-related-points : identifies-related-points â‰‹ f
    f-identifies-related-points r =
     to-subtype-ï¼ P-is-prop-valued (Î·/-identifies-related-points â‰‹ r)
    Î£P-is-set : is-set (Î£ P)
    Î£P-is-set = subsets-of-sets-are-sets (X / â‰‹) P (/-is-set â‰‹)
                                         (Î» {x'} â†’ P-is-prop-valued x')
    u : âˆƒ! fÌ… ê‰ (X / â‰‹ â†’ Î£ P) , fÌ… âˆ˜ Î·/ â‰‹ âˆ¼ f
    u = /-universality â‰‹ Î£P-is-set f f-identifies-related-points
    fÌ… : X / â‰‹ â†’ Î£ P
    fÌ… = âˆƒ!-witness u
    fÌ…-after-Î·-is-f : fÌ… âˆ˜ Î·/ â‰‹ âˆ¼ f
    fÌ…-after-Î·-is-f = âˆƒ!-is-witness u
    fÌ…-section-of-prâ‚ : prâ‚ âˆ˜ fÌ… ï¼ id
    fÌ…-section-of-prâ‚ = ap prâ‚ (singletons-are-props c (prâ‚ âˆ˜ fÌ… , h)
                                                      (id , Î» x â†’ refl))
     where
      c : âˆƒ! g ê‰ (X / â‰‹ â†’ X / â‰‹) , g âˆ˜ Î·/ â‰‹ âˆ¼ Î·/ â‰‹
      c = /-universality â‰‹ (/-is-set â‰‹) (Î·/ â‰‹) (Î·/-identifies-related-points â‰‹)
      h : prâ‚ âˆ˜ fÌ… âˆ˜ Î·/ â‰‹ âˆ¼ Î·/ â‰‹
      h x = ap prâ‚ (fÌ…-after-Î·-is-f x)

\end{code}

Paying attention to universe levels, it is important to note that the quotient
of X : ğ“¤ by a ğ“¥-valued equivalence relation is assumed to live in ğ“¤ âŠ” ğ“¥. In
particular, the quotient of type in ğ“¤ by a ğ“¤-valued equivalence relation lives
in ğ“¤ again.

The following is boilerplate and duplicates some of the material in
Quotient.Large, where large set quotients are constructed using propositional
truncations, function extensionality and propositional extensionality.

We need the boilerplate in OrdinalOfOrdinalsSuprema.lagda, where we use set
quotients to construct small suprema of small ordinals.

A quotient is said to be effective if for every x, y : X, we have x â‰ˆ y whenever
Î·/ x ï¼ â€ŒÎ·/ y. Notice that we did not include effectivity as a requirement in
'set-quotients-exist'. But actually it follows from the other properties, at
least in the presence of function extensionality and propositonal
extensionality, as MartÃ­n observed. The proof is as follows:

(1) First construct propositional truncations using assumed set quotients.
(2) Construct another (large) quotient as described in Quotient.Larges.lagda.
(3) This large quotient is effective, but has to be isomorphic to the assumed
    set quotient, hence this quotient has to be effective as well.

TODO: Implement this in Agda.

\begin{code}

 module _
         {X : ğ“¤ Ì‡ }
         (â‰‹@(_â‰ˆ_ , â‰ˆp , â‰ˆr , â‰ˆs , â‰ˆt) : EqRel {ğ“¤} {ğ“¥} X)
        where

  module _
          {A : ğ“¦ Ì‡ }
          (A-is-set : is-set A)
         where

   mediating-map/ : (f : X â†’ A)
                  â†’ identifies-related-points â‰‹ f
                  â†’ X / â‰‹ â†’ A
   mediating-map/ f p = âˆƒ!-witness (/-universality â‰‹ A-is-set f p)

   universality-triangle/ : (f : X â†’ A)
                            (p : identifies-related-points â‰‹ f)
                          â†’ mediating-map/ f p âˆ˜ Î·/ â‰‹ âˆ¼ f
   universality-triangle/ f p = âˆƒ!-is-witness (/-universality â‰‹ A-is-set f p)

\end{code}

We extend unary and binary prop-valued relations to the quotient.

\begin{code}

 module extending-relations-to-quotient (fe : Fun-Ext) (pe : Prop-Ext) where

  module _
          {X : ğ“¤ Ì‡ }
          (â‰‹@(_â‰ˆ_ , â‰ˆp , â‰ˆr , â‰ˆs , â‰ˆt) : EqRel {ğ“¤} {ğ“¥} X)
         where

   module _
           (r : X â†’ Î© ğ“£)
           (p : {x y : X} â†’ x â‰ˆ y â†’ r x ï¼ r y)
          where

    extension-relâ‚ : X / â‰‹ â†’ Î© ğ“£
    extension-relâ‚ = mediating-map/ â‰‹ (Î©-is-set fe pe) r p

    extension-rel-triangleâ‚ : extension-relâ‚ âˆ˜ Î·/ â‰‹ âˆ¼ r
    extension-rel-triangleâ‚ = universality-triangle/ â‰‹ (Î©-is-set fe pe) r p

   module _ (r : X â†’ X â†’ Î© ğ“£)
            (p : {x y x' y' : X} â†’ x â‰ˆ x' â†’ y â‰ˆ y' â†’ r x y ï¼ r x' y')
          where

    abstract
     private
      p' : (x : X) {y y' : X} â†’ y â‰ˆ y' â†’ r x y ï¼ r x y'
      p' x {y} {y'} = p (â‰ˆr x)

      râ‚ : X â†’ X / â‰‹ â†’ Î© ğ“£
      râ‚ x = extension-relâ‚ (r x) (p' x)

      Î´ : {x x' : X} â†’ x â‰ˆ x' â†’ (y : X) â†’ râ‚ x (Î·/ â‰‹ y) ï¼ râ‚ x' (Î·/ â‰‹ y)
      Î´ {x} {x'} e y =
        râ‚ x  (Î·/ â‰‹ y)  ï¼âŸ¨ extension-rel-triangleâ‚ (r x) (p (â‰ˆr x)) y        âŸ©
        r  x     y      ï¼âŸ¨ p e (â‰ˆr y)                                        âŸ©
        r  x'    y      ï¼âŸ¨ (extension-rel-triangleâ‚ (r x') (p (â‰ˆr x')) y) â»Â¹ âŸ©
        râ‚ x' (Î·/ â‰‹ y)  âˆ

      Ï : (q : X / â‰‹) {x x' : X} â†’ x â‰ˆ x' â†’ râ‚ x q ï¼ râ‚ x' q
      Ï q {x} {x'} e = /-induction â‰‹ (Î» q â†’ Î©-is-set fe pe) (Î´ e) q

      râ‚‚ : X / â‰‹ â†’ X / â‰‹ â†’ Î© ğ“£
      râ‚‚ = mediating-map/ â‰‹ (Î -is-set fe (Î» _ â†’ Î©-is-set fe pe)) râ‚
                            (Î» {x} {x'} e â†’ dfunext fe (Î» q â†’ Ï q e))

      Ïƒ : (x : X) â†’ râ‚‚ (Î·/ â‰‹ x) ï¼ râ‚ x
      Ïƒ = universality-triangle/ â‰‹ (Î -is-set fe (Î» _ â†’ Î©-is-set fe pe)) râ‚
                                   (Î» {x} {x'} e â†’ dfunext fe (Î» q â†’ Ï q e))

      Ï„ : (x y : X) â†’ râ‚‚ (Î·/ â‰‹ x) (Î·/ â‰‹ y) ï¼ r x y
      Ï„ x y = râ‚‚ (Î·/ â‰‹ x) (Î·/ â‰‹ y) ï¼âŸ¨ happly (Ïƒ x) (Î·/ â‰‹ y) âŸ©
              râ‚ x        (Î·/ â‰‹ y) ï¼âŸ¨ extension-rel-triangleâ‚ (r x) (p' x) y âŸ©
              r  x            y    âˆ

     extension-relâ‚‚ : X / â‰‹ â†’ X / â‰‹ â†’ Î© ğ“£
     extension-relâ‚‚ = râ‚‚

     extension-rel-triangleâ‚‚ : (x y : X)
                             â†’ extension-relâ‚‚ (Î·/ â‰‹ x) (Î·/ â‰‹ y) ï¼ r x y
     extension-rel-triangleâ‚‚ = Ï„

\end{code}

For proving properties of an extended binary relation, it is useful to have a
binary and ternary versions of quotient induction.

\begin{code}

 module _
         (fe : Fun-Ext)
         {X : ğ“¤ Ì‡ }
         (â‰‹ : EqRel {ğ“¤ } {ğ“¥} X)
        where

  /-inductionâ‚‚ : âˆ€ {ğ“¦} {P : X / â‰‹ â†’ X / â‰‹ â†’ ğ“¦ Ì‡ }
               â†’ ((x' y' : X / â‰‹) â†’ is-prop (P x' y'))
               â†’ ((x y : X) â†’ P (Î·/ â‰‹ x) (Î·/ â‰‹ y))
               â†’ (x' y' : X / â‰‹) â†’ P x' y'
  /-inductionâ‚‚ p h =
   /-induction â‰‹ (Î» x' â†’ Î -is-prop fe (p x'))
                 (Î» x â†’ /-induction â‰‹ (p (Î·/ â‰‹ x)) (h x))

  /-inductionâ‚ƒ : âˆ€ {ğ“¦} â†’ {P : X / â‰‹ â†’ X / â‰‹ â†’ X / â‰‹ â†’ ğ“¦ Ì‡ }
               â†’ ((x' y' z' : X / â‰‹) â†’ is-prop (P x' y' z'))
               â†’ ((x y z : X) â†’ P (Î·/ â‰‹ x) (Î·/ â‰‹ y) (Î·/ â‰‹ z))
               â†’ (x' y' z' : X / â‰‹) â†’ P x' y' z'
  /-inductionâ‚ƒ p h =
   /-inductionâ‚‚ (Î» x' y' â†’ Î -is-prop fe (p x' y'))
                (Î» x y â†’ /-induction â‰‹ (p (Î·/ â‰‹ x) (Î·/ â‰‹ y)) (h x y))

 quotients-equivalent : (X : ğ“¤ Ì‡ ) (R : EqRel {ğ“¤} {ğ“¥} X) (R' : EqRel {ğ“¤} {ğ“¦} X)
                      â†’ ({x y : X} â†’ x â‰ˆ[ R ] y â‡” x â‰ˆ[ R' ] y)
                      â†’ (X / R) â‰ƒ (X / R')
 quotients-equivalent X (_â‰ˆ_  , â‰ˆp ,  â‰ˆr  , â‰ˆs  , â‰ˆt )
                        (_â‰ˆ'_ , â‰ˆp' , â‰ˆr' , â‰ˆs' , â‰ˆt') Îµ = Î³
  where
   â‰‹  = (_â‰ˆ_  , â‰ˆp ,  â‰ˆr  , â‰ˆs  , â‰ˆt )
   â‰‹' = (_â‰ˆ'_ , â‰ˆp' , â‰ˆr' , â‰ˆs' , â‰ˆt')
   i : {x y : X} â†’ x â‰ˆ y â†’ Î·/ â‰‹' x ï¼ Î·/ â‰‹' y
   i e = Î·/-identifies-related-points â‰‹' (lr-implication Îµ e)
   i' : {x y : X} â†’ x â‰ˆ' y â†’ Î·/ â‰‹ x ï¼ Î·/ â‰‹ y
   i' e = Î·/-identifies-related-points â‰‹ (rl-implication Îµ e)
   f : X / â‰‹ â†’ X / â‰‹'
   f = mediating-map/ â‰‹ (/-is-set â‰‹') (Î·/ â‰‹') i
   f' : X / â‰‹' â†’ X / â‰‹
   f' = mediating-map/ â‰‹' (/-is-set â‰‹) (Î·/ â‰‹) i'
   a : (x : X) â†’ f (f' (Î·/ â‰‹' x)) ï¼ Î·/ â‰‹' x
   a x = f (f' (Î·/ â‰‹' x)) ï¼âŸ¨ I âŸ©
         f (Î·/ â‰‹ x)       ï¼âŸ¨ II âŸ©
         Î·/ â‰‹' x          âˆ
    where
     I  = ap f (universality-triangle/ â‰‹' (/-is-set â‰‹) (Î·/ â‰‹) i' x)
     II = universality-triangle/ â‰‹ (/-is-set â‰‹') (Î·/ â‰‹') i x
   Î± : f âˆ˜ f' âˆ¼ id
   Î± = /-induction â‰‹' (Î» _ â†’ /-is-set â‰‹') a
   a' : (x : X) â†’ f' (f (Î·/ â‰‹ x)) ï¼ Î·/ â‰‹ x
   a' x = f' (f (Î·/ â‰‹ x)) ï¼âŸ¨ I âŸ©
         f' (Î·/ â‰‹' x)     ï¼âŸ¨ II âŸ©
         Î·/ â‰‹ x           âˆ
    where
     I  = ap f' (universality-triangle/ â‰‹ (/-is-set â‰‹') (Î·/ â‰‹') i x)
     II = universality-triangle/ â‰‹' (/-is-set â‰‹) (Î·/ â‰‹) i' x
   Î±' : f' âˆ˜ f âˆ¼ id
   Î±' = /-induction â‰‹ (Î» _ â†’ /-is-set â‰‹) a'
   Î³ : (X / â‰‹) â‰ƒ (X / â‰‹')
   Î³ = qinveq f (f' , Î±' , Î±)

\end{code}

We now define the existence of small and large quotients:

\begin{code}

set-quotients-exist large-set-quotients-exist : ğ“¤Ï‰
set-quotients-exist       = general-set-quotients-exist id
large-set-quotients-exist = general-set-quotients-exist (_âº)

\end{code}

\begin{code}

are-effective : {F : Universe â†’ Universe} â†’ general-set-quotients-exist F â†’ ğ“¤Ï‰
are-effective {F} sq = {ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ )
                       {R : EqRel {ğ“¤} {ğ“¥} X}
                       {x y : X}
                     â†’ Î·/ R x ï¼ Î·/ R y â†’ x â‰ˆ[ R ] y
 where
  open general-set-quotients-exist sq

\end{code}
