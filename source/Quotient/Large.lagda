Martin Escardo, August 2018.

Large set quotients in univalent mathematics in Agda notation.

This took place during the Dagstuhl meeting "Formalization of
Mathematics in Type Theory", because Dan Grayson wanted to see how
universe levels work in Agda and I thought that this would be a nice
example to illustrate that.

We assume, in addition to Spartan Martin-LÃ¶f type theory,

 * function extensionality
   (any two pointwise equal functions are equal),

 * propositional extensionality
   (any two logically equivalent propositions are equal),

 * propositional truncation
   (any type can be universally mapped into a prop in the same
   universe),

and no resizing axioms.

The K axiom is not used (the without-K option below). We also make
sure pattern matching corresponds to Martin-LÃ¶f eliminators, using the
option exact-split. With the option safe we make sure that nothing
is postulated - any non-MLTT axiom has to be an explicit assumption
(argument to a function or a module).

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import Quotient.Type
open import UF.Base hiding (_â‰ˆ_)
open import UF.Equiv
open import UF.FunExt
open import UF.Hedberg
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

module Quotient.Large
        (pt  : propositional-truncations-exist)
        (fe  : Fun-Ext)
        (pe  : Prop-Ext)
       where

open PropositionalTruncation pt
open import UF.ImageAndSurjection pt

\end{code}

(NB. The Agda library uses the word "Level" for universes, and then
what we write "ğ“¤ Ì‡" here is written "Set ğ“¤". This is not good for
univalent mathematics, because the types in ğ“¤ Ì‡ need not be sets, and
also because it places emphasis on levels rather than universes
themselves.)

Now, using an anonymous module UF.with parameters (corresponding to a
section in Coq), we assume propositional truncations that stay in the
same universe, function extensionality for all universes, two
universes ğ“¤ and ğ“¥, propositional truncation for the universe ğ“¥, a type
X : ğ“¤ Ì‡, and an equivalence relation _â‰ˆ_ with values in ğ“¥ Ì‡.

\begin{code}

module large-quotient
       {ğ“¤ ğ“¥ : Universe}
       (X   : ğ“¤ Ì‡ )
       (â‰‹@(_â‰ˆ_ , â‰ˆp , â‰ˆr , â‰ˆs , â‰ˆt) : EqRel {ğ“¤} {ğ“¥} X)
      where

\end{code}

Now, Î© ğ“¥ is the type of subsingletons, or (univalent) propositions, or
h-propositions, or mere propositions, in the universe ğ“¥, which lives
in the next universe ğ“¥ âº.

From the relation _â‰ˆ_ : X â†’ (X â†’ ğ“¥ Ì‡ ) we define a relation
X â†’ (X â†’ Î© ğ“¥), which of course is formally a function. We then take
the quotient X/â‰ˆ to be the image of this function.

Of course, it is for constructing the image that we need propositional
truncations.

\begin{code}

 equiv-rel : X â†’ (X â†’ Î© ğ“¥)
 equiv-rel x y = x â‰ˆ y , â‰ˆp x y

\end{code}

Then the quotient lives in the least upper bound of ğ“¤ and ğ“¥ âº, where ğ“¥ âº
is the successor of the universe ğ“¥:

\begin{code}

 X/â‰ˆ : ğ“¤ âŠ” (ğ“¥ âº) Ì‡
 X/â‰ˆ = image equiv-rel

 X/â‰ˆ-is-set : is-set X/â‰ˆ
 X/â‰ˆ-is-set = subsets-of-sets-are-sets (X â†’ Î© ğ“¥) _
               (powersets-are-sets'' fe fe pe)
               âˆ¥âˆ¥-is-prop

 Î· : X â†’ X/â‰ˆ
 Î· = corestriction equiv-rel

\end{code}

Then Î· is the universal solution to the problem of transforming
equivalence _â‰ˆ_ into equality _ï¼_ (identity type).

By construction, Î· is a surjection, of course:

\begin{code}

 Î·-surjection : is-surjection Î·
 Î·-surjection = corestrictions-are-surjections equiv-rel

\end{code}

It is convenient to use the following induction principle for
reasoning about the image. Notice that the property we consider has
values in any universe ğ“¦ we please:

\begin{code}

 quotient-induction : âˆ€ {ğ“¦} (P : X/â‰ˆ â†’ ğ“¦ Ì‡ )
                    â†’ ((x' : X/â‰ˆ) â†’ is-prop (P x'))
                    â†’ ((x : X) â†’ P (Î· x))
                    â†’ (x' : X/â‰ˆ) â†’ P x'
 quotient-induction = surjection-induction Î· Î·-surjection

\end{code}

The first part of the universal property of Î· says that equivalent
points are mapped to equal points:

\begin{code}

 Î·-identifies-related-points : {x y : X} â†’ x â‰ˆ y â†’ Î· x ï¼ Î· y
 Î·-identifies-related-points {x} {y} e =
   to-Î£-ï¼ (dfunext fe
          (Î» z â†’ to-Î£-ï¼ (pe (â‰ˆp x z) (â‰ˆp y z) (â‰ˆt y x z (â‰ˆs x y e)) (â‰ˆt x y z e) ,
                         being-prop-is-prop fe _ _)) ,
       âˆ¥âˆ¥-is-prop _ _)

\end{code}

We also need the fact that Î· reflects equality into equivalence:

\begin{code}

 Î·-relates-identified-points : {x y : X} â†’ Î· x ï¼ Î· y â†’ x â‰ˆ y
 Î·-relates-identified-points {x} {y} p = equiv-rel-reflect (ap prâ‚ p)
  where
   equiv-rel-reflect : equiv-rel x ï¼ equiv-rel y â†’ x â‰ˆ y
   equiv-rel-reflect q = b (â‰ˆr y)
    where
     a : (y â‰ˆ y) ï¼ (x â‰ˆ y)
     a = ap (Î» - â†’ prâ‚ (- y)) (q â»Â¹)
     b : (y â‰ˆ y) â†’ (x â‰ˆ y)
     b = Idtofun a

\end{code}

We are now ready to formulate and prove the universal property of the
quotient. What is noteworthy here, regarding universes, is that the
universal property says that we can eliminate into any set A of any
universe ğ“¦.

                   Î·
              X ------> X/â‰ˆ
               \       .
                \     .
               f \   . f'
                  \ .
                   v
                   A

\begin{code}

 universal-property : âˆ€ {ğ“¦} (A : ğ“¦ Ì‡ )
                    â†’ is-set A
                    â†’ (f : X â†’ A)
                    â†’ ({x x' : X} â†’ x â‰ˆ x' â†’ f x ï¼ f x')
                    â†’ âˆƒ! f' ê‰ ( X/â‰ˆ â†’ A), f' âˆ˜ Î· âˆ¼ f
 universal-property {ğ“¦} A iss f pr = ic
  where
   Ï† : (x' : X/â‰ˆ) â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X ,  (Î· x ï¼ x') Ã— (f x ï¼ a))
   Ï† = quotient-induction _ Î³ induction-step
     where
      induction-step : (y : X) â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X ,  (Î· x ï¼ Î· y) Ã— (f x ï¼ a))
      induction-step x (a , d) (b , e) = to-Î£-ï¼ (p , âˆ¥âˆ¥-is-prop _ _)
       where
        h : (Î£ x' ê‰ X , (Î· x' ï¼ Î· x) Ã— (f x' ï¼ a))
          â†’ (Î£ y' ê‰ X , (Î· y' ï¼ Î· x) Ã— (f y' ï¼ b))
          â†’ a ï¼ b
        h (x' , r , s) (y' , t , u) = s â»Â¹ âˆ™ pr (Î·-relates-identified-points (r âˆ™ t â»Â¹)) âˆ™ u

        p : a ï¼ b
        p = âˆ¥âˆ¥-rec iss (Î» Ïƒ â†’ âˆ¥âˆ¥-rec iss (h Ïƒ) e) d

      Î³ : (x' : X/â‰ˆ) â†’ is-prop (is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ x') Ã— (f x ï¼ a)))
      Î³ x' = being-prop-is-prop fe

   k : (x' : X/â‰ˆ) â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ x') Ã— (f x ï¼ a)
   k = quotient-induction _ Ï† induction-step
    where
     induction-step : (y : X) â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ a)
     induction-step x = f x , âˆ£ x , refl , refl âˆ£

   f' : X/â‰ˆ â†’ A
   f' x' = prâ‚ (k x')

   r : f' âˆ˜ Î· âˆ¼ f
   r = h
    where
     g : (y : X) â†’ âˆƒ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ f' (Î· y))
     g y = prâ‚‚ (k (Î· y))

     j : (y : X) â†’ (Î£ x ê‰ X , (Î· x ï¼ Î· y) Ã— (f x ï¼ f' (Î· y))) â†’ f' (Î· y) ï¼ f y
     j y (x , p , q) = q â»Â¹ âˆ™ pr (Î·-relates-identified-points p)

     h : (y : X) â†’ f' (Î· y) ï¼ f y
     h y = âˆ¥âˆ¥-rec iss (j y) (g y)

   c : (Ïƒ : Î£ f'' ê‰ (X/â‰ˆ â†’ A), f'' âˆ˜ Î· âˆ¼ f) â†’ (f' , r) ï¼ Ïƒ
   c (f'' , s) = to-Î£-ï¼ (t , v) -- (t , v)
    where
     w : âˆ€ x â†’ f' (Î· x) ï¼ f'' (Î· x)
     w x = r x âˆ™ (s x)â»Â¹

     t : f' ï¼ f''
     t = dfunext fe (quotient-induction _ (Î» _ â†’ iss) w)

     u : f'' âˆ˜ Î· âˆ¼ f
     u = transport (Î» - â†’ - âˆ˜ Î· âˆ¼ f) t r

     v : u ï¼ s
     v = dfunext fe (Î» x â†’ iss (u x) (s x))

   ic : âˆƒ! f' ê‰ (X/â‰ˆ â†’ A), f' âˆ˜ Î· âˆ¼ f
   ic = (f' , r) , c

\end{code}

Added 11 Sep 2023 by Martin Escardo. Package the above into the type
of existence of large effective set quotients.

\begin{code}

large-set-quotients : large-set-quotients-exist
large-set-quotients =
 record
  { _/_ = Î» {ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ ) â†’ large-quotient.X/â‰ˆ X
  ; Î·/ = Î» {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } â†’ large-quotient.Î· X
  ; Î·/-identifies-related-points = Î» {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ }
                                 â†’ large-quotient.Î·-identifies-related-points X
  ; /-is-set = Î» {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } â†’ large-quotient.X/â‰ˆ-is-set X
  ; /-universality = Î» {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } (â‰‹ : EqRel {ğ“¤} {ğ“¥} X)
                       {ğ“¦ : Universe} {Y : ğ“¦ Ì‡ }
                   â†’ large-quotient.universal-property X â‰‹ Y
  }

large-effective-set-quotients : are-effective large-set-quotients
large-effective-set-quotients {ğ“¤} {ğ“¥} {X} â‰‹ {x} {y} =
 large-quotient.Î·-relates-identified-points X â‰‹

\end{code}
