Martin Escardo, 1st March 2022

This extends OrdinalNotationInterpretation.lagda.

This is a draft version that needs polishing.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt

module OrdinalExtendedNotationInterpretation (fe : FunExt) where

open import ToppedOrdinalsType fe
open import OrdinalArithmetic fe
open import ToppedOrdinalArithmetic fe
open import OrdinalsClosure fe
open import DiscreteAndSeparated
open import InjectiveTypes fe
open import GenericConvergentSequence
open import ConvergentSequenceHasLeast
open import PropInfTychonoff fe

open import UF-Base
open import UF-Subsingletons
open import UF-Retracts
open import UF-Embeddings
open import UF-Equiv
open import UF-Subsingletons-FunExt

\end{code}

We define E and Î” by simultaneous induction:

\begin{code}

data E : ğ“¤â‚€ Ì‡
Î” : E â†’ Ordáµ€

data E where
 âŒœğŸ™âŒ     : E
 âŒœÏ‰+ğŸ™âŒ   : E
 _âŒœ+âŒ_   : E â†’ E â†’ E
 _âŒœÃ—âŒ_   : E â†’ E â†’ E
 âŒœÎ£âŒ     : (Î½ : E) â†’ (âŸª Î” Î½ âŸ« â†’ E) â†’ E

Î” âŒœğŸ™âŒ         = ğŸ™áµ’
Î” âŒœÏ‰+ğŸ™âŒ       = succâ‚’ â„•â‚’
Î” (Î½â‚€ âŒœ+âŒ Î½â‚) = Î” Î½â‚€ +áµ’ Î” Î½â‚
Î” (Î½â‚€ âŒœÃ—âŒ Î½â‚) = Î” Î½â‚€ Ã—áµ’ Î” Î½â‚
Î” (âŒœÎ£âŒ Î½ A)   = âˆ‘ (Î” Î½) (Î” âˆ˜ A)

\end{code}

And now we define Îš, Î¹, Î¹-is-embedding by simultaneous
induction, using the above definitions:

\begin{code}

Îš : E â†’ Ordáµ€
Î¹ : (Î½ : E) â†’ âŸª Î” Î½ âŸ« â†’ âŸª Îš Î½ âŸ«
Î¹-is-embedding : (Î½ : E) â†’ is-embedding (Î¹ Î½)

I : (Î½ : E) â†’ âŸª Î” Î½ âŸ« â†ª âŸª Îš Î½ âŸ«
I Î½ = (Î¹ Î½ , Î¹-is-embedding Î½)

module _ (Î½ : E) (A : âŸª Î” Î½ âŸ« â†’ E) where

 Ïˆ : âŸª Îš Î½ âŸ« â†’ Ordáµ€
 Ïˆ = (Îš âˆ˜ A) â†— I Î½

 Ï• : (x : âŸª Î” Î½ âŸ«) â†’ ((Î» x â†’ âŸª Îš (A x) âŸ«) / (Î¹ Î½)) (Î¹ Î½ x) â‰ƒ âŸª Îš (A x) âŸ«
 Ï• = Î -extension-property (Î» x â†’ âŸª Îš (A x) âŸ«) (Î¹ Î½) (Î¹-is-embedding Î½)

 Ï† : (x : âŸª Î” Î½ âŸ«) â†’ âŸª Ïˆ (Î¹ Î½ x) âŸ« â†’ âŸª Îš (A x) âŸ«
 Ï† x = âŒœ Ï• x âŒ

 Ï†â»Â¹ : (x : âŸª Î” Î½ âŸ«) â†’ âŸª Îš (A x) âŸ« â†’ âŸª Ïˆ (Î¹ Î½ x) âŸ«
 Ï†â»Â¹ x = âŒœ Ï• x âŒâ»Â¹

 ÎµÏ† : (x : âŸª Î” Î½ âŸ«) â†’ Ï† x âˆ˜ Ï†â»Â¹ x âˆ¼ id
 ÎµÏ† x = inverses-are-sections (Ï† x) (âŒœâŒ-is-equiv (Ï• x))

 Î³ : (x : âŸª Î” Î½ âŸ«) â†’ âŸª Î” (A x) âŸ« â†’ âŸª Ïˆ (Î¹ Î½ x) âŸ«
 Î³ x = Ï†â»Â¹ x âˆ˜ Î¹ (A x)

 Î³-is-embedding : (x : âŸª Î” Î½ âŸ«) â†’ is-embedding (Î³ x)
 Î³-is-embedding x = âˆ˜-is-embedding
                     (Î¹-is-embedding (A x))
                     (equivs-are-embeddings _ (âŒœâŒâ»Â¹-is-equiv (Ï• x)))

 Î¹-Î³-lemma : (x : âŸª Î” Î½ âŸ«) (y : âŸª Î” (A x) âŸ«)
           â†’ Î¹ (A x) y â‰¡ Î³ x y (x , refl)
 Î¹-Î³-lemma x = q
  where
   p : refl â‰¡ (Î¹-is-embedding Î½ (Î¹ Î½ x) (x , refl) (x , refl))
   p = props-are-sets (Î¹-is-embedding Î½ (Î¹ Î½ x)) _ _

   q : (y : âŸª Î” (A x) âŸ«) â†’ Î¹ (A x) y â‰¡ Î³ x y (x , refl)
   q y = ap (Î» - â†’ transport (Î» (x , _) â†’ âŸª Îš (A x) âŸ«) - (Î¹ (A x) y)) p

Îš âŒœğŸ™âŒ         = ğŸ™áµ’
Îš âŒœÏ‰+ğŸ™âŒ       = â„•âˆáµ’
Îš (Î½â‚€ âŒœ+âŒ Î½â‚) = Îš Î½â‚€ +áµ’ Îš Î½â‚
Îš (Î½â‚€ âŒœÃ—âŒ Î½â‚) = Îš Î½â‚€ Ã—áµ’ Îš Î½â‚
Îš (âŒœÎ£âŒ Î½ A)   = âˆ‘ (Îš Î½) (Ïˆ Î½ A)

Î¹ âŒœğŸ™âŒ         = id
Î¹ âŒœÏ‰+ğŸ™âŒ       = Î¹ğŸ™
Î¹ (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun id (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
Î¹ (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun (Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚)
Î¹ (âŒœÎ£âŒ Î½ A)   = pair-fun (Î¹ Î½) (Î³ Î½ A)

Î¹-is-embedding âŒœğŸ™âŒ         = id-is-embedding
Î¹-is-embedding âŒœÏ‰+ğŸ™âŒ       = Î¹ğŸ™-is-embedding (fe ğ“¤â‚€ ğ“¤â‚€)
Î¹-is-embedding (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-is-embedding
                              id
                              (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                              id-is-embedding
                             (dep-cases (Î» _ â†’ Î¹-is-embedding Î½â‚€) (Î» _ â†’ Î¹-is-embedding Î½â‚))
Î¹-is-embedding (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-embedding _ _
                              (Î¹-is-embedding Î½â‚€)
                              (Î» _ â†’ Î¹-is-embedding Î½â‚)
Î¹-is-embedding (âŒœÎ£âŒ Î½ A)   = pair-fun-is-embedding _ _
                              (Î¹-is-embedding Î½)
                              (Î³-is-embedding Î½ A)
\end{code}

The Îš interpretation gives ordinals such that every decidable subset
is either empty or has a least element:

\begin{code}

K-has-least-element-property : propext ğ“¤â‚€ â†’ (Î½ : E) â†’ has-least-element-property (Îš Î½)
K-has-least-element-property pe âŒœğŸ™âŒ         = ğŸ™áµ’-has-least-element-property
K-has-least-element-property pe âŒœÏ‰+ğŸ™âŒ       = â„•âˆáµ’-has-least-element-property pe
K-has-least-element-property pe (Î½â‚€ âŒœ+âŒ Î½â‚) = âˆ‘-has-least-element-property pe
                                               ğŸšáµ’
                                               (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
                                               ğŸšáµ’-has-least-element-property
                                               (dep-cases (Î» _ â†’ K-has-least-element-property pe Î½â‚€)
                                                          (Î» _ â†’ K-has-least-element-property pe Î½â‚))
K-has-least-element-property pe (Î½â‚€ âŒœÃ—âŒ Î½â‚) = âˆ‘-has-least-element-property pe
                                               (Îš Î½â‚€)
                                               (Î» _ â†’ Îš Î½â‚)
                                               (K-has-least-element-property pe Î½â‚€)
                                               (Î» _ â†’ K-has-least-element-property pe Î½â‚)
K-has-least-element-property pe (âŒœÎ£âŒ Î½ A)   = âˆ‘-has-least-element-property pe (Îš Î½)
                                               (Ïˆ Î½ A)
                                               (K-has-least-element-property pe Î½)
                                               (Î» x â†’ prop-inf-tychonoff
                                                       (Î¹-is-embedding Î½ x)
                                                       (Î» {w} x y â†’ x â‰ºâŸª Îš (A (prâ‚ w)) âŸ« y)
                                                       (Î» (x , _) â†’ K-has-least-element-property pe (A x)))
\end{code}

The embedding of the Î” interpretation into the Îš interpretation is
order-preserving, order-reflecting, and dense (its image has empty
complement):

\begin{code}

Î¹-is-order-preserving : (Î½ : E) (x y : âŸª Î” Î½ âŸ«)
                      â†’     x â‰ºâŸª Î” Î½ âŸ«     y
                      â†’ Î¹ Î½ x â‰ºâŸª Îš Î½ âŸ« Î¹ Î½ y
Î¹-is-order-preserving âŒœğŸ™âŒ         = Î» x y l â†’ l
Î¹-is-order-preserving âŒœÏ‰+ğŸ™âŒ       = Î¹ğŸ™áµ’-is-order-preserving
Î¹-is-order-preserving (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-is-order-preserving
                                     ğŸšáµ’
                                     ğŸšáµ’
                                     (cases (Î» _ â†’ Î” Î½â‚€) (Î» _ â†’ Î” Î½â‚))
                                     (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
                                     id
                                     (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                                     (Î» x y l â†’ l)
                                     (dep-cases (Î» _ â†’ Î¹-is-order-preserving Î½â‚€)
                                                (Î» _ â†’ Î¹-is-order-preserving Î½â‚))
Î¹-is-order-preserving (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-order-preserving
                                     (Î” Î½â‚€)
                                     (Îš Î½â‚€)
                                     (Î» _ â†’ Î” Î½â‚)
                                     (Î» _ â†’ Îš Î½â‚)
                                     (Î¹ Î½â‚€)
                                     (Î» _ â†’ Î¹ Î½â‚)
                                     (Î¹-is-order-preserving Î½â‚€)
                                     (Î» _ â†’ Î¹-is-order-preserving Î½â‚)
Î¹-is-order-preserving (âŒœÎ£âŒ Î½ A)   = pair-fun-is-order-preserving
                                     (Î” Î½)
                                     (Îš Î½)
                                     (Î» x â†’ Î” (A x))
                                     (Ïˆ Î½ A)
                                     (Î¹ Î½)
                                     (Î³ Î½ A)
                                     (Î¹-is-order-preserving Î½)
                                     g
 where
  IH : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
     â†’ y â‰ºâŸª Î” (A x) âŸ« z
     â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
  IH x = Î¹-is-order-preserving (A x)

  f : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
    â†’ Î³ Î½ A x y â‰ºâŸª Ïˆ Î½ A (Î¹ Î½ x) âŸ« Î³ Î½ A x z
  f x y z l = (x , refl) ,
              transportâ‚‚ (Î» j k â†’ j â‰ºâŸª Îš (A x) âŸ« k)
               (Î¹-Î³-lemma Î½ A x y)
               (Î¹-Î³-lemma Î½ A x z) l

  g : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ y â‰ºâŸª Î” (A x) âŸ« z
    â†’ Î³ Î½ A x y â‰ºâŸª Ïˆ Î½ A (Î¹ Î½ x) âŸ« Î³ Î½ A x z
  g x y z l = f x y z (IH x y z l)

Î¹-is-order-reflecting : (Î½ : E) (x y : âŸª Î” Î½ âŸ«)
                      â†’ Î¹ Î½ x â‰ºâŸª Îš Î½ âŸ« Î¹ Î½ y
                      â†’     x â‰ºâŸª Î” Î½ âŸ«     y
Î¹-is-order-reflecting âŒœğŸ™âŒ        = Î» x y l â†’ l
Î¹-is-order-reflecting âŒœÏ‰+ğŸ™âŒ      = Î¹ğŸ™áµ’-is-order-reflecting
Î¹-is-order-reflecting (Î½â‚€ âŒœ+âŒ Î½â‚) =  pair-fun-is-order-reflecting
                                      ğŸšáµ’
                                      ğŸšáµ’
                                      (cases (Î» _ â†’ Î” Î½â‚€) (Î» _ â†’ Î” Î½â‚))
                                      (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
                                      id
                                      (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                                      (Î» x y l â†’ l)
                                      id-is-embedding
                                      (dep-cases (Î» _ â†’ Î¹-is-order-reflecting Î½â‚€)
                                                 (Î» _ â†’ Î¹-is-order-reflecting Î½â‚))
Î¹-is-order-reflecting (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-order-reflecting
                                     (Î” Î½â‚€)
                                     (Îš Î½â‚€)
                                     (Î» _ â†’ Î” Î½â‚)
                                     (Î» _ â†’ Îš Î½â‚)
                                     (Î¹ Î½â‚€)
                                     (Î» _ â†’ Î¹ Î½â‚)
                                     (Î¹-is-order-reflecting Î½â‚€)
                                     (Î¹-is-embedding Î½â‚€)
                                     (Î» _ â†’ Î¹-is-order-reflecting Î½â‚)
Î¹-is-order-reflecting (âŒœÎ£âŒ Î½ A)  = pair-fun-is-order-reflecting
                                    (Î” Î½)
                                    (Îš Î½)
                                    (Î» x â†’ Î” (A x))
                                    (Ïˆ Î½ A)
                                    (Î¹ Î½)
                                    (Î³ Î½ A)
                                    (Î¹-is-order-reflecting Î½)
                                    (Î¹-is-embedding Î½)
                                    g
 where
  IH : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
     â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
     â†’ y â‰ºâŸª Î” (A x) âŸ« z
  IH x = Î¹-is-order-reflecting (A x)

  f : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ Î³ Î½ A x y â‰ºâŸª Ïˆ Î½ A (Î¹ Î½ x) âŸ« Î³ Î½ A x z
    â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
  f x y z ((x' , p) , l) = n
   where
    q : (x' , p) â‰¡ (x , refl)
    q = Î¹-is-embedding Î½ (Î¹ Î½ x) _ _

    m : Ï†â»Â¹ Î½ A x  (Î¹ (A x) y) (x , refl) â‰ºâŸª Îš (A x) âŸ«  Ï†â»Â¹ Î½ A x (Î¹ (A x) z) (x , refl)
    m = transport (Î» (x' , p) â†’ Î³ Î½ A x y (x' , p) â‰ºâŸª Îš (A x') âŸ« Î³ Î½ A x z (x' , p)) q l

    n : Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ«  Î¹ (A x) z
    n = transportâ‚‚ (Î» u v â†’ u â‰ºâŸª Îš (A x) âŸ« v) ((Î¹-Î³-lemma Î½ A x y)â»Â¹) ((Î¹-Î³-lemma Î½ A x z)â»Â¹) m

  g : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ Î³ Î½ A x y â‰ºâŸª Ïˆ Î½ A (Î¹ Î½ x) âŸ« Î³ Î½ A x z
    â†’ y â‰ºâŸª Î” (A x) âŸ« z
  g x y z l = IH x y z (f x y z l)

Î¹-is-dense : (Î½ : E) â†’ is-dense (Î¹ Î½)
Î¹-is-dense âŒœğŸ™âŒ         = id-is-dense
Î¹-is-dense âŒœÏ‰+ğŸ™âŒ       = Î¹ğŸ™-dense (fe ğ“¤â‚€ ğ“¤â‚€)
Î¹-is-dense (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-dense
                          id
                          (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                          id-is-dense
                          (dep-cases (Î» _ â†’ Î¹-is-dense Î½â‚€) (Î» _ â†’ Î¹-is-dense Î½â‚))
Î¹-is-dense (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-dense _ _
                          (Î¹-is-dense Î½â‚€)
                          (Î» _ â†’ Î¹-is-dense Î½â‚)
Î¹-is-dense (âŒœÎ£âŒ Î½ A)   = pair-fun-dense
                          (Î¹ Î½)
                          (Î³ Î½ A)
                          (Î¹-is-dense Î½)
                          (Î» x â†’ comp-is-dense
                                  (Î¹-is-dense (A x))
                                  (equivs-are-dense
                                    (Ï†â»Â¹ Î½ A x)
                                    (inverses-are-equivs (Ï† Î½ A x) (âŒœâŒ-is-equiv (Ï• Î½ A x)))))
\end{code}
