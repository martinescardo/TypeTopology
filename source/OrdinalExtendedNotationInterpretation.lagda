Martin Escardo, March 2022

This generalizes the 2018 file OrdinalNotationInterpretation.

A Tarski universe E of ordinal codes with two related decoding
functions Î” and Îš (standing for "discrete" and "compact"
respectively).

Roughly speaking, E gives ordinal codes or expressions denoting
infinite ordinals. The expressions themselves are infinitary.

An ordinal is a type equipped with an order _â‰º_ that satisfies
suitable properties, which in particular imply that the type is a set
in the sense of HoTT/UF. The adopted notion of ordinal is that of the
HoTT book.

For a code Î½ : E, we have an ordinal Î” Î½, which is discrete (has
decidable equality).

For a code Î½ : E, we have an ordinal Îš Î½, which is compact (or
"searchable"). More than that, every complemented subset of Îš Î½ is
either empty or has a minimal element.

There is an embedding Î¹ : Î” Î½ â†’ Îš Î½ which is order preserving and
reflecting, and whose image has empty complement. The assumption that
it is a bijection implies LPO.

This extends and generalizes OrdinalNotationInterpretation.lagda, for
which slides for a talk are available at
https://www.cs.bham.ac.uk/~mhe/.talks/csl2022.pdf which may well serve
as an introduction to this file. The main difference is that the
ordinal expressions considered there amount to a W type, whereas the
ones considered here amount to an inductive-recursive type,
generalizing that.

This is a draft version that needs polishing and more explanation.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-FunExt

module OrdinalExtendedNotationInterpretation (fe : FunExt) where

private
 feâ‚€ = fe ð“¤â‚€ ð“¤â‚€

open import UF-Base
open import UF-Subsingletons
open import UF-Retracts
open import UF-Embeddings
open import UF-Equiv
open import UF-Subsingletons-FunExt
open import UF-Miscelanea

open import SigmaDiscreteAndTotallySeparated
open import ToppedOrdinalsType fe
open import OrdinalArithmetic fe
open import ToppedOrdinalArithmetic fe
open import OrdinalsClosure fe
open import DiscreteAndSeparated
open import GenericConvergentSequence
open import ConvergentSequenceHasLeast
open import PropTychonoff fe
open import PropInfTychonoff fe
open import BinaryNaturals hiding (_+_)
open import Two-Properties
open import CompactTypes
open import LeastElementProperty
open import WLPO
open import LPO fe
open import Density
open import PairFun

\end{code}

We define E and Î” by simultaneous induction. The type Ordáµ€ is that of
ordinals with a top element (classically, successor ordinals). Recall
that âŸª Î± âŸ« is the underlying type of Î± : Ordáµ€.

\begin{code}

data E : ð“¤â‚€ Ì‡
Î” : E â†’ Ordáµ€

data E where
 âŒœðŸ™âŒ     : E
 âŒœÏ‰+ðŸ™âŒ   : E
 _âŒœ+âŒ_   : E â†’ E â†’ E
 _âŒœÃ—âŒ_   : E â†’ E â†’ E
 âŒœÎ£âŒ     : (Î½ : E) â†’ (âŸª Î” Î½ âŸ« â†’ E) â†’ E

Î” âŒœðŸ™âŒ         = ðŸ™áµ’
Î” âŒœÏ‰+ðŸ™âŒ       = succâ‚’ â„•â‚’
Î” (Î½â‚€ âŒœ+âŒ Î½â‚) = Î” Î½â‚€ +áµ’ Î” Î½â‚
Î” (Î½â‚€ âŒœÃ—âŒ Î½â‚) = Î” Î½â‚€ Ã—áµ’ Î” Î½â‚
Î” (âŒœÎ£âŒ Î½ A)   = âˆ‘ (Î” Î½) (Î” âˆ˜ A)

\end{code}

All ordinals in the image of Î” are retracts of â„•.

\begin{code}

Î”-retract-of-â„• : (Î½ : E) â†’ retract âŸª Î” Î½ âŸ« of â„•
Î”-retract-of-â„• âŒœðŸ™âŒ         = (Î» _ â†’ â‹†) , (Î» _ â†’ 0) , ðŸ™-is-prop â‹†
Î”-retract-of-â„• âŒœÏ‰+ðŸ™âŒ       = â‰ƒ-gives-â— â„•-plus-ðŸ™
Î”-retract-of-â„• (Î½â‚€ âŒœ+âŒ Î½â‚) = Î£-retract-of-â„•
                              retract-ðŸ™+ðŸ™-of-â„•
                              (dep-cases (Î» _ â†’ Î”-retract-of-â„• Î½â‚€)
                                         (Î» _ â†’ Î”-retract-of-â„• Î½â‚))
Î”-retract-of-â„• (Î½â‚€ âŒœÃ—âŒ Î½â‚) = Î£-retract-of-â„•
                              (Î”-retract-of-â„• Î½â‚€)
                              (Î» _ â†’ Î”-retract-of-â„• Î½â‚)
Î”-retract-of-â„• (âŒœÎ£âŒ Î½ A)   = Î£-retract-of-â„•
                              (Î”-retract-of-â„• Î½)
                              (Î» x â†’ Î”-retract-of-â„• (A x))
\end{code}

Hence all ordinals in the image of Î” are discrete (have decidable equality):

\begin{code}

Î”-is-discrete : (Î½ : E) â†’ is-discrete âŸª Î” Î½ âŸ«
Î”-is-discrete Î½ = retract-is-discrete (Î”-retract-of-â„• Î½) â„•-is-discrete

\end{code}

And now we define Îš, Î¹, Î¹-is-embedding by simultaneous
induction:

\begin{code}

Îš : E â†’ Ordáµ€
Î¹ : (Î½ : E) â†’ âŸª Î” Î½ âŸ« â†’ âŸª Îš Î½ âŸ«
Î¹-is-embedding : (Î½ : E) â†’ is-embedding (Î¹ Î½)

\end{code}

We use the following auxiliary extension constructions:

\begin{code}

ð“š : (Î½ : E) â†’ (âŸª Î” Î½ âŸ« â†’ E) â†’ âŸª Îš Î½ âŸ« â†’ Ordáµ€
ð“š Î½ A = (Îš âˆ˜ A) â†— (Î¹ Î½ , Î¹-is-embedding Î½)

module Îš-extension (Î½ : E) (A : âŸª Î” Î½ âŸ« â†’ E) where

 open import InjectiveTypes fe

 Ï• : (x : âŸª Î” Î½ âŸ«) â†’ âŸª ð“š Î½ A (Î¹ Î½ x) âŸ« â‰ƒ âŸª Îš (A x) âŸ«
 Ï• = Î -extension-property (Î» x â†’ âŸª Îš (A x) âŸ«) (Î¹ Î½) (Î¹-is-embedding Î½)

 Ï† : (x : âŸª Î” Î½ âŸ«) â†’ âŸª ð“š Î½ A (Î¹ Î½ x) âŸ« â†’ âŸª Îš (A x) âŸ«
 Ï† x = âŒœ Ï• x âŒ

 Ï†â»Â¹ : (x : âŸª Î” Î½ âŸ«) â†’ âŸª Îš (A x) âŸ« â†’ âŸª ð“š Î½ A (Î¹ Î½ x) âŸ«
 Ï†â»Â¹ x = âŒœ Ï• x âŒâ»Â¹

 Î³ : (x : âŸª Î” Î½ âŸ«) â†’ âŸª Î” (A x) âŸ« â†’ âŸª ð“š Î½ A (Î¹ Î½ x) âŸ«
 Î³ x = Ï†â»Â¹ x âˆ˜ Î¹ (A x)

 Î³-is-embedding : (x : âŸª Î” Î½ âŸ«) â†’ is-embedding (Î³ x)
 Î³-is-embedding x = âˆ˜-is-embedding
                     (Î¹-is-embedding (A x))
                     (equivs-are-embeddings _ (âŒœâŒâ»Â¹-is-equiv (Ï• x)))

 Î¹-fiber-point : (x : âŸª Î” Î½ âŸ«) â†’ fiber (Î¹ Î½) (Î¹ Î½ x)
 Î¹-fiber-point x = (x , refl)

 notice-that : (x : âŸª Î” Î½ âŸ«) (y : âŸª Î” (A x) âŸ«)
             â†’ Ï† x (Î³ x y) â‰¡ Î³ x y (Î¹-fiber-point x)
 notice-that x y = refl

 Î¹-Î³-lemma : (x : âŸª Î” Î½ âŸ«) (y : âŸª Î” (A x) âŸ«)
           â†’ Î¹ (A x) y â‰¡ Ï† x (Î³ x y)
 Î¹-Î³-lemma x y =
  Î¹ (A x) y               â‰¡âŸ¨ (inverses-are-sections (Ï† x)
                               (âŒœâŒ-is-equiv (Ï• x)) (Î¹ (A x) y))â»Â¹ âŸ©
  Ï† x (Ï†â»Â¹ x (Î¹ (A x) y)) â‰¡âŸ¨ refl âŸ©
  Ï† x (Î³ x y)             âˆŽ

 isolated-Î³-gives-isolated-Î¹ : (x : âŸª Î” Î½ âŸ«) (y : âŸª Î” (A x) âŸ«)
                             â†’ is-isolated (Î³ x y) â†’ is-isolated (Î¹ (A x) y)
 isolated-Î³-gives-isolated-Î¹ x y i = iii
   where
    ii : is-isolated (Ï† x (Î³ x y))
    ii = equivs-preserve-isolatedness (Ï† x) (âŒœâŒ-is-equiv (Ï• x)) (Î³ x y) i

    iii : is-isolated (Î¹ (A x) y)
    iii = transport is-isolated ((Î¹-Î³-lemma x y)â»Â¹) ii

Îš âŒœðŸ™âŒ         = ðŸ™áµ’
Îš âŒœÏ‰+ðŸ™âŒ       = â„•âˆžáµ’
Îš (Î½â‚€ âŒœ+âŒ Î½â‚) = Îš Î½â‚€ +áµ’ Îš Î½â‚
Îš (Î½â‚€ âŒœÃ—âŒ Î½â‚) = Îš Î½â‚€ Ã—áµ’ Îš Î½â‚
Îš (âŒœÎ£âŒ Î½ A)   = âˆ‘ (Îš Î½) (ð“š Î½ A)

Î¹ âŒœðŸ™âŒ         = id
Î¹ âŒœÏ‰+ðŸ™âŒ       = Î¹ðŸ™
Î¹ (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun id (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
Î¹ (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun (Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚)
Î¹ (âŒœÎ£âŒ Î½ A)   = pair-fun (Î¹ Î½) Î³
 where
  open Îš-extension Î½ A

Î¹-is-embedding âŒœðŸ™âŒ         = id-is-embedding
Î¹-is-embedding âŒœÏ‰+ðŸ™âŒ       = Î¹ðŸ™-is-embedding feâ‚€
Î¹-is-embedding (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-is-embedding
                              id
                              (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                              id-is-embedding
                              (dep-cases (Î» _ â†’ Î¹-is-embedding Î½â‚€)
                                         (Î» _ â†’ Î¹-is-embedding Î½â‚))
Î¹-is-embedding (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-embedding _ _
                              (Î¹-is-embedding Î½â‚€)
                              (Î» _ â†’ Î¹-is-embedding Î½â‚)
Î¹-is-embedding (âŒœÎ£âŒ Î½ A)   = pair-fun-is-embedding _ _
                              (Î¹-is-embedding Î½)
                              Î³-is-embedding
 where
  open Îš-extension Î½ A

\end{code}

This completes the definitions of Îš, Î¹ and Î¹-is-embedding.

The important fact about the Îš interpretation is that the ordinals in
its image have the least element property for decidable subsets, and,
in particular, they are compact.

\begin{code}

module _ (pe : propext ð“¤â‚€) where

 K-has-least-element-property : (Î½ : E)
                              â†’ has-least-element-property (Îš Î½)
 ð“š-has-least-element-property : (Î½ : E) (A : âŸª Î” Î½ âŸ« â†’ E) (x : âŸª Îš Î½ âŸ«)
                              â†’ has-least-element-property (ð“š Î½ A x)

 K-has-least-element-property âŒœðŸ™âŒ         = ðŸ™áµ’-has-least-element-property
 K-has-least-element-property âŒœÏ‰+ðŸ™âŒ       = â„•âˆžáµ’-has-least-element-property pe
 K-has-least-element-property (Î½â‚€ âŒœ+âŒ Î½â‚) =
   âˆ‘-has-least-element-property pe
     ðŸšáµ’
     (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
     ðŸšáµ’-has-least-element-property
     (dep-cases (Î» _ â†’ K-has-least-element-property Î½â‚€)
                (Î» _ â†’ K-has-least-element-property Î½â‚))
 K-has-least-element-property (Î½â‚€ âŒœÃ—âŒ Î½â‚) =
   âˆ‘-has-least-element-property pe
     (Îš Î½â‚€)
     (Î» _ â†’ Îš Î½â‚)
     (K-has-least-element-property Î½â‚€)
     (Î» _ â†’ K-has-least-element-property Î½â‚)
 K-has-least-element-property (âŒœÎ£âŒ Î½ A)   =
   âˆ‘-has-least-element-property pe (Îš Î½) (ð“š Î½ A)
     (K-has-least-element-property Î½)
     (ð“š-has-least-element-property Î½ A)

 ð“š-has-least-element-property Î½ A x =
   prop-inf-tychonoff
    (Î¹-is-embedding Î½ x)
    (Î» {(x , _)} y z â†’ y â‰ºâŸª Îš (A x) âŸ« z)
    (Î» (x , _) â†’ K-has-least-element-property (A x))

 Îš-Compact : {ð“¥ : Universe} (Î½ : E) â†’ Compact âŸª Îš Î½ âŸ« {ð“¥}
 Îš-Compact Î½ = has-least-gives-Compact _ (K-has-least-element-property Î½)

 ð“š-Compact : {ð“¥ : Universe} (Î½ : E) (A : âŸª Î” Î½ âŸ« â†’ E) (x : âŸª Îš Î½ âŸ«)
            â†’ Compact âŸª ð“š Î½ A x âŸ« {ð“¥}
 ð“š-Compact Î½ A x = has-least-gives-Compact _ (ð“š-has-least-element-property Î½ A x)

\end{code}

The embedding of the Î” interpretation into the Îš interpretation is
order-preserving, order-reflecting, and dense (its image has empty
complement):

\begin{code}

Î¹-is-order-preserving : (Î½ : E) (x y : âŸª Î” Î½ âŸ«)
                      â†’     x â‰ºâŸª Î” Î½ âŸ«     y
                      â†’ Î¹ Î½ x â‰ºâŸª Îš Î½ âŸ« Î¹ Î½ y
Î¹-is-order-preserving âŒœðŸ™âŒ         = Î» x y l â†’ l
Î¹-is-order-preserving âŒœÏ‰+ðŸ™âŒ       = Î¹ðŸ™áµ’-is-order-preserving
Î¹-is-order-preserving (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-is-order-preserving
                                     ðŸšáµ’
                                     ðŸšáµ’
                                     (cases (Î» _ â†’ Î” Î½â‚€) (Î» _ â†’ Î” Î½â‚))
                                     (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
                                     id
                                     (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                                     (Î» x y l â†’ l)
                                     (dep-cases (Î» _ â†’ Î¹-is-order-preserving Î½â‚€)
                                                (Î» _ â†’ Î¹-is-order-preserving Î½â‚))
Î¹-is-order-preserving (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-order-preserving
                                     (Î” Î½â‚€)
                                     (Îš Î½â‚€)
                                     (Î» _ â†’ Î” Î½â‚)
                                     (Î» _ â†’ Îš Î½â‚)
                                     (Î¹ Î½â‚€)
                                     (Î» _ â†’ Î¹ Î½â‚)
                                     (Î¹-is-order-preserving Î½â‚€)
                                     (Î» _ â†’ Î¹-is-order-preserving Î½â‚)
Î¹-is-order-preserving (âŒœÎ£âŒ Î½ A)   = pair-fun-is-order-preserving
                                     (Î” Î½)
                                     (Îš Î½)
                                     (Î” âˆ˜ A)
                                     (ð“š Î½ A)
                                     (Î¹ Î½)
                                     Î³
                                     (Î¹-is-order-preserving Î½)
                                     g
 where
  open Îš-extension Î½ A

  IH : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
     â†’         y â‰ºâŸª Î” (A x) âŸ« z
     â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
  IH x = Î¹-is-order-preserving (A x)

  f : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ«        Î¹ (A x) z
    â†’     Î³ x y â‰ºâŸª ð“š Î½ A (Î¹ Î½ x) âŸ«     Î³ x z
  f x y z l = (Î¹-fiber-point x ,
              transportâ‚‚ (Î» j k â†’ j â‰ºâŸª Îš (A x) âŸ« k)
               (Î¹-Î³-lemma x y)
               (Î¹-Î³-lemma x z)
              l)

  g : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’     y â‰ºâŸª Î” (A x) âŸ«            z
    â†’ Î³ x y â‰ºâŸª ð“š Î½ A (Î¹ Î½ x) âŸ« Î³ x z
  g x y z l = f x y z (IH x y z l)


Î¹-is-order-reflecting : (Î½ : E) (x y : âŸª Î” Î½ âŸ«)
                      â†’ Î¹ Î½ x â‰ºâŸª Îš Î½ âŸ« Î¹ Î½ y
                      â†’     x â‰ºâŸª Î” Î½ âŸ«     y
Î¹-is-order-reflecting âŒœðŸ™âŒ        = Î» x y l â†’ l
Î¹-is-order-reflecting âŒœÏ‰+ðŸ™âŒ      = Î¹ðŸ™áµ’-is-order-reflecting
Î¹-is-order-reflecting (Î½â‚€ âŒœ+âŒ Î½â‚) =  pair-fun-is-order-reflecting
                                      ðŸšáµ’
                                      ðŸšáµ’
                                      (cases (Î» _ â†’ Î” Î½â‚€) (Î» _ â†’ Î” Î½â‚))
                                      (cases (Î» _ â†’ Îš Î½â‚€) (Î» _ â†’ Îš Î½â‚))
                                      id
                                      (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                                      (Î» x y l â†’ l)
                                      id-is-embedding
                                      (dep-cases (Î» _ â†’ Î¹-is-order-reflecting Î½â‚€)
                                                 (Î» _ â†’ Î¹-is-order-reflecting Î½â‚))
Î¹-is-order-reflecting (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-order-reflecting
                                     (Î” Î½â‚€)
                                     (Îš Î½â‚€)
                                     (Î» _ â†’ Î” Î½â‚)
                                     (Î» _ â†’ Îš Î½â‚)
                                     (Î¹ Î½â‚€)
                                     (Î» _ â†’ Î¹ Î½â‚)
                                     (Î¹-is-order-reflecting Î½â‚€)
                                     (Î¹-is-embedding Î½â‚€)
                                     (Î» _ â†’ Î¹-is-order-reflecting Î½â‚)
Î¹-is-order-reflecting (âŒœÎ£âŒ Î½ A)  = pair-fun-is-order-reflecting
                                    (Î” Î½)
                                    (Îš Î½)
                                    (Î” âˆ˜ A)
                                    (ð“š Î½ A)
                                    (Î¹ Î½)
                                    Î³
                                    (Î¹-is-order-reflecting Î½)
                                    (Î¹-is-embedding Î½)
                                    g
 where
  open Îš-extension Î½ A

  IH : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
     â†’ Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
     â†’         y â‰ºâŸª Î” (A x) âŸ«         z
  IH x = Î¹-is-order-reflecting (A x)

  f : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’     Î³ x y â‰ºâŸª ð“š Î½ A (Î¹ Î½ x) âŸ«    Î³ x z
    â†’ Î¹ (A x) y â‰ºâŸª Îš (A x)   âŸ«     Î¹ (A x) z
  f x y z (w , l) = n
   where
    q : w â‰¡ Î¹-fiber-point x
    q = Î¹-is-embedding Î½ (Î¹ Î½ x) _ _

    m : Î³ x y (Î¹-fiber-point x) â‰ºâŸª Îš (A x) âŸ«  Î³ x z (Î¹-fiber-point x)
    m = transport (Î» (x' , p) â†’ Î³ x y (x' , p) â‰ºâŸª Îš (A x') âŸ« Î³ x z (x' , p)) q l

    n : Î¹ (A x) y â‰ºâŸª Îš (A x) âŸ« Î¹ (A x) z
    n = transportâ‚‚ (Î» u v â†’ u â‰ºâŸª Îš (A x) âŸ« v)
         ((Î¹-Î³-lemma x y)â»Â¹)
         ((Î¹-Î³-lemma x z)â»Â¹)
         m

  g : (x : âŸª Î” Î½ âŸ«) (y z : âŸª Î” (A x) âŸ«)
    â†’ Î³ x y â‰ºâŸª ð“š Î½ A (Î¹ Î½ x) âŸ« Î³ x z
    â†’     y â‰ºâŸª Î” (A x)   âŸ«     z
  g x y z l = IH x y z (f x y z l)

Î¹-is-dense : (Î½ : E) â†’ is-dense (Î¹ Î½)
Î¹-is-dense âŒœðŸ™âŒ         = id-is-dense
Î¹-is-dense âŒœÏ‰+ðŸ™âŒ       = Î¹ðŸ™-dense feâ‚€
Î¹-is-dense (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-dense
                          id
                          (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                          id-is-dense
                          (dep-cases (Î» _ â†’ Î¹-is-dense Î½â‚€) (Î» _ â†’ Î¹-is-dense Î½â‚))
Î¹-is-dense (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-dense _ _
                          (Î¹-is-dense Î½â‚€)
                          (Î» _ â†’ Î¹-is-dense Î½â‚)
Î¹-is-dense (âŒœÎ£âŒ Î½ A)   = pair-fun-dense
                          (Î¹ Î½)
                          Î³
                          (Î¹-is-dense Î½)
                          (Î» x â†’ comp-is-dense
                                  (Î¹-is-dense (A x))
                                  (equivs-are-dense
                                    (Ï†â»Â¹ x)
                                    (inverses-are-equivs (Ï† x) (âŒœâŒ-is-equiv (Ï• x)))))
 where
  open Îš-extension Î½ A

\end{code}

We define limit points as follows:

\begin{code}

private
 recall-notion-of-isolatedness  : {X : ð“¤ Ì‡ } (x : X)
                                â†’ is-isolated x â‰¡ ((y : X) â†’ decidable (x â‰¡ y))
 recall-notion-of-isolatedness x = refl

is-limit-point : {X : ð“¤ Ì‡ } â†’ X â†’ ð“¤ Ì‡
is-limit-point x = is-isolated x â†’ WLPO

\end{code}

The characteristic function of limit points:

\begin{code}

â„“ : (Î½ : E) â†’ âŸª Î” Î½ âŸ« â†’ ðŸš
â„“ âŒœðŸ™âŒ         â‹†            = â‚€
â„“ âŒœÏ‰+ðŸ™âŒ       (inl n)      = â‚€
â„“ âŒœÏ‰+ðŸ™âŒ       (inr â‹†)      = â‚
â„“ (Î½â‚€ âŒœ+âŒ Î½â‚) (inl â‹† , xâ‚€) = â„“ Î½â‚€ xâ‚€
â„“ (Î½â‚€ âŒœ+âŒ Î½â‚) (inr â‹† , xâ‚) = â„“ Î½â‚ xâ‚
â„“ (Î½â‚€ âŒœÃ—âŒ Î½â‚) (xâ‚€ , xâ‚)    = maxðŸš (â„“ Î½â‚€ xâ‚€) (â„“ Î½â‚ xâ‚)
â„“ (âŒœÎ£âŒ Î½ A)   (x  , y)     = maxðŸš (â„“ Î½ x) (â„“ (A x) y)

\end{code}

Non-limit points are isolated in the Îš interpretation:

\begin{code}

â„“-isolated : (Î½ : E) (x : âŸª Î” Î½ âŸ«) â†’ â„“ Î½ x â‰¡ â‚€ â†’ is-isolated (Î¹ Î½ x)
â„“-isolated âŒœðŸ™âŒ         â‹†            p    = ðŸ™-is-discrete â‹†
â„“-isolated âŒœÏ‰+ðŸ™âŒ       (inl n)      refl = finite-isolated feâ‚€ n
â„“-isolated (Î½â‚€ âŒœ+âŒ Î½â‚) (inl â‹† , xâ‚€) p    = Î£-isolated
                                            (inl-is-isolated â‹† (ðŸ™-is-discrete â‹†))
                                            (â„“-isolated Î½â‚€ xâ‚€ p)
â„“-isolated (Î½â‚€ âŒœ+âŒ Î½â‚) (inr â‹† , xâ‚) p    = Î£-isolated
                                            (inr-is-isolated â‹† (ðŸ™-is-discrete â‹†))
                                            (â„“-isolated Î½â‚ xâ‚ p)
â„“-isolated (Î½â‚€ âŒœÃ—âŒ Î½â‚) (xâ‚€ , xâ‚)    p    = Î£-isolated
                                            (â„“-isolated Î½â‚€ xâ‚€ (maxðŸš-â‚€-left p))
                                            (â„“-isolated Î½â‚ xâ‚ (maxðŸš-â‚€-right p))
â„“-isolated (âŒœÎ£âŒ Î½ A)   (x , y)      p    = iv
 where
  open Îš-extension Î½ A

  i : is-isolated (Î¹ Î½ x)
  i = â„“-isolated Î½ x (maxðŸš-â‚€-left p)

  ii : is-isolated (Î¹ (A x) y)
  ii = â„“-isolated (A x) y (maxðŸš-â‚€-right p)

  iii : is-isolated (Î³ x y)
  iii = equivs-preserve-isolatedness (Ï†â»Â¹ x) (âŒœâŒâ»Â¹-is-equiv (Ï• x)) (Î¹ (A x) y) ii

  iv : is-isolated (Î¹ Î½ x , Î³ x y)
  iv = Î£-isolated i iii

\end{code}

The function â„“ really does detect limit points:

\begin{code}

module _ (pe : propext ð“¤â‚€) where

 â„“-limit : (Î½ : E) (x : âŸª Î” Î½ âŸ«) â†’ â„“ Î½ x â‰¡ â‚ â†’ is-limit-point (Î¹ Î½ x)
 â„“-limit âŒœÏ‰+ðŸ™âŒ       (inr â‹†)      p i = is-isolated-gives-is-isolated' âˆž i
 â„“-limit (Î½â‚€ âŒœ+âŒ Î½â‚) (inl â‹† , xâ‚€) p i = â„“-limit Î½â‚€ xâ‚€ p
                                         (Î£-isolated-right
                                           (underlying-type-is-setáµ€ fe ðŸšáµ’) i)
 â„“-limit (Î½â‚€ âŒœ+âŒ Î½â‚) (inr â‹† , xâ‚) p i = â„“-limit Î½â‚ xâ‚ p
                                         (Î£-isolated-right
                                           (underlying-type-is-setáµ€ fe ðŸšáµ’) i)
 â„“-limit (Î½â‚€ âŒœÃ—âŒ Î½â‚) (xâ‚€ , xâ‚)    p i =
   Cases (maxðŸš-lemma p)
    (Î» (pâ‚€ : â„“ Î½â‚€ xâ‚€ â‰¡ â‚) â†’ â„“-limit Î½â‚€ xâ‚€ pâ‚€ (Ã—-isolated-left i))
    (Î» (pâ‚ : â„“ Î½â‚ xâ‚ â‰¡ â‚) â†’ â„“-limit Î½â‚ xâ‚ pâ‚ (Ã—-isolated-right i))
 â„“-limit (âŒœÎ£âŒ Î½ A)   (x , y)      p i =
   Cases (maxðŸš-lemma p)
    (Î» (pâ‚€ : â„“ Î½ x â‰¡ â‚)
           â†’ â„“-limit Î½ x pâ‚€ (Î£-isolated-left (ð“š-Compact pe Î½ A) i))
    (Î» (pâ‚ : â„“ (A x) y â‰¡ â‚)
           â†’ â„“-limit (A x) y pâ‚
              (isolated-Î³-gives-isolated-Î¹ x y
                (Î£-isolated-right
                  (underlying-type-is-setáµ€ fe (Îš Î½)) i)))
  where
   open Îš-extension Î½ A

 isolatedness-decision : (Î½ : E) (x : âŸª Î” Î½ âŸ«)
                       â†’ is-isolated (Î¹ Î½ x) + is-limit-point (Î¹ Î½ x)
 isolatedness-decision Î½ x = ðŸš-equality-cases
                              (Î» (p : â„“ Î½ x â‰¡ â‚€) â†’ inl (â„“-isolated Î½ x p))
                              (Î» (p : â„“ Î½ x â‰¡ â‚) â†’ inr (â„“-limit Î½ x p))

 isolatedness-decision' : Â¬ WLPO â†’ (Î½ : E) (x : âŸª Î” Î½ âŸ«)
                        â†’ decidable (is-isolated (Î¹ Î½ x))
 isolatedness-decision' f Î½ x =
   Cases (isolatedness-decision Î½ x)
    inl
    (Î» (g : is-isolated (Î¹ Î½ x) â†’ WLPO)  â†’ inr (contrapositive g f))

\end{code}

We conclude with some impossibility results.

\begin{code}

Î¹-is-equiv-gives-LPO : ((Î½ : E) â†’ is-equiv (Î¹ Î½)) â†’ LPO
Î¹-is-equiv-gives-LPO f = Î¹ðŸ™-is-equiv-gives-LPO (f âŒœÏ‰+ðŸ™âŒ)

LPO-gives-Î¹-is-equiv : LPO â†’ (Î½ : E) â†’ is-equiv (Î¹ Î½)
LPO-gives-Î¹-is-equiv lpo âŒœðŸ™âŒ         = id-is-equiv ðŸ™
LPO-gives-Î¹-is-equiv lpo âŒœÏ‰+ðŸ™âŒ       = LPO-gives-Î¹ðŸ™-is-equiv lpo
LPO-gives-Î¹-is-equiv lpo (Î½â‚€ âŒœ+âŒ Î½â‚) = pair-fun-is-equiv
                                          id
                                          (dep-cases (Î» _ â†’ Î¹ Î½â‚€) (Î» _ â†’ Î¹ Î½â‚))
                                          (id-is-equiv (ðŸ™ + ðŸ™))
                                          (dep-cases
                                            (Î» _ â†’ LPO-gives-Î¹-is-equiv lpo Î½â‚€)
                                            (Î» _ â†’ LPO-gives-Î¹-is-equiv lpo Î½â‚))
LPO-gives-Î¹-is-equiv lpo (Î½â‚€ âŒœÃ—âŒ Î½â‚) = pair-fun-is-equiv _ _
                                          (LPO-gives-Î¹-is-equiv lpo Î½â‚€)
                                          (Î» _ â†’ LPO-gives-Î¹-is-equiv lpo Î½â‚)
LPO-gives-Î¹-is-equiv lpo (âŒœÎ£âŒ Î½ A)   = pair-fun-is-equiv
                                          (Î¹ Î½)
                                          Î³
                                          (LPO-gives-Î¹-is-equiv lpo Î½)
                                          (Î» x â†’ âˆ˜-is-equiv
                                                  (LPO-gives-Î¹-is-equiv lpo (A x))
                                                  (âŒœâŒâ»Â¹-is-equiv (Ï• x)))
 where
  open Îš-extension Î½ A

Î¹-is-equiv-iff-LPO : ((Î½ : E) â†’ is-equiv (Î¹ Î½)) â‡” LPO
Î¹-is-equiv-iff-LPO = Î¹-is-equiv-gives-LPO , LPO-gives-Î¹-is-equiv

\end{code}

We also have the following:

\begin{code}

Î¹-has-section-gives-Îš-discrete : (Î½ : E) â†’ has-section (Î¹ Î½) â†’ is-discrete âŸª Îš Î½ âŸ«
Î¹-has-section-gives-Îš-discrete Î½ (Î¸ , Î¹Î¸) = lc-maps-reflect-discreteness Î¸
                                              (sections-are-lc Î¸ (Î¹ Î½ , Î¹Î¸))
                                              (Î”-is-discrete Î½)

Î¹-is-equiv-gives-Îš-discrete : (Î½ : E) â†’ is-equiv (Î¹ Î½) â†’ is-discrete âŸª Îš Î½ âŸ«
Î¹-is-equiv-gives-Îš-discrete Î½ e = Î¹-has-section-gives-Îš-discrete Î½
                                   (equivs-have-sections (Î¹ Î½) e)

LPO-gives-Îš-discrete : LPO â†’ (Î½ : E) â†’ is-discrete âŸª Îš Î½ âŸ«
LPO-gives-Îš-discrete lpo Î½ = Î¹-is-equiv-gives-Îš-discrete Î½
                              (LPO-gives-Î¹-is-equiv lpo Î½)

Îš-discrete-gives-WLPO : ((Î½ : E) â†’ is-discrete âŸª Îš Î½ âŸ«) â†’ WLPO
Îš-discrete-gives-WLPO f = â„•âˆž-discrete-gives-WLPO (f âŒœÏ‰+ðŸ™âŒ)

\end{code}

TODO. Can we close the gap between the last two facts? The difficulty
that arises here is similar to the following.

Let P be a proposition and assume function extensionality.

(0) If P is decidable, then the function type (P â†’ ðŸš) has decidable equality.

(1) If (P â†’ ðŸš) has decidable equality, then Â¬ P is decidable.

It doesn't seem to be possible to reverse any of the implications (0)
and (1), so that the proposition "(P -> 2) has decidable equality"
seems to be strictly between "P is decidable" and "Â¬P is decidable".
