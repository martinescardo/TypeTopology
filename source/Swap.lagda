Martin Escardo, November 2019.

The swap automorphism.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module Swap where

open import SpartanMLTT
open import Plus-Properties
open import DiscreteAndSeparated
open import UF-Equiv
open import UF-Miscelanea

\end{code}

We change the value of one isolated argument of a function, and no
other value, with patch. Recall that a point x:X is called isolated if
x=y is decidable for all y:X.

\begin{code}

module _ {ğ“¤ ğ“¥} {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (a : X) (b : Y) (i : is-isolated a) (f : X â†’ Y) where

 private
  Ï† : (x : X) â†’ (a â‰¡ x) + (a â‰¢ x) â†’ Y
  Ï† x (inl p) = b
  Ï† x (inr u) = f x

  f' : X â†’ Y
  f' x = Ï† x (i x)

  Î³ : (z : (a â‰¡ a) + (a â‰¢ a)) â†’ i a â‰¡ z â†’ Ï† a z â‰¡ b
  Î³ (inl p) q = refl
  Î³ (inr u) q = ğŸ˜-elim (u refl)

  Î´ : (x : X) (u : a â‰¢ x) (z : (a â‰¡ x) + (a â‰¢ x)) â†’ i x â‰¡ z â†’ Ï† x z â‰¡ f x
  Î´ x u (inl p) q = ğŸ˜-elim (u p)
  Î´ x u (inr v) q = refl

 patch : X â†’ Y
 patch = f'

 patch-equationâ‚€ : f' a â‰¡ b
 patch-equationâ‚€ = Î³ (i a) refl

 patch-equationâ‚ : (x : X) â†’ a â‰¢ x â†’ f' x â‰¡ f x
 patch-equationâ‚ x u = Î´ x u (i x) refl

\end{code}

The (involutive) swap automorphism is obtained by patching the
identity function twice:

\begin{code}

swap : {X : ğ“¤ Ì‡ } (a b : X) â†’ is-isolated a â†’ is-isolated b â†’ X â†’ X
swap a b i j = patch a b i (patch b a j id)

swap-equationâ‚€ : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
               â†’ swap a b i j a â‰¡ b
swap-equationâ‚€ a b i j = patch-equationâ‚€ a b i (patch b a j id)

swap-equationâ‚ : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
               â†’ swap a b i j b â‰¡ a
swap-equationâ‚ a b i j = Î³ (j a)
 where
  Î³ : (b â‰¡ a) + (b â‰¢ a) â†’ swap a b i j b â‰¡ a
  Î³ (inl r) =
      swap a b i j b â‰¡âŸ¨ ap (swap a b i j) r âŸ©
      swap a b i j a â‰¡âŸ¨ swap-equationâ‚€ a b i j âŸ©
      b              â‰¡âŸ¨ r âŸ©
      a              âˆ
  Î³ (inr n) =
      swap a b i j b                 â‰¡âŸ¨ refl   âŸ©
      patch a b i (patch b a j id) b â‰¡âŸ¨ patch-equationâ‚ a b i (patch b a j id) b (â‰¢-sym n) âŸ©
      patch b a j id b               â‰¡âŸ¨ patch-equationâ‚€ b a j id âŸ©
      a                              âˆ

swap-equationâ‚‚ : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
               â†’ (x : X) â†’ a â‰¢ x â†’ b â‰¢ x â†’ swap a b i j x â‰¡ x
swap-equationâ‚‚ a b i j x m n =
  swap a b i j x                 â‰¡âŸ¨ refl âŸ©
  patch a b i (patch b a j id) x â‰¡âŸ¨ patch-equationâ‚ a b i (patch b a j id) x m âŸ©
  patch b a j id x               â‰¡âŸ¨ patch-equationâ‚ b a j id x n âŸ©
  x                              âˆ

swap-symmetric : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
               â†’ swap a b i j âˆ¼ swap b a j i
swap-symmetric a b i j x = Î³ (i x) (j x)
 where
  Î³ : (a â‰¡ x) + (a â‰¢ x) â†’ (b â‰¡ x) + (b â‰¢ x) â†’ swap a b i j x â‰¡ swap b a j i x
  Î³ (inl p) _ =
    swap a b i j x â‰¡âŸ¨ ap (swap a b i j) (p â»Â¹) âŸ©
    swap a b i j a â‰¡âŸ¨ swap-equationâ‚€ a b i j âŸ©
    b              â‰¡âŸ¨ (swap-equationâ‚ b a j i)â»Â¹ âŸ©
    swap b a j i a â‰¡âŸ¨ ap (swap b a j i) p âŸ©
    swap b a j i x âˆ
  Î³ (inr _) (inl q) =
    swap a b i j x â‰¡âŸ¨ ap (swap a b i j) (q â»Â¹) âŸ©
    swap a b i j b â‰¡âŸ¨ swap-equationâ‚ a b i j âŸ©
    a              â‰¡âŸ¨ (swap-equationâ‚€ b a j i)â»Â¹ âŸ©
    swap b a j i b â‰¡âŸ¨ ap (swap b a j i) q âŸ©
    swap b a j i x âˆ
  Î³ (inr m) (inr n) =
    swap a b i j x â‰¡âŸ¨ swap-equationâ‚‚ a b i j x m n âŸ©
    x              â‰¡âŸ¨ (swap-equationâ‚‚ b a j i x n m)â»Â¹ âŸ©
    swap b a j i x âˆ

swap-involutive : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
                â†’ swap a b i j âˆ˜ swap a b i j âˆ¼ id
swap-involutive a b i j x = Î³ (i x) (j x)
 where
  Î³ : (a â‰¡ x) + (a â‰¢ x) â†’ (b â‰¡ x) + (b â‰¢ x) â†’ swap a b i j (swap a b i j x) â‰¡ x
  Î³ (inl p) _ =
    swap a b i j (swap a b i j x) â‰¡âŸ¨ ap (Î» - â†’ swap a b i j (swap a b i j -)) (p â»Â¹) âŸ©
    swap a b i j (swap a b i j a) â‰¡âŸ¨ ap (swap a b i j) (swap-equationâ‚€ a b i j) âŸ©
    swap a b i j b                â‰¡âŸ¨ swap-equationâ‚ a b i j âŸ©
    a                             â‰¡âŸ¨ p    âŸ©
    x                             âˆ
  Î³ (inr _) (inl q) =
    swap a b i j (swap a b i j x) â‰¡âŸ¨ ap (Î» - â†’ swap a b i j (swap a b i j -)) (q â»Â¹) âŸ©
    swap a b i j (swap a b i j b) â‰¡âŸ¨ ap (swap a b i j) (swap-equationâ‚ a b i j) âŸ©
    swap a b i j a                â‰¡âŸ¨ swap-equationâ‚€ a b i j âŸ©
    b                             â‰¡âŸ¨ q    âŸ©
    x                             âˆ
  Î³ (inr m) (inr n) =
    swap a b i j (swap a b i j x) â‰¡âŸ¨ ap (swap a b i j) (swap-equationâ‚‚ a b i j x m n) âŸ©
    swap a b i j x                â‰¡âŸ¨ swap-equationâ‚‚ a b i j x m n âŸ©
    x                             âˆ

swap-is-equiv : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b)
              â†’ is-equiv (swap a b i j)
swap-is-equiv a b i j = qinvs-are-equivs
                         (swap a b i j)
                         (swap a b i j , (swap-involutive a b i j , swap-involutive a b i j))

â‰ƒ-swap : {X : ğ“¤ Ì‡ } (a b : X) (i : is-isolated a) (j : is-isolated b) â†’ X â‰ƒ X
â‰ƒ-swap a b i j = swap a b i j , swap-is-equiv a b i j

\end{code}
