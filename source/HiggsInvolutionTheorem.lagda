Martin Escardo, 15 August 2014, with additions 23 January 2021.

Higgs' Involution Theorem. In any topos, if f : Î© â†’ Î© is a
monomorphism, then it is an involution.

We adapt and prove the result in univalent mathematics, using
propositional and functional extensionality. (We don't rely on
propositional resizing (or impredicativity).)

There is a proof by diagram chasing with iterated pullbacks, in page
65 of Johnstone's Sketches of an Elephant, volume 1.

The proof given here is based on an exercise in page 160 of Lambek and
Scott's Introduction to Higher-Order Categorical Logic, attributed to
Scedrov. Thanks to Phil Scott for bringing my attention to this proof
during a visit to Birmingham.

Added 23 Jan 2021. From a group structure on Î© we get excluded middle,
as an application of Higgs Theorem. This doesn't seems to be known in
the topos theory community.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons renaming (Î© to Î©' ; âŠ¤Î© to âŠ¤ ; âŠ¥Î© to âŠ¥)
open import UF-FunExt
open import UF-Subsingletons-FunExt

\end{code}

We work with a universe ğ“¤ and assume functional and propositional
extensionality:

\begin{code}

module HiggsInvolutionTheorem
        {ğ“¤ : Universe}
        (fe : Fun-Ext)
        (pe : propext ğ“¤)
       where

\end{code}

We work with Î© of universe ğ“¤:

\begin{code}

Î© = Î©' ğ“¤

\end{code}

Recall that a map f is left-cancellable if f p â‰¡ f q â†’ p â‰¡ q, and
involutive if f (f p) â‰¡ p.

\begin{code}

higgs : (f : Î© â†’ Î©) â†’ left-cancellable f â†’ involutive f
higgs f lc = VIII
  where
   I : (p : Î©) â†’ f p â‰¡ âŠ¤ â†’ p â‰¡ âŠ¤ â†’ f âŠ¤ â‰¡ âŠ¤
   I p r s = transport (Î» - â†’ f - â‰¡ âŠ¤) s r

   II : (p : Î©) â†’ f p â‰¡ âŠ¤ â†’ f âŠ¤ â‰¡ âŠ¤ â†’ p â‰¡ âŠ¤
   II p r s = lc (f p â‰¡âŸ¨ r âŸ©
                  âŠ¤   â‰¡âŸ¨ s â»Â¹ âŸ©
                  f âŠ¤ âˆ)

   III : (p : Î©) â†’ f p â‰¡ âŠ¤ â†’ p â‰¡ f âŠ¤
   III p r = Î©-ext pe fe (I p r) (II p r)

   IV : (p : Î©) â†’ f (f p) â‰¡ âŠ¤ â†’ p â‰¡ âŠ¤
   IV p r = lc (III (f p) r)

   V : (p : Î©) â†’ f (f (f p)) â‰¡ âŠ¤ â†’ f p â‰¡ âŠ¤
   V p = IV (f p)

   VI : (p : Î©) â†’ f p â‰¡ âŠ¤ â†’ f (f (f p)) â‰¡ âŠ¤
   VI p r = iv âˆ™ r
    where
     i : f (f p) â‰¡ f âŠ¤
     i = ap f r

     ii : f âŠ¤ â‰¡ p
     ii = (III p r)â»Â¹

     iii : f (f p) â‰¡ p
     iii = i âˆ™ ii

     iv : f (f (f p)) â‰¡ f p
     iv = ap f iii

   VII : (p : Î©) â†’ f (f (f p)) â‰¡ f p
   VII p = Î©-ext pe fe (V p) (VI p)

   VIII : (p : Î©) â†’ f (f p) â‰¡ p
   VIII p = lc (VII p)

\end{code}

Added 23 Jan 2021. From a group structure on Î© we get excluded middle,
as an application of Higgs Theorem. This doesn't seems to be known in
the topos theory community. I've written a blog post about this here:

https://homotopytypetheory.org/2021/01/23/can-the-type-of-truth-values-be-given-the-structure-of-a-group/

Such a group structure is necessarily abelian.

Moreover, any left-cancellable monoid structure (_âŠ•_ , O) on Î© is an
abelian group structure with p âŠ• p = O for all p : Î©, that is, such
that every element is its own inverse.

To define negation on Î© we need function extensionality, which we are
assuming in this module. We introduce friendlier notation for it:

\begin{code}

â‡_ : Î© â†’ Î©
â‡_ = not fe

â‡â‡_ : Î© â†’ Î©
â‡â‡ p = â‡(â‡ p)

open import UF-ExcludedMiddle

lc-monoid-structure-on-Î©-gives-EM : (O : Î©)
                                    (_âŠ•_ : Î© â†’ Î© â†’ Î©)
                                  â†’ left-neutral O _âŠ•_
                                  â†’ right-neutral O _âŠ•_
                                  â†’ associative _âŠ•_
                                  â†’ ((p : Î©) â†’ left-cancellable (p âŠ•_))
                                  â†’ excluded-middle ğ“¤
lc-monoid-structure-on-Î©-gives-EM O _âŠ•_ left-neutral right-neutral assoc lc = Î³
 where
  invol : (p : Î©) â†’ involutive (p âŠ•_)
  invol p = higgs (p âŠ•_) (lc p)

  own-inv : (p : Î©) â†’ p âŠ• p â‰¡ O
  own-inv p = p âŠ• p       â‰¡âŸ¨ (right-neutral (p âŠ• p))â»Â¹ âŸ©
              (p âŠ• p) âŠ• O â‰¡âŸ¨ assoc p p O âŸ©
              p âŠ• (p âŠ• O) â‰¡âŸ¨ invol p O âŸ©
              O           âˆ

  to-â‰¡ : {p q : Î©} â†’ p âŠ• q â‰¡ O â†’ p â‰¡ q
  to-â‰¡ {p} {q} e = p           â‰¡âŸ¨ (right-neutral p)â»Â¹ âŸ©
                   p âŠ• O       â‰¡âŸ¨ ap (p âŠ•_) (e â»Â¹) âŸ©
                   p âŠ• (p âŠ• q) â‰¡âŸ¨ (assoc p p q)â»Â¹ âŸ©
                   (p âŠ• p) âŠ• q â‰¡âŸ¨ ap (_âŠ• q) (own-inv p) âŸ©
                   O âŠ• q       â‰¡âŸ¨ left-neutral q âŸ©
                   q           âˆ

  f : Î© â†’ Î©
  f p = p âŠ• (âŠ¥ âŠ• âŠ¤)

  f-invol : involutive f
  f-invol p = f (f p)                 â‰¡âŸ¨ refl âŸ©
              (p âŠ• (âŠ¥ âŠ• âŠ¤)) âŠ• (âŠ¥ âŠ• âŠ¤) â‰¡âŸ¨ assoc p (âŠ¥ âŠ• âŠ¤) (âŠ¥ âŠ• âŠ¤) âŸ©
              p âŠ• ((âŠ¥ âŠ• âŠ¤) âŠ• (âŠ¥ âŠ• âŠ¤)) â‰¡âŸ¨ ap (p âŠ•_) (own-inv (âŠ¥ âŠ• âŠ¤)) âŸ©
              p âŠ• O                   â‰¡âŸ¨ right-neutral p âŸ©
              p                       âˆ

  Î± : (p : Î©) â†’ f p â‰¡ âŠ¤ â†’ p â‰¡ âŠ¥
  Î± p e = to-â‰¡ (p âŠ• âŠ¥             â‰¡âŸ¨ (right-neutral (p âŠ• âŠ¥))â»Â¹ âŸ©
                (p âŠ• âŠ¥) âŠ• O       â‰¡âŸ¨ ap ((p âŠ• âŠ¥) âŠ•_) ((own-inv âŠ¤)â»Â¹) âŸ©
                (p âŠ• âŠ¥) âŠ• (âŠ¤ âŠ• âŠ¤) â‰¡âŸ¨ (assoc (p âŠ• âŠ¥) âŠ¤ âŠ¤)â»Â¹ âŸ©
                ((p âŠ• âŠ¥) âŠ• âŠ¤) âŠ• âŠ¤ â‰¡âŸ¨ ap (_âŠ• âŠ¤) (assoc p âŠ¥ âŠ¤) âŸ©
                (p âŠ• (âŠ¥ âŠ• âŠ¤)) âŠ• âŠ¤ â‰¡âŸ¨ refl âŸ©
                f p âŠ• âŠ¤           â‰¡âŸ¨ ap (_âŠ• âŠ¤) e âŸ©
                âŠ¤ âŠ• âŠ¤             â‰¡âŸ¨ own-inv âŠ¤ âŸ©
                O                 âˆ)

  Î² : (p : Î©) â†’ p â‰¡ âŠ¥ â†’ f p â‰¡ âŠ¤
  Î² p e = f p         â‰¡âŸ¨ refl âŸ©
          p âŠ• (âŠ¥ âŠ• âŠ¤) â‰¡âŸ¨ (assoc p âŠ¥ âŠ¤)â»Â¹ âŸ©
          (p âŠ• âŠ¥) âŠ• âŠ¤ â‰¡âŸ¨ ap (Î» - â†’ (- âŠ• âŠ¥) âŠ• âŠ¤) e âŸ©
          (âŠ¥ âŠ• âŠ¥) âŠ• âŠ¤ â‰¡âŸ¨ ap (_âŠ• âŠ¤) (own-inv âŠ¥) âŸ©
          O âŠ• âŠ¤       â‰¡âŸ¨ left-neutral âŠ¤ âŸ©
          âŠ¤           âˆ

  characterization-of-f : (p : Î©) â†’ f p â‰¡ â‡ p
  characterization-of-f p = Î©-ext pe fe a b
   where
    a : f p â‰¡ âŠ¤ â†’ (â‡ p) â‰¡ âŠ¤
    a e = equal-âŠ¥-gives-not-equal-âŠ¤ fe pe p (Î± p e)

    b : (â‡ p) â‰¡ âŠ¤ â†’ f p â‰¡ âŠ¤
    b e = Î² p (not-equal-âŠ¤-gives-equal-âŠ¥ fe pe p e)

  Î½ : (p : Î©) â†’ (â‡â‡ p) â‰¡ p
  Î½ p = â‡â‡ p      â‰¡âŸ¨ ap â‡_ ((characterization-of-f p)â»Â¹) âŸ©
        (â‡ (f p)) â‰¡âŸ¨ (characterization-of-f (f p))â»Â¹ âŸ©
        f (f p)   â‰¡âŸ¨ f-invol p âŸ©
        p         âˆ

  Î´ : (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ P
  Î´ P i = Idtofun (ap _holds (Î½ (P , i)))

  Î³ : excluded-middle ğ“¤
  Î³ = DNE-gives-EM fe Î´

\end{code}

Additional facts that are not needed to conclude excluded middle:

\begin{code}

  from-â‰¡ : (p q : Î©) â†’ p â‰¡ q â†’ p âŠ• q â‰¡ O
  from-â‰¡ p q e = p âŠ• q â‰¡âŸ¨ ap (_âŠ• q) e âŸ©
                 q âŠ• q â‰¡âŸ¨ own-inv q âŸ©
                 O     âˆ

  abelian : (p q : Î©) â†’ p âŠ• q â‰¡ q âŠ• p
  abelian p q = to-â‰¡ ((p âŠ• q) âŠ• (q âŠ• p) â‰¡âŸ¨ assoc p q (q âŠ• p) âŸ©
                      p âŠ• (q âŠ• (q âŠ• p)) â‰¡âŸ¨ ap (p âŠ•_) ((assoc q q p)â»Â¹) âŸ©
                      p âŠ• ((q âŠ• q) âŠ• p) â‰¡âŸ¨ ap (Î» - â†’ p âŠ• (- âŠ• p)) (own-inv q) âŸ©
                      p âŠ• (O âŠ• p)       â‰¡âŸ¨ ap (p âŠ•_) (left-neutral p) âŸ©
                      p âŠ• p             â‰¡âŸ¨ own-inv p âŸ©
                      O                 âˆ)

  characâ‚‚-of-f : (p : Î©) â†’ f p â‰¡ (âŠ¥ âŠ• âŠ¤) âŠ• p
  characâ‚‚-of-f p = abelian p (âŠ¥ âŠ• âŠ¤)

  f-invol' : involutive f
  f-invol' p = f (f p)                   â‰¡âŸ¨ characâ‚‚-of-f (f p) âŸ©
               ((âŠ¥ âŠ• âŠ¤) âŠ• f p)           â‰¡âŸ¨ ap ((âŠ¥ âŠ• âŠ¤) âŠ•_) (characâ‚‚-of-f p) âŸ©
               ((âŠ¥ âŠ• âŠ¤) âŠ• ((âŠ¥ âŠ• âŠ¤) âŠ• p)) â‰¡âŸ¨ higgs ((âŠ¥ âŠ• âŠ¤) âŠ•_) (lc (âŠ¥ âŠ• âŠ¤)) p âŸ©
               p âˆ

\end{code}

This shows that any cancellative monoid structure on Î© is
automatically an abelian group structure (which is not very surprising
given that we have already established excluded middle, but justifies
our additive notation).
