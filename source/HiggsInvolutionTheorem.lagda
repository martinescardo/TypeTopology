Martin Escardo, 15 August 2014.

Higgs' Involution Theorem. In any topos, if f : ฮฉ โ ฮฉ is a
monomorphism, then it is an involution.

We adapt and prove the result in univalent mathematics, using
propositional and functional extensionality. (We don't rely on
propositional resizing (or impredicativity).)

There is a proof by diagram chasing with iterated pullbacks, in page
65 of Johnstone's Sketches of an Elephant, volume 1.

The proof given here is based on an exercise in page 160 of Lambek and
Scott's Introduction to Higher-Order Categorical Logic, attributed to
Scedrov. Thanks to Phil Scott for bringing my attention to this proof.

TODO. Generalize from the type ฮฉ ๐คโ of propositions in the universe ๐คโ
to any universe ๐ค (easy).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons renaming (โคฮฉ to โค ; โฅฮฉ to โฅ)
open import UF-FunExt
open import UF-Subsingletons-FunExt

module HiggsInvolutionTheorem
        (fe : funext ๐คโ ๐คโ)
        (pe : propext ๐คโ)
       where

involutive : {X : ๐ค ฬ } โ (f : X โ X) โ ๐ค ฬ
involutive f = โ x โ f (f x) โก x

higgs : (f : ฮฉ ๐คโ โ ฮฉ ๐คโ) โ left-cancellable f โ involutive f
higgs f c = VIII
  where
   I : (p : ฮฉ ๐คโ) โ f p โก โค โ p โก โค โ f โค โก โค
   I p r s = transport (ฮป - โ f - โก โค) s r

   II : (p : ฮฉ ๐คโ) โ f p โก โค โ f โค โก โค โ p โก โค
   II p r s = c (r โ s โปยน)

   III : (p : ฮฉ ๐คโ) โ f p โก โค โ p โก f โค
   III p r = ฮฉ-ext' pe fe (I p r) (II p r)

   IV : (p : ฮฉ ๐คโ) โ f (f p) โก โค โ p โก โค
   IV p r = c (III (f p) r)

   V : (p : ฮฉ ๐คโ) โ f (f (f p)) โก โค โ f p โก โค
   V p = IV (f p)

   VI : (p : ฮฉ ๐คโ) โ f p โก โค โ f (f (f p)) โก โค
   VI p r = iv โ r
    where
     i : f (f p) โก f โค
     i = ap f r

     ii : f โค โก p
     ii = (III p r)โปยน

     iii : f (f p) โก p
     iii = i โ ii

     iv : f (f (f p)) โก f p
     iv = ap f iii

   VII : (p : ฮฉ ๐คโ) โ f (f (f p)) โก f p
   VII p = ฮฉ-ext' pe fe (V p) (VI p)

   VIII : (p : ฮฉ ๐คโ) โ f (f p) โก p
   VIII p = c (VII p)

\end{code}
