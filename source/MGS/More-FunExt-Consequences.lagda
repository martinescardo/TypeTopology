Martin Escardo 1st May 2020.

This is ported from the Midlands Graduate School 2019 lecture notes

 https://www.cs.bham.ac.uk/~mhe/HoTT-UF.in-Agda-Lecture-Notes/HoTT-UF-Agda.html
 https://github.com/martinescardo/HoTT-UF.Agda-Lecture-Notes

\begin{code}

{-# OPTIONS --safe --without-K #-}

module MGS.More-FunExt-Consequences where

open import MGS.HAE public
open import MGS.Subsingleton-Theorems public

being-subsingleton-is-subsingleton : dfunext ğ“¤ ğ“¤
                                   â†’  {X : ğ“¤ Ì‡ }
                                   â†’ is-subsingleton (is-subsingleton X)

being-subsingleton-is-subsingleton fe {X} i j = c
 where
  l : is-set X
  l = subsingletons-are-sets X i

  a : (x y : X) â†’ i x y ï¼ j x y
  a x y = l x y (i x y) (j x y)

  b : (x : X) â†’ i x ï¼ j x
  b x = fe (a x)

  c : i ï¼ j
  c = fe b

being-center-is-subsingleton : dfunext ğ“¤ ğ“¤
                             â†’ {X : ğ“¤ Ì‡ } (c : X)
                             â†’ is-subsingleton (is-center X c)

being-center-is-subsingleton fe {X} c Ï† Î³ = k
 where
  i : is-singleton X
  i = c , Ï†

  j : (x : X) â†’ is-subsingleton (c ï¼ x)
  j x = singletons-are-sets X i c x

  k : Ï† ï¼ Î³
  k = fe (Î» x â†’ j x (Ï† x) (Î³ x))

Î -is-set : hfunext ğ“¤ ğ“¥
         â†’ {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
         â†’ ((x : X) â†’ is-set (A x)) â†’ is-set (Î  A)

Î -is-set hfe s f g = b
 where
  a : is-subsingleton (f âˆ¼ g)
  a p q = Î³
   where
    h : âˆ€ x â†’  p x ï¼ q x
    h x = s x (f x) (g x) (p x) (q x)
    Î³ : p ï¼  q
    Î³ = hfunext-gives-dfunext hfe h

  e : (f ï¼ g) â‰ƒ (f âˆ¼ g)
  e = (happly f g , hfe f g)

  b : is-subsingleton (f ï¼ g)
  b = equiv-to-subsingleton e a

being-set-is-subsingleton : dfunext ğ“¤ ğ“¤
                          â†’ {X : ğ“¤ Ì‡ }
                          â†’ is-subsingleton (is-set X)

being-set-is-subsingleton fe = Î -is-subsingleton fe
                                (Î» x â†’ Î -is-subsingleton fe
                                (Î» y â†’ being-subsingleton-is-subsingleton fe))

hlevel-relation-is-subsingleton : dfunext ğ“¤ ğ“¤
                                â†’ (n : â„•) (X : ğ“¤ Ì‡ )
                                â†’ is-subsingleton (X is-of-hlevel n)

hlevel-relation-is-subsingleton {ğ“¤} fe zero X =
 being-singleton-is-subsingleton fe

hlevel-relation-is-subsingleton fe (succ n) X =
 Î -is-subsingleton fe
  (Î» x â†’ Î -is-subsingleton fe
  (Î» x' â†’ hlevel-relation-is-subsingleton fe n (x ï¼ x')))

â—-assoc : dfunext ğ“£ (ğ“¤ âŠ” ğ“£) â†’ dfunext (ğ“¤ âŠ” ğ“£) (ğ“¤ âŠ” ğ“£)
        â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } {T : ğ“£ Ì‡ }
          (Î± : X â‰ƒ Y) (Î² : Y â‰ƒ Z) (Î³ : Z â‰ƒ T)
        â†’ Î± â— (Î² â— Î³) ï¼ (Î± â— Î²) â— Î³

â—-assoc fe fe' (f , a) (g , b) (h , c) = ap (h âˆ˜ g âˆ˜ f ,_) q
 where
  d e : is-equiv (h âˆ˜ g âˆ˜ f)
  d = âˆ˜-is-equiv (âˆ˜-is-equiv c b) a
  e = âˆ˜-is-equiv c (âˆ˜-is-equiv b a)

  q : d ï¼ e
  q = being-equiv-is-subsingleton fe fe' (h âˆ˜ g âˆ˜ f) _ _

â‰ƒ-sym-involutive : dfunext ğ“¥ (ğ“¤ âŠ” ğ“¥) â†’ dfunext (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥) â†’
                   {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (Î± : X â‰ƒ Y)
                 â†’ â‰ƒ-sym (â‰ƒ-sym Î±) ï¼ Î±

â‰ƒ-sym-involutive fe fe' (f , a) = to-subtype-ï¼
                                   (being-equiv-is-subsingleton fe fe')
                                   (inversion-involutive f a)

â‰ƒ-sym-is-equiv : dfunext ğ“¥ (ğ“¤ âŠ” ğ“¥) â†’ dfunext ğ“¤ (ğ“¤ âŠ” ğ“¥) â†’ dfunext (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
               â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
               â†’ is-equiv (â‰ƒ-sym {ğ“¤} {ğ“¥} {X} {Y})

â‰ƒ-sym-is-equiv feâ‚€ feâ‚ feâ‚‚ = invertibles-are-equivs â‰ƒ-sym
                              (â‰ƒ-sym ,
                               â‰ƒ-sym-involutive feâ‚€ feâ‚‚ ,
                               â‰ƒ-sym-involutive feâ‚ feâ‚‚)

â‰ƒ-sym-â‰ƒ : dfunext ğ“¥ (ğ“¤ âŠ” ğ“¥) â†’ dfunext ğ“¤ (ğ“¤ âŠ” ğ“¥) â†’ dfunext (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
        â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ )
        â†’ (X â‰ƒ Y) â‰ƒ (Y â‰ƒ X)

â‰ƒ-sym-â‰ƒ feâ‚€ feâ‚ feâ‚‚ X Y = â‰ƒ-sym , â‰ƒ-sym-is-equiv feâ‚€ feâ‚ feâ‚‚

Î -cong : dfunext ğ“¤ ğ“¥ â†’ dfunext ğ“¤ ğ“¦
       â†’ {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ } {Y' : X â†’ ğ“¦ Ì‡ }
       â†’ ((x : X) â†’ Y x â‰ƒ Y' x) â†’ Î  Y â‰ƒ Î  Y'

Î -cong fe fe' {X} {Y} {Y'} Ï† = invertibility-gives-â‰ƒ F (G , GF , FG)
 where
  f : (x : X) â†’ Y x â†’ Y' x
  f x = âŒœ Ï† x âŒ

  e : (x : X) â†’ is-equiv (f x)
  e x = âŒœâŒ-is-equiv (Ï† x)

  g : (x : X) â†’ Y' x â†’ Y x
  g x = inverse (f x) (e x)

  fg : (x : X) (y' : Y' x) â†’ f x (g x y') ï¼ y'
  fg x = inverses-are-sections (f x) (e x)

  gf : (x : X) (y : Y x) â†’ g x (f x y) ï¼ y
  gf x = inverses-are-retractions (f x) (e x)

  F : ((x : X) â†’ Y x) â†’ ((x : X) â†’ Y' x)
  F Ï† x = f x (Ï† x)

  G : ((x : X) â†’ Y' x) â†’ (x : X) â†’ Y x
  G Î³ x = g x (Î³ x)

  FG : (Î³ : ((x : X) â†’ Y' x)) â†’ F (G Î³) ï¼ Î³
  FG Î³ = fe' (Î» x â†’ fg x (Î³ x))

  GF : (Ï† : ((x : X) â†’ Y x)) â†’ G (F Ï†) ï¼ Ï†
  GF Ï† = fe (Î» x â†’ gf x (Ï† x))

hfunext-â‰ƒ : hfunext ğ“¤ ğ“¥
          â†’ {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ } (f g : Î  A)
          â†’ (f ï¼ g) â‰ƒ (f âˆ¼ g)

hfunext-â‰ƒ hfe f g = (happly f g , hfe f g)

hfunextâ‚‚-â‰ƒ : hfunext ğ“¤ (ğ“¥ âŠ” ğ“¦) â†’ hfunext ğ“¥ ğ“¦
           â†’ {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ } {A : (x : X) â†’ Y x â†’ ğ“¦ Ì‡ }
             (f g : (x : X) (y : Y x) â†’ A x y)
           â†’ (f ï¼ g) â‰ƒ (âˆ€ x y â†’ f x y ï¼ g x y)

hfunextâ‚‚-â‰ƒ fe fe' {X} f g =

 (f ï¼ g)                  â‰ƒâŸ¨ i âŸ©
 (âˆ€ x â†’ f x ï¼ g x)        â‰ƒâŸ¨ ii âŸ©
 (âˆ€ x y â†’ f x y ï¼ g x y)  â– 

 where
  i  = hfunext-â‰ƒ fe f g
  ii = Î -cong
        (hfunext-gives-dfunext fe)
        (hfunext-gives-dfunext fe)
        (Î» x â†’ hfunext-â‰ƒ fe' (f x) (g x))

precomp-invertible : dfunext ğ“¥ ğ“¦ â†’ dfunext ğ“¤ ğ“¦
                   â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } (f : X â†’ Y)
                   â†’ invertible f
                   â†’ invertible (Î» (h : Y â†’ Z) â†’ h âˆ˜ f)

precomp-invertible fe fe' {X} {Y} {Z} f (g , Î· , Îµ) = (g' , Î·' , Îµ')
 where
  f' : (Y â†’ Z) â†’ (X â†’ Z)
  f' h = h âˆ˜ f

  g' : (X â†’ Z) â†’ (Y â†’ Z)
  g' k = k âˆ˜ g

  Î·' : (h : Y â†’ Z) â†’ g' (f' h) ï¼ h
  Î·' h = fe (Î» y â†’ ap h (Îµ y))

  Îµ' : (k : X â†’ Z) â†’ f' (g' k) ï¼ k
  Îµ' k = fe' (Î» x â†’ ap k (Î· x))

dprecomp : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (A : Y â†’ ğ“¦ Ì‡ ) (f : X â†’ Y)
         â†’ Î  A â†’ Î  (A âˆ˜ f)

dprecomp A f = _âˆ˜ f

dprecomp-is-equiv : dfunext ğ“¤ ğ“¦
                  â†’ dfunext ğ“¥ ğ“¦
                  â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (A : Y â†’ ğ“¦ Ì‡ ) (f : X â†’ Y)
                  â†’ is-equiv f
                  â†’ is-equiv (dprecomp A f)

dprecomp-is-equiv fe fe' {X} {Y} A f i = invertibles-are-equivs Ï† (Ïˆ , ÏˆÏ† , Ï†Ïˆ)
 where
  g = inverse f i
  Î· = inverses-are-retractions f i
  Îµ = inverses-are-sections f i

  Ï„ : (x : X) â†’ ap f (Î· x) ï¼ Îµ (f x)
  Ï„ = half-adjoint-condition f i

  Ï† : Î  A â†’ Î  (A âˆ˜ f)
  Ï† = dprecomp A f

  Ïˆ : Î  (A âˆ˜ f) â†’ Î  A
  Ïˆ k y = transport A (Îµ y) (k (g y))

  Ï†Ïˆâ‚€ : (k : Î  (A âˆ˜ f)) (x : X) â†’ transport A (Îµ (f x)) (k (g (f x))) ï¼ k x
  Ï†Ïˆâ‚€ k x = transport A (Îµ (f x))   (k (g (f x))) ï¼âŸ¨ a âŸ©
            transport A (ap f (Î· x))(k (g (f x))) ï¼âŸ¨ b âŸ©
            transport (A âˆ˜ f) (Î· x) (k (g (f x))) ï¼âŸ¨ c âŸ©
            k x                                   âˆ
    where
     a = ap (Î» - â†’ transport A - (k (g (f x)))) ((Ï„ x)â»Â¹)
     b = (transport-ap A f (Î· x) (k (g (f x))))â»Â¹
     c = apd k (Î· x)

  Ï†Ïˆ : Ï† âˆ˜ Ïˆ âˆ¼ id
  Ï†Ïˆ k = fe (Ï†Ïˆâ‚€ k)

  ÏˆÏ†â‚€ : (h : Î  A) (y : Y) â†’ transport A (Îµ y) (h (f (g y))) ï¼ h y
  ÏˆÏ†â‚€ h y = apd h (Îµ y)

  ÏˆÏ† : Ïˆ âˆ˜ Ï† âˆ¼ id
  ÏˆÏ† h = fe' (ÏˆÏ†â‚€ h)

Î -change-of-variable : dfunext ğ“¤ ğ“¦
                     â†’ dfunext ğ“¥ ğ“¦
                     â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (A : Y â†’ ğ“¦ Ì‡ ) (f : X â†’ Y)
                     â†’ is-equiv f
                     â†’ (Î  y ê‰ Y , A y) â‰ƒ (Î  x ê‰ X , A (f x))

Î -change-of-variable fe fe' A f i = dprecomp A f , dprecomp-is-equiv fe fe' A f i

at-most-one-section : dfunext ğ“¥ ğ“¤ â†’ hfunext ğ“¥ ğ“¥
                    â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
                    â†’ has-retraction f
                    â†’ is-subsingleton (has-section f)

at-most-one-section {ğ“¥} {ğ“¤} fe hfe {X} {Y} f (g , gf) (h , fh) = d
 where
  fe' : dfunext ğ“¥ ğ“¥
  fe' = hfunext-gives-dfunext hfe

  a : invertible f
  a = joyal-equivs-are-invertible f ((h , fh) , (g , gf))

  b : is-singleton (fiber (Î» h â†’  f âˆ˜ h) id)
  b = invertibles-are-equivs (Î» h â†’ f âˆ˜ h) (postcomp-invertible fe fe' f a) id

  r : fiber (Î» h â†’  f âˆ˜ h) id â†’ has-section f
  r (h , p) = (h , happly (f âˆ˜ h) id p)

  s : has-section f â†’ fiber (Î» h â†’ f âˆ˜ h) id
  s (h , Î·) = (h , fe' Î·)

  rs : (Ïƒ : has-section f) â†’ r (s Ïƒ) ï¼ Ïƒ
  rs (h , Î·) = to-Î£-ï¼' q
   where
    q : happly (f âˆ˜ h) id (inverse (happly (f âˆ˜ h) id) (hfe (f âˆ˜ h) id) Î·) ï¼ Î·
    q = inverses-are-sections (happly (f âˆ˜ h) id) (hfe (f âˆ˜ h) id) Î·

  c : is-singleton (has-section f)
  c = retract-of-singleton (r , s , rs) b

  d : (Ïƒ : has-section f) â†’ h , fh ï¼ Ïƒ
  d = singletons-are-subsingletons (has-section f) c (h , fh)

at-most-one-retraction : hfunext ğ“¤ ğ“¤ â†’ dfunext ğ“¥ ğ“¤
                       â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
                       â†’ has-section f
                       â†’ is-subsingleton (has-retraction f)

at-most-one-retraction {ğ“¤} {ğ“¥} hfe fe' {X} {Y} f (g , fg) (h , hf) = d
 where
  fe : dfunext ğ“¤ ğ“¤
  fe = hfunext-gives-dfunext hfe

  a : invertible f
  a = joyal-equivs-are-invertible f ((g , fg) , (h , hf))

  b : is-singleton (fiber (Î» h â†’  h âˆ˜ f) id)
  b = invertibles-are-equivs (Î» h â†’ h âˆ˜ f) (precomp-invertible fe' fe f a) id

  r : fiber (Î» h â†’  h âˆ˜ f) id â†’ has-retraction f
  r (h , p) = (h , happly (h âˆ˜ f) id p)

  s : has-retraction f â†’ fiber (Î» h â†’  h âˆ˜ f) id
  s (h , Î·) = (h , fe Î·)

  rs : (Ïƒ : has-retraction f) â†’ r (s Ïƒ) ï¼ Ïƒ
  rs (h , Î·) = to-Î£-ï¼' q
   where
    q : happly (h âˆ˜ f) id (inverse (happly (h âˆ˜ f) id) (hfe (h âˆ˜ f) id) Î·) ï¼ Î·
    q = inverses-are-sections (happly (h âˆ˜ f) id) (hfe (h âˆ˜ f) id) Î·

  c : is-singleton (has-retraction f)
  c = retract-of-singleton (r , s , rs) b

  d : (Ï : has-retraction f) â†’ h , hf ï¼ Ï
  d = singletons-are-subsingletons (has-retraction f) c (h , hf)

being-joyal-equiv-is-subsingleton : hfunext ğ“¤ ğ“¤ â†’ hfunext ğ“¥ ğ“¥ â†’ dfunext ğ“¥ ğ“¤
                                  â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                                  â†’ (f : X â†’ Y)
                                  â†’ is-subsingleton (is-joyal-equiv f)

being-joyal-equiv-is-subsingleton feâ‚€ feâ‚ feâ‚‚ f = Ã—-is-subsingleton'
                                                   (at-most-one-section    feâ‚‚ feâ‚ f ,
                                                    at-most-one-retraction feâ‚€ feâ‚‚ f)

being-hae-is-subsingleton : dfunext ğ“¥ ğ“¤ â†’ hfunext ğ“¥ ğ“¥ â†’ dfunext ğ“¤ (ğ“¥ âŠ” ğ“¤)
                          â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
                          â†’ is-subsingleton (is-hae f)

being-hae-is-subsingleton feâ‚€ feâ‚ feâ‚‚ {X} {Y} f = subsingleton-criterion' Î³
 where
  a = Î» g Îµ x
    â†’ ((g (f x) , Îµ (f x)) ï¼ (x , refl (f x)))                                   â‰ƒâŸ¨ i  g Îµ x âŸ©
      (Î£ p ê‰ g (f x) ï¼ x , transport (Î» - â†’ f - ï¼ f x) p (Îµ (f x)) ï¼ refl (f x)) â‰ƒâŸ¨ ii g Îµ x âŸ©
      (Î£ p ê‰ g (f x) ï¼ x , ap f p ï¼ Îµ (f x))                                     â– 
   where
    i  = Î» g Îµ x â†’ Î£-ï¼-â‰ƒ (g (f x) , Îµ (f x)) (x , refl (f x))
    ii = Î» g Îµ x â†’ Î£-cong (Î» p â†’ transport-ap-â‰ƒ f p (Îµ (f x)))

  b = (Î£ (g , Îµ) ê‰ has-section f , âˆ€ x â†’ (g (f x) , Îµ (f x)) ï¼ (x , refl (f x)))         â‰ƒâŸ¨ i âŸ©
      (Î£ (g , Îµ) ê‰ has-section f , âˆ€ x â†’ Î£  p ê‰ g (f x) ï¼ x , ap f p ï¼ Îµ (f x))          â‰ƒâŸ¨ ii âŸ©
      (Î£ g ê‰ (Y â†’ X) , Î£ Îµ ê‰ f âˆ˜ g âˆ¼ id , âˆ€ x â†’ Î£  p ê‰ g (f x) ï¼ x , ap f p ï¼ Îµ (f x))   â‰ƒâŸ¨ iii âŸ©
      (Î£ g ê‰ (Y â†’ X) , Î£ Îµ ê‰ f âˆ˜ g âˆ¼ id , Î£ Î· ê‰ g âˆ˜ f âˆ¼ id , âˆ€ x â†’ ap f (Î· x) ï¼ Îµ (f x)) â‰ƒâŸ¨ iv âŸ©
      is-hae f                                                                           â– 
   where
    i   = Î£-cong (Î» (g , Îµ) â†’ Î -cong feâ‚‚ feâ‚‚ (a g Îµ))
    ii  = Î£-assoc
    iii = Î£-cong (Î» g â†’ Î£-cong (Î» Îµ â†’ Î Î£-distr-â‰ƒ))
    iv  = Î£-cong (Î» g â†’ Î£-flip)

  Î³ : is-hae f â†’ is-subsingleton (is-hae f)
  Î³ (gâ‚€ , Îµâ‚€ , Î·â‚€ , Ï„â‚€) = i
   where
    c : (x : X) â†’ is-set (fiber f (f x))
    c x = singletons-are-sets (fiber f (f x)) (haes-are-equivs f (gâ‚€ , Îµâ‚€ , Î·â‚€ , Ï„â‚€) (f x))

    d : ((g , Îµ) : has-section f) â†’ is-subsingleton (âˆ€ x â†’ (g (f x) , Îµ (f x)) ï¼ (x , refl (f x)))
    d (g , Îµ) = Î -is-subsingleton feâ‚‚ (Î» x â†’ c x (g (f x) , Îµ (f x)) (x , refl (f x)))

    e : is-subsingleton (Î£ (g , Îµ) ê‰ has-section f , âˆ€ x â†’ (g (f x) , Îµ (f x)) ï¼ (x , refl (f x)))
    e = Î£-is-subsingleton (at-most-one-section feâ‚€ feâ‚ f (gâ‚€ , Îµâ‚€)) d

    i : is-subsingleton (is-hae f)
    i = equiv-to-subsingleton (â‰ƒ-sym b) e

emptiness-is-subsingleton : dfunext ğ“¤ ğ“¤â‚€ â†’ (X : ğ“¤ Ì‡ )
                          â†’ is-subsingleton (is-empty X)

emptiness-is-subsingleton fe X f g = fe (Î» x â†’ !ğŸ˜ (f x ï¼ g x) (f x))

+-is-subsingleton : {P : ğ“¤ Ì‡ } {Q : ğ“¥ Ì‡ }
                  â†’ is-subsingleton P
                  â†’ is-subsingleton Q
                  â†’ (P â†’ Q â†’ ğŸ˜) â†’ is-subsingleton (P + Q)

+-is-subsingleton {ğ“¤} {ğ“¥} {P} {Q} i j f = Î³
 where
  Î³ : (x y : P + Q) â†’ x ï¼ y
  Î³ (inl p) (inl p') = ap inl (i p p')
  Î³ (inl p) (inr q)  = !ğŸ˜ (inl p ï¼ inr q) (f p q)
  Î³ (inr q) (inl p)  = !ğŸ˜ (inr q ï¼ inl p) (f p q)
  Î³ (inr q) (inr q') = ap inr (j q q')

+-is-subsingleton' : dfunext ğ“¤ ğ“¤â‚€
                   â†’ {P : ğ“¤ Ì‡ } â†’ is-subsingleton P â†’ is-subsingleton (P + Â¬ P)

+-is-subsingleton' fe {P} i = +-is-subsingleton i
                               (emptiness-is-subsingleton fe P)
                               (Î» p n â†’ n p)

EM-is-subsingleton : dfunext (ğ“¤ âº) ğ“¤ â†’ dfunext ğ“¤ ğ“¤ â†’ dfunext ğ“¤ ğ“¤â‚€
                   â†’ is-subsingleton (EM ğ“¤)

EM-is-subsingleton feâº fe feâ‚€ = Î -is-subsingleton feâº
                                 (Î» P â†’ Î -is-subsingleton fe
                                         (Î» i â†’ +-is-subsingleton' feâ‚€ i))
\end{code}
