Martin Escardo 1st May 2020.

This is ported from the Midlands Graduate School 2019 lecture notes

 https://www.cs.bham.ac.uk/~mhe/HoTT-UF.in-Agda-Lecture-Notes/HoTT-UF-Agda.html
 https://github.com/martinescardo/HoTT-UF.Agda-Lecture-Notes

\begin{code}

{-# OPTIONS --safe --without-K #-}

module MGS.Universe-Lifting where

open import MGS.Equivalence-Constructions
open import MGS.Embeddings public

record Lift {ğ“¤ : Universe} (ğ“¥ : Universe) (X : ğ“¤ Ì‡ ) : ğ“¤ âŠ” ğ“¥ Ì‡  where
 constructor
  lift
 field
  lower : X

open Lift public

type-of-Lift  :             type-of (Lift  {ğ“¤} ğ“¥)       ï¼ (ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡ )
type-of-lift  : {X : ğ“¤ Ì‡ } â†’ type-of (lift  {ğ“¤} {ğ“¥} {X}) ï¼ (X â†’ Lift ğ“¥ X)
type-of-lower : {X : ğ“¤ Ì‡ } â†’ type-of (lower {ğ“¤} {ğ“¥} {X}) ï¼ (Lift ğ“¥ X â†’ X)

type-of-Lift  = refl _
type-of-lift  = refl _
type-of-lower = refl _

Lift-induction : âˆ€ {ğ“¤} ğ“¥ (X : ğ“¤ Ì‡ ) (A : Lift ğ“¥ X â†’ ğ“¦ Ì‡ )
               â†’ ((x : X) â†’ A (lift x))
               â†’ (l : Lift ğ“¥ X) â†’ A l

Lift-induction ğ“¥ X A Ï† (lift x) = Ï† x

Lift-recursion : âˆ€ {ğ“¤} ğ“¥ {X : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
               â†’ (X â†’ B) â†’ Lift ğ“¥ X â†’ B

Lift-recursion ğ“¥ {X} {B} = Lift-induction ğ“¥ X (Î» _ â†’ B)

lower-lift : {X : ğ“¤ Ì‡ } (x : X) â†’ lower {ğ“¤} {ğ“¥} (lift x) ï¼ x
lower-lift = refl

lift-lower : {X : ğ“¤ Ì‡ } (l : Lift ğ“¥ X) â†’ lift (lower l) ï¼ l
lift-lower = refl

Lift-â‰ƒ : (X : ğ“¤ Ì‡ ) â†’ Lift ğ“¥ X â‰ƒ X
Lift-â‰ƒ {ğ“¤} {ğ“¥} X = invertibility-gives-â‰ƒ lower
                     (lift , lift-lower , lower-lift {ğ“¤} {ğ“¥})

â‰ƒ-Lift : (X : ğ“¤ Ì‡ ) â†’ X â‰ƒ Lift ğ“¥ X
â‰ƒ-Lift {ğ“¤} {ğ“¥} X = invertibility-gives-â‰ƒ lift
                     (lower , lower-lift {ğ“¤} {ğ“¥} , lift-lower)

lower-dfunext : âˆ€ ğ“¦ ğ“£ ğ“¤ ğ“¥ â†’ dfunext (ğ“¤ âŠ” ğ“¦) (ğ“¥ âŠ” ğ“£) â†’ dfunext ğ“¤ ğ“¥
lower-dfunext ğ“¦ ğ“£ ğ“¤ ğ“¥ fe {X} {A} {f} {g} h = p
 where
  A' : Lift ğ“¦ X â†’ ğ“¥ âŠ” ğ“£ Ì‡
  A' y = Lift ğ“£ (A (lower y))

  f' g' : Î  A'
  f' y = lift (f (lower y))
  g' y = lift (g (lower y))

  h' : f' âˆ¼ g'
  h' y = ap lift (h (lower y))

  p' : f' ï¼ g'
  p' = fe h'

  p : f ï¼ g
  p = ap (Î» f' x â†’ lower (f' (lift x))) p'

universe-embedding-criterion : is-univalent ğ“¤
                             â†’ is-univalent (ğ“¤ âŠ” ğ“¥)
                             â†’ (f : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡ )
                             â†’ ((X : ğ“¤ Ì‡ ) â†’ f X â‰ƒ X)
                             â†’ is-embedding f

universe-embedding-criterion {ğ“¤} {ğ“¥} ua ua' f e = embedding-criterion f Î³
 where
  fe : dfunext (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
  fe = univalence-gives-dfunext ua'

  feâ‚€ : dfunext ğ“¤ ğ“¤
  feâ‚€ = lower-dfunext ğ“¥ ğ“¥ ğ“¤ ğ“¤ fe

  feâ‚ : dfunext ğ“¤ (ğ“¤ âŠ” ğ“¥)
  feâ‚ = lower-dfunext ğ“¥ ğ“¥ ğ“¤ (ğ“¤ âŠ” ğ“¥) fe

  Î³ : (X X' : ğ“¤ Ì‡ ) â†’ (f X ï¼ f X') â‰ƒ (X ï¼ X')
  Î³ X X' =  (f X ï¼ f X')  â‰ƒâŸ¨ i âŸ©
            (f X â‰ƒ f X')  â‰ƒâŸ¨ ii âŸ©
            (X â‰ƒ X')      â‰ƒâŸ¨ iii âŸ©
            (X ï¼ X')      â– 
   where
    i   = univalence-â‰ƒ ua' (f X) (f X')
    ii  = Eq-Eq-cong' fe fe fe fe fe feâ‚€ feâ‚ fe feâ‚€ feâ‚€ feâ‚€ feâ‚€ (e X) (e X')
    iii = â‰ƒ-sym (univalence-â‰ƒ ua X X')

Lift-is-embedding : is-univalent ğ“¤ â†’ is-univalent (ğ“¤ âŠ” ğ“¥)
                  â†’ is-embedding (Lift {ğ“¤} ğ“¥)

Lift-is-embedding {ğ“¤} {ğ“¥} ua ua' = universe-embedding-criterion {ğ“¤} {ğ“¥} ua ua'
                                    (Lift ğ“¥) Lift-â‰ƒ

module _ {ğ“¤ ğ“¥ : Universe}
         (ua : is-univalent ğ“¥)
         (ua' : is-univalent (ğ“¤ âŠ” ğ“¥))
 where

 private
  fe : dfunext (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
  fe = univalence-gives-dfunext ua'

  feâ‚€ : dfunext ğ“¥ (ğ“¤ âŠ” ğ“¥)
  feâ‚€ = lower-dfunext ğ“¤ ğ“¤ ğ“¥ (ğ“¤ âŠ” ğ“¥) fe

  feâ‚ : dfunext ğ“¤ (ğ“¤ âŠ” ğ“¥)
  feâ‚ = lower-dfunext (ğ“¤ âŠ” ğ“¥) ğ“¤ ğ“¤ (ğ“¤ âŠ” ğ“¥) fe

  feâ‚‚ : dfunext ğ“¥ ğ“¥
  feâ‚‚ = lower-dfunext ğ“¤ ğ“¤ ğ“¥ ğ“¥ fe

  feâ‚ƒ : dfunext ğ“¤ ğ“¤
  feâ‚ƒ = lower-dfunext ğ“¥ ğ“¥ ğ“¤ ğ“¤ fe

 univalenceâ†’' : (X : ğ“¤ Ì‡ ) â†’ is-subsingleton (Î£ Y ê‰ ğ“¥ Ì‡ , X â‰ƒ Y)
 univalenceâ†’' X = s
  where
   e : (Y : ğ“¥ Ì‡ ) â†’ (X â‰ƒ Y) â‰ƒ (Lift ğ“¤ Y ï¼ Lift ğ“¥ X)
   e Y = (X â‰ƒ Y)                 â‰ƒâŸ¨ i âŸ©
         (Y â‰ƒ X)                 â‰ƒâŸ¨ ii âŸ©
         (Lift ğ“¤ Y â‰ƒ Lift ğ“¥ X)   â‰ƒâŸ¨ iii âŸ©
         (Lift ğ“¤ Y ï¼ Lift ğ“¥ X)   â– 
    where
     i   = â‰ƒ-Sym feâ‚€ feâ‚ fe
     ii  = Eq-Eq-cong' feâ‚ fe feâ‚‚ feâ‚ fe fe fe feâ‚ƒ
             fe fe fe fe (â‰ƒ-Lift Y) (â‰ƒ-Lift X)
     iii = â‰ƒ-sym (univalence-â‰ƒ ua' (Lift ğ“¤ Y) (Lift ğ“¥ X))

   d : (Î£ Y ê‰ ğ“¥ Ì‡ , X â‰ƒ Y) â‰ƒ (Î£ Y ê‰ ğ“¥ Ì‡ , Lift ğ“¤ Y ï¼ Lift ğ“¥ X)
   d = Î£-cong e

   j : is-subsingleton (Î£ Y ê‰ ğ“¥ Ì‡ , Lift ğ“¤ Y ï¼ Lift ğ“¥ X)
   j = Lift-is-embedding ua ua' (Lift ğ“¥ X)

   abstract
    s : is-subsingleton (Î£ Y ê‰ ğ“¥ Ì‡ , X â‰ƒ Y)
    s = equiv-to-subsingleton d j

 univalenceâ†’'-dual : (Y : ğ“¤ Ì‡ ) â†’ is-subsingleton (Î£ X ê‰ ğ“¥ Ì‡ , X â‰ƒ Y)
 univalenceâ†’'-dual Y = equiv-to-subsingleton e i
  where
   e : (Î£ X ê‰ ğ“¥ Ì‡ , X â‰ƒ Y) â‰ƒ (Î£ X ê‰ ğ“¥ Ì‡ , Y â‰ƒ X)
   e = Î£-cong (Î» X â†’ â‰ƒ-Sym feâ‚ feâ‚€ fe)

   i : is-subsingleton (Î£ X ê‰ ğ“¥ Ì‡ , Y â‰ƒ X)
   i = univalenceâ†’' Y

univalenceâ†’'' : is-univalent (ğ“¤ âŠ” ğ“¥)
              â†’ (X : ğ“¤ Ì‡ ) â†’ is-subsingleton (Î£ Y ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y)

univalenceâ†’'' ua = univalenceâ†’' ua ua

univalenceâ†’''-dual : is-univalent (ğ“¤ âŠ” ğ“¥)
                   â†’ (Y : ğ“¤ Ì‡ ) â†’ is-subsingleton (Î£ X ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y)

univalenceâ†’''-dual ua = univalenceâ†’'-dual ua ua

Gâ†‘-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (X : ğ“¤ Ì‡ ) (A : (Î£ Y ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y) â†’ ğ“¦ Ì‡ )
     â†’ A (Lift ğ“¥ X , â‰ƒ-Lift X) â†’ (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (e : X â‰ƒ Y) â†’ A (Y , e)

Gâ†‘-â‰ƒ {ğ“¤} {ğ“¥} ua X A a Y e = transport A p a
 where
  t : Î£ Y ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y
  t = (Lift ğ“¥ X , â‰ƒ-Lift X)

  p : t ï¼ (Y , e)
  p = univalenceâ†’'' {ğ“¤} {ğ“¥} ua X t (Y , e)

Hâ†‘-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (X : ğ“¤ Ì‡ ) (A : (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡ )
     â†’ A (Lift ğ“¥ X) (â‰ƒ-Lift X) â†’ (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (e : X â‰ƒ Y) â†’ A Y e

Hâ†‘-â‰ƒ ua X A = Gâ†‘-â‰ƒ ua X (Î£-induction A)

Jâ†‘-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (A : (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡ )
     â†’ ((X : ğ“¤ Ì‡ ) â†’ A X (Lift ğ“¥ X) (â‰ƒ-Lift X))
     â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (e : X â‰ƒ Y) â†’ A X Y e

Jâ†‘-â‰ƒ ua A Ï† X = Hâ†‘-â‰ƒ ua X (A X) (Ï† X)

Hâ†‘-equiv : is-univalent (ğ“¤ âŠ” ğ“¥)
         â†’ (X : ğ“¤ Ì‡ ) (A : (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
         â†’ A (Lift ğ“¥ X) lift â†’ (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (f : X â†’ Y) â†’ is-equiv f â†’ A Y f

Hâ†‘-equiv {ğ“¤} {ğ“¥} {ğ“¦} ua X A a Y f i = Î³ (f , i)
 where
  B : (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡
  B Y (f , i) = A Y f

  b : B (Lift ğ“¥ X) (â‰ƒ-Lift X)
  b = a

  Î³ : (e : X â‰ƒ Y) â†’ B Y e
  Î³ = Hâ†‘-â‰ƒ ua X B b Y

Jâ†‘-equiv : is-univalent (ğ“¤ âŠ” ğ“¥)
         â†’ (A : (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
         â†’ ((X : ğ“¤ Ì‡ ) â†’ A X (Lift ğ“¥ X) lift)
         â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (f : X â†’ Y) â†’ is-equiv f â†’ A X Y f

Jâ†‘-equiv ua A Ï† X = Hâ†‘-equiv ua X (A X) (Ï† X)

Jâ†‘-invertible : is-univalent (ğ“¤ âŠ” ğ“¥)
              â†’ (A : (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
              â†’ ((X : ğ“¤ Ì‡ ) â†’ A X (Lift ğ“¥ X) lift)
              â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¤ âŠ” ğ“¥ Ì‡ ) (f : X â†’ Y) â†’ invertible f â†’ A X Y f

Jâ†‘-invertible ua A Ï† X Y f i = Jâ†‘-equiv ua A Ï† X Y f (invertibles-are-equivs f i)

lift-is-hae : (X : ğ“¤ Ì‡ ) â†’ is-hae {ğ“¤} {ğ“¤ âŠ” ğ“¥} {X} {Lift ğ“¥ X} (lift {ğ“¤} {ğ“¥})
lift-is-hae {ğ“¤} {ğ“¥} X = lower ,
                        lower-lift {ğ“¤} {ğ“¥} ,
                        lift-lower ,
                        Î» x â†’ refl (refl (lift x))

equivs-are-haesâ†‘ : is-univalent (ğ“¤ âŠ” ğ“¥)
                 â†’ {X : ğ“¤ Ì‡ } {Y : ğ“¤ âŠ” ğ“¥ Ì‡ } (f : X â†’ Y)
                 â†’ is-equiv f â†’ is-hae f

equivs-are-haesâ†‘ {ğ“¤} {ğ“¥} ua {X} {Y} = Jâ†‘-equiv {ğ“¤} {ğ“¥} ua (Î» X Y f â†’ is-hae f)
                                       lift-is-hae X Y

Gâ†“-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (Y : ğ“¤ Ì‡ ) (A : (Î£ X ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y) â†’ ğ“¦ Ì‡ )
     â†’ A (Lift ğ“¥ Y , Lift-â‰ƒ Y) â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (e : X â‰ƒ Y) â†’ A (X , e)

Gâ†“-â‰ƒ {ğ“¤} {ğ“¥} ua Y A a X e = transport A p a
 where
  t : Î£ X ê‰ ğ“¤ âŠ” ğ“¥ Ì‡ , X â‰ƒ Y
  t = (Lift ğ“¥ Y , Lift-â‰ƒ Y)

  p : t ï¼ (X , e)
  p = univalenceâ†’'-dual {ğ“¤} {ğ“¤ âŠ” ğ“¥} ua ua Y t (X , e)

Hâ†“-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (Y : ğ“¤ Ì‡ ) (A : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡ )
     â†’ A (Lift ğ“¥ Y) (Lift-â‰ƒ Y) â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (e : X â‰ƒ Y) â†’ A X e

Hâ†“-â‰ƒ ua Y A = Gâ†“-â‰ƒ ua Y (Î£-induction A)

Jâ†“-â‰ƒ : is-univalent (ğ“¤ âŠ” ğ“¥)
     â†’ (A : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡ )
     â†’ ((Y : ğ“¤ Ì‡ ) â†’ A (Lift ğ“¥ Y) Y (Lift-â‰ƒ Y))
     â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) (e : X â‰ƒ Y) â†’ A X Y e

Jâ†“-â‰ƒ ua A Ï† X Y = Hâ†“-â‰ƒ ua Y (Î» X â†’ A X Y) (Ï† Y) X

Hâ†“-equiv : is-univalent (ğ“¤ âŠ” ğ“¥)
         â†’ (Y : ğ“¤ Ì‡ ) (A : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
         â†’ A (Lift ğ“¥ Y) lower â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (f : X â†’ Y) â†’ is-equiv f â†’ A X f

Hâ†“-equiv {ğ“¤} {ğ“¥} {ğ“¦} ua Y A a X f i = Î³ (f , i)
 where
  B : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ ğ“¦ Ì‡
  B X (f , i) = A X f

  b : B (Lift ğ“¥ Y) (Lift-â‰ƒ Y)
  b = a

  Î³ : (e : X â‰ƒ Y) â†’ B X e
  Î³ = Hâ†“-â‰ƒ ua Y B b X

Jâ†“-equiv : is-univalent (ğ“¤ âŠ” ğ“¥)
         â†’ (A : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
         â†’ ((Y : ğ“¤ Ì‡ ) â†’ A (Lift ğ“¥ Y) Y lower)
         â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) (f : X â†’ Y) â†’ is-equiv f â†’ A X Y f

Jâ†“-equiv ua A Ï† X Y = Hâ†“-equiv ua Y (Î» X â†’ A X Y) (Ï† Y) X

Jâ†“-invertible : is-univalent (ğ“¤ âŠ” ğ“¥)
              â†’ (A : (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) â†’ (X â†’ Y) â†’ ğ“¦ Ì‡ )
              â†’ ((Y : ğ“¤ Ì‡ ) â†’ A (Lift ğ“¥ Y) Y lower)
              â†’ (X : ğ“¤ âŠ” ğ“¥ Ì‡ ) (Y : ğ“¤ Ì‡ ) (f : X â†’ Y) â†’ invertible f â†’ A X Y f

Jâ†“-invertible ua A Ï† X Y f i = Jâ†“-equiv ua A Ï† X Y f (invertibles-are-equivs f i)

lower-is-hae : (X : ğ“¤ Ì‡ ) â†’ is-hae (lower {ğ“¤} {ğ“¥} {X})
lower-is-hae {ğ“¤} {ğ“¥} X = lift ,
                         lift-lower ,
                         lower-lift {ğ“¤} {ğ“¥} ,
                         (Î» x â†’ refl (refl (lower x)))

equivs-are-haesâ†“ : is-univalent (ğ“¤ âŠ” ğ“¥)
                 â†’ {X : ğ“¤ âŠ” ğ“¥ Ì‡ } {Y : ğ“¤ Ì‡ } (f : X â†’ Y)
                 â†’ is-equiv f â†’ is-hae f

equivs-are-haesâ†“ {ğ“¤} {ğ“¥} ua {X} {Y} = Jâ†“-equiv {ğ“¤} {ğ“¥} ua (Î» X Y f â†’ is-hae f)
                                       lower-is-hae X Y

Idâ†’Eq-is-hae' : is-univalent ğ“¤ â†’ is-univalent (ğ“¤ âº)
              â†’ {X Y : ğ“¤ Ì‡ } â†’ is-hae (Idâ†’Eq X Y)

Idâ†’Eq-is-hae' ua uaâº {X} {Y} = equivs-are-haesâ†“ uaâº (Idâ†’Eq X Y) (ua X Y)

Idâ†’Eq-is-hae : is-univalent ğ“¤
             â†’ {X Y : ğ“¤ Ì‡ } â†’ is-hae (Idâ†’Eq X Y)

Idâ†’Eq-is-hae ua {X} {Y} = invertibles-are-haes (Idâ†’Eq X Y)
                           (equivs-are-invertible (Idâ†’Eq X Y) (ua X Y))

global-property-of-types : ğ“¤Ï‰
global-property-of-types = {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡

cumulative : global-property-of-types â†’ ğ“¤Ï‰
cumulative A = {ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ ) â†’ A X â‰ƒ A (Lift ğ“¥ X)

global-â‰ƒ-ap : Univalence
            â†’ (A : global-property-of-types)
            â†’ cumulative A
            â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ A X â‰ƒ A Y

global-â‰ƒ-ap' : Univalence
             â†’ (F : Universe â†’ Universe)
             â†’ (A : {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ (F ğ“¤) Ì‡ )
             â†’ ({ğ“¤ ğ“¥ : Universe} (X : ğ“¤ Ì‡ ) â†’ A X â‰ƒ A (Lift ğ“¥ X))
             â†’ (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) â†’ X â‰ƒ Y â†’ A X â‰ƒ A Y

global-â‰ƒ-ap' {ğ“¤} {ğ“¥} ua F A Ï† X Y e =

  A X          â‰ƒâŸ¨ Ï† X âŸ©
  A (Lift ğ“¥ X) â‰ƒâŸ¨ Idâ†’Eq (A (Lift ğ“¥ X)) (A (Lift ğ“¤ Y)) q âŸ©
  A (Lift ğ“¤ Y) â‰ƒâŸ¨ â‰ƒ-sym (Ï† Y) âŸ©
  A Y          â– 
 where
  d : Lift ğ“¥ X â‰ƒ Lift ğ“¤ Y
  d = Lift ğ“¥ X â‰ƒâŸ¨ Lift-â‰ƒ X âŸ©
      X        â‰ƒâŸ¨ e âŸ©
      Y        â‰ƒâŸ¨ â‰ƒ-sym (Lift-â‰ƒ Y) âŸ©
      Lift ğ“¤ Y â– 

  p : Lift ğ“¥ X ï¼ Lift ğ“¤ Y
  p = Eqâ†’Id (ua (ğ“¤ âŠ” ğ“¥)) (Lift ğ“¥ X) (Lift ğ“¤ Y) d

  q : A (Lift ğ“¥ X) ï¼ A (Lift ğ“¤ Y)
  q = ap A p

global-â‰ƒ-ap ua = global-â‰ƒ-ap' ua (Î» ğ“¤ â†’ ğ“¤)

\end{code}
