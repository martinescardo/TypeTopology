Martin Escardo 2011, reorganized and expanded 2018.

Compact types.

(This is related to my 2008 LMCS paper "Exhaustible sets in higher-type
computation", where compact types correspond to "exhaustible sets" and
compactâˆ™ types (compact-pointed types) correpond to searchable sets.
It is also related to joint work with Oliva on selection functions in
proof theory.)

Here we don't assume continuity axioms, but all functions are secretly
continuous, and compact sets are secretly topologically compact.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module CompactTypes where

open import SpartanMLTT
open import Two
open import Two-Prop-Density
open import DiscreteAndSeparated
open import DecidableAndDetachable
open import UF-Subsingletons
open import UF-FunExt
open import UF-Retracts
open import UF-Equiv
open import UF-PropTrunc
open import UF-ImageAndSurjection
open import UF-Miscelanea

\end{code}

We say that a type is compact if for every ğŸš-valued function defined
on it, it decidable whether it has a root:

\begin{code}

compact : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
compact X = (p : X â†’ ğŸš) â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + (Î  \(x : X) â†’ p x â‰¡ â‚)

\end{code}

Notice that compactness in this sense is not in general a univalent
proposition (subsingleton). A weaker notion that is always a proposition is
defined and studied in the module WeaklyCompactTypes.

The following notion is logically equivalent to the conjunction of
compactness and pointedness, and hence the notation "compactâˆ™":

\begin{code}

compactâˆ™ : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
compactâˆ™ X = (p : X â†’ ğŸš) â†’ Î£ \(xâ‚€ : X) â†’ p xâ‚€ â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚

\end{code}

Terminology: we call xâ‚€ the universal witness.

\begin{code}

compact-pointed-gives-compactâˆ™ : âˆ€ {U} {X : U Ì‡} â†’ compact X â†’ X â†’ compactâˆ™ X
compact-pointed-gives-compactâˆ™ {U} {X} Ï† xâ‚€ p = lemma(Ï† p)
 where
  lemma : (Î£ \(x : X) â†’ p x â‰¡ â‚€) + ((x : X) â†’ p x â‰¡ â‚) â†’
           Î£ \(xâ‚€ : X) â†’ p xâ‚€ â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚
  lemma (inl(x , r)) = x , (Î» s â†’ ğŸ˜-elim(Lemma[bâ‰¡â‚€â†’bâ‰¢â‚] r s))
  lemma (inr f) = xâ‚€ , (Î» r â†’ f)

compactâˆ™-gives-compact : âˆ€ {U} {X : U Ì‡} â†’ compactâˆ™ X â†’ compact X
compactâˆ™-gives-compact {U} {X} Îµ p = ğŸš-equality-cases caseâ‚€ caseâ‚
 where
  xâ‚€ : X
  xâ‚€ = prâ‚(Îµ p)
  lemma : p xâ‚€ â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚
  lemma = prâ‚‚(Îµ p)
  caseâ‚€ : p xâ‚€ â‰¡ â‚€ â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + ((x : X) â†’ p x â‰¡ â‚)
  caseâ‚€ r = inl(xâ‚€ , r)
  caseâ‚ : p xâ‚€ â‰¡ â‚ â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + ((x : X) â†’ p x â‰¡ â‚)
  caseâ‚ r = inr(lemma r)

compactâˆ™-gives-pointed : âˆ€ {U} {X : U Ì‡} â†’ compactâˆ™ X â†’ X
compactâˆ™-gives-pointed Îµ = prâ‚(Îµ(Î» x â†’ â‚€))

\end{code}

For example, every finite set is compact, and in particular the
set ğŸš = { â‚€ , â‚ } of binary numerals is compact. To find xâ‚€ : ğŸš
such that

   (â€ ) p xâ‚€ â‰¡ â‚ â†’ âˆ€(x : X) â†’ p x â‰¡ â‚,

we can check whether p â‚€ â‰¡ â‚ and p â‚ â‰¡ â‚.

     If this is the case, then âˆ€(x : X) â†’ p x â‰¡ â‚ holds, which is
     the conclusion the implication (â€ ), and hence we can take any
     xâ‚€ : ğŸš to make (â€ ) hold.

     Otherwise, we can take any xâ‚€ such that p xâ‚€ â‰¡ â‚€ so that the
     implication (â€ ) holds vacuously.

That is, either the conclusion âˆ€(x : X) â†’ p x â‰¡ â‚ of (â€ ) holds, or
its premise p xâ‚€ â‰¡ â‚ fails for suitable xâ‚€.

However, there is a more direct proof: we claim that, without
checking the two possibilities, we can always take xâ‚€ = p â‚€.
(Cf. Section 8.1 of the LMCS'2008 paper.)

\begin{code}

ğŸš-compactâˆ™ : compactâˆ™ ğŸš
ğŸš-compactâˆ™ p = xâ‚€ , (Î» r â†’ ğŸš-induction (lemmaâ‚€ r) (lemmaâ‚ r))
 where
    xâ‚€ : ğŸš
    xâ‚€ = p â‚€
    claim : p xâ‚€ â‰¡ â‚ â†’ p â‚€ â‰¡ â‚€ â†’ p â‚€ â‰¡ â‚
    claim r s = transport (Î» - â†’ p - â‰¡ â‚) s r
    lemmaâ‚€ : p xâ‚€ â‰¡ â‚ â†’ p â‚€ â‰¡ â‚
    lemmaâ‚€ r = ğŸš-equality-cases (claim r) (Î» s â†’ s)
    lemmaâ‚ : p xâ‚€ â‰¡ â‚ â†’ p â‚ â‰¡ â‚
    lemmaâ‚ r = transport (Î» - â†’ p - â‰¡ â‚) (lemmaâ‚€ r) r

\end{code}

Even though excluded middle is undecided, the set Î© U of univalent
propositions in any universe U is compact (assuming propositional
extensionality, which is a consequence of univalence):

\begin{code}

Î©-compactâˆ™ : âˆ€ {U} â†’ funext U U â†’ propext U â†’ compactâˆ™ (Î© U)
Î©-compactâˆ™ {U} fe pe p = ğŸš-equality-cases a b
  where
    A = Î£ \(xâ‚€ : Î© U) â†’ p xâ‚€ â‰¡ â‚ â†’ (x : Î© U) â†’ p x â‰¡ â‚
    a : p âŠ¥ â‰¡ â‚€ â†’ A
    a r = âŠ¥ , Î» s â†’ ğŸ˜-elim (zero-is-not-one (r â»Â¹ âˆ™ s))
    b : p âŠ¥ â‰¡ â‚ â†’ A
    b r = ğŸš-equality-cases c d
      where
        c : p âŠ¤ â‰¡ â‚€ â†’ A
        c s = âŠ¤ , Î» t â†’ ğŸ˜-elim (zero-is-not-one (s â»Â¹ âˆ™ t))
        d : p âŠ¤ â‰¡ â‚ â†’ A
        d s = âŠ¤ , âŠ¥-âŠ¤-density fe pe p r

\end{code}

We could have used the same idea of proof as for ğŸš-compact, again
using density.

\begin{code}

ğŸ™-compactâˆ™ : âˆ€ {U} â†’ compactâˆ™ (ğŸ™ {U})
ğŸ™-compactâˆ™ p = * , f
 where
  f : (r : p * â‰¡ â‚) (x : ğŸ™) â†’ p x â‰¡ â‚
  f r * = r

\end{code}

In this module we prove some closure properties of compact
sets. Before doing this, we investigate their general nature.

We first show that the universal witness xâ‚€ is a root of p if and
only if p has a root.

\begin{code}

_is-a-root-of_ : âˆ€ {U} {X : U Ì‡} â†’ X â†’ (X â†’ ğŸš) â†’ Uâ‚€ Ì‡
x is-a-root-of p = p x â‰¡ â‚€

_has-a-root : âˆ€ {U} {X : U Ì‡} â†’ (X â†’ ğŸš) â†’ U Ì‡
p has-a-root = Î£ \x â†’ x is-a-root-of p

putative-root : âˆ€ {U} {X : U Ì‡}
              â†’ compactâˆ™ X â†’ (p : X â†’ ğŸš) â†’ Î£ \(xâ‚€ : X) â†’ (p has-a-root) â‡” (xâ‚€ is-a-root-of p)
putative-root {U} {X} Îµ p = xâ‚€ , (lemmaâ‚€ , lemmaâ‚)
 where
  xâ‚€ : X
  xâ‚€ = prâ‚(Îµ p)
  lemma : Â¬((x : X) â†’ p x â‰¡ â‚) â†’ p xâ‚€ â‰¡ â‚€
  lemma = Lemma[bâ‰¢â‚â†’bâ‰¡â‚€] âˆ˜ contrapositive(prâ‚‚(Îµ p))
  lemmaâ‚€ : p has-a-root â†’ xâ‚€ is-a-root-of p
  lemmaâ‚€ (x , r) = lemma claim
   where claim : Â¬((x : X) â†’ p x â‰¡ â‚)
         claim f = Lemma[bâ‰¡â‚â†’bâ‰¢â‚€] (f x) r
  lemmaâ‚ : xâ‚€ is-a-root-of p â†’ p has-a-root
  lemmaâ‚ h = xâ‚€ , h
\end{code}

We now relate our definition to the original definition using
selection functions.

\begin{code}

_has-selection_ : âˆ€ {U} (X : U Ì‡) â†’ ((X â†’ ğŸš) â†’ X) â†’ U Ì‡
X has-selection Îµ = (p : X â†’ ğŸš) â†’ p(Îµ p) â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚

compactâˆ™' : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
compactâˆ™' X = Î£ \(Îµ : (X â†’ ğŸš) â†’ X) â†’ X has-selection Îµ

compactâˆ™-gives-compactâˆ™' : âˆ€ {U} {X : U Ì‡} â†’ compactâˆ™ X â†’ compactâˆ™' X
compactâˆ™-gives-compactâˆ™' {U} {X} Îµ' = Îµ , lemma
 where
  Îµ : (X â†’ ğŸš) â†’ X
  Îµ p = prâ‚(Îµ' p)
  lemma : (p : X â†’ ğŸš) â†’ p(Îµ p) â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚
  lemma p = prâ‚‚(Îµ' p)

\end{code}

Notice that Bishop's limited principle of omniscience LPO, which is a
taboo, in Aczel's terminology, is the compactness of â„•. LPO is
independent - it is not provable, and its negation is not provable. In
classical mathematics it is uncomfortable to have independent
propositions, but of course unavoidable. Independence occurs often in
constructive mathematics, particular in classically compatible
constructive mathematics, like Bishop's methamatics and Martin-LÃ¶f
type theory (in its various flavours); even the principle of excluded
middle is independent.

We'll see that the infinite set â„•âˆ defined in the module
ConvergentSequenceCompact.

If a set X is compactâˆ™ and a set Y has decidable equality, then the
function space (X â†’ Y) has decidable equality, if we assume function
extensionality. In our topological correspondence, decidable equality
is called discreteness. More generally we have:

\begin{code}

apart-or-equal : âˆ€ {U V} {X : U Ì‡} â†’ funext U V â†’ {Y : X â†’ V Ì‡}
              â†’ compact X â†’ ((x : X) â†’ discrete(Y x))
              â†’ (f g : (x : X) â†’ Y x) â†’ (f â™¯ g) + (f â‰¡ g)
apart-or-equal {U} {V} {X} fe {Y} Ï† d f g = lemmaâ‚‚ lemmaâ‚
 where
  claim : (x : X) â†’ (f x â‰¢ g x) + (f x â‰¡ g x)
  claim x = +-commutative(d x (f x) (g x))
  lemmaâ‚€ : Î£ \(p : X â†’ ğŸš) â†’ (x : X) â†’ (p x â‰¡ â‚€ â†’ f x â‰¢ g x) Ã— (p x â‰¡ â‚ â†’ f x â‰¡ g x)
  lemmaâ‚€ = indicator claim
  p : X â†’ ğŸš
  p = prâ‚ lemmaâ‚€
  lemmaâ‚ : (Î£ \x â†’ p x â‰¡ â‚€) + ((x : X) â†’ p x â‰¡ â‚)
  lemmaâ‚ = Ï† p
  lemmaâ‚‚ : (Î£ \x â†’ p x â‰¡ â‚€) + ((x : X) â†’ p x â‰¡ â‚) â†’ (f â™¯ g) + (f â‰¡ g)
  lemmaâ‚‚(inl(x , r)) = inl(x , (prâ‚(prâ‚‚ lemmaâ‚€ x) r))
  lemmaâ‚‚(inr h) = inr (dfunext fe (Î» x â†’ prâ‚‚(prâ‚‚ lemmaâ‚€ x) (h x)))

compact-discrete-discrete : âˆ€ {U V} {X : U Ì‡} â†’ funext U V â†’ {Y : X â†’ V Ì‡} â†’

   compact X â†’ ((x : X) â†’ discrete(Y x)) â†’ discrete((x : X) â†’ Y x)

compact-discrete-discrete fe Ï† d f g = h(apart-or-equal fe Ï† d f g)
 where
  h : (f â™¯ g) + (f â‰¡ g) â†’ (f â‰¡ g) + (f â‰¢ g)
  h(inl a) = inr(apart-is-different a)
  h(inr r) = inl r

compact-discrete-discrete' : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ funext U V
                             â†’ compact X â†’ discrete Y â†’ discrete(X â†’ Y)
compact-discrete-discrete' fe Ï† d = compact-discrete-discrete fe Ï† (Î» x â†’ d)

ğŸ˜-compact : âˆ€ {U} â†’ compact ğŸ˜
ğŸ˜-compact {U} p = inr (Î» x â†’ ğŸ˜-elim {Uâ‚€} {U} x)

compact-decidable : âˆ€ {U} (X : U Ì‡) â†’ compact X â†’ decidable X
compact-decidable X Ï† = f a
 where
  a : (X Ã— (â‚€ â‰¡ â‚€)) + (X â†’ â‚€ â‰¡ â‚)
  a = Ï† (Î» x â†’ â‚€)
  f : (X Ã— (â‚€ â‰¡ â‚€)) + (X â†’ â‚€ â‰¡ â‚) â†’ decidable X
  f (inl (x , _)) = inl x
  f (inr u)       = inr (Î» x â†’ zero-is-not-one (u x))

decidable-prop-compact : âˆ€ {U} (X : U Ì‡) â†’ is-prop X â†’ decidable X â†’ compact X
decidable-prop-compact X isp Î´ p = g Î´
 where
  g : decidable X â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + Î  \(x : X) â†’ p x â‰¡ â‚
  g (inl x) = ğŸš-equality-cases b c
   where
    b : p x â‰¡ â‚€ â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + Î  \(x : X) â†’ p x â‰¡ â‚
    b r = inl (x , r)
    c : p x â‰¡ â‚ â†’ (Î£ \(x : X) â†’ p x â‰¡ â‚€) + Î  \(x : X) â†’ p x â‰¡ â‚
    c r = inr (Î» y â†’ transport (Î» - â†’ p - â‰¡ â‚) (isp x y) r)
  g (inr u) = inr (Î» x â†’ ğŸ˜-elim (u x))

\end{code}

Some closure properties now.

As a warm-up, we discuss a construction on selection functions
(X â†’ R) â†’ X, and generalized quantifiers (X â†’ R) â†’ R, which we
generalize to get closure of compact types under Î£.

\begin{code}

module warmup {U} {V} {R : V Ì‡} where

  quantifier : U Ì‡ â†’ U âŠ” V Ì‡
  quantifier X = (X â†’ R) â†’ R

  quant-prod : {X : U Ì‡} {Y : X â†’ U Ì‡} â†’ quantifier X â†’ ((x : X)  â†’ quantifier (Y x)) â†’ quantifier (Î£ Y)
  quant-prod Ï† Î³ p = Ï†(Î» x â†’ Î³ x (Î» y â†’ p(x , y)))

  selection : U Ì‡ â†’ U âŠ” V Ì‡
  selection X = (X â†’ R) â†’ X

  sel-prod : {X : U Ì‡} {Y : X â†’ U Ì‡} â†’ selection X â†’ ((x : X) â†’ selection (Y x)) â†’ selection (Î£ Y)
  sel-prod {X} {Y} Îµ Î´ p = (xâ‚€ , yâ‚€)
    where
     next : (x : X) â†’ Y x
     next x = Î´ x (Î» y â†’ p(x , y))
     xâ‚€ : X
     xâ‚€ = Îµ(Î» x â†’ p(x , next x))
     yâ‚€ : Y xâ‚€
     yâ‚€ = next xâ‚€

\end{code}

 Alternative, equivalent, construction:

\begin{code}

  overline : {X : U Ì‡} â†’ selection X â†’ quantifier X
  overline Îµ p = p(Îµ p)

  sel-prod' : {X : U Ì‡} {Y : X â†’ U Ì‡} â†’ selection X â†’ ((x : X) â†’ selection (Y x)) â†’ selection (Î£ Y)
  sel-prod' {X} {Y} Îµ Î´ p = (xâ‚€ , yâ‚€)
   where
    xâ‚€ : X
    xâ‚€ = Îµ(Î» x â†’ overline(Î´ x) (Î» y â†’ p(x , y)))
    yâ‚€ : Y xâ‚€
    yâ‚€ = Î´ xâ‚€ (Î» y â†’ p(xâ‚€ , y))

\end{code}

Back to compact sets:

\begin{code}

Î£-compactâˆ™ : âˆ€ {U V} {X : U Ì‡} {Y : X â†’ V Ì‡}
           â†’ compactâˆ™ X â†’ ((x : X) â†’ compactâˆ™(Y x)) â†’ compactâˆ™(Î£ Y)
Î£-compactâˆ™ {i} {j} {X} {Y} Îµ Î´ p = (xâ‚€ , yâ‚€) , correctness
 where
  lemma-next : (x : X) â†’ Î£ \(yâ‚€ : Y x) â†’ p(x , yâ‚€) â‰¡ â‚ â†’ (y : Y x) â†’ p(x , y) â‰¡ â‚
  lemma-next x = Î´ x (Î» y â†’ p(x , y))
  next : (x : X) â†’ Y x
  next x = prâ‚(lemma-next x)
  next-correctness : (x : X) â†’ p(x , next x) â‰¡ â‚ â†’ (y : Y x) â†’ p(x , y) â‰¡ â‚
  next-correctness x = prâ‚‚(lemma-next x)
  lemma-first : Î£ \(xâ‚€ : X) â†’ p(xâ‚€ , next xâ‚€) â‰¡ â‚ â†’ (x : X) â†’ p(x , next x) â‰¡ â‚
  lemma-first = Îµ(Î» x â†’ p(x , next x))
  xâ‚€ : X
  xâ‚€ = prâ‚ lemma-first
  first-correctness : p(xâ‚€ , next xâ‚€) â‰¡ â‚ â†’ (x : X) â†’ p(x , next x) â‰¡ â‚
  first-correctness = prâ‚‚ lemma-first
  yâ‚€ : Y xâ‚€
  yâ‚€ = next xâ‚€
  correctness : p(xâ‚€ , yâ‚€) â‰¡ â‚ â†’ (t : (Î£ \(x : X) â†’ Y x)) â†’ p t â‰¡ â‚
  correctness r (x , y) = next-correctness x (first-correctness r x) y

\end{code}

Corollary: Binary products preserve compactness:

\begin{code}

binary-Tychonoff : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ compactâˆ™ X â†’ compactâˆ™ Y â†’ compactâˆ™(X Ã— Y)
binary-Tychonoff Îµ Î´ = Î£-compactâˆ™ Îµ (Î» i â†’ Î´)

binary-Î£-compactâˆ™' : âˆ€ {U} {Xâ‚€ : U Ì‡} {Xâ‚ : U Ì‡}
                   â†’ compactâˆ™ Xâ‚€ â†’ compactâˆ™ Xâ‚ â†’ compactâˆ™(Xâ‚€ +' Xâ‚)
binary-Î£-compactâˆ™' {U} {Xâ‚€} {Xâ‚} Îµâ‚€ Îµâ‚ = Î£-compactâˆ™ ğŸš-compactâˆ™ Îµ
 where
  Îµ : (i : ğŸš) â†’ _
  Îµ â‚€ = Îµâ‚€
  Îµ â‚ = Îµâ‚

retractions-preserve-compactness : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} {f : X â†’ Y}
                                 â†’ retraction f â†’ compactâˆ™ X â†’ compactâˆ™ Y
retractions-preserve-compactness {i} {j} {X} {Y} {f} f-retract Îµ q = yâ‚€ , h
  where
   p : X â†’ ğŸš
   p x = q(f x)
   xâ‚€ : X
   xâ‚€ = prâ‚(Îµ p)
   yâ‚€ : Y
   yâ‚€ = f xâ‚€
   lemma : p xâ‚€ â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚
   lemma = prâ‚‚(Îµ p)
   h : q yâ‚€ â‰¡ â‚ â†’ (a : Y) â†’ q a â‰¡ â‚
   h r a = factâ‚ â»Â¹ âˆ™ factâ‚€
    where
     fact : Î£ \(x : X) â†’ f x â‰¡ a
     fact = f-retract a
     x : X
     x = prâ‚ fact
     factâ‚€ : q(f x) â‰¡ â‚
     factâ‚€ = lemma r x
     factâ‚ : q(f x) â‰¡ q a
     factâ‚ = ap q (prâ‚‚ fact)

retract-compactâˆ™ : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ retract Y Of X â†’ compactâˆ™ X â†’ compactâˆ™ Y
retract-compactâˆ™ (_ , Ï†) = retractions-preserve-compactness Ï†

ğŸ™+ğŸ™-compactâˆ™ : âˆ€ {U} {V} â†’ compactâˆ™ (ğŸ™ {U} + ğŸ™ {V})
ğŸ™+ğŸ™-compactâˆ™ = retract-compactâˆ™ (f , r) ğŸš-compactâˆ™
 where
  f : ğŸš â†’ ğŸ™ + ğŸ™
  f = ğŸš-cases (inl *) (inr *)
  r : (y : ğŸ™ + ğŸ™) â†’ Î£ \(x : ğŸš) â†’ f x â‰¡ y
  r (inl *) = â‚€ , refl
  r (inr *) = â‚ , refl

equiv-compactâˆ™ : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} â†’ X â‰ƒ Y â†’ compactâˆ™ X â†’ compactâˆ™ Y
equiv-compactâˆ™ (f , (g , fg) , (h , hf)) = retract-compactâˆ™ (f , (Î» y â†’ g y , fg y))

singleton-compactâˆ™ : âˆ€ {U} {X : U Ì‡} â†’ is-singleton X â†’ compactâˆ™ X
singleton-compactâˆ™ {U} {X} (x , Ï†) p = x , g
 where
  g : p x â‰¡ â‚ â†’ (y : X) â†’ p y â‰¡ â‚
  g r y = transport (Î» - â†’ p - â‰¡ â‚) (Ï† y) r

module _ (pt : PropTrunc) where

 open ImageAndSurjection (pt)

 surjection-compactâˆ™ : âˆ€ {U V} {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                     â†’ is-surjection f â†’ compactâˆ™ X â†’ compactâˆ™ Y
 surjection-compactâˆ™ {U} {V} {X} {Y} f su Îµ q = (yâ‚€ , h)
  where
   p : X â†’ ğŸš
   p = q âˆ˜ f
   xâ‚€ : X
   xâ‚€ = prâ‚(Îµ p)
   g : q(f xâ‚€) â‰¡ â‚ â†’ (x : X) â†’ q(f x) â‰¡ â‚
   g = prâ‚‚(Îµ p)
   yâ‚€ : Y
   yâ‚€ = f xâ‚€
   isp : (y : Y) â†’ is-prop (q y â‰¡ â‚)
   isp y = ğŸš-is-set
   h : q yâ‚€ â‰¡ â‚ â†’ (y : Y) â†’ q y â‰¡ â‚
   h r = surjection-induction f su (Î» y â†’ q y â‰¡ â‚) isp (g r)

 image-compactâˆ™ : âˆ€ {X Y : Uâ‚€ Ì‡} (f : X â†’ Y)
                â†’ compactâˆ™ X â†’ compactâˆ™ (image f)
 image-compactâˆ™ f = surjection-compactâˆ™ (corestriction f)
                                        (corestriction-surjection f)

\end{code}

The following is from 2011 originally in the module ExhaustibleTypes,
where "wcompact" was "exhaustible". We should remove this, or move it
to the module WeaklyCompactTypes, as wcompact is equivalent to
Î -compact.

\begin{code}

wcompact : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
wcompact X = (p : X â†’ ğŸš) â†’ Î£ \(y : ğŸš) â†’ y â‰¡ â‚ â‡” ((x : X) â†’ p x â‰¡ â‚)

\end{code}

Closer to the original definition of exhaustibility in LICS'2007 amd LMCS'2008:

\begin{code}

wcompact' : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
wcompact' X = Î£ \(A : (X â†’ ğŸš) â†’ ğŸš) â†’ (p : X â†’ ğŸš) â†’ A p â‰¡ â‚ â‡” ((x : X) â†’ p x â‰¡ â‚)

\end{code}

Because the Curry-Howard interpretation of the axiom of choice holds
in MLTT:

\begin{code}

wcompact-implies-wcompact' : âˆ€ {U} {X : U Ì‡} â†’ wcompact X â†’ wcompact' X
wcompact-implies-wcompact' {U} {X} Ï† = A , lemma
 where A : (X â†’ ğŸš) â†’ ğŸš
       A p = prâ‚(Ï† p)
       lemma : (p : X â†’ ğŸš) â†’ A p â‰¡ â‚ â‡” ((x : X) â†’ p x â‰¡ â‚)
       lemma p = prâ‚‚(Ï† p)

compact-gives-wcompact : âˆ€ {U} {X : U Ì‡} â†’ compactâˆ™ X â†’ wcompact X
compact-gives-wcompact {U} {X} Îµ p = y , (lemmaâ‚€ , lemmaâ‚)
 where
  xâ‚€ : X
  xâ‚€ = prâ‚(Îµ p)
  y : ğŸš
  y = p xâ‚€
  lemmaâ‚€ :  y â‰¡ â‚ â†’ (x : X) â†’ p x â‰¡ â‚
  lemmaâ‚€ = prâ‚‚(Îµ p)
  lemmaâ‚ : ((x : X) â†’ p x â‰¡ â‚) â†’ y â‰¡ â‚
  lemmaâ‚ h = h xâ‚€

\end{code}
