Martin Escardo 2011.

(Totally separated types moved to the module TotallySeparated January
2018, and extended.)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module DiscreteAndSeparated where

open import SpartanMLTT

open import Two-Properties
open import Plus-Properties
open import NaturalNumbers-Properties
open import DecidableAndDetachable
open import UF-Base
open import UF-Subsingletons renaming (âŠ¤Î© to âŠ¤ ; âŠ¥Î© to âŠ¥)
open import UF-Subsingletons-FunExt
open import UF-Equiv
open import UF-Retracts
open import UF-FunExt

is-isolated : {X : ğ“¤ Ì‡ } â†’ X â†’ ğ“¤ Ì‡
is-isolated x = âˆ€ y â†’ decidable (x â‰¡ y)

is-perfect : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-perfect X = is-empty (Î£ x ê‰ X , is-isolated x)

is-isolated' : {X : ğ“¤ Ì‡ } â†’ X â†’ ğ“¤ Ì‡
is-isolated' x = âˆ€ y â†’ decidable (y â‰¡ x)

decidable-eq-sym : {X : ğ“¤ Ì‡ } (x y : X) â†’ decidable (x â‰¡ y) â†’ decidable (y â‰¡ x)
decidable-eq-sym x y = cases
                        (Î» (p : x â‰¡ y) â†’ inl (p â»Â¹))
                        (Î» (n : Â¬ (x â‰¡ y)) â†’ inr (Î» (q : y â‰¡ x) â†’ n (q â»Â¹)))

is-isolated'-gives-is-isolated : {X : ğ“¤ Ì‡ } (x : X) â†’ is-isolated' x â†’ is-isolated x
is-isolated'-gives-is-isolated x i' y = decidable-eq-sym y x (i' y)

is-isolated-gives-is-isolated' : {X : ğ“¤ Ì‡ } (x : X) â†’ is-isolated x â†’ is-isolated' x
is-isolated-gives-is-isolated' x i y = decidable-eq-sym x y (i y)

is-discrete : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-discrete X = (x : X) â†’ is-isolated x

\end{code}

Standard examples:

\begin{code}

props-are-discrete : {P : ğ“¤ Ì‡ } â†’ is-prop P â†’ is-discrete P
props-are-discrete i x y = inl (i x y)

ğŸ˜-is-discrete : is-discrete (ğŸ˜ {ğ“¤})
ğŸ˜-is-discrete = props-are-discrete ğŸ˜-is-prop

ğŸ™-is-discrete : is-discrete (ğŸ™ {ğ“¤})
ğŸ™-is-discrete = props-are-discrete ğŸ™-is-prop

ğŸš-is-discrete : is-discrete ğŸš
ğŸš-is-discrete â‚€ â‚€ = inl refl
ğŸš-is-discrete â‚€ â‚ = inr (Î» (p : â‚€ â‰¡ â‚) â†’ ğŸ˜-elim (zero-is-not-one p))
ğŸš-is-discrete â‚ â‚€ = inr (Î» (p : â‚ â‰¡ â‚€) â†’ ğŸ˜-elim (zero-is-not-one (p â»Â¹)))
ğŸš-is-discrete â‚ â‚ = inl refl

â„•-is-discrete : is-discrete â„•
â„•-is-discrete 0 0 = inl refl
â„•-is-discrete 0 (succ n) = inr (Î» (p : zero â‰¡ succ n) â†’ positive-not-zero n (p â»Â¹))
â„•-is-discrete (succ m) 0 = inr (Î» (p : succ m â‰¡ zero) â†’ positive-not-zero m p)
â„•-is-discrete (succ m) (succ n) =  step (â„•-is-discrete m n)
  where
   step : (m â‰¡ n) + (m â‰¢ n) â†’ (succ m â‰¡ succ n) + (succ m â‰¢ succ n)
   step (inl r) = inl (ap succ r)
   step (inr f) = inr (Î» s â†’ f (succ-lc s))

inl-is-isolated : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (x : X)
                â†’ is-isolated x â†’ is-isolated (inl x)
inl-is-isolated {ğ“¤} {ğ“¥} {X} {Y} x i = Î³
 where
  Î³ : (z : X + Y) â†’ decidable (inl x â‰¡ z)
  Î³ (inl x') = Cases (i x')
                (Î» (p : x â‰¡ x') â†’ inl (ap inl p))
                (Î» (n : Â¬ (x â‰¡ x')) â†’ inr (contrapositive inl-lc n))
  Î³ (inr y)  = inr +disjoint

inr-is-isolated : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (y : Y)
                â†’ is-isolated y â†’ is-isolated (inr y)
inr-is-isolated {ğ“¤} {ğ“¥} {X} {Y} y i = Î³
 where
  Î³ : (z : X + Y) â†’ decidable (inr y â‰¡ z)
  Î³ (inl x)  = inr +disjoint'
  Î³ (inr y') = Cases (i y')
                (Î» (p : y â‰¡ y') â†’ inl (ap inr p))
                (Î» (n : Â¬ (y â‰¡ y')) â†’ inr (contrapositive inr-lc n))

+-is-discrete : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
              â†’ is-discrete X â†’ is-discrete Y â†’ is-discrete (X + Y)
+-is-discrete d e (inl x) = inl-is-isolated x (d x)
+-is-discrete d e (inr y) = inr-is-isolated y (e y)

\end{code}

The closure of discrete types under Î£ is proved in the module
UF-Miscelanea (as this requires to first prove that discrete types
are sets).

General properties:

\begin{code}

discrete-is-cotransitive : {X : ğ“¤ Ì‡ }
                         â†’ is-discrete X â†’ {x y z : X} â†’ x â‰¢ y â†’ (x â‰¢ z) + (z â‰¢ y)
discrete-is-cotransitive d {x} {y} {z} Ï† = f (d x z)
 where
  f : (x â‰¡ z) + (x â‰¢ z) â†’ (x â‰¢ z) + (z â‰¢ y)
  f (inl r) = inr (Î» s â†’ Ï† (r âˆ™ s))
  f (inr Î³) = inl Î³

retract-is-discrete : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                    â†’ retract Y of X â†’ is-discrete X â†’ is-discrete Y
retract-is-discrete (f , (s , Ï†)) d y y' = g (d (s y) (s y'))
 where
  g : decidable (s y â‰¡ s y') â†’ decidable (y â‰¡ y')
  g (inl p) = inl ((Ï† y) â»Â¹ âˆ™ ap f p âˆ™ Ï† y')
  g (inr u) = inr (contrapositive (ap s) u)

ğŸš-retract-of-non-trivial-type-with-isolated-point : {X : ğ“¤ Ì‡ } {xâ‚€ xâ‚ : X} â†’ xâ‚€ â‰¢ xâ‚
                                                  â†’ is-isolated xâ‚€ â†’ retract ğŸš of X
ğŸš-retract-of-non-trivial-type-with-isolated-point {ğ“¤} {X} {xâ‚€} {xâ‚} ne d = r , (s , rs)
 where
  r : X â†’ ğŸš
  r = prâ‚ (characteristic-function d)
  Ï† : (x : X) â†’ (r x â‰¡ â‚€ â†’ xâ‚€ â‰¡ x) Ã— (r x â‰¡ â‚ â†’ Â¬ (xâ‚€ â‰¡ x))
  Ï† = prâ‚‚ (characteristic-function d)
  s : ğŸš â†’ X
  s â‚€ = xâ‚€
  s â‚ = xâ‚
  rs : (n : ğŸš) â†’ r (s n) â‰¡ n
  rs â‚€ = different-from-â‚-equal-â‚€ (Î» p â†’ prâ‚‚ (Ï† xâ‚€) p refl)
  rs â‚ = different-from-â‚€-equal-â‚ Î» p â†’ ğŸ˜-elim (ne (prâ‚ (Ï† xâ‚) p))

ğŸš-retract-of-discrete : {X : ğ“¤ Ì‡ } {xâ‚€ xâ‚ : X} â†’ xâ‚€ â‰¢ xâ‚ â†’ is-discrete X â†’ retract ğŸš of X
ğŸš-retract-of-discrete {ğ“¤} {X} {xâ‚€} {xâ‚} ne d = ğŸš-retract-of-non-trivial-type-with-isolated-point ne (d xâ‚€)

\end{code}

Â¬Â¬-Separated types form an exponential ideal, assuming
extensionality. More generally:

\begin{code}

is-Â¬Â¬-separated : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-Â¬Â¬-separated X = (x y : X) â†’ Â¬Â¬-stable (x â‰¡ y)

Î -is-Â¬Â¬-separated : funext ğ“¤ ğ“¥
                  â†’ {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ }
                  â†’ ((x : X) â†’ is-Â¬Â¬-separated (Y x))
                  â†’ is-Â¬Â¬-separated (Î  Y)
Î -is-Â¬Â¬-separated fe s f g h = dfunext fe lemmaâ‚‚
 where
  lemmaâ‚€ : f â‰¡ g â†’ âˆ€ x â†’ f x â‰¡ g x
  lemmaâ‚€ r x = ap (Î» - â†’ - x) r
  lemmaâ‚ : âˆ€ x â†’ Â¬Â¬ (f x â‰¡ g x)
  lemmaâ‚ = double-negation-unshift (Â¬Â¬-functor lemmaâ‚€ h)
  lemmaâ‚‚ : âˆ€ x â†’ f x â‰¡ g x
  lemmaâ‚‚ x =  s x (f x) (g x) (lemmaâ‚ x)

discrete-is-Â¬Â¬-separated : {X : ğ“¤ Ì‡ } â†’ is-discrete X â†’ is-Â¬Â¬-separated X
discrete-is-Â¬Â¬-separated d x y = Â¬Â¬-elim (d x y)

ğŸš-is-Â¬Â¬-separated : is-Â¬Â¬-separated ğŸš
ğŸš-is-Â¬Â¬-separated = discrete-is-Â¬Â¬-separated ğŸš-is-discrete

subtype-is-Â¬Â¬-separated : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (m : X â†’ Y)
                                     â†’ left-cancellable m
                                     â†’ is-Â¬Â¬-separated Y
                                     â†’ is-Â¬Â¬-separated X
subtype-is-Â¬Â¬-separated {ğ“¤} {ğ“¥} {X} m i s x x' e = i (s (m x) (m x') (Â¬Â¬-functor (ap m) e))

\end{code}

The following is an apartness relation when Y is Â¬Â¬-separated, but we
define it without this assumption. (Are all types Â¬Â¬-separated? See
below.)

\begin{code}

infix 21 _â™¯_

_â™¯_ : {X : ğ“¤ Ì‡ } â†’ {Y : X â†’ ğ“¥ Ì‡ } â†’ (f g : (x : X) â†’ Y x) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
f â™¯ g = Î£ x ê‰ domain f , f x â‰¢ g x


apart-is-different : {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ }
                   â†’ {f g : (x : X) â†’ Y x} â†’ f â™¯ g â†’ f â‰¢ g
apart-is-different (x , Ï†) r = Ï† (ap (Î» - â†’ - x) r)


apart-is-symmetric : {X : ğ“¤ Ì‡ } â†’ {Y : X â†’ ğ“¥ Ì‡ }
                   â†’ {f g : (x : X) â†’ Y x} â†’ f â™¯ g â†’ g â™¯ f
apart-is-symmetric (x , Ï†)  = (x , (Ï† âˆ˜ _â»Â¹))

apart-is-cotransitive : {X : ğ“¤ Ì‡ } â†’ {Y : X â†’ ğ“¥ Ì‡ }
                     â†’ ((x : X) â†’ is-discrete (Y x))
                     â†’ (f g h : (x : X) â†’ Y x)
                     â†’ f â™¯ g â†’ f â™¯ h  +  h â™¯ g
apart-is-cotransitive d f g h (x , Ï†)  = lemmaâ‚ (lemmaâ‚€ Ï†)
 where
  lemmaâ‚€ : f x â‰¢ g x â†’ (f x â‰¢ h x)  +  (h x â‰¢ g x)
  lemmaâ‚€ = discrete-is-cotransitive (d x)
  lemmaâ‚ : (f x â‰¢ h x) + (h x â‰¢ g x) â†’ f â™¯ h  +  h â™¯ g
  lemmaâ‚ (inl Î³) = inl (x , Î³)
  lemmaâ‚ (inr Î´) = inr (x , Î´)

\end{code}

We now consider two cases which render the apartness relation â™¯ tight,
assuming extensionality:

\begin{code}

tight : {X : ğ“¤ Ì‡ }
      â†’ funext ğ“¤ ğ“¥
      â†’ {Y : X â†’ ğ“¥ Ì‡ }
      â†’ ((x : X) â†’ is-Â¬Â¬-separated (Y x))
      â†’ (f g : (x : X) â†’ Y x)
      â†’ Â¬ (f â™¯ g) â†’ f â‰¡ g
tight fe s f g h = dfunext fe lemmaâ‚
 where
  lemmaâ‚€ : âˆ€ x â†’ Â¬Â¬ (f x â‰¡ g x)
  lemmaâ‚€ = not-Î£-implies-Î -not h
  lemmaâ‚ : âˆ€ x â†’ f x â‰¡ g x
  lemmaâ‚ x = (s x (f x) (g x)) (lemmaâ‚€ x)

tight' : {X : ğ“¤ Ì‡ }
       â†’ funext ğ“¤ ğ“¥
       â†’ {Y : X â†’ ğ“¥ Ì‡ }
       â†’ ((x : X) â†’ is-discrete (Y x)) â†’ (f g : (x : X) â†’ Y x) â†’ Â¬ (f â™¯ g) â†’ f â‰¡ g
tight' fe d = tight fe (Î» x â†’ discrete-is-Â¬Â¬-separated (d x))

\end{code}

What about sums? The special case they reduce to binary products is
easy:

\begin{code}

binary-product-is-Â¬Â¬-separated : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                               â†’ is-Â¬Â¬-separated X
                               â†’ is-Â¬Â¬-separated Y
                               â†’ is-Â¬Â¬-separated (X Ã— Y)
binary-product-is-Â¬Â¬-separated s t (x , y) (x' , y') Ï† =
 lemma (lemmaâ‚€ Ï†) (lemmaâ‚ Ï†)
 where
  lemmaâ‚€ : Â¬Â¬ ((x , y) â‰¡ (x' , y')) â†’ x â‰¡ x'
  lemmaâ‚€ = (s x x') âˆ˜ Â¬Â¬-functor (ap prâ‚)
  lemmaâ‚ : Â¬Â¬ ((x , y) â‰¡ (x' , y')) â†’ y â‰¡ y'
  lemmaâ‚ = (t y y') âˆ˜ Â¬Â¬-functor (ap prâ‚‚)
  lemma : x â‰¡ x' â†’ y â‰¡ y' â†’ (x , y) â‰¡ (x' , y')
  lemma = apâ‚‚ (_,_)

\end{code}

This proof doesn't work for general dependent sums, because, among
other things, (ap prâ‚) doesn't make sense in that case.  A different
special case is also easy:

\begin{code}

binary-sum-is-Â¬Â¬-separated : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                           â†’ is-Â¬Â¬-separated X
                           â†’ is-Â¬Â¬-separated Y
                           â†’ is-Â¬Â¬-separated (X + Y)
binary-sum-is-Â¬Â¬-separated {ğ“¤} {ğ“¥} {X} {Y} s t (inl x) (inl x') = lemma
 where
  claim : inl x â‰¡ inl x' â†’ x â‰¡ x'
  claim = ap p
   where
    p : X + Y â†’ X
    p (inl u) = u
    p (inr v) = x

  lemma : Â¬Â¬ (inl x â‰¡ inl x') â†’ inl x â‰¡ inl x'
  lemma = ap inl âˆ˜ s x x' âˆ˜ Â¬Â¬-functor claim

binary-sum-is-Â¬Â¬-separated s t (inl x) (inr y) =  Î» Ï† â†’ ğŸ˜-elim (Ï† +disjoint )
binary-sum-is-Â¬Â¬-separated s t (inr y) (inl x)  = Î» Ï† â†’ ğŸ˜-elim (Ï† (+disjoint âˆ˜ _â»Â¹))
binary-sum-is-Â¬Â¬-separated {ğ“¤} {ğ“¥} {X} {Y} s t (inr y) (inr y') = lemma
 where
  claim : inr y â‰¡ inr y' â†’ y â‰¡ y'
  claim = ap q
   where
    q : X + Y â†’ Y
    q (inl u) = y
    q (inr v) = v

  lemma : Â¬Â¬ (inr y â‰¡ inr y') â†’ inr y â‰¡ inr y'
  lemma = (ap inr) âˆ˜ (t y y') âˆ˜ Â¬Â¬-functor claim

âŠ¥-âŠ¤-density' : funext ğ“¤ ğ“¤
             â†’ propext ğ“¤
             â†’ âˆ€ {ğ“¥} {X : ğ“¥ Ì‡ }
             â†’ is-Â¬Â¬-separated X
             â†’ (f : Î© ğ“¤ â†’ X) â†’ f âŠ¥ â‰¡ f âŠ¤
             â†’ wconstant f
âŠ¥-âŠ¤-density' fe pe s f r p q = g p âˆ™ (g q)â»Â¹
  where
    a : âˆ€ p â†’ Â¬Â¬ (f p â‰¡ f âŠ¤)
    a p t = no-truth-values-other-than-âŠ¥-or-âŠ¤ fe pe (p , (b , c))
      where
        b : p â‰¢ âŠ¥
        b u = t (ap f u âˆ™ r)

        c : p â‰¢ âŠ¤
        c u = t (ap f u)

    g : âˆ€ p â†’ f p â‰¡ f âŠ¤
    g p = s (f p) (f âŠ¤) (a p)

\end{code}

Added 19th March 2021.

\begin{code}

equality-of-Â¬Â¬stable-propositions' : propext ğ“¤
                                   â†’ (P Q : ğ“¤ Ì‡ )
                                   â†’ is-prop P
                                   â†’ is-prop Q
                                   â†’ Â¬Â¬-stable P
                                   â†’ Â¬Â¬-stable Q
                                   â†’ Â¬Â¬-stable (P â‰¡ Q)
equality-of-Â¬Â¬stable-propositions' pe P Q i j f g a = V
 where
  I : Â¬Â¬ (P â†’ Q)
  I = Â¬Â¬-functor (transport id) a

  II : P â†’ Q
  II = â†’-is-Â¬Â¬-stable g I

  III : Â¬Â¬ (Q â†’ P)
  III = Â¬Â¬-functor (transport id âˆ˜ _â»Â¹) a

  IV : Q â†’ P
  IV = â†’-is-Â¬Â¬-stable f III

  V : P â‰¡ Q
  V = pe i j II IV

equality-of-Â¬Â¬stable-propositions : funext ğ“¤ ğ“¤
                                  â†’ propext ğ“¤
                                  â†’ (p q : Î© ğ“¤)
                                  â†’ Â¬Â¬-stable (p holds)
                                  â†’ Â¬Â¬-stable (q holds)
                                  â†’ Â¬Â¬-stable (p â‰¡ q)
equality-of-Â¬Â¬stable-propositions fe pe p q f g a = Î³
 where
  Î´ : p holds â‰¡ q holds
  Î´ = equality-of-Â¬Â¬stable-propositions'
       pe (p holds) (q holds) (holds-is-prop p) (holds-is-prop q)
       f g (Â¬Â¬-functor (ap _holds) a)

  Î³ : p â‰¡ q
  Î³ = to-subtype-â‰¡ (Î» _ â†’ being-prop-is-prop fe) Î´

\end{code}

Added by Tom de Jong in January 2022.

Another logical place for these three lemmas would be Negation.lagda, but
(1) the first lemma needs _â‡”_ which is defined in GeneralNotation.lagda, which
    imports Negation.lagda;
(2) the second lemma needs _â‰ƒ_ which is only defined in UF-Equiv.lagda;
(3) the third lemma needs funext, which is only defined in UF-FunExt.lagda.

\begin{code}

Â¬Â¬-stable-â‡” : {X : ğ“¤ Ì‡  } {Y : ğ“¥ Ì‡  }
            â†’ X â‡” Y
            â†’ Â¬Â¬-stable X
            â†’ Â¬Â¬-stable Y
Â¬Â¬-stable-â‡” (f , g) Ïƒ h = f (Ïƒ (Â¬Â¬-functor g h))

Â¬Â¬-stable-â‰ƒ : {X : ğ“¤ Ì‡  } {Y : ğ“¥ Ì‡  }
            â†’ X â‰ƒ Y
            â†’ Â¬Â¬-stable X
            â†’ Â¬Â¬-stable Y
Â¬Â¬-stable-â‰ƒ e = Â¬Â¬-stable-â‡” (âŒœ e âŒ , âŒœ e âŒâ»Â¹)

being-Â¬Â¬-stable-is-prop : {X : ğ“¤ Ì‡  }
                        â†’ funext ğ“¤ ğ“¤
                        â†’ is-prop X â†’ is-prop (Â¬Â¬-stable X)
being-Â¬Â¬-stable-is-prop fe i = Î -is-prop fe (Î» _ â†’ i)

\end{code}

\begin{code}

Î©Â¬Â¬ : (ğ“¤ : Universe)  â†’ ğ“¤ âº Ì‡
Î©Â¬Â¬ ğ“¤ = Î£ p ê‰ Î© ğ“¤ , Â¬Â¬-stable (p holds)

Î©Â¬Â¬-is-Â¬Â¬-separated : funext ğ“¤ ğ“¤
                    â†’ propext ğ“¤
                    â†’ is-Â¬Â¬-separated (Î©Â¬Â¬ ğ“¤)
Î©Â¬Â¬-is-Â¬Â¬-separated fe pe (p , s) (q , t) Î½ = Î³
 where
  Î± : Â¬Â¬ (p â‰¡ q)
  Î± = Â¬Â¬-functor (ap prâ‚) Î½

  Î´ : p â‰¡ q
  Î´ = equality-of-Â¬Â¬stable-propositions fe pe p q s t Î±

  Î³ : (p , s) â‰¡ (q , t)
  Î³ = to-subtype-â‰¡ (Î» p â†’ Î -is-prop fe (Î» _ â†’ holds-is-prop p)) Î´

âŠ¥-âŠ¤-Density : funext ğ“¤ ğ“¤
            â†’ propext ğ“¤
            â†’ {X : ğ“¥ Ì‡ }
              (f : Î© ğ“¤ â†’ X)
            â†’ is-Â¬Â¬-separated X
            â†’ f âŠ¥ â‰¡ f âŠ¤
            â†’ (p : Î© ğ“¤) â†’ f p â‰¡ f âŠ¤
âŠ¥-âŠ¤-Density fe pe f s r p = s (f p) (f âŠ¤) a
 where
  a : Â¬Â¬ (f p â‰¡ f âŠ¤)
  a u = no-truth-values-other-than-âŠ¥-or-âŠ¤ fe pe (p , b , c)
   where
    b : p â‰¢ âŠ¥
    b v = u (ap f v âˆ™ r)

    c : p â‰¢ âŠ¤
    c w = u (ap f w)

âŠ¥-âŠ¤-density : funext ğ“¤ ğ“¤
            â†’ propext ğ“¤
            â†’ (f : Î© ğ“¤ â†’ ğŸš)
            â†’ f âŠ¥ â‰¡ â‚
            â†’ f âŠ¤ â‰¡ â‚
            â†’ (p : Î© ğ“¤) â†’ f p â‰¡ â‚
âŠ¥-âŠ¤-density fe pe f r s p = âŠ¥-âŠ¤-Density fe pe f ğŸš-is-Â¬Â¬-separated (r âˆ™ s â»Â¹) p âˆ™ s

\end{code}

21 March 2018

\begin{code}

qinvs-preserve-isolatedness : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) â†’ qinv f
                            â†’ (x : X) â†’ is-isolated x â†’ is-isolated (f x)
qinvs-preserve-isolatedness {ğ“¤} {ğ“¥} {X} {Y} f (g , Îµ , Î·) x i y = h (i (g y))
 where
  h : decidable (x â‰¡ g y) â†’ decidable (f x â‰¡ y)
  h (inl p) = inl (ap f p âˆ™ Î· y)
  h (inr u) = inr (contrapositive (Î» (q : f x â‰¡ y) â†’ (Îµ x)â»Â¹ âˆ™ ap g q) u)

equivs-preserve-isolatedness : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) â†’ is-equiv f
                             â†’ (x : X) â†’ is-isolated x â†’ is-isolated (f x)
equivs-preserve-isolatedness f e = qinvs-preserve-isolatedness f (equivs-are-qinvs f e)

new-point-is-isolated : {X : ğ“¤ Ì‡ } â†’ is-isolated {ğ“¤ âŠ” ğ“¥} {X + ğŸ™ {ğ“¥}} (inr â‹†)
new-point-is-isolated {ğ“¤} {ğ“¥} {X} = h
 where
  h :  (y : X + ğŸ™) â†’ decidable (inr â‹† â‰¡ y)
  h (inl x) = inr +disjoint'
  h (inr â‹†) = inl refl

\end{code}

Back to old stuff:

\begin{code}

â‰¡-indicator :  (m : â„•) â†’ Î£ p ê‰ (â„• â†’ ğŸš) , ((n : â„•) â†’ (p n â‰¡ â‚€ â†’ m â‰¢ n) Ã— (p n â‰¡ â‚ â†’ m â‰¡ n))
â‰¡-indicator m = co-characteristic-function (â„•-is-discrete m)

Ï‡â‰¡ : â„• â†’ â„• â†’ ğŸš
Ï‡â‰¡ m = prâ‚ (â‰¡-indicator m)

Ï‡â‰¡-spec : (m n : â„•) â†’ (Ï‡â‰¡ m n â‰¡ â‚€ â†’ m â‰¢ n) Ã— (Ï‡â‰¡ m n â‰¡ â‚ â†’ m â‰¡ n)
Ï‡â‰¡-spec m = prâ‚‚ (â‰¡-indicator m)

_â‰¡[â„•]_ : â„• â†’ â„• â†’ ğ“¤â‚€ Ì‡
m â‰¡[â„•] n = (Ï‡â‰¡ m n) â‰¡ â‚

infix  30 _â‰¡[â„•]_

â‰¡-agrees-with-â‰¡[â„•] : (m n : â„•) â†’ m â‰¡ n â‡” m â‰¡[â„•] n
â‰¡-agrees-with-â‰¡[â„•] m n = (Î» r â†’ different-from-â‚€-equal-â‚ (Î» s â†’ prâ‚ (Ï‡â‰¡-spec m n) s r)) , prâ‚‚ (Ï‡â‰¡-spec m n)

â‰¢-indicator :  (m : â„•) â†’ Î£ p ê‰ (â„• â†’ ğŸš) , ((n : â„•) â†’ (p n â‰¡ â‚€ â†’ m â‰¡ n) Ã— (p n â‰¡ â‚ â†’ m â‰¢ n))
â‰¢-indicator m = indicator (â„•-is-discrete m)

Ï‡â‰¢ : â„• â†’ â„• â†’ ğŸš
Ï‡â‰¢ m = prâ‚ (â‰¢-indicator m)

Ï‡â‰¢-spec : (m n : â„•) â†’ (Ï‡â‰¢ m n â‰¡ â‚€ â†’ m â‰¡ n) Ã— (Ï‡â‰¢ m n â‰¡ â‚ â†’ m â‰¢ n)
Ï‡â‰¢-spec m = prâ‚‚ (â‰¢-indicator m)

_â‰ _ : â„• â†’ â„• â†’ ğ“¤â‚€ Ì‡
m â‰  n = (Ï‡â‰¢ m n) â‰¡ â‚

infix  30 _â‰ _

â‰ -agrees-with-â‰¢ : (m n : â„•) â†’ m â‰  n â‡” m â‰¢ n
â‰ -agrees-with-â‰¢ m n = prâ‚‚ (Ï‡â‰¢-spec m n) , (Î» d â†’ different-from-â‚€-equal-â‚ (contrapositive (prâ‚ (Ï‡â‰¢-spec m n)) d))

\end{code}

Added 14th Feb 2020:

\begin{code}

discrete-exponential-has-decidable-emptiness-of-exponent : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                                                         â†’ funext ğ“¤ ğ“¥
                                                         â†’ (Î£ yâ‚€ ê‰ Y , Î£ yâ‚ ê‰ Y , yâ‚€ â‰¢ yâ‚)
                                                         â†’ is-discrete (X â†’ Y)
                                                         â†’ decidable (is-empty X)
discrete-exponential-has-decidable-emptiness-of-exponent {ğ“¤} {ğ“¥} {X} {Y} fe (yâ‚€ , yâ‚ , ne) d = Î³
 where
  a : decidable ((Î» _ â†’ yâ‚€) â‰¡ (Î» _ â†’ yâ‚))
  a = d (Î» _ â†’ yâ‚€) (Î» _ â†’ yâ‚)

  f : decidable ((Î» _ â†’ yâ‚€) â‰¡ (Î» _ â†’ yâ‚)) â†’ decidable (is-empty X)
  f (inl p) = inl g
   where
    g : is-empty X
    g x = ne q
     where
      q : yâ‚€ â‰¡ yâ‚
      q = ap (Î» - â†’ - x) p

  f (inr Î½) = inr (contrapositive g Î½)
   where
    g : is-empty X â†’ (Î» _ â†’ yâ‚€) â‰¡ (Î» _ â†’ yâ‚)
    g Î½ = dfunext fe (Î» x â†’ ğŸ˜-elim (Î½ x))

  Î³ : decidable (is-empty X)
  Î³ = f a

\end{code}
