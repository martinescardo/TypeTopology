Tom de Jong, 5 April 2022, after discussion with MartÃ­n.
(Refactoring an earlier addition dated 15 March 2022.)

The construction of set quotients in UF-Large-Quotients.lagda takes a type X : ğ“¤
and a ğ“¥-valued equivalence relation and constructs the quotient as a type in ğ“¥ âº
âŠ” ğ“¤.

If we assume Set Replacement, as defined and explained in UF-Size.lagda, then we
get a quotient in ğ“¥ âŠ” ğ“¤. In particular, for a ğ“¤-valued equivalence relation on a
type X : ğ“¤, the quotient will live in the same universe ğ“¤. This particular case
was first proved in [Corollary 5.1, Rijke2017], but under a different
replacement assumption (again, see UF-Size.lagda for details).

[Rijke2017]  Egbert Rijke. The join construction.
             https://arxiv.org/abs/1701.07538, January 2017.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import UF-FunExt
open import UF-PropTrunc
open import UF-Subsingletons

module UF-Quotient-Replacement
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
       where

open import SpartanMLTT

open import UF-Base hiding (_â‰ˆ_)
open import UF-Subsingletons-FunExt
open import UF-ImageAndSurjection
open import UF-Equiv

open import UF-Large-Quotient pt fe pe
open import UF-Quotient using (set-quotients-exist)
open import UF-Size

module _
        (R : Set-Replacement pt)
        {X : ğ“¤ Ì‡  }
        (â‰‹@(_â‰ˆ_ , â‰ˆp , â‰ˆr , â‰ˆs , â‰ˆt) : EqRel {ğ“¤} {ğ“¥} X)
       where

 open import UF-Equiv
 open import UF-EquivalenceExamples

 abstract
  resize-set-quotient : (X / â‰‹) is (ğ“¤ âŠ” ğ“¥) small
  resize-set-quotient = R equiv-rel (X , (â‰ƒ-refl X)) Î³
                          (powersets-are-sets'' fe fe pe)
   where
    open quotient X _â‰ˆ_ â‰ˆp â‰ˆr â‰ˆs â‰ˆt using (equiv-rel)
    Î³ : (X â†’ Î© ğ“¥) is-locally ğ“¤ âŠ” ğ“¥ small
    Î³ f g = S , â‰ƒ-sym e
     where
      S : ğ“¤ âŠ” ğ“¥ Ì‡
      S = (x : X) â†’ f x holds â‡” g x holds
      e : (f â‰¡ g) â‰ƒ S
      e = (f â‰¡ g) â‰ƒâŸ¨ â‰ƒ-funext fe f g âŸ©
          f âˆ¼ g   â‰ƒâŸ¨ I âŸ©
          S       â– 
       where
        I : (f âˆ¼ g) â‰ƒ S
        I = Î -cong fe fe X (Î» x â†’ f x â‰¡ g x) (Î» x â†’ f x holds â‡” g x holds) II
         where
          II : (x : X) â†’ (f x â‰¡ g x) â‰ƒ (f x holds â‡” g x holds)
          II x = logically-equivalent-props-are-equivalent
                  (Î©-is-set fe pe)
                  (Ã—-is-prop (Î -is-prop fe (Î» _ â†’ holds-is-prop (g x)))
                             (Î -is-prop fe (Î» _ â†’ holds-is-prop (f x))))
                  (Î» p â†’ transport _holds p , transportâ»Â¹ _holds p)
                  (Î» (u , v) â†’ Î©-extensionality fe pe u v)

\end{code}

We now use the above resizing to construct a quotient that strictly lives in the
universe ğ“¤ âŠ” ğ“¥, yielding set quotients as defined in
UF-Quotient.lagda.

\begin{code}

 X/â‚›â‰ˆ : ğ“¤ âŠ” ğ“¥ Ì‡
 X/â‚›â‰ˆ = prâ‚ resize-set-quotient
 Ï† : X/â‚›â‰ˆ â‰ƒ X / â‰‹
 Ï† = prâ‚‚ resize-set-quotient
 Î·/â‚› : X â†’ X/â‚›â‰ˆ
 Î·/â‚› = âŒœ Ï† âŒâ»Â¹  âˆ˜ Î·/ â‰‹
 Î·/â‚›-identifies-related-points : identifies-related-points â‰‹ Î·/â‚›
 Î·/â‚›-identifies-related-points e = ap âŒœ Ï† âŒâ»Â¹ (Î·/-identifies-related-points â‰‹ e)
 /â‚›-is-set : is-set (X/â‚›â‰ˆ)
 /â‚›-is-set = equiv-to-set Ï† (quotient-is-set â‰‹)
 /â‚›-induction : âˆ€ {ğ“¦} {P : X/â‚›â‰ˆ â†’ ğ“¦ Ì‡ }
              â†’ ((x' : X/â‚›â‰ˆ) â†’ is-prop (P x'))
              â†’ ((x : X) â†’ P (Î·/â‚› x))
              â†’ (x' : X/â‚›â‰ˆ) â†’ P x'
 /â‚›-induction {ğ“¦} {P} i h x' = transport P e (Î³ (âŒœ Ï† âŒ x'))
  where
   P' : X / â‰‹ â†’ ğ“¦ Ì‡
   P' = P âˆ˜ âŒœ Ï† âŒâ»Â¹
   Î³ : (y : X / â‰‹) â†’ P' y
   Î³ = /-induction' â‰‹ (Î» y â†’ i (âŒœ Ï† âŒâ»Â¹ y)) h
   e : âŒœ Ï† âŒâ»Â¹ (âŒœ Ï† âŒ x') â‰¡ x'
   e = â‰ƒ-sym-is-linv Ï† x'
 /â‚›-universality : {A : ğ“¦ Ì‡  } â†’ is-set A
                 â†’ (f : X â†’ A)
                 â†’ identifies-related-points â‰‹ f
                 â†’ âˆƒ! f' ê‰ (X/â‚›â‰ˆ â†’ A), f' âˆ˜ Î·/â‚› âˆ¼ f
 /â‚›-universality {ğ“¦} {A} i f p =
  equiv-to-singleton (â‰ƒ-sym e) (universal-property/ â‰‹ i f p)
   where
    e = (Î£ f' ê‰ (X / â‰‹ â†’ A)  , f' âˆ˜ Î·/ â‰‹ â‰¡ f)        â‰ƒâŸ¨ â¦…1â¦† âŸ©
        (Î£ f' ê‰ (X / â‰‹ â†’ A)  , f' âˆ˜ Î·/ â‰‹ âˆ¼ f)        â‰ƒâŸ¨ â¦…2â¦† âŸ©
        (Î£ f' ê‰ (X / â‰‹ â†’ A)  , f' âˆ˜ âŒœ Ï† âŒ âˆ˜ Î·/â‚› âˆ¼ f) â‰ƒâŸ¨ â¦…3â¦† âŸ©
        (Î£ f' ê‰ (X/â‚›â‰ˆ â†’ A) , f' âˆ˜ Î·/â‚› âˆ¼ f)         â– 
     where
      â¦…1â¦† = Î£-cong (Î» f' â†’ â‰ƒ-funext fe (f' âˆ˜ Î·/ â‰‹) f)
      â¦…2â¦† = Î£-cong
            (Î» f' â†’ Î -cong fe fe X _ _
                    (Î» x â†’ â‰¡-cong-l (f' (Î·/ â‰‹ x)) (f x)
                                    (ap f' ((â‰ƒ-sym-is-rinv Ï† (Î·/ â‰‹ x)) â»Â¹))))
      â¦…3â¦† = Î£-change-of-variable _ (_âˆ˜ âŒœ Ï† âŒ)
            (qinvs-are-equivs (_âˆ˜ âŒœ Ï† âŒ)
              (qinv-pre (Î» _ _ â†’ dfunext fe) âŒœ Ï† âŒ
               (equivs-are-qinvs âŒœ Ï† âŒ (âŒœâŒ-is-equiv Ï†))))
       where
        open import UF-Equiv-FunExt using (qinv-pre)

set-replacement-gives-set-quotients : Set-Replacement pt â†’ set-quotients-exist
set-replacement-gives-set-quotients R = record
 { _/_                          = Î» X â†’ X/â‚›â‰ˆ R
 ; Î·/                           = Î·/â‚› R
 ; Î·/-identifies-related-points = Î·/â‚›-identifies-related-points R
 ; /-is-set                     = /â‚›-is-set R
 ; /-induction                  = /â‚›-induction R
 ; /-universality               = /â‚›-universality R
 }

\end{code}
