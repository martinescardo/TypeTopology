Martin Escardo

UF things that depend on non-UF things.

\begin{code}

module UF-SetExamples where

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons
open import UF-FunExt
open import UF-Subsingletons-FunExt

decidable-is-collapsible : âˆ€ {U} {X : U Ì‡} â†’ decidable X â†’ collapsible X
decidable-is-collapsible (inl x) = inhabited-is-collapsible x
decidable-is-collapsible (inr u) = is-empty-is-collapsible u

open import DiscreteAndSeparated

discrete-is-identification-collapsible : âˆ€ {U} {X : U Ì‡} â†’ discrete X â†’ identification-collapsible X
discrete-is-identification-collapsible d = decidable-is-collapsible (d _ _)

discrete-is-set : âˆ€ {U} {X : U Ì‡} â†’ discrete X â†’ is-set X
discrete-is-set d = identification-collapsible-is-set(discrete-is-identification-collapsible d)

discrete-inl : âˆ€ {U} {X : U Ì‡} (d : discrete X) (x y : X) (r : x â‰¡ y) â†’ d x y â‰¡ inl r
discrete-inl d x y r =
  equality-cases (d x y)
    (Î» (p : x â‰¡ y) (q : d x y â‰¡ inl p) â†’ q âˆ™ ap inl (discrete-is-set d p r))
    (Î» (h : Â¬(x â‰¡ y)) (q : d x y â‰¡ inr h) â†’ ğŸ˜-elim(h r))

discrete-inr : âˆ€ {U} {X : U Ì‡} â†’ funext U Uâ‚€
            â†’ (d : discrete X) (x y : X) (n : Â¬(x â‰¡ y)) â†’ d x y â‰¡ inr n
discrete-inr fe d x y n =
  equality-cases (d x y)
    (Î» (p : x â‰¡ y) (q : d x y â‰¡ inl p) â†’ ğŸ˜-elim (n p))
    (Î» (m : Â¬(x â‰¡ y)) (q : d x y â‰¡ inr m) â†’ q âˆ™ ap inr (nfunext fe (Î» (p : x â‰¡ y) â†’ ğŸ˜-elim (m p))))

isolated-Id-is-prop : âˆ€ {U} {X : U Ì‡} (x : X) â†’ isolated' x â†’ (y : X) â†’ is-prop (y â‰¡ x)
isolated-Id-is-prop x i = local-hedberg' x (Î» y â†’ decidable-is-collapsible (i y))

Î£-discrete : âˆ€ {U} {X : U Ì‡} â†’ {Y : X â†’ U Ì‡}
          â†’ discrete X â†’ ((x : X) â†’ discrete(Y x)) â†’ discrete(Î£ Y)
Î£-discrete {U} {X} {Y} d e (x , y) (x' , y') = g (d x x')
 where
  g : decidable(x â‰¡ x') â†’ decidable(x , y â‰¡ x' , y')
  g (inl p) = f (e x' (transport Y p y) y')
   where
    f : decidable(transport Y p y â‰¡ y') â†’ decidable((x , y) â‰¡ (x' , y'))
    f (inl q) = inl (to-Î£-â‰¡ (p , q))
    f (inr Ïˆ) = inr c
     where
      c : x , y â‰¡ x' , y' â†’ ğŸ˜
      c r = Ïˆ q
       where
        p' : x â‰¡ x'
        p' = ap prâ‚ r
        q' : transport Y p' y â‰¡ y'
        q' = from-Î£-â‰¡ r
        s : p â‰¡ p'
        s = discrete-is-set d p p'
        q : transport Y p y â‰¡ y'
        q = ap (Î» - â†’ transport Y - y) s âˆ™ q'
  g (inr Ï†) = inr (Î» q â†’ Ï† (ap prâ‚ q))

ğŸš-is-set : is-set ğŸš
ğŸš-is-set = discrete-is-set ğŸš-discrete

â„•-is-set : is-set â„•
â„•-is-set = discrete-is-set â„•-discrete

nonempty : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
nonempty X = is-empty(is-empty X)

stable : âˆ€ {U} â†’ U Ì‡ â†’ U Ì‡
stable X = nonempty X â†’ X

decidable-is-stable : âˆ€ {U} {X : U Ì‡} â†’ decidable X â†’ stable X
decidable-is-stable (inl x) Ï† = x
decidable-is-stable (inr u) Ï† = unique-from-ğŸ˜(Ï† u)

stable-is-collapsible : âˆ€ {U} â†’ funext U Uâ‚€ â†’ {X : U Ì‡} â†’ stable X â†’ collapsible X
stable-is-collapsible {U} fe {X} s = (f , g)
 where
  f : X â†’ X
  f x = s(Î» u â†’ u x)
  claimâ‚€ : (x y : X) â†’ (u : is-empty X) â†’ u x â‰¡ u y
  claimâ‚€ x y u = unique-from-ğŸ˜(u x)
  claimâ‚ : (x y : X) â†’ (Î» u â†’ u x) â‰¡ (Î» u â†’ u y)
  claimâ‚ x y = dfunext fe (claimâ‚€ x y)
  g : (x y : X) â†’ f x â‰¡ f y
  g x y = ap s (claimâ‚ x y)

separated-is-identification-collapsible : âˆ€ {U} â†’ funext U Uâ‚€ â†’ {X : U Ì‡} â†’ separated X â†’ identification-collapsible X
separated-is-identification-collapsible fe s = stable-is-collapsible fe (s _ _)

separated-is-set : âˆ€ {U} â†’ funext U Uâ‚€ â†’ {X : U Ì‡} â†’ separated X â†’ is-set X
separated-is-set fe s = identification-collapsible-is-set (separated-is-identification-collapsible fe s)

is-prop-separated : âˆ€ {U} â†’ funext U U â†’ funext U Uâ‚€ â†’ {X : U Ì‡} â†’ is-prop(separated X)
is-prop-separated fe feâ‚€ {X} = iis-prop-is-prop f
 where
  f : separated X â†’ is-prop(separated X)
  f s = Î -is-prop fe
          (Î» _ â†’ Î -is-prop fe
                    (Î» _ â†’ Î -is-prop fe
                              (Î» _ â†’ separated-is-set feâ‚€ s)))

\end{code}
