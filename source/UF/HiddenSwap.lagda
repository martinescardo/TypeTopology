Martin Escardo, 1st September 2023

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt
open import UF.PropTrunc

module UF.HiddenSwap
        (fe : Fun-Ext)
        (pt : propositional-truncations-exist)
       where

open import MLTT.Spartan
open import MLTT.Two-Properties
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

open PropositionalTruncation pt

\end{code}

What is noteworthy about the following is that, without knowing a
specific equivalence of X with ğŸš, so that, in particular, we cannot
get any particular point of X, we can still swap the two unknown
points of X, so to speak.

\begin{code}

hidden-swap : {X : ğ“¤ Ì‡ }
            â†’ âˆ¥ X â‰ƒ ğŸš âˆ¥
            â†’ Î£ f ê‰ (X â†’ X) , (f â‰  id) Ã— (f âˆ˜ f âˆ¼ id)
hidden-swap {ğ“¤} {X} s = VII
 where
  I : (x : X) â†’ X â‰ƒ ğŸš â†’ Î£ y ê‰ X , x â‰  y
  I x ğ•˜ = âŒœ ğ•˜ âŒâ»Â¹ (complement (âŒœ ğ•˜ âŒ x)) , Iâ‚€
   where
    Iâ‚€ : x â‰  âŒœ ğ•˜ âŒâ»Â¹ (complement (âŒœ ğ•˜ âŒ x))
    Iâ‚€ p = complement-no-fp (âŒœ ğ•˜ âŒ x) Iâ‚
     where
      Iâ‚ = âŒœ ğ•˜ âŒ x                                 ï¼âŸ¨ ap âŒœ ğ•˜ âŒ p âŸ©
            âŒœ ğ•˜ âŒ (âŒœ ğ•˜ âŒâ»Â¹ (complement (âŒœ ğ•˜ âŒ x))) ï¼âŸ¨ Iâ‚‚ âŸ©
            (complement (âŒœ ğ•˜ âŒ x))                 âˆ
            where
             Iâ‚‚ = inverses-are-sections âŒœ ğ•˜ âŒ âŒœ ğ•˜ âŒ-is-equiv _

  X-is-set : is-set X
  X-is-set = âˆ¥âˆ¥-rec (being-set-is-prop fe) (Î» ğ•˜ â†’ equiv-to-set ğ•˜ ğŸš-is-set) s

  II : (x y y' : X) â†’ x â‰  y â†’ x â‰  y' â†’ y ï¼ y'
  II x y y' Î½ Î½' = âˆ¥âˆ¥-rec X-is-set (Î» ğ•˜ â†’ d' ğ•˜ x y y' Î½ Î½') s
   where
    d' : X â‰ƒ ğŸš â†’ (x y y' : X) â†’ x â‰  y â†’ x â‰  y' â†’ y ï¼ y'
    d' ğ•˜ x y y' Î½ Î½' = equivs-are-lc âŒœ ğ•˜ âŒ âŒœ ğ•˜ âŒ-is-equiv IIâ‚€
     where
      IIâ‚€ : âŒœ ğ•˜ âŒ y ï¼ âŒœ ğ•˜ âŒ y'
      IIâ‚€ = ğŸš-things-distinct-from-a-third-are-equal (âŒœ ğ•˜ âŒ y) (âŒœ ğ•˜ âŒ y') (âŒœ ğ•˜ âŒ x)
             (Î» (p : âŒœ ğ•˜ âŒ y ï¼ âŒœ ğ•˜ âŒ x)
                   â†’ Î½ (equivs-are-lc âŒœ ğ•˜ âŒ âŒœ ğ•˜ âŒ-is-equiv (p â»Â¹)))
             (Î» (p : âŒœ ğ•˜ âŒ y' ï¼ âŒœ ğ•˜ âŒ x)
                   â†’ Î½' (equivs-are-lc âŒœ ğ•˜ âŒ âŒœ ğ•˜ âŒ-is-equiv (p â»Â¹)))

  III : (x : X) â†’ is-prop (Î£ y ê‰ X , x â‰  y)
  III x (y , Î½) (y' , Î½') =
   to-subtype-ï¼ (Î» x â†’ negations-are-props fe) (II x y y' Î½ Î½')

  IV : (x : X) â†’ Î£ y ê‰ X , x â‰  y
  IV x = âˆ¥âˆ¥-rec (III x) (I x) s

  f : X â†’ X
  f x = prâ‚ (IV x)

  V : f âˆ˜ f âˆ¼ id
  V x = Vâ‚‚
   where
    Vâ‚€ : x â‰  f x
    Vâ‚€ = prâ‚‚ (IV x)

    Vâ‚ : f x â‰  f (f x)
    Vâ‚ = prâ‚‚ (IV (f x))

    Vâ‚‚ : f (f x) ï¼ x
    Vâ‚‚ = II (f x) (f (f x)) x Vâ‚ (â‰ -sym Vâ‚€)

  VI : f â‰  id
  VI p = VIâ‚
   where
    VIâ‚€ : âˆƒ x ê‰ X , (x â‰  f x)
    VIâ‚€ = âˆ¥âˆ¥-rec âˆƒ-is-prop (Î» ğ•˜ â†’ âˆ£ âŒœ ğ•˜ âŒâ»Â¹ â‚€ , prâ‚‚ (IV (âŒœ ğ•˜ âŒâ»Â¹ â‚€)) âˆ£) s

    VIâ‚ : ğŸ˜
    VIâ‚ = âˆ¥âˆ¥-rec ğŸ˜-is-prop (Î» (x , Î½) â†’ Î½ (happly (p â»Â¹) x)) VIâ‚€

  VII :  Î£ f ê‰ (X â†’ X) , (f â‰  id) Ã— (f âˆ˜ f âˆ¼ id)
  VII = f , VI , V

\end{code}

Because involutions are equivalences, we get the following.

\begin{code}

hidden-swap-corollary : {X : ğ“¤ Ì‡ }
                      â†’ âˆ¥ X â‰ƒ ğŸš âˆ¥
                      â†’ Î£ ğ•— ê‰ X â‰ƒ X , âŒœ ğ•— âŒ â‰  id
hidden-swap-corollary {ğ“¤} {X} s = I (hidden-swap s)
 where
  I : (Î£ f ê‰ (X â†’ X) , (f â‰  id) Ã— (f âˆ˜ f âˆ¼ id))
    â†’ Î£ ğ•— ê‰ X â‰ƒ X , (âŒœ ğ•— âŒ â‰  id)
  I (f , Î½ , i) = qinveq f (f , i , i) , Î½

\end{code}

The above is a solution to exercises proposed on
https://mathstodon.xyz/@MartinEscardo/110991799307299727

An independent solution by github user Seiryn21 is at
https://gist.github.com/Seiryn21/4173b1ee0b88be7b5a6054ac3222c8e1
