Tom de Jong, 28 October 2022 - 7 November 2022.
In collaboration with Nicolai Kraus, Fredrik Nordvall Forsberg and Chuangjie Xu.

We define the induction principle (with a non-judgemental computation principle)
of the cumulative hierarchy ğ• (with respect to a type universe ğ“¤) as introduced
in Section 10.5 of the HoTT Book [1]. Using the induction principle we formulate
what it means for the cumulative hierarchy to exist, so that can use it as an
(module) assumption in further developments.

For example, in Ordinals/CumulativeHierarchy we show that the (type theoretic)
ordinal of set theoretic ordinals in ğ• (w.r.t. ğ“¤) is isomorphic to the ordinal
of ordinals in ğ“¤.

This file has three parts:
(I)    Introduction of the cumulative hierarchy ğ• and the statement of its
       (most general) induction principle.
(II)   Statements and proofs of some simpler, more specialised, induction and
       recursion principles for ğ•.
(III)  Basic constructions and proofs for ğ•, i.e. the definition of set
       membership (âˆˆ), subset relation (âŠ†) and proofs of âˆˆ-extensionality and
       âˆˆ-induction.

The cumulative hierarchy ğ• can be seen as a HoTT-refined version of Aczel's [2]
type theoretic interpretation of constructive set theory and draws inspiration
form Joyal and Moerdijk's [3] algebraic set theory.

References
----------

[1] The Univalent Foundations Program
    Homotopy Type Theory: Univalent Foundations of Mathematics
    https://homotopytypetheory.org/book
    Institute for Advanced Study
    2013

[2] Peter Aczel
    The type theoretic interpretation of constructive set theory
    In A. MacIntyre, L. Pacholski, and J. Paris (eds.) Logic Colloquium â€™77
    Volume 96 of Studies in Logic and the Foundations of Mathematics
    Pages 55â€“66
    North-Holland Publishing Company
    1978
    doi:10.1016/S0049-237X(08)71989-X

[3] A. Joyal and I. Moerdijk
    Algebraic set theory
    Volume 220 of London Mathematical Society Lecture Note Series
    Cambridge University Press
    1995
    doi:10.1017/CBO9780511752483

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons

module UF.CumulativeHierarchy
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
       where

open PropositionalTruncation pt

open import MLTT.Spartan
open import UF.Base hiding (_â‰ˆ_)
open import UF.Sets
open import UF.SubtypeClassifier
open import UF.SubtypeClassifier-Properties
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties

_â‰²_ : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } {X : ğ“£ Ì‡ } â†’ (A â†’ X) â†’ (B â†’ X) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“£ Ì‡
_â‰²_ {ğ“¤} {ğ“¥} {ğ“£} {A} {B} f g = (a : A) â†’ âˆƒ b ê‰ B , g b ï¼ f a

-- Note that _â‰ˆ_ says that f and g have equal images
_â‰ˆ_ : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } {X : ğ“£ Ì‡ } â†’ (A â†’ X) â†’ (B â†’ X) â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“£ Ì‡
f â‰ˆ g = f â‰² g Ã— g â‰² f

â‰ˆ-sym : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } {X : ğ“£ Ì‡ } {f : A â†’ X} {g : B â†’ X}
      â†’ f â‰ˆ g â†’ g â‰ˆ f
â‰ˆ-sym (u , v) = (v , u)

\end{code}

Part I
------

Introduction of the cumulative hierarchy ğ• and the statement of its (most
general) induction principle.

See Section 10.5 of the HoTT Book [1] for more of an explanation regarding the
induction principle of ğ•.

For comparison, the higher inductive type (HIT) presentation of ğ• (w.r.t. ğ“¤) is:
  âˆ™ For every A : ğ“¤ and f : A â†’ ğ•, we have an element ğ•-set f : ğ•
  âˆ™ For every A, B : ğ“¤, f : A â†’ ğ• and g : B â†’ ğ•, if f â‰ˆ g, then ğ•-set f ï¼ ğ•-set g
  âˆ™ ğ• is set-truncated: for every x, y : ğ• and p, q : x ï¼ y, we have p ï¼ q.

We require that the type ğ• is a set in the sense of HoTT, i.e. its elements are
equal in at most one way. In the set theoretic sense it is of course a proper
class and not a set: the type ğ• lives in the next type universe ğ“¤ âº. To try to
avoid confusion, we explicitly introduce the definition "is-large-set" below, as
suggested by MartÃ­n EscardÃ³.

\begin{code}

module _ (ğ“¤ : Universe) where

 is-large-set : ğ“¤ âº Ì‡ â†’ ğ“¤ âº Ì‡
 is-large-set = is-set

 record cumulative-hierarchy-exists : ğ“¤Ï‰ where
  field
   ğ• : ğ“¤ âº Ì‡
   ğ•-is-large-set : is-large-set ğ•
   ğ•-set : {A : ğ“¤ Ì‡ } â†’ (A â†’ ğ•) â†’ ğ•
   ğ•-set-ext : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•) â†’ f â‰ˆ g â†’ ğ•-set f ï¼ ğ•-set g
   ğ•-induction : {ğ“£ : Universe} (P : ğ• â†’ ğ“£ Ì‡ )
               â†’ ((x : ğ•) â†’ is-set (P x))
               â†’ (Ï : {A : ğ“¤ Ì‡ } (f : A â†’ ğ• ) â†’ ((a : A) â†’ P (f a)) â†’ P (ğ•-set f))
               â†’ ({A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•) (e : f â‰ˆ g)
                   â†’ (IHâ‚ : (a : A) â†’ P (f a))
                   â†’ (IHâ‚‚ : (b : B) â†’ P (g b))
                   â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b ,
                                    transport P p (IHâ‚ a) ï¼ IHâ‚‚ b âˆ¥)
                   â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a ,
                                    transport P p (IHâ‚‚ b) ï¼ IHâ‚ a âˆ¥)
                   â†’ transport P (ğ•-set-ext f g e) (Ï f IHâ‚) ï¼ Ï g IHâ‚‚)
               â†’ (x : ğ•) â†’ P x
   ğ•-induction-computes : {ğ“£ : Universe} (P : ğ• â†’ ğ“£ Ì‡ )
                        â†’ (Ïƒ : (x : ğ•) â†’ is-set (P x))
                        â†’ (Ï : {A : ğ“¤ Ì‡ } (f : A â†’ ğ• ) â†’ ((a : A) â†’ P (f a))
                                                        â†’ P (ğ•-set f))
                        â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•) (e : f â‰ˆ g)
                             â†’ (IHâ‚ : (a : A) â†’ P (f a))
                             â†’ (IHâ‚‚ : (b : B) â†’ P (g b))
                             â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b ,
                                              transport P p (IHâ‚ a) ï¼ IHâ‚‚ b âˆ¥)
                             â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a ,
                                              transport P p (IHâ‚‚ b) ï¼ IHâ‚ a âˆ¥)
                             â†’ transport P (ğ•-set-ext f g e) (Ï f IHâ‚) ï¼ Ï g IHâ‚‚)
                        â†’ {A : ğ“¤ Ì‡ } (f : A â†’ ğ•) (IH : (a : A) â†’ P (f a))
                           â†’ ğ•-induction P Ïƒ Ï Ï„ (ğ•-set f) ï¼ Ï f IH

\end{code}

Part II
-------

Statements and proofs of some simpler, more specialised, induction and recursion
principles for ğ•.

We start with deriving the recursion principle for ğ•, i.e. its nondependent
induction principle. It should be noted that this is completely routine.

\begin{code}

  ğ•-recursion-with-computation :
     {ğ“£ : Universe} {X : ğ“£ Ì‡ }
   â†’ (Ïƒ : is-set X)
   â†’ (Ï : {A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ X) â†’ X)
   â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
        â†’ (IHâ‚ : A â†’ X)
        â†’ (IHâ‚‚ : B â†’ X)
        â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
        â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a , IHâ‚‚ b ï¼ IHâ‚ a âˆ¥)
        â†’ f â‰ˆ g â†’ Ï f IHâ‚ ï¼ Ï g IHâ‚‚)
   â†’ Î£ Ï• ê‰ (ğ• â†’ X) , ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•)
                      (IH : A â†’ X) â†’ Ï• (ğ•-set f) ï¼ Ï f IH)
  ğ•-recursion-with-computation {ğ“£} {X} Ïƒ Ï Ï„ =
   ( ğ•-induction (Î» _ â†’ X) (Î» _ â†’ Ïƒ) Ï Ï„'
   , ğ•-induction-computes (Î» _ â†’ X) (Î» _ â†’ Ïƒ) Ï Ï„')
      where
       Ï„' : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
          â†’ (e : f â‰ˆ g) (IHâ‚ : A â†’ X) (IHâ‚‚ : B â†’ X)
          â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b ,
                           transport (Î» _ â†’ X) p (IHâ‚ a) ï¼ IHâ‚‚ b âˆ¥)
          â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a ,
                           transport (Î» _ â†’ X) p (IHâ‚‚ b) ï¼ IHâ‚ a âˆ¥)
          â†’ transport (Î» _ â†’ X) (ğ•-set-ext f g e) (Ï f IHâ‚) ï¼ Ï g IHâ‚‚
       Ï„' {A} {B} f g e IHâ‚ IHâ‚‚ hIHâ‚ hIHâ‚‚ =
        transport (Î» _ â†’ X) e' (Ï f IHâ‚) ï¼âŸ¨ transport-const e'          âŸ©
        Ï f IHâ‚                          ï¼âŸ¨ Ï„ f g IHâ‚ IHâ‚‚ hIHâ‚' hIHâ‚‚' e âŸ©
        Ï g IHâ‚‚                          âˆ
         where
          e' = ğ•-set-ext f g e
          hIHâ‚' : (a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥
          hIHâ‚' a = âˆ¥âˆ¥-functor
                     (Î» (b , p , q) â†’ (b , p , ((transport-const p) â»Â¹ âˆ™ q)))
                     (hIHâ‚ a)
          hIHâ‚‚' : (b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a , IHâ‚‚ b ï¼ IHâ‚ a âˆ¥
          hIHâ‚‚' b = âˆ¥âˆ¥-functor
                     (Î» (a , p , q) â†’ (a , p , ((transport-const p) â»Â¹ âˆ™ q)))
                     (hIHâ‚‚ b)

  ğ•-recursion : {ğ“£ : Universe} {X : ğ“£ Ì‡ }
              â†’ is-set X
              â†’ (Ï : ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ X) â†’ X))
              â†’ ({A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
                  â†’ (IHâ‚ : A â†’ X) (IHâ‚‚ : B â†’ X)
                  â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
                  â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a , IHâ‚‚ b ï¼ IHâ‚ a âˆ¥)
                  â†’ f â‰ˆ g â†’ Ï f IHâ‚ ï¼ Ï g IHâ‚‚)
              â†’ ğ• â†’ X
  ğ•-recursion Ïƒ Ï Ï„ = prâ‚ (ğ•-recursion-with-computation Ïƒ Ï Ï„)

  ğ•-recursion-computes :
      {ğ“£ : Universe} {X : ğ“£ Ì‡ }
    â†’ (Ïƒ : is-set X)
    â†’ (Ï : {A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ X) â†’ X)
    â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ (IHâ‚ : A â†’ X) (IHâ‚‚ : B â†’ X)
         â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
         â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a , IHâ‚‚ b ï¼ IHâ‚ a âˆ¥)
         â†’ f â‰ˆ g â†’ Ï f IHâ‚ ï¼ Ï g IHâ‚‚)
    â†’ ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) (IH : A â†’ X)
        â†’ ğ•-recursion Ïƒ Ï Ï„ (ğ•-set f) ï¼ Ï f IH)
  ğ•-recursion-computes Ïƒ Ï Ï„ f = prâ‚‚ (ğ•-recursion-with-computation Ïƒ Ï Ï„) f

\end{code}

Next, we observe that when P is a family of propositions, then the induction
principle simplifies significantly.

\begin{code}

  ğ•-prop-induction : {ğ“£ : Universe} (P : ğ• â†’ ğ“£ Ì‡ )
                   â†’ ((x : ğ•) â†’ is-prop (P x))
                   â†’ ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ ((a : A) â†’ P (f a)) â†’ P (ğ•-set f))
                   â†’ (x : ğ•) â†’ P x
  ğ•-prop-induction {ğ“£} P P-is-prop-valued Ï =
   ğ•-induction P (Î» x â†’ props-are-sets (P-is-prop-valued x)) Ï
                 (Î» f g e IHâ‚ IHâ‚‚ _ _ â†’ P-is-prop-valued _ _ _)


  ğ•-prop-simple-induction : {ğ“£ : Universe} (P : ğ• â†’ ğ“£ Ì‡ )
                          â†’ ((x : ğ•) â†’ is-prop (P x))
                          â†’ ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ P (ğ•-set f))
                          â†’ (x : ğ•) â†’ P x
  ğ•-prop-simple-induction P Ïƒ Ï = ğ•-prop-induction P Ïƒ (Î» f _ â†’ Ï f)

\end{code}

Because implication makes the set Î© into a poset, we can give specialised
recursion principles for ğ• â†’ Î© by (roughly) asking that â‰² is mapped to
implication.

\begin{code}

  private
   ğ•-prop-recursion-with-computation :
      {ğ“£ : Universe}
    â†’ (Ï : ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ Î© ğ“£) â†’ Î© ğ“£))
    â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ (IHâ‚ : A â†’ Î© ğ“£) (IHâ‚‚ : B â†’ Î© ğ“£)
         â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
         â†’ f â‰² g â†’ Ï f IHâ‚ holds â†’ Ï g IHâ‚‚ holds)
    â†’ Î£ Ï• ê‰ (ğ• â†’ Î© ğ“£) , ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) (IH : A â†’ Î© ğ“£)
                      â†’ Ï• (ğ•-set f) ï¼ Ï f IH)
   ğ•-prop-recursion-with-computation {ğ“£} Ï Ï„ =
    ( ğ•-recursion (Î©-is-set fe pe) Ï Ï„'
    , ğ•-recursion-computes (Î©-is-set fe pe) Ï Ï„')
     where
      Ï„' : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ (IHâ‚ : A â†’ Î© ğ“£) (IHâ‚‚ : B â†’ Î© ğ“£)
         â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
         â†’ ((b : B) â†’ âˆ¥ Î£ a ê‰ A , Î£ p ê‰ g b ï¼ f a , IHâ‚‚ b ï¼ IHâ‚ a âˆ¥)
         â†’ f â‰ˆ g â†’ Ï f IHâ‚ ï¼ Ï g IHâ‚‚
      Ï„' f g IHâ‚ IHâ‚‚ hIHâ‚ hIHâ‚‚ (mâ‚ , mâ‚‚) =
       Î©-extensionality pe fe (Ï„ f g IHâ‚ IHâ‚‚ hIHâ‚ mâ‚)
                              (Ï„ g f IHâ‚‚ IHâ‚ hIHâ‚‚ mâ‚‚)

  ğ•-prop-recursion : {ğ“£ : Universe}
                   â†’ (Ï : ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ Î© ğ“£) â†’ Î© ğ“£))
                   â†’ ({A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
                       â†’ (IHâ‚ : A â†’ Î© ğ“£) (IHâ‚‚ : B â†’ Î© ğ“£)
                       â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B ,
                                      Î£ p ê‰ f a ï¼ g b , IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
                     â†’ f â‰² g â†’ Ï f IHâ‚ holds â†’ Ï g IHâ‚‚ holds)
                   â†’ ğ• â†’ Î© ğ“£
  ğ•-prop-recursion {ğ“£} Ï Ï„ =
   prâ‚ (ğ•-prop-recursion-with-computation Ï Ï„)

  ğ•-prop-recursion-computes : {ğ“£ : Universe}
                            â†’ (Ï : ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ (A â†’ Î© ğ“£) â†’ Î© ğ“£))
                            â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
                                 â†’ (IHâ‚ : A â†’ Î© ğ“£) (IHâ‚‚ : B â†’ Î© ğ“£)
                                 â†’ ((a : A) â†’ âˆ¥ Î£ b ê‰ B , Î£ p ê‰ f a ï¼ g b ,
                                                  IHâ‚ a ï¼ IHâ‚‚ b âˆ¥)
                                 â†’ f â‰² g â†’ Ï f IHâ‚ holds â†’ Ï g IHâ‚‚ holds)
                            â†’ ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) (IH : A â†’ Î© ğ“£)
                              â†’ ğ•-prop-recursion Ï Ï„ (ğ•-set f) ï¼ Ï f IH)
  ğ•-prop-recursion-computes Ï Ï„ f =
   prâ‚‚ (ğ•-prop-recursion-with-computation Ï Ï„) f

\end{code}

We also have a simpler version of the above in the case that we don't need to
make recursive calls.

\begin{code}

  ğ•-prop-simple-recursion : {ğ“£ : Universe}
                          â†’ (Ï : ({A : ğ“¤ Ì‡ } â†’ (A â†’ ğ•) â†’ Î© ğ“£))
                          â†’ ({A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
                            â†’ f â‰² g â†’ Ï f holds â†’ Ï g holds)
                          â†’ ğ• â†’ Î© ğ“£
  ğ•-prop-simple-recursion {ğ“£} Ï Ï„ =
   ğ•-prop-recursion (Î» f _ â†’ Ï f) (Î» f g _ _ _ â†’ Ï„ f g)

  ğ•-prop-simple-recursion-computes :
      {ğ“£ : Universe}
    â†’ (Ï : ({A : ğ“¤ Ì‡ } â†’ (A â†’ ğ•) â†’ Î© ğ“£))
    â†’ (Ï„ : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ f â‰² g â†’ Ï f holds â†’ Ï g holds)
    â†’ ({A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ ğ•-prop-simple-recursion Ï Ï„ (ğ•-set f) ï¼ Ï f)
  ğ•-prop-simple-recursion-computes Ï Ï„ f =
   ğ•-prop-recursion-computes (Î» f _ â†’ Ï f) (Î» f g _ _ _ â†’ Ï„ f g)
                             f (Î» _ â†’ ğŸ™ , ğŸ™-is-prop)

\end{code}

Part III
--------

Basic constructions and proofs for ğ•, i.e. the definition of set membership (âˆˆ),
subset relation (âŠ†) and proofs of âˆˆ-extensionality and âˆˆ-induction.

\begin{code}

  _âˆˆ[Î©]_ : ğ• â†’ ğ• â†’ Î© (ğ“¤ âº)
  _âˆˆ[Î©]_ x = ğ•-prop-simple-recursion
              (Î» {A} f â†’ (âˆƒ a ê‰ A , f a ï¼ x) , âˆƒ-is-prop) e
   where
    e : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
      â†’ f â‰² g â†’ (âˆƒ a ê‰ A , f a ï¼ x) â†’ (âˆƒ b ê‰ B , g b ï¼ x)
    e {A} {B} f g s = âˆ¥âˆ¥-rec âˆƒ-is-prop
                             (Î» (a , p)
                                â†’ âˆ¥âˆ¥-functor (Î» (b , q)
                                                â†’ b , (q âˆ™ p)) (s a))

  _âˆˆ_ : ğ• â†’ ğ• â†’ ğ“¤ âº Ì‡
  x âˆˆ y = (x âˆˆ[Î©] y) holds

  âˆˆ-is-prop-valued : {x y : ğ•} â†’ is-prop (x âˆˆ y)
  âˆˆ-is-prop-valued {x} {y} = holds-is-prop (x âˆˆ[Î©] y)

  âˆˆ-for-ğ•-sets : (x : ğ•) {A : ğ“¤ Ì‡ } (f : A â†’ ğ•)
               â†’ (x âˆˆ ğ•-set f) ï¼ (âˆƒ a ê‰ A , f a ï¼ x)
  âˆˆ-for-ğ•-sets x f = ap prâ‚ (ğ•-prop-simple-recursion-computes _ _ f)

  from-âˆˆ-of-ğ•-set : {x : ğ•} {A : ğ“¤ Ì‡ } {f : A â†’ ğ•}
                    â†’ (x âˆˆ ğ•-set f) â†’ (âˆƒ a ê‰ A , f a ï¼ x)
  from-âˆˆ-of-ğ•-set {x} {A} {f} = Idtofun (âˆˆ-for-ğ•-sets x f)

  to-âˆˆ-of-ğ•-set : {x : ğ•} {A : ğ“¤ Ì‡ } {f : A â†’ ğ•}
                  â†’ (âˆƒ a ê‰ A , f a ï¼ x) â†’ (x âˆˆ ğ•-set f)
  to-âˆˆ-of-ğ•-set {x} {A} {f} = Idtofunâ»Â¹ (âˆˆ-for-ğ•-sets x f)

  _âŠ†_ : ğ• â†’ ğ• â†’ ğ“¤ âº Ì‡
  x âŠ† y = (v : ğ•) â†’ v âˆˆ x â†’ v âˆˆ y

  âŠ†-to-â‰² : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ ğ•-set f âŠ† ğ•-set g â†’ f â‰² g
  âŠ†-to-â‰² {A} {B} f g s a = from-âˆˆ-of-ğ•-set m
   where
    m : f a âˆˆ ğ•-set g
    m = s (f a) (to-âˆˆ-of-ğ•-set âˆ£ a , refl âˆ£)

  â‰²-to-âŠ† : {A B : ğ“¤ Ì‡ } (f : A â†’ ğ•) (g : B â†’ ğ•)
         â†’ f â‰² g â†’ ğ•-set f âŠ† ğ•-set g
  â‰²-to-âŠ† {A} {B} f g s x m = to-âˆˆ-of-ğ•-set n
   where
    m' : âˆƒ a ê‰ A , f a ï¼ x
    m' = from-âˆˆ-of-ğ•-set m
    n : âˆƒ b ê‰ B , g b ï¼ x
    n = âˆ¥âˆ¥-rec âˆƒ-is-prop
               (Î» (a , p) â†’ âˆ¥âˆ¥-functor (Î» (b , q) â†’ b , (q âˆ™ p)) (s a)) m'

  âŠ†-is-prop-valued : {x y : ğ•} â†’ is-prop (x âŠ† y)
  âŠ†-is-prop-valued = Î â‚‚-is-prop fe (Î» _ _ â†’ âˆˆ-is-prop-valued)

  âŠ†-is-reflexive : {x : ğ•} â†’ x âŠ† x
  âŠ†-is-reflexive _ = id

  ï¼-to-âŠ† : {x y : ğ•} â†’ x ï¼ y â†’ x âŠ† y
  ï¼-to-âŠ† refl = âŠ†-is-reflexive

\end{code}

We now prove, using the induction principles of ğ• above, two simple
set-theoretic axioms: âˆˆ-extensionality and âˆˆ-induction.

\begin{code}

  pre-extensionality : {A : ğ“¤ Ì‡ } (f : A â†’ ğ•) (x : ğ•)
                     â†’ x âŠ† ğ•-set f â†’ ğ•-set f âŠ† x â†’ x ï¼ ğ•-set f
  pre-extensionality f =
   ğ•-prop-simple-induction (Î» x â†’ x âŠ† ğ•-set f â†’ ğ•-set f âŠ† x â†’ x ï¼ ğ•-set f)
                           (Î» _ â†’ Î â‚‚-is-prop fe (Î» _ _ â†’ ğ•-is-large-set))
                           Î³
    where
     Î³ : {B : ğ“¤ Ì‡ } (g : B â†’ ğ•)
       â†’ ğ•-set g âŠ† ğ•-set f â†’ ğ•-set f âŠ† ğ•-set g â†’ ğ•-set g ï¼ ğ•-set f
     Î³ g s t = ğ•-set-ext g f (âŠ†-to-â‰² g f s , âŠ†-to-â‰² f g t)

  âˆˆ-extensionality : (x y : ğ•) â†’ x âŠ† y â†’ y âŠ† x â†’ x ï¼ y
  âˆˆ-extensionality x y =
   ğ•-prop-simple-induction (Î» v â†’ x âŠ† v â†’ v âŠ† x â†’ x ï¼ v)
                           (Î» _ â†’ Î â‚‚-is-prop fe (Î» _ _ â†’ ğ•-is-large-set))
                           (Î» f â†’ pre-extensionality f x)
                           y

  âˆˆ-induction : {ğ“£ : Universe} (P : ğ• â†’ ğ“£ Ì‡ )
              â†’ ((x : ğ•) â†’ is-prop (P x))
              â†’ ((x : ğ•) â†’ ((y : ğ•) â†’ y âˆˆ x â†’ P y) â†’ P x)
              â†’ (x : ğ•) â†’ P x
  âˆˆ-induction P P-is-prop-valued h = ğ•-prop-induction P P-is-prop-valued Î³
   where
    Î³ : {A : ğ“¤ Ì‡ } (f : A â†’ ğ•) â†’ ((a : A) â†’ P (f a)) â†’ P (ğ•-set f)
    Î³ {A} f IH = h (ğ•-set f) c
     where
      c : (y : ğ•) â†’ y âˆˆ ğ•-set f â†’ P y
      c y m = âˆ¥âˆ¥-rec (P-is-prop-valued y) (Î» (a , p) â†’ transport P p (IH a)) m'
       where
        m' : âˆƒ a ê‰ A , f a ï¼ y
        m' = from-âˆˆ-of-ğ•-set m

\end{code}
