Ian Ray, 21st Jun 2025.

We develop sequential colimits in HoTT/UF. This formalization follows Section 26
of Introduction to Homotopy Type Theory by Egbert Rijke (HoTTEST summer school
version:
https://github.com/martinescardo/HoTTEST-Summer-School/blob/main/HoTT/hott-intro.pdf).

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module UF.SequentialColimits (fe : Fun-Ext) where

open import MLTT.Spartan
open import UF.Base
open import UF.CoconesofSpans fe
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.PropIndexedPiSigma
open import UF.Pushouts fe
open import UF.Subsingletons
open import UF.Yoneda

\end{code}

A diagram of the following form

          aâ‚€      aâ‚      aâ‚‚
     Aâ‚€ ----> Aâ‚ ----> Aâ‚‚ ----> ...

is a type sequence. We can give a formal specification as follows.

\begin{code}

type-sequence : (ğ“¤ : Universe) â†’ (ğ“¤ âº) Ì‡
type-sequence ğ“¤ = Î£ A ê‰ (â„• â†’ ğ“¤ Ì‡) , ((n : â„•) â†’ A n â†’ A (succ n))

\end{code}

A sequential cocone on a type sequence consists of a sequence of maps to a
specified type

          aâ‚€      aâ‚      aâ‚‚
     Aâ‚€ ----> Aâ‚ ----> Aâ‚‚ ----> ...
      \       |        /
       \      |       /
    bâ‚€  \     | bâ‚   / bâ‚‚ ...
         \    |     /
          \   |    /
           v  v   v
              B

such that every triangle commutes. Formally we can define this as
follows.

\begin{code}

sequential-cocone : {ğ“¤ ğ“¥ : Universe}
                  â†’ type-sequence ğ“¤
                  â†’ ğ“¥ Ì‡
                  â†’ ğ“¤ âŠ” ğ“¥ Ì‡ 
sequential-cocone (A , a) B
 = Î£ b ê‰ ((n : â„•) â†’ A n â†’ B) , ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n)

\end{code}

We now characterize the identity type of sequential cocones.

\begin{code}

module _ (ğ“@(A , a) : type-sequence ğ“¤)
         (B : ğ“¥ Ì‡)
       where

 sequential-cocone-identity : sequential-cocone ğ“ B
                            â†’ sequential-cocone ğ“ B
                            â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 sequential-cocone-identity (s , S) (r , R)
  = Î£ H ê‰ ((n : â„•) â†’ s n âˆ¼ r n) ,
    ((n : â„•) â†’ âˆ¼-trans (S n) (H (succ n) âˆ˜ a n) âˆ¼ âˆ¼-trans (H n) (R n))

 id-to-sequential-cocone-family : (ğ“‘ ğ“‘' : sequential-cocone ğ“ B)
                                â†’ ğ“‘ ï¼ ğ“‘'
                                â†’ sequential-cocone-identity ğ“‘ ğ“‘'
 id-to-sequential-cocone-family ğ“‘ ğ“‘ refl
  = ((Î» - â†’ âˆ¼-refl) , Î» - â†’ Î» -' â†’ refl-left-neutral â»Â¹)

 sequential-cocone-identity-is-identity-system
  : (ğ“‘ : sequential-cocone ğ“ B)
  â†’ is-contr (Î£ ğ“‘' ê‰ (sequential-cocone ğ“ B) , sequential-cocone-identity ğ“‘ ğ“‘')
 sequential-cocone-identity-is-identity-system (b , G)
  = equiv-to-singleton e ğŸ™-is-singleton
  where
   e : (Î£ ğ“‘' ê‰ (sequential-cocone ğ“ B) , sequential-cocone-identity (b , G) ğ“‘')
     â‰ƒ ğŸ™ {ğ“¤ âŠ” ğ“¥}
   e = (Î£ ğ“‘' ê‰ (sequential-cocone ğ“ B) , sequential-cocone-identity (b , G) ğ“‘')
                                                                        â‰ƒâŸ¨ I âŸ©
       (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) ,
        Î£ G' ê‰ ((n : â„•) â†’ b' n âˆ¼ b' (succ n) âˆ˜ a n) ,
         Î£ H ê‰ ((n : â„•) â†’ b n âˆ¼ b' n) ,
          ((n : â„•) â†’ âˆ¼-trans (G n) (H (succ n) âˆ˜ a n) âˆ¼ âˆ¼-trans (H n) (G' n)))
                                                                        â‰ƒâŸ¨ II âŸ©
       (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) ,
        Î£ H ê‰ ((n : â„•) â†’ b n âˆ¼ b' n) ,
         Î£ G' ê‰ ((n : â„•) â†’ b' n âˆ¼ b' (succ n) âˆ˜ a n) ,
          ((n : â„•) â†’ âˆ¼-trans (G n) (H (succ n) âˆ˜ a n) âˆ¼ âˆ¼-trans (H n) (G' n)))
                                                                        â‰ƒâŸ¨ III âŸ©
       (Î£ G' ê‰ ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n) ,
        ((n : â„•) â†’ G n âˆ¼ G' n))
                                                                        â‰ƒâŸ¨ IV âŸ©
       ğŸ™                                                                â– 
    where
     I = Î£-assoc
     II = Î£-cong (Î» - â†’ Î£-flip)
     III = (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) ,
            Î£ H ê‰ ((n : â„•) â†’ b n âˆ¼ b' n) ,
             Î£ G' ê‰ ((n : â„•) â†’ b' n âˆ¼ b' (succ n) âˆ˜ a n) ,
              ((n : â„•) â†’ âˆ¼-trans (G n) (H (succ n) âˆ˜ a n)
                       âˆ¼ âˆ¼-trans (H n) (G' n)))
                                                                       â‰ƒâŸ¨ V âŸ©
           (Î£ (b' , H) ê‰ (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) , ((n : â„•) â†’ b n âˆ¼ b' n)) ,
            (Î£ G' ê‰ ((n : â„•) â†’ b' n âˆ¼ b' (succ n) âˆ˜ a n) ,
              ((n : â„•) â†’ âˆ¼-trans (G n) (H (succ n) âˆ˜ a n)
                       âˆ¼ âˆ¼-trans (H n) (G' n))))
                                                                       â‰ƒâŸ¨ VII âŸ©
           (Î£ G' ê‰ ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n) ,
            ((n : â„•) â†’ âˆ¼-trans (G n) âˆ¼-refl âˆ¼ âˆ¼-trans âˆ¼-refl (G' n)))
                                                                       â‰ƒâŸ¨ VIII âŸ©
           (Î£ G' ê‰ ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n) , ((n : â„•) â†’ G n âˆ¼ G' n))
                                                                       â– 
      where
       V = â‰ƒ-sym Î£-assoc
       VI = (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) , ((n : â„•) â†’ b n âˆ¼ b' n)) â‰ƒâŸ¨ Î£-cong IX âŸ©
            (Î£ b' ê‰ ((n : â„•) â†’ A n â†’ B) , b ï¼ b')                â‰ƒâŸ¨ X âŸ©
            ğŸ™                                                     â– 
        where
         IX : (b' : (n : â„•) â†’ A n â†’ B)
            â†’ ((n : â„•) â†’ b n âˆ¼ b' n) â‰ƒ (b ï¼ b')
         IX b' = ((n : â„•) â†’ b n âˆ¼ b' n)
                                         â‰ƒâŸ¨ Î -cong fe fe
                                             (Î» n â†’ â‰ƒ-sym
                                              (â‰ƒ-funext fe (b n) (b' n))) âŸ©
                 ((n : â„•) â†’ b n ï¼ b' n)
                                         â‰ƒâŸ¨ â‰ƒ-sym (â‰ƒ-funext fe b b') âŸ©
                 (b ï¼ b')               â– 
         X = singleton-â‰ƒ-ğŸ™ {ğ“¤ âŠ” ğ“¥} {ğ“¥} (singleton-types-are-singletons b)
       VII = prop-indexed-sum (b , (Î» n â†’ âˆ¼-refl)) (equiv-to-prop VI ğŸ™-is-prop)
       VIII = Î£-cong (Î» G' â†’ Î -cong fe fe
               (Î» n â†’ Î -cong fe fe
                (Î» x â†’ ï¼-cong (G n x) (âˆ¼-trans (Î» - â†’ refl) (G' n) x)
                 refl refl-left-neutral)))
     IV = (Î£ G' ê‰ ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n) , ((n : â„•) â†’ G n âˆ¼ G' n))
                                                                        â‰ƒâŸ¨ VI âŸ©
          (Î£ G' ê‰ ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n) , G ï¼ G')
                                                                        â‰ƒâŸ¨ VII âŸ©
          ğŸ™                                                             â– 
      where
       V : (G' : ((n : â„•) â†’ b n âˆ¼ b (succ n) âˆ˜ a n))
         â†’ ((n : â„•) â†’ G n âˆ¼ G' n) â‰ƒ (G ï¼ G')
       V G' = ((n : â„•) â†’ G n âˆ¼ G' n)
                                      â‰ƒâŸ¨ Î -cong fe fe
                                          (Î» n â†’ â‰ƒ-sym
                                           (â‰ƒ-funext fe (G n) (G' n))) âŸ©
              ((n : â„•) â†’ G n ï¼ G' n)
                                      â‰ƒâŸ¨ â‰ƒ-sym (â‰ƒ-funext fe G G') âŸ©
              (G ï¼ G')               â– 
       VI = Î£-cong V
       VII = singleton-â‰ƒ-ğŸ™ (singleton-types-are-singletons G)

 sequential-cocone-identity-characterization
  : (ğ“‘ ğ“‘' : sequential-cocone ğ“ B)
  â†’ (ğ“‘ ï¼ ğ“‘') â‰ƒ (sequential-cocone-identity ğ“‘ ğ“‘')
 sequential-cocone-identity-characterization ğ“‘ ğ“‘' =
  (id-to-sequential-cocone-family ğ“‘ ğ“‘' ,
    Yoneda-Theorem-forth ğ“‘ (id-to-sequential-cocone-family ğ“‘)
     (sequential-cocone-identity-is-identity-system ğ“‘) ğ“‘')

 sequential-cocone-family-to-id : (ğ“‘ ğ“‘' : sequential-cocone ğ“ B)
                                â†’ (sequential-cocone-identity ğ“‘ ğ“‘')
                                â†’ ğ“‘ ï¼ ğ“‘'
 sequential-cocone-family-to-id ğ“‘ ğ“‘'
  = âŒœ sequential-cocone-identity-characterization ğ“‘ ğ“‘' âŒâ»Â¹

\end{code}

Given a sequential cocone over X and a map X â†’ Y there is a canonical assignment
to a sequential cocone over Y.

\begin{code}

module _ (ğ“ : type-sequence ğ“¤)
         (X : ğ“¥ Ì‡) (Y : ğ“£ Ì‡)
       where

 canonical-map-to-sequential-cocone : sequential-cocone ğ“ X
                                    â†’ (X â†’ Y)
                                    â†’ sequential-cocone ğ“ Y
 canonical-map-to-sequential-cocone (h , H) u =
  ((Î» n â†’ u âˆ˜ h n) , Î» n â†’ âˆ¼-ap-âˆ˜ u (H n))

\end{code}

A sequential cocone over X is universal if the above map is an equivalence for
any Y. Such a sequential cocone is said to be the sequential colimit of a type
sequence.

\begin{code}

 Sequential-Colimit-Universal-Property : (ğ“§ : sequential-cocone ğ“ X)
                                       â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“£ Ì‡
 Sequential-Colimit-Universal-Property ğ“§ =
  is-equiv (canonical-map-to-sequential-cocone ğ“§)

\end{code}

We now give a construction of the sequential colimit in terms of the pushout.
This construction follows 26.2 in Introduction to Homotopy Type Theory
(link above).

The sequential colimit Aâˆ can be constructed as the pushout of the following
diagram

                     [id , Ïƒ]
          Î£ A + Î£ A ------------> Î£ A
              |                    |
   [id , id]  |                    | inrr
              |                    |
             Î£ A ----------------> Aâˆ
                       inll

where Ïƒ (n , x) = (n + 1 , a n x).

\begin{code}

module _ (ğ“@(A , a) : type-sequence ğ“¤) where

 Ïƒ : Î£ A â†’ Î£ A
 Ïƒ (n , x) = (succ n , a n x)

 id-case : Î£ A + Î£ A â†’ Î£ A
 id-case = cases id id

 succ-case : Î£ A + Î£ A â†’ Î£ A
 succ-case = cases id Ïƒ

 private
  index : Î£ A â†’ â„•
  index = prâ‚

  element-at : ((n , x) : Î£ A) â†’ A n
  element-at = prâ‚‚

 module _ (push-ex : pushout-exists id-case succ-case)
           where

  open pushout-exists push-ex

  sequential-colimit : ğ“¤ Ì‡
  sequential-colimit = pushout

\end{code}

We provide the sequential cocone structure for the sequential colimit. 

\begin{code}

  Î¹ : (n : â„•) â†’ A n â†’ sequential-colimit
  Î¹ n x = inrr (n , x)

  seq-colim-homotopy : (n : â„•) â†’ Î¹ n âˆ¼ Î¹ (succ n) âˆ˜ a n
  seq-colim-homotopy n x = glue (inl (n , x)) â»Â¹ âˆ™ glue (inr (n , x))

  sequential-colimit-is-cocone : sequential-cocone ğ“ sequential-colimit
  sequential-colimit-is-cocone = (Î¹ , seq-colim-homotopy)

\end{code}

We will quickly provide names and a technical lemma that will prove useful
later.

\begin{code}

  module _ (X : ğ“£ Ì‡) where

   ap-on-glue : (u : sequential-colimit â†’ X)
              â†’ ((n , x) : Î£ A)
              â†’ ap u (seq-colim-homotopy n x)
              ï¼ ap u (glue (inl (n , x))) â»Â¹ âˆ™ ap u (glue (inr (n , x)))
   ap-on-glue u (n , x)
    = ap u (seq-colim-homotopy n x)                             ï¼âŸ¨ I âŸ©
      ap u (glue (inl (n , x)) â»Â¹) âˆ™ ap u (glue (inr (n , x)))  ï¼âŸ¨ II âŸ©
      ap u (glue (inl (n , x))) â»Â¹ âˆ™ ap u (glue (inr (n , x)))  âˆ
    where
     I = ap-âˆ™ u (glue (inl (n , x)) â»Â¹) (glue (inr (n , x)))
     II = ap (_âˆ™ ap u (glue (inr (n , x)))) (ap-sym u (glue (inl (n , x)))) â»Â¹

\end{code}

We show that cocones over the above pushout diagram are equivalent to sequential
cocones over the above type sequence. 

\begin{code}

   gluing-from-sequential-cocone
    : ((b , H) : sequential-cocone ğ“ X)
    â†’ (c : Î£ A + Î£ A)
    â†’ b (index (id-case c)) (element-at (id-case c))
    ï¼ b (index (succ-case c)) (element-at (succ-case c))
   gluing-from-sequential-cocone (b , H) (inl -) = refl
   gluing-from-sequential-cocone (b , H) (inr (n , x)) = H n x

   pushout-cocone-to-seq-cocone : cocone id-case succ-case X
                                â†’ sequential-cocone ğ“ X
   pushout-cocone-to-seq-cocone (i , j , H) = (curry j , I)
    where
     I : (n : â„•) â†’ (curry j n) âˆ¼ (Î» - â†’ j (succ n , a n -))
     I n x = H (inl (n , x)) â»Â¹ âˆ™ H (inr (n , x))

   seq-cocone-to-pushout-cocone : sequential-cocone ğ“ X
                                â†’ cocone id-case succ-case X
   seq-cocone-to-pushout-cocone (b , H)
    = (uncurry b , uncurry b , gluing-from-sequential-cocone (b , H))

   pushout-cocone-to-seq-cocone-is-retraction
    : pushout-cocone-to-seq-cocone âˆ˜ seq-cocone-to-pushout-cocone âˆ¼ id
   pushout-cocone-to-seq-cocone-is-retraction (b , H)
    = sequential-cocone-family-to-id ğ“ X
       (pushout-cocone-to-seq-cocone (seq-cocone-to-pushout-cocone (b , H)))
       (b , H) ((Î» n â†’ Î» x â†’ refl) , (Î» n â†’ Î» x â†’ refl))

   pushout-cocone-to-seq-cocone-is-section
    : seq-cocone-to-pushout-cocone âˆ˜ pushout-cocone-to-seq-cocone âˆ¼ id
   pushout-cocone-to-seq-cocone-is-section (i , j , H)
    = inverse-cocone-map id-case succ-case X
       (seq-cocone-to-pushout-cocone (pushout-cocone-to-seq-cocone (i , j , H)))
       (i , j , H) ((Î» (n , x) â†’ H (inl (n , x)) â»Â¹) , âˆ¼-refl , I)
    where
     I : (z : Î£ A + Î£ A)
       â†’ H (inl (index (id-case z) , element-at (id-case z))) â»Â¹ âˆ™ H z
       ï¼ gluing-from-sequential-cocone
          (curry j , Î» n â†’ Î» x â†’ H (inl (n , x)) â»Â¹ âˆ™ H (inr (n , x))) z
     I (inl -) = left-inverse (H (inl -))
     I (inr -) = refl

   pushout-to-seq-cocone-is-equiv : is-equiv pushout-cocone-to-seq-cocone
   pushout-to-seq-cocone-is-equiv
    = qinvs-are-equivs pushout-cocone-to-seq-cocone
       (seq-cocone-to-pushout-cocone ,
        pushout-cocone-to-seq-cocone-is-section ,
         pushout-cocone-to-seq-cocone-is-retraction)

\end{code}

Additionally, we show that the canonical map to sequential cocones factors
through the canonical map to pushout cocones and the above map that translates
between them.

\begin{code}

   canonical-maps-commute
    : canonical-map-to-sequential-cocone ğ“ sequential-colimit X
       sequential-colimit-is-cocone
    âˆ¼ pushout-cocone-to-seq-cocone
     âˆ˜ canonical-map-to-cocone sequential-colimit id-case succ-case
        pushout-cocone X
   canonical-maps-commute u
    = sequential-cocone-family-to-id ğ“ X
       (canonical-map-to-sequential-cocone ğ“ sequential-colimit X
        sequential-colimit-is-cocone u)
       (pushout-cocone-to-seq-cocone
        (canonical-map-to-cocone sequential-colimit id-case succ-case
         pushout-cocone X u))
       (I , II)
     where
      I : (n : â„•) â†’ u âˆ˜ Î¹ n âˆ¼ curry (u âˆ˜ inrr) n
      I n x = refl
      II : (n : â„•) (x : A n)
         â†’ ap u (seq-colim-homotopy n x)
         ï¼ refl âˆ™ (ap u (glue (inl (n , x))) â»Â¹ âˆ™ ap u (glue (inr (n , x))))
      II n x
       = ap u (seq-colim-homotopy n x)                                ï¼âŸ¨ III âŸ©
         ap u (glue (inl (n , x)) â»Â¹) âˆ™ ap u (glue (inr (n , x)))     ï¼âŸ¨ IV âŸ©
         ap u (glue (inl (n , x))) â»Â¹ âˆ™ ap u (glue (inr (n , x)))     ï¼âŸ¨ V âŸ©
         refl âˆ™ (ap u (glue (inl (n , x))) â»Â¹ âˆ™ ap u (glue (inr (n , x)))) âˆ
       where
        III = ap-âˆ™ u (glue (inl (n , x)) â»Â¹) (glue (inr (n , x)))
        IV = ap (_âˆ™ ap u (glue (inr (n , x))))
                (ap-sym u (glue (inl (n , x))) â»Â¹)
        V = refl-left-neutral â»Â¹

\end{code}

Using the above results we prove that the pushout constructed above satisfies
the universal property of the sequential colimit.

\begin{code}

   sequential-colimit-universal-property
    : Sequential-Colimit-Universal-Property ğ“ sequential-colimit X
       sequential-colimit-is-cocone  
   sequential-colimit-universal-property
    = transport is-equiv (dfunext fe (âˆ¼-sym canonical-maps-commute))
       (âˆ˜-is-equiv pushout-universal-property pushout-to-seq-cocone-is-equiv)

\end{code}

We unpack some useful results from the from the universal property.

\begin{code}

   module _ (ğ“§@(h , H) : sequential-cocone ğ“ X)
          where

    canonical-map-seq-cocone-fiber-contr
     : is-contr
        (fiber (canonical-map-to-sequential-cocone ğ“ sequential-colimit X
                 sequential-colimit-is-cocone) ğ“§)
    canonical-map-seq-cocone-fiber-contr
     = equivs-are-vv-equivs
        (canonical-map-to-sequential-cocone ğ“ sequential-colimit X
         sequential-colimit-is-cocone) sequential-colimit-universal-property ğ“§

    canonical-map-seq-cocone-fiber-contr'
     : is-contr (Î£ u ê‰ (sequential-colimit â†’ X) ,
        sequential-cocone-identity ğ“ X
         ((Î» n â†’ u âˆ˜ Î¹ n) , Î» n â†’ âˆ¼-ap-âˆ˜ u (seq-colim-homotopy n)) ğ“§)
    canonical-map-seq-cocone-fiber-contr' =
     equiv-to-singleton'
      (Î£-cong (Î» - â†’ sequential-cocone-identity-characterization ğ“ X
       ((Î» n â†’ - âˆ˜ Î¹ n) , Î» n â†’ âˆ¼-ap-âˆ˜ - (seq-colim-homotopy n)) ğ“§))
        (canonical-map-seq-cocone-fiber-contr)

    sequential-colimit-unique-map
     : Î£ u ê‰ (sequential-colimit â†’ X) ,
        sequential-cocone-identity ğ“ X
         ((Î» n â†’ u âˆ˜ Î¹ n) , Î» n â†’ âˆ¼-ap-âˆ˜ u (seq-colim-homotopy n)) ğ“§
     â†’ sequential-colimit â†’ X
    sequential-colimit-unique-map (u , _ , _) = u

    sequential-colimit-homotopy
     : (z : Î£ u ê‰ (sequential-colimit â†’ X) ,
        sequential-cocone-identity ğ“ X
         ((Î» n â†’ u âˆ˜ Î¹ n) , Î» n â†’ âˆ¼-ap-âˆ˜ u (seq-colim-homotopy n)) ğ“§)
     â†’ (n : â„•) â†’ sequential-colimit-unique-map z âˆ˜ Î¹ n âˆ¼ h n
    sequential-colimit-homotopy (_ , G , _) = G

    sequential-colimit-glue
     : ((u , G , M) : Î£ u ê‰ (sequential-colimit â†’ X) ,
        sequential-cocone-identity ğ“ X
         ((Î» n â†’ u âˆ˜ Î¹ n) , Î» n â†’ âˆ¼-ap-âˆ˜ u (seq-colim-homotopy n)) ğ“§)
     â†’ (n : â„•)
     â†’ âˆ¼-trans (âˆ¼-ap-âˆ˜ u (seq-colim-homotopy n)) (Î» x â†’ G (succ n) (a n x))
     âˆ¼ âˆ¼-trans (G n) (H n)
    sequential-colimit-glue (_ , _ , M) = M

\end{code}

From the universal property we derive the recursion principle and computation
rules for sequential colimits.

\begin{code}

   sequential-colimit-recursion : sequential-cocone ğ“ X
                                â†’ sequential-colimit â†’ X
   sequential-colimit-recursion ğ“§
    = sequential-colimit-unique-map ğ“§
       (center (canonical-map-seq-cocone-fiber-contr' ğ“§))

   sequential-colimit-recursion-computation
    : ((h , H) : sequential-cocone ğ“ X)
    â†’ (n : â„•)
    â†’ (x : A n)
    â†’ sequential-colimit-recursion (h , H) (Î¹ n x) ï¼ h n x
   sequential-colimit-recursion-computation ğ“§
    = sequential-colimit-homotopy ğ“§
       (center (canonical-map-seq-cocone-fiber-contr' ğ“§))

   sequential-colimit-recursion-glue
    : ((h , H) : sequential-cocone ğ“ X)
    â†’ (n : â„•)
    â†’ (x : A n)
    â†’ ap (sequential-colimit-recursion (h , H)) (seq-colim-homotopy n x)
      âˆ™ sequential-colimit-recursion-computation (h , H) (succ n) (a n x)
    ï¼ sequential-colimit-recursion-computation (h , H) n x âˆ™ H n x
   sequential-colimit-recursion-glue ğ“§
    = sequential-colimit-glue ğ“§
       (center (canonical-map-seq-cocone-fiber-contr' ğ“§))

\end{code}

Finally, we prove the uniqueness principle for sequential colimits.

\begin{code}

   sequential-colimit-uniqueness
    : (u u' : sequential-colimit â†’ X)
    â†’ (G : (n : â„•) â†’ u âˆ˜ (Î¹ n) âˆ¼ u' âˆ˜ (Î¹ n))
    â†’ (M : (n : â„•) (x : A n)
         â†’ ap u (seq-colim-homotopy n x) âˆ™ G (succ n) (a n x)
    ï¼ G n x âˆ™ ap u' (seq-colim-homotopy n x))
    â†’ u âˆ¼ u'
   sequential-colimit-uniqueness u u' G M = pushout-uniqueness u u' I II III
    where
     I : (z : Î£ A) â†’ u (inll z) ï¼ u' (inll z)
     I (n , x)
      = ap u (glue (inl (n , x))) âˆ™ G n x âˆ™ ap u' (glue (inl (n , x))) â»Â¹
     II : (z : Î£ A) â†’ u (inrr z) ï¼ u' (inrr z)
     II (n , x) = G n x
     III : (c : Î£ A + Î£ A)
         â†’ ap u (glue c) âˆ™ II (succ-case c) ï¼ I (id-case c) âˆ™ ap u' (glue c)
     III (inl (n , x)) = p âˆ™ G n x                 ï¼âŸ¨ IV âŸ©
                         p âˆ™ G n x âˆ™ (p' â»Â¹ âˆ™ p')  ï¼âŸ¨ V âŸ©
                         I (n , x) âˆ™ p'            âˆ
      where
       p = ap u (glue (inl (n , x)))
       p' = ap u' (glue (inl (n , x)))
       IV = ap (p âˆ™ G n x âˆ™_) (sym-is-inverse p')
       V = âˆ™assoc (p âˆ™ G n x) (p' â»Â¹) p' â»Â¹
     III (inr (n , x)) =
      q âˆ™ G (succ n) (a n x)                                    ï¼âŸ¨ IV âŸ©
      (p âˆ™ p â»Â¹) âˆ™ (q âˆ™ G (succ n) (a n x))                     ï¼âŸ¨ V âŸ©
      p âˆ™ (p â»Â¹ âˆ™ (q âˆ™ G (succ n) (a n x)))                     ï¼âŸ¨ VI âŸ©
      p âˆ™ (p â»Â¹ âˆ™ q âˆ™ G (succ n) (a n x))                       ï¼âŸ¨ VII âŸ©
      p âˆ™ (ap u (seq-colim-homotopy n x) âˆ™ G (succ n) (a n x))  ï¼âŸ¨ VIII âŸ©
      p âˆ™ (G n x âˆ™ ap u' (seq-colim-homotopy n x))              ï¼âŸ¨ IX âŸ©
      p âˆ™ G n x âˆ™ ap u' (seq-colim-homotopy n x)                ï¼âŸ¨ X' âŸ©
      I (n , x) âˆ™ q'                                            âˆ
      where
       p = ap u (glue (inl (n , x)))
       q = ap u (glue (inr (n , x)))
       p' = ap u' (glue (inl (n , x)))
       q' = ap u' (glue (inr (n , x)))
       IV = refl-left-neutral â»Â¹ âˆ™ ap (_âˆ™ (q âˆ™ G (succ n) (a n x)))
                                      (sym-is-inverse' p)
       V = âˆ™assoc p (p â»Â¹) (q âˆ™ G (succ n) (a n x))
       VI = ap (p âˆ™_) (âˆ™assoc (p â»Â¹) q (G (succ n) (a n x)) â»Â¹)
       VII = ap (p âˆ™_) (ap (_âˆ™ G (succ n) (a n x)) (ap-on-glue u (n , x) â»Â¹))
       VIII = ap (p âˆ™_) (M n x)
       IX = âˆ™assoc p (G n x) (ap u' (seq-colim-homotopy n x)) â»Â¹
       X' = ap (p âˆ™ G n x âˆ™_ ) (ap-on-glue u' (n , x))
            âˆ™ (âˆ™assoc (p âˆ™ G n x) (p' â»Â¹) q') â»Â¹

\end{code}

TODO. Derive the dependent universal property and induction principle for
sequential colimits.
