Ian Ray, 23rd July 2024

Modifications made by Ian Ray on 26th September 2024.

Modifications made by Ian Ray on 17th December 2024.

We define connected types and maps. We then explore relationships, closure
properties and characterizations of interest pertaining to the concept of
connectedness.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module UF.ConnectedTypes
        (fe : Fun-Ext)
       where

open import MLTT.Spartan hiding (_+_)
open import Notation.Order
open import UF.Base
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.PropTrunc
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.TruncatedTypes fe
open import UF.Truncations fe
open import UF.TruncationLevels
open import UF.Univalence
open import UF.Yoneda

\end{code}

We now define the notion of connectedness for types and functions with respect
to truncation levels.

\begin{code}

private
 fe' : FunExt
 fe' ğ“¤ ğ“¥ = fe {ğ“¤} {ğ“¥}

module connectedness-results (te : general-truncations-exist) where

 private
  pt : propositional-truncations-exist
  pt = general-truncations-give-propositional-truncations te

 open general-truncations-exist te
 open propositional-truncations-exist pt
 open import UF.ImageAndSurjection pt

 _is_connected : ğ“¤ Ì‡ â†’ â„•â‚‹â‚‚ â†’ ğ“¤ Ì‡
 X is k connected = is-contr (âˆ¥ X âˆ¥[ k ])

 _is_connected-map : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (f : X â†’ Y) â†’ â„•â‚‹â‚‚ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 f is k connected-map = each-fiber-of f (Î» - â†’ - is k connected)

 being-connected-is-prop : {k : â„•â‚‹â‚‚} {X : ğ“¤ Ì‡ }
                         â†’ is-prop (X is k connected)
 being-connected-is-prop = being-truncated-is-prop

 being-connected-map-is-prop : {k : â„•â‚‹â‚‚} {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y}
                             â†’ is-prop (f is k connected-map)
 being-connected-map-is-prop = Î -is-prop fe (Î» y â†’ being-connected-is-prop)

 connected-if-contr : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                    â†’ is-contr X
                    â†’ X is k connected
 connected-if-contr {_} {X} {âˆ’2} X-is-contr = âˆ’2-trunc-is-contr
 connected-if-contr {_} {X} {succ k} (c , C) = (âˆ£ c âˆ£[ k + 1 ] , C')
  where
   C'' : (x : X) â†’ âˆ£ c âˆ£[ k + 1 ] ï¼ âˆ£ x âˆ£[ k + 1 ]
   C'' x = canonical-identity-trunc-map âˆ£ C x âˆ£[ k ]
   C' : is-central âˆ¥ X âˆ¥[ k + 1 ] âˆ£ c âˆ£[ k + 1 ]
   C' = âˆ¥âˆ¥â‚™-ind (Î» v â†’ Î» p q â†’ truncation-levels-closed-under-Id
                 (âˆ¥âˆ¥â‚™-is-truncated âˆ£ c âˆ£[ succ k ] v) p q) C''

\end{code}

TODO: show that connectedness as defined elsewhere in the library is
a special case of k-connectedness. Connectedness typically means set
connectedness, by our convention it will mean 0-connectedness.

We characterize âˆ’1-connected types as inhabited types and âˆ’1-connected maps as
surjections.

\begin{code}

 âˆ’1-connected-types-are-inhabited : {X : ğ“¤ Ì‡ }
                                  â†’ X is âˆ’1 connected â†’ âˆ¥ X âˆ¥
 âˆ’1-connected-types-are-inhabited X-1-conn =
  âˆ’1-trunc-to-prop-trunc pt (center X-1-conn)

 inhabited-types-are-âˆ’1-connected : {X : ğ“¤ Ì‡ }
                                  â†’ âˆ¥ X âˆ¥ â†’ X is âˆ’1 connected
 inhabited-types-are-âˆ’1-connected x-anon =
  pointed-props-are-singletons (prop-trunc-to-âˆ’1-trunc pt x-anon)
                               âˆ’1-trunc-is-prop

 âˆ’1-connected-iff-inhabited : {X : ğ“¤ Ì‡ }
                            â†’ X is âˆ’1 connected â†” âˆ¥ X âˆ¥
 âˆ’1-connected-iff-inhabited =
  (âˆ’1-connected-types-are-inhabited , inhabited-types-are-âˆ’1-connected)

 âˆ’1-connected-maps-are-surjections : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y}
                                   â†’ f is âˆ’1 connected-map â†’ is-surjection f
 âˆ’1-connected-maps-are-surjections m y = âˆ’1-connected-types-are-inhabited (m y)

 surjections-are-âˆ’1-connected : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y}
                              â†’ is-surjection f â†’ f is âˆ’1 connected-map
 surjections-are-âˆ’1-connected f-is-surj y =
  inhabited-types-are-âˆ’1-connected (f-is-surj y)

 âˆ’1-connected-map-iff-surjection : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y}
                                 â†’ f is âˆ’1 connected-map â†” is-surjection f
 âˆ’1-connected-map-iff-surjection =
  (âˆ’1-connected-maps-are-surjections , surjections-are-âˆ’1-connected)

\end{code}

We develop some closure conditions pertaining to connectedness. Connectedness
is closed under retracts and equivalence as expected, but more importantly
connectedness extends below:
more precisely if a type is k connected then it is l connected for all l â‰¤ k.
We provide a few incarnations of this fact below which may prove useful.

\begin{code}

 retract-is-connected : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {k : â„•â‚‹â‚‚}
                      â†’ retract Y of X
                      â†’ X is k connected
                      â†’ Y is k connected
 retract-is-connected R X-conn =
  retract-of-singleton (truncation-closed-under-retract R) X-conn

 connectedness-closed-under-equiv : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {k : â„•â‚‹â‚‚}
                                  â†’ X â‰ƒ Y
                                  â†’ Y is k connected
                                  â†’ X is k connected
 connectedness-closed-under-equiv e Y-con =
  equiv-to-singleton (truncation-closed-under-equiv e) Y-con

 contractible-types-are-connected : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                                  â†’ is-contr X
                                  â†’ X is k connected
 contractible-types-are-connected {ğ“¤} {X} {k} (c , C) = ((âˆ£ c âˆ£[ k ]) , C')
  where
   C' : (s : âˆ¥ X âˆ¥[ k ]) â†’ (âˆ£ c âˆ£[ k ]) ï¼ s
   C' = âˆ¥âˆ¥â‚™-ind (truncation-levels-closed-under-Id âˆ¥âˆ¥â‚™-is-truncated (âˆ£ c âˆ£[ k ]))
                (Î» x â†’ ap âˆ£_âˆ£[ k ] (C x))

 connectedness-is-lower-closed : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                               â†’ X is (k + 1) connected
                               â†’ X is k connected
 connectedness-is-lower-closed {ğ“¤} {X} {k} X-succ-con =
  equiv-to-singleton successive-truncations-equiv
                     (contractible-types-are-connected X-succ-con)

 connectedness-is-lower-closed-+ : {X : ğ“¤ Ì‡ } {l : â„•â‚‹â‚‚} {k : â„•}
                                 â†’ X is (l + k) connected
                                 â†’ X is l connected
 connectedness-is-lower-closed-+ {ğ“¤} {X} {l} {0} X-con = X-con
 connectedness-is-lower-closed-+ {ğ“¤} {X} {l} {succ k} X-con =
  connectedness-is-lower-closed-+ (connectedness-is-lower-closed X-con)

 connectedness-is-lower-closed' : {X : ğ“¤ Ì‡ } {k l : â„•â‚‹â‚‚}
                                â†’ (l â‰¤ k)
                                â†’ X is k connected
                                â†’ X is l connected
 connectedness-is-lower-closed' {ğ“¤} {X} {k} {l} o X-con =
  connectedness-is-lower-closed-+ (transport (Î» z â†’ X is z connected) p X-con)
  where
   m : â„•
   m = subtraction-â„•â‚‹â‚‚-term l k o
   p = k       ï¼âŸ¨ subtraction-â„•â‚‹â‚‚-identification l k o â»Â¹ âŸ©
       l + m   âˆ

 map-connectedness-is-lower-closed : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {k l : â„•â‚‹â‚‚}
                                   â†’ (l â‰¤ k)
                                   â†’ f is k connected-map
                                   â†’ f is l connected-map
 map-connectedness-is-lower-closed o f-k-con y =
  connectedness-is-lower-closed' o (f-k-con y)

\end{code}

We characterize connected types in terms of inhabitedness and connectedness of
the identity type at one level below. We will assume univalence only when necessary.

\begin{code}

 connected-types-are-inhabited : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                               â†’ X is (k + 1) connected â†’ âˆ¥ X âˆ¥
 connected-types-are-inhabited {_} {_} {k} X-conn =
  âˆ’1-connected-types-are-inhabited (connectedness-is-lower-closed' â‹† X-conn)

 _is-locally_connected : (X : ğ“¤ Ì‡ ) (k : â„•â‚‹â‚‚) â†’ ğ“¤ Ì‡
 X is-locally k connected = (x y : X) â†’ (x ï¼ y) is k connected

 connected-types-are-locally-connected : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                                       â†’ is-univalent ğ“¤
                                       â†’ X is (k + 1) connected
                                       â†’ X is-locally k connected
 connected-types-are-locally-connected {_} {_} {k} ua X-conn x y =
  equiv-to-singleton (eliminated-trunc-identity-char ua)
   (is-prop-implies-is-prop' (singletons-are-props X-conn)
    âˆ£ x âˆ£[ succ k ] âˆ£ y âˆ£[ succ k ])

 connected-types-are-inhabited-and-locally-connected : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                                                     â†’ is-univalent ğ“¤
                                                     â†’ X is (k + 1) connected
                                                     â†’ âˆ¥ X âˆ¥
                                                     Ã— X is-locally k connected
 connected-types-are-inhabited-and-locally-connected ua X-conn =
  (connected-types-are-inhabited X-conn , connected-types-are-locally-connected ua X-conn)

 inhabited-and-locally-connected-types-are-connected : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                                                     â†’ is-univalent ğ“¤
                                                     â†’ âˆ¥ X âˆ¥
                                                     â†’ X is-locally k connected
                                                     â†’ X is (k + 1) connected
 inhabited-and-locally-connected-types-are-connected
  {_} {_} {âˆ’2} ua anon-x id-conn =
  pointed-props-are-singletons (prop-trunc-to-âˆ’1-trunc pt anon-x) âˆ’1-trunc-is-prop
 inhabited-and-locally-connected-types-are-connected
  {_} {_} {succ k} ua anon-x id-conn =
  âˆ¥âˆ¥-rec (being-singleton-is-prop fe)
         (Î» x â†’ (âˆ£ x âˆ£[ (k + 1) + 1 ]
          , âˆ¥âˆ¥â‚™-ind (Î» v â†’ truncation-levels-are-upper-closed
           (Î» p q â†’ âˆ¥âˆ¥â‚™-is-truncated âˆ£ x âˆ£[ (k + 1) + 1 ] v p q))
            (Î» y â†’ forth-trunc-id-char ua (center (id-conn x y)))))
         anon-x

 connected-characterization : {X : ğ“¤ Ì‡ } {k : â„•â‚‹â‚‚}
                            â†’ is-univalent ğ“¤
                            â†’ X is (k + 1) connected
                            â†” âˆ¥ X âˆ¥ Ã— X is-locally k connected
 connected-characterization ua =
  (connected-types-are-inhabited-and-locally-connected ua
   , uncurry (inhabited-and-locally-connected-types-are-connected ua))

 ap-is-less-connected : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {k : â„•â‚‹â‚‚}
                      â†’ (ua : is-univalent (ğ“¤ âŠ” ğ“¥))
                      â†’ (f : X â†’ Y)
                      â†’ f is (k + 1) connected-map
                      â†’ {x x' : X}
                      â†’ (ap f {x} {x'}) is k connected-map
 ap-is-less-connected ua f f-conn {x} {x'} p =
  equiv-to-singleton (truncation-closed-under-equiv (fiber-of-ap-â‰ƒ f p))
   (connected-types-are-locally-connected ua (f-conn (f x'))
    (x , p) (x' , refl))

\end{code}

We prove a characterization of connectedness from the HoTT book
(see Corollary 7.5.9.). Notice we choose to directly prove this result
rather than instantiate it as a special case of a more general result
(Lemma 7.5.7.) which is stated below.

NOTE: We will NOT state the corollary as an iff statement due to a large
quantification issue.

\begin{code}

 private
  consts : (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ )
         â†’ Y â†’ (X â†’ Y)
  consts X Y y x = y

 maps-with-connected-dom-truncated-cod-are-constant : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {n : â„•â‚‹â‚‚}
                                                    â†’ X is n connected
                                                    â†’ Y is n truncated
                                                    â†’ Y â‰ƒ (X â†’ Y)
 maps-with-connected-dom-truncated-cod-are-constant {ğ“¤} {_} {X} {Y} {n}
  X-conn Y-trunc = e
  where
   e : Y â‰ƒ (X â†’ Y)
   e = Y                â‰ƒâŸ¨ ğŸ™â†’ fe âŸ©
       (ğŸ™ {ğ“¤} â†’ Y)      â‰ƒâŸ¨ â†’cong'' fe fe I âŸ©
       (âˆ¥ X âˆ¥[ n ] â†’ Y) â‰ƒâŸ¨ âˆ¥âˆ¥â‚™-universal-property Y-trunc âŸ©
       (X â†’ Y)          â– 
    where
     I : ğŸ™ {ğ“¤} â‰ƒ âˆ¥ X âˆ¥[ n ]
     I = ğŸ™-â‰ƒ-singleton X-conn
   observation : consts X Y ï¼ âŒœ e âŒ
   observation = refl

 constants-map-from-truncated-type-is-equiv : {X : ğ“¤ Ì‡ } {n : â„•â‚‹â‚‚}
                                            â†’ (Y : ğ“¥ Ì‡ )
                                            â†’ X is n connected
                                            â†’ Y is n truncated
                                            â†’ is-equiv (consts X Y)
 constants-map-from-truncated-type-is-equiv Y X-conn Y-trunc =
  âŒœâŒ-is-equiv (maps-with-connected-dom-truncated-cod-are-constant X-conn Y-trunc)

 connected-when-consts-is-equiv : {X : ğ“¤ Ì‡ } {n : â„•â‚‹â‚‚}
                              â†’ ({ğ“¥ : Universe} (Y : ğ“¥ Ì‡ )
                               â†’ Y is n truncated
                               â†’ is-equiv (consts X Y))
                              â†’ X is n connected
 connected-when-consts-is-equiv {_} {X} {n} is-equiv-from-trunc = (c , G)
  where
   s : (X â†’ âˆ¥ X âˆ¥[ n ]) â†’ âˆ¥ X âˆ¥[ n ]
   s = section-map (consts X âˆ¥ X âˆ¥[ n ])
        (equivs-have-sections (consts X âˆ¥ X âˆ¥[ n ])
        (is-equiv-from-trunc âˆ¥ X âˆ¥[ n ] âˆ¥âˆ¥â‚™-is-truncated))
   H : (consts X âˆ¥ X âˆ¥[ n ]) âˆ˜ s âˆ¼ id
   H = section-equation (consts X âˆ¥ X âˆ¥[ n ])
        (equivs-have-sections (consts X âˆ¥ X âˆ¥[ n ])
         (is-equiv-from-trunc âˆ¥ X âˆ¥[ n ] âˆ¥âˆ¥â‚™-is-truncated))
   c : âˆ¥ X âˆ¥[ n ]
   c = s âˆ£_âˆ£[ n ]
   H' : (consts X âˆ¥ X âˆ¥[ n ]) c ï¼ âˆ£_âˆ£[ n ]
   H' = H âˆ£_âˆ£[ n ]
   G : (v : âˆ¥ X âˆ¥[ n ]) â†’ c ï¼ v
   G = âˆ¥âˆ¥â‚™-ind (Î» - â†’ truncation-levels-are-upper-closed âˆ¥âˆ¥â‚™-is-truncated c -)
               (Î» x â†’ happly H' x)

\end{code}

We will now prove a general result from the HoTT book that characterizes when
a map is connected (see Lemma 7.5.7.)

\begin{code}

 dependent-equiv-from-truncated-fam-connected-map
  : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {n : â„•â‚‹â‚‚}
  â†’ (f : X â†’ Y)
  â†’ (P : Y â†’ ğ“¦ Ì‡ )
  â†’ ((y : Y) â†’ (P y) is n truncated)
  â†’ f is n connected-map
  â†’ ((y : Y) â†’ P y) â‰ƒ ((x : X) â†’ P (f x))
 dependent-equiv-from-truncated-fam-connected-map
  {_} {_} {_} {X} {Y} {n} f P
  P-trunc f-conn = e
  where
   e : ((y : Y) â†’ P y) â‰ƒ ((x : X) â†’ P (f x))
   e = ((y : Y) â†’ P y)                                         â‰ƒâŸ¨ I âŸ©
       ((y : Y) â†’ (fiber f y â†’ P y))                           â‰ƒâŸ¨ II âŸ©
       ((y : Y) â†’ (x : X) â†’ (p : f x ï¼ y) â†’ P y)              â‰ƒâŸ¨ Î -flip' âŸ©
       ((x : X) â†’ (y : Y) â†’ (p : f x ï¼ y) â†’ P y)              â‰ƒâŸ¨ III âŸ©
       ((x : X) â†’ P (f x))                                     â– 
    where
     I = Î -cong fe fe (Î» - â†’ maps-with-connected-dom-truncated-cod-are-constant
                       (f-conn -) (P-trunc -))
     II = Î -cong fe fe (Î» - â†’ curry-uncurry' fe fe)
     III = Î -cong fe fe (Î» - â†’ â‰ƒ-sym (Yoneda-equivalence fe' (f -) P))
   observation : âŒœ e âŒ ï¼ dprecomp P f
   observation = refl

 dep-precomp-from-truncated-family-is-equiv
  : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {n : â„•â‚‹â‚‚}
  â†’ (f : X â†’ Y)
  â†’ (P : Y â†’ ğ“¦ Ì‡ )
  â†’ ((y : Y) â†’ (P y) is n truncated)
  â†’ f is n connected-map
  â†’ is-equiv (dprecomp P f)
 dep-precomp-from-truncated-family-is-equiv f P P-trunc f-conn =
  âŒœâŒ-is-equiv (dependent-equiv-from-truncated-fam-connected-map f P P-trunc f-conn)

 dep-precomp-has-section-if-is-equiv
  : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
  â†’ (f : X â†’ Y)
  â†’ (P : Y â†’ ğ“¦ Ì‡ )
  â†’ is-equiv (dprecomp P f)
  â†’ has-section (dprecomp P f)
 dep-precomp-has-section-if-is-equiv f P = equivs-have-sections (dprecomp P f)

 map-is-connected-if-dep-precomp-from-truncated-family-has-section
  : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {n : â„•â‚‹â‚‚}
  â†’ (f : X â†’ Y)
  â†’ ({ğ“¦ : Universe} (P : Y â†’ ğ“¦ Ì‡ ) â†’ ((y : Y) â†’ (P y) is n truncated)
                                 â†’ has-section (dprecomp P f))
  â†’ f is n connected-map
 map-is-connected-if-dep-precomp-from-truncated-family-has-section
  {ğ“¤} {ğ“¥} {X} {Y} {n} f sec-from-trunc y = (c y , C)
  where
   Q : Y â†’ ğ“¤ âŠ” ğ“¥ Ì‡
   Q y = âˆ¥ fiber f y âˆ¥[ n ]
   c' : ((x : X) â†’ âˆ¥ fiber f (f x) âˆ¥[ n ])
      â†’ ((y : Y) â†’ âˆ¥ fiber f y âˆ¥[ n ])
   c' = section-map (dprecomp Q f) (sec-from-trunc Q (Î» - â†’ âˆ¥âˆ¥â‚™-is-truncated))
   c : (y : Y) â†’ âˆ¥ fiber f y âˆ¥[ n ]
   c = c' (Î» - â†’ âˆ£ (- , refl) âˆ£[ n ])
   H' : (dprecomp Q f) âˆ˜ c' âˆ¼ id
   H' = section-equation (dprecomp Q f)
                         (sec-from-trunc Q (Î» - â†’ âˆ¥âˆ¥â‚™-is-truncated))
   H : (x : X) â†’ c (f x) ï¼ âˆ£ (x , refl) âˆ£[ n ]
   H = happly' ((dprecomp Q f âˆ˜ c') (Î» - â†’ âˆ£ (- , refl) âˆ£[ n ]))
               (Î» - â†’ âˆ£ (- , refl) âˆ£[ n ]) (H' (Î» - â†’ âˆ£ (- , refl) âˆ£[ n ]))
   C : (w : âˆ¥ fiber f y âˆ¥[ n ]) â†’ c y ï¼ w
   C = âˆ¥âˆ¥â‚™-ind (Î» - â†’ truncation-levels-are-upper-closed âˆ¥âˆ¥â‚™-is-truncated
                (c y) -)
               C'
    where
     C' : ((x , p) : fiber f y) â†’ c y ï¼ âˆ£ (x , p) âˆ£[ n ]
     C' (x , refl) = H x

\end{code}

We show that the n-truncation map is n-connected.

\begin{code}

 trunc-map-is-connected : {X : ğ“¤ Ì‡ } {n : â„•â‚‹â‚‚}
                        â†’ âˆ£_âˆ£[ n ] is n connected-map
 trunc-map-is-connected {_} {X} {n} =
  map-is-connected-if-dep-precomp-from-truncated-family-has-section
   âˆ£_âˆ£[ n ] has-sec
  where
   has-sec : {ğ“¦ : Universe}
           â†’ (P : âˆ¥ X âˆ¥[ n ] â†’ ğ“¦ Ì‡ )
           â†’ ((v : âˆ¥ X âˆ¥[ n ]) â†’ P v is n truncated)
           â†’ has-section (dprecomp P âˆ£_âˆ£[ n ])
   has-sec {_} P P-trunc = (âˆ¥âˆ¥â‚™-ind P-trunc , comp-rule)
    where
     comp-rule : dprecomp P âˆ£_âˆ£[ n ] âˆ˜ âˆ¥âˆ¥â‚™-ind P-trunc âˆ¼ id
     comp-rule h = (dprecomp P âˆ£_âˆ£[ n ]) (âˆ¥âˆ¥â‚™-ind P-trunc h) ï¼âŸ¨reflâŸ©
                   (âˆ¥âˆ¥â‚™-ind P-trunc h) âˆ˜ âˆ£_âˆ£[ n ]            ï¼âŸ¨ I âŸ©
                   h                                         âˆ
      where
       I = dfunext fe (âˆ¥âˆ¥â‚™-ind-comp P-trunc h)

\end{code}

We provide a useful result about maps from the constant type to a connected
type (see Lemma 7.5.11 of the HoTT book for the full statement). Observe we
do assume univalence locally here (the HoTT book assumes it implicitly).

\begin{code}

 basepoint-map-is-less-connected : {X : ğ“¤ Ì‡ } {n : â„•â‚‹â‚‚}
                                 â†’ is-univalent ğ“¤
                                 â†’ (xâ‚€ : ğŸ™ {ğ“¤} â†’ X)
                                 â†’ X is (n + 1) connected
                                 â†’ xâ‚€ is n connected-map
 basepoint-map-is-less-connected {_} {X} {n} ua xâ‚€ X-con x =
  connectedness-closed-under-equiv ğŸ™-lneutral
   (connected-types-are-locally-connected ua X-con (xâ‚€ â‹†) x)

\end{code}
