Martin Escardo

Based on

 Kraus, N., EscardÃ³, M., Coquand, T., Altenkirch, T.
 Generalizations of Hedbergâ€™s Theorem.
 In: Hasegawa, M. (eds) Typed Lambda Calculi and Applications.
 TLCA 2013. Lecture Notes in Computer Science, vol 7941.
 Springer, Berlin, Heidelberg.
 https://doi.org/10.1007/978-3-642-38946-7_14

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.Hedberg where

open import MLTT.Spartan
open import UF.Base
open import UF.Sets
open import UF.Subsingletons

wconstant : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (f : X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
wconstant f = âˆ€ x y â†’ f x ï¼ f y

wconstant-pre-comp : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ }
                     (f : X â†’ Y) (g : Y â†’ Z)
                   â†’ wconstant f
                   â†’ wconstant (g âˆ˜ f)
wconstant-pre-comp f g c x x' = ap g (c x x')

wconstant-post-comp : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ }
                      (f : X â†’ Y) (g : Y â†’ Z)
                    â†’ wconstant g
                    â†’ wconstant (g âˆ˜ f)
wconstant-post-comp f g c x x' = c (f x) (f x')

collapsible : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
collapsible X = Î£ f ê‰ (X â†’ X) , wconstant f

Id-collapsible' : {X : ğ“¤ Ì‡ } â†’ X â†’ ğ“¤ Ì‡

Id-collapsible' x = âˆ€ {y} â†’ collapsible (x ï¼ y)

Id-collapsible : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Id-collapsible X = {x : X} â†’ Id-collapsible' x

h-isolated-points-are-Id-collapsible : {X : ğ“¤ Ì‡ } {x : X}
                                     â†’ is-h-isolated x
                                     â†’ Id-collapsible' x
h-isolated-points-are-Id-collapsible h = id , h

sets-are-Id-collapsible : {X : ğ“¤ Ì‡ } â†’ is-set X â†’ Id-collapsible X
sets-are-Id-collapsible u = (id , u)

local-hedberg : {X : ğ“¤ Ì‡ } (x : X)
              â†’ ((y : X) â†’ collapsible (x ï¼ y))
              â†’ (y : X) â†’ is-prop (x ï¼ y)
local-hedberg {ğ“¤} {X} x pc y p q =
 p                    ï¼âŸ¨ c y p âŸ©
 f x refl â»Â¹ âˆ™ f y p  ï¼âŸ¨ ap (Î» - â†’ (f x refl)â»Â¹ âˆ™ -) (Îº y p q) âŸ©
 f x refl â»Â¹ âˆ™ f y q  ï¼âŸ¨ (c y q)â»Â¹ âŸ©
 q                    âˆ
 where
  f : (y : X) â†’ x ï¼ y â†’ x ï¼ y
  f y = prâ‚ (pc y)

  Îº : (y : X) (p q : x ï¼ y) â†’ f y p ï¼ f y q
  Îº y = prâ‚‚ (pc y)

  c : (y : X) (r : x ï¼ y) â†’ r ï¼ (f x refl)â»Â¹ âˆ™ f y r
  c _ refl = sym-is-inverse (f x refl)

Id-collapsibles-are-h-isolated : {X : ğ“¤ Ì‡ } (x : X)
                               â†’ Id-collapsible' x
                               â†’ is-h-isolated x
Id-collapsibles-are-h-isolated x pc {y} p q =
 local-hedberg x (Î» y â†’ (prâ‚ (pc {y})) , (prâ‚‚ (pc {y}))) y p q

Id-collapsibles-are-sets : {X : ğ“¤ Ì‡ } â†’ Id-collapsible X â†’ is-set X
Id-collapsibles-are-sets pc {x} = Id-collapsibles-are-h-isolated x pc

\end{code}

Here is an example. Any type that admits a prop-valued, reflexive and
antisymmetric relation is a set.

\begin{code}

type-with-prop-valued-refl-antisym-rel-is-set : {X : ğ“¤ Ì‡ }
                                              â†’ (_â‰¤_ : X â†’ X â†’ ğ“¥ Ì‡ )
                                              â†’ ((x y : X) â†’ is-prop (x â‰¤ y))
                                              â†’ ((x : X) â†’ x â‰¤ x)
                                              â†’ ((x y : X) â†’ x â‰¤ y â†’ y â‰¤ x â†’ x ï¼ y)
                                              â†’ is-set X
type-with-prop-valued-refl-antisym-rel-is-set
 {ğ“¤} {ğ“¥} {X} _â‰¤_ â‰¤-prop-valued â‰¤-refl â‰¤-anti = Î³
 where
  Î± : âˆ€ {x y} (l l' : x â‰¤ y) (m m' : y â‰¤ x) â†’ â‰¤-anti x y l m ï¼ â‰¤-anti x y l' m'
  Î± {x} {y} l l' m m' = apâ‚‚ (â‰¤-anti x y)
                            (â‰¤-prop-valued x y l l')
                            (â‰¤-prop-valued y x m m')

  g : âˆ€ {x y} â†’ x ï¼ y â†’ x â‰¤ y
  g {x} p = transport (x â‰¤_) p (â‰¤-refl x)

  h : âˆ€ {x y} â†’ x ï¼ y â†’ y â‰¤ x
  h p = g (p â»Â¹)

  f : âˆ€ {x y} â†’ x ï¼ y â†’ x ï¼ y
  f {x} {y} p = â‰¤-anti x y (g p) (h p)

  Îº : âˆ€ {x y} p q â†’ f {x} {y} p ï¼ f {x} {y} q
  Îº p q = Î± (g p) (g q) (h p) (h q)

  Î³ : is-set X
  Î³ = Id-collapsibles-are-sets (f , Îº)

\end{code}

We also need the following symmetrical version of local Hedberg, which
can be proved by reduction to the above (using the fact that
collapsible types are closed under equivalence), but at this point we
don't have the machinery at our disposal (which is developed in
modules that depend on this one), and hence we prove it directly by
symmetrizing the proof.

\begin{code}

local-hedberg' : {X : ğ“¤ Ì‡ } (x : X)
               â†’ ((y : X) â†’ collapsible (y ï¼ x))
               â†’ (y : X) â†’ is-prop (y ï¼ x)
local-hedberg' {ğ“¤} {X} x pc y p q =
  p                     ï¼âŸ¨ c y p âŸ©
  f y p âˆ™ (f x refl)â»Â¹  ï¼âŸ¨  ap (Î» - â†’ - âˆ™ (f x refl)â»Â¹) (Îº y p q) âŸ©
  f y q âˆ™ (f x refl)â»Â¹  ï¼âŸ¨ (c y q)â»Â¹ âŸ©
  q                     âˆ
 where
  f : (y : X) â†’ y ï¼ x â†’ y ï¼ x
  f y = prâ‚ (pc y)

  Îº : (y : X) (p q : y ï¼ x) â†’ f y p ï¼ f y q
  Îº y = prâ‚‚ (pc y)

  c : (y : X) (r : y ï¼ x) â†’ r ï¼  (f y r) âˆ™ (f x refl)â»Â¹
  c _ refl = sym-is-inverse' (f x refl)

\end{code}
