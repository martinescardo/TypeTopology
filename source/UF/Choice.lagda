Martin Escardo 7 May 2014, 10 Oct 2014, 25 January 2018, 17 December 2022.

Several equivalent formulations of the axiom of choice in HoTT/UF.

We first look at choice as in the HoTT book a little bit more
abstractly, where for the HoTT book we take T X = âˆ¥ X âˆ¥. It also makes
sense to consider T = Â¬Â¬, in connection with the double-negation
shift.

Choice in the HoTT book, under the assumption that X is a set and A is
an X-indexed family of sets is

    (Î  x ê‰ X , âˆ¥ A x âˆ¥) â†’ âˆ¥ Î  x ê‰ X , A x âˆ¥

(a set-indexed product of inhabited sets is inhabited).

We show that, under the same assumptions, this is equivalent

    âˆ¥ (Î  x ê‰ X , âˆ¥ A x âˆ¥ â†’ A x) âˆ¥.

Notice that, as shown in the HoTT book, the statement

    (B : ğ“¤ Ì‡ ) â†’ âˆ¥ B âˆ¥ â†’ B

is in contradiction with the univalence axiom (we cannot reveal
secrets in general). However, univalent choice is consistent with the
univalent axiom, and, moreover, gives that

   âˆ¥(B : ğ“¤ Ì‡ ) â†’ âˆ¥ âˆ¥ B âˆ¥ â†’ B âˆ¥

(one can secretly reveal secrets always), which is equivalent to
choice where X is a proposition (see https://arxiv.org/abs/1610.03346).

And there are also a number of other equivalent formulations of the
axiom of choice, of which the following seems to be new:

  Under the presence of propositional extensionality, the axiom of
  choice is equivalent to the conjunction of the principle of excluded
  middle and the double negation shift (DNS) for *sets* rather than
  propositions.

Here DNS is

    (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
  â†’ is-set X
  â†’ ((x : X) â†’ is-set (A x))
  â†’ (Î  x ê‰ X , Â¬Â¬ A x)
  â†’ Â¬Â¬ (Î  x ê‰ X , A x)

All implications and logical equivalences here are proved in a spartan
(intensional) MLTT extended with the existence propositional
truncations (formulated in the language of MLTT).

Notice that we cannot apply excluded middle to A x, because, by
assumption, it is a set, and excluded middle applies to propositions
(types with at most one element).

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.Choice where

open import MLTT.Spartan
open import UF.Base
open import UF.ClassicalLogic
open import UF.DiscreteAndSeparated
open import UF.FunExt
open import UF.LeftCancellable
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier
open import UF.SubtypeClassifier-Properties

module Shift
        (T : {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡ )
        (T-functor : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ T X â†’ T Y)
       where

\end{code}

The T-shift for a family A : X â†’ ğ“¤ Ì‡ is

    (Î  x ê‰ X , T (A x)) â†’  T (Î  x ê‰ X , A x).

We observe that this is equivalent to

    T (Î  x ê‰ X , T (A x) â†’ A x)

This generalizes the fact that the double negation shift is equivalent
to

   Â¬Â¬ (Î  x ê‰ X , A x + Â¬ (A x))

or

   Â¬Â¬ (Î  x ê‰ X , Â¬Â¬ A x â†’ A x)

\begin{code}

 Shift : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 Shift {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
               â†’ (Î  x ê‰ X , T (A x))
               â†’ T (Î  x ê‰ X , A x)

 Shift' : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 Shift' {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
                â†’ T (Î  x ê‰ X , (T (A x) â†’ A x))

 Shift-gives-Shift' : Shift {ğ“¤} {ğ“¤} â†’ Shift' {ğ“¤} {ğ“¤}
 Shift-gives-Shift' {ğ“¤} s X A = s X (Î» x â†’ T (A x) â†’ A x) (Î» x â†’ F s (A x))
  where
   F : Shift â†’ (X : ğ“¤ Ì‡ ) â†’ T (T X â†’ X)
   F s X = s (T X) (Î» _ â†’ X) (Î» x â†’ x)

 Shift'-gives-Shift : Shift' {ğ“¤} {ğ“¥} â†’ Shift {ğ“¤} {ğ“¥}
 Shift'-gives-Shift s' X A Ï† = T-functor (F Ï†) (s' X A)
  where
   F : ((x : X) â†’ T (A x)) â†’ ((x : X) â†’ T (A x) â†’ A x) â†’ (x : X) â†’ A x
   F Ï† Ïˆ x = Ïˆ x (Ï† x)

\end{code}

We now add the above constraints of the HoTT book for choice, but
abstractly, where T may be âˆ¥_âˆ¥ and S may be is-set.

\begin{code}

module TChoice
        (T : {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡ )
        (T-functor : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ T X â†’ T Y)
        (S : {ğ“¤ : Universe} â†’ ğ“¤ Ì‡ â†’ ğ“¤ Ì‡ )
        (S-exponential-ideal : {ğ“¤ ğ“¥ : Universe} {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                             â†’ S Y â†’ S (X â†’ Y))
        (T-is-S : {ğ“¤ : Universe} {X : ğ“¤ Ì‡ } â†’ S (T X))
       where

 TAC : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 TAC {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
              â†’ S X
              â†’ (Î  x ê‰ X , S (A x))
              â†’ ((x : X) â†’ T (A x)) â†’ T (Î  x ê‰ X , A x)

 TAC' : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 TAC' {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
               â†’ S X
               â†’ (Î  x ê‰ X , S (A x))
               â†’ T (Î  x ê‰ X , (T (A x) â†’ A x))

 T-lemma : TAC â†’ (X : ğ“¤ Ì‡ ) â†’ S X â†’ T (T X â†’ X)
 T-lemma tac X s = tac (T X) (Î» _ â†’ X) T-is-S  (Î» x â†’ s) (Î» x â†’ x)

 TAC-gives-TAC' : TAC {ğ“¤} {ğ“¤} â†’ TAC' {ğ“¤} {ğ“¤}
 TAC-gives-TAC' tac X A s t = tac
                               X
                               (Î» x â†’ T (A x) â†’ A x)
                               s
                               (Î» x â†’ S-exponential-ideal (t x))
                               (Î» x â†’ T-lemma tac (A x) (t x))

 TAC'-gives-TAC : TAC' {ğ“¤} {ğ“¥} â†’ TAC {ğ“¤} {ğ“¥}
 TAC'-gives-TAC c' X A s t Ï† = T-functor (Î» Ïˆ x â†’ Ïˆ x (Ï† x)) (c' X A s t)

\end{code}

January 2018.

We now implement the examples discussed above, which give
characterizations choice as in the HoTT book, which we refer to as
Univalent Choice.

\begin{code}

module Univalent-Choice
        (fe : FunExt)
        (pt : propositional-truncations-exist)
        where

 open PropositionalTruncation pt

 open TChoice
       âˆ¥_âˆ¥
       âˆ¥âˆ¥-functor
       is-set
       (Î» Y-is-set â†’ Î -is-set (fe _ _) (Î» _ â†’ Y-is-set))
       (props-are-sets âˆ¥âˆ¥-is-prop)

 ACâ‚€ : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥) âº Ì‡
 ACâ‚€ {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ ) (P : (x : X) â†’ A x â†’ ğ“¥ Ì‡ )
             â†’ is-set X
             â†’ ((x : X) â†’ is-set (A x))
             â†’ ((x : X) (a : A x) â†’ is-prop (P x a))
             â†’ ((x : X) â†’ âˆƒ a ê‰ A x , P x a)
             â†’ âˆƒ f ê‰ Î  A , ((x : X) â†’ P x (f x))

 ACâ‚ : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 ACâ‚ {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
              â†’ is-set X
              â†’ ((x : X) â†’ is-set (A x))
              â†’ (Î  x ê‰ X , âˆ¥ A x âˆ¥)
              â†’ âˆ¥(Î  x ê‰ X , A x)âˆ¥

 ACâ‚‚ : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 ACâ‚‚ {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
              â†’ is-set X
              â†’ ((x : X) â†’ is-set (A x))
              â†’ âˆ¥(Î  x ê‰ X , (âˆ¥ A x âˆ¥ â†’ A x))âˆ¥

 Axiom-of-Choice Axiom-of-Choiceâ‚ Axiom-of-Choiceâ‚‚ : ğ“¤Ï‰
 Axiom-of-Choice  = {ğ“¤ ğ“¥ : Universe} â†’ ACâ‚€  {ğ“¤} {ğ“¥}
 Axiom-of-Choiceâ‚ = {ğ“¤ ğ“¥ : Universe} â†’ ACâ‚ {ğ“¤} {ğ“¥}
 Axiom-of-Choiceâ‚‚ = {ğ“¤ ğ“¥ : Universe} â†’ ACâ‚‚ {ğ“¤} {ğ“¥}

 ACâ‚€-gives-ACâ‚ : ACâ‚€ {ğ“¤} {ğ“¥} â†’ ACâ‚ {ğ“¤} {ğ“¥}
 ACâ‚€-gives-ACâ‚ ac X A i j f = h
  where
   g : âˆƒ f ê‰ Î  A , (X â†’ ğŸ™)
   g = ac X A
        (Î» x a â†’ ğŸ™)
        i
        j (
        Î» x a â†’ ğŸ™-is-prop)
        (Î» x â†’ âˆ¥âˆ¥-functor (Î» z â†’ z , â‹†)
        (f x))

   h : âˆ¥ Î  A âˆ¥
   h = âˆ¥âˆ¥-functor prâ‚ g

 ACâ‚-gives-ACâ‚€ : ACâ‚ {ğ“¤} {ğ“¥} â†’ ACâ‚€ {ğ“¤} {ğ“¥}
 ACâ‚-gives-ACâ‚€ acâ‚ X A P s t i f = âˆ¥âˆ¥-functor Î Î£-distr g
  where
   g : âˆ¥(Î  x ê‰ X , Î£ a ê‰ A x , P x a)âˆ¥
   g = acâ‚ X (Î» x â†’ Î£ a ê‰ A x , P x a)
        s
        (Î» x â†’ subsets-of-sets-are-sets (A x) (P x) (t x) (Î» {a} â†’ i x a))
        f

 ACâ‚-gives-ACâ‚‚ : ACâ‚ {ğ“¤} {ğ“¤} â†’ ACâ‚‚ {ğ“¤} {ğ“¤}
 ACâ‚-gives-ACâ‚‚ = TAC-gives-TAC'

 ACâ‚‚-gives-ACâ‚ : ACâ‚‚ {ğ“¤} {ğ“¥} â†’ ACâ‚ {ğ“¤} {ğ“¥}
 ACâ‚‚-gives-ACâ‚ = TAC'-gives-TAC

 secretly-revealing-secrets : ACâ‚ â†’ (B : ğ“¤ Ì‡ ) â†’ is-set B â†’ âˆ¥(âˆ¥ B âˆ¥ â†’ B)âˆ¥
 secretly-revealing-secrets = T-lemma

\end{code}

But choice implies excluded middle. Provided we have quotients. In
fact, the quotient ğŸš/P of ğŸš by the relation R â‚€ â‚ = P, for any given
proposition P, suffices. In that case, we conclude that, assuming
function extensionality, AC is equivalent to EM Ã— DNS.

What if we don't (necessarily) have the quotient ğŸš/P for an arbitrary
proposition P?  We get from AC that all sets have decidable
equality. This is because the quotient ğŸš/(aâ‚€ï¼aâ‚), for two points aâ‚€
and aâ‚ of a set X can be constructed as the image of the map a : ğŸš â†’ X
with values a â‚€ = aâ‚€ and a â‚ = aâ‚.

\begin{code}

module ExcludedMiddle
        (pt : propositional-truncations-exist)
        (fe : FunExt)
       where

 open PropositionalTruncation pt
 open Univalent-Choice fe pt
 open import UF.ImageAndSurjection pt

\end{code}

I originally proved this on 1st April 2013.

\begin{code}

 decidability-lemma : {X : ğ“¤ Ì‡ } (a : ğŸš â†’ X)
                    â†’ ((x : X) â†’ (âˆƒ i ê‰ ğŸš , a i ï¼ x) â†’ Î£ i ê‰ ğŸš , a i ï¼ x)
                    â†’ is-decidable (a â‚€ ï¼ a â‚)
 decidability-lemma a c = claim (ğŸš-is-discrete (s(r â‚€)) (s(r â‚)))
  where
   r : ğŸš â†’ image a
   r = corestriction a

   r-splits : (y : image a) â†’ Î£ i ê‰ ğŸš , r i ï¼ y
   r-splits (x , t) = f (c x t)
    where
     f : (Î£ i ê‰ ğŸš , a i ï¼ x) â†’ Î£ i ê‰ ğŸš , r i ï¼ (x , t)
     f (i , p) = i , to-Î£-ï¼ (p , âˆ¥âˆ¥-is-prop _ t)

   s : image a â†’ ğŸš
   s y = prâ‚(r-splits y)

   rs : (y : image a) â†’ r(s y) ï¼ y
   rs y = prâ‚‚(r-splits y)

   s-lc : left-cancellable s
   s-lc = section-lc s (r , rs)

   a-r : {i j : ğŸš} â†’ a i ï¼ a j â†’ r i ï¼ r j
   a-r p = to-Î£-ï¼ (p , âˆ¥âˆ¥-is-prop _ _)

   r-a : {i j : ğŸš} â†’ r i ï¼ r j â†’ a i ï¼ a j
   r-a = ap prâ‚

   a-s : {i j : ğŸš} â†’ a i ï¼ a j â†’ s(r i) ï¼ s(r j)
   a-s p = ap s (a-r p)

   s-a : {i j : ğŸš} â†’ s(r i) ï¼ s(r j) â†’ a i ï¼ a j
   s-a p = r-a (s-lc p)

   claim : is-decidable (s(r â‚€) ï¼ s(r â‚)) â†’ is-decidable (a â‚€ ï¼ a â‚)
   claim (inl p) = inl (s-a p)
   claim (inr u) = inr (contrapositive a-s u)

 decidability-lemmaâ‚‚ : {X : ğ“¤ Ì‡ }
                     â†’ is-set X
                     â†’ (a : ğŸš â†’ X)
                     â†’ âˆ¥((x : X) â†’ (âˆƒ i ê‰ ğŸš , a i ï¼ x) â†’ Î£ i ê‰ ğŸš , a i ï¼ x)âˆ¥
                     â†’ is-decidable (a â‚€ ï¼ a â‚)
 decidability-lemmaâ‚‚ i a =
  âˆ¥âˆ¥-rec (decidability-of-prop-is-prop (fe _ _) i) (decidability-lemma a)

 ACâ‚€-renders-all-sets-discrete' : ACâ‚€ {ğ“¤} {ğ“¤}
                                â†’ (X : ğ“¤ Ì‡ )
                                â†’ is-set X
                                â†’ (a : ğŸš â†’ X) â†’ is-decidable (a â‚€ ï¼ a â‚)
 ACâ‚€-renders-all-sets-discrete' {ğ“¤} ac X i a =
  decidability-lemmaâ‚‚ i a (acâ‚‚ X A i j)
  where
   A : X â†’ ğ“¤ Ì‡
   A x = Î£ i ê‰ ğŸš , a i ï¼ x

   j : (x : X) â†’ is-set (A x)
   j x = subsets-of-sets-are-sets ğŸš (Î» i â†’ a i ï¼ x) ğŸš-is-set i

   acâ‚‚ : ACâ‚‚ {ğ“¤} {ğ“¤}
   acâ‚‚ = ACâ‚-gives-ACâ‚‚ (ACâ‚€-gives-ACâ‚ ac)

 ACâ‚€-renders-all-sets-discrete : ACâ‚€ {ğ“¤} {ğ“¤}
                               â†’ (X : ğ“¤ Ì‡ )
                               â†’ is-set X
                               â†’ (aâ‚€ aâ‚ : X) â†’ is-decidable (aâ‚€ ï¼ aâ‚)
 ACâ‚€-renders-all-sets-discrete {ğ“¤} ac X isx aâ‚€ aâ‚ =
  ACâ‚€-renders-all-sets-discrete' {ğ“¤} ac X isx (ğŸš-cases aâ‚€ aâ‚)

 ACâ‚€-gives-EM : PropExt â†’ ACâ‚€ {ğ“¤ âº} {ğ“¤ âº} â†’ EM ğ“¤
 ACâ‚€-gives-EM {ğ“¤} pe ac =
  Î©-discrete-gives-EM (fe _ _) (pe _)
   (ACâ‚€-renders-all-sets-discrete {ğ“¤ âº} ac (Î© ğ“¤)
                                  (Î©-is-set (fe ğ“¤ ğ“¤) (pe ğ“¤)))

 Choice-gives-Excluded-Middle : PropExt
                              â†’ Axiom-of-Choice
                              â†’ Excluded-Middle
 Choice-gives-Excluded-Middle pe ac {ğ“¤} = ACâ‚€-gives-EM {ğ“¤} pe (ac {ğ“¤ âº})

\end{code}

Is there a way to define the quotient ğŸš/P for an arbitrary
proposition P, in the universe ğ“¤, using propositional truncation as
the only HIT, and funext, propext? We could allow, more generally,
univalence.

If so, then, under these conditions, AC is equivalent to excluded
middle together with the double-negation shift for set-indexed
families of sets.

If we assume choice for ğ“¤â‚ we get excluded middle at ğ“¤â‚€. This is
because the quotient ğŸš/P, for a proposition P in ğ“¤â‚€, exists in ğ“¤â‚. In
fact, it is the image of the map ğŸš â†’ Î© that sends â‚€ to ğŸ™ and â‚ to P,
because (ğŸ™ï¼P)ï¼P.

Now, choice is equivalent to the conjunction of the principle of
excluded middle and the double negation shift for families of sets
with arbitrary index set, written DNSâ‚€, which amounts to saying that
products of non-empty sets are non-empty.

\begin{code}

module DNS
        (pt : propositional-truncations-exist)
        (fe : FunExt)
       where

 open PropositionalTruncation pt
 open Univalent-Choice fe pt
 open ExcludedMiddle pt fe

 DNSâ‚€ : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 DNSâ‚€ {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
               â†’ is-set X
               â†’ ((x : X) â†’ is-set (A x))
               â†’ (Î  x ê‰ X , Â¬Â¬ A x)
               â†’ Â¬Â¬ (Î  x ê‰ X , A x)

 Double-Negation-Shiftâ‚€ : ğ“¤Ï‰
 Double-Negation-Shiftâ‚€ = {ğ“¤ ğ“¥ : Universe} â†’ DNSâ‚€ {ğ“¤} {ğ“¥}

 private
  Î± : {X : ğ“¤ Ì‡ } â†’ âˆ¥ X âˆ¥ â†’ Â¬Â¬ X
  Î± = inhabited-is-nonempty

  Î² : EM ğ“¤ â†’ {X : ğ“¤ Ì‡ } â†’ Â¬Â¬ X â†’ âˆ¥ X âˆ¥
  Î² = non-empty-is-inhabited pt

  Î³ : {X : ğ“¤ Ì‡ } â†’ is-set (Â¬Â¬ X)
  Î³ = props-are-sets (negations-are-props (fe _ _))

  Î´ : {ğ“¤ ğ“¥ : Universe} â†’ {X : ğ“¤ Ì‡ } {A : ğ“¥ Ì‡ } â†’ is-set A â†’ is-set (X â†’ A)
  Î´ {ğ“¤} {ğ“¥} A-is-set = Î -is-set (fe _ _) (Î» _ â†’ A-is-set)

 EM-and-ACâ‚-give-DNSâ‚€ : EM ğ“¥ â†’ ACâ‚ {ğ“¤} {ğ“¥} â†’ DNSâ‚€ {ğ“¤} {ğ“¥}
 EM-and-ACâ‚-give-DNSâ‚€ em ac X A i j f = Î± (ac X A i j (Î» x â†’ Î² em (f x)))

 EM-and-DNSâ‚€-give-ACâ‚ : EM (ğ“¤ âŠ” ğ“¥) â†’ DNSâ‚€ {ğ“¤} {ğ“¥} â†’ ACâ‚ {ğ“¤} {ğ“¥}
 EM-and-DNSâ‚€-give-ACâ‚ em dns X A i j g = Î² em (dns X A i j (Î» x â†’ Î± (g x)))

\end{code}

DNS for prop-valued families, written DNSâ‚‹â‚ below, is implies by DNSâ‚€
and is equivalent to the double negation of the (universally
quantified) principle of excluded middle.

\begin{code}

 DNSâ‚‹â‚ : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 DNSâ‚‹â‚ {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¥ Ì‡ )
               â†’ is-set X
               â†’ ((x : X) â†’ is-prop (A x))
               â†’ (Î  x ê‰ X , Â¬Â¬ A x)
               â†’ Â¬Â¬ (Î  x ê‰ X , A x)

 DNSâ‚€-gives-DNSâ‚‹â‚ : DNSâ‚€ {ğ“¤} {ğ“¥} â†’ DNSâ‚‹â‚ {ğ“¤} {ğ“¥}
 DNSâ‚€-gives-DNSâ‚‹â‚ dns X A i j = dns X A i (Î» x â†’ props-are-sets (j x))

 DNSâ‚‹â‚-gives-Â¬Â¬EM : propext ğ“¤ â†’ DNSâ‚‹â‚ {ğ“¤ âº} {ğ“¤} â†’ Â¬Â¬ EM ğ“¤
 DNSâ‚‹â‚-gives-Â¬Â¬EM {ğ“¤} pe dns' = Â¬Â¬-functor (Î» f P i â†’ f (P , i)) I
  where
   A : Î© ğ“¤ â†’ ğ“¤ Ì‡
   A (P , i) = P + Â¬ P

   j : (p : Î© ğ“¤) â†’ is-prop (A p)
   j (P , i) = decidability-of-prop-is-prop (fe _ _) i

   I : Â¬Â¬ (((P , i) : Î© ğ“¤) â†’ P + Â¬ P)
   I = dns'
        (Î© ğ“¤)
        A
        (Î©-is-set (fe _ _) pe)
        (Î» (P , i) â†’ decidability-of-prop-is-prop (fe _ _) i)
        (Î» _ â†’ fake-Â¬Â¬-EM)

 Â¬Â¬EM-gives-DNSâ‚‹â‚ : Â¬Â¬ EM ğ“¤ â†’ DNSâ‚‹â‚ {ğ“¤} {ğ“¤}
 Â¬Â¬EM-gives-DNSâ‚‹â‚ {ğ“¤} nnem X A X-is-set A-is-prop-valued f = Â¬Â¬-functor g nnem
  where
   g : EM ğ“¤ â†’ (x : X) â†’ A x
   g em x = EM-gives-DNE em (A x) (A-is-prop-valued x) (f x)

\end{code}

In the presence of propositional extensionality, the axiom of choice
is equivalent to the conjunction of the principle of excluded middle
and the double negation shift for set-valued (rather than prop-valued)
predicates, which seems to be a new result:

\begin{code}

 Choice-gives-Double-Negation-Shift : PropExt
                                    â†’ Axiom-of-Choiceâ‚
                                    â†’ Double-Negation-Shiftâ‚€
 Choice-gives-Double-Negation-Shift pe ac {ğ“¤} {ğ“¥} = III
  where
   em : Excluded-Middle
   em = ACâ‚€-gives-EM pe (ACâ‚-gives-ACâ‚€ ac)


   III : DNSâ‚€ {ğ“¤} {ğ“¥}
   III = EM-and-ACâ‚-give-DNSâ‚€ em ac

 Double-Negation-Shift-gives-Choice : Excluded-Middle
                                    â†’ Double-Negation-Shiftâ‚€
                                    â†’ Axiom-of-Choiceâ‚
 Double-Negation-Shift-gives-Choice em dns {ğ“¤} {ğ“¥} =
  EM-and-DNSâ‚€-give-ACâ‚ em (dns {ğ“¤} {ğ“¥})

\end{code}

And here is an equivalent variant of DNSâ‚€:

\begin{code}

 DNA : {ğ“¤ ğ“¥ : Universe} â†’ ğ“¤ âº Ì‡
 DNA {ğ“¤} {ğ“¥} = (X : ğ“¤ Ì‡ ) (A : X â†’ ğ“¤ Ì‡ )
              â†’ is-set X
              â†’ ((x : X) â†’ is-set (A x))
              â†’ Â¬Â¬ (Î  x ê‰ X , (Â¬Â¬ A x â†’ A x))

 open TChoice

 DNSâ‚€-gives-DNA : DNSâ‚€ {ğ“¤} {ğ“¤} â†’ DNA {ğ“¤} {ğ“¥}
 DNSâ‚€-gives-DNA = TAC-gives-TAC' Â¬Â¬_ Â¬Â¬-functor is-set Î´ Î³

 DNA-gives-DNSâ‚€ : DNA {ğ“¤} {ğ“¥} â†’ DNSâ‚€ {ğ“¤} {ğ“¤}
 DNA-gives-DNSâ‚€ = TAC'-gives-TAC Â¬Â¬_ Â¬Â¬-functor is-set Î´ Î³

\end{code}

Added 17th December 2022:

\begin{code}

module choice-functions
        (pt : propositional-truncations-exist)
        (pe : PropExt)
        (fe : FunExt)
       where

 open PropositionalTruncation pt
 open Univalent-Choice fe pt
 open ExcludedMiddle pt fe
 open UF.Powerset.inhabited-subsets pt

 Choice-Function : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
 Choice-Function X = âˆƒ Îµ ê‰ (ğ“Ÿâº X â†’ X) , ((ğ“ : ğ“Ÿâº X) â†’ Îµ ğ“ âˆˆâº ğ“)

 ACâ‚ƒ : {ğ“¤ : Universe} â†’ ğ“¤ âº Ì‡
 ACâ‚ƒ {ğ“¤} = (X : ğ“¤ Ì‡ ) â†’ is-set X â†’ Choice-Function X

 ACâ‚€-gives-ACâ‚ƒ : {ğ“¤ : Universe} â†’ ACâ‚€ {ğ“¤ âº} {ğ“¤} â†’ ACâ‚ƒ {ğ“¤}
 ACâ‚€-gives-ACâ‚ƒ ac X X-is-set =
  ac (ğ“Ÿâº X)
     (Î» (ğ“ : ğ“Ÿâº X) â†’ X)
     (Î» ((A , i) : ğ“Ÿâº X) (x : X) â†’ x âˆˆ A)
     (ğ“Ÿâº-is-set' (fe _ _) (pe _))
     (Î» (_ : ğ“Ÿâº X) â†’ X-is-set)
     (Î» (A , i) x â†’ âˆˆ-is-prop A x)
     (Î» (A , i) â†’ i)

 ACâ‚ƒ-gives-ACâ‚ : {ğ“¤ ğ“¥ : Universe} â†’ ACâ‚ƒ {ğ“¤ âŠ” ğ“¥} â†’ ACâ‚ {ğ“¤} {ğ“¥}
 ACâ‚ƒ-gives-ACâ‚ {ğ“¤} {ğ“¥} acâ‚ƒ X A X-is-set A-is-set-valued = V
  where
   X' : ğ“¤ âŠ” ğ“¥ Ì‡
   X' = Î£ x ê‰ X , A x

   X'-is-set : is-set X'
   X'-is-set = Î£-is-set X-is-set A-is-set-valued

   I : âˆƒ Îµ ê‰ (ğ“Ÿâº X' â†’ X') , ((ğ“ : ğ“Ÿâº X') â†’ Îµ ğ“ âˆˆâº ğ“)
   I = acâ‚ƒ X' X'-is-set

   II : (Î  x ê‰ X , âˆ¥ A x âˆ¥)
      â†’ (Î£ Îµ ê‰ (ğ“Ÿâº X' â†’ X') , ((ğ“ : ğ“Ÿâº X') â†’ Îµ ğ“ âˆˆâº ğ“))
      â†’ (Î  x ê‰ X , A x)
   II g (Îµ , Ï•) x = IV
    where
     C : ğ“Ÿ X'
     C (xâ‚€ , aâ‚€) = ((xâ‚€ ï¼ x) Ã— âˆ¥ A x âˆ¥) , Ã—-is-prop X-is-set âˆ¥âˆ¥-is-prop

     j : is-inhabited C
     j = âˆ¥âˆ¥-functor (Î» a â†’ (x , a) , (refl , âˆ£ a âˆ£)) (g x)

     x' : X'
     x' = Îµ (C , j)

     xâ‚€ : X
     xâ‚€ = prâ‚ x'

     aâ‚€ : A xâ‚€
     aâ‚€ = prâ‚‚ x'

     III : (xâ‚€ ï¼ x) Ã— âˆ¥ A x âˆ¥
     III = Ï• (C , j)

     IV : A x
     IV = transport A (prâ‚ III) aâ‚€

   V : (Î  x ê‰ X , âˆ¥ A x âˆ¥)
     â†’ âˆ¥(Î  x ê‰ X , A x)âˆ¥
   V g = âˆ¥âˆ¥-functor (II g) I

 ACâ‚ƒ-gives-ACâ‚€ : {ğ“¤ ğ“¥ : Universe} â†’ ACâ‚ƒ {ğ“¤ âŠ” ğ“¥} â†’ ACâ‚€ {ğ“¤} {ğ“¥}
 ACâ‚ƒ-gives-ACâ‚€ acâ‚ƒ = ACâ‚-gives-ACâ‚€ (ACâ‚ƒ-gives-ACâ‚ acâ‚ƒ)

 Axiom-of-Choiceâ‚ƒ : ğ“¤Ï‰
 Axiom-of-Choiceâ‚ƒ = {ğ“¤ : Universe} â†’ ACâ‚ƒ {ğ“¤}

 Choice-gives-Choiceâ‚ƒ : Axiom-of-Choice â†’ Axiom-of-Choiceâ‚ƒ
 Choice-gives-Choiceâ‚ƒ c {ğ“¤} = ACâ‚€-gives-ACâ‚ƒ {ğ“¤} (c {ğ“¤ âº} {ğ“¤})

 Choiceâ‚ƒ-gives-Choice : Axiom-of-Choiceâ‚ƒ â†’ Axiom-of-Choice
 Choiceâ‚ƒ-gives-Choice c {ğ“¤} {ğ“¥} = ACâ‚ƒ-gives-ACâ‚€ {ğ“¤} {ğ“¥} (c {ğ“¤ âŠ” ğ“¥})

 Choice-Functionâ» : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
 Choice-Functionâ» X = âˆƒ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A)

 ACâ‚„ : {ğ“¤ : Universe} â†’ ğ“¤ âº Ì‡
 ACâ‚„ {ğ“¤} = (X : ğ“¤ Ì‡ ) â†’ is-set X â†’ âˆ¥ X âˆ¥ â†’ Choice-Functionâ» X

 Axiom-of-Choiceâ‚„ : ğ“¤Ï‰
 Axiom-of-Choiceâ‚„ = {ğ“¤ : Universe} â†’ ACâ‚„ {ğ“¤}

 improve-choice-function : EM ğ“¤
                         â†’ {X : ğ“¤ Ì‡ }
                         â†’ Choice-Function X
                         â†’ âˆ¥ X âˆ¥
                         â†’ Choice-Functionâ» X
 improve-choice-function em {X} c s = III
  where
   I : (Î£ Îµâº ê‰ (ğ“Ÿâº X â†’ X) , (((A , i) : ğ“Ÿâº X) â†’ (Îµâº (A , i) âˆˆ A)))
     â†’ (Î£ Îµâº ê‰ (ğ“Ÿâº X â†’ X) , ((A : ğ“Ÿ X) (i : is-inhabited A) â†’ Îµâº (A , i) âˆˆ A))
   I = NatÎ£ (Î» (Îµâº : ğ“Ÿâº X â†’ X) Îµâº-behaviour A i â†’ Îµâº-behaviour (A , i))

   II : (Î£ Îµâº ê‰ (ğ“Ÿâº X â†’ X) , ((A : ğ“Ÿ X) (i : is-inhabited A) â†’ Îµâº (A , i) âˆˆ A))
      â†’ X
      â†’ (Î£ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A))
   II (Îµâº , f) x = Îµ , Îµ-behaviour
    where
     Îµ' : (A : ğ“Ÿ X) â†’ is-decidable (is-inhabited A) â†’ X
     Îµ' A (inl i) = Îµâº (A , i)
     Îµ' A (inr Î½) = x

     d : (A : ğ“Ÿ X) â†’ is-decidable (is-inhabited A)
     d A = em (is-inhabited A) (being-inhabited-is-prop A)

     Îµ : ğ“Ÿ X â†’ X
     Îµ A = Îµ' A (d A)

     Îµ'-behaviour : (A : ğ“Ÿ X)
                  â†’ is-inhabited A
                  â†’ (Î´ : is-decidable (is-inhabited A))
                  â†’  Îµ' A Î´ âˆˆ A
     Îµ'-behaviour A _ (inl j) = f A j
     Îµ'-behaviour A i (inr Î½) = ğŸ˜-elim (Î½ i)

     Îµ-behaviour : (A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A
     Îµ-behaviour A i = Îµ'-behaviour A i (d A)

   III : Choice-Functionâ» X
   III = âˆ¥âˆ¥-rec âˆƒ-is-prop (Î» x â†’ âˆ¥âˆ¥-rec âˆƒ-is-prop (Î» Ïƒ â†’ âˆ£ II (I Ïƒ) x âˆ£) c) s

 Choice-gives-Choiceâ‚„ : Axiom-of-Choice â†’ Axiom-of-Choiceâ‚„
 Choice-gives-Choiceâ‚„ ac X X-is-set = improve-choice-function
                                       (ACâ‚€-gives-EM pe ac)
                                       (ACâ‚€-gives-ACâ‚ƒ ac X X-is-set)
\end{code}

End of addition.

The following is probably not going to be useful for anything here,
but it is stronger than the above decidability lemma:

\begin{code}

module Observation
        (fe : FunExt)
        where

 decidability-observation
  : {X : ğ“¤ Ì‡ } (a : ğŸš â†’ X)
  â†’ ((x : X) â†’ Â¬Â¬ (Î£ i ê‰ ğŸš , a i ï¼ x) â†’ Î£ i ê‰ ğŸš , a i ï¼ x)
  â†’ is-decidable (a â‚€ ï¼ a â‚)
 decidability-observation {ğ“¤} {X} a c = claim (ğŸš-is-discrete (s(r â‚€)) (s(r â‚)))
  where
   Y = Î£ x ê‰ X , Â¬Â¬ (Î£ i ê‰ ğŸš , a i ï¼ x)

   r : ğŸš â†’ Y
   r i = a i , Î» u â†’ u (i , refl)

   r-splits : (y : Y) â†’ Î£ i ê‰ ğŸš , r i ï¼ y
   r-splits (x , t) = f (c x t)
    where
     f : (Î£ i ê‰ ğŸš , a i ï¼ x) â†’ Î£ i ê‰ ğŸš , r i ï¼ (x , t)
     f (i , p) = i , to-Î£-ï¼ (p , negations-are-props (fe ğ“¤ ğ“¤â‚€) _ t)

   s : Y â†’ ğŸš
   s y = prâ‚(r-splits y)

   rs : (y : Y) â†’ r(s y) ï¼ y
   rs y = prâ‚‚(r-splits y)

   s-lc : left-cancellable s
   s-lc = section-lc s (r , rs)

   a-r : {i j : ğŸš} â†’ a i ï¼ a j â†’ r i ï¼ r j
   a-r p = to-Î£-ï¼ (p , negations-are-props (fe ğ“¤ ğ“¤â‚€) _ _)

   r-a : {i j : ğŸš} â†’ r i ï¼ r j â†’ a i ï¼ a j
   r-a = ap prâ‚

   a-s : {i j : ğŸš} â†’ a i ï¼ a j â†’ s(r i) ï¼ s(r j)
   a-s p = ap s (a-r p)

   s-a : {i j : ğŸš} â†’ s(r i) ï¼ s(r j) â†’ a i ï¼ a j
   s-a p = r-a (s-lc p)

   claim : is-decidable (s(r â‚€) ï¼ s(r â‚)) â†’ is-decidable (a â‚€ ï¼ a â‚)
   claim (inl p) = inl (s-a p)
   claim (inr u) = inr (Î» p â†’ u (a-s p))

\end{code}

Added Friday 8th September 2023.

The axiom of propositional choice from
https://doi.org/10.23638/LMCS-13(1:15)2017

\begin{code}

module Propositional-Choice
        (pt : propositional-truncations-exist)
        where

 open PropositionalTruncation pt

 PAC : {ğ“¤ ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 PAC {ğ“¤} {ğ“¥} = (P : ğ“¤ Ì‡ ) (Y : P â†’ ğ“¥ Ì‡ )
              â†’ is-prop P
              â†’ (Î  p ê‰ P , âˆ¥ Y p âˆ¥)
              â†’ âˆ¥(Î  p ê‰ P , Y p)âˆ¥

\end{code}

Notice that we don't require that this is a family of sets. Notice
also that excluded middle implies PAC. For more information, see
Theorem 7.7 of the above reference.

TODO. Add these and more facts about this. Some of them can be adapted
from this Agda file: https://www.cs.bham.ac.uk/~mhe/GeneralizedHedberg/html/GeneralizedHedberg.html

Added 6th Feb 2025 by Martin Escardo.

\begin{code}

module local-choice
        (pt : propositional-truncations-exist)
        where

 open PropositionalTruncation pt

 AC : (ğ“¦ : Universe) â†’ ğ“¤ Ì‡  â†’ ğ“¥ Ì‡  â†’ ğ“¤ âŠ” ğ“¥ âŠ” (ğ“¦ âº) Ì‡
 AC {ğ“¤} {ğ“¥} ğ“¦ X Y = (P : X â†’ Y â†’ ğ“¦ Ì‡ )
                    â†’ ((x : X) (y : Y) â†’ is-prop (P x y))
                    â†’ ((x : X) â†’ âˆƒ y ê‰ Y , P x y)
                    â†’ âˆƒ f ê‰ (X â†’ Y) , ((x : X) â†’ P x (f x))

\end{code}

Added 20 October 2025 by Tom de Jong.

If we restrict the family Y in the axiom of propositional choice (PAC) to be a
family of doubletons, then we get the "world's simplest axiom choice" (WSAC), as
introduced, and shown to fail in some toposes, by Fourman and Å Äedrov in [1].

[1] M. P. Fourman and A. Å Äedrov
    The "world's simplest axiom of choice" fails
    manuscripta mathematica, volume 38, pp. 325â€”332, 1982
    https://doi.org/10.1007/BF01170929

We consider two formulations of WSAC and prove them to be equivalent.

\begin{code}

module world's-simplest-axiom-of-choice
        (fe : FunExt)
        (pt : propositional-truncations-exist)
        where

 open import Fin.ArithmeticViaEquivalence
 open import Fin.Bishop
 open import Fin.Kuratowski pt
 open import Fin.Type
 open import UF.Equiv
 open import UF.Equiv-FunExt
 open import UF.ExitPropTrunc
 open import UF.PropIndexedPiSigma

 open PropositionalTruncation pt
 open exponentiation-and-factorial fe
 open finiteness pt
 open split-support-and-collapsibility pt

 WSAC : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥) âº Ì‡
 WSAC ğ“¤ ğ“¥ = (P : ğ“¤ Ì‡ ) (Y : P â†’ ğ“¥ Ì‡ )
            â†’ is-prop P
            â†’ ((p : P) â†’ Y p has-cardinality 2)
            â†’ âˆ¥ Î  Y âˆ¥

 world's-simplest-axiom-of-choice = WSAC

\end{code}

The following formulation is exploited in InjectiveTypes.CounterExamples.

\begin{code}

 WSAC' : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
 WSAC' ğ“¤ = (X : ğ“¤ Ì‡ ) â†’ âˆ¥ has-split-support (X â‰ƒ ğŸš) âˆ¥

 WSAC-implies-WSAC' : WSAC ğ“¤ ğ“¤ â†’ WSAC' ğ“¤
 WSAC-implies-WSAC' {ğ“¤} wsac X = wsac P Y P-is-prop Y-doubletons
  where
   P : ğ“¤ Ì‡
   P = âˆ¥ X â‰ƒ ğŸš âˆ¥
   Y : P â†’ ğ“¤ Ì‡
   Y _ = X â‰ƒ ğŸš
   P-is-prop : is-prop P
   P-is-prop = âˆ¥âˆ¥-is-prop
   Y-doubletons : (p : P) â†’ Y p has-cardinality 2
   Y-doubletons p = âˆ¥âˆ¥-functor I p
    where
     I : X â‰ƒ ğŸš â†’ Y p â‰ƒ Fin 2
     I e =
      Y p             â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
      (X â‰ƒ ğŸš)         â‰ƒâŸ¨ â‰ƒ-cong-left fe e âŸ©
      (ğŸš â‰ƒ ğŸš)         â‰ƒâŸ¨ â‰ƒ-cong fe (ğŸš-is-Fin2) ğŸš-is-Fin2 âŸ©
      (Fin 2 â‰ƒ Fin 2) â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
      Aut (Fin 2)     â‰ƒâŸ¨ â‰ƒ-sym (prâ‚‚ (!construction 2)) âŸ©
      Fin 2           â– 

 WSAC'-implies-WSAC : WSAC' ğ“¤ â†’ WSAC ğ“¤ ğ“¤
 WSAC'-implies-WSAC {ğ“¤} wsac' P Y P-is-prop Y-doubletons =
  âˆ¥âˆ¥-functor I (wsac' (Î  Y))
    where
     I : has-split-support (Î  Y â‰ƒ ğŸš) â†’ Î  Y
     I h p = II (h' III)
      where
       e : Î  Y â‰ƒ Y p
       e = prop-indexed-product p (fe ğ“¤ ğ“¤) P-is-prop
       h' : has-split-support (Y p â‰ƒ ğŸš)
       h' t = Iâ‚‚
        where
         ğ•— : (Î  Y â‰ƒ ğŸš) â‰ƒ (Y p â‰ƒ ğŸš)
         ğ•— = â‰ƒ-cong-left fe e
         Iâ‚ : Î  Y â‰ƒ ğŸš
         Iâ‚ = h (âˆ¥âˆ¥-functor âŒœ ğ•— âŒâ»Â¹ t)
         Iâ‚‚ : Y p â‰ƒ ğŸš
         Iâ‚‚ = âŒœ ğ•— âŒ Iâ‚
       II : Y p â‰ƒ ğŸš â†’ Y p
       II f = âŒœ f âŒâ»Â¹ â‚€
       III : âˆ¥ Y p â‰ƒ ğŸš âˆ¥
       III = âˆ¥âˆ¥-functor III' (Y-doubletons p)
        where
         III' : Y p â‰ƒ Fin 2 â†’ Y p â‰ƒ ğŸš
         III' = âŒœ â‰ƒ-cong-right fe (â‰ƒ-sym ğŸš-is-Fin2) âŒ

 WSAC-equivalent-formulations : WSAC ğ“¤ ğ“¤ â‰ƒ WSAC' ğ“¤
 WSAC-equivalent-formulations =
  logically-equivalent-props-are-equivalent
   (Î â‚„-is-prop (fe _ _) (Î» _ _ _ _ â†’ âˆ¥âˆ¥-is-prop))
   (Î -is-prop (fe _ _) (Î» _ â†’ âˆ¥âˆ¥-is-prop))
   WSAC-implies-WSAC'
   WSAC'-implies-WSAC

\end{code}
