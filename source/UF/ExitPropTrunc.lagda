Martin Escardo 2012.

Based on [1] and [2].

1. Nicolai Kraus, MartÃ­n EscardÃ³, Thierry Coquand & Thorsten Altenkirch.
   Generalizations of Hedbergâ€™s Theorem.
   TLCA 2013
   https://doi.org/10.1007/978-3-642-38946-7_14

2. Nicolai Kraus, MartÃ­n EscardÃ³, Thierry Coquand & Thorsten Altenkirch.
   Notions of Anonymous Existence in Martin-LÃ¶f Type Theory.
   Logical Methods in Computer Science, March 24, 2017, Volume 13, Issue 1.
   https://doi.org/10.23638/LMCS-13(1:15)2017

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.ExitPropTrunc where

open import MLTT.Spartan
open import UF.Hedberg
open import UF.KrausLemma
open import UF.PropTrunc
open import UF.Subsingletons

to-fix : {X : ğ“¤ Ì‡ } (f : X â†’ X) â†’ wconstant f â†’ X â†’ fix f
to-fix f g x = (f x , g x (f x))

from-to-fix : {X : ğ“¤ Ì‡ } (f : X â†’ X) (Îº : wconstant f)
            â†’ from-fix f âˆ˜ to-fix f Îº âˆ¼ f
from-to-fix f Îº w = refl

to-from-fix : {X : ğ“¤ Ì‡ } (f : X â†’ X) (Îº : wconstant f)
            â†’ to-fix f Îº âˆ˜ from-fix f âˆ¼ id
to-from-fix f Îº _ = fix-is-prop f Îº _ _

has-split-support' : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
has-split-support' {ğ“¤} X = Î£ P ê‰ ğ“¤ Ì‡ , is-prop P Ã— (X â†” P)

fix-has-split-support' : {X : ğ“¤ Ì‡ }
                       â†’ collapsible X
                       â†’ has-split-support' X
fix-has-split-support' {ğ“¤} {X} (f , Îº) =
 fix f , fix-is-prop f Îº , to-fix f Îº , from-fix f

has-prop-truncation : (ğ“¥ : Universe) â†’ ğ“¤ Ì‡ â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
has-prop-truncation {ğ“¤} ğ“¥ X =
 Î£ X' ê‰ ğ“¤ Ì‡ , is-prop X'
           Ã— (X â†’ X')
           Ã— ((P : ğ“¥ Ì‡ ) â†’ is-prop P â†’ (X â†’ P) â†’ X' â†’ P)

split-truncation : {X : ğ“¤ Ì‡ }
                 â†’ has-split-support' X
                 â†’ âˆ€ ğ“¥ â†’ has-prop-truncation ğ“¥ X
split-truncation {ğ“¤} {X} (X' , i , f , g) V = X' , i , f , Î» P j h x' â†’ h (g x')

collapsible-has-prop-truncation : {X : ğ“¤ Ì‡ }
                                â†’ collapsible X
                                â†’ âˆ€ ğ“¥ â†’ has-prop-truncation ğ“¥ X
collapsible-has-prop-truncation {ğ“¤} {X} c =
 split-truncation (fix-has-split-support' c)

module split-support-and-collapsibility (pe : propositional-truncations-exist) where

 open PropositionalTruncation pe

 has-split-support : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
 has-split-support X = âˆ¥ X âˆ¥ â†’ X

 has-split-supportâ†’ : {X : ğ“¤ Ì‡ } â†’ has-split-support X â†’ has-split-support' X
 has-split-supportâ†’ {ğ“¤} {X} f = âˆ¥ X âˆ¥ , âˆ¥âˆ¥-is-prop , (Î» x â†’ âˆ£ x âˆ£) , f

 has-split-supportâ† : {X : ğ“¤ Ì‡ } â†’ has-split-support' X â†’ has-split-support X
 has-split-supportâ† {ğ“¤} {X} (P , P-is-prop , g , f) = f âˆ˜ âˆ¥âˆ¥-rec P-is-prop g

\end{code}

TODO. Are the above two functions mutually inverse and hence we get an
equivalence?

\begin{code}

 collapsible-gives-split-support : {X : ğ“¤ Ì‡ }
                                 â†’ collapsible X
                                 â†’ has-split-support X
 collapsible-gives-split-support {ğ“¤} {X} (f , Îº) s = x
  where
   g : âˆ¥ X âˆ¥ â†’ fix f
   g = âˆ¥âˆ¥-rec (fix-is-prop f Îº) (to-fix f Îº)

   x : X
   x = from-fix f (g s)

 exit-prop-trunc : {X : ğ“¤ Ì‡ }
                 â†’ (f : X â†’ X)
                 â†’ wconstant f
                 â†’ âˆ¥ X âˆ¥ â†’ X
 exit-prop-trunc f Îº = collapsible-gives-split-support (f , Îº)

 exit-prop-trunc-is-fixed : {X : ğ“¤ Ì‡ }
                            (f : X â†’ X)
                            (Îº : wconstant f)
                            (s : âˆ¥ X âˆ¥)
                          â†’ f (exit-prop-trunc f Îº s) ï¼ exit-prop-trunc f Îº s
 exit-prop-trunc-is-fixed f Îº s =
  (from-fix-is-fixed f (âˆ¥âˆ¥-rec (fix-is-prop f Îº) (to-fix f Îº) s))â»Â¹

 split-support-gives-collapsible : {X : ğ“¤ Ì‡ }
                                 â†’ has-split-support X
                                 â†’ collapsible X
 split-support-gives-collapsible {ğ“¤} {X} g = Î³
  where
   f : X â†’ X
   f x = g âˆ£ x âˆ£

   Îº : (x y : X) â†’ f x ï¼ f y
   Îº x y = ap g (âˆ¥âˆ¥-is-prop âˆ£ x âˆ£ âˆ£ y âˆ£)

   Î³ : collapsible X
   Î³ = f , Îº

\end{code}

Added 23rd September 2024. Perhaps the following is better notation
for the above.

\begin{code}

âˆ¥_âˆ¥âŒœ_âŒ : (X : ğ“¤ Ì‡ ) â†’ collapsible X â†’ ğ“¤ Ì‡
âˆ¥ X âˆ¥âŒœ f , w âŒ = fix f

âˆ¥âˆ¥âŒœ_âŒ-is-prop : {X : ğ“¤ Ì‡ } (c : collapsible X) â†’ is-prop âˆ¥ X âˆ¥âŒœ c âŒ
âˆ¥âˆ¥âŒœ f , w âŒ-is-prop = fix-is-prop f w

âˆ£_âˆ£âŒœ_âŒ : {X : ğ“¤ Ì‡ } â†’ X â†’ (c : collapsible X) â†’ âˆ¥ X âˆ¥âŒœ c âŒ
âˆ£ x âˆ£âŒœ f , w âŒ = to-fix f w x

\end{code}

Notice that recursion principle doesn't require the family A to be
prop-valued, which allows us to exit truncations.

\begin{code}

âˆ¥âˆ¥âŒœ_âŒ-rec : {X : ğ“¤ Ì‡ } (c : collapsible X) {A : ğ“¥ Ì‡ }
         â†’ (X â†’ A) â†’ âˆ¥ X âˆ¥âŒœ c âŒ â†’ A
âˆ¥âˆ¥âŒœ c âŒ-rec {A} g (x , Ï†) = g x

âˆ£âˆ£âŒœ_âŒ-exit : {X : ğ“¤ Ì‡ } (c : collapsible X) â†’ âˆ¥ X âˆ¥âŒœ c âŒ â†’ X
âˆ£âˆ£âŒœ c âŒ-exit = âˆ¥âˆ¥âŒœ c âŒ-rec id

infix 0 âˆ¥_âˆ¥âŒœ_âŒ
infix 0 âˆ£_âˆ£âŒœ_âŒ

module propositional-truncation-of-decidable-type
        (pt : propositional-truncations-exist)
       where

 open propositional-truncations-exist pt public

 module _ {X : ğ“¤ Ì‡ } (c : collapsible X) where

  âˆ¥âˆ¥âŒœ_âŒ-to-âˆ¥âˆ¥ : âˆ¥ X âˆ¥âŒœ c âŒ â†’ âˆ¥ X âˆ¥
  âˆ¥âˆ¥âŒœ_âŒ-to-âˆ¥âˆ¥ = âˆ¥âˆ¥âŒœ c âŒ-rec âˆ£_âˆ£

  âˆ¥âˆ¥-to-âˆ¥âˆ¥âŒœ_âŒ : âˆ¥ X âˆ¥ â†’ âˆ¥ X âˆ¥âŒœ c âŒ
  âˆ¥âˆ¥-to-âˆ¥âˆ¥âŒœ_âŒ = âˆ¥âˆ¥-rec (âˆ¥âˆ¥âŒœ c âŒ-is-prop) âˆ£_âˆ£âŒœ c âŒ

  collapsible-types-have-split-support : âˆ¥ X âˆ¥ â†’ X
  collapsible-types-have-split-support s = âˆ£âˆ£âŒœ c âŒ-exit (âˆ¥âˆ¥-to-âˆ¥âˆ¥âŒœ_âŒ s)

\end{code}

TODO. Perhaps rewrite all uses of this file to use the new notation,
and get rid of the original old notation.
