\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.Retracts where

open import MLTT.AlternativePlus
open import MLTT.Spartan
open import UF.Base
open import UF.Subsingletons

has-section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
has-section r = Î£ s ê‰ (codomain r â†’ domain r), r âˆ˜ s âˆ¼ id

section-of : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (r : X â†’ Y)
           â†’ has-section r
           â†’ (Y â†’ X)
section-of r (s , rs) = s

section-equation : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (r : X â†’ Y)
                 â†’ (h : has-section r)
                 â†’ r âˆ˜ section-of r h âˆ¼ id
section-equation r (s , rs) = rs

is-section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-section s = Î£ r ê‰ (codomain s â†’ domain s), r âˆ˜ s âˆ¼ id

sections-are-lc : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (s : X â†’ Y)
                â†’ is-section s
                â†’ left-cancellable s
sections-are-lc s (r , rs) {x} {x'} p = (rs x)â»Â¹ âˆ™ ap r p âˆ™ rs x'

retract_of_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
retract Y of X = Î£ r ê‰ (X â†’ Y) , has-section r

retraction : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ retract X of Y â†’ (Y â†’ X)
retraction (r , s , rs) = r

section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ retract X of Y â†’ (X â†’ Y)
section (r , s , rs) = s

section-is-section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                   â†’ (Ï : retract X of Y)
                   â†’ is-section (section Ï)
section-is-section (r , s , rs) = r , rs

retract-condition : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (Ï : retract X of Y)
                  â†’ retraction Ï âˆ˜ section Ï âˆ¼ id
retract-condition (r , s , rs) = rs

retraction-has-section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (Ï : retract X of Y)
                       â†’ has-section (retraction Ï)
retraction-has-section (r , h) = h

retract-of-singleton : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                     â†’ retract Y of X
                     â†’ is-singleton X
                     â†’ is-singleton Y
retract-of-singleton (r , s , rs) (c , Ï†) = r c , (Î» y â†’ ap r (Ï† (s y)) âˆ™ rs y)

retract-of-prop : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                â†’ retract Y of X
                â†’ is-prop X
                â†’ is-prop Y
retract-of-prop (r , s , rs) = subtypes-of-props-are-props' s
                                (sections-are-lc s (r , rs))

identity-retraction : {X : ğ“¤ Ì‡ } â†’ retract X of X
identity-retraction = id , id , Î» x â†’ refl

has-section-closed-under-âˆ¼ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f g : X â†’ Y)
                           â†’ has-section f
                           â†’ g âˆ¼ f
                           â†’ has-section g
has-section-closed-under-âˆ¼ {ğ“¤} {ğ“¥} {X} {Y} f g (s , fs) h =
 (s , Î» y â†’ g (s y) ï¼âŸ¨ h (s y) âŸ© f (s y) ï¼âŸ¨ fs y âŸ©
  y                 âˆ)

has-section-closed-under-âˆ¼' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f g : X â†’ Y}
                            â†’ has-section f
                            â†’ f âˆ¼ g
                            â†’ has-section g
has-section-closed-under-âˆ¼' ise h =
 has-section-closed-under-âˆ¼ _ _ ise (Î» x â†’ (h x)â»Â¹)

is-section-closed-under-âˆ¼ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f g : X â†’ Y)
                          â†’ is-section f
                          â†’  g âˆ¼ f
                          â†’ is-section g
is-section-closed-under-âˆ¼ {ğ“¤} {ğ“¥} {X} {Y} f g (r , rf) h =
  (r , Î» x â†’ r (g x) ï¼âŸ¨ ap r (h x) âŸ©
             r (f x) ï¼âŸ¨ rf x âŸ©
             x       âˆ)

is-section-closed-under-âˆ¼' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f g : X â†’ Y}
                           â†’ is-section f
                           â†’ f âˆ¼ g
                           â†’ is-section g
is-section-closed-under-âˆ¼' ise h =
 is-section-closed-under-âˆ¼ _ _ ise (Î» x â†’ (h x)â»Â¹)

\end{code}

Surjection expressed in Curry-Howard logic amounts to retraction.

\begin{code}

has-section' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
has-section' f = (y : codomain f) â†’ Î£ x ê‰ domain f , f x ï¼ y

retract_Of_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
retract Y Of X = Î£ f ê‰ (X â†’ Y) , has-section' f

retract-of-gives-retract-Of : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                            â†’ retract Y of X
                            â†’ retract Y Of X
retract-of-gives-retract-Of {ğ“¤} {ğ“¥} {X} {Y} Ï = (retraction Ï , h)
 where
  h : (y : Y) â†’ Î£ x ê‰ X , retraction Ï x ï¼ y
  h y = section Ï y , retract-condition Ï y

retract-Of-gives-retract-of : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                            â†’ retract Y Of X
                            â†’ retract Y of X
retract-Of-gives-retract-of {ğ“¤} {ğ“¥} {X} {Y} (f , hass) = (f , Ï†)
 where
  Ï† : Î£ s ê‰ (Y â†’ X) , f âˆ˜ s âˆ¼ id
  Ï† = (Î» y â†’ prâ‚ (hass y)) , (Î» y â†’ prâ‚‚ (hass y))

retracts-compose : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ }
                 â†’ retract Y of X
                 â†’ retract Z of Y
                 â†’ retract Z of X
retracts-compose (r , s , rs) (r' , s' , rs') =
  r' âˆ˜ r , s âˆ˜ s' , Î» z â†’ r' (r (s (s' z))) ï¼âŸ¨ ap r' (rs (s' z)) âŸ©
                          r' (s' z)         ï¼âŸ¨ rs' z âŸ©
                          z                 âˆ

Ã—-retract : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {A : ğ“¦ Ì‡ } {B : ğ“£ Ì‡ }
          â†’ retract X of A
          â†’ retract Y of B
          â†’ retract (X Ã— Y) of (A Ã— B)
Ã—-retract {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A Ã— B â†’ X Ã— Y
  f (a , b) = (r a , t b)

  g : X Ã— Y â†’ A Ã— B
  g (x , y) = s x , u y

  fg : (z : X Ã— Y) â†’ f (g z) ï¼ z
  fg (x , y) = to-Ã—-ï¼ (rs x) (tu y)

+-retract : {X : ğ“¤ Ì‡ } {Y : ğ“¦ Ì‡ } {A : ğ“¥ Ì‡ } {B : ğ“£ Ì‡ }
          â†’ retract X of A
          â†’ retract Y of B
          â†’ retract (X + Y) of (A + B)
+-retract {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A + B â†’ X + Y
  f (inl a) = inl (r a)
  f (inr b) = inr (t b)

  g : X + Y â†’ A + B
  g (inl x) = inl (s x)
  g (inr y) = inr (u y)

  fg : (p : X + Y) â†’ f (g p) ï¼ p
  fg (inl x) = ap inl (rs x)
  fg (inr y) = ap inr (tu y)

+'-retract-of-+ : {X Y : ğ“¤ Ì‡ }
                â†’ retract (X +' Y) of (X + Y)
+'-retract-of-+ {ğ“¤} {X} {Y} = f , g , fg
 where
  f : X + Y â†’ X +' Y
  f (inl x) = â‚€ , x
  f (inr y) = â‚ , y

  g : X +' Y â†’ X + Y
  g (â‚€ , x) = inl x
  g (â‚ , y) = inr y

  fg : (z : X +' Y) â†’ f (g z) ï¼ z
  fg (â‚€ , x) = refl
  fg (â‚ , y) = refl

+-retract-of-+' : {X Y : ğ“¤ Ì‡ }
                â†’ retract (X + Y) of (X +' Y)
+-retract-of-+' {ğ“¤} {X} {Y} = g , f , gf
 where
  f : X + Y â†’ X +' Y
  f (inl x) = â‚€ , x
  f (inr y) = â‚ , y

  g : X +' Y â†’ X + Y
  g (â‚€ , x) = inl x
  g (â‚ , y) = inr y

  gf : (z : X + Y) â†’ g (f z) ï¼ z
  gf (inl x) = refl
  gf (inr y) = refl

+'-retract : {X Y : ğ“¤ Ì‡ } {A B : ğ“¥ Ì‡ }
           â†’ retract X of A
           â†’ retract Y of B
           â†’ retract (X +' Y) of (A +' B)
+'-retract {ğ“¤} {ğ“¥} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A +' B â†’ X +' Y
  f (â‚€ , a) = â‚€ , r a
  f (â‚ , b) = â‚ , t b

  g : X +' Y â†’ A +' B
  g (â‚€ , x) = â‚€ , s x
  g (â‚ , y) = â‚ , u y

  fg : (p : X +' Y) â†’ f (g p) ï¼ p
  fg (â‚€ , x) = ap (Î» - â†’ (â‚€ , -)) (rs x)
  fg (â‚ , y) = ap (Î» - â†’ (â‚ , -)) (tu y)

Î£-reindex-retract : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {A : X â†’ ğ“¦ Ì‡ } (r : Y â†’ X)
                  â†’ has-section r
                  â†’ retract (Î£ A) of (Î£ (A âˆ˜ r))
Î£-reindex-retract {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y} {A} r (s , rs) = Î³ , Ï† , Î³Ï†
 where
  Î³ : (Î£ y ê‰ Y , A (r y)) â†’ Î£ A
  Î³ (y , a) = (r y , a)

  Ï† : Î£ A â†’ Î£ y ê‰ Y , A (r y)
  Ï† (x , a) = (s x , transportâ»Â¹ A (rs x) a)

  Î³Ï† : (Ïƒ : Î£ A) â†’ Î³ (Ï† Ïƒ) ï¼ Ïƒ
  Î³Ï† (x , a) = to-Î£-ï¼ (rs x , p)
   where
    p : transport A (rs x) (transportâ»Â¹ A (rs x) a) ï¼ a
    p = back-and-forth-transport (rs x)

Î£-reindex-retract' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {A : X â†’ ğ“¦ Ì‡ }
                   â†’ (Ï : retract X of Y)
                   â†’ retract (Î£ x ê‰ X , A x) of (Î£ y ê‰ Y , A (retraction Ï y))
Î£-reindex-retract' (r , s , rs) = Î£-reindex-retract r (s , rs)

Î£-retract : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) (B : X â†’ ğ“¦ Ì‡ )
          â†’ ((x : X) â†’ retract (A x) of (B x))
          â†’ retract (Î£ A) of (Î£ B)
Î£-retract {ğ“¤} {ğ“¥} {ğ“¦} {X} A B Ï = NatÎ£ R , NatÎ£ S , rs
 where
  R : (x : X) â†’ B x â†’ A x
  R x = retraction (Ï x)

  S : (x : X) â†’ A x â†’ B x
  S x = section (Ï x)

  RS : (x : X) (a : A x) â†’ R x (S x a) ï¼ a
  RS x = retract-condition (Ï x)

  rs : (Ïƒ : Î£ A) â†’ NatÎ£ R (NatÎ£ S Ïƒ) ï¼ Ïƒ
  rs (x , a) = to-Î£-ï¼' (RS x a)

retract-ğŸ™+ğŸ™-of-ğŸš : retract ğŸ™ + ğŸ™ of ğŸš
retract-ğŸ™+ğŸ™-of-ğŸš = f , (g , fg)
 where
  f : ğŸš â†’ ğŸ™ {ğ“¤â‚€} + ğŸ™ {ğ“¤â‚€}
  f = ğŸš-cases (inl â‹†) (inr â‹†)

  g : ğŸ™ + ğŸ™ â†’ ğŸš
  g = cases (Î» x â†’ â‚€) (Î» x â†’ â‚)

  fg : (x : ğŸ™ + ğŸ™) â†’ f (g x) ï¼ x
  fg (inl â‹†) = refl
  fg (inr â‹†) = refl

\end{code}

TODO. Several retractions here are actually equivalences. So some code
should be generalized and moved to an equivalences module. Similarly,
some retracts proved here are also shown as equivalences in other
modules, and hence there is some amount of repetition that should be
removed. This is the result of (1) merging initially independent
developments, and (2) work over many years with uncontrolled growth.

\begin{code}

Î£-retractâ‚‚ : {X : ğ“¤ Ì‡ } {Y : X â†’ ğ“¥ Ì‡ } {A : ğ“¦ Ì‡ } {B : ğ“£ Ì‡ }
           â†’ retract X of A
           â†’ ((x : X) â†’ retract  (Y x) of B)
           â†’ retract (Î£ Y) of (A Ã— B)
Î£-retractâ‚‚ {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) R = f , g , gf
 where
  Ï† : (x : X) â†’ B â†’ Y x
  Ï† x = retraction (R x)

  Î³ : (x : X) â†’ Y x â†’ B
  Î³ x = section (R x)

  Ï†Î³ : (x : X) â†’ (y : Y x) â†’ Ï† x (Î³ x y) ï¼ y
  Ï†Î³ x = retract-condition (R x)

  f : A Ã— B â†’ Î£ Y
  f (a , b) = r a , Ï† (r a) b

  g : Î£ Y â†’ A Ã— B
  g (x , y) = s x , Î³ x y

  gf : (z : Î£ Y) â†’ f (g z) ï¼ z
  gf (x , y) = to-Î£-ï¼ (rs x , l (rs x))
   where
    l : {x' : X} (p : x' ï¼ x) â†’ transport Y p (Ï† x' (Î³ x y)) ï¼ y
    l refl = Ï†Î³ x y

retract-ğŸ™+ğŸ™-of-â„• : retract ğŸ™ + ğŸ™ of â„•
retract-ğŸ™+ğŸ™-of-â„• = r , s , rs
 where
  r : â„• â†’ ğŸ™ + ğŸ™
  r zero = inl â‹†
  r (succ _) = inr â‹†

  s : ğŸ™ + ğŸ™ â†’ â„•
  s (inl â‹†) = zero
  s (inr â‹†) = succ zero

  rs : (z : ğŸ™ {ğ“¤â‚€} + ğŸ™ {ğ“¤â‚€}) â†’ r (s z) ï¼ z
  rs (inl â‹†) = refl
  rs (inr â‹†) = refl

\end{code}

Added 5th March 2019. Notation for composing retracts. I should have
added this ages ago to make the above proofs more readable.

\begin{code}

_â—_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
Y â— X = retract Y of X

_â—âŸ¨_âŸ©_ : (X : ğ“¤ Ì‡ ) {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } â†’ X â— Y â†’ Y â— Z â†’ X â— Z
_ â—âŸ¨ d âŸ© e = retracts-compose e d

â—-refl : (X : ğ“¤ Ì‡ ) â†’ X â— X
â—-refl {ğ“¤} X = identity-retraction {ğ“¤} {X}


_â—€ : (X : ğ“¤ Ì‡ ) â†’ X â— X
_â—€ = â—-refl

\end{code}

Added 20 February 2020 by Tom de Jong.

\begin{code}

ap-of-section-is-section : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (s : X â†’ Y)
                         â†’ is-section s
                         â†’ (x x' : X) â†’ is-section (ap s {x} {x'})
ap-of-section-is-section {ğ“¤} {ğ“¥} {X} {Y} s (r , rs) x x' = Ï , Ïap
 where
  Ï : s x ï¼ s x' â†’ x ï¼ x'
  Ï q = x        ï¼âŸ¨ (rs x) â»Â¹ âŸ©
        r (s x)  ï¼âŸ¨ ap r q âŸ©
        r (s x') ï¼âŸ¨ rs x' âŸ©
        x'       âˆ

  Ïap : (p : x ï¼ x') â†’ Ï (ap s p) ï¼ p
  Ïap p = Ï (ap s p)                          ï¼âŸ¨ by-definition âŸ©
          (rs x) â»Â¹ âˆ™ (ap r (ap s p) âˆ™ rs x') ï¼âŸ¨ i âŸ©
          (rs x) â»Â¹ âˆ™ ap r (ap s p) âˆ™ rs x'   ï¼âŸ¨ ii âŸ©
          (rs x) â»Â¹ âˆ™ ap (r âˆ˜ s) p âˆ™  rs x'   ï¼âŸ¨ iii âŸ©
          ap id p                             ï¼âŸ¨ (ap-id-is-id' p)â»Â¹ âŸ©
          p                                   âˆ
   where
    i   = âˆ™assoc ((rs x) â»Â¹) (ap r (ap s p)) (rs x') â»Â¹
    ii  = ap (Î» - â†’ (rs x) â»Â¹ âˆ™ - âˆ™ rs x') (ap-ap s r p)
    iii = homotopies-are-natural'' (r âˆ˜ s) id rs {x} {x'} {p}

Î£-section-retract : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } (Ï : Y â— Z) (g : X â†’ Y)
                  â†’ (y : Y)
                  â†’ fiber g y
                  â— fiber (section Ï âˆ˜ g) (section Ï y)
Î£-section-retract {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y} {Z} (r , s , rs) g y =
 Î£-retract (Î» x â†’ g x ï¼ y) (Î» x â†’ s (g x) ï¼ s y) Î³
  where
   Î³ : (x : X) â†’ (g x ï¼ y) â— (s (g x) ï¼ s y)
   Î³ x = Ï , (Ïƒ , ÏÏƒ)
    where
     Ïƒ : g x ï¼ y â†’ s (g x) ï¼ s y
     Ïƒ = ap s

     Ï : s (g x) ï¼ s y â†’ g x ï¼ y
     Ï = prâ‚ (ap-of-section-is-section s (r , rs) (g x) y)

     ÏÏƒ : (p : g x ï¼ y) â†’ Ï (Ïƒ p) ï¼ p
     ÏÏƒ = prâ‚‚ (ap-of-section-is-section s ((r , rs)) (g x) y)

\end{code}

Fixities:

\begin{code}

infix  0 _â—_
infix  1 _â—€
infixr 0 _â—âŸ¨_âŸ©_

\end{code}
