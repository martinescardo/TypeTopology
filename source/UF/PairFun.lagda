Martin Escardo 2011, extended 2018 with more properties of the function pair-fun.

Combining two functions to get a function Î£ A â†’ Î£ B, and properties of
the resulting function.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.PairFun where

open import MLTT.Spartan
open import TypeTopology.Density

open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Subsingletons

module _ {ğ“¤ ğ“¥ ğ“¦ ğ“£}
         {X : ğ“¤ Ì‡ }
         {A : X â†’ ğ“¥ Ì‡ }
         {Y : ğ“¦ Ì‡ }
         {B : Y â†’ ğ“£ Ì‡ }
         (f : X â†’ Y)
         (g : (x : X) â†’ A x â†’ B (f x))
       where

 pair-fun : Î£ A â†’ Î£ B
 pair-fun (x , a) = (f x , g x a)

 pair-fun-fiber' : (y : Y) â†’ B y â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“£ Ì‡
 pair-fun-fiber' y b = Î£ (x , a) ê‰ fiber f y , fiber (g x) (transportâ»Â¹ B a b)

 pair-fun-fiber-â‰ƒ : (y : Y) (b : B y)
                  â†’ fiber pair-fun (y , b)
                  â‰ƒ pair-fun-fiber' y b
 pair-fun-fiber-â‰ƒ  y b = qinveq Ï† (Î³ , Î³Ï† , Ï†Î³)
  where
   Ï† : fiber pair-fun (y , b) â†’ pair-fun-fiber' y b
   Ï† ((x , a) , refl) = (x , refl) , (a , refl)

   Î³ : pair-fun-fiber' y b â†’ fiber pair-fun (y , b)
   Î³ ((x , refl) , (a , refl)) = (x , a) , refl

   Î³Ï† : (t : fiber pair-fun (y , b)) â†’ Î³ (Ï† t) ï¼ t
   Î³Ï† ((x , a) , refl) = refl

   Ï†Î³ : (s : pair-fun-fiber' y b) â†’ Ï† (Î³ s) ï¼ s
   Ï†Î³ ((x , refl) , (a , refl)) = refl


 pair-fun-is-embedding : is-embedding f
                       â†’ ((x : X) â†’ is-embedding (g x))
                       â†’ is-embedding pair-fun
 pair-fun-is-embedding e d (y , b) = h
  where
   i : is-prop (pair-fun-fiber' y b)
   i = subtypes-of-props-are-props'
        prâ‚
        (prâ‚-lc (Î» {w} â†’ d (prâ‚ w) (transportâ»Â¹ B (prâ‚‚ w) b)))
        (e y)

   h : is-prop (fiber pair-fun (y , b))
   h = equiv-to-prop (pair-fun-fiber-â‰ƒ y b) i

 pair-fun-is-vv-equiv : is-vv-equiv f
                      â†’ ((x : X) â†’ is-vv-equiv (g x))
                      â†’ is-vv-equiv pair-fun
 pair-fun-is-vv-equiv e d (y , b) = h
  where
   k : is-prop (fiber pair-fun (y , b))
   k = pair-fun-is-embedding
        (equivs-are-embeddings f (vv-equivs-are-equivs f e))
        (Î» x â†’ equivs-are-embeddings (g x) (vv-equivs-are-equivs (g x) (d x)))
        (y , b)

   x : X
   x = fiber-point (center (e y))

   i : f x ï¼ y
   i = fiber-identification (center (e y))

   w : pair-fun-fiber' y b
   w = (center (e y) , center (d x (transportâ»Â¹ B i b)))

   h : is-singleton (fiber pair-fun (y , b))
   h = pointed-props-are-singletons (âŒœ pair-fun-fiber-â‰ƒ y b âŒâ»Â¹ w) k

 pair-fun-is-equiv : is-equiv f
                   â†’ ((x : X) â†’ is-equiv (g x))
                   â†’ is-equiv pair-fun
 pair-fun-is-equiv e d = vv-equivs-are-equivs
                          pair-fun
                          (pair-fun-is-vv-equiv
                            (equivs-are-vv-equivs f e)
                            (Î» x â†’ equivs-are-vv-equivs (g x) (d x)))

 pair-fun-dense : is-dense f
                â†’ ((x : X) â†’ is-dense (g x))
                â†’ is-dense pair-fun
 pair-fun-dense i j = contrapositive Î³ i
  where
   Î³ : (Î£ w ê‰ Î£ B , Â¬ fiber pair-fun w) â†’ Î£ y ê‰ Y , Â¬ fiber f y
   Î³ ((y , b) , n) = y , m
    where
     m : Â¬ fiber f y
     m (x , refl) = j x (b , l)
      where
       l : Â¬ fiber (g x) b
       l (a , refl) = n ((x , a) , refl)

 open import UF.PropTrunc

 module pair-fun-surjection (pt : propositional-truncations-exist) where

  open PropositionalTruncation pt
  open import UF.ImageAndSurjection pt

  pair-fun-is-surjection : is-surjection f
                         â†’ ((x : X) â†’ is-surjection (g x))
                         â†’ is-surjection pair-fun
  pair-fun-is-surjection s t (y , b) = Î³
   where
    Î³ : âˆƒ (x , a) ê‰ Î£ A , (pair-fun (x , a) ï¼ y , b)
    Î³ = âˆ¥âˆ¥-rec âˆƒ-is-prop
         (Î» {(x , refl) â†’ âˆ¥âˆ¥-rec âˆƒ-is-prop
                           (Î» {(a , refl) â†’ âˆ£ (x , a) , refl âˆ£})
                           (t x b)})
         (s y)

pair-fun-equiv : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                 {Y : ğ“¦ Ì‡ } {B : Y â†’ ğ“£ Ì‡ }
                 (f : X â‰ƒ Y)
               â†’ ((x : X) â†’ A x â‰ƒ B (âŒœ f âŒ x))
               â†’ Î£ A â‰ƒ Î£ B
pair-fun-equiv f g = pair-fun âŒœ f âŒ (Î» x â†’ âŒœ g x âŒ) ,
                     pair-fun-is-equiv _ _ âŒœ f âŒ-is-equiv (Î» x â†’ âŒœ g x âŒ-is-equiv)

Î£-change-of-variable-embedding : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
                                 (A : X â†’ ğ“¦ Ì‡ ) (g : Y â†’ X)
                               â†’ is-embedding g
                               â†’ (Î£ y ê‰ Y , A (g y)) â†ª (Î£ x ê‰ X , A x)
Î£-change-of-variable-embedding A g e = pair-fun g (Î» _ â†’ id) ,
                                       pair-fun-is-embedding
                                        g
                                        (Î» _ â†’ id)
                                        e
                                        (Î» _ â†’ id-is-embedding)

pair-fun-embedding : {X : ğ“¤ Ì‡ }
                     {A : X â†’ ğ“¥ Ì‡ }
                     {Y : ğ“¦ Ì‡ }
                     {B : Y â†’ ğ“£ Ì‡ }
                   â†’ (e : X â†ª Y)
                   â†’ ((x : X) â†’ A x â†ª B (âŒŠ e âŒ‹ x))
                   â†’ Î£ A â†ª Î£ B
pair-fun-embedding (f , i) g = pair-fun f (Î» x â†’ âŒŠ g x âŒ‹) ,
                               pair-fun-is-embedding
                                f
                                ((Î» x â†’ âŒŠ g x âŒ‹))
                                i
                                (Î» x â†’ âŒŠ g x âŒ‹-is-embedding)


pair-fun-is-embedding-special : {ğ“¤ ğ“¥ ğ“¦ : Universe}
                                {X : ğ“¤ Ì‡  } {Y : ğ“¥ Ì‡  } {B : Y â†’ ğ“¦ Ì‡  }
                              â†’ (f : X â†’ Y)
                              â†’ (g : (x : X) â†’ B (f x))
                              â†’ is-embedding f
                              â†’ ((y : Y) â†’ is-prop (B y))
                              â†’ is-embedding (Î» x â†’ f x , g x)
pair-fun-is-embedding-special {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y} {B} f g f-emb B-is-prop = e
 where
  k : X â‰ƒ X Ã— ğŸ™ {ğ“¤}
  k = â‰ƒ-sym ğŸ™-rneutral

  k-emb : is-embedding âŒœ k âŒ
  k-emb = equivs-are-embeddings âŒœ k âŒ âŒœ k âŒ-is-equiv

  h : X â†’ Î£ B
  h x = f x , g x

  g' : (x : X) â†’ ğŸ™ â†’ B (f x)
  g' x _ = g x

  g'-emb : (x : X) â†’ is-embedding (g' x)
  g'-emb x = maps-of-props-are-embeddings (g' x) ğŸ™-is-prop (B-is-prop (f x))

  remark : h ï¼ pair-fun f g' âˆ˜ âŒœ k âŒ
  remark = refl

  e : is-embedding h
  e = âˆ˜-is-embedding k-emb (pair-fun-is-embedding f g' f-emb g'-emb)

\end{code}
