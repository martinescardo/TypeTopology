Jonathan Sterling, started 22 March, 2023.

Based on Egbert Rijke's "Introduction to Homotopy Type Theory".

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.IdentitySystems where

open import MLTT.Spartan
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.PairFun as PairFun

record Has-Id-Sys {ğ“¦} (A : ğ“¤ Ì‡ ) (a : A) (fam : A â†’ ğ“¦ Ì‡) : ğ“¤Ï‰ where
 field
  ctr : fam a
  ind : {ğ“¥ : Universe} (P : (x : A) (q : fam x) â†’ ğ“¥ Ì‡) (p : P a ctr) (x : A) (q : fam x) â†’ P x q
  ind-Î² : {ğ“¥ : Universe} (P : (x : A) (q : fam x) â†’ ğ“¥ Ì‡) (p : P a ctr) â†’ ind P p a ctr ï¼ p

 to-ï¼ : (x : A) â†’ fam x â†’ a ï¼ x
 to-ï¼ = ind _ refl

 from-ï¼ : (x : A) â†’ a ï¼ x â†’ fam x
 from-ï¼ x refl = ctr

 to-ï¼-is-equiv : (x : A) â†’ is-equiv (to-ï¼ x)
 prâ‚ (prâ‚ (to-ï¼-is-equiv x)) = from-ï¼ x
 prâ‚‚ (prâ‚ (to-ï¼-is-equiv x)) refl = ind-Î² _ _
 prâ‚ (prâ‚‚ (to-ï¼-is-equiv x)) = from-ï¼ x
 prâ‚‚ (prâ‚‚ (to-ï¼-is-equiv x)) q = aux x q
  where
   aux : (x : A) (q : fam x) â†’ from-ï¼ x (to-ï¼ x q) ï¼ q
   aux = ind _ (ap (from-ï¼ a) (ind-Î² _ _))

record Id-Sys ğ“¦ (A : ğ“¤ Ì‡ ) (a : A) : ğ“¤Ï‰ where
 field
  fam : A â†’ ğ“¦  Ì‡
  sys : Has-Id-Sys A a fam
 open Has-Id-Sys sys public


Unbiased-Id-Sys : Universe â†’ ğ“¤ Ì‡ â†’ ğ“¤Ï‰
Unbiased-Id-Sys ğ“¦ A = (a : A) â†’ Id-Sys ğ“¦ A a


module from-path-characterization
  {A : ğ“¤ Ì‡ }
  (Q : A â†’ A â†’ ğ“¤ Ì‡ )
  (H : {x y : A} â†’ (x ï¼ y) â‰ƒ Q x y)
  (a : A)
 where
  open Id-Sys
  open Has-Id-Sys

  private
   Q-refl : {x : A} â†’ Q x x
   Q-refl = eqtofun H refl

   aux
    : (P : (x : A) (q : Q a x) â†’ ğ“¥ Ì‡ )
    â†’ (p : P a Q-refl)
    â†’ (x : A)
    â†’ (q : a ï¼ x)
    â†’ P x (eqtofun H q)
   aux P p x refl = p

  id-sys : Id-Sys ğ“¤ A a
  fam id-sys = Q a
  ctr (sys id-sys) = Q-refl
  ind (sys id-sys) P p x q =
   transport (P x)
    (inverses-are-sections _ (eqtofun- H) q)
    (aux P p x (back-eqtofun H q))
  ind-Î² (sys id-sys) P p =
   ap gen
    (Aux-is-prop
     (back-eqtofun H Q-refl ,
      inverses-are-sections _ (eqtofun- H)  Q-refl)
     (refl , refl))
   where
    Aux = Î£ Ï• ê‰ a ï¼ a , eqtofun H Ï• ï¼ Q-refl

    Aux-singl : singleton-type' refl â‰ƒ Aux
    Aux-singl =
     pair-fun-equiv (â‰ƒ-refl (a ï¼ a)) Î» Ï• â†’
     ap (eqtofun H) ,
     embedding-gives-embedding' _
      (equivs-are-embeddings _ (eqtofun- H))
      Ï•
      refl

    Aux-is-prop : is-prop Aux
    Aux-is-prop =
     retract-of-prop
      (â‰ƒ-gives-â— (â‰ƒ-sym Aux-singl))
      (singleton-types'-are-props refl)

    gen : Aux â†’ P a Q-refl
    gen (Ï• , Ïˆ ) = transport (P a) Ïˆ (aux P p a Ï•)


module _ ğ“¦ ğ“¦' (A : ğ“¤ Ì‡ ) (B : A â†’ ğ“¥ Ì‡ ) where
 record Dep-Id-Sys {a : A} ([a] : Id-Sys ğ“¦ A a) (b : B a) : ğ“¤Ï‰ where
  private
   module [a] = Id-Sys [a]
  field
   fam : (x : A) (q : [a].fam x) (y : B x) â†’ ğ“¦' Ì‡
   sys : Has-Id-Sys (B a) b (fam a [a].ctr)

  open Has-Id-Sys sys public


module _
  {A : ğ“¤ Ì‡ } {B : A â†’ ğ“¥ Ì‡ }
  {a : A} {b : B a}
  ([a] : Id-Sys ğ“¦ A a)
  ([b] : Dep-Id-Sys ğ“¦ ğ“¦' A B [a] b)
 where
  module [a] = Id-Sys [a]
  module [b] = Dep-Id-Sys [b]

  open Id-Sys
  open Has-Id-Sys

  pair-id-sys : Id-Sys (ğ“¦ âŠ” ğ“¦') (Î£ B) (a , b)
  fam pair-id-sys (x , y) = Î£ Ï• ê‰ [a].fam x , [b].fam x Ï• y
  prâ‚ (ctr (sys pair-id-sys)) = [a].ctr
  prâ‚‚ (ctr (sys pair-id-sys)) = [b].ctr
  ind (sys pair-id-sys) P p =
   Î» (x , y) (Ï• , Ïˆ) â†’ aux x Ï• y Ïˆ
   where
    aux : (x : A) (Ï• : [a].fam x) (y : B x) (Ïˆ : [b].fam x Ï• y) â†’ P (x , y) (Ï• , Ïˆ)
    aux = [a].ind _ ([b].ind _ p)
  ind-Î² (sys pair-id-sys) P p =
   happly (happly ([a].ind-Î² _ _) b) [b].ctr âˆ™ [b].ind-Î² _ _

module _ (A : ğ“¤ Ì‡ ) where
 open Id-Sys
 open Has-Id-Sys

 ï¼-id-sys : Unbiased-Id-Sys ğ“¤ A
 fam (ï¼-id-sys a) = a ï¼_
 ctr (sys (ï¼-id-sys a)) = refl
 ind (sys (ï¼-id-sys a)) P p x refl = p
 ind-Î² (sys (ï¼-id-sys a)) _ _ = refl

module _ (fe : funext ğ“¤ ğ“¥) {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } (f : A â†’ B) where
 homotopy-id-sys : Id-Sys (ğ“¤ âŠ” ğ“¥) (A â†’ B) f
 homotopy-id-sys = from-path-characterization.id-sys _âˆ¼_ (happly-â‰ƒ fe) f

\end{code}
