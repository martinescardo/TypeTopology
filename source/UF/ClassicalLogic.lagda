Martin Escardo.

Excluded middle related things. Notice that this file doesn't
postulate excluded middle. It only defines what the principle of
excluded middle is.

In the Curry-Howard interpretation, excluded middle say that every
type has an inhabitant or os empty. In univalent foundations, where
one works with propositions as subsingletons, excluded middle is the
principle that every subsingleton type is inhabited or empty.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module UF.ClassicalLogic where

open import MLTT.Spartan

open import UF.Base
open import UF.Equiv
open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier
open import UF.UniverseEmbedding

\end{code}

Excluded middle (EM) is not provable or disprovable. However, we do
have that there is no truth value other than false (âŠ¥) or true (âŠ¤),
which we refer to as the density of the decidable truth values.

\begin{code}

EM : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
EM ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ P + Â¬ P

excluded-middle = EM

lower-EM : âˆ€ ğ“¥ â†’ EM (ğ“¤ âŠ” ğ“¥) â†’ EM ğ“¤
lower-EM ğ“¥ em P P-is-prop = f d
 where
  d : Lift ğ“¥ P + Â¬ Lift ğ“¥ P
  d = em (Lift ğ“¥ P) (equiv-to-prop (Lift-is-universe-embedding ğ“¥ P) P-is-prop)

  f : Lift ğ“¥ P + Â¬ Lift ğ“¥ P â†’ P + Â¬ P
  f (inl p) = inl (lower p)
  f (inr Î½) = inr (Î» p â†’ Î½ (lift ğ“¥ p))

Excluded-Middle : ğ“¤Ï‰
Excluded-Middle = âˆ€ {ğ“¤} â†’ EM ğ“¤

EM-is-prop : FunExt â†’ is-prop (EM ğ“¤)
EM-is-prop {ğ“¤} fe = Î â‚‚-is-prop
                     (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                     (Î» _ â†’ decidability-of-prop-is-prop (fe ğ“¤ ğ“¤â‚€))

LEM : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
LEM ğ“¤ = (p : Î© ğ“¤) â†’ p holds + Â¬ (p holds)

EM-gives-LEM : EM ğ“¤ â†’ LEM ğ“¤
EM-gives-LEM em p = em (p holds) (holds-is-prop p)

LEM-gives-EM : LEM ğ“¤ â†’ EM ğ“¤
LEM-gives-EM lem P i = lem (P , i)

\end{code}

Added by Martin Escardo and Tom de Jong 29th August 2024. Originally
we worked with WEM. But it turns out that it is not necessary to
assume that P is a proposition, and so we now work with the new
definition typal-WEM, which removes this assumption.

\begin{code}

WEM : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
WEM ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬ P + Â¬Â¬ P

typal-WEM : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
typal-WEM ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ Â¬ P + Â¬Â¬ P

WEM-gives-typal-WEM : funext ğ“¤ ğ“¤â‚€ â†’ WEM ğ“¤ â†’ typal-WEM ğ“¤
WEM-gives-typal-WEM fe wem' P =
 Cases (wem' (Â¬ P) (negations-are-props fe))
  inr
  (inl âˆ˜ three-negations-imply-one)

typal-WEM-gives-WEM : typal-WEM ğ“¤ â†’ WEM ğ“¤
typal-WEM-gives-WEM wem P P-is-prop = wem P

typal-WEM-is-prop : FunExt â†’ is-prop (typal-WEM ğ“¤)
typal-WEM-is-prop {ğ“¤} fe = Î -is-prop (fe (ğ“¤ âº) ğ“¤)
                            (Î» _ â†’ decidability-of-prop-is-prop (fe ğ“¤ ğ“¤â‚€)
                                    (negations-are-props (fe ğ“¤ ğ“¤â‚€)))

WEM-is-prop : FunExt â†’ is-prop (WEM ğ“¤)
WEM-is-prop {ğ“¤} fe = Î â‚‚-is-prop (Î» {ğ“¥} {ğ“¦} â†’ fe ğ“¥ ğ“¦)
                       (Î» _ _ â†’ decidability-of-prop-is-prop (fe ğ“¤ ğ“¤â‚€)
                                 (negations-are-props (fe ğ“¤ ğ“¤â‚€)))

\end{code}

End of addition.

Double negation elimination is equivalent to excluded middle.

\begin{code}

DNE : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
DNE ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Â¬Â¬ P â†’ P

EM-gives-DNE : EM ğ“¤ â†’ DNE ğ“¤
EM-gives-DNE em P i Ï† = cases id (Î» u â†’ ğŸ˜-elim (Ï† u)) (em P i)

double-negation-elim : EM ğ“¤ â†’ DNE ğ“¤
double-negation-elim = EM-gives-DNE

fake-Â¬Â¬-EM : {X : ğ“¤ Ì‡ } â†’ Â¬Â¬ (X + Â¬ X)
fake-Â¬Â¬-EM u = u (inr (Î» p â†’ u (inl p)))

DNE-gives-EM : funext ğ“¤ ğ“¤â‚€ â†’ DNE ğ“¤ â†’ EM ğ“¤
DNE-gives-EM fe dne P isp = dne (P + Â¬ P)
                             (decidability-of-prop-is-prop fe isp)
                             fake-Â¬Â¬-EM

all-props-negative-gives-DNE : funext ğ“¤ ğ“¤â‚€
                            â†’ ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Î£ Q ê‰ ğ“¤ Ì‡ , (P â†” Â¬ Q))
                            â†’ DNE ğ“¤
all-props-negative-gives-DNE {ğ“¤} fe Ï• P P-is-prop = I (Ï• P P-is-prop)
 where
  I : (Î£ Q ê‰ ğ“¤ Ì‡ , (P â†” Â¬ Q)) â†’ Â¬Â¬ P â†’ P
  I (Q , f , g) Î½ = g (three-negations-imply-one (double-contrapositive f Î½))

all-props-negative-gives-EM
 : funext ğ“¤ ğ“¤â‚€
 â†’ ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Î£ Q ê‰ ğ“¤ Ì‡ , (P â†” Â¬ Q))
 â†’ EM ğ“¤
all-props-negative-gives-EM {ğ“¤} fe Ï•
 = DNE-gives-EM fe (all-props-negative-gives-DNE fe Ï•)

fe-and-em-give-propositional-truncations
 : FunExt
 â†’ Excluded-Middle
 â†’ propositional-truncations-exist
fe-and-em-give-propositional-truncations fe em =
 record {
  âˆ¥_âˆ¥          = Î» X â†’ Â¬Â¬ X ;
  âˆ¥âˆ¥-is-prop   = Î -is-prop (fe _ _) (Î» _ â†’ ğŸ˜-is-prop) ;
  âˆ£_âˆ£          = Î» x u â†’ u x ;
  âˆ¥âˆ¥-rec       = Î» i u Ï† â†’ EM-gives-DNE em _ i (Â¬Â¬-functor u Ï†)
  }

\end{code}

We now consider various logically equivalent formulations of De Morgan
Law.

\begin{code}

untruncated-De-Morgan : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
untruncated-De-Morgan ğ“¤ = (P Q : ğ“¤ Ì‡ )
                        â†’ is-prop P
                        â†’ is-prop Q
                        â†’ Â¬ (P Ã— Q) â†’ Â¬ P + Â¬ Q

EM-gives-untruncated-De-Morgan : EM ğ“¤
                               â†’ untruncated-De-Morgan ğ“¤
EM-gives-untruncated-De-Morgan em A B i j =
 Î» (Î½ : Â¬ (A Ã— B)) â†’
      Cases (em A i)
       (Î» (a : A) â†’ Cases (em B j)
                     (Î» (b : B) â†’ ğŸ˜-elim (Î½ (a , b)))
                     inr)
       inl

\end{code}

But already weak excluded middle gives De Morgan.

Added by Martin Escardo and Tom de Jong 29th August 2024. A typal
version of De Morgan.

\begin{code}

non-contradiction : {X : ğ“¤ Ì‡ } â†’ Â¬ (X Ã— Â¬ X)
non-contradiction (x , Î½) = Î½ x

untruncated-typal-De-Morgan : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
untruncated-typal-De-Morgan ğ“¤ = (P Q : ğ“¤ Ì‡ ) â†’ Â¬ (P Ã— Q) â†’ Â¬ P + Â¬ Q

untruncated-typal-De-Morgan-gives-untruncated-De-Morgan
 : untruncated-typal-De-Morgan ğ“¤
 â†’ untruncated-De-Morgan ğ“¤
untruncated-typal-De-Morgan-gives-untruncated-De-Morgan d' P Q i j = d' P Q

typal-WEM-gives-untruncated-typal-De-Morgan : typal-WEM ğ“¤
                                            â†’ untruncated-typal-De-Morgan ğ“¤
typal-WEM-gives-untruncated-typal-De-Morgan wem A B =
 Î» (Î½ : Â¬ (A Ã— B)) â†’
      Cases (wem A)
       inl
       (Î» (Ï• : Â¬Â¬ A)
             â†’ Cases (wem B)
                inr
                (Î» (Î³ : Â¬Â¬ B) â†’ ğŸ˜-elim
                                 (Ï• (Î» (a : A) â†’ Î³ (Î» (b : B) â†’ Î½ (a , b))))))

typal-WEM-gives-untruncated-De-Morgan : typal-WEM ğ“¤ â†’ untruncated-De-Morgan ğ“¤
typal-WEM-gives-untruncated-De-Morgan =
 untruncated-typal-De-Morgan-gives-untruncated-De-Morgan
 âˆ˜ typal-WEM-gives-untruncated-typal-De-Morgan

untruncated-De-Morgan-gives-typal-WEM : funext ğ“¤ ğ“¤â‚€
                                      â†’ untruncated-De-Morgan ğ“¤
                                      â†’ typal-WEM ğ“¤
untruncated-De-Morgan-gives-typal-WEM fe d =
 WEM-gives-typal-WEM fe
  (Î» P i â†’ d P (Â¬ P) i (negations-are-props fe) non-contradiction)

untruncated-De-Morgan-gives-untruncated-typal-De-Morgan
 : funext ğ“¤ ğ“¤â‚€
 â†’ untruncated-De-Morgan ğ“¤
 â†’ untruncated-typal-De-Morgan ğ“¤
untruncated-De-Morgan-gives-untruncated-typal-De-Morgan fe =
 typal-WEM-gives-untruncated-typal-De-Morgan
 âˆ˜ untruncated-De-Morgan-gives-typal-WEM fe

\end{code}

End of addition.

Is the above untruncated De Morgan Law a proposition? Not in
general. If it doesn't hold, it is vacuously a proposition. But if it
does hold, it is not a proposition. We prove this by modifying any
given Î´ : De-Mordan ğ“¤ to a different Î´' : untruncated-De-Morgan
ğ“¤. Then we also consider a truncated version of untruncated-De-Morgan
that is a proposition and is logically equivalent to
untruncated-De-Morgan. So untruncated-De-Morgan ğ“¤ is not necessarily a
proposition, but it always has split support (it has a proposition as
a retract).

\begin{code}

untruncated-De-Morgan-is-prop
 : Â¬ untruncated-De-Morgan ğ“¤
 â†’ is-prop (untruncated-De-Morgan ğ“¤)
untruncated-De-Morgan-is-prop Î½ Î´ = ğŸ˜-elim (Î½ Î´)

untruncated-typal-De-Morgan-has-two-witnesses-if-it-has-one
 : funext ğ“¤ ğ“¤â‚€
 â†’ (Î´ : untruncated-typal-De-Morgan ğ“¤)
 â†’ Î£ Î´' ê‰ untruncated-typal-De-Morgan ğ“¤ , Î´' â‰  Î´
untruncated-typal-De-Morgan-has-two-witnesses-if-it-has-one {ğ“¤} fe Î´
 = (Î´' , III)
 where
  open import MLTT.Plus-Properties

  wem : typal-WEM ğ“¤
  wem = untruncated-De-Morgan-gives-typal-WEM fe
         (untruncated-typal-De-Morgan-gives-untruncated-De-Morgan Î´)

  g : (P Q : ğ“¤ Ì‡ )
      (Î½ : Â¬ (P Ã— Q))
      (a : Â¬ P + Â¬Â¬ P)
      (b : Â¬ Q + Â¬Â¬ Q)
      (c : Â¬ P + Â¬ Q)
    â†’ Â¬ P + Â¬ Q
  g P Q Î½ (inl _) (inl v) (inl _) = inr v
  g P Q Î½ (inl u) (inl _) (inr _) = inl u
  g P Q Î½ (inl _) (inr _) _       = Î´ P Q Î½
  g P Q Î½ (inr _) _       _       = Î´ P Q Î½

  Î´' : untruncated-typal-De-Morgan ğ“¤
  Î´' P Q Î½ = g P Q Î½ (wem P) (wem Q) (Î´ P Q Î½)

  I : (h : Â¬ ğŸ˜) â†’ wem ğŸ˜ ï¼ inl h
  I h = Iâ‚€ (wem ğŸ˜) refl
   where
    Iâ‚€ : (a : Â¬ ğŸ˜ + Â¬Â¬ ğŸ˜) â†’ wem ğŸ˜ ï¼ a â†’ wem ğŸ˜ ï¼ inl h
    Iâ‚€ (inl u) = transport
                  (Î» - â†’ wem ğŸ˜ ï¼ inl -)
                  (negations-are-props fe u h)
    Iâ‚€ (inr Ï•) p = ğŸ˜-elim (Ï• h)

  Î½ : Â¬ (ğŸ˜ Ã— ğŸ˜)
  Î½ (p , q) = ğŸ˜-elim p

  II : Î´' ğŸ˜ ğŸ˜ Î½ â‰  Î´ ğŸ˜ ğŸ˜ Î½
  II = IIâ‚ƒ
   where
    m n : Â¬ ğŸ˜ + Â¬ ğŸ˜
    m = Î´ ğŸ˜ ğŸ˜ Î½
    n = g ğŸ˜ ğŸ˜ Î½ (inl ğŸ˜-elim) (inl ğŸ˜-elim) m

    IIâ‚ : Î´' ğŸ˜ ğŸ˜ Î½ ï¼ n
    IIâ‚ = apâ‚‚ (Î» -â‚ -â‚‚ â†’ g ğŸ˜ ğŸ˜ Î½ -â‚ -â‚‚ m)
              (I ğŸ˜-elim)
              (I ğŸ˜-elim)

    IIâ‚‚ : (m' : Â¬ ğŸ˜ + Â¬ ğŸ˜)
        â†’ m ï¼ m'
        â†’ g ğŸ˜ ğŸ˜ Î½ (inl ğŸ˜-elim) (inl ğŸ˜-elim) m' â‰  m
    IIâ‚‚ (inl x) p q = +disjoint
                       (inl x      ï¼âŸ¨ p â»Â¹ âŸ©
                        m          ï¼âŸ¨ q â»Â¹ âŸ©
                        inr ğŸ˜-elim âˆ)
    IIâ‚‚ (inr x) p q = +disjoint
                       (inl ğŸ˜-elim ï¼âŸ¨ q âŸ©
                        m          ï¼âŸ¨ p âŸ©
                        inr x      âˆ)

    IIâ‚ƒ : Î´' ğŸ˜ ğŸ˜ Î½ â‰  m
    IIâ‚ƒ = transport (_â‰  m) (IIâ‚ â»Â¹) (IIâ‚‚ m refl)

  III : Î´' â‰  Î´
  III e = II IIIâ‚€
   where
    IIIâ‚€ : Î´' ğŸ˜ ğŸ˜ Î½ ï¼ Î´ ğŸ˜ ğŸ˜ Î½
    IIIâ‚€ = ap (Î» - â†’ - ğŸ˜ ğŸ˜ Î½) e

untruncated-typal-De-Morgan-is-not-prop
 : funext ğ“¤ ğ“¤â‚€
 â†’ untruncated-typal-De-Morgan ğ“¤
 â†’ Â¬ is-prop (untruncated-typal-De-Morgan ğ“¤)
untruncated-typal-De-Morgan-is-not-prop {ğ“¤} fe Î´
 = IV (untruncated-typal-De-Morgan-has-two-witnesses-if-it-has-one fe Î´)
 where
  IV : (Î£ Î´' ê‰ untruncated-typal-De-Morgan ğ“¤ , Î´' â‰  Î´)
    â†’ Â¬ is-prop (untruncated-typal-De-Morgan ğ“¤)
  IV (Î´' , III) i = III (i Î´' Î´)

\end{code}

We repeat the above proof adding more information.

TODO. Is it possible to prove the following from the above, or
vice-versa, to avoid the repetition?

\begin{code}

untruncated-De-Morgan-has-at-least-two-witnesses-if-it-has-one
 : funext ğ“¤ ğ“¤â‚€
 â†’ (Î´ : untruncated-De-Morgan ğ“¤)
 â†’ Î£ Î´' ê‰ untruncated-De-Morgan ğ“¤ , Î´' â‰  Î´
untruncated-De-Morgan-has-at-least-two-witnesses-if-it-has-one {ğ“¤} fe Î´
 = (Î´' , III)
 where
  open import MLTT.Plus-Properties

  wem : typal-WEM ğ“¤
  wem = untruncated-De-Morgan-gives-typal-WEM fe Î´

  g : (P Q : ğ“¤ Ì‡ )
      (i : is-prop P) (j : is-prop Q)
      (Î½ : Â¬ (P Ã— Q))
      (a : Â¬ P + Â¬Â¬ P)
      (b : Â¬ Q + Â¬Â¬ Q)
      (c : Â¬ P + Â¬ Q)
    â†’ Â¬ P + Â¬ Q
  g P Q i j Î½ (inl _) (inl v) (inl _) = inr v
  g P Q i j Î½ (inl u) (inl _) (inr _) = inl u
  g P Q i j Î½ (inl _) (inr _) _       = Î´ P Q i j Î½
  g P Q i j Î½ (inr _) _       _       = Î´ P Q i j Î½

  Î´' : untruncated-De-Morgan ğ“¤
  Î´' P Q i j Î½ = g P Q i j Î½ (wem P) (wem Q) (Î´ P Q i j Î½)

  I : (i : is-prop ğŸ˜) (h : Â¬ ğŸ˜) â†’ wem ğŸ˜ ï¼ inl h
  I i h = Iâ‚€ (wem ğŸ˜) refl
   where
    Iâ‚€ : (a : Â¬ ğŸ˜ + Â¬Â¬ ğŸ˜) â†’ wem ğŸ˜ ï¼ a â†’ wem ğŸ˜ ï¼ inl h
    Iâ‚€ (inl u) = transport
                  (Î» - â†’ wem ğŸ˜ ï¼ inl -)
                  (negations-are-props fe u h)
    Iâ‚€ (inr Ï•) p = ğŸ˜-elim (Ï• h)

  Î½ : Â¬ (ğŸ˜ Ã— ğŸ˜)
  Î½ (p , q) = ğŸ˜-elim p

  II : (i j : is-prop ğŸ˜) â†’ Î´' ğŸ˜ ğŸ˜ i j Î½ â‰  Î´ ğŸ˜ ğŸ˜ i j Î½
  II i j = IIâ‚ƒ
   where
    m n : Â¬ ğŸ˜ + Â¬ ğŸ˜
    m = Î´ ğŸ˜ ğŸ˜ i j Î½
    n = g ğŸ˜ ğŸ˜ i j Î½ (inl ğŸ˜-elim) (inl ğŸ˜-elim) m

    IIâ‚ : Î´' ğŸ˜ ğŸ˜ i j Î½ ï¼ n
    IIâ‚ = apâ‚‚ (Î» -â‚ -â‚‚ â†’ g ğŸ˜ ğŸ˜ i j Î½ -â‚ -â‚‚ m)
              (I i ğŸ˜-elim)
              (I j ğŸ˜-elim)

    IIâ‚‚ : (m' : Â¬ ğŸ˜ + Â¬ ğŸ˜)
        â†’ m ï¼ m'
        â†’ g ğŸ˜ ğŸ˜ i j Î½ (inl ğŸ˜-elim) (inl ğŸ˜-elim) m' â‰  m
    IIâ‚‚ (inl x) p q = +disjoint
                       (inl x      ï¼âŸ¨ p â»Â¹ âŸ©
                        m          ï¼âŸ¨ q â»Â¹ âŸ©
                        inr ğŸ˜-elim âˆ)
    IIâ‚‚ (inr x) p q = +disjoint
                       (inl ğŸ˜-elim ï¼âŸ¨ q âŸ©
                        m          ï¼âŸ¨ p âŸ©
                        inr x      âˆ)

    IIâ‚ƒ : Î´' ğŸ˜ ğŸ˜ i j Î½ â‰  m
    IIâ‚ƒ = transport (_â‰  m) (IIâ‚ â»Â¹) (IIâ‚‚ m refl)

  III : Î´' â‰  Î´
  III e = II ğŸ˜-is-prop ğŸ˜-is-prop IIIâ‚€
   where
    IIIâ‚€ : Î´' ğŸ˜ ğŸ˜ ğŸ˜-is-prop ğŸ˜-is-prop Î½ ï¼ Î´ ğŸ˜ ğŸ˜ ğŸ˜-is-prop ğŸ˜-is-prop Î½
    IIIâ‚€ = ap (Î» - â†’ - ğŸ˜ ğŸ˜ ğŸ˜-is-prop ğŸ˜-is-prop Î½) e

untruncated-De-Morgan-is-not-prop
 : funext ğ“¤ ğ“¤â‚€
 â†’ untruncated-De-Morgan ğ“¤
 â†’ Â¬ is-prop (untruncated-De-Morgan ğ“¤)
untruncated-De-Morgan-is-not-prop {ğ“¤} fe Î´
 = IV (untruncated-De-Morgan-has-at-least-two-witnesses-if-it-has-one fe Î´)
 where
  IV : (Î£ Î´' ê‰ untruncated-De-Morgan ğ“¤ , Î´' â‰  Î´)
    â†’ Â¬ is-prop (untruncated-De-Morgan ğ“¤)
  IV (Î´' , III) i = III (i Î´' Î´)

untruncated-De-Morgan-curiousity : funext ğ“¤ ğ“¤â‚€
                                 â†’ Â¬Â¬ is-prop (untruncated-De-Morgan ğ“¤)
                                 â†’ is-prop (untruncated-De-Morgan ğ“¤)
untruncated-De-Morgan-curiousity fe =
 untruncated-De-Morgan-is-prop
 âˆ˜ contrapositive (untruncated-De-Morgan-is-not-prop fe)

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 typal-De-Morgan : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
 typal-De-Morgan ğ“¤ = (P Q : ğ“¤ Ì‡ ) â†’ Â¬ (P Ã— Q) â†’ Â¬ P âˆ¨ Â¬ Q

 De-Morgan : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
 De-Morgan ğ“¤ = (P Q : ğ“¤ Ì‡ )
             â†’ is-prop P
             â†’ is-prop Q
             â†’ Â¬ (P Ã— Q) â†’ Â¬ P âˆ¨ Â¬ Q

 typal-De-Morgan-is-prop : FunExt
                         â†’ is-prop (typal-De-Morgan ğ“¤)
 typal-De-Morgan-is-prop fe = Î â‚ƒ-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                                         (Î» P Q Î½ â†’ âˆ¨-is-prop)

 De-Morgan-is-prop : FunExt â†’ is-prop (De-Morgan ğ“¤)
 De-Morgan-is-prop fe = Î â‚…-is-prop (Î» {ğ“¤} {ğ“¥} â†’ fe ğ“¤ ğ“¥)
                                   (Î» P Q i j Î½ â†’ âˆ¨-is-prop)

 untruncated-De-Morgan-gives-De-Morgan : untruncated-De-Morgan ğ“¤
                                       â†’ De-Morgan ğ“¤
 untruncated-De-Morgan-gives-De-Morgan d P Q i j Î½ = âˆ£ d P Q i j Î½ âˆ£

 De-Morgan-gives-WEM : funext ğ“¤ ğ“¤â‚€
                     â†’ De-Morgan ğ“¤ â†’ WEM ğ“¤
 De-Morgan-gives-WEM {ğ“¤} fe dm P i = III
  where
   I : Â¬ (P Ã— Â¬ P) â†’ Â¬ P âˆ¨ Â¬Â¬ P
   I = dm P (Â¬ P) i (negations-are-props fe)

   II : Â¬ P âˆ¨ Â¬Â¬ P
   II = I non-contradiction

   III : Â¬ P + Â¬Â¬ P
   III = exit-âˆ¥âˆ¥
          (decidability-of-prop-is-prop fe (negations-are-props fe))
          II

 De-Morgan-gives-typal-WEM : funext ğ“¤ ğ“¤â‚€ â†’ De-Morgan ğ“¤ â†’ typal-WEM ğ“¤
 De-Morgan-gives-typal-WEM {ğ“¤} fe =
  WEM-gives-typal-WEM fe âˆ˜ De-Morgan-gives-WEM fe

 De-Morgan-gives-untruncated-De-Morgan : funext ğ“¤ ğ“¤â‚€
                                       â†’ De-Morgan ğ“¤
                                       â†’ untruncated-De-Morgan ğ“¤
 De-Morgan-gives-untruncated-De-Morgan fe t P Q i j Î½ =
  typal-WEM-gives-untruncated-De-Morgan
   (De-Morgan-gives-typal-WEM fe t)
   P Q i j Î½

 typal-De-Morgan-gives-De-Morgan : typal-De-Morgan ğ“¤ â†’ De-Morgan ğ“¤
 typal-De-Morgan-gives-De-Morgan d P Q i j = d P Q

 De-Morgan-gives-typal-De-Morgan : funext ğ“¤ ğ“¤â‚€
                                 â†’ De-Morgan ğ“¤
                                 â†’ typal-De-Morgan ğ“¤
 De-Morgan-gives-typal-De-Morgan {ğ“¤} fe d P Q Î½ =
  âˆ£ typal-WEM-gives-untruncated-typal-De-Morgan
     (De-Morgan-gives-typal-WEM fe d)
     P Q Î½ âˆ£

\end{code}

The above shows that weak excluded middle, De Morgan and truncated De
Morgan are logically equivalent, all in their two (proposional and
typal) versions, so in a total of six logically equivalent statements.

That weak excluded middle and De Morgan are equivalent is long known
and now part of the folklore. We don't know who proved this first,
but, for example, it is in Johnstone's papers on topos theory and his
Elephant two-volume book.

Mike Shulman asked in the HoTT mailing list [1] whether untruncated De
Morgan implies truncated De Morgan, and Martin Escardo offered a proof
as an answer [2], which Mike Shulman added to the nLab [3].

[1] Mike Shulman. de Morgan's Law.
    https://groups.google.com/g/homotopytypetheory/c/Azq6GVU98II/m/qEp8TeInYgAJ
    1st September 2014.

[3] Martin Escardo. de Morgan's Law.
    https://groups.google.com/g/homotopytypetheory/c/Azq6GVU98II/m/bXMixO9s1boJ
    2nd September 2014

[3] Added to the nLab by Mike Shulman.
    https://ncatlab.org/nlab/show/De%20Morgan%20laws.
    2nd September 2014

Here we have added, to both typal-WEM and De Morgan, truncated or not, the
discussion of whether the types in question need to be propositions or
not for them to be all equivalent, and the answer is that it doesn't
matter whether we assume that the types in question are all
propositions.

\begin{code}

 double-negation-is-truncation-gives-DNE : ((X : ğ“¤ Ì‡ ) â†’ Â¬Â¬ X â†’ âˆ¥ X âˆ¥) â†’ DNE ğ“¤
 double-negation-is-truncation-gives-DNE f P i u = exit-âˆ¥âˆ¥ i (f P u)

 non-empty-is-inhabited : EM ğ“¤ â†’ {X : ğ“¤ Ì‡ } â†’ Â¬Â¬ X â†’ âˆ¥ X âˆ¥
 non-empty-is-inhabited em {X} Ï† =
  cases
   (Î» (s : âˆ¥ X âˆ¥)
         â†’ s)
   (Î» (u : Â¬ âˆ¥ X âˆ¥)
         â†’ ğŸ˜-elim (Ï† (contrapositive âˆ£_âˆ£ u))) (em âˆ¥ X âˆ¥ âˆ¥âˆ¥-is-prop)

 Â¬Â¬Î£â†’âˆƒ : {ğ“¤ ğ“£ : Universe} {X : ğ“¤ Ì‡ } â†’ {A : X â†’ ğ“£  Ì‡}
       â†’ DNE (ğ“¤ âŠ” ğ“£)
       â†’ Â¬Â¬ (Î£ x ê‰ X , A x )
       â†’ (âˆƒ x ê‰ X , A x)
 Â¬Â¬Î£â†’âˆƒ {ğ“¤} {A} {X} {Aâ‚} dn Â¬Â¬Î£ = dn _ âˆ¥âˆ¥-is-prop (Â¬Â¬-functor âˆ£_âˆ£ Â¬Â¬Î£)

 âˆƒ-not+Î  : EM (ğ“¤ âŠ” ğ“¥)
         â†’ {X : ğ“¤ Ì‡ }
         â†’ (A : X â†’ ğ“¥ Ì‡ )
         â†’ ((x : X) â†’ is-prop (A x))
         â†’ (âˆƒ x ê‰ X , Â¬ (A x)) + (Î  x ê‰ X , A x)
 âˆƒ-not+Î  {ğ“¤} {ğ“¥} em {X} A is-prop-valued =
  Cases (em (âˆƒ x ê‰ X , Â¬ (A x)) âˆƒ-is-prop)
   inl
   (Î» (u : Â¬ (âˆƒ x ê‰ X , Â¬ (A x)))
         â†’ inr (Î» (x : X) â†’ EM-gives-DNE
                              (lower-EM (ğ“¤ âŠ” ğ“¥) em)
                              (A x)
                              (is-prop-valued x)
                              (Î» (v : Â¬ A x) â†’ u âˆ£ (x , v) âˆ£)))

 âˆƒ+Î -not : EM (ğ“¤ âŠ” ğ“¥)
         â†’ {X : ğ“¤ Ì‡ }
         â†’ (A : X â†’ ğ“¥ Ì‡ )
         â†’ ((x : X) â†’ is-prop (A x))
         â†’ (âˆƒ x ê‰ X , A x) + (Î  x ê‰ X , Â¬ (A x))
 âˆƒ+Î -not {ğ“¤} {ğ“¥} em {X} A is-prop-valued =
  Cases (em (âˆƒ x ê‰ X , A x) âˆƒ-is-prop)
   inl
   (Î» (u : Â¬ (âˆƒ x ê‰ X , A x))
         â†’ inr (Î» (x : X) (v : A x) â†’ u âˆ£ x , v âˆ£))

 not-Î -implies-âˆƒ-not : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                     â†’ EM (ğ“¤ âŠ” ğ“¥)
                     â†’ ((x : X) â†’ is-prop (A x))
                     â†’ Â¬ ((x : X) â†’ A x)
                     â†’ âˆƒ x ê‰ X , Â¬ A x
 not-Î -implies-âˆƒ-not {ğ“¤} {ğ“¥} {X} {A} em i f =
  Cases (em E âˆƒ-is-prop)
   id
   (Î» (u : Â¬ E)
         â†’ ğŸ˜-elim (f (Î» (x : X) â†’ EM-gives-DNE
                                   (lower-EM ğ“¤ em)
                                   (A x)
                                   (i x)
                                   (Î» (v : Â¬ A x) â†’ u âˆ£ x , v âˆ£))))
  where
   E = âˆƒ x ê‰ X , Â¬ A x

\end{code}

Added by Tom de Jong in August 2021.

\begin{code}

 not-Î -not-implies-âˆƒ : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                     â†’ EM (ğ“¤ âŠ” ğ“¥)
                     â†’ Â¬ ((x : X) â†’ Â¬ A x)
                     â†’ âˆƒ x ê‰ X , A x
 not-Î -not-implies-âˆƒ {ğ“¤} {ğ“¥} {X} {A} em f = EM-gives-DNE em (âˆƒ A) âˆ¥âˆ¥-is-prop Î³
   where
    Î³ : Â¬Â¬ (âˆƒ A)
    Î³ g = f (Î» x a â†’ g âˆ£ x , a âˆ£)

\end{code}

Added by Martin Escardo 26th April 2022.

\begin{code}

Global-Choice' : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
Global-Choice' ğ“¤ = (X : ğ“¤ Ì‡ ) â†’ is-nonempty X â†’ X

Global-Choice : âˆ€ ğ“¤ â†’ ğ“¤ âº Ì‡
Global-Choice ğ“¤ = (X : ğ“¤ Ì‡ ) â†’ X + is-empty X

Global-Choice-gives-Global-Choice' : Global-Choice ğ“¤ â†’ Global-Choice' ğ“¤
Global-Choice-gives-Global-Choice' gc X Ï† = cases id (Î» u â†’ ğŸ˜-elim (Ï† u)) (gc X)

Global-Choice'-gives-Global-Choice : Global-Choice' ğ“¤ â†’ Global-Choice ğ“¤
Global-Choice'-gives-Global-Choice gc X = gc (X + Â¬ X)
                                             (Î» u â†’ u (inr (Î» p â†’ u (inl p))))
\end{code}

TODO. Global choice contradicts univalence. This is already present in
the directory MGS.
