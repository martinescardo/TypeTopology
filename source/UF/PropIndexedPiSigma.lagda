Martin Escardo, 27 April 2014.

With additions 18th December 2017, and slightly refactored
15th May 2025, with minor improvements in the code.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

module UF.PropIndexedPiSigma
        {X : ğ“¤ Ì‡ }
        {Y : X â†’ ğ“¥ Ì‡ }
       where

open import UF.Base
open import UF.Equiv
open import UF.FunExt
open import UF.Subsingletons
open import UF.Subsingletons-Properties

private
 transport-lemma : {x : X} {y : Y x} (i : is-prop X)
                 â†’ transport Y (i x x) y ï¼ y
 transport-lemma {x} {y} i = ap (Î» - â†’ transport Y - y)
                                (identifications-in-props-are-refl i x)

module _ (a : X) where

 Î -proj : Î  Y â†’ Y a
 Î -proj f = f a

 Î -projâ»Â¹ : is-prop X â†’ Y a â†’ Î  Y
 Î -projâ»Â¹ i y x = transport Y (i a x) y

 Î -proj-is-equiv : funext ğ“¤ ğ“¥
                 â†’ is-prop X
                 â†’ is-equiv Î -proj
 Î -proj-is-equiv fe i = qinvs-are-equivs Î -proj (Î -projâ»Â¹ i , Î· , Îµ)
  where
   Î· : Î -projâ»Â¹ i âˆ˜ Î -proj âˆ¼ id
   Î· f = dfunext fe I
    where
     I : Î -projâ»Â¹ i (Î -proj f) âˆ¼ f
     I x =
      Î -projâ»Â¹ i (Î -proj f) x   ï¼âŸ¨ refl âŸ©
      transport Y (i a x) (f a) ï¼âŸ¨ II (i x a) âŸ©
      f x                       âˆ
       where
        II : x ï¼ a â†’ transport Y (i a x) (f a) ï¼ f x
        II refl =
         transport Y (i a a) (f a) ï¼âŸ¨ transport-lemma i âŸ©
         f a                       âˆ

   Îµ : Î -proj âˆ˜ Î -projâ»Â¹ i âˆ¼ id
   Îµ y =
    (Î -proj âˆ˜ Î -projâ»Â¹ i) y ï¼âŸ¨ refl âŸ©
    transport Y (i a a) y   ï¼âŸ¨ transport-lemma i âŸ©
    y                       âˆ

 prop-indexed-product : funext ğ“¤ ğ“¥
                      â†’ is-prop X
                      â†’ Î  Y â‰ƒ Y a
 prop-indexed-product fe i = Î -proj , Î -proj-is-equiv fe i

empty-indexed-product-is-ğŸ™ : funext ğ“¤ ğ“¥
                           â†’ (X â†’ ğŸ˜ {ğ“¦})
                           â†’ Î  Y â‰ƒ ğŸ™ {ğ“£}
empty-indexed-product-is-ğŸ™ {ğ“¦} {ğ“£} fe v = qinveq unique-to-ğŸ™ (g , Î· , Îµ)
 where
  g : ğŸ™ â†’ Î  Y
  g â‹† x = unique-from-ğŸ˜ {ğ“¥} {ğ“¦} (v x)

  Î· : (f : Î  Y) â†’ g â‹† ï¼ f
  Î· f = dfunext fe I
   where
    I : (x : X) â†’ g (unique-to-ğŸ™ f) x ï¼ f x
    I x = unique-from-ğŸ˜ (v x)

  Îµ : (u : ğŸ™) â†’ â‹† ï¼ u
  Îµ â‹† = refl

\end{code}

Added 18th December 2017.

\begin{code}

module _ (a : X) where

 Î£-in : Y a â†’ Î£ Y
 Î£-in y = (a , y)

 Î£-inâ»Â¹ : is-prop X â†’ Î£ Y â†’ Y a
 Î£-inâ»Â¹ i (x , y) = transport Y (i x a) y

 Î£-in-is-equiv : is-prop X â†’ is-equiv Î£-in
 Î£-in-is-equiv i = qinvs-are-equivs Î£-in (Î£-inâ»Â¹ i , Î· , Îµ)
  where
   Î· : (y : Y a) â†’ Î£-inâ»Â¹ i (Î£-in y) ï¼ y
   Î· y =
    Î£-inâ»Â¹ i (Î£-in y)     ï¼âŸ¨ refl âŸ©
    transport Y (i a a) y ï¼âŸ¨ transport-lemma i âŸ©
    y                     âˆ

   Îµ : (Ïƒ : Î£ Y) â†’ Î£-in (Î£-inâ»Â¹ i Ïƒ) ï¼ Ïƒ
   Îµ (x , y) =
    Î£-in (Î£-inâ»Â¹ i (x , y))     ï¼âŸ¨ refl âŸ©
    (a , transport Y (i x a) y) ï¼âŸ¨ to-Î£-ï¼ (i a x , I (i x a)) âŸ©
    (x , y)                     âˆ
     where
      I : x ï¼ a â†’ transport Y (i a x) (transport Y (i x a) y) ï¼ y
      I refl =
       transport Y (i a a) (transport Y (i a a) y) ï¼âŸ¨ transport-lemma i âŸ©
       transport Y (i a a) y                       ï¼âŸ¨ transport-lemma i âŸ©
       y                                           âˆ

 prop-indexed-sum : is-prop X â†’ Î£ Y â‰ƒ Y a
 prop-indexed-sum i = â‰ƒ-sym (Î£-in , Î£-in-is-equiv i)

empty-indexed-sum-is-ğŸ˜ : (X â†’ ğŸ˜ {ğ“¦}) â†’ Î£ Y â‰ƒ (ğŸ˜ {ğ“£})
empty-indexed-sum-is-ğŸ˜ {ğ“¦} {ğ“£} Ï† = qinveq f (g , Î· , Îµ)
 where
  f : Î£ Y â†’ ğŸ˜
  f (x , y) = ğŸ˜-elim (Ï† x)

  g : ğŸ˜ â†’ Î£ Y
  g = unique-from-ğŸ˜

  Îµ : (x : ğŸ˜) â†’ f (g x) ï¼ x
  Îµ = ğŸ˜-induction

  Î· : (Ïƒ : Î£ Y) â†’ g (f Ïƒ) ï¼ Ïƒ
  Î· (x , y) = ğŸ˜-elim (Ï† x)

\end{code}
