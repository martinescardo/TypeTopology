\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt
open import UF-PropTrunc

module OrderedIntervalObject (fe : FunExt) (pt : propositional-truncations-exist) where

open import Escardo-Simpson-LICS2001 fe
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import DiscreteAndSeparated hiding (_â™¯_)
open import TotallySeparated

\end{code}


\begin{code}

record is-ordered (io : Interval-object ğ“¤) : ğ“¤â‚ âŠ” ğ“¤ Ì‡ where

\end{code}


\begin{code}

  open Interval-object io
  
  âŠ•-commutative : commutative _âŠ•_
  âŠ•-commutative = prâ‚ (prâ‚‚ (prâ‚‚ mpaa))

  M : (â„• â†’ ğ•€) â†’ ğ•€
  M = prâ‚ (ia)

\end{code}


\begin{code}
  
  field
    _<_ : ğ•€ â†’ ğ•€ â†’ ğ“¤â‚€ Ì‡
    <-prop-valued : {x y : ğ•€} â†’ is-prop (x < y)
  
  _â‰®_ : ğ•€ â†’ ğ•€ â†’ ğ“¤â‚€ Ì‡
  x â‰® y = Â¬ (x < y)
  
  field
    u<v : u < v
    <-âŠ•áµ£    : {x y z : ğ•€} â†’ y < z â†’ (x âŠ• y) < (x âŠ• z)
    <-âŠ•áµ£'   : {x y z : ğ•€} â†’ (x âŠ• y) < (x âŠ• z) â†’ y < z
    <-âŠ•â‚˜    : {x : ğ•€} {a : â„• â†’ ğ•€} â†’ ((n : â„•) â†’ x < a n) â†’ x < M a
    <-âŠ•â‚˜'   : {x : ğ•€} {a : â„• â†’ ğ•€} â†’ ((n : â„•) â†’ a n < x) â†’ M a < x

  field
    <-irreflexive : {x     : ğ•€} â†’ x â‰® x
    <-asymmetric  : {x y   : ğ•€} â†’ x < y â†’ y â‰® x
    <-transitive  : {x y z : ğ•€} â†’ x < y â†’ y < z â†’ x < z
    <-connected   : {x y   : ğ•€} â†’ x â‰® y â†’ y â‰® x â†’ x â‰¡ y
    <-decidable   : {x y   : ğ•€} â†’ (x < y) + (x â‰® y)
    <-comparison  : {x y z : ğ•€} â†’ x < z â†’ (x < y) + (y < z)


  <-âŠ•â‚— : {x y z : ğ•€} â†’ x < z â†’ (x âŠ• y) < (z âŠ• y)
  <-âŠ•â‚— {x} {y} {z} x<z = transport (_< (z âŠ• y)) âŠ•-commutative
                          (transport ((y âŠ• x) <_) âŠ•-commutative (<-âŠ•áµ£ x<z))
                          
  <-âŠ•â‚‚ : {x y z w : ğ•€} â†’ x < z â†’ y < w â†’ (x âŠ• y) < (z âŠ• w)
  <-âŠ•â‚‚ {x} {y} {z} {w} x<z y<w = <-transitive (<-âŠ•áµ£ y<w) (<-âŠ•â‚— x<z)

  <-âŠ•â‚—' : {x y z : ğ•€} â†’ (x âŠ• y) < (z âŠ• y) â†’ x < z
  <-âŠ•â‚—' {x} {y} {z} xâŠ•y<zâŠ•y = <-âŠ•áµ£' (transport (_< (y âŠ• z)) âŠ•-commutative
                                    (transport ((x âŠ• y) <_) âŠ•-commutative xâŠ•y<zâŠ•y))

\end{code}


\begin{code}

  _â‰¤_  : ğ•€ â†’ ğ•€ â†’ ğ“¤â‚€ Ì‡  
  x â‰¤  y = y â‰® x

  â‰¤-trichotomous : {x y : ğ•€} â†’ x < y + y â‰¤ x
  â‰¤-trichotomous = <-decidable

  â‰¤-reflexive : {x : ğ•€} â†’ x â‰¤ x
  â‰¤-reflexive = <-irreflexive

  _â‰¤'_ : ğ•€ â†’ ğ•€ â†’ ğ“¤ Ì‡
  x â‰¤' y = {z : ğ•€} â†’ z < x â†’ z < y

  _â‰¤''_ : ğ•€ â†’ ğ•€ â†’ ğ“¤ Ì‡
  x â‰¤'' y = {z : ğ•€} â†’ y < z â†’ x < z

  â‰¤â†’â‰¤' : {x y : ğ•€} â†’ x â‰¤ y â†’ x â‰¤' y
  â‰¤â†’â‰¤' xâ‰¤y z<x = Cases (<-comparison z<x) id (Î» y<x â†’ ğŸ˜-elim (xâ‰¤y y<x))

  â‰¤'â†’â‰¤ : {x y : ğ•€} â†’ x â‰¤' y â†’ x â‰¤ y
  â‰¤'â†’â‰¤ xâ‰¤'y y<x = <-irreflexive (xâ‰¤'y y<x)

  â‰¤â†’â‰¤'' : {x y : ğ•€} â†’ x â‰¤ y â†’ x â‰¤'' y
  â‰¤â†’â‰¤'' xâ‰¤y y<z = Cases (<-comparison y<z) (Î» y<x â†’ ğŸ˜-elim (xâ‰¤y y<x)) id

  â‰¤''â†’â‰¤ : {x y : ğ•€} â†’ x â‰¤'' y â†’ x â‰¤ y
  â‰¤''â†’â‰¤ xâ‰¤''y y<x = <-irreflexive (xâ‰¤''y y<x)

  â‰¤'â†’â‰¤'' : {x y : ğ•€} â†’ x â‰¤' y â†’ x â‰¤'' y
  â‰¤'â†’â‰¤'' = â‰¤â†’â‰¤'' âˆ˜ â‰¤'â†’â‰¤

  â‰¤''â†’â‰¤' : {x y : ğ•€} â†’ x â‰¤'' y â†’ x â‰¤' y
  â‰¤''â†’â‰¤' = â‰¤â†’â‰¤' âˆ˜ â‰¤''â†’â‰¤

  â‰¤-âŠ•áµ£ : {x y z : ğ•€} â†’ y â‰¤ z â†’ (x âŠ• y) â‰¤ (x âŠ• z)
  â‰¤-âŠ•áµ£ yâ‰¤z xâŠ•z<xâŠ•y = yâ‰¤z (<-âŠ•áµ£' xâŠ•z<xâŠ•y)

  â‰¤-âŠ•â‚— : {x y z : ğ•€} â†’ x â‰¤ z â†’ (x âŠ• y) â‰¤ (z âŠ• y)
  â‰¤-âŠ•â‚— yâ‰¤z xâŠ•z<xâŠ•y = yâ‰¤z (<-âŠ•â‚—' xâŠ•z<xâŠ•y)

  â‰¤-trans : {x y z : ğ•€} â†’ x â‰¤ y â†’ y â‰¤ z â†’ x â‰¤ z
  â‰¤-trans xâ‰¤y yâ‰¤z z<x = xâ‰¤y (Cases (<-comparison z<x) (Î» z<y â†’ ğŸ˜-elim (yâ‰¤z z<y)) id)

  <â‰¤-trans : {x y z : ğ•€} â†’ x < y â†’ y â‰¤ z â†’ x < z
  <â‰¤-trans x<y yâ‰¤z = Cases (<-comparison x<y) id (Î» z<y â†’ ğŸ˜-elim (yâ‰¤z z<y))

  â‰¤<-trans : {x y z : ğ•€} â†’ x â‰¤ y â†’ y < z â†’ x < z
  â‰¤<-trans xâ‰¤y y<z = Cases (<-comparison y<z) (Î» y<x â†’ ğŸ˜-elim (xâ‰¤y y<x)) id

  â‰¤-âŠ•â‚‚ : {x y z w : ğ•€} â†’ x â‰¤ z â†’ y â‰¤ w â†’ (x âŠ• y) â‰¤ (z âŠ• w)
  â‰¤-âŠ•â‚‚ {x} {y} {z} {w} xâ‰¤z yâ‰¤w = â‰¤-trans (â‰¤-âŠ•áµ£ yâ‰¤w) (â‰¤-âŠ•â‚— xâ‰¤z)

  â‰¤-âŠ•â‚˜ : {x : ğ•€} {a : â„• â†’ ğ•€} â†’ ((n : â„•) â†’ x â‰¤ a n) â†’ x â‰¤ M a
  â‰¤-âŠ•â‚˜ {x} {a} f = â‰¤'â†’â‰¤ (Î» z<x â†’ <-âŠ•â‚˜ (Î» n â†’ <â‰¤-trans z<x (f n)))

  â‰¤-âŠ•â‚˜' : {x : ğ•€} {a : â„• â†’ ğ•€} â†’ ((n : â„•) â†’ a n â‰¤ x) â†’ M a â‰¤ x
  â‰¤-âŠ•â‚˜' {x} {a} f = â‰¤''â†’â‰¤ (Î» x<z â†’ <-âŠ•â‚˜' (Î» n â†’ â‰¤<-trans (f n) x<z))

  â‰¤-prop-valued : {x y : ğ•€} â†’ is-prop (x â‰¤ y)
  â‰¤-prop-valued = Â¬-is-prop (fe ğ“¤â‚€ ğ“¤â‚€)


\end{code}


\begin{code}

  open Apartness pt
  open propositional-truncations-exist pt

  _â™¯_ : ğ•€ â†’ ğ•€ â†’ ğ“¤â‚€ Ì‡
  x â™¯ y = (x < y) + (y < x)

  â™¯-prop-valued : is-prop-valued _â™¯_
  â™¯-prop-valued x y = +-is-prop <-prop-valued <-prop-valued <-asymmetric
  
  â™¯-irreflexive : is-irreflexive _â™¯_
  â™¯-irreflexive x xâ™¯x = <-irreflexive (Cases xâ™¯x id id)
  
  â™¯-symmetric : is-symmetric _â™¯_
  â™¯-symmetric x y xâ™¯y = Cases xâ™¯y inr inl

  â™¯-cotransitive' : {x y z : ğ•€} â†’ x â™¯ y â†’ (x â™¯ z) + (y â™¯ z)
  â™¯-cotransitive' xâ™¯y
    = Cases xâ™¯y (Î» x<y â†’ Cases (<-comparison x<y)
                  (Î» x<z â†’ inl (inl x<z))
                  (Î» z<y â†’ inr (inr z<y)))
                (Î» y<x â†’ Cases (<-comparison y<x)
                  (Î» y<z â†’ inr (inl y<z))
                  (Î» z<y â†’ inl (inr z<y)))

  â™¯-cotransitive : is-cotransitive _â™¯_
  â™¯-cotransitive x y z xâ™¯y = âˆ£ â™¯-cotransitive' xâ™¯y âˆ£

  â™¯-apartness : is-apartness _â™¯_
  â™¯-apartness = â™¯-prop-valued , â™¯-irreflexive , â™¯-symmetric , â™¯-cotransitive

  â™¯-tight : is-tight _â™¯_
  â™¯-tight x y Â¬xâ™¯y = <-connected (Î» x<y â†’ ğŸ˜-elim (Â¬xâ™¯y (inl x<y)))
                                 (Î» y<x â†’ ğŸ˜-elim (Â¬xâ™¯y (inr y<x)))

  ğ•€-is-separated : is-separated ğ•€
  ğ•€-is-separated = tight-is-separated _â™¯_ â™¯-apartness â™¯-tight

\end{code}


\begin{code}

  <-irreflexive' : {x y : ğ•€} â†’ x < y â†’ x â‰¢ y
  <-irreflexive' {x} {.x} x<x refl = <-irreflexive x<x
