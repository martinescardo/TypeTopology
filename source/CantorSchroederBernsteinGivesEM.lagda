Martin Escardo, 22nd January 2020

This is a univalent-foundations version of Pierre Pradic and Chad
E. Brown's argument that Cantor-Schroeder-Bernstein implies excluded
middle in constructive set theory (https://arxiv.org/abs/1904.09193).

Their proof, reproduced below, uses the compactness (also know as the
searchability or omniscience) of â„•âˆ.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt

module CantorSchroederBernsteinGivesEM where

open import UF-ExcludedMiddle
open import UF-Subsingletons
open import UF-Equiv
open import UF-Embeddings
open import UF-Retracts
open import GenericConvergentSequence
open import DecidableAndDetachable
open import Plus-Properties
open import CompactTypes
open import ConvergentSequenceCompact

CantorSchroederBernstein : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
CantorSchroederBernstein X Y = (X â†ª Y) â†’ (Y â†ª X) â†’ X â‰ƒ Y

\end{code}

The following is Lemma 7 of the above reference, using retractions
rather than surjections, for simplicity:

\begin{code}

csb-lemma : {X : ğ“¤ Ì‡ } {A : ğ“¥ Ì‡ } â†’ Compact X â†’ retract (A + X) of X â†’ decidable A
csb-lemma {ğ“¤} {ğ“¥} {X} {A} c (r , s , Î·) = Î³ e
 where
  P : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x = Î£ \(a : A) â†’ r x â‰¡ inl a
  d : detachable P
  d x = equality-cases (r x)
         (Î» (a : A) (p : r x â‰¡ inl a) â†’ inl (a , p))
         (Î» (y : X) (q : r x â‰¡ inr y) â†’ inr (Î» {(a , p) â†’ +disjoint (inl a â‰¡âŸ¨ p â»Â¹ âŸ©
                                                                     r x   â‰¡âŸ¨ q    âŸ©
                                                                     inr y âˆ)}))
  e : decidable (Î£ (\(x : X) â†’ P x))
  e = c P d
  f : A â†’ Î£ \(x : X) â†’ P x
  f a = s (inl a) , a , Î· (inl a)
  Î³ : decidable (Î£ (\(x : X) â†’ P x)) â†’ decidable A
  Î³ (inl (x , a , p)) = inl a
  Î³ (inr Ï†)           = inr (contrapositive f Ï†)

\end{code}

Function extensionality is used twice in the following, once to know
that â„•âˆ is a set, and once to know that it is compact.

\begin{code}

CantorSchroederBernstein-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
                                  â†’ (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ CantorSchroederBernstein â„•âˆ (P + â„•âˆ) â†’ P + Â¬ P
CantorSchroederBernstein-gives-EM fe P i csb = Î³
 where
  f : â„•âˆ â†’ P + â„•âˆ
  f = inr

  j : is-embedding f
  j = inr-is-embedding P â„•âˆ

  z : P â†’ â„•âˆ
  z _ = Zero

  g : P + â„•âˆ â†’ â„•âˆ
  g = cases z Succ

  a : is-embedding z
  a = maps-of-props-into-sets-are-embeddings (Î» p â†’ Zero) i (â„•âˆ-is-set fe)

  b : is-embedding Succ
  b = lc-maps-into-sets-are-embeddings Succ Succ-lc (â„•âˆ-is-set fe)

  c : disjoint-images z Succ
  c = Î» (p : P) (x : â„•âˆ) (q : Zero â‰¡ Succ x) â†’ Zero-not-Succ q

  k : is-embedding g
  k = disjoint-cases-embedding z Succ a b c

  e : â„•âˆ â‰ƒ P + â„•âˆ
  e = csb (f , j) (g , k)

  Ï : retract (P + â„•âˆ) of â„•âˆ
  Ï = equiv-retract-r e

  Î³ : P + Â¬ P
  Î³ = csb-lemma (â„•âˆ-Compact fe) Ï

\end{code}

The classical Cantor-Schroeder-Bernstein theorem, which assumes
excluded middle for its proof, works for sets, because the proofs use
decidability of equality, and, under excluded middle, the types that
have decidable equality are precisely the sets, by Hedberg's
Theorem. Hence the following is the appropriate formulation of
Cantor-Schroeder-Bernstein for univalent foundations:

\begin{code}

CSB : ğ“¤Ï‰
CSB = (ğ“¤ ğ“¥ : Universe) (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) â†’ is-set X â†’ is-set Y â†’ CantorSchroederBernstein X Y

CSB-gives-excluded-middle : funext ğ“¤â‚€ ğ“¤â‚€ â†’ CSB â†’ (ğ“¤ : Universe) â†’ EM ğ“¤
CSB-gives-excluded-middle fe csb ğ“¤ P i = Î³
 where
  a : CantorSchroederBernstein â„•âˆ (P + â„•âˆ)
  a = csb ğ“¤â‚€ ğ“¤ â„•âˆ (P + â„•âˆ) (â„•âˆ-is-set fe) (+-is-set P â„•âˆ (props-are-sets i) (â„•âˆ-is-set fe))
  Î³ : P + Â¬ P
  Î³ = CantorSchroederBernstein-gives-EM fe P i a

\end{code}

Remark. If instead of requiring that we have a designated equivalence,
we required that there is an an unspecified equivalence in the
formulation of Cantor-Schroeder-Bernstein, we still get excluded
middle, because P + Â¬P is a proposition.
