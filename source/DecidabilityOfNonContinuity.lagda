Martin Escardo, 7 May 2014.

For any function f : â„•âˆ â†’ â„•, it is decidable whether f is non-continuous.

  Î  (f : â„•âˆ â†’ â„•). Â¬ (continuous f) + Â¬Â¬ (continuous f).

Based on the paper

    Mathematical Structures in Computer Science , Volume 25,
    October 2015 , pp. 1578 - 1589
    DOI: https://doi.org/10.1017/S096012951300042X

The title of this paper is a bit misleading. It should have been
called "Decidability of non-continuity".

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-FunExt

module DecidabilityOfNonContinuity (fe : funext ğ“¤â‚€ ğ“¤â‚€) where

open import Two-Properties
open import DiscreteAndSeparated
open import GenericConvergentSequence
open import ADecidableQuantificationOverTheNaturals fe
open import DecidableAndDetachable
open import CanonicalMapNotation

Lemma-3Â·1 : (q : â„•âˆ â†’ â„•âˆ â†’ ğŸš)
          â†’ decidable ((m : â„•) â†’ Â¬ ((n : â„•) â†’ q (Î¹ m) (Î¹ n) â‰¡ â‚))
Lemma-3Â·1 q = claimâ‚„
 where
  A : â„•âˆ â†’ ğ“¤â‚€ Ì‡
  A u = (n : â„•) â†’ q u (Î¹ n) â‰¡ â‚

  claimâ‚€ :  (u : â„•âˆ) â†’ decidable (A u)
  claimâ‚€ u = Theorem-8Â·2 (q u)

  p : â„•âˆ â†’ ğŸš
  p = prâ‚ (indicator claimâ‚€)

  p-spec : (x : â„•âˆ) â†’ (p x â‰¡ â‚€ â†’ A x) Ã— (p x â‰¡ â‚ â†’ Â¬ A x)
  p-spec = prâ‚‚ (indicator claimâ‚€)

  claimâ‚ : decidable ((n : â„•) â†’ p (Î¹ n) â‰¡ â‚)
  claimâ‚ = Theorem-8Â·2 p

  claimâ‚‚ : ((n : â„•) â†’ Â¬ A (Î¹ n)) â†’ (n : â„•) â†’ p (Î¹ n) â‰¡ â‚
  claimâ‚‚ Ï† n = different-from-â‚€-equal-â‚ (Î» v â†’ Ï† n (prâ‚ (p-spec (Î¹ n)) v))

  claimâ‚ƒ : decidable ((n : â„•) â†’ p (Î¹ n) â‰¡ â‚) â†’ decidable ((n : â„•) â†’ Â¬ A (Î¹ n))
  claimâ‚ƒ (inl f) = inl (Î» n â†’ prâ‚‚ (p-spec (Î¹ n)) (f n))
  claimâ‚ƒ (inr u) = inr (contrapositive claimâ‚‚ u)

  claimâ‚„ : decidable ((n : â„•) â†’ Â¬ (A (Î¹ n)))
  claimâ‚„ = claimâ‚ƒ claimâ‚

\end{code}

Omitting the inclusion function, or coercion,

   Î¹ : â„• â†’ â„•âˆ,

a map f : â„•âˆ â†’ â„• is called continuous iff

   âˆƒ m. âˆ€ n â‰¥ m. f n â‰¡ âˆ,

where m and n range over the natural numbers.

The negation of this statement is equivalent to

   âˆ€ m. Â¬ âˆ€ n â‰¥ m. f n â‰¡ âˆ.

We can implement âˆ€ y â‰¥ x. A y as âˆ€ x. A (max x y), so that the
continuity of f amounts to

   âˆƒ m. âˆ€ n. f (max m n) â‰¡ âˆ,

and its negation to

   âˆ€ m. Â¬ âˆ€ n. f (max m n) â‰¡ âˆ.

\begin{code}

non-continuous : (â„•âˆ â†’ â„•) â†’ ğ“¤â‚€ Ì‡
non-continuous f = (m : â„•) â†’ Â¬ ((n : â„•) â†’ f (max (Î¹ m) (Î¹ n)) â‰¡[â„•] f âˆ)

Theorem-3Â·2 : (f : â„•âˆ â†’ â„•) â†’ decidable (non-continuous f)
Theorem-3Â·2 f = Lemma-3Â·1 ((Î» x y â†’ Ï‡â‰¡ (f (max x y)) (f âˆ)))

\end{code}

(Maybe) to be continued (see the paper for the moment).

   * MP gives that continuity and doubly negated continuity agree.

   * WLPO is equivalent to the existence of a non-continuous function â„•âˆ â†’ â„•.

   * Â¬WLPO is equivalent to the doubly negated continuity of all functions â„•âˆ â†’ â„•.

   * If MP and Â¬WLPO then all functions â„•âˆ â†’ â„• are continuous.

For future use:

\begin{code}

continuous : (â„•âˆ â†’ â„•) â†’ ğ“¤â‚€ Ì‡
continuous f = Î£ m ê‰ â„• , ((n : â„•) â†’ f (max (Î¹ m) (Î¹ n)) â‰¡ f âˆ)

\end{code}
