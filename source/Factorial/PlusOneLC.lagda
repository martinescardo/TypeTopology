Martin Escardo, 21 March 2018, 1st December 2019.

We prove the known (constructive) fact that

  X + ğŸ™ â‰ƒ Y + ğŸ™ â†’ X â‰ƒ Y.

The new proof from 1st December 2019 is extracted from the module
UF.Factorial and doesn't use function extensionality. The old proof
from 21 March 2018 is included at the end.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Factorial.PlusOneLC where

open import Factorial.Swap
open import MLTT.Plus-Properties
open import MLTT.Spartan
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Retracts

+ğŸ™-cancellable : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ }
               â†’ (X + ğŸ™ {ğ“¦} â‰ƒ Y + ğŸ™ {ğ“£})
               â†’ X â‰ƒ Y
+ğŸ™-cancellable {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} (Ï† , i) = qinveq f' (g' , Î·' , Îµ')
 where
  zâ‚€ : X + ğŸ™
  zâ‚€ = inr â‹†

  tâ‚€ : Y + ğŸ™
  tâ‚€ = inr â‹†

  j : is-isolated zâ‚€
  j = new-point-is-isolated

  k : is-isolated (Ï† zâ‚€)
  k = equivs-preserve-isolatedness Ï† i zâ‚€ j

  l : is-isolated tâ‚€
  l = new-point-is-isolated

  s : Y + ğŸ™ â†’ Y + ğŸ™
  s = swap (Ï† zâ‚€) tâ‚€ k l

  f : X + ğŸ™ â†’ Y + ğŸ™
  f = s âˆ˜ Ï†

  p : f zâ‚€ ï¼ tâ‚€
  p = swap-equationâ‚€ (Ï† zâ‚€) tâ‚€ k l

  g : Y + ğŸ™ â†’ X + ğŸ™
  g = inverse Ï† i âˆ˜ s

  h : s âˆ˜ s âˆ¼ id
  h = swap-involutive (Ï† zâ‚€) tâ‚€ k l

  Î· : g âˆ˜ f âˆ¼ id
  Î· z = inverse Ï† i (s (s (Ï† z))) ï¼âŸ¨ ap (inverse Ï† i) (h (Ï† z)) âŸ©
        inverse Ï† i (Ï† z)         ï¼âŸ¨ inverses-are-retractions Ï† i z âŸ©
        z                         âˆ

  Îµ : f âˆ˜ g âˆ¼ id
  Îµ t = s (Ï† (inverse Ï† i (s t))) ï¼âŸ¨ ap s (inverses-are-sections Ï† i (s t)) âŸ©
        s (s t)                   ï¼âŸ¨ h t âŸ©
        t                         âˆ

  f' : X â†’ Y
  f' x = prâ‚ (inl-preservation f p (sections-are-lc f (g , Î·)) x)

  a : (x : X) â†’ f (inl x) ï¼ inl (f' x)
  a x = prâ‚‚ (inl-preservation f p (sections-are-lc f (g , Î·)) x)

  q = g tâ‚€     ï¼âŸ¨ ap g (p â»Â¹) âŸ©
      g (f zâ‚€) ï¼âŸ¨ Î· zâ‚€ âŸ©
      inr â‹†    âˆ

  g' : Y â†’ X
  g' y = prâ‚ (inl-preservation g q (sections-are-lc g (f , Îµ)) y)

  b : (y : Y) â†’ g (inl y) ï¼ inl (g' y)
  b y = prâ‚‚ (inl-preservation g q (sections-are-lc g (f , Îµ)) y)

  Î·' : (x : X) â†’ g' (f' x) ï¼ x
  Î·' x = inl-lc (inl (g' (f' x)) ï¼âŸ¨ (b (f' x))â»Â¹ âŸ©
                 g (inl (f' x))  ï¼âŸ¨ (ap g (a x))â»Â¹ âŸ©
                 g (f (inl x))   ï¼âŸ¨ Î· (inl x) âŸ©
                 inl x           âˆ)

  Îµ' : (y : Y) â†’ f' (g' y) ï¼ y
  Îµ' y = inl-lc (inl (f' (g' y)) ï¼âŸ¨ (a (g' y))â»Â¹ âŸ©
                 f (inl (g' y))  ï¼âŸ¨ (ap f (b y))â»Â¹ âŸ©
                 f (g (inl y))   ï¼âŸ¨ Îµ (inl y) âŸ©
                 inl y           âˆ)

\end{code}

The old version from 21 March 2018, which uses function
extensionality:

\begin{code}

_âˆ–_ : (X : ğ“¤ Ì‡ ) (a : X) â†’ ğ“¤ Ì‡
X âˆ– a = Î£ x ê‰ X , x â‰  a

open import UF.FunExt

module _ (fe : FunExt) where

 open import UF.Base
 open import UF.Subsingletons-FunExt

 add-and-remove-point : {X : ğ“¤ Ì‡ } â†’  X â‰ƒ (X + ğŸ™) âˆ– (inr â‹†)
 add-and-remove-point {ğ“¤} {X} = qinveq f (g , Îµ , Î·)
  where
   f : X â†’ (X + ğŸ™ {ğ“¤}) âˆ– inr â‹†
   f x = (inl x , +disjoint)

   g : (X + ğŸ™) âˆ– inr â‹† â†’ X
   g (inl x , u) = x
   g (inr â‹† , u) = ğŸ˜-elim (u refl)

   Î· : f âˆ˜ g âˆ¼ id
   Î· (inl x , u) = to-Î£-ï¼' (negations-are-props (fe ğ“¤ ğ“¤â‚€) _ _)
   Î· (inr â‹† , u) = ğŸ˜-elim (u refl)

   Îµ : g âˆ˜ f âˆ¼ id
   Îµ x = refl

 remove-points : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y)
               â†’ qinv f
               â†’ (a : X) â†’ X âˆ– a â‰ƒ Y âˆ– (f a)
 remove-points {ğ“¤} {ğ“¥} {X} {Y} f (g , Îµ , Î·) a = qinveq f' (g' , Îµ' , Î·')
  where
   f' : X âˆ– a â†’ Y âˆ– (f a)
   f' (x , u) = (f x , Î» (p : f x ï¼ f a) â†’ u ((Îµ x)â»Â¹ âˆ™ ap g p âˆ™ Îµ a))

   g' : Y âˆ– (f a) â†’ X âˆ– a
   g' (y , v) = (g y , Î» (p : g y ï¼ a) â†’ v ((Î· y) â»Â¹ âˆ™ ap f p))

   Îµ' : g' âˆ˜ f' âˆ¼ id
   Îµ' (x , _) = to-Î£-ï¼ (Îµ x , negations-are-props (fe ğ“¤ ğ“¤â‚€) _ _)

   Î·' : f' âˆ˜ g' âˆ¼ id
   Î·' (y , _) = to-Î£-ï¼ (Î· y , negations-are-props (fe ğ“¥ ğ“¤â‚€) _ _)

 add-one-and-remove-isolated-point : {Y : ğ“¥ Ì‡ } (z : Y + ğŸ™)
                                   â†’ is-isolated z
                                   â†’ ((Y + ğŸ™) âˆ– z) â‰ƒ Y
 add-one-and-remove-isolated-point {ğ“¥} {Y} (inl b) i = qinveq f (g , Îµ , Î·)
  where
   f : (Y + ğŸ™) âˆ– (inl b) â†’ Y
   f (inl y , u) = y
   f (inr â‹† , u) = b

   g' : (y : Y) â†’ is-decidable (inl b ï¼ inl y) â†’ (Y + ğŸ™) âˆ– (inl b)
   g' y (inl p) = (inr â‹† , +disjoint')
   g' y (inr u) = (inl y , contrapositive (_â»Â¹) u)

   g : Y â†’ (Y + ğŸ™) âˆ– (inl b)
   g y = g' y (i (inl y))

   Îµ : g âˆ˜ f âˆ¼ id
   Îµ (inl y , u) = to-Î£-ï¼ (p , negations-are-props (fe ğ“¥ ğ“¤â‚€) _ _)
    where
     Ï† : (p : inl b ï¼ inl y) (q : i (inl y) ï¼ inl p) â†’ i (inl y) ï¼ inr (â‰ -sym u)
     Ï† p q = ğŸ˜-elim (u (p â»Â¹))
     Ïˆ : (v : inl b â‰  inl y) (q : i (inl y) ï¼ inr v) â†’ i (inl y) ï¼ inr (â‰ -sym u)
     Ïˆ v q = q âˆ™ ap inr (negations-are-props (fe ğ“¥ ğ“¤â‚€) _ _)
     h : i (inl y) ï¼ inr (â‰ -sym u)
     h = equality-cases (i (inl y)) Ï† Ïˆ
     p : prâ‚ (g' y (i (inl y))) ï¼ inl y
     p = ap (prâ‚ âˆ˜ (g' y)) h
   Îµ (inr â‹† , u) = equality-cases (i (inl b)) Ï† Ïˆ
    where
     Ï† : (p : inl b ï¼ inl b) â†’ i (inl b) ï¼ inl p â†’ g (f (inr â‹† , u)) ï¼ (inr â‹† , u)
     Ï† p q = r âˆ™ to-Î£-ï¼ (refl , negations-are-props (fe ğ“¥ ğ“¤â‚€) _ _)
      where
       r : g b ï¼ (inr â‹† , +disjoint')
       r = ap (g' b) q
     Ïˆ : (v : inl b â‰  inl b) â†’ i (inl b) ï¼ inr v â†’ g (f (inr â‹† , u)) ï¼ (inr â‹† , u)
     Ïˆ v q = ğŸ˜-elim (v refl)

   Î· : f âˆ˜ g âˆ¼ id
   Î· y = equality-cases (i (inl y)) Ï† Ïˆ
    where
     Ï† : (p : inl b ï¼ inl y) â†’ i (inl y) ï¼ inl p â†’ f (g' y (i (inl y))) ï¼ y
     Ï† p q = ap (Î» - â†’ f (g' y -)) q âˆ™ inl-lc p
     Ïˆ : (u : inl b â‰  inl y) â†’ i (inl y) ï¼ inr u â†’ f (g' y (i (inl y))) ï¼ y
     Ïˆ _ = ap ((Î» d â†’ f (g' y d)))

 add-one-and-remove-isolated-point {ğ“¥} {Y} (inr â‹†) _ = â‰ƒ-sym add-and-remove-point

 +ğŸ™-cancellable' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X + ğŸ™) â‰ƒ (Y + ğŸ™) â†’ X â‰ƒ Y
 +ğŸ™-cancellable' {ğ“¤} {ğ“¥} {X} {Y} (Ï† , e) =
    X                  â‰ƒâŸ¨ add-and-remove-point âŸ©
   (X + ğŸ™) âˆ– inr â‹†     â‰ƒâŸ¨ remove-points Ï† (equivs-are-qinvs Ï† e) (inr â‹†) âŸ©
   (Y + ğŸ™) âˆ– Ï† (inr â‹†) â‰ƒâŸ¨ add-one-and-remove-isolated-point
                           (Ï† (inr â‹†))
                           (equivs-preserve-isolatedness Ï† e (inr â‹†)
                             new-point-is-isolated) âŸ©
    Y                  â– 

\end{code}

Added 16th April 2021.

\begin{code}

open import UF.Subsingletons-FunExt

remove-and-add-isolated-point : funext ğ“¤ ğ“¤â‚€
                              â†’ {X : ğ“¤ Ì‡ } (xâ‚€ : X)
                              â†’ is-isolated xâ‚€
                              â†’ X â‰ƒ (X âˆ– xâ‚€ + ğŸ™ {ğ“¥})
remove-and-add-isolated-point fe {X} xâ‚€ Î¹ = qinveq f (g , Îµ , Î·)
 where
  Ï• : (x : X) â†’ is-decidable (xâ‚€ ï¼ x) â†’ X âˆ– xâ‚€ + ğŸ™
  Ï• x (inl p) = inr â‹†
  Ï• x (inr Î½) = inl (x , (Î» (p : x ï¼ xâ‚€) â†’ Î½ (p â»Â¹)))

  f : X â†’ X âˆ– xâ‚€ + ğŸ™
  f x = Ï• x (Î¹ x)

  g : X âˆ– xâ‚€ + ğŸ™ â†’ X
  g (inl (x , _)) = x
  g (inr â‹†) = xâ‚€

  Î·' : (y : X âˆ– xâ‚€ + ğŸ™) (d : is-decidable (xâ‚€ ï¼ g y)) â†’ Ï• (g y) d ï¼ y
  Î·' (inl (x , Î½)) (inl q) = ğŸ˜-elim (Î½ (q â»Â¹))
  Î·' (inl (x , Î½)) (inr _) = ap (Î» - â†’ inl (x , -)) (negations-are-props fe _ _)
  Î·' (inr â‹†) (inl p)       = refl
  Î·' (inr â‹†) (inr Î½)       = ğŸ˜-elim (Î½ refl)

  Î· : f âˆ˜ g âˆ¼ id
  Î· y = Î·' y (Î¹ (g y))

  Îµ' : (x : X) (d : is-decidable (xâ‚€ ï¼ x)) â†’ g (Ï• x d) ï¼ x
  Îµ' x (inl p) = p
  Îµ' x (inr Î½) = refl

  Îµ : g âˆ˜ f âˆ¼ id
  Îµ x = Îµ' x (Î¹ x)

\end{code}
