Andrew Sneap - 12/05/2022

This file defines division of natural numbers as a Œ£ type. Many common
proofs of properties of division are also provided.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT renaming (_+_ to _‚àî_) 

open import NaturalsAddition
open import NaturalsMultiplication
open import NaturalNumbers-Properties
open import NaturalsOrder
open import OrderNotation
open import UF-Base
open import UF-Miscelanea
open import UF-Subsingletons

module NaturalsDivision where

\end{code}

First, we have the definition of division. Division can also be
defined inductively, but as with most definitions I have instead
chosen to express division as a Œ£ type.

\begin{code}

_‚à£_ : ‚Ñï ‚Üí ‚Ñï ‚Üí ùì§‚ÇÄ Ãá
x ‚à£ y = Œ£ a Íûâ ‚Ñï , (x * a ‚â° y)

\end{code}

Notice that we cannot prove that (x y : ‚Ñï) ‚Üí is-prop (x ‚à£ y).
When x ‚â° 0, and y ‚â° 0, we can choose any factor a and the identity type holds.

On the other hand, for values x > 0, it is a proposition that x | y.
This is proved using the cancellative property of multiplication of
factors greater than 0.

\begin{code}

_‚à£_-is-prop : (x y : ‚Ñï) ‚Üí is-prop (succ x ‚à£ y)
_‚à£_-is-prop x y (a , p) (b , p') = to-subtype-‚â° (Œª _ ‚Üí ‚Ñï-is-set) II
 where
  I : succ x * a ‚â° succ x * b
  I = p ‚àô p' ‚Åª¬π
  
  II : a ‚â° b
  II = mult-left-cancellable a b x I

\end{code}

Clearly, 1 divides anything, which is easily proved since 1 is the
multiplicative identity of naturals.

0 does not divide any value greater than 0. If this was the case, then
we would have that 0 * a ‚â° 0 ‚â° succ x, which is a contradiction.

Also, any number divides itself, and divides zero.

\begin{code}

1-divides-all : (x : ‚Ñï) ‚Üí 1 ‚à£ x
1-divides-all x = x , mult-left-id x

zero-does-not-divide-positive : (x : ‚Ñï) ‚Üí ¬¨(0 ‚à£ succ x)
zero-does-not-divide-positive x (a , p) = positive-not-zero x (p ‚Åª¬π ‚àô zero-left-base a)

‚à£-refl : {x : ‚Ñï} ‚Üí x ‚à£ x
‚à£-refl = 1 , refl

everything-divides-zero : {x : ‚Ñï} ‚Üí x ‚à£ 0
everything-divides-zero = 0 , refl

\end{code}

For natural numbers, division has the property that if x | y and
y | x, then x ‚â° y.  This property is used to prove that if the
numerator a of a rational is 0, then the rational is 0.  In order to
prove this, we first need two lemmas.

The first is that if x < y, and x < z, then x < y * z.
This follows as a corollary of <-+.

\begin{code}

‚à£-anti-lemma : (x y z : ‚Ñï) ‚Üí x < y ‚Üí x < z ‚Üí x < y * z
‚à£-anti-lemma x 0        z        l‚ÇÅ l‚ÇÇ = ùüò-elim (zero-least' z l‚ÇÅ)
‚à£-anti-lemma x (succ y) 0        l‚ÇÅ l‚ÇÇ = ùüò-elim (zero-least' y l‚ÇÇ)
‚à£-anti-lemma x (succ y) (succ z) l‚ÇÅ l‚ÇÇ = <-+ x (succ y) (succ y * z) l‚ÇÅ

\end{code}

The second is that if the product of two naturals is 1, then the left
argument is 1. Of course, both arguments are 1 by commutativity of
multiplication.

The proof is by case analysis. When x ‚â° 1, we are done.
If x ‚â° 0, then x * y ‚â° 0 ‚â° 1, which is a contradiction.
If x > 1, the consider y. In each case, we find a contradiction.

\begin{code}

product-one-gives-one : (x y : ‚Ñï) ‚Üí x * y ‚â° 1 ‚Üí x ‚â° 1
product-one-gives-one 0               y               e = ùüò-elim (zero-not-positive 0 (zero-left-base y ‚Åª¬π ‚àô e))
product-one-gives-one 1               y               e = refl
product-one-gives-one (succ (succ x)) 0               e = ùüò-elim (zero-not-positive 0 e)
product-one-gives-one (succ (succ x)) 1               e = ùüò-elim (zero-not-positive x (succ-lc (e ‚Åª¬π)))
product-one-gives-one (succ (succ x)) (succ (succ y)) e = ùüò-elim (less-than-not-equal _ _ l (e ‚Åª¬π))
 where
  l : 1 < succ (succ x) * succ (succ y)
  l = ‚à£-anti-lemma 1 (succ (succ x)) (succ (succ y)) (zero-least (succ x)) (zero-least (succ y))

\end{code}

And we can finally prove that division is anti-symmetric property,
using the two lemmas, and case analysis on y. In the case y ‚â° 0, we
have 0 * b ‚â° x, and hence x ‚â° y ‚â° 0.

In the case y > 0, we can use the fact that multiplication is
cancellable, and the hypothesis x * a ‚â° succ y, succ y * b ‚â° x to
prove that b ‚â° 1, and conclude that division is anti-symmetric.


\begin{code}

‚à£-anti : (x y : ‚Ñï) ‚Üí x ‚à£ y ‚Üí y ‚à£ x ‚Üí x ‚â° y
‚à£-anti x 0        (a , e‚ÇÅ) (b , e‚ÇÇ) = e‚ÇÇ ‚Åª¬π ‚àô zero-left-base b
‚à£-anti x (succ y) (a , e‚ÇÅ) (b , e‚ÇÇ) = e‚ÇÇ ‚Åª¬π ‚àô ap (succ y *_) b-is-1
 where
  I : succ y * (b * a) ‚â° succ y * 1
  I = succ y * (b * a) ‚â°‚ü® mult-associativity (succ y) b a ‚Åª¬π ‚ü©
      succ y * b * a   ‚â°‚ü® ap (_* a) e‚ÇÇ ‚àô e‚ÇÅ                  ‚ü©
      succ y ‚àé

  b*a-is-1 : b * a ‚â° 1
  b*a-is-1 = mult-left-cancellable (b * a) 1 y I

  b-is-1 : b ‚â° 1
  b-is-1 = product-one-gives-one b a b*a-is-1

\end{code}

Division distributes over addition, over multiples, and is
transitive. The proofs are simple and corollaries of the properties of
multiplication.

\begin{code}

‚à£-respects-addition : (x y z : ‚Ñï) ‚Üí x ‚à£ y ‚Üí x ‚à£ z ‚Üí x ‚à£ (y + z)
‚à£-respects-addition x y z (a , p) (b , q) = (a + b , I)
 where
  I : x * (a + b) ‚â° y + z
  I = x * (a + b)   ‚â°‚ü® distributivity-mult-over-addition x a b ‚ü©
      x * a + x * b ‚â°‚ü® ap (_+ x * b) p                    ‚ü©
      y + x * b     ‚â°‚ü® ap (y +_) q                        ‚ü©
      y + z         ‚àé

‚à£-divisor-divides-multiple : (a b k : ‚Ñï) ‚Üí a ‚à£ b ‚Üí a ‚à£ k * b
‚à£-divisor-divides-multiple a b k (x , p) = (x * k) , I
 where
  I : a * (x * k) ‚â° k * b
  I = a * (x * k) ‚â°‚ü® mult-associativity a x k ‚Åª¬π ‚ü©
      a * x * k   ‚â°‚ü® ap (_* k) p                 ‚ü©
      b * k       ‚â°‚ü® mult-commutativity b k ‚ü©
      k * b       ‚àé

‚à£-respects-multiples : (a b c k l : ‚Ñï) ‚Üí a ‚à£ b ‚Üí a ‚à£ c ‚Üí a ‚à£ (k * b + l * c)
‚à£-respects-multiples a b c k l p‚ÇÅ p‚ÇÇ = ‚à£-respects-addition a (k * b) (l * c) I II
 where
  I : a ‚à£ (k * b)
  I = ‚à£-divisor-divides-multiple a b k p‚ÇÅ
  II : a ‚à£ (l * c)
  II = ‚à£-divisor-divides-multiple a c l p‚ÇÇ
                                                                            
‚à£-trans : (a b c : ‚Ñï) ‚Üí a ‚à£ b ‚Üí b ‚à£ c ‚Üí a ‚à£ c
‚à£-trans a b c (x , p) (y , q) = (x * y) , I
 where
  I : a * (x * y) ‚â° c
  I = a * (x * y)  ‚â°‚ü® mult-associativity a x y ‚Åª¬π ‚ü©
      (a * x) * y  ‚â°‚ü® ap ( _* y) p                ‚ü©
      b * y        ‚â°‚ü® q                           ‚ü©
      c            ‚àé

\end{code}

Now we state the division theorem for natural numbers. This states
that for a natural number a and d, there exists a quotient q and
remainder r with a ‚â° q * (d + 1) + r, with the remainder r less than
succ d.

\begin{code}

division-theorem : (a d : ‚Ñï) ‚Üí ùì§‚ÇÄ Ãá
division-theorem a d = Œ£ q Íûâ ‚Ñï , Œ£ r Íûâ ‚Ñï , (a ‚â° q * succ d + r) √ó (r < succ d)

\end{code}

There are many ways to compute division on natural numbers. The chosen
method here (mainly to satisfy the termination checker) is to use
natural induction.

To compute (succ d) | a, we do induction on a.

Base: If a ‚â° 0, then the quotient and remainder are both 0.

Inductive step: Suppose that (succ d) | k, then there exists q , r
such that k = q * succ d + r, and r < succ d.

We want to show that (succ d) | (succ k).
Since r < succ d, we have that either r < d or r ‚â° d.

If r < d, then the quotient remains the same and the remainder
increases by 1. Since r < d, (succ r) < (succ d), and we are done.

If r ‚â° d, then the quotient increases by 1 and the remainder is 0.
Clearly, 0 < succ d.  Proving that succ k ‚â° succ q + succ q * d
follows from the inductive hypothesis and r ‚â° d.

\begin{code}

division : (a d : ‚Ñï) ‚Üí division-theorem a d
division a d = induction base step a
 where
  base : Œ£ q Íûâ ‚Ñï , Œ£ r Íûâ ‚Ñï , (0 ‚â° q * succ d + r) √ó (r < succ d)  
  base = 0 , (0 , (I , II))
   where
    I : 0 ‚â° 0 * succ d + 0
    I = 0         ‚â°‚ü® refl                               ‚ü©
        0 + 0     ‚â°‚ü® ap (0 +_) (zero-left-base d ‚Åª¬π) ‚ü©
        0 + 0 * d ‚àé

    II : 0 < succ d
    II = zero-least d

  step : (k : ‚Ñï)
       ‚Üí Œ£ q Íûâ ‚Ñï , Œ£ r Íûâ ‚Ñï , (k ‚â° q * succ d + r) √ó (r < succ d)
       ‚Üí Œ£ q Íûâ ‚Ñï , Œ£ r Íûâ ‚Ñï , (succ k ‚â° q * succ d + r) √ó (r < succ d)
  step k (q , r , e , l) = helper (<-split r d l)
   where
    helper : (r < d) ‚àî (r ‚â° d) ‚Üí  Œ£ q Íûâ ‚Ñï , Œ£ r Íûâ ‚Ñï , (succ k ‚â° q * succ d + r) √ó (r < succ d)
    helper (inl x) = q , succ r , ap succ e , x
    helper (inr x) = succ q , 0 , I , unique-to-ùüô (0 < succ d)
     where
      I : succ k ‚â° succ q + succ q * d
      I = succ k                        ‚â°‚ü® ap succ e                                           ‚ü©
          succ (q + q * d + r)          ‚â°‚ü® ap succ (ap (q + q * d +_) x)                       ‚ü©
          succ (q + q * d + d)          ‚â°‚ü® ap succ (addition-associativity q (q * d) d)        ‚ü©
          succ (q + (q * d + d))        ‚â°‚ü® succ-left q (q * d + d) ‚Åª¬π                          ‚ü©
          succ q + (q * d + d)          ‚â°‚ü® ap (succ q +_) (ap (_+ d) (mult-commutativity q d)) ‚ü©
          succ q + (d * q + d)          ‚â°‚ü® ap (succ q +_) (addition-commutativity (d * q) d)   ‚ü© 
          succ q + (d + d * q)          ‚â°‚ü® ap (succ q +_) (mult-commutativity d (succ q))      ‚ü© 
          succ q + succ q * d           ‚àé

\end{code}

The division theorem is clearly a proposition.

First, we fix the quotient, and prove that the remainder is unique.

That is, if we have that a ‚â° q * succ d + r, and
                         a ‚â° q * succ d + r', then r ‚â° r'.
This is easy to prove using cancellation of addition.

\begin{code}

division-is-prop' : (a d q : ‚Ñï) ‚Üí is-prop (Œ£ r Íûâ ‚Ñï , (a ‚â° q * succ d + r) √ó r < succ d)
division-is-prop' a d q (r‚ÇÄ , e‚ÇÄ , l‚ÇÄ) (r‚ÇÅ , e‚ÇÅ , l‚ÇÅ)
 = to-subtype-‚â°
  (Œª r ‚Üí √ó-is-prop ‚Ñï-is-set (<-is-prop-valued r (succ d)))
   (addition-left-cancellable r‚ÇÄ r‚ÇÅ (q * succ d) (e‚ÇÄ ‚Åª¬π ‚àô e‚ÇÅ))

\end{code}

To conclude that the division theorem is a proposition, we use
trichotomy on the two quotient values q‚ÇÄ and q‚ÇÅ.

When q‚ÇÄ ‚â° q‚ÇÅ, we are done.

In either of two cases, we can generalise a contradiction proof, which
is done in division-is-prop-lemma.

The idea of the proof is as follows:

We have that:
r‚ÇÄ ‚â§ d,
r‚ÇÅ ‚â§ d,
q‚ÇÄ < q‚ÇÅ

a ‚â° q‚ÇÄ * succ d + r‚ÇÄ,
a ‚â° q‚ÇÅ * succ d + r‚ÇÅ,

Hence we have that

q‚ÇÄ + k ‚â° q‚ÇÅ, with k > 0
q‚ÇÄ * succ d + r‚ÇÄ ‚â° q‚ÇÅ * succ d + r‚ÇÅ
                 ‚â° (q‚ÇÄ + k) * succ d + r‚ÇÅ
                 ‚â° (q‚ÇÄ * d) + k * succ d + r‚ÇÅ
Now, r‚ÇÄ ‚â° k * succ d + r‚ÇÅ
   ‚áí k * succ d + r‚ÇÅ ‚â§ d    (since r‚ÇÄ ‚â§ d)
   ‚áí k * succ d ‚â§ d
   ‚áí succ d ‚â§ d, and hence we have a contradiction.

\begin{code}

division-is-prop-lemma : (a d q‚ÇÄ q‚ÇÅ r‚ÇÄ r‚ÇÅ : ‚Ñï)
                       ‚Üí r‚ÇÄ ‚â§ d
                       ‚Üí a ‚â° q‚ÇÄ * succ d + r‚ÇÄ
                       ‚Üí a ‚â° q‚ÇÅ * succ d + r‚ÇÅ
                       ‚Üí ¬¨ (q‚ÇÄ < q‚ÇÅ)
division-is-prop-lemma a d q‚ÇÄ q‚ÇÅ r‚ÇÄ r‚ÇÅ l‚ÇÅ e‚ÇÅ e‚ÇÇ l‚ÇÇ with subtraction (succ q‚ÇÄ) q‚ÇÅ l‚ÇÇ
... | k , e‚ÇÉ = not-less-than-itself d V
 where
  I : q‚ÇÄ * succ d + r‚ÇÄ ‚â° q‚ÇÄ * succ d + (succ k * succ d + r‚ÇÅ)
  I = q‚ÇÄ * succ d + r‚ÇÄ                     ‚â°‚ü® e‚ÇÅ ‚Åª¬π                                                                ‚ü©
      a                                    ‚â°‚ü® e‚ÇÇ                                                                   ‚ü©
      q‚ÇÅ + q‚ÇÅ * d + r‚ÇÅ                     ‚â°‚ü® ap (Œª - ‚Üí - * succ d + r‚ÇÅ) (e‚ÇÉ ‚Åª¬π)                                   ‚ü©
      succ (k + q‚ÇÄ) * succ d + r‚ÇÅ          ‚â°‚ü® ap (Œª - ‚Üí succ - * succ d + r‚ÇÅ) (addition-commutativity k q‚ÇÄ)        ‚ü©
      (q‚ÇÄ + succ k) * succ d + r‚ÇÅ          ‚â°‚ü® ap (_+ r‚ÇÅ) (distributivity-mult-over-addition' q‚ÇÄ (succ k) (succ d)) ‚ü©
      q‚ÇÄ * succ d + succ k * succ d + r‚ÇÅ   ‚â°‚ü® addition-associativity (q‚ÇÄ * succ d) (succ k * succ d) r‚ÇÅ            ‚ü©
      q‚ÇÄ * succ d + (succ k * succ d + r‚ÇÅ) ‚àé

  II : r‚ÇÄ ‚â° succ k * succ d + r‚ÇÅ
  II = addition-left-cancellable r‚ÇÄ (succ k * succ d + r‚ÇÅ) (q‚ÇÄ * succ d)  I

  III : succ k * succ d + r‚ÇÅ ‚â§ d
  III = transport (_‚â§ d) II l‚ÇÅ

  IV : succ k * succ d ‚â§ d
  IV = ‚â§-trans (succ k * succ d) (succ k * succ d + r‚ÇÅ) d (‚â§-+ (succ k * succ d) r‚ÇÅ) III

  V : succ d ‚â§ d
  V = product-order-cancellable (succ d) k d (transport (_‚â§ d) (mult-commutativity (succ k) (succ d)) IV)

division-is-prop : (a d : ‚Ñï) ‚Üí is-prop (division-theorem a d)
division-is-prop a d (q‚ÇÄ , r‚ÇÄ , e‚ÇÄ , l‚ÇÄ) (q‚ÇÅ , r‚ÇÅ , e‚ÇÅ , l‚ÇÅ) with <-trichotomous q‚ÇÄ q‚ÇÅ
... | inl      q‚Çí<q‚ÇÅ  = ùüò-elim (division-is-prop-lemma a d q‚ÇÄ q‚ÇÅ r‚ÇÄ r‚ÇÅ l‚ÇÄ e‚ÇÄ e‚ÇÅ q‚Çí<q‚ÇÅ)
... | inr (inl q‚ÇÄ‚â°q‚ÇÅ) = to-subtype-‚â° (division-is-prop' a d) q‚ÇÄ‚â°q‚ÇÅ
... | inr (inr q‚ÇÅ<q‚ÇÄ) = ùüò-elim (division-is-prop-lemma a d q‚ÇÅ q‚ÇÄ r‚ÇÅ r‚ÇÄ l‚ÇÅ e‚ÇÅ e‚ÇÄ q‚ÇÅ<q‚ÇÄ)
 
\end{code}

A property of division which is sometimes useful is the following.
If a * b ‚â° a * c + d. If we were considering integers, this would be
easy to prove by simply by rewriting the equation as a * (b - c) ‚â°
d. With natural numbers, instead use induction on b and c.

If c ‚â° 0, we are done, since a * b ‚à£ d.
If b ‚â° 0, then 0 ‚â° a * c + d, and hence d ‚â° 0, and a * 0 ‚â° 0, and we are done.
If b , c > 0, then we use induction.
The inductive hypothesis is: a * b       ‚â° a * c       + d ‚Üí a ‚à£ d,
                 and we have a * (b + 1) ‚â° a * (c + 1) + d.

Since addition is left cancellable, we can find that a * b ‚â° a * c + d and we are done.

\begin{code}

factor-of-sum-consequence : (a b c d : ‚Ñï) ‚Üí a * b ‚â° a * c + d ‚Üí a ‚à£ d
factor-of-sum-consequence a b        0        d e = b , (e ‚àô zero-left-neutral d)
factor-of-sum-consequence a 0        (succ c) d e = 0 , (sum-to-zero-gives-zero (a * succ c) d (e ‚Åª¬π) ‚Åª¬π)
factor-of-sum-consequence a (succ b) (succ c) d e =
 let e' = (addition-left-cancellable (a * b) (a * c + d) a (e ‚àô addition-associativity a (a * c) d))
 in factor-of-sum-consequence a b c d e'
                                      
\end{code}


