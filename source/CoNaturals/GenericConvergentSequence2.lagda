Martin Escardo, 14th January 2022, with major additions in November
2023 and a few additions later.


We develop an automorphism of the Cantor type â„• â†’ ğŸš which
induces an equivalent copy â„•âˆ' of â„•âˆ.

The function Ï• restricts to an equivalence between â„•âˆ and the subtype

     Î£ Î± ê‰ (â„• â†’ ğŸš) , is-prop (Î£ n ê‰ â„• , Î± n ï¼ â‚)

of the Cantor type (the sequences with at most one â‚).

Notice that the condition on Î± can be expressed as "is-prop (fiber Î± â‚)".

\begin{code}

{-# OPTIONS --safe --without-K #-}

module CoNaturals.GenericConvergentSequence2 where

open import CoNaturals.GenericConvergentSequence
open import MLTT.Spartan
open import MLTT.Two-Properties
open import Naturals.Order hiding (max)
open import Naturals.Properties
open import Notation.CanonicalMap
open import Notation.Order
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.FunExt
open import UF.NotNotStablePropositions
open import UF.Sets
open import UF.Subsingletons
open import UF.Subsingletons-FunExt

T-cantor : (â„• â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
T-cantor Î± = Î£ n ê‰ â„• , Î± n ï¼ â‚

private
 T = T-cantor

has-at-most-one-â‚ : (â„• â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
has-at-most-one-â‚ Î± = is-prop (T Î±)

has-at-most-one-â‚-is-prop : funextâ‚€ â†’ (Î± : â„• â†’ ğŸš) â†’ is-prop (has-at-most-one-â‚ Î±)
has-at-most-one-â‚-is-prop fe Î± = being-prop-is-prop fe

â„•âˆ' : ğ“¤â‚€ Ì‡
â„•âˆ' = Î£ Î± ê‰ (â„• â†’ ğŸš) , has-at-most-one-â‚ Î±

â„•âˆ'-to-â„•â†’ğŸš : â„•âˆ' â†’ (â„• â†’ ğŸš)
â„•âˆ'-to-â„•â†’ğŸš = prâ‚

â„•âˆ'-to-â„•â†’ğŸš-lc : funextâ‚€ â†’ left-cancellable â„•âˆ'-to-â„•â†’ğŸš
â„•âˆ'-to-â„•â†’ğŸš-lc fe = prâ‚-lc (being-prop-is-prop fe)

â„•âˆ'-is-Â¬Â¬-separated : funextâ‚€ â†’ is-Â¬Â¬-separated â„•âˆ'
â„•âˆ'-is-Â¬Â¬-separated fe = subtype-is-Â¬Â¬-separated
                         prâ‚
                         (â„•âˆ'-to-â„•â†’ğŸš-lc fe)
                         (Cantor-is-Â¬Â¬-separated fe)

â„•âˆ'-is-set : funextâ‚€ â†’ is-set â„•âˆ'
â„•âˆ'-is-set fe = Â¬Â¬-separated-types-are-sets fe (â„•âˆ'-is-Â¬Â¬-separated fe)

private
 Â¬T : (â„• â†’ ğŸš) â†’ ğ“¤â‚€ Ì‡
 Â¬T Î± = (n : â„•) â†’ Î± n ï¼ â‚€

not-T-gives-Â¬T : {Î± : â„• â†’ ğŸš} â†’ Â¬ (T Î±) â†’ Â¬T Î±
not-T-gives-Â¬T {Î±} Ï• n = different-from-â‚-equal-â‚€ (Î» (e : Î± n ï¼ â‚) â†’ Ï• (n , e))

Â¬T-gives-not-T : {Î± : â„• â†’ ğŸš} â†’ Â¬T Î± â†’ Â¬ (T Î±)
Â¬T-gives-not-T {Î±} Ïˆ (n , e) = zero-is-not-one ((Ïˆ n)â»Â¹ âˆ™ e)

to-T-ï¼ : {Î± : â„• â†’ ğŸš}
          {n n' : â„•}
        â†’ n ï¼ n'
        â†’ {e : Î± n ï¼ â‚} {e' : Î± n' ï¼ â‚}
        â†’ (n , e) ï¼[ T Î± ] (n' , e')
to-T-ï¼ p = to-subtype-ï¼ (Î» - â†’ ğŸš-is-set) p

from-T-ï¼ : {Î± : â„• â†’ ğŸš}
          {n n' : â„•}
        â†’ {e : Î± n ï¼ â‚} {e' : Î± n' ï¼ â‚}
        â†’ (n , e) ï¼[ T Î± ] (n' , e')
        â†’ n ï¼ n'
from-T-ï¼ p = ap prâ‚ p

index-uniqueness : (Î± : â„• â†’ ğŸš)
                 â†’ is-prop (T Î±)
                 â†’ {n n' : â„•} â†’ Î± n ï¼ â‚ â†’ Î± n' ï¼ â‚ â†’ n ï¼ n'
index-uniqueness Î± i {n} {n'} e e' = from-T-ï¼ (i (n , e) (n' , e'))

index-uniqueness-converse : (Î± : â„• â†’ ğŸš)
                          â†’ ({n n' : â„•} â†’ Î± n ï¼ â‚ â†’ Î± n' ï¼ â‚ â†’ n ï¼ n')
                          â†’ is-prop (T Î±)
index-uniqueness-converse Î± Ï• (n , e) (n' , e') = to-T-ï¼ (Ï• e e')

private
 instance
  canonical-map-â„•âˆ'-â„•â†’ğŸš : Canonical-Map â„•âˆ' (â„• â†’ ğŸš)
  Î¹ {{canonical-map-â„•âˆ'-â„•â†’ğŸš}} = â„•âˆ'-to-â„•â†’ğŸš

â„•âˆ'-to-â„•â†’ğŸš-at-most-one-â‚ : (u : â„•âˆ') â†’ is-prop (T (â„•âˆ'-to-â„•â†’ğŸš u))
â„•âˆ'-to-â„•â†’ğŸš-at-most-one-â‚ = prâ‚‚

â„•âˆ'-index-uniqueness : (u : â„•âˆ')
                     â†’ {n n' : â„•} â†’ Î¹ u n ï¼ â‚ â†’ Î¹ u n' ï¼ â‚ â†’ n ï¼ n'
â„•âˆ'-index-uniqueness (Î± , i) = index-uniqueness Î± i

Zero' : â„•âˆ'
Zero' = Î± , h
 where
  Î± : â„• â†’ ğŸš
  Î± 0        = â‚
  Î± (succ n) = â‚€

  i : is-prop (T Î±)
  i (0 , e) (0 , e') = to-T-ï¼ refl

  h : has-at-most-one-â‚ Î±
  h (n , e) (n' , e') = to-T-ï¼ (index-uniqueness Î± i e e')

Succ' : â„•âˆ' â†’ â„•âˆ'
Succ' (Î± , h) = cons â‚€ Î± , h'
 where
  h' : has-at-most-one-â‚ (cons â‚€ Î±)
  h' (succ n , e) (succ n' , e') = to-T-ï¼ (ap succ (index-uniqueness Î± h e e'))

â„•-to-â„•âˆ' : â„• â†’ â„•âˆ'
â„•-to-â„•âˆ' 0        = Zero'
â„•-to-â„•âˆ' (succ n) = Succ' (â„•-to-â„•âˆ' n)

private
 instance
  Canonical-Map-â„•-â„•âˆ' : Canonical-Map â„• â„•âˆ'
  Î¹ {{Canonical-Map-â„•-â„•âˆ'}} = â„•-to-â„•âˆ'

is-finite' : â„•âˆ' â†’ ğ“¤â‚€ Ì‡
is-finite' u@(Î± , a) = T Î±

being-finite'-is-prop : funextâ‚€ â†’ (u : â„•âˆ') â†’ is-prop (is-finite' u)
being-finite'-is-prop feâ‚€ u@(Î± , a) = a

size' : {u : â„•âˆ'} â†’ is-finite' u â†’ â„•
size' (n , e) = n

size'-property : {u : â„•âˆ'} (Ï† : is-finite' u) â†’ â„•âˆ'-to-â„•â†’ğŸš u (size' {u} Ï†) ï¼ â‚
size'-property (n , e) = e

Zero'-is-finite : is-finite' Zero'
Zero'-is-finite = 0 , refl

is-finite'-up : (u : â„•âˆ')
              â†’ is-finite' u
              â†’ is-finite' (Succ' u)
is-finite'-up _ (n , e) = succ n , e

is-finite'-down : (u : â„•âˆ')
                â†’ is-finite' (Succ' u)
                â†’ is-finite' u
is-finite'-down _ (succ n , e) = n , e

â„•-to-â„•âˆ'-is-finite' : (n : â„•) â†’ is-finite' (Î¹ n)
â„•-to-â„•âˆ'-is-finite' 0        = Zero'-is-finite
â„•-to-â„•âˆ'-is-finite' (succ n) = is-finite'-up (Î¹ n)
                                (â„•-to-â„•âˆ'-is-finite' n)

âˆ' : â„•âˆ'
âˆ' = (Î» _ â†’ â‚€) , (Î» (n , e) (n' , e') â†’ ğŸ˜-elim (zero-is-not-one e))

not-finite'-is-âˆ' : funextâ‚€ â†’ (u : â„•âˆ') â†’ Â¬ is-finite' u â†’ u ï¼ âˆ'
not-finite'-is-âˆ' fe u Î½ = â„•âˆ'-to-â„•â†’ğŸš-lc fe
                            (dfunext fe
                              (Î» i â†’ different-from-â‚-equal-â‚€
                                      (Î» (e : â„•âˆ'-to-â„•â†’ğŸš u i ï¼ â‚) â†’ Î½ (i , e))))

not-T-is-âˆ' : funextâ‚€ â†’ (u : â„•âˆ') â†’ Â¬ T (Î¹ u) â†’ u ï¼ âˆ'
not-T-is-âˆ' fe u Î½ = â„•âˆ'-to-â„•â†’ğŸš-lc fe (dfunext fe (not-T-gives-Â¬T Î½))

is-infinite-âˆ' : Â¬ is-finite' âˆ'
is-infinite-âˆ' (n , e) = zero-is-not-one e

\end{code}

To show that â„•âˆ' gives an equivalent copy of â„•âˆ, we consider a
particular equivalence (â„• â†’ ğŸš) â‰ƒ (â„• â†’ ğŸš).

\begin{code}

Ï•-cantor Î³-cantor : (â„• â†’ ğŸš) â†’ (â„• â†’ ğŸš)

Ï•-cantor Î± n = cons â‚ Î± n âŠ• Î± n

Î³-cantor Î² 0        = complement (Î² 0)
Î³-cantor Î² (succ n) = Î³-cantor Î² n âŠ• Î² (succ n)

private
 Ï• Î³ : (â„• â†’ ğŸš) â†’ (â„• â†’ ğŸš)
 Ï• = Ï•-cantor
 Î³ = Î³-cantor

Î·-cantor : (Î² : â„• â†’ ğŸš) â†’ Ï• (Î³ Î²) âˆ¼ Î²
Î·-cantor Î² 0        = complement-involutive (Î² 0)
Î·-cantor Î² (succ n) = âŠ•-involutive {Î³ Î² n} {Î² (succ n)}

Îµ-cantor : (Î± : â„• â†’ ğŸš) â†’ Î³ (Ï• Î±) âˆ¼ Î±
Îµ-cantor Î± 0        = complement-involutive (Î± 0)
Îµ-cantor Î± (succ n) = Î³ (Ï• Î±) (succ n)             ï¼âŸ¨ refl âŸ©
                      Î³ (Ï• Î±) n âŠ• Î± n âŠ• Î± (succ n) ï¼âŸ¨ I âŸ©
                      Î± n âŠ• Î± n âŠ• Î± (succ n)       ï¼âŸ¨ II âŸ©
                      Î± (succ n)                   âˆ
 where
  I  = ap (_âŠ• Î± n âŠ• Î± (succ n)) (Îµ-cantor Î± n)
  II = âŠ•-involutive {Î± n} {Î± (succ n)}

private
 Î· : (Î² : â„• â†’ ğŸš) â†’ Ï• (Î³ Î²) âˆ¼ Î²
 Îµ : (Î± : â„• â†’ ğŸš) â†’ Î³ (Ï• Î±) âˆ¼ Î±

 Î· = Î·-cantor
 Îµ = Îµ-cantor

\end{code}

We now discuss the restrictions of Ï• and Î³ mentioned above. Notice
that the following is by four cases without induction.

\begin{code}

Ï•-property : funextâ‚€
           â†’ (Î± : â„• â†’ ğŸš)
           â†’ is-decreasing Î±
           â†’ has-at-most-one-â‚ (Ï• Î±)
Ï•-property fe Î± Î´ (0 , p) (0 ,      q) = to-T-ï¼ refl
Ï•-property fe Î± Î´ (0 , p) (succ m , q) = ğŸ˜-elim (Zero-not-Succ (II â»Â¹ âˆ™ IV))
 where
  u : â„•âˆ
  u = (Î± , Î´)

  I = Î± 0                           ï¼âŸ¨ (complement-involutive (Î± 0))â»Â¹ âŸ©
      complement (complement (Î± 0)) ï¼âŸ¨ ap complement p âŸ©
      complement â‚                  ï¼âŸ¨ refl âŸ©
      â‚€                             âˆ

  II : u ï¼ Zero
  II = is-Zero-equal-Zero fe I

  III : (Î± m ï¼ â‚) Ã— (Î± (succ m) ï¼ â‚€)
  III = âŠ•-propertyâ‚ {Î± m} {Î± (succ m)} (Î´ m) q

  IV : u ï¼ Succ (Î¹ m)
  IV = uncurry (Succ-criterion fe) III

Ï•-property fe Î± Î´ (succ n , p) (0 , q)= ğŸ˜-elim (Zero-not-Succ (II â»Â¹ âˆ™ IV))
 where
  u : â„•âˆ
  u = (Î± , Î´)

  I = Î± 0                           ï¼âŸ¨ (complement-involutive (Î± 0))â»Â¹ âŸ©
      complement (complement (Î± 0)) ï¼âŸ¨ ap complement q âŸ©
      complement â‚                  ï¼âŸ¨ refl âŸ©
      â‚€                             âˆ

  II : u ï¼ Zero
  II = is-Zero-equal-Zero fe I

  III : (Î± n ï¼ â‚) Ã— (Î± (succ n) ï¼ â‚€)
  III = âŠ•-propertyâ‚ {Î± n} {Î± (succ n)} (Î´ n) p

  IV : u ï¼ Succ (Î¹ n)
  IV = uncurry (Succ-criterion fe) III

Ï•-property fe Î± Î´ (succ n , p) (succ m , q) = VI
 where
  u : â„•âˆ
  u = (Î± , Î´)

  I : (Î± n ï¼ â‚) Ã— (Î± (succ n) ï¼ â‚€)
  I = âŠ•-propertyâ‚ (Î´ n) p

  II : (Î± m ï¼ â‚) Ã— (Î± (succ m) ï¼ â‚€)
  II = âŠ•-propertyâ‚ (Î´ m) q

  III : u ï¼ Succ (Î¹ n)
  III = uncurry (Succ-criterion fe) I

  IV : u ï¼ Succ (Î¹ m)
  IV = uncurry (Succ-criterion fe) II

  V : succ n ï¼ succ m
  V = â„•-to-â„•âˆ-lc (III â»Â¹ âˆ™ IV)

  VI : (succ n , p) ï¼ (succ m , q)
  VI = to-T-ï¼ V

\end{code}

The following two observations give an alternative understanding of
the definition of Î³:

\begin{code}

Î³-caseâ‚€ : {Î² : â„• â†’ ğŸš} {n : â„•}
        â†’ Î² (succ n) ï¼ â‚€ â†’ Î³ Î² (succ n) ï¼ Î³ Î² n
Î³-caseâ‚€ = âŠ•-â‚€-right-neutral'

Î³-caseâ‚ : {Î² : â„• â†’ ğŸš} {n : â„•}
        â†’ Î² (succ n) ï¼ â‚ â†’ Î³ Î² (succ n) ï¼ complement (Î³ Î² n)
Î³-caseâ‚ = âŠ•-left-complement

\end{code}

We need the following consequences of the sequence Î² having at most
one â‚.

\begin{code}

at-most-one-â‚-Lemmaâ‚€ : (Î² : â„• â†’ ğŸš)
                     â†’ has-at-most-one-â‚ Î²
                     â†’ {m n : â„•} â†’ (Î² m ï¼ â‚) Ã— (Î² n ï¼ â‚) â†’ m ï¼ n
at-most-one-â‚-Lemmaâ‚€ Î² Ï€ {m} {n} (p , q) = ap prâ‚ (Ï€ (m , p) (n , q))

at-most-one-â‚-Lemmaâ‚ : (Î² : â„• â†’ ğŸš)
                     â†’ has-at-most-one-â‚ Î²
                     â†’ {m n : â„•} â†’ m â‰  n â†’ Î² m ï¼ â‚ â†’ Î² n ï¼ â‚€
at-most-one-â‚-Lemmaâ‚ Î² Ï€ {m} {n} Î½ p = II
 where
  I : Î² n â‰  â‚
  I q = Î½ (at-most-one-â‚-Lemmaâ‚€ Î² Ï€ (p , q))

  II : Î² n ï¼ â‚€
  II = different-from-â‚-equal-â‚€ I

\end{code}

The main lemma about Î³ is the following, where we are interested in
the choice k = n, but we need to prove the lemma for general k to get
a suitable induction hypothesis.

\begin{code}

Î³-lemma : (Î² : â„• â†’ ğŸš)
        â†’ has-at-most-one-â‚ Î²
        â†’ (n : â„•) â†’ Î² (succ n) ï¼ â‚ â†’ (k : â„•) â†’ k â‰¤ n â†’ Î³ Î² k ï¼ â‚
Î³-lemma Î² Ï€ n p 0 l = w
 where
  w : complement (Î² 0) ï¼ â‚
  w = complement-introâ‚€ (at-most-one-â‚-Lemmaâ‚ Î² Ï€ (positive-not-zero n) p)

Î³-lemma Î² Ï€ 0 p (succ k) ()
Î³-lemma Î² Ï€ (succ n) p (succ k) l = w
 where
  IH : Î³ Î² k ï¼ â‚
  IH = Î³-lemma Î² Ï€ (succ n) p k (â‰¤-trans k n (succ n) l (â‰¤-succ n))

  I : succ (succ n) â‰  succ k
  I m = not-less-than-itself n r
   where
    q : succ n ï¼ k
    q = succ-lc m

    r : succ n â‰¤ n
    r = transportâ»Â¹ (_â‰¤ n) q l

  II : Î² (succ k) ï¼ â‚€
  II = at-most-one-â‚-Lemmaâ‚ Î² Ï€ I p

  w : Î³ Î² k âŠ• Î² (succ k) ï¼ â‚
  w =  âŠ•-introâ‚â‚€ IH II

\end{code}

With this it is almost immediate that Î³ produces a decreasing
sequence if it is given a sequence with at most one â‚:

\begin{code}

Î³-property : (Î² : â„• â†’ ğŸš)
           â†’ has-at-most-one-â‚ Î²
           â†’ is-decreasing (Î³ Î²)
Î³-property Î² Ï€ n = IV
 where
  I : Î² (succ n) ï¼ â‚ â†’ Î³ Î² n ï¼ â‚
  I p = Î³-lemma Î² Ï€ n p n (â‰¤-refl n)

  II : Î² (succ n) â‰¤ Î³ Î² n
  II = â‰¤â‚‚-criterion I

  III : Î³ Î² n âŠ• Î² (succ n) â‰¤ Î³ Î² n
  III = â‰¤â‚‚-add-left (Î³ Î² n) (Î² (succ n)) II

  IV : Î³ Î² (succ n) â‰¤ Î³ Î² n
  IV = III

module _ (fe : funextâ‚€) where

 â„•âˆ-to-â„•âˆ' : â„•âˆ â†’ â„•âˆ'
 â„•âˆ-to-â„•âˆ' (Î± , Î´) = Ï• Î± , Ï•-property fe Î± Î´

 â„•âˆ'-to-â„•âˆ : â„•âˆ' â†’ â„•âˆ
 â„•âˆ'-to-â„•âˆ (Î² , Ï€) = Î³ Î² , Î³-property Î² Ï€

 â„•âˆ-Î· : â„•âˆ'-to-â„•âˆ âˆ˜ â„•âˆ-to-â„•âˆ' âˆ¼ id
 â„•âˆ-Î· (Î± , Î´) = to-subtype-ï¼
                 (being-decreasing-is-prop fe)
                 (dfunext fe (Îµ Î±))

 â„•âˆ-Îµ : â„•âˆ-to-â„•âˆ' âˆ˜ â„•âˆ'-to-â„•âˆ âˆ¼ id
 â„•âˆ-Îµ (Î² , Ï€) = to-subtype-ï¼
                 (Î» Î² â†’ being-prop-is-prop fe)
                 (dfunext fe (Î· Î²))

\end{code}

And with this we get the promised equivalence.

\begin{code}

 â„•âˆ-to-â„•âˆ'-â‰ƒ : â„•âˆ â‰ƒ â„•âˆ'
 â„•âˆ-to-â„•âˆ'-â‰ƒ = qinveq â„•âˆ-to-â„•âˆ' (â„•âˆ'-to-â„•âˆ , â„•âˆ-Î· , â„•âˆ-Îµ)

 private
  trivial-fact : (i : â„•) â†’ Ï• (â„•âˆ-to-â„•â†’ğŸš âˆ) i ï¼ â‚€
  trivial-fact 0        = refl
  trivial-fact (succ i) = refl

 Zero-preservation : â„•âˆ-to-â„•âˆ' Zero ï¼ Zero'
 Zero-preservation = to-subtype-ï¼ (has-at-most-one-â‚-is-prop fe) (dfunext fe I)
  where
   I : Ï• (Î¹ Zero) âˆ¼ Î¹ Zero'
   I 0        = refl
   I (succ i) = trivial-fact 0

 Succ-preservation : (u : â„•âˆ) â†’ â„•âˆ-to-â„•âˆ' (Succ u) ï¼ Succ' (â„•âˆ-to-â„•âˆ' u)
 Succ-preservation u@(Î± , d) = to-subtype-ï¼ (has-at-most-one-â‚-is-prop fe) II
  where
   I : Ï• (â„•âˆ-to-â„•â†’ğŸš (Succ u)) âˆ¼ cons â‚€ (Î¹ (â„•âˆ-to-â„•âˆ' u))
   I 0        = refl
   I (succ _) = refl

   II : Ï• (â„•âˆ-to-â„•â†’ğŸš (Succ u)) ï¼ cons â‚€ (Î¹ (â„•âˆ-to-â„•âˆ' u))
   II = dfunext fe I

 âˆ-preservation : â„•âˆ-to-â„•âˆ' âˆ ï¼ âˆ'
 âˆ-preservation = to-subtype-ï¼ (has-at-most-one-â‚-is-prop fe)
                   (dfunext fe trivial-fact)

 âˆ-gives-âˆ' : (u : â„•âˆ') â†’ â„•âˆ'-to-â„•âˆ u ï¼ âˆ â†’ u ï¼ âˆ'
 âˆ-gives-âˆ' u e =
  u                       ï¼âŸ¨ IIâ‚€ âŸ©
  â„•âˆ-to-â„•âˆ' (â„•âˆ'-to-â„•âˆ u) ï¼âŸ¨ IIâ‚ âŸ©
  â„•âˆ-to-â„•âˆ' âˆ             ï¼âŸ¨ IIâ‚‚ âŸ©
  âˆ'                      âˆ
  where
   IIâ‚€ = (inverses-are-sections' â„•âˆ-to-â„•âˆ'-â‰ƒ u)â»Â¹
   IIâ‚ = ap â„•âˆ-to-â„•âˆ' e
   IIâ‚‚ = âˆ-preservation

 âˆ'-gives-âˆ : (u : â„•âˆ) â†’ â„•âˆ-to-â„•âˆ' u ï¼ âˆ' â†’ u ï¼ âˆ
 âˆ'-gives-âˆ u e =
  u                       ï¼âŸ¨ (inverses-are-retractions' â„•âˆ-to-â„•âˆ'-â‰ƒ u)â»Â¹ âŸ©
  â„•âˆ'-to-â„•âˆ (â„•âˆ-to-â„•âˆ' u) ï¼âŸ¨ ap â„•âˆ'-to-â„•âˆ e âŸ©
  â„•âˆ'-to-â„•âˆ âˆ'            ï¼âŸ¨ ap â„•âˆ'-to-â„•âˆ (âˆ-preservation â»Â¹) âŸ©
  â„•âˆ'-to-â„•âˆ (â„•âˆ-to-â„•âˆ' âˆ) ï¼âŸ¨ inverses-are-retractions' â„•âˆ-to-â„•âˆ'-â‰ƒ âˆ âŸ©
  âˆ                       âˆ

 finite-preservation : (n : â„•) â†’ â„•âˆ-to-â„•âˆ' (Î¹ n) ï¼ Î¹ n
 finite-preservation 0        = Zero-preservation
 finite-preservation (succ n) =
  â„•âˆ-to-â„•âˆ' (Î¹ (succ n))  ï¼âŸ¨ refl âŸ©
  â„•âˆ-to-â„•âˆ' (Succ (Î¹ n))  ï¼âŸ¨ Succ-preservation (Î¹ n) âŸ©
  Succ' (â„•âˆ-to-â„•âˆ' (Î¹ n)) ï¼âŸ¨ ap Succ' (finite-preservation n) âŸ©
  Succ' (Î¹ n)             ï¼âŸ¨ refl âŸ©
  Î¹ (succ n)              âˆ

 finite-gives-finite' : (u : â„•âˆ') â†’ is-finite (â„•âˆ'-to-â„•âˆ u) â†’ is-finite' u
 finite-gives-finite' u (n , e) = III
  where
   I : is-finite' (Î¹ n)
   I = â„•-to-â„•âˆ'-is-finite' n

   II = Î¹ n                     ï¼âŸ¨ (finite-preservation n)â»Â¹ âŸ©
        â„•âˆ-to-â„•âˆ' (Î¹ n)         ï¼âŸ¨ ap â„•âˆ-to-â„•âˆ' e âŸ©
        â„•âˆ-to-â„•âˆ' (â„•âˆ'-to-â„•âˆ u) ï¼âŸ¨ inverses-are-sections' â„•âˆ-to-â„•âˆ'-â‰ƒ u âŸ©
        u                       âˆ

   III : is-finite' u
   III = transport is-finite' II I

 finite'-preservation : (n : â„•) â†’ â„•âˆ'-to-â„•âˆ (Î¹ n) ï¼ Î¹ n
 finite'-preservation n =
  â„•âˆ'-to-â„•âˆ (Î¹ n)             ï¼âŸ¨ I âŸ©
  â„•âˆ'-to-â„•âˆ (â„•âˆ-to-â„•âˆ' (Î¹ n)) ï¼âŸ¨ II âŸ©
  Î¹ n                         âˆ
   where
    I  = (ap â„•âˆ'-to-â„•âˆ (finite-preservation n))â»Â¹
    II = inverses-are-retractions' â„•âˆ-to-â„•âˆ'-â‰ƒ (Î¹ n)

 â„•-to-â„•âˆ'-lc : left-cancellable â„•-to-â„•âˆ'
 â„•-to-â„•âˆ'-lc {n} {n'} e =
  â„•-to-â„•âˆ-lc (
   Î¹ n              ï¼âŸ¨ (finite'-preservation n)â»Â¹ âŸ©
   â„•âˆ'-to-â„•âˆ (Î¹ n)  ï¼âŸ¨ ap â„•âˆ'-to-â„•âˆ e âŸ©
   â„•âˆ'-to-â„•âˆ (Î¹ n') ï¼âŸ¨ finite'-preservation n' âŸ©
   Î¹ n'             âˆ)

 â„•-to-â„•âˆ'-diagonal : (n : â„•) â†’ â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) n ï¼ â‚
 â„•-to-â„•âˆ'-diagonal 0        = refl
 â„•-to-â„•âˆ'-diagonal (succ n) = â„•-to-â„•âˆ'-diagonal n

 diagonal-lemma : (n : â„•) (u : â„•âˆ') â†’ â„•âˆ'-to-â„•â†’ğŸš u n ï¼ â‚ â†’ u ï¼ Î¹ n
 diagonal-lemma n u p = â„•âˆ'-to-â„•â†’ğŸš-lc fe (dfunext fe f)
  where
   I : â„•âˆ'-to-â„•â†’ğŸš u n ï¼ â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) n
   I = â„•âˆ'-to-â„•â†’ğŸš u n     ï¼âŸ¨ p âŸ©
       â‚                  ï¼âŸ¨ (â„•-to-â„•âˆ'-diagonal n)â»Â¹ âŸ©
       â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) n âˆ

   II : (i : â„•) â†’ n â‰  i â†’ â„•âˆ'-to-â„•â†’ğŸš u i ï¼ â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) i
   II i Î½ =
    â„•âˆ'-to-â„•â†’ğŸš u i      ï¼âŸ¨ IIâ‚€ âŸ©
    â‚€                   ï¼âŸ¨ IIâ‚ â»Â¹ âŸ©
    â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) i  âˆ
     where
      IIâ‚€ = different-from-â‚-equal-â‚€
             (Î» (e : â„•âˆ'-to-â„•â†’ğŸš u i ï¼ â‚)
                   â†’ Î½ (â„•âˆ'-index-uniqueness u p e))

      IIâ‚ = different-from-â‚-equal-â‚€
             (Î» (e : â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) i ï¼ â‚)
                   â†’ Î½ (â„•âˆ'-index-uniqueness (Î¹ n) (â„•-to-â„•âˆ'-diagonal n) e))

   f : (i : â„•) â†’ â„•âˆ'-to-â„•â†’ğŸš u i ï¼ â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) i
   f i = Cases (â„•-is-discrete n i)
          (Î» (q : n ï¼ i)
                â†’ transport (Î» - â†’ â„•âˆ'-to-â„•â†’ğŸš u - ï¼ â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) -) q I)
          (Î» (Î½ : n â‰  i)
                â†’ II i Î½)

 size'-property' : {u : â„•âˆ'} (Ï† : is-finite' u) â†’ Î¹ (size' {u} Ï†) ï¼ u
 size'-property' {u} Ï† = II â»Â¹
  where
   I : â„•âˆ'-to-â„•â†’ğŸš u (size' {u} Ï†) ï¼ â‚
   I = size'-property {u} Ï†

   II : u ï¼ Î¹ (size' {u} Ï†)
   II = diagonal-lemma (size' {u} Ï†) u I

 finite'-is-natural : (u : â„•âˆ') â†’ is-finite' u â†’ Î£ n ê‰ â„• , u ï¼ Î¹ n
 finite'-is-natural u (n , p) = (n , diagonal-lemma n u p)

 finite'-gives-finite : (u : â„•âˆ) â†’ is-finite' (â„•âˆ-to-â„•âˆ' u) â†’ is-finite u
 finite'-gives-finite u (n , e) = III
  where
   I : is-finite (Î¹ n)
   I = â„•-to-â„•âˆ-is-finite n

   II = Î¹ n                     ï¼âŸ¨ IIâ‚€ âŸ©
        â„•âˆ'-to-â„•âˆ (Î¹ n)         ï¼âŸ¨ IIâ‚ âŸ©
        â„•âˆ'-to-â„•âˆ (â„•âˆ-to-â„•âˆ' u) ï¼âŸ¨ IIâ‚‚ âŸ©
        u                       âˆ
         where
          IIâ‚€ = (finite'-preservation n)â»Â¹
          IIâ‚ = ap â„•âˆ'-to-â„•âˆ ((diagonal-lemma n (â„•âˆ-to-â„•âˆ' u) e)â»Â¹)
          IIâ‚‚ = inverses-are-retractions' â„•âˆ-to-â„•âˆ'-â‰ƒ u

   III : is-finite u
   III = transport is-finite II I

 finite'-isolated : (n : â„•) â†’ is-isolated (â„•-to-â„•âˆ' n)
 finite'-isolated n u = I (finite-isolated fe n (â„•âˆ'-to-â„•âˆ u))
  where
   I : is-decidable (Î¹ n ï¼ â„•âˆ'-to-â„•âˆ u) â†’ is-decidable (Î¹ n ï¼ u)
   I (inl e) = inl (Î¹ n                     ï¼âŸ¨ (finite-preservation n)â»Â¹ âŸ©
                    â„•âˆ-to-â„•âˆ' (Î¹ n)         ï¼âŸ¨ ap â„•âˆ-to-â„•âˆ' e âŸ©
                    â„•âˆ-to-â„•âˆ' (â„•âˆ'-to-â„•âˆ u) ï¼âŸ¨ â„•âˆ-Îµ u âŸ©
                    u                       âˆ)
   I (inr Î½) = inr (Î» (e : Î¹ n ï¼ u)
                         â†’ Î½ (Î¹ n             ï¼âŸ¨ (finite'-preservation n)â»Â¹ âŸ©
                              â„•âˆ'-to-â„•âˆ (Î¹ n) ï¼âŸ¨ ap â„•âˆ'-to-â„•âˆ e âŸ©
                              â„•âˆ'-to-â„•âˆ u     âˆ))

 â„•âˆ'-equality-criterion : (x y : â„•âˆ')
                        â†’ ((n : â„•) â†’ Î¹ n ï¼ x â†’ Î¹ n ï¼ y)
                        â†’ ((n : â„•) â†’ Î¹ n ï¼ y â†’ Î¹ n ï¼ x)
                        â†’ x ï¼ y
 â„•âˆ'-equality-criterion x y f g = â„•âˆ'-to-â„•â†’ğŸš-lc fe V
  where
   I : (n : â„•) (x y : â„•âˆ')
     â†’ (Î¹ n ï¼ x â†’ Î¹ n ï¼ y)
     â†’ â„•âˆ'-to-â„•â†’ğŸš x n â‰¤â‚‚ â„•âˆ'-to-â„•â†’ğŸš y n
   I n x y h = â‰¤â‚‚-criterion a
    where
     a : â„•âˆ'-to-â„•â†’ğŸš x n ï¼ â‚ â†’ â„•âˆ'-to-â„•â†’ğŸš y n ï¼ â‚
     a e = â„•âˆ'-to-â„•â†’ğŸš y n     ï¼âŸ¨ (ap (Î» - â†’ - n) IV)â»Â¹ âŸ©
           â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) n ï¼âŸ¨ â„•-to-â„•âˆ'-diagonal n âŸ©
           â‚                  âˆ
      where
       II : Î¹ n ï¼ x
       II = (diagonal-lemma n x e)â»Â¹

       III : Î¹ n ï¼ y
       III = h II

       IV : â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) ï¼ â„•âˆ'-to-â„•â†’ğŸš y
       IV = ap â„•âˆ'-to-â„•â†’ğŸš III

   V : â„•âˆ'-to-â„•â†’ğŸš x ï¼ â„•âˆ'-to-â„•â†’ğŸš y
   V = dfunext fe (Î» n â†’ â‰¤â‚‚-anti (I n x y (f n)) (I n y x (g n)))

\end{code}
