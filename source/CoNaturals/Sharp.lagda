Martin Escardo 12-14 March 2024.

We construct an embedding of â„•âˆ into â„•âŠ¥, the lifting on â„•, or the
flat domain of natural numbers, and prove various properties of it.

We conclude that we can recover â„•âˆ as the subtype of sharp elements of â„•âŠ¥,

  â„•âˆ â‰ƒ (Î£ y ê‰ â„•âŠ¥ , is-sharp y),

where y is called sharp if Î· n âŠ‘ y is decidable for every n ê‰ â„• [1].

[1] Tom de Jong. Apartness, sharp elements, and the Scott topology of
    domains.
    Mathematical Structures in Computer Science , Volume 33 , Issue 7,
    August 2023 , pp. 573 - 604.
    https://doi.org/10.1017/S0960129523000282

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.Subsingletons

module CoNaturals.Sharp
        (feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€)
        (pe : Prop-Ext)
       where

open import CoNaturals.BothTypes
open import Lifting.Construction ğ“¤â‚€
open import Lifting.IdentityViaSIP ğ“¤â‚€ {ğ“¤â‚€} {â„•}
open import Lifting.Set ğ“¤â‚€
open import Lifting.UnivalentPrecategory ğ“¤â‚€ {ğ“¤â‚€} â„•
open import MLTT.Plus-Properties
open import MLTT.Two-Properties
open import Notation.CanonicalMap
open import NotionsOfDecidability.Decidable
open import UF.DiscreteAndSeparated
open import UF.Embeddings
open import UF.Equiv
open import UF.FunExt
open import UF.PropTrunc
open import UF.Subsingletons-FunExt

open â„•âˆ-equivalence feâ‚€

\end{code}

We introduce the following standard notation, and write Î¹ : â„• â†’ â„•âŠ¥ for
the canonical inclusion of the natural numbers into the flat domain â„•âŠ¥
of natural numbers. Notice that we also write Î¹ : â„• â†’ â„•âˆ for the
canonical inclusion of the natural numbers into the generic convergent
sequence, introduced in the module that defines â„•âˆ.

\begin{code}

â„•âŠ¥ = ğ“› â„•

instance
 canonical-map-â„•-â„•âŠ¥ : Canonical-Map â„• â„•âŠ¥
 Î¹ {{canonical-map-â„•-â„•âŠ¥}} = Î·

\end{code}

We define a map

   sharp : â„•âˆ â†’ â„•âŠ¥

so that

 * sharp (Î¹ n) ï¼ Î¹ n and

 * sharp âˆ ï¼ âŠ¥.

\begin{code}

sharp : â„•âˆ â†’ â„•âŠ¥
sharp x = is-finite x , size , being-finite-is-prop feâ‚€ x

sharp-finite : (n : â„•) â†’ sharp (Î¹ n) ï¼ Î¹ n
sharp-finite n = II
 where
  I : sharp (Î¹ n) âŠ‘ Î¹ n
  I = unique-to-ğŸ™ , (Î» (n , p) â†’ â„•-to-â„•âˆ-lc p)

  II : sharp (Î¹ n) ï¼ Î¹ n
  II = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ I
        (Î» (_ : is-defined (Î¹ n)) â†’ â„•-to-â„•âˆ-is-finite n)

sharp-âˆ : sharp âˆ ï¼ âŠ¥
sharp-âˆ = II
 where
  I : sharp âˆ âŠ‘ âŠ¥
  I = is-infinite-âˆ , (Î» is-finite-âˆ â†’ ğŸ˜-elim (is-infinite-âˆ is-finite-âˆ))

  II : sharp âˆ ï¼ âŠ¥
  II = âŠ‘-anti pe feâ‚€ feâ‚€ (I , âŠ¥-least (sharp âˆ))

\end{code}

The map sharp is left-cancellable and hence an embedding.

\begin{code}

sharp-lc : left-cancellable sharp
sharp-lc {x} {x'} e = II
 where
  I : (x x' : â„•âˆ) â†’ sharp x â‹Â· sharp x' â†’ (n : â„•) â†’ Î¹ n ï¼ x â†’ Î¹ n ï¼ x'
  I x x' a n e =
   Î¹ n                      ï¼âŸ¨ ap Î¹ (g (n , e)) âŸ©
   Î¹ (size (âŒœ f âŒ (n , e))) ï¼âŸ¨ size-property (âŒœ f âŒ (n , e)) âŸ©
   x'                        âˆ
   where
    f : is-finite x â‰ƒ is-finite x'
    f = is-defined-â‹Â· (sharp x) (sharp x') a

    g : (Ï† : is-finite x) â†’ size Ï† ï¼ size (âŒœ f âŒ Ï†)
    g = value-â‹Â· (sharp x) (sharp x') a

  II : x ï¼ x'
  II = â„•âˆ-equality-criterion feâ‚€ x x'
        (I x  x' (Id-to-â‹Â· _ _ e))
        (I x' x  (Id-to-â‹Â· _ _ (e â»Â¹)))

sharp-is-embedding : is-embedding sharp
sharp-is-embedding =
 lc-maps-into-sets-are-embeddings sharp sharp-lc
  (lifting-of-set-is-set feâ‚€ feâ‚€ pe â„• â„•-is-set)

\end{code}

We have the following two corollaries.

\begin{code}

sharp-finite' : (x : â„•âˆ) (n : â„•) â†’ sharp x ï¼ Î¹ n â†’ x ï¼ Î¹ n
sharp-finite' x n e = sharp-lc (sharp x     ï¼âŸ¨ e âŸ©
                                Î¹ n         ï¼âŸ¨ (sharp-finite n)â»Â¹ âŸ©
                                sharp (Î¹ n) âˆ)

sharp-âˆ' : (x : â„•âˆ) â†’ sharp x ï¼ âŠ¥ â†’ x ï¼ âˆ
sharp-âˆ' x e = sharp-lc (sharp x ï¼âŸ¨ e âŸ©
                         âŠ¥       ï¼âŸ¨ sharp-âˆ â»Â¹ âŸ©
                         sharp âˆ âˆ)

\end{code}

The image of the embedding has empty complement, in the following
sense.

\begin{code}

sharp-image-has-empty-complement : Â¬ (Î£ y ê‰ â„•âŠ¥ , ((x : â„•âˆ) â†’ sharp x â‰  y))
sharp-image-has-empty-complement (y , f) =
 Î·-image feâ‚€ feâ‚€ pe
   (y ,
   (Î» (e : y ï¼ âŠ¥)
         â†’ f âˆ (sharp âˆ ï¼âŸ¨ sharp-âˆ âŸ©
                âŠ¥       ï¼âŸ¨ e â»Â¹ âŸ©
                y       âˆ)) ,
   (Î» n (e : y ï¼ Î¹ n)
         â†’ f (Î¹ n)
             (sharp (Î¹ n) ï¼âŸ¨ sharp-finite n âŸ©
              Î¹ n         ï¼âŸ¨ e â»Â¹ âŸ©
              y           âˆ)))

\end{code}

But the embedding is a surjection if and only if excluded middle
holds.

\begin{code}

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt
 open import UF.ImageAndSurjection pt
 open import UF.ExcludedMiddle

 sharp-is-surjection-gives-EM : is-surjection sharp â†’ EM ğ“¤â‚€
 sharp-is-surjection-gives-EM sharp-is-surjection P P-is-prop =
  âˆ¥âˆ¥-rec (decidability-of-prop-is-prop feâ‚€ P-is-prop) II I
  where
   y : â„•âŠ¥
   y = P , (Î» _ â†’ 0) , P-is-prop

   I : âˆƒ x ê‰ â„•âˆ , sharp x ï¼ y
   I = sharp-is-surjection y

   II : (Î£ x ê‰ â„•âˆ , sharp x ï¼ y) â†’ P + Â¬ P
   II (x , e) = IV III
    where
     III : (Î¹ 0 ï¼ x) + (Î¹ 0 â‰  x)
     III = finite-isolated feâ‚€ 0 x

     IV : (Î¹ 0 ï¼ x) + (Î¹ 0 â‰  x) â†’ P + Â¬ P
     IV (inl d) = inl (âŒœ C âŒâ»Â¹ â‹†)
      where
       A = y           ï¼âŸ¨ e â»Â¹ âŸ©
           sharp x     ï¼âŸ¨ ap sharp (d â»Â¹) âŸ©
           sharp (Î¹ 0) ï¼âŸ¨ sharp-finite 0 âŸ©
           Î¹ 0         âˆ

       B : y â‹Â· Î¹ 0
       B = Id-to-â‹Â· _ _ A

       C : P â‰ƒ ğŸ™
       C = is-defined-â‹Â· y (Î¹ 0) B

     IV (inr Î½) = inr (contrapositive B C)
      where
       A : y âŠ‘ Î¹ 0
       A = unique-to-ğŸ™ , (Î» (p : P) â†’ refl)

       B : P â†’ y ï¼ Î¹ 0
       B p = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ A (Î» _ â†’ p)

       C : Â¬ (y ï¼ Î¹ 0)
       C d = Î½ (Câ‚ â»Â¹)
        where
         Câ‚€ = sharp x ï¼âŸ¨ e âŸ©
              y       ï¼âŸ¨ d âŸ©
              Î¹ 0     âˆ

         Câ‚ : x ï¼ Î¹ 0
         Câ‚ = sharp-finite' x 0 Câ‚€

 EM-gives-sharp-is-retraction : EM ğ“¤â‚€ â†’ (y : â„•âŠ¥) â†’ Î£ x ê‰ â„•âˆ , sharp x ï¼ y
 EM-gives-sharp-is-retraction em y@(P , Ï† , P-is-prop) =
   I (em P P-is-prop)
  where
   I : P + Â¬ P â†’ Î£ x ê‰ â„•âˆ , sharp x ï¼ y
   I (inl p) = Î¹ (Ï† p) , III
    where
     II : sharp (Î¹ (Ï† p)) âŠ‘ y
     II = (Î» _ â†’ p) , (Î» (n , e) â†’ â„•-to-â„•âˆ-lc e)

     III : sharp (Î¹ (Ï† p)) ï¼ y
     III = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ II (Î» _ â†’ â„•-to-â„•âˆ-is-finite (Ï† p))

   I (inr Î½) = âˆ , III
    where
     II : sharp âˆ âŠ‘ y
     II = transport (_âŠ‘ y) (sharp-âˆ â»Â¹) (âŠ¥-least y)

     III : sharp âˆ ï¼ y
     III = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ II Î» (p : P) â†’ ğŸ˜-elim (Î½ p)

 EM-gives-sharp-is-surjection : EM ğ“¤â‚€ â†’ is-surjection sharp
 EM-gives-sharp-is-surjection em y = âˆ£ EM-gives-sharp-is-retraction em y âˆ£

\end{code}

TODO. Actually notice that EM implies that the map sharp is an
equivalence.

Added 14th March 2024.

The image of the function sharp consists precisely of the sharp
elements of â„•âŠ¥ in the sense of [1], so that we can recover â„•âˆ as the
subtype of sharp elements of â„•âŠ¥:

            â„•âˆ â‰ƒ (Î£ y : â„•âŠ¥ , is-sharp y).

In an algebraic domain D, it is proved in [1] that d : D is sharp if
and only if b âŠ‘ d is decidable for every compact element b : D.

In DomainTheory.Lifting.LiftingSetAlgebraic, it is shown that the
compact elements of ğ“› X for a set X are precisely âŠ¥ and those of the
form Î· x. But âŠ¥ âŠ‘ d is always decidable, simply because it is true. So
an element d of ğ“› X is sharp if and only if Î· x âŠ‘ d is decidable for
every x : X.

We continue with the particular case X = â„•.

\begin{code}

is-sharp : â„•âŠ¥ â†’ ğ“¤â‚€ Ì‡
is-sharp y = (n : â„•) â†’ is-decidable (Î¹ n âŠ‘ y)

being-sharp-is-prop : (y : â„•âŠ¥) â†’ is-prop (is-sharp y)
being-sharp-is-prop l = Î -is-prop feâ‚€
                         (Î» n â†’ decidability-of-prop-is-prop feâ‚€
                                 (âŠ‘-prop-valued feâ‚€ feâ‚€ â„•-is-set (Î¹ n) l))

sharp-is-sharp : (x : â„•âˆ) â†’ is-sharp (sharp x)
sharp-is-sharp x n = I (finite-isolated feâ‚€ n x)
 where
  I : is-decidable (Î¹ n ï¼ x) â†’ is-decidable (Î¹ n âŠ‘ sharp x)
  I (inl e) = inl ((Î» (_ : is-defined (Î¹ n)) â†’ n , e) , (Î» (_ : ğŸ™) â†’ refl))
  I (inr Î½) = inr (Î» ((a , b) : Î· n âŠ‘ sharp x)
                              â†’ Î½ (Î¹ n            ï¼âŸ¨ ap Î¹ (b â‹†) âŸ©
                                   Î¹ (size (a â‹†)) ï¼âŸ¨ size-property (a â‹†) âŸ©
                                   x              âˆ))
\end{code}

Now we need to show that

  (y : â„•âŠ¥) â†’ is-sharp y â†’ Î£ x ê‰ â„•âˆ , sharp x ï¼ y.

However, this turns out to be rather laborious to do directly, and so
we instead work with the isomorphic copy â„•âˆ' of â„•âˆ consisting of the
binary sequences that have at most one 1.

We will repeat part of the above development for this isomorphic copy,
where the constructions and proofs look very similar. Perhaps we
should have worked with only â„•âˆ' in this file.

\begin{code}

open import CoNaturals.Type2

instance
 Canonical-Map-â„•-â„•âˆ' : Canonical-Map â„• â„•âˆ'
 Î¹ {{Canonical-Map-â„•-â„•âˆ'}} = â„•-to-â„•âˆ'

sharp' : â„•âˆ' â†’ â„•âŠ¥
sharp' x = is-finite' x , size' {x} , being-finite'-is-prop feâ‚€ x

sharp'-lc : left-cancellable sharp'
sharp'-lc {x} {y} e = II
 where
  I : (x y : â„•âˆ') â†’ sharp' x â‹Â· sharp' y â†’ (n : â„•) â†’ Î¹ n ï¼ x â†’ Î¹ n ï¼ y
  I x y a n e =
   Î¹ n                     ï¼âŸ¨ ap Î¹ (g Ï†) âŸ©
   Î¹ (size' {y} (âŒœ f âŒ Ï†)) ï¼âŸ¨ size'-property' feâ‚€ (âŒœ f âŒ Ï†) âŸ©
   y                       âˆ
   where
    Ï† : is-finite' x
    Ï† = n , (â„•âˆ'-to-â„•â†’ğŸš x n     ï¼âŸ¨ ap (Î» - â†’ â„•âˆ'-to-â„•â†’ğŸš - n) (e â»Â¹) âŸ©
             â„•âˆ'-to-â„•â†’ğŸš (Î¹ n) n ï¼âŸ¨ â„•-to-â„•âˆ'-diagonal feâ‚€ n âŸ©
             â‚                  âˆ)

    f : is-finite' x â‰ƒ is-finite' y
    f = is-defined-â‹Â· (sharp' x) (sharp' y) a

    g : (Ï† : is-finite' x) â†’ size' {x} Ï† ï¼ size' {y} (âŒœ f âŒ Ï†)
    g = value-â‹Â· (sharp' x) (sharp' y) a

  II : x ï¼ y
  II = â„•âˆ'-equality-criterion feâ‚€ x y
        (I x y (Id-to-â‹Â· _ _ e))
        (I y x (Id-to-â‹Â· _ _ (e â»Â¹)))

sharp'-is-embedding : is-embedding sharp'
sharp'-is-embedding =
 lc-maps-into-sets-are-embeddings sharp' sharp'-lc
  (lifting-of-set-is-set feâ‚€ feâ‚€ pe â„• â„•-is-set)

sharp'-is-sharp : (x : â„•âˆ') â†’ is-sharp (sharp' x)
sharp'-is-sharp x n = I (finite'-isolated feâ‚€ n x)
 where
  I : is-decidable (Î¹ n ï¼ x) â†’ is-decidable (Î¹ n âŠ‘ sharp' x)
  I (inl refl) = inl ((Î» â‹† â†’ n , â„•-to-â„•âˆ'-diagonal feâ‚€ n) , (Î» â‹† â†’ refl))
  I (inr Î½) = inr f
   where
    f : Â¬ (Î¹ n âŠ‘ sharp' x)
    f (a , b) = Î½ (Î¹ n                 ï¼âŸ¨ ap Î¹ (b â‹†) âŸ©
                   Î¹ (size' {x} (a â‹†)) ï¼âŸ¨ size'-property' feâ‚€ (a â‹†) âŸ©
                   x                   âˆ)

\end{code}

After the above repetition, we come to something new.

\begin{code}

only-sharp'-is-sharp : (y : â„•âŠ¥) â†’ is-sharp y â†’ Î£ x ê‰ â„•âˆ' , sharp' x ï¼ y
only-sharp'-is-sharp y@(P , Ï† , P-is-prop) y-is-sharp = Î³
 where
  I : (n n' : â„•) â†’ Î¹ n âŠ‘ y â†’ Î¹ n' âŠ‘ y â†’ n ï¼ n'
  I n n' (p , e) (p' , e') = n        ï¼âŸ¨ e â‹† âŸ©
                             Ï† (p  â‹†) ï¼âŸ¨ ap Ï† (P-is-prop (p â‹†) (p' â‹†)) âŸ©
                             Ï† (p' â‹†) ï¼âŸ¨ (e' â‹†)â»Â¹ âŸ©
                             n'       âˆ

  y-is-sharp' : (n : â„•) â†’ Â¬ (Î¹ n âŠ‘ y) + (Î¹ n âŠ‘ y)
  y-is-sharp' n = +-commutative (y-is-sharp n)

  Î± : â„• â†’ ğŸš
  Î± = indicator-map y-is-sharp'

  Î±-propertyâ‚€ : (n : â„•) â†’ Î± n ï¼ â‚€ â†’ Â¬ (Î¹ n âŠ‘ y)
  Î±-propertyâ‚€ = indicatorâ‚€ y-is-sharp'

  Î±-propertyâ‚ : (n : â„•) â†’ Î± n ï¼ â‚ â†’ Î¹ n âŠ‘ y
  Î±-propertyâ‚ = indicatorâ‚ y-is-sharp'

  Î±-propertyâ‚' : (n : â„•) â†’ Î± n ï¼ â‚ â†’ Î¹ n ï¼ y
  Î±-propertyâ‚' n Î± = Î·-maximal pe feâ‚€ feâ‚€ n y (Î±-propertyâ‚ n Î±)

  Î±-property : (n n' : â„•) â†’ Î± n ï¼ â‚ â†’ Î± n' ï¼ â‚ â†’ n ï¼ n'
  Î±-property n n' e e' = I n n' (Î±-propertyâ‚ n e) (Î±-propertyâ‚ n' e')

  a : has-at-most-one-â‚ Î±
  a (n , e) (n' , e') = to-subtype-ï¼ (Î» _ â†’ ğŸš-is-set) (Î±-property n n' e e')

  x : â„•âˆ'
  x = Î± , a

  II : sharp' x âŠ‘ y
  II = IIâ‚€ , IIâ‚
   where
    IIâ‚€ : (Î£ n ê‰ â„• , Î± n ï¼ â‚) â†’ P
    IIâ‚€ (n , e) = def-pr (Î¹ n) y (Î±-propertyâ‚ n e) â‹†

    IIâ‚ : ((n , e) : Î£ n ê‰ â„• , Î± n ï¼ â‚) â†’ n ï¼ Ï† (IIâ‚€ (n , e))
    IIâ‚ (n , e) = Î±-property n n' e B
     where
      n' : â„•
      n' = Ï† (IIâ‚€ (n , e))

      A : Î¹ n' âŠ‘ y
      A = (Î» _ â†’ IIâ‚€ (n , e)) , (Î» _ â†’ refl)

      B : Î± n' ï¼ â‚
      B = different-from-â‚€-equal-â‚ (Î» (d : Î± n' ï¼ â‚€) â†’ Î±-propertyâ‚€ n' d A)

  III : P â†’ Î£ n ê‰ â„• , Î± n ï¼ â‚
  III p = Ï† p , different-from-â‚€-equal-â‚ (Î» (e : Î± (Ï† p) ï¼ â‚€) â†’ IIIâ‚ e IIIâ‚€)
   where
    IIIâ‚€ : Î¹ (Ï† p) âŠ‘ y
    IIIâ‚€ = (Î» _ â†’ p) , (Î» _ â†’ refl)

    IIIâ‚ : Î± (Ï† p) ï¼ â‚€ â†’ Â¬ (Î¹ (Ï† p) âŠ‘ y)
    IIIâ‚ = Î±-propertyâ‚€ (Ï† p)

  IV : sharp' x ï¼ y
  IV = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ II III

  Î³ : Î£ x ê‰ â„•âˆ' , sharp' x ï¼ y
  Î³ = x , IV

theorem : â„•âˆ' â‰ƒ (Î£ y ê‰ â„•âŠ¥ , is-sharp y)
theorem = r , r-is-equiv
 where
  r : â„•âˆ' â†’ (Î£ y ê‰ â„•âŠ¥ , is-sharp y)
  r x = sharp' x , sharp'-is-sharp x

  r-is-embedding : is-embedding r
  r-is-embedding = factor-is-embedding r prâ‚
                    sharp'-is-embedding
                    (prâ‚-is-embedding being-sharp-is-prop)

  s : (Î£ y ê‰ â„•âŠ¥ , is-sharp y) â†’ â„•âˆ'
  s (y , y-is-sharp) = prâ‚ (only-sharp'-is-sharp y y-is-sharp)

  h : (Ïƒ@(y , _) : Î£ y ê‰ â„•âŠ¥ , is-sharp y) â†’ sharp' (s Ïƒ) ï¼ y
  h (y , y-is-sharp) = prâ‚‚ (only-sharp'-is-sharp y y-is-sharp)

  rs : r âˆ˜ s âˆ¼ id
  rs Ïƒ = to-subtype-ï¼ being-sharp-is-prop (h Ïƒ)

  r-is-equiv : is-equiv r
  r-is-equiv = embeddings-with-sections-are-equivs r r-is-embedding (s , rs)

corollary : â„•âˆ â‰ƒ (Î£ y ê‰ â„•âŠ¥ , is-sharp y)
corollary = â„•âˆ-to-â„•âˆ'-â‰ƒ â— theorem

\end{code}

Other ways to distinguish â„•âˆ and â„•âŠ¥:

 * â„•âˆ is totally separated.

 * â„•âŠ¥ is injective and hence indecomposable.

This is already proved in other modules.

For the sake of completeness, we repeat further things done above with
â„•âˆ' replacing â„•âˆ.

\begin{code}

sharp'-finite : (n : â„•) â†’ sharp' (Î¹ n) ï¼ Î¹ n
sharp'-finite n = II
 where
  I : sharp' (Î¹ n) âŠ‘ Î¹ n
  I = (Î» _ â†’ â‹†) ,
      (Î» (n' , e) â†’ â„•-to-â„•âˆ'-lc feâ‚€ ((diagonal-lemma feâ‚€ n' (Î¹ n) e)â»Â¹))

  II : sharp' (Î¹ n) ï¼ Î¹ n
  II = âŠ‘-anti-lemma pe feâ‚€ feâ‚€ I
        (Î» _ â†’ n , (â„•-to-â„•âˆ'-diagonal feâ‚€ n))

sharp'-âˆ' : sharp' âˆ' ï¼ âŠ¥
sharp'-âˆ' = II
 where
  I : sharp' âˆ' âŠ‘ âŠ¥
  I = is-infinite-âˆ' , (Î» is-finite-âˆ' â†’ ğŸ˜-elim (is-infinite-âˆ' is-finite-âˆ'))

  II : sharp' âˆ' ï¼ âŠ¥
  II = âŠ‘-anti pe feâ‚€ feâ‚€ (I , âŠ¥-least (sharp' âˆ'))

sharp'-finite'' : (x : â„•âˆ') (n : â„•) â†’ sharp' x ï¼ Î¹ n â†’ x ï¼ Î¹ n
sharp'-finite'' x n e = sharp'-lc (sharp' x     ï¼âŸ¨ e âŸ©
                                   Î¹ n          ï¼âŸ¨ (sharp'-finite n)â»Â¹ âŸ©
                                   sharp' (Î¹ n) âˆ)

sharp'-âˆ'' : (x : â„•âˆ') â†’ sharp' x ï¼ âŠ¥ â†’ x ï¼ âˆ'
sharp'-âˆ'' x e = sharp'-lc (sharp' x  ï¼âŸ¨ e âŸ©
                            âŠ¥         ï¼âŸ¨ sharp'-âˆ' â»Â¹ âŸ©
                            sharp' âˆ' âˆ)

\end{code}
