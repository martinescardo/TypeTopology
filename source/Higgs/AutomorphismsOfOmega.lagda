Martin Escardo, 1-7 November 2023.

We prove the main results of [1] and [2] about automorphisms of Î©.

[1] Peter T. Johnstone. Automorphisms of Î©. Algebra Universalis,
    9 (1979) 1-7.
    https://doi.org/10.1007/BF02488012

[2] Peter Freyd. Choice and well-ordering.  Annals of Pure and Applied
    Logic 35 (1987) 149-166.
    https://doi.org/10.1016/0168-0072(87)90060-1
    https://core.ac.uk/download/pdf/81927529.pdf

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import Groups.Type
open import MLTT.Spartan
open import UF.Embeddings
open import UF.Equiv hiding (_â‰…_)
open import UF.Equiv-FunExt
open import UF.FunExt
open import UF.Logic
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier hiding (Î©)
open import UF.SubtypeClassifier-Properties

module Higgs.AutomorphismsOfOmega
        {ğ“¤ : Universe}
        (fe : Fun-Ext)
        (pe : propext ğ“¤)
       where

private
 ğ“¤âº = ğ“¤ âº

open import Higgs.InvolutionTheorem fe pe
open import Higgs.Rigidity fe pe

open Conjunction
open Implication fe
open Universal fe

can-recover-automorphism-from-its-value-at-âŠ¤
 : (ğ•— : Aut Î©)
   (p : Î©)
 â†’ âŒœ ğ•— âŒ p ï¼ (p â‡” âŒœ ğ•— âŒ âŠ¤)
can-recover-automorphism-from-its-value-at-âŠ¤ ğ•—@(f , _) p =
 Î©-ext' pe fe
  ((f p ï¼ âŠ¤)       â‰ƒâŸ¨ Î©-automorphism-swap-â‰ƒ ğ•— âŸ©
   (f âŠ¤ ï¼ p)       â‰ƒâŸ¨ â‰ƒ-sym (â‡”-equiv-to-ï¼ pe (f âŠ¤) p) âŸ©
   ((f âŠ¤ â‡” p) ï¼ âŠ¤) â‰ƒâŸ¨ transport-â‰ƒ (_ï¼ âŠ¤) (â‡”-sym pe (f âŠ¤) p) âŸ©
   ((p â‡” f âŠ¤) ï¼ âŠ¤) â– )

\end{code}

The Higgs object â„ as defined by Johnstone in [1].

\begin{code}

is-widespread : Î© â†’ ğ“¤âº Ì‡
is-widespread r = (p : Î©) â†’ ((p â‡” r) â‡” r) ï¼ p

being-widespread-is-prop : (r : Î©) â†’ is-prop (is-widespread r)
being-widespread-is-prop r = Î -is-prop fe (Î» p â†’ Î©-is-set fe pe)

â„ : ğ“¤âº Ì‡
â„ = Î£ r ê‰ Î© , is-widespread r

âŸª_âŸ« : â„ â†’ Î©
âŸª r , _ âŸ« = r

âŸª_âŸ«-is-widespread : (x : â„) â†’ is-widespread âŸª x âŸ«
âŸª _ , i âŸ«-is-widespread = i

â„-is-set : is-set â„
â„-is-set = subsets-of-sets-are-sets
            Î©
            is-widespread
            (Î©-is-set fe pe)
            (Î» {r} â†’ being-widespread-is-prop r)

to-â„-ï¼ : (r s : Î©) {i : is-widespread r} {j : is-widespread s}
       â†’ r ï¼ s
       â†’ (r , i) ï¼[ â„ ] (s , j)
to-â„-ï¼ r s {i} {j} = to-subtype-ï¼ being-widespread-is-prop

to-â„-ï¼' : (x@(r , i) y@(s , j) : â„)
         â†’ (r holds â†” s holds)
         â†’ x ï¼ y
to-â„-ï¼' (r , i) (s , j) (f , g) = to-â„-ï¼ r s (Î©-extensionality pe fe f g)

\end{code}

The equality of the Higgs object has values in ğ“¤âº, but is equivalent
to an equality with values in ğ“¤ and hence in Î©.

\begin{code}

_ï¼â‚•_ : â„ â†’ â„ â†’ Î©
(p , p-is-ws) ï¼â‚• (q , q-is-ws) = p â‡” q

infix 4 _ï¼â‚•_

ï¼â‚•-agrees-with-ï¼ : (x y : â„) â†’ ((x ï¼â‚• y) holds) â‰ƒ (x ï¼ y)
ï¼â‚•-agrees-with-ï¼ x@(p , p-is-ws) y@(q , q-is-ws)
 = logically-equivalent-props-are-equivalent
    (holds-is-prop (x ï¼â‚• y))
    â„-is-set
    (to-â„-ï¼' x y)
    (Î» (e : x ï¼ y) â†’ idtofun _ _ (ap (_holds âˆ˜ âŸª_âŸ«) e) ,
                      idtofun _ _ (ap (_holds âˆ˜ âŸª_âŸ«) (e â»Â¹)))

Î©-automorphisms-are-â‡”-embeddings : (ğ•— : Aut Î©)
                                   (p q : Î©)
                                 â†’ (p â‡” q) ï¼ (âŒœ ğ•— âŒ p â‡” âŒœ ğ•— âŒ q)
Î©-automorphisms-are-â‡”-embeddings ğ•—@(f , f-is-equiv) p q =
 Î©-ext' pe fe
  (((p â‡” q) ï¼ âŠ¤)     â‰ƒâŸ¨ I âŸ©
   (p ï¼ q)           â‰ƒâŸ¨ II âŸ©
   (f p ï¼ f q)       â‰ƒâŸ¨ III âŸ©
   ((f p â‡” f q) ï¼ âŠ¤) â– )
  where
   I   = â‡”-equiv-to-ï¼ pe p q
   II  = embedding-criterion-converse' f (equivs-are-embeddings' ğ•—) p q
   III = â‰ƒ-sym (â‡”-equiv-to-ï¼ pe (f p) (f q))

eval-at-âŠ¤-is-widespread : (ğ•— : Aut Î©) â†’ is-widespread (eval-at-âŠ¤ ğ•—)
eval-at-âŠ¤-is-widespread ğ•—@(f , _) p = II
 where
  I = p â‡” âŠ¤           ï¼âŸ¨ Iâ‚€ âŸ©
      f p â‡” f âŠ¤       ï¼âŸ¨ Iâ‚ âŸ©
      (p â‡” f âŠ¤) â‡” f âŠ¤ âˆ
   where
    Iâ‚€ = Î©-automorphisms-are-â‡”-embeddings ğ•— p âŠ¤
    Iâ‚ = ap (_â‡” f âŠ¤) (can-recover-automorphism-from-its-value-at-âŠ¤ ğ•— p)

  II : ((p â‡” f âŠ¤) â‡” f âŠ¤) ï¼ p
  II = transport (_ï¼ p) I (âŠ¤-â‡”-neutral pe p)

Aut-Î©-to-â„ : Aut Î© â†’ â„
Aut-Î©-to-â„ ğ•— = eval-at-âŠ¤ ğ•— , eval-at-âŠ¤-is-widespread ğ•—

â„-to-Aut-Î© : â„ â†’ Aut Î©
â„-to-Aut-Î© (r , i) = (Î» p â†’ p â‡” r) , involutions-are-equivs _ i

Î·-â„ : â„-to-Aut-Î© âˆ˜ Aut-Î©-to-â„ âˆ¼ id
Î·-â„ ğ•—@(f , f-is-equiv) = to-â‰ƒ-ï¼ fe h
 where
  h : (Î» p â†’ p â‡” f âŠ¤) âˆ¼ f
  h p = (can-recover-automorphism-from-its-value-at-âŠ¤ ğ•— p)â»Â¹

Îµ-â„ : Aut-Î©-to-â„ âˆ˜ â„-to-Aut-Î© âˆ¼ id
Îµ-â„ (r , i) = to-â„-ï¼ (âŠ¤ â‡” r) r (âŠ¤-â‡”-neutral' pe r)

â„-to-Aut-Î©-is-equiv : is-equiv â„-to-Aut-Î©
â„-to-Aut-Î©-is-equiv = qinvs-are-equivs â„-to-Aut-Î© (Aut-Î©-to-â„ , Îµ-â„ , Î·-â„)

Aut-Î©-to-â„-is-equiv : is-equiv Aut-Î©-to-â„
Aut-Î©-to-â„-is-equiv = inverses-are-equivs â„-to-Aut-Î© â„-to-Aut-Î©-is-equiv

opaque
 â„-to-Aut-Î©-equivalence : â„ â‰ƒ Aut Î©
 â„-to-Aut-Î©-equivalence = â„-to-Aut-Î© , â„-to-Aut-Î©-is-equiv

\end{code}

The type Aut Î© is a group under composition, the symmetric group on Î©,
where the neutral element is the identity automorphism and the inverse
of any element is itself.  That is, Aut Î© is a boolean group, or a
group of order 2. We now show that the group structure on â„ induced by
the above equivalence is given by logical equivalence _â‡”_ with neutral
element âŠ¤.

\begin{code}

open import Groups.Type
open import Groups.Symmetric fe

Î©â‚› : Group ğ“¤âº
Î©â‚› = symmetric-group Î© (Î©-is-set fe pe)

opaque
 ğ“—-construction : Î£ s ê‰ Group-structure â„ , is-hom (â„ , s) Î©â‚› â„-to-Aut-Î©
 ğ“—-construction = transport-Group-structure Î©â‚› â„ â„-to-Aut-Î© â„-to-Aut-Î©-is-equiv

ğ“— : Group ğ“¤âº
ğ“— = â„ , prâ‚ ğ“—-construction

ğ“—-to-Î©â‚›-isomorphism : ğ“— â‰… Î©â‚›
ğ“—-to-Î©â‚›-isomorphism = â„-to-Aut-Î© , â„-to-Aut-Î©-is-equiv , prâ‚‚ ğ“—-construction

ğ“š-isomorphism-explicitly : (x : â„) (p : Î©)
                         â†’ âŒœ â„-to-Aut-Î© x âŒ p ï¼ (p â‡” âŸª x âŸ«)
ğ“š-isomorphism-explicitly x p = refl

\end{code}

The unit of ğ“— is âŠ¤ and its multiplication is logical equivalence.

\begin{code}

opaque
 unfolding ğ“—-construction

 ğ“—-unit : âŸª unit ğ“— âŸ« ï¼ âŠ¤
 ğ“—-unit = refl

 ğ“—-multiplication : (x y : â„) â†’ âŸª x Â·âŸ¨ ğ“— âŸ© y âŸ« ï¼ (âŸª x âŸ« â‡” âŸª y âŸ«)
 ğ“—-multiplication x y =
  âŸª x Â·âŸ¨ ğ“— âŸ© y âŸ«     ï¼âŸ¨reflâŸ©
  (âŠ¤ â‡” âŸª x âŸ«) â‡” âŸª y âŸ« ï¼âŸ¨ ap (_â‡” âŸª y âŸ«) (âŠ¤-â‡”-neutral' pe âŸª x âŸ«) âŸ©
  âŸª x âŸ« â‡” âŸª y âŸ«       âˆ

 corollary-âŠ¤ : is-widespread âŠ¤
 corollary-âŠ¤ = âŸª unit ğ“— âŸ«-is-widespread

ğ•¥ : â„
ğ•¥ = âŠ¤ , corollary-âŠ¤

corollary-â‡” : (r s : Î©)
            â†’ is-widespread r
            â†’ is-widespread s
            â†’ is-widespread (r â‡” s)
corollary-â‡” r s i j = II
 where
  x y : â„
  x = (r , i)
  y = (s , j)

  I : âŸª x Â·âŸ¨ ğ“— âŸ© y âŸ« ï¼ (r â‡” s)
  I = ğ“—-multiplication x y

  II : is-widespread (r â‡” s)
  II = transport is-widespread I âŸª x Â·âŸ¨ ğ“— âŸ© y âŸ«-is-widespread

corollary-â‡”-assoc : (r s t : Î©)
                  â†’ is-widespread r
                  â†’ is-widespread s
                  â†’ is-widespread t
                  â†’ (r â‡” s) â‡” t ï¼ r â‡” (s â‡” t)
corollary-â‡”-assoc r s t i j k = I
 where
  _Â·_ : â„ â†’ â„ â†’ â„
  x Â· y = x Â·âŸ¨ ğ“— âŸ© y

  x y z : â„
  x = (r , i)
  y = (s , j)
  z = (t , k)

  I =  (r â‡” s) â‡” t             ï¼âŸ¨reflâŸ©
       (âŸª x âŸ« â‡” âŸª y âŸ«) â‡” âŸª z âŸ« ï¼âŸ¨ ap (_â‡” âŸª z âŸ«) ((ğ“—-multiplication _ _)â»Â¹) âŸ©
       âŸª x Â· y âŸ« â‡” âŸª z âŸ«       ï¼âŸ¨ (ğ“—-multiplication _ _)â»Â¹ âŸ©
       âŸª (x Â· y) Â· z âŸ«         ï¼âŸ¨ ap âŸª_âŸ« (assoc ğ“— x y z) âŸ©
       âŸª x Â· (y Â· z) âŸ«         ï¼âŸ¨ ğ“—-multiplication _ _ âŸ©
       âŸª x âŸ« â‡” âŸª y Â· z âŸ«       ï¼âŸ¨ ap (âŸª x âŸ« â‡”_) (ğ“—-multiplication _ _) âŸ©
       âŸª x âŸ« â‡” (âŸª y âŸ« â‡” âŸª z âŸ«) ï¼âŸ¨reflâŸ©
       r â‡” (s â‡” t)             âˆ

\end{code}

Alternative characterization of the widespread property, as stated in
Johnstone's Elephant.

\begin{code}

open import UF.PropTrunc

module _ (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt hiding (_âˆ¨_ ; âˆ¨-elim)
 open Disjunction pt

 is-widespread' : Î© â†’ ğ“¤âº Ì‡
 is-widespread' r = (p : Î©) â†’ (p âˆ¨ (p â‡’ r)) holds

 widespread'-gives-widespread : (r : Î©)
                              â†’ is-widespread' r
                              â†’ is-widespread  r
 widespread'-gives-widespread r w' = w
  where
   I : (p : Î©)
     â†’ (p holds + (p holds â†’ r holds))
     â†’ ((p â‡” r) â‡” r) ï¼ p
   I p (inl h) =
    (p â‡” r) â‡” r ï¼âŸ¨ ap (Î» - â†’ (- â‡” r) â‡” r) Iâ‚€ âŸ©
    (âŠ¤ â‡” r) â‡” r ï¼âŸ¨ ap (_â‡” r) (âŠ¤-â‡”-neutral' pe r) âŸ©
    r â‡” r       ï¼âŸ¨ â‡”-refl pe r âŸ©
    âŠ¤           ï¼âŸ¨ Iâ‚€ â»Â¹ âŸ©
    p           âˆ
     where
      Iâ‚€ : p ï¼ âŠ¤
      Iâ‚€ = holds-gives-equal-âŠ¤ pe fe p h

   I p (inr f) = Î©-ext pe fe Iâ‚ Iâ‚‚
    where
     have-f : (p â‡’ r) holds
     have-f = f

     Iâ‚ : (p â‡” r) â‡” r ï¼ âŠ¤ â†’ p ï¼ âŠ¤
     Iâ‚ eâ‚ =
      p     ï¼âŸ¨ Iâ‚„ âŸ©
      r     ï¼âŸ¨ (â‡”-gives-ï¼ pe _ _ eâ‚)â»Â¹ âŸ©
      p â‡” r ï¼âŸ¨ ï¼-gives-â‡” pe _ _ Iâ‚„ âŸ©
      âŠ¤     âˆ
       where
        Iâ‚‚ : r â‡’ (p â‡” r) ï¼ âŠ¤
        Iâ‚‚ = âˆ§-elim-R pe fe _ _ eâ‚

        Iâ‚ƒ : (r â‡’ (p â‡” r)) holds
        Iâ‚ƒ = equal-âŠ¤-gives-holds _ Iâ‚‚

        g : (r â‡’ p) holds
        g r-holds = âˆ§-Elim-R (p â‡’ r) (r â‡’ p) (Iâ‚ƒ r-holds) r-holds

        Iâ‚„ : p ï¼ r
        Iâ‚„ = Î©-extensionality pe fe f g

     Iâ‚‚ : p ï¼ âŠ¤ â†’ (p â‡” r) â‡” r ï¼ âŠ¤
     Iâ‚‚ eâ‚‚ =
      (p â‡” r) â‡” r ï¼âŸ¨ ap (Î» - â†’ (- â‡” r) â‡” r) eâ‚‚ âŸ©
      (âŠ¤ â‡” r) â‡” r ï¼âŸ¨ ap (_â‡” r) (âŠ¤-â‡”-neutral' pe r) âŸ©
      r â‡” r       ï¼âŸ¨ â‡”-refl pe r âŸ©
      âŠ¤           âˆ

   w : is-widespread r
   w p = âˆ¥âˆ¥-rec (Î©-is-set fe pe) (I p) (w' p)

\end{code}

TODO. Write the above proof purely equationally. In order to do this,
first formulate and prove the equational definition of Heyting algebra
in other modules. Or to begin with, for simplicity, just prove in
UF.Logic that Î© satisfies the equations that define a lattice to be a
Heyting algebra.

\begin{code}

 widespread-gives-widespread' : (r : Î©)
                              â†’ is-widespread  r
                              â†’ is-widespread' r
 widespread-gives-widespread' r@(R , R-is-prop) w = IV
  where
   have-w : (p : Î©) â†’ ((p â‡” r) â‡” r) ï¼ p
   have-w = w

   module _ (p@(P , P-is-prop) : Î©) where

    P' : ğ“¤ Ì‡
    P' = âˆ¥ P + (P â†’ R) âˆ¥

    I : ((P' â†” R) â†” R) â†” P'
    I = equal-âŠ¤-gives-holds _ (ï¼-gives-â‡” pe _ _ (w (P' , âˆ¥âˆ¥-is-prop)))

    II : (P' â†’ R) â†’ R
    II f = f âˆ£ inr (Î» (Ï€ : P) â†’ f âˆ£ inl Ï€ âˆ£) âˆ£

    III : P'
    III = lr-implication I
           ((Î» (e : P' â†” R) â†’ II (lr-implication e)) ,
            (Î» (Ï : R) â†’ (Î» (_ : P') â†’ Ï) ,
                         (Î» (_ : R) â†’ âˆ£ inr (Î» (_ : P) â†’ Ï) âˆ£)))

    IV : (p âˆ¨ (p â‡’ r)) holds
    IV = III

\end{code}
