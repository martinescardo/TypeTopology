Martin Escardo, December 2017 (but done much earlier on paper)

As discussed in the module Omniscience, Bishop's "limited principle of
omniscience" amount to the omniscience of the type â„•, that is,

  Î  \(p : â„• â†’ ğŸš) â†’ (Î£ \(n : â„•) â†’ p n â‰¡ â‚€) + (Î  \(n : â„•) â†’ p n â‰¡ â‚).

This is in general not a univalent proposition, because there may be
many n:â„• with p n â‰¡ â‚€. In univalent mathematics, we may get a
proposition by truncating the Î£ to get the existential quantifier âˆƒ
(see the Homotopy Type Theory book). Here instead we construct the
truncation directly, and call it LPO.

Using this and the module Prop-Tychonoff, we show that the function
type LPOâ†’â„• is searchable and hence omniscient, despite the fact that
LPO is undecided in our type theory.

(We needed to add new helper lemmas in the module
GenericConvergentSequence)

\begin{code}

open import UF

module LPO (fe : {U V : Universe} â†’ FunExt U V) where

open import Naturals
open import Two
open import GenericConvergentSequence
open import DecidableAndDetachable
open import OmniscientTypes

LPO : Uâ‚€ Ì‡
LPO = (x : â„•âˆ) â†’ decidable(Î£ \(n : â„•) â†’ x â‰¡ under n)

LPO-isProp : isProp LPO
LPO-isProp = isProp-exponential-ideal fe f
 where
  a : (x : â„•âˆ) â†’ isProp(Î£ \n â†’ x â‰¡ under n)
  a x (n , p) (m , q) = Î£-â‰¡ n m p q (under-lc (p â»Â¹ âˆ™ q)) (â„•âˆ-set fe _ _)
  
  f : (x : â„•âˆ) â†’ isProp (decidable (Î£ \n â†’ x â‰¡ under n))
  f x = sum-of-contradictory-props (a x) (neg-isProp fe) (Î» u Ï† â†’ Ï† u)

LPO-implies-omniscient-â„• : LPO â†’ omniscient â„•
LPO-implies-omniscient-â„• lpo Î² = cases a b d
  where
    A = (Î£ \(n : â„•) â†’ Î² n â‰¡ â‚€) + (Î  \(n : â„•) â†’ Î² n â‰¡ â‚)
    
    Î± : â„• â†’ ğŸš
    Î± = force-decreasing Î²
    
    x : â„•âˆ
    x = (Î± , force-decreasing-is-decreasing Î²)
    
    d : decidable(Î£ \(n : â„•) â†’ x â‰¡ under n)
    d = lpo x
    
    a : (Î£ \(n : â„•) â†’ x â‰¡ under n) â†’ A
    a (n , p) = inl (force-decreasing-is-not-much-smaller Î² n c) 
      where
        c : Î± n â‰¡ â‚€
        c = ap (Î» x â†’ incl x n) p âˆ™ under-diagonalâ‚€ n
        
    b : (Â¬ Î£ \(n : â„•) â†’ x â‰¡ under n) â†’ A
    b u = inr g
      where
        v : (n : â„•) â†’ x â‰¡ under n â†’ ğŸ˜
        v = curry u
        
        g : (n : â„•) â†’ Î² n â‰¡ â‚
        g n = force-decreasing-is-smaller Î² n e
          where
            c : x â‰¡ under n â†’ ğŸ˜
            c = v n
            
            l : x â‰¡ âˆ
            l = not-â„•-is-âˆ fe v
            
            e : Î± n â‰¡ â‚
            e = ap (Î» x â†’ incl x n) l

omniscient-â„•â†’LPO : omniscient â„• â†’ LPO
omniscient-â„•â†’LPO chlpo x = cases a b d
  where
    A = decidable (Î£ \(n : â„•) â†’ x â‰¡ under n)
    
    Î² : â„• â†’ ğŸš
    Î² = incl x
    
    d : (Î£ \(n : â„•) â†’ Î² n â‰¡ â‚€) + (Î  \(n : â„•) â†’ Î² n â‰¡ â‚)
    d = chlpo Î²
    
    a : (Î£ \(n : â„•) â†’ Î² n â‰¡ â‚€) â†’ A
    a (n , p) = inl g
      where
        g : Î£ \(n : â„•) â†’ x â‰¡ under n
        g = under-lemma fe x n p
        
    b : (Î  \(n : â„•) â†’ Î² n â‰¡ â‚) â†’ A
    b Ï† = inr g
      where
        Ïˆ : Â¬ Î£ \(n : â„•) â†’ Î² n â‰¡ â‚€
        Ïˆ = uncurry (Î» n â†’ Lemma[bâ‰¡â‚â†’bâ‰¢â‚€](Ï† n))
        
        f : (Î£ \(n : â„•) â†’ x â‰¡ under n) â†’ Î£ \(n : â„•) â†’ Î² n â‰¡ â‚€
        f (n , p) = (n , (ap (Î» x â†’ incl x n) p âˆ™ under-diagonalâ‚€ n))
          where
           l : incl x n â‰¡ incl (under n) n
           l = ap (Î» x â†’ incl x n) p
        
        g : Â¬ Î£ \(n : â„•) â†’ x â‰¡ under n
        g = contrapositive f Ïˆ

\end{code}

Now, if LPO is false, that is, an empty type, then the function type

  LPO â†’ â„•
  
is isomorphic to the unit type ğŸ™, and hence is searchable and
omniscient. If LPO holds, that is, LPO is isomorphic to ğŸ™ because it
is a univalent proposition, then the function type LPO â†’ â„• is
isomorphic to â„•, and hence the type LPO â†’ â„• is again searchable by
LPO. So in any case we have that the type LPO â†’ â„• is
searchable. However, LPO is an undecided proposition in our type
theory, so that the nature of the function type LPO â†’ â„• is
undecided. Nevertheless, we can show that it is searchable, without
knowing whether LPO holds or not!

\begin{code}

open import SearchableTypes
open import PropTychonoff

LPOâ†’â„•-searchable : searchable(LPO â†’ â„•)
LPOâ†’â„•-searchable = prop-tychonoff-corollary' fe LPO-isProp f
 where
   f : LPO â†’ searchable â„•
   f = inhabited-omniscient-implies-searchable 0 âˆ˜ LPO-implies-omniscient-â„•

LPOâ†’â„•-omniscient : omniscient(LPO â†’ â„•)
LPOâ†’â„•-omniscient = searchable-implies-omniscient LPOâ†’â„•-searchable

\end{code}
