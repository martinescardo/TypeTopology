Martin Escardo, 22nd January 2020. (This file needs the Agda release candidate 2.6.1.)

There are two parts, which assume function extensionality but not
univalence or the existence of propositional truncations:


(1) A univalent-foundations version of Pierre Pradic and Chad
    E. Brown's argument that Cantor-SchrÃ¶der-Bernstein implies
    excluded middle in constructive set theory
    (https://arxiv.org/abs/1904.09193).

    Their proof, reproduced here, uses the compactness (also known as
    the searchability or omniscience) of â„•âˆ.


(2) A proof that excluded middle implies Cantor-SchrÃ¶der-Bernstein for
    all homotopy types, or âˆ-groupoids. (Added 24th January.)

    For any pair of types, if each one is embedded into the other,
    then they are equivalent.

    For this it is crucial that a map is an embedding if and only if
    its fibers are all propositions (rather than merely the map being
    left-cancellable).

    As far as we know, (2) is a new result.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module CantorSchroederBernstein where

open import SpartanMLTT
open import GenericConvergentSequence
open import DecidableAndDetachable
open import Plus-Properties
open import CompactTypes
open import ConvergentSequenceCompact
open import UF-Subsingletons
open import UF-Equiv
open import UF-Embeddings
open import UF-Retracts
open import UF-FunExt
open import UF-Subsingletons-FunExt
open import UF-ExcludedMiddle

\end{code}

Our formulation of Cantor-SchrÃ¶der-Bernstein:

\begin{code}

CSB : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
CSB X Y = (X â†ª Y) â†’ (Y â†ª X) â†’ X â‰ƒ Y

CantorSchrÃ¶derBernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
CantorSchrÃ¶derBernstein ğ“¤ ğ“¥ = (X : ğ“¤ Ì‡ ) (Y : ğ“¥ Ì‡ ) â†’ CSB X Y

\end{code}

Part 1
------

The following is Lemma 7 of the above reference, using retractions
rather than surjections, for simplicity:

\begin{code}

Pradic-Brown-lemma : {X : ğ“¤ Ì‡ } {A : ğ“¥ Ì‡ }
                   â†’ retract (A + X) of X
                   â†’ Compact X
                   â†’ decidable A
Pradic-Brown-lemma {ğ“¤} {ğ“¥} {X} {A} (r , s , Î·) c = Î³ e
 where
  P : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x = Î£ \(a : A) â†’ r x â‰¡ inl a

  d : (x : X) â†’ decidable (P x)
  d x = equality-cases (r x)
         (Î» (a : A) (u : r x â‰¡ inl a) â†’ inl (a , u))
         (Î» (y : X) (v : r x â‰¡ inr y) â†’ inr (Î» (a , u) â†’ +disjoint (inl a â‰¡âŸ¨ u â»Â¹ âŸ©
                                                                    r x   â‰¡âŸ¨ v    âŸ©
                                                                    inr y âˆ)))

  e : decidable (Î£ \(x : X) â†’ P x)
  e = c P d

  f : A â†’ Î£ \(x : X) â†’ P x
  f a = s (inl a) , a , Î· (inl a)

  Î³ : decidable (Î£ \(x : X) â†’ P x) â†’ decidable A
  Î³ (inl (x , a , u)) = inl a
  Î³ (inr Ï†)           = inr (contrapositive f Ï†)

\end{code}

Function extensionality is used twice in the following, once to know
that â„•âˆ is a set, and once to know that it is compact.

\begin{code}

CSB-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
             â†’ (P : ğ“¤ Ì‡ )
             â†’ is-prop P
             â†’ CSB â„•âˆ (P + â„•âˆ)
             â†’ P + Â¬ P
CSB-gives-EM fe P i csb = Î³
 where
  f : â„•âˆ â†’ P + â„•âˆ
  f = inr

  j : is-embedding f
  j = inr-is-embedding P â„•âˆ

  z : P â†’ â„•âˆ
  z _ = Zero

  g : P + â„•âˆ â†’ â„•âˆ
  g = cases z Succ

  a : is-embedding z
  a = maps-of-props-into-sets-are-embeddings (Î» p â†’ Zero) i (â„•âˆ-is-set fe)

  b : is-embedding Succ
  b = lc-maps-into-sets-are-embeddings Succ Succ-lc (â„•âˆ-is-set fe)

  c : disjoint-images z Succ
  c = Î» (p : P) (x : â„•âˆ) (q : Zero â‰¡ Succ x) â†’ Zero-not-Succ q

  k : is-embedding g
  k = disjoint-cases-embedding z Succ a b c

  e : â„•âˆ â‰ƒ P + â„•âˆ
  e = csb (f , j) (g , k)

  Ï : retract (P + â„•âˆ) of â„•âˆ
  Ï = equiv-retract-r e

  Î³ : P + Â¬ P
  Î³ = Pradic-Brown-lemma Ï (â„•âˆ-Compact fe)

\end{code}

Hence if we assume Cantor-SchrÃ¶der-Bernstein for the first universe ğ“¤â‚€
and an arbitrary universe ğ“¥, as formulated above, then we get excluded
middle for propositions in the universe ğ“¥:

\begin{code}

CantorSchrÃ¶derBernstein-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
                                 â†’ CantorSchrÃ¶derBernstein ğ“¤â‚€ ğ“¥
                                 â†’ EM ğ“¥
CantorSchrÃ¶derBernstein-gives-EM fe csb P i = CSB-gives-EM fe P i (csb â„•âˆ (P + â„•âˆ))

\end{code}

Remark. If instead of requiring that we have a designated equivalence,
we required that there is an unspecified equivalence in the
formulation of Cantor-SchrÃ¶der-Bernstein, we would still get excluded
middle, because P + Â¬ P is a proposition.


Part 2
------

The Cantor-SchrÃ¶der-Bernstein Theorem holds for all homotopy types, or
âˆ-gropoids, in the presence of excluded middle. It is crucial here
that embeddings have subsingleton fibers, so that e.g. the function
is-g-point defined in the proof is property rather than data and hence
we can apply univalent excluded middle to it. It is also worth
remembering, for the sake of comparing the classical result for sets
with its generalization to âˆ-groupoids, that a map of types that are
sets is an embedding if and only if it is left-cancellable.


Our proof adapts Wikipedia's "alternate proof" (consulted 23rd January 2020)

  https://en.wikipedia.org/wiki/Schr%C3%B6der%E2%80%93Bernstein_theorem#Alternate_proof

to our more general situation.


For foundational reasons, we make clear which instances of function
extensionality and excluded middle are needed to conclude
Cantor-SchrÃ¶der-Bernstein for arbitrary universes ğ“¤ and ğ“¥.

Added 28th January. To better understand this proof, you may consult the blog
post

    https://homotopytypetheory.org/2020/01/26/the-cantor-schroder-bernstein-theorem-for-%e2%88%9e-groupoids/

first. However, we try to make the proof understandable as we can
here, and hopefully it should be possible to read it without reference
to the blog post.

\begin{code}

EM-gives-CantorSchrÃ¶derBernstein : funext ğ“¤ (ğ“¤ âŠ” ğ“¥)
                                 â†’ funext (ğ“¤ âŠ” ğ“¥) ğ“¤â‚€
                                 â†’ funext ğ“¤â‚€ (ğ“¤ âŠ” ğ“¥)
                                 â†’ EM (ğ“¤ âŠ” ğ“¥)
                                 â†’ CantorSchrÃ¶derBernstein ğ“¤ ğ“¥
EM-gives-CantorSchrÃ¶derBernstein {ğ“¤} {ğ“¥} fe feâ‚€ feâ‚ excluded-middle X Y (f , f-is-emb) (g , g-is-emb) =

  need (X â‰ƒ Y) which-is-given-by ğ’½

 where

  remark-f : type-of f â‰¡ (X â†’ Y)
  remark-f = by-assumption

  remark-g : type-of g â‰¡ (Y â†’ X)
  remark-g = by-assumption

\end{code}

In order to define ğ’½ : X â‰ƒ Y, we use a notion of g-point.

\begin{code}

  is-g-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  is-g-point x = (xâ‚€ : X) (n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x â†’ fiber g xâ‚€

\end{code}

What is important for our purposes is that this is property rather
than data, using the fact that g is an embedding, which means that its
fibers are all propositions.

\begin{code}

  recall : (x : X) â†’ fiber g x â‰¡ Î£ \(y : Y) â†’ g y â‰¡ x
  recall _ = by-definition

  also-recall : is-embedding g â‰¡ ((x : X) â†’ is-prop (fiber g x))
  also-recall = by-definition

\end{code}

We use the fact that propositions are closed under products, which
requires function extensionality:

\begin{code}

  being-g-point-is-a-prop : (x : X) â†’ is-prop (is-g-point x)
  being-g-point-is-a-prop x =
   Î -is-prop fe  (Î» (xâ‚€ : X                   ) â†’
   Î -is-prop feâ‚ (Î» (n  : â„•                   ) â†’
   Î -is-prop fe  (Î» (p  : ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) â†’ need (is-prop (fiber g xâ‚€))
                                                  which-is-given-by (g-is-emb xâ‚€))))
\end{code}

We collect the g-points in a subtype of X.

\begin{code}

  G-point : ğ“¤ âŠ” ğ“¥ Ì‡
  G-point = Î£ \(x : X) â†’ is-g-point x

\end{code}

By construction, considering xâ‚€ = x and n = 0, we have that g is
invertible at g-points, because, by definition, we have that
((g âˆ˜ f) ^ 0) x â‰¡ x).

\begin{code}

  g-is-invertible-at-g-points : ((x , Î³) : G-point) â†’ fiber g x
  g-is-invertible-at-g-points (x , Î³) = Î³ x 0 (by-definition âˆ¶ ((g âˆ˜ f) ^ 0) x â‰¡ x)

\end{code}

The fiber point is given by the first projection of the fiber:

\begin{code}

  gâ»Â¹ : G-point â†’ Y
  gâ»Â¹ (x , Î³) = fiber-point g x (g-is-invertible-at-g-points (x , Î³))

\end{code}

Because being a g-point is property, we can apply excluded middle to
it:

\begin{code}

  recall-the-notion-of-decidability : {ğ“¦ : Universe} {A : ğ“¦ Ì‡ } â†’ decidable A â‰¡ (A + Â¬ A)
  recall-the-notion-of-decidability = by-definition

  Î´ : (x : X) â†’ decidable (is-g-point x)
  Î´ x = excluded-middle (is-g-point x) (being-g-point-is-a-prop x)

\end{code}

The rest of the proof consists in showing that the following function
is an equivalence:

\begin{code}

  h : X â†’ Y
  h x = Cases (Î´ x)
        (Î» (Î³ :   is-g-point x) â†’ gâ»Â¹ (x , Î³))
        (Î» (Î½ : Â¬ is-g-point x) â†’ f x)

\end{code}

For that purpose, it is enough to show that it is left-cancellable and
split-surjective.

To show that it is left-cancellable, we first show that gâ»Â¹ is a
two-sided inverse in its domain of definition.

That it is a right inverse follows from the definition of fiber, by
taking the fiber path, which is given by the second projection:

\begin{code}

  gâ»Â¹-is-rinv : ((x , Î³) : G-point) â†’ g (gâ»Â¹ (x , Î³)) â‰¡ x
  gâ»Â¹-is-rinv (x , Î³) = fiber-path g x (g-is-invertible-at-g-points (x , Î³))

\end{code}

That it is a left inverse follows from the above and the fact that g,
being an embedding, is left-cancellable:

\begin{code}

  gâ»Â¹-is-linv : (y : Y) (Î³ : is-g-point (g y)) â†’ gâ»Â¹ (g y , Î³) â‰¡ y
  gâ»Â¹-is-linv y Î³ = have (g (gâ»Â¹ (g y , Î³)) â‰¡âŸ¨ gâ»Â¹-is-rinv (g y , Î³) âŸ©
                          g y               âˆ)
                    so-apply embeddings-are-left-cancellable g g-is-emb

\end{code}

We also need the following two facts to establish the
left-cancellability of h:

\begin{code}

  Î± : (x : X) â†’ is-g-point (g (f x)) â†’ is-g-point x
  Î± x Î³ = need is-g-point x
          which-is-given-by
           assume xâ‚€ âˆ¶ X                    then
           assume n  âˆ¶ â„•                    then
           assume p  âˆ¶ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x then
            (need fiber g xâ‚€
             which-is-given-by
               have ap (g âˆ˜ f) p âˆ¶ ((g âˆ˜ f) ^ (succ n)) xâ‚€ â‰¡ g (f x)
               so-apply Î³ xâ‚€ (succ n))

  f-gâ»Â¹-disjoint-images : (x : X) â†’ Â¬ is-g-point x â†’ ((x' , Î³) : G-point) â†’ f x â‰¢ gâ»Â¹ (x' , Î³)
  f-gâ»Â¹-disjoint-images x Î½ (x' , Î³) p = have p âˆ¶ f x â‰¡ gâ»Â¹ (x' , Î³)
                                         so need contradiction
                                            which-is-given-by
                                             have Î³ âˆ¶ is-g-point x'
                                             which-is-impossible-by (v âˆ¶ Â¬ is-g-point x')
   where
    q : g (f x) â‰¡ x'
    q = have p âˆ¶ f x â‰¡ gâ»Â¹ (x' , Î³)
        so-use (g (f x)          â‰¡âŸ¨ ap g p                âŸ©
                g (gâ»Â¹ (x' , Î³)) â‰¡âŸ¨ gâ»Â¹-is-rinv (x' , Î³)  âŸ©
                x'               âˆ)
    u : Â¬ is-g-point (g (f x))
    u = have Î½ âˆ¶ Â¬ is-g-point x
        so-apply contrapositive (Î± x)
    v : Â¬ is-g-point x'
    v = transport (Î» - â†’ Â¬ is-g-point -) q u

\end{code}

It is convenient to work with the following auxiliary function H and
prove properties of H and then specialize them to h:

\begin{code}

  H : (x : X) â†’ decidable (is-g-point x) â†’ Y
  H x d = Cases d
           (Î» (Î³ :   is-g-point x) â†’ gâ»Â¹ (x , Î³))
           (Î» (Î½ : Â¬ is-g-point x) â†’ f x)

  notice-that : h â‰¡ Î» x â†’ H x (Î´ x)
  notice-that = by-definition

  h-lc : left-cancellable h
  h-lc {x} {x'} = l (Î´ x) (Î´ x')
   where
    l : (d : decidable (is-g-point x)) (d' : decidable (is-g-point x')) â†’ H x d â‰¡ H x' d' â†’ x â‰¡ x'

    l (inl Î³) (inl Î³') p = have p âˆ¶ gâ»Â¹ (x , Î³) â‰¡ gâ»Â¹ (x' , Î³')
                           so (x                 â‰¡âŸ¨ (gâ»Â¹-is-rinv (x , Î³))â»Â¹ âŸ©
                               g (gâ»Â¹ (x  , Î³ )) â‰¡âŸ¨ ap g p                  âŸ©
                               g (gâ»Â¹ (x' , Î³')) â‰¡âŸ¨ gâ»Â¹-is-rinv (x' , Î³')   âŸ©
                               x'                âˆ)

    l (inl Î³) (inr Î½') p = have p âˆ¶ gâ»Â¹ (x , Î³) â‰¡ f x'
                           which-is-impossible-by Î» - â†’ f-gâ»Â¹-disjoint-images x' Î½' (x , Î³) (- â»Â¹)

    l (inr Î½) (inl Î³') p = have p âˆ¶ f x â‰¡ gâ»Â¹ (x' , Î³')
                           which-is-impossible-by f-gâ»Â¹-disjoint-images x Î½ (x' , Î³')

    l (inr Î½) (inr Î½') p = have p âˆ¶ f x â‰¡ f x'
                           so-apply embeddings-are-left-cancellable f f-is-emb

\end{code}

Next we want to show that h is split surjective. For that purpose, we
define the notion of f-point, which is data rather than property (as
several xâ‚€ and n are possible answers in general).

(In particular, excluded middle can't be applied to the type
f-point x, because excluded middle applies only to truth values.)

\begin{code}

  f-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  f-point x = Î£ \(xâ‚€ : X) â†’ (Î£ \(n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) Ã— Â¬ fiber g xâ‚€

\end{code}

What is important for our argument is that non-f-points are g-points:

\begin{code}

  non-f-point-is-g-point : (x : X) â†’ Â¬ f-point x â†’ is-g-point x
  non-f-point-is-g-point x Î½ xâ‚€ n p = need (fiber g xâ‚€) which-is-given-by
    (Cases (excluded-middle (fiber g xâ‚€) (g-is-emb xâ‚€))
      (Î» (Ïƒ :   fiber g xâ‚€) â†’ Ïƒ)
      (Î» (u : Â¬ fiber g xâ‚€) â†’ have (xâ‚€ , (n , p) , u) âˆ¶ f-point x
                              which-is-impossible-by (Î½ âˆ¶ Â¬ f-point x)))

\end{code}

We use the notion of f-point to prove the following, whose statement
doesn't refer to the notion of f-point.

\begin{code}

  claim : (y : Y) â†’ Â¬ is-g-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
  claim y Î½ = v
   where
    i : Â¬Â¬ f-point (g y)
    i = have Î½ âˆ¶ Â¬ is-g-point (g y)
        so-apply contrapositive (non-f-point-is-g-point (g y))

    ii : f-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
    ii (xâ‚€ , (0 , p) , u) = have p âˆ¶ xâ‚€ â‰¡ g y
                            so have (y , (p â»Â¹)) âˆ¶ fiber g xâ‚€
                               which-is-impossible-by (u âˆ¶ Â¬ fiber g xâ‚€)
    ii (xâ‚€ , (succ n , p) , u) = a , b
     where
      q : f (((g âˆ˜ f) ^ n) xâ‚€) â‰¡ y
      q = have p âˆ¶ ((g âˆ˜ f) ^ (succ n)) xâ‚€  â‰¡ g y
                 âˆ¶ g (f (((g âˆ˜ f) ^ n) xâ‚€)) â‰¡ g y
          so-apply embeddings-are-left-cancellable g g-is-emb
      a : fiber f y
      a = ((g âˆ˜ f) ^ n) xâ‚€ , q
      b : Â¬ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
      b = assume Î³ âˆ¶ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
          then (have Î³ xâ‚€ n refl âˆ¶ fiber g xâ‚€
                which-is-impossible-by (u âˆ¶ Â¬ fiber g xâ‚€))

    iii : Â¬Â¬ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
    iii = double-contrapositive ii i

    iv : is-prop (Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x)
    iv = have f-is-emb y âˆ¶ is-prop (fiber f y)
         so-apply subtype-of-prop-is-a-prop prâ‚ (prâ‚-lc (Î» {Ïƒ} â†’ negations-are-props feâ‚€))

    v : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
    v = double-negation-elimination excluded-middle _ iv iii

\end{code}

With this we are ready to show that h is a split surjection. The idea
is that, given y : Y, we check whether g y is a g-point or not, and if
it is we map it to g y, and otherwise we map y to the point x : X
given by the above claim. But then, of course, we also need to argue
that this works. As above, we use the auxiliary function H for that
purpose.

\begin{code}

  h-split-surjection : (y : Y) â†’ Î£ \(x : X) â†’ h x â‰¡ y
  h-split-surjection y = x , p
   where
    a : decidable (is-g-point (g y)) â†’ Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    a (inl Î³) = g y , Ïˆ
     where
      Ïˆ : (d : decidable (is-g-point (g y))) â†’ H (g y) d â‰¡ y
      Ïˆ (inl Î³') = H (g y) (inl Î³') â‰¡âŸ¨ by-definition    âŸ©
                   gâ»Â¹ (g y , Î³')   â‰¡âŸ¨ gâ»Â¹-is-linv y Î³' âŸ©
                   y                âˆ
      Ïˆ (inr Î½)  = have Î½ âˆ¶ Â¬ is-g-point (g y)
                   which-contradicts (Î³ âˆ¶ is-g-point (g y))
    a (inr Î½) = x , Ïˆ
     where
      w : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
      w = have Î½ âˆ¶ Â¬ is-g-point (g y)
          so-apply claim y
      x : X
      x = fiber-point f y (prâ‚ w)
      p : f x â‰¡ y
      p = fiber-path f y (prâ‚ w)
      Ïˆ : (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
      Ïˆ (inl Î³) = have Î³ âˆ¶ is-g-point x
                  which-is-impossible-by (prâ‚‚ w âˆ¶ Â¬ is-g-point x)
      Ïˆ (inr Î½) = H x (inr Î½) â‰¡âŸ¨ by-definition âŸ©
                  f x         â‰¡âŸ¨ p             âŸ©
                  y           âˆ
    b : Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    b = a (Î´ (g y))
    x : X
    x = prâ‚ b
    p : h x â‰¡ y
    p = prâ‚‚ b (Î´ x)

\end{code}

And because left-cancellable split surjections are equivalences, we
are done:

\begin{code}

  ğ’½ : X â‰ƒ Y
  ğ’½ = h , lc-split-surjections-are-equivs h h-lc h-split-surjection

\end{code}

We record the following special case:

\begin{code}

EM-gives-CantorSchrÃ¶derBernsteinâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
                                  â†’ EM ğ“¤â‚€
                                  â†’ CantorSchrÃ¶derBernstein ğ“¤â‚€ ğ“¤â‚€
EM-gives-CantorSchrÃ¶derBernsteinâ‚€ fe = EM-gives-CantorSchrÃ¶derBernstein fe fe fe

\end{code}


APPENDIX
--------

The above is an attempt to make the proof more readable and match the
blog post. Here is a more concise version of the above in a more
direct Agda style which some will prefer (and which could be made even
more concise by avoiding auxiliary definitions used for the purpose of
indicating types explicitly).

\begin{code}

EM-gives-CantorSchrÃ¶derBernstein' : funext ğ“¤ (ğ“¤ âŠ” ğ“¥)
                                  â†’ funext (ğ“¤ âŠ” ğ“¥) ğ“¤â‚€
                                  â†’ funext ğ“¤â‚€ (ğ“¤ âŠ” ğ“¥)
                                  â†’ EM (ğ“¤ âŠ” ğ“¥)
                                  â†’ CantorSchrÃ¶derBernstein ğ“¤ ğ“¥
EM-gives-CantorSchrÃ¶derBernstein' {ğ“¤} {ğ“¥} fe feâ‚€ feâ‚ excluded-middle X Y (f , f-is-emb) (g , g-is-emb) = ğ’½
 where
  is-g-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  is-g-point x = (xâ‚€ : X) (n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x â†’ fiber g xâ‚€

  G-point : ğ“¤ âŠ” ğ“¥ Ì‡
  G-point = Î£ \(x : X) â†’ is-g-point x

  g-is-invertible-at-g-points : ((x , Î³) : G-point) â†’ fiber g x
  g-is-invertible-at-g-points (x , Î³) = Î³ x 0 refl

  gâ»Â¹ : G-point â†’ Y
  gâ»Â¹ (x , Î³) = fiber-point g x (g-is-invertible-at-g-points (x , Î³))

  gâ»Â¹-is-rinv : ((x , Î³) : G-point) â†’ g (gâ»Â¹ (x , Î³)) â‰¡ x
  gâ»Â¹-is-rinv (x , Î³) = fiber-path g x (g-is-invertible-at-g-points (x , Î³))

  gâ»Â¹-is-linv : (y : Y) (Î³ : is-g-point (g y)) â†’ gâ»Â¹ (g y , Î³) â‰¡ y
  gâ»Â¹-is-linv y Î³ = embeddings-are-left-cancellable g g-is-emb (gâ»Â¹-is-rinv (g y , Î³))

  Î± : (x : X) â†’ is-g-point (g (f x)) â†’ is-g-point x
  Î± x Î³ xâ‚€ n p = Î³ xâ‚€ (succ n) (ap (g âˆ˜ f) p)

  f-gâ»Â¹-disjoint-images : (x : X) â†’ Â¬ is-g-point x â†’ ((x' , Î³) : G-point) â†’ f x â‰¢ gâ»Â¹ (x' , Î³)
  f-gâ»Â¹-disjoint-images x Î½ (x' , Î³) p = ğŸ˜-elim (v Î³)
   where
    q = g (f x)          â‰¡âŸ¨ ap g p                âŸ©
        g (gâ»Â¹ (x' , Î³)) â‰¡âŸ¨ gâ»Â¹-is-rinv (x' , Î³)  âŸ©
        x'               âˆ
    u : Â¬ is-g-point (g (f x))
    u = contrapositive (Î± x) Î½
    v : Â¬ is-g-point x'
    v = transport (Î» - â†’ Â¬ is-g-point -) q u

  being-g-point-is-a-prop : (x : X) â†’ is-prop (is-g-point x)
  being-g-point-is-a-prop x = Î -is-prop fe (Î» xâ‚€ â†’ Î -is-prop feâ‚ (Î» _ â†’ Î -is-prop fe (Î» _ â†’ g-is-emb xâ‚€)))

  Î´ : (x : X) â†’ decidable (is-g-point x)
  Î´ x = excluded-middle (is-g-point x) (being-g-point-is-a-prop x)

  H : (x : X) â†’ decidable (is-g-point x) â†’ Y
  H x (inl Î³) = gâ»Â¹ (x , Î³)
  H x (inr _) = f x

  h : X â†’ Y
  h x = H x (Î´ x)

  h-lc : left-cancellable h
  h-lc {x} {x'} = l (Î´ x) (Î´ x')
   where
    l : (d : decidable (is-g-point x)) (d' : decidable (is-g-point x')) â†’ H x d â‰¡ H x' d' â†’ x â‰¡ x'
    l (inl Î³) (inl Î³') p = x                 â‰¡âŸ¨ (gâ»Â¹-is-rinv (x , Î³))â»Â¹ âŸ©
                           g (gâ»Â¹ (x  , Î³ )) â‰¡âŸ¨ ap g p                  âŸ©
                           g (gâ»Â¹ (x' , Î³')) â‰¡âŸ¨ gâ»Â¹-is-rinv (x' , Î³')   âŸ©
                           x'                âˆ
    l (inl Î³) (inr Î½') p = ğŸ˜-elim(f-gâ»Â¹-disjoint-images x' Î½' (x  , Î³ )(p â»Â¹))
    l (inr Î½) (inl Î³') p = ğŸ˜-elim(f-gâ»Â¹-disjoint-images x  Î½  (x' , Î³') p)
    l (inr Î½) (inr Î½') p = embeddings-are-left-cancellable f f-is-emb p

  f-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  f-point x = Î£ \(xâ‚€ : X) â†’ (Î£ \(n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) Ã— Â¬ fiber g xâ‚€

  non-f-point-is-g-point : (x : X) â†’ Â¬ f-point x â†’ is-g-point x
  non-f-point-is-g-point x Î½ xâ‚€ n p =
   Cases (excluded-middle (fiber g xâ‚€) (g-is-emb xâ‚€))
    (Î» (Ïƒ :   fiber g xâ‚€) â†’ Ïƒ)
    (Î» (u : Â¬ fiber g xâ‚€) â†’ ğŸ˜-elim(Î½ (xâ‚€ , (n , p) , u)))

  claim : (y : Y) â†’ Â¬ is-g-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
  claim y Î½ = v
   where
   i : Â¬Â¬ f-point (g y)
   i = contrapositive (non-f-point-is-g-point (g y)) Î½

   ii : f-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   ii (xâ‚€ , (0      , p) , u) = ğŸ˜-elim (u (y , (p â»Â¹)))
   ii (xâ‚€ , (succ n , p) , u) = a , b
    where
     q : f (((g âˆ˜ f) ^ n) xâ‚€) â‰¡ y
     q = embeddings-are-left-cancellable g g-is-emb p
     a : fiber f y
     a = ((g âˆ˜ f) ^ n) xâ‚€ , q
     b : Â¬ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
     b Î³ = ğŸ˜-elim (u (Î³ xâ‚€ n refl))

   iii : Â¬Â¬ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   iii = double-contrapositive ii i

   iv : is-prop (Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x)
   iv = subtype-of-prop-is-a-prop prâ‚ (prâ‚-lc (Î» {Ïƒ} â†’ negations-are-props feâ‚€)) (f-is-emb y)

   v : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   v = double-negation-elimination excluded-middle _ iv iii

  h-split-surjection : (y : Y) â†’ Î£ \(x : X) â†’ h x â‰¡ y
  h-split-surjection y = x , p
   where
    a : decidable (is-g-point (g y)) â†’ Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    a (inl Î³) = g y , Ïˆ
     where
      Ïˆ : (d : decidable (is-g-point (g y))) â†’ H (g y) d â‰¡ y
      Ïˆ (inl Î³') = gâ»Â¹-is-linv y Î³'
      Ïˆ (inr Î½)  = ğŸ˜-elim (Î½ Î³)
    a (inr Î½) = x , Ïˆ
     where
      w : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
      w = claim y Î½
      x : X
      x = fiber-point f y (prâ‚ w)
      Ïˆ : (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
      Ïˆ (inl Î³) = ğŸ˜-elim (prâ‚‚ w Î³)
      Ïˆ (inr Î½) = fiber-path f y (prâ‚ w)

    b : Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    b = a (Î´ (g y))
    x : X
    x = prâ‚ b
    p : h x â‰¡ y
    p = prâ‚‚ b (Î´ x)

  ğ’½ : X â‰ƒ Y
  ğ’½ = h , lc-split-surjections-are-equivs h h-lc h-split-surjection

\end{code}

Check our lecture notes https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/
if you want to learn HoTT/UF and Agda.
