Martin Escardo, 22nd January 2020

This file has two parts, which assume function extensionality but not
univalence or the existence of propositional truncations:


(1) A univalent-foundations version of Pierre Pradic and Chad
    E. Brown's argument that Cantor-SchrÃ¶der-Bernstein implies
    excluded middle in constructive set theory
    (https://arxiv.org/abs/1904.09193).

    Their proof, reproduced here, uses the compactness (also known as
    the searchability or omniscience) of â„•âˆ.


(2) A proof that excluded middle implies Cantor-SchrÃ¶der-Bernstein for
    all homotopy types, or âˆ-groupoids. (Added 24th January.)

    For any pair of types, if each one is embedded into the other,
    then they are equivalent.

    For this it is crucial that a map is an embedding if and only if
    its fibers are all propositions (rather than merely the map being
    left-cancellable).

    As far as we know, (2) is a new result.

NB. This file needs the Agda release candidate 2.6.1.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module CantorSchroederBernstein where

open import SpartanMLTT
open import GenericConvergentSequence
open import DecidableAndDetachable
open import Plus-Properties
open import CompactTypes
open import ConvergentSequenceCompact
open import UF-Subsingletons
open import UF-Equiv
open import UF-Embeddings
open import UF-Retracts
open import UF-FunExt
open import UF-Subsingletons-FunExt
open import UF-ExcludedMiddle

\end{code}

Our formulation of Cantor-SchrÃ¶der-Bernstein:

\begin{code}

CSB : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
CSB X Y = (X â†ª Y) â†’ (Y â†ª X) â†’ X â‰ƒ Y

CantorSchrÃ¶derBernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
CantorSchrÃ¶derBernstein ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ CSB X Y

\end{code}

Part 1
------

The following is Lemma 7 of the above reference, using retractions
rather than surjections, for simplicity:

\begin{code}

Pradic-Brown-lemma : {X : ğ“¤ Ì‡ } {A : ğ“¥ Ì‡ }
                   â†’ retract (A + X) of X
                   â†’ Compact X
                   â†’ decidable A
Pradic-Brown-lemma {ğ“¤} {ğ“¥} {X} {A} (r , s , Î·) c = Î³ e
 where
  P : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x = Î£ \(a : A) â†’ r x â‰¡ inl a

  d : (x : X) â†’ decidable (P x)
  d x = equality-cases (r x)
         (Î» (a : A) (u : r x â‰¡ inl a) â†’ inl (a , u))
         (Î» (y : X) (v : r x â‰¡ inr y) â†’ inr (Î» (a , u) â†’ +disjoint (inl a â‰¡âŸ¨ u â»Â¹ âŸ©
                                                                    r x   â‰¡âŸ¨ v    âŸ©
                                                                    inr y âˆ)))

  e : decidable (Î£ \(x : X) â†’ P x)
  e = c P d

  f : A â†’ Î£ \(x : X) â†’ P x
  f a = s (inl a) , a , Î· (inl a)

  Î³ : decidable (Î£ \(x : X) â†’ P x) â†’ decidable A
  Î³ (inl (x , a , u)) = inl a
  Î³ (inr Ï†)           = inr (contrapositive f Ï†)

\end{code}

Function extensionality is used twice in the following, once to know
that â„•âˆ is a set, and once to know that it is compact.

\begin{code}

CSB-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
             â†’ (P : ğ“¤ Ì‡ )
             â†’ is-prop P
             â†’ CSB â„•âˆ (P + â„•âˆ)
             â†’ P + Â¬ P
CSB-gives-EM fe P i csb = Î³
 where
  f : â„•âˆ â†’ P + â„•âˆ
  f = inr

  j : is-embedding f
  j = inr-is-embedding P â„•âˆ

  z : P â†’ â„•âˆ
  z _ = Zero

  g : P + â„•âˆ â†’ â„•âˆ
  g = cases z Succ

  a : is-embedding z
  a = maps-of-props-into-sets-are-embeddings (Î» p â†’ Zero) i (â„•âˆ-is-set fe)

  b : is-embedding Succ
  b = lc-maps-into-sets-are-embeddings Succ Succ-lc (â„•âˆ-is-set fe)

  c : disjoint-images z Succ
  c = Î» (p : P) (x : â„•âˆ) (q : Zero â‰¡ Succ x) â†’ Zero-not-Succ q

  k : is-embedding g
  k = disjoint-cases-embedding z Succ a b c

  e : â„•âˆ â‰ƒ P + â„•âˆ
  e = csb (f , j) (g , k)

  Ï : retract (P + â„•âˆ) of â„•âˆ
  Ï = equiv-retract-r e

  Î³ : P + Â¬ P
  Î³ = Pradic-Brown-lemma Ï (â„•âˆ-Compact fe)

\end{code}

Hence if we assume Cantor-SchrÃ¶der-Bernstein for the first universe ğ“¤â‚€
and an arbitrary universe ğ“¥, as formulated above, then we get excluded
middle for propositions in the universe ğ“¥:

\begin{code}

CantorSchrÃ¶derBernstein-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
                                 â†’ CantorSchrÃ¶derBernstein ğ“¤â‚€ ğ“¥
                                 â†’ EM ğ“¥
CantorSchrÃ¶derBernstein-gives-EM fe csb P i = CSB-gives-EM fe P i csb

\end{code}

Remark. If instead of requiring that we have a designated equivalence,
we required that there is an unspecified equivalence in the
formulation of Cantor-SchrÃ¶der-Bernstein, we would still get excluded
middle, because P + Â¬ P is a proposition.


Part 2
------

The Cantor-SchrÃ¶der-Bernstein Theorem holds for all homotopy types, or
âˆ-gropoids, in the presence of excluded middle. It is crucial here
that embeddings have subsingleton fibers, so that e.g. the function
is-g-point defined in the proof is property rather than data and hence
we can apply univalent excluded middle to it. It is also worth
remembering, for the sake of comparing the classical result for sets
with its generalization to âˆ-groupoids, that a map of types that are
sets is an embedding if and only if it is left-cancellable.


Our proof adapts Wikipedia's "alternate proof" (consulted 23rd January 2020)

  https://en.wikipedia.org/wiki/Schr%C3%B6der%E2%80%93Bernstein_theorem#Alternate_proof

to our more general situation.


For foundational reasons, we make clear which instances of function
extensionality and excluded middle are needed to conclude
Cantor-SchrÃ¶der-Bernstein for arbitrary universes ğ“¤ and ğ“¥.

\begin{code}

EM-gives-CantorSchrÃ¶derBernstein : funext ğ“¤ (ğ“¤ âŠ” ğ“¥)
                                 â†’ funext (ğ“¤ âŠ” ğ“¥) ğ“¤â‚€
                                 â†’ funext ğ“¤â‚€ (ğ“¤ âŠ” ğ“¥)
                                 â†’ EM (ğ“¤ âŠ” ğ“¥)
                                 â†’ CantorSchrÃ¶derBernstein ğ“¤ ğ“¥
EM-gives-CantorSchrÃ¶derBernstein {ğ“¤} {ğ“¥} fe feâ‚€ feâ‚ em {X} {Y} (f , f-is-emb) (g , g-is-emb) = ğ“±
 where
  is-g-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  is-g-point x = (xâ‚€ : X) (n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x â†’ fiber g xâ‚€

  g-is-invertible-at-g-points : (x : X) â†’ is-g-point x â†’ fiber g x
  g-is-invertible-at-g-points x Î³ = Î³ x 0 refl

  gâ»Â¹ : (x : X) â†’ is-g-point x â†’ Y
  gâ»Â¹ x Î³ = fiber-point g x (g-is-invertible-at-g-points x Î³)

  gâ»Â¹-is-rinv : (x : X) (Î³ : is-g-point x) â†’ g (gâ»Â¹ x Î³) â‰¡ x
  gâ»Â¹-is-rinv x Î³ = fiber-path g x (g-is-invertible-at-g-points x Î³)

  gâ»Â¹-is-linv : (y : Y) (Î³ : is-g-point (g y)) â†’ gâ»Â¹ (g y) Î³ â‰¡ y
  gâ»Â¹-is-linv y Î³ = apply (embedding-lc g g-is-emb)
                    to (gâ»Â¹-is-rinv (g y) Î³ âˆ¶ g (gâ»Â¹ (g y) Î³) â‰¡ g y)

  being-g-point-is-a-prop : (x : X) â†’ is-prop (is-g-point x)
  being-g-point-is-a-prop x = Î -is-prop fe  (Î» (xâ‚€ : X                   ) â†’
                              Î -is-prop feâ‚ (Î» (n  : â„•                   ) â†’
                              Î -is-prop fe  (Î» (p  : ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) â†’ g-is-emb xâ‚€)))

  Î´ : (x : X) â†’ decidable (is-g-point x)
  Î´ x = em (is-g-point x) (being-g-point-is-a-prop x)

  H : (x : X) â†’ decidable (is-g-point x) â†’ Y
  H x d = Cases d
           (Î» (Î³ :   is-g-point x) â†’ gâ»Â¹ x Î³)
           (Î» (Î½ : Â¬ is-g-point x) â†’ f x)

  h : X â†’ Y
  h x = H x (Î´ x)

  Î± : (x : X) â†’ is-g-point (g (f x)) â†’ is-g-point x
  Î± x Î³ xâ‚€ n p = Î³ xâ‚€ (succ n) (ap (g âˆ˜ f) p)

  f-gâ»Â¹-disjoint-images : (x x' : X) â†’ Â¬ is-g-point x â†’ (Î³ : is-g-point x') â†’ f x â‰¢ gâ»Â¹ x' Î³
  f-gâ»Â¹-disjoint-images x x' Î½ Î³ p = have (v âˆ¶ Â¬ is-g-point x')
                                     which-contradicts Î³
   where
    u : Â¬ is-g-point (g (f x))
    u = contrapositive (Î± x) Î½
    q : g (f x) â‰¡ x'
    q = have (p âˆ¶ f x â‰¡ gâ»Â¹ x' Î³)
        so-use (g (f x)      â‰¡âŸ¨ ap g p            âŸ©
                g (gâ»Â¹ x' Î³) â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³  âŸ©
                x'           âˆ)
    v : Â¬ is-g-point x'
    v = transport (Î» - â†’ Â¬ is-g-point -) q u

  h-lc : left-cancellable h
  h-lc {x} {x'} = l (Î´ x) (Î´ x')
   where
    l : (d : decidable (is-g-point x)) (d' : decidable (is-g-point x'))
      â†’ H x d â‰¡ H x' d' â†’ x â‰¡ x'

    l (inl Î³) (inl Î³') p = have (p âˆ¶ gâ»Â¹ x Î³ â‰¡ gâ»Â¹ x' Î³')
                           so (x             â‰¡âŸ¨ (gâ»Â¹-is-rinv x Î³)â»Â¹ âŸ©
                               g (gâ»Â¹ x Î³)   â‰¡âŸ¨ ap g p              âŸ©
                               g (gâ»Â¹ x' Î³') â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³'   âŸ©
                               x'            âˆ)

    l (inl Î³) (inr Î½') p = have (p âˆ¶ gâ»Â¹ x Î³ â‰¡ f x')
                           which-is-impossible-by (Î» - â†’ f-gâ»Â¹-disjoint-images x' x Î½' Î³ (- â»Â¹))

    l (inr Î½) (inl Î³') p = have (p âˆ¶ f x â‰¡ gâ»Â¹ x' Î³')
                           which-is-impossible-by (f-gâ»Â¹-disjoint-images x x' Î½ Î³')

    l (inr Î½) (inr Î½') p = have (p âˆ¶ f x â‰¡ f x')
                           so (embedding-lc f f-is-emb p âˆ¶ x â‰¡ x')

  f-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  f-point x = Î£ \(xâ‚€ : X) â†’ (Î£ \(n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) Ã— Â¬ fiber g xâ‚€

  non-f-point-is-g-point : (x : X) â†’ Â¬ f-point x â†’ is-g-point x
  non-f-point-is-g-point x Î½ xâ‚€ n p = Cases (em (fiber g xâ‚€) (g-is-emb xâ‚€))
                                       (Î» (Ïƒ :   fiber g xâ‚€) â†’ Ïƒ)
                                       (Î» (u : Â¬ fiber g xâ‚€) â†’ have ((xâ‚€ , (n , p) , u) âˆ¶ f-point x)
                                                               which-is-impossible-by (Î½ âˆ¶ Â¬ f-point x))

  claim : (y : Y) â†’ Â¬ is-g-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
  claim y u = v
   where
   i : Â¬Â¬ f-point (g y)
   i = contrapositive (non-f-point-is-g-point (g y)) u

   ii : f-point (g y) â†’ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   ii (xâ‚€ , (0 , p) , Î½) = have (p âˆ¶ xâ‚€ â‰¡ g y) which-is-impossible-by a
    where
     a : xâ‚€ â‰¢ g y
     a = assume p âˆ¶ xâ‚€ â‰¡ g y
         then (have ((y , (p â»Â¹)) âˆ¶ fiber g xâ‚€)
               which-is-impossible-by (Î½ âˆ¶ Â¬ fiber g xâ‚€))
   ii (xâ‚€ , (succ n , p) , Î½) = a , b
    where
     q : f (((g âˆ˜ f) ^ n) xâ‚€) â‰¡ y
     q = have (p âˆ¶ ((g âˆ˜ f) ^ (succ n)) xâ‚€ â‰¡ g y
                 âˆ¶ g (f (((g âˆ˜ f) ^ n) xâ‚€)) â‰¡ g y)
         so-use (embedding-lc g g-is-emb p)
     a : fiber f y
     a = ((g âˆ˜ f) ^ n) xâ‚€ , q
     b : Â¬ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
     b = assume Î³ âˆ¶ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
         then (have (Î³ xâ‚€ n refl âˆ¶ fiber g xâ‚€)
               which-is-impossible-by (Î½ âˆ¶ Â¬ fiber g xâ‚€))

   iii : Â¬Â¬ Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   iii = Â¬Â¬-functor ii i

   iv : is-prop (Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x)
   iv = subtype-of-prop-is-a-prop prâ‚ (prâ‚-lc (Î» {Ïƒ} â†’ negations-are-props feâ‚€)) (f-is-emb y)

   v : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
   v = EM-gives-DNE em _ iv iii

  h-split-surjection : (y : Y) â†’ Î£ \(x : X) â†’ h x â‰¡ y
  h-split-surjection y = x , p
   where
    a : decidable (is-g-point (g y)) â†’ Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    a (inl Î³) = g y , Ïˆ
     where
      Ïˆ : (d : decidable (is-g-point (g y))) â†’ H (g y) d â‰¡ y
      Ïˆ (inl Î³') = H (g y) (inl Î³') â‰¡âŸ¨ refl             âŸ©
                   gâ»Â¹ (g y) Î³'     â‰¡âŸ¨ gâ»Â¹-is-linv y Î³' âŸ©
                   y                âˆ
      Ïˆ (inr Î½)  = have (Î½ âˆ¶ Â¬ is-g-point (g y))
                   which-contradicts (Î³ âˆ¶ is-g-point (g y))
    a (inr Î½) = x , Ïˆ
     where
      w : Î£ \((x , p) : fiber f y) â†’ Â¬ is-g-point x
      w = apply (claim y) to (Î½ âˆ¶ Â¬ is-g-point (g y))
      x : X
      x = fiber-point f y (prâ‚ w)
      p : f x â‰¡ y
      p = fiber-path f y (prâ‚ w)
      Ïˆ : (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
      Ïˆ (inl Î³) = have (Î³ âˆ¶ is-g-point x)
                  which-is-impossible-by (prâ‚‚ w âˆ¶ Â¬ is-g-point x)
      Ïˆ (inr Î½) = H x (inr Î½) â‰¡âŸ¨ refl âŸ©
                  f x         â‰¡âŸ¨ p    âŸ©
                  y           âˆ
    b : Î£ \(x : X) â†’ (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
    b = a (Î´ (g y))
    x : X
    x = prâ‚ b
    p : h x â‰¡ y
    p = prâ‚‚ b (Î´ x)

  ğ“± : X â‰ƒ Y
  ğ“± = h , lc-split-surjections-are-equivs h h-lc h-split-surjection


EM-gives-CantorSchrÃ¶derBernsteinâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
                                  â†’ EM ğ“¤â‚€
                                  â†’ CantorSchrÃ¶derBernstein ğ“¤â‚€ ğ“¤â‚€
EM-gives-CantorSchrÃ¶derBernsteinâ‚€ fe = EM-gives-CantorSchrÃ¶derBernstein fe fe fe

\end{code}

Added 28th January. A blog post with more information in the comments
is available here:

   https://homotopytypetheory.org/2020/01/26/the-cantor-schroder-bernstein-theorem-for-%e2%88%9e-groupoids/
