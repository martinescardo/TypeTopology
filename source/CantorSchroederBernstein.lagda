Martin Escardo, 22nd and 24th January 2020, with further additions
after that.

The Cantor-SchrÃ¶der-Bernstein for homotopy types, or âˆ-groupoids, in Agda
-------------------------------------------------------------------------

An unformalized version of this file was published in
the Journal of Homotopy and Related Structures, Springer, 28th June 2021.
https://doi.org/10.1007/s40062-021-00284-6

There are two parts, which assume function extensionality but not
univalence or the existence of propositional truncations (any
assumption beyond MLTT is explicit in each claim).


(1) A univalent-foundations version of Pierre Pradic and Chad
    E. Brown's argument that Cantor-SchrÃ¶der-Bernstein implies
    excluded middle in constructive set theory. (Added 22nd January.)
    (https://arxiv.org/abs/1904.09193).

    Their proof, reproduced here, uses the compactness (also known as
    the searchability or omniscience) of â„•âˆ.

    (See also Appendix II.)


(2) A proof that excluded middle implies Cantor-SchrÃ¶der-Bernstein for
    all homotopy types, or âˆ-groupoids. (Added 24th January.)

    For any pair of types, if each one is embedded into the other,
    then they are equivalent.

    For this it is crucial that a map is an embedding if and only if
    its fibers are all propositions (rather than merely the map being
    left-cancellable).

    As far as we know, (2) is a new result.

    This part is the Agda version of https://arxiv.org/abs/2002.07079.
    Check our lecture notes to learn HoTT/UF with Agda:
    https://www.cs.bham.ac.uk/~mhe/HoTT-UF-in-Agda-Lecture-Notes/

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module CantorSchroederBernstein where

open import SpartanMLTT
open import CompactTypes
open import ConvergentSequenceCompact
open import DecidableAndDetachable
open import DiscreteAndSeparated
open import GenericConvergentSequence
open import NaturalNumbers-Properties
open import Plus-Properties
open import UF-Base
open import UF-Equiv
open import UF-Embeddings
open import UF-ExcludedMiddle
open import UF-FunExt
open import UF-Lower-FunExt
open import UF-Miscelanea
open import UF-PropTrunc
open import UF-Retracts
open import UF-Subsingletons
open import UF-Subsingletons-FunExt

\end{code}

Our formulation of Cantor-SchrÃ¶der-Bernstein:

\begin{code}

CSB : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
CSB X Y = (X â†ª Y) Ã— (Y â†ª X) â†’ X â‰ƒ Y

Cantor-SchrÃ¶der-Bernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
Cantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ CSB X Y

\end{code}

Part 1
------

The following is Lemma 7 of the above reference, using retractions
rather than surjections, for simplicity:

\begin{code}

Pradic-Brown-lemma : {X : ğ“¤ Ì‡ } {A : ğ“¥ Ì‡ }
                   â†’ retract (A + X) of X
                   â†’ Compact X
                   â†’ decidable A
Pradic-Brown-lemma {ğ“¤} {ğ“¥} {X} {A} (r , s , Î·) c = Î³ e
 where
  P : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  P x = Î£ a ê‰ A , r x â‰¡ inl a

  d : (x : X) â†’ decidable (P x)
  d x = equality-cases (r x)
         (Î» (a : A) (u : r x â‰¡ inl a) â†’ inl (a , u))
         (Î» (y : X) (v : r x â‰¡ inr y) â†’ inr (Î» (a , u) â†’ +disjoint (inl a â‰¡âŸ¨ u â»Â¹ âŸ©
                                                                    r x   â‰¡âŸ¨ v âŸ©
                                                                    inr y âˆ)))

  e : decidable (Î£ x ê‰ X , P x)
  e = c P d

  f : A â†’ Î£ x ê‰ X , P x
  f a = s (inl a) , a , Î· (inl a)

  Î³ : decidable (Î£ x ê‰ X , P x) â†’ decidable A
  Î³ (inl (x , a , u)) = inl a
  Î³ (inr Ï†)           = inr (contrapositive f Ï†)

\end{code}

We formulate the following in more generality than we need
here. Recall that a point x is h-isolated if the identity type x â‰¡ y
is a subsingleton for every y.

\begin{code}

econstruction' : {X : ğ“¤ Ì‡ } (P : ğ“¥ Ì‡ ) (z : P â†’ X) (s : X â†’ X)
               â†’ is-prop P
               â†’ ((p : P) â†’ is-h-isolated (z p))
               â†’ disjoint-images z s
               â†’ is-embedding s
               â†’ (X â†ª P + X) Ã— (P + X â†ª X)
econstruction' {ğ“¤} {ğ“¥} {X} P z s i h d e = ((f , j) , (g , k))
 where
  f : X â†’ P + X
  f = inr

  j : is-embedding f
  j = inr-is-embedding P X

  g : P + X â†’ X
  g = cases z s

  l : is-embedding z
  l = maps-of-props-into-h-isolated-points-are-embeddings z i h

  k : is-embedding g
  k = disjoint-cases-embedding z s l e d

\end{code}

The level of generality we need here is the following. Recall that a
point is x isolated if equality x â‰¡ y is decidable for every y. By
Hedberg's Theorem, every isolated point is h-isolated.

\begin{code}

econstruction : {X : ğ“¤ Ì‡ } (P : ğ“¥ Ì‡ ) (xâ‚€ : X) (s : X â†’ X)
              â†’ is-set X
              â†’ is-prop P
              â†’ is-isolated xâ‚€
              â†’ ((x : X) â†’ xâ‚€ â‰¢ s x)
              â†’ left-cancellable s
              â†’ (X â†ª P + X) Ã— (P + X â†ª X)
econstruction {ğ“¤} {ğ“¥} {X} P xâ‚€ s j i k d' lc = econstruction' P z s i h d e
 where
  z : P â†’ X
  z p = xâ‚€

  h : (p : P) â†’ is-h-isolated (z p)
  h p = isolated-is-h-isolated xâ‚€ k

  d : disjoint-images z s
  d p = d'

  e : is-embedding s
  e = lc-maps-into-sets-are-embeddings s lc j

\end{code}

The Pradic-Brown argument uses the special case X = â„•âˆ with Zero and
Succ, but, in Appendix II, we also consider X = â„• with zero and succ.

In the following, function extensionality is used to know that (1) â„•âˆ
is a set, (2) its finite elements (in particular zero) are isolated,
(3) â„•âˆ is compact.

\begin{code}

econstruction-â„•âˆ : funext ğ“¤â‚€ ğ“¤â‚€ â†’ (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ (â„•âˆ â†ª P + â„•âˆ) Ã— (P + â„•âˆ â†ª â„•âˆ)
econstruction-â„•âˆ fe P i = econstruction P Zero Succ
                           (â„•âˆ-is-set fe) i (finite-isolated fe zero) (x â†¦ Zero-not-Succ) Succ-lc

CSB-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
             â†’ (P : ğ“¤ Ì‡ )
             â†’ is-prop P
             â†’ CSB â„•âˆ (P + â„•âˆ)
             â†’ P + Â¬ P
CSB-gives-EM fe P i csb = Î³
 where
  e : â„•âˆ â‰ƒ P + â„•âˆ
  e = csb (econstruction-â„•âˆ fe P i)

  Ï : retract (P + â„•âˆ) of â„•âˆ
  Ï = â‰ƒ-gives-â–· e

  Î³ : P + Â¬ P
  Î³ = Pradic-Brown-lemma Ï (â„•âˆ-Compact fe)

\end{code}

Hence if we assume Cantor-SchrÃ¶der-Bernstein for the first universe ğ“¤â‚€
and an arbitrary universe ğ“¥, as formulated above, then we get excluded
middle for propositions in the universe ğ“¥:

\begin{code}

Cantor-SchrÃ¶der-Bernstein-gives-EM : funext ğ“¤â‚€ ğ“¤â‚€
                                   â†’ Cantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥
                                   â†’ EM ğ“¥
Cantor-SchrÃ¶der-Bernstein-gives-EM fe csb P i = CSB-gives-EM fe P i csb

\end{code}

We remark that if instead of requiring that we have a designated
equivalence, we required that there is an unspecified equivalence in
the formulation of Cantor-SchrÃ¶der-Bernstein, we would still get
excluded middle, because P + Â¬ P is a proposition if P is:

\begin{code}

module wCSB-still-gives-EM (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt public

 wCSB : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 wCSB X Y = (X â†ª Y) Ã— (Y â†ª X) â†’ âˆ¥ X â‰ƒ Y âˆ¥

 wCantor-SchrÃ¶der-Bernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 wCantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ wCSB X Y

 wCantor-SchrÃ¶der-Bernstein-gives-EM : funext ğ“¥ ğ“¤â‚€
                                     â†’ wCantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥
                                     â†’ EM ğ“¥
 wCantor-SchrÃ¶der-Bernstein-gives-EM {ğ“¥} fe w P i = Î³
  where
   feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
   feâ‚€ = lower-funext ğ“¥ ğ“¤â‚€ fe

   s : âˆ¥ â„•âˆ â‰ƒ P + â„•âˆ âˆ¥
   s = w (econstruction-â„•âˆ feâ‚€ P i)

   t : â„•âˆ â‰ƒ P + â„•âˆ â†’ P + Â¬ P
   t e = Pradic-Brown-lemma (â‰ƒ-gives-â–· e) (â„•âˆ-Compact feâ‚€)

   Î³ : P + Â¬ P
   Î³ = âˆ¥âˆ¥-rec (decidability-of-prop-is-prop fe i) t s

\end{code}

Part 2
------

The Cantor-SchrÃ¶der-Bernstein Theorem holds for all homotopy types, or
âˆ-gropoids, in the presence of excluded middle. It is crucial here
that embeddings have subsingleton fibers, so that e.g. the function
is-g-point defined in the proof is property rather than data and hence
we can apply univalent excluded middle to it. It is also worth
remembering, for the sake of comparing the classical result for sets
with its generalization to âˆ-groupoids, that a map of types that are
sets is an embedding if and only if it is left-cancellable.

Our proof adapts Halmos' proof in his book Naive Set Theory to our
more general situation.

For foundational reasons, we make clear which instances of function
extensionality and excluded middle are needed to conclude
Cantor-SchrÃ¶der-Bernstein for arbitrary universes ğ“¤ and ğ“¥.

Added 28th January. To better understand this proof, the reader may
consult the blog post

  https://homotopytypetheory.org/2020/01/26/the-cantor-schroder-bernstein-theorem-for-âˆ-groupoids/

first. However, we have tried to make the proof as understandable as
we can here, and hopefully it should be possible to read it without
reference to the blog post.

\begin{code}

EM-gives-Cantor-SchrÃ¶der-Bernstein : Fun-Ext
                                   â†’ EM (ğ“¤ âŠ” ğ“¥)
                                   â†’ Cantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥
EM-gives-Cantor-SchrÃ¶der-Bernstein {ğ“¤} {ğ“¥} fe excluded-middle {X} {Y} ((f , f-is-emb) , (g , g-is-emb)) =

  need X â‰ƒ Y which-is-given-by ğ’½

 where

  remark-f : type-of (f , f-is-emb) â‰¡ (X â†ª Y)
  remark-f = by-assumption

  remark-g : type-of (g , g-is-emb) â‰¡ (Y â†ª X)
  remark-g = by-assumption

\end{code}

In order to define ğ’½ : X â‰ƒ Y, we use a notion of g-point.

\begin{code}

  is-g-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  is-g-point x = (xâ‚€ : X) (n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x â†’ fiber g xâ‚€

\end{code}

What is important for our purposes is that this is property rather
than data, using the fact that g is an embedding, which means that its
fibers are all propositions.

\begin{code}

  recall : (x : X) â†’ fiber g x â‰¡ (Î£ y ê‰ Y , g y â‰¡ x)
  recall _ = by-definition

  also-recall : is-embedding g â‰¡ ((x : X) â†’ is-prop (fiber g x))
  also-recall = by-definition

\end{code}

We use the fact that propositions are closed under products, which
requires function extensionality:

\begin{code}

  being-g-point-is-prop : (x : X) â†’ is-prop (is-g-point x)
  being-g-point-is-prop x = Î â‚ƒ-is-prop fe
                             (Î» xâ‚€ n (p : ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x)
                               â†’ need is-prop (fiber g xâ‚€)
                                 which-is-given-by (g-is-emb xâ‚€))
\end{code}

By construction, considering xâ‚€ = x and n = 0, we have that g is
invertible at g-points, because, by definition, we have that
((g âˆ˜ f) ^ 0) x â‰¡ x).

\begin{code}

  g-is-invertible-at-g-points : (x : X) â†’ is-g-point x â†’ fiber g x
  g-is-invertible-at-g-points x Î³ = Î³ x 0 (by-definition âˆ¶ ((g âˆ˜ f) ^ 0) x â‰¡ x)

\end{code}

The fiber point is given by the first projection of the fiber:

\begin{code}

  gâ»Â¹ : (x : X) â†’ is-g-point x â†’ Y
  gâ»Â¹ x Î³ = fiber-point (g-is-invertible-at-g-points x Î³)

\end{code}

Because being a g-point is property, we can apply excluded middle to
it:

\begin{code}

  recall-the-notion-of-decidability : {ğ“¦ : Universe} {A : ğ“¦ Ì‡ } â†’ decidable A â‰¡ (A + Â¬ A)
  recall-the-notion-of-decidability = by-definition

  Î´ : (x : X) â†’ decidable (is-g-point x)
  Î´ x = excluded-middle (is-g-point x) (being-g-point-is-prop x)

\end{code}

The rest of the proof shows that the following function is an
equivalence:

\begin{code}

  h : X â†’ Y
  h x = Cases (Î´ x)
         (Î³ ê‰   is-g-point x â†¦ gâ»Â¹ x Î³)
         (Î½ ê‰ Â¬ is-g-point x â†¦ f x)

\end{code}

For that purpose, it is enough to show that it is left-cancellable and
split-surjective.

To show that it is left-cancellable, we first show that gâ»Â¹ is a
two-sided inverse in its domain of definition.

That it is a right inverse follows from the definition of fiber, by
taking the fiber identification, which is given by the second
projection:

\begin{code}

  gâ»Â¹-is-rinv : (x : X) (Î³ : is-g-point x) â†’ g (gâ»Â¹ x Î³) â‰¡ x
  gâ»Â¹-is-rinv x Î³ = fiber-identification (g-is-invertible-at-g-points x Î³)

\end{code}

That it is a left inverse follows from the above and the fact that g,
being an embedding, is left-cancellable:

\begin{code}

  gâ»Â¹-is-linv : (y : Y) (Î³ : is-g-point (g y)) â†’ gâ»Â¹ (g y) Î³ â‰¡ y
  gâ»Â¹-is-linv y Î³ = have (g (gâ»Â¹ (g y) Î³) â‰¡âŸ¨ gâ»Â¹-is-rinv (g y) Î³ âŸ©
                          g y             âˆ)
                    so-apply embeddings-are-lc g g-is-emb

\end{code}

We also need the following two facts to establish the
left-cancellability of h:

\begin{code}

  Î± : (x : X) â†’ is-g-point (g (f x)) â†’ is-g-point x
  Î± x Î³ = need is-g-point x
          which-is-given-by
           assume xâ‚€ âˆ¶ X                    and
           assume n  âˆ¶ â„•                    and
           assume p  âˆ¶ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x then
            (need fiber g xâ‚€
             which-is-given-by
              have ap (g âˆ˜ f) p âˆ¶ ((g âˆ˜ f) ^ (succ n)) xâ‚€ â‰¡ g (f x)
              so-apply Î³ xâ‚€ (succ n))

  f-gâ»Â¹-disjoint-images : (x x' : X)
                        â†’ Â¬ is-g-point x
                        â†’ (Î³ : is-g-point x')
                        â†’ f x â‰¢ gâ»Â¹ x' Î³
  f-gâ»Â¹-disjoint-images x x' Î½ Î³ p = have p âˆ¶ f x â‰¡ gâ»Â¹ x' Î³
                                     so need contradiction
                                        which-is-given-by
                                         have Î³ âˆ¶ is-g-point x'
                                         which-is-impossible-by (v âˆ¶ Â¬ is-g-point x')
   where
    q : g (f x) â‰¡ x'
    q = have p âˆ¶ f x â‰¡ gâ»Â¹ x' Î³
        so-use (g (f x)      â‰¡âŸ¨ ap g p âŸ©
                g (gâ»Â¹ x' Î³) â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³ âŸ©
                x'           âˆ)

    u : Â¬ is-g-point (g (f x))
    u = have Î½ âˆ¶ Â¬ is-g-point x
        so-apply contrapositive (Î± x)

    v : Â¬ is-g-point x'
    v = transport (- â†¦ Â¬ is-g-point -) q u

\end{code}

It is convenient to work with the following auxiliary function H and
prove properties of H and then specialize them to h:

\begin{code}

  H : (x : X) â†’ decidable (is-g-point x) â†’ Y
  H x d = Cases d
           (Î³ ê‰   is-g-point x â†¦ gâ»Â¹ x Î³)
           (Î½ ê‰ Â¬ is-g-point x â†¦ f x)

  notice-that : h â‰¡ x â†¦ H x (Î´ x)
  notice-that = by-definition

  h-lc : left-cancellable h
  h-lc {x} {x'} = l (Î´ x) (Î´ x')
   where
    l : (d : decidable (is-g-point x)) (d' : decidable (is-g-point x')) â†’ H x d â‰¡ H x' d' â†’ x â‰¡ x'

    l (inl Î³) (inl Î³') p = have p âˆ¶ gâ»Â¹ x Î³ â‰¡ gâ»Â¹ x' Î³'
                           so (x             â‰¡âŸ¨ (gâ»Â¹-is-rinv x Î³)â»Â¹ âŸ©
                               g (gâ»Â¹ x Î³)   â‰¡âŸ¨ ap g p âŸ©
                               g (gâ»Â¹ x' Î³') â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³' âŸ©
                               x'            âˆ)

    l (inl Î³) (inr Î½') p = have p âˆ¶ gâ»Â¹ x Î³ â‰¡ f x'
                           which-is-impossible-by (- â†¦ f-gâ»Â¹-disjoint-images x' x Î½' Î³ (- â»Â¹))

    l (inr Î½) (inl Î³') p = have p âˆ¶ f x â‰¡ gâ»Â¹ x' Î³'
                           which-is-impossible-by f-gâ»Â¹-disjoint-images x x' Î½ Î³'

    l (inr Î½) (inr Î½') p = have p âˆ¶ f x â‰¡ f x'
                           so-apply embeddings-are-lc f f-is-emb

\end{code}

Next we want to show that h is split surjective. For that purpose, we
define the notion of f-point, which is data rather than property (as
several xâ‚€ and n are possible answers in general).

(In particular, excluded middle can't be applied to the type
f-point x, because excluded middle applies only to truth values.)

\begin{code}

  f-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  f-point x = Î£ xâ‚€ ê‰ X , (Î£ n ê‰ â„• , ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) Ã— Â¬ fiber g xâ‚€

\end{code}

What is important for our argument is that non-f-points are g-points:

\begin{code}

  non-f-point-is-g-point : (x : X) â†’ Â¬ f-point x â†’ is-g-point x
  non-f-point-is-g-point x Î½ xâ‚€ n p = need fiber g xâ‚€ which-is-given-by
    (Cases (excluded-middle (fiber g xâ‚€) (g-is-emb xâ‚€))
      (Ïƒ ê‰   fiber g xâ‚€ â†¦ Ïƒ)
      (u ê‰ Â¬ fiber g xâ‚€ â†¦ have (xâ‚€ , (n , p) , u) âˆ¶ f-point x
                          which-is-impossible-by (Î½ âˆ¶ Â¬ f-point x)))
\end{code}

We use the notion of f-point to prove the following, whose statement
doesn't refer to the notion of f-point.

\begin{code}

  claim : (y : Y) â†’ Â¬ is-g-point (g y) â†’ Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
  claim y Î½ = v
   where
    i : Â¬Â¬ f-point (g y)
    i = have Î½ âˆ¶ Â¬ is-g-point (g y)
        so-apply contrapositive (non-f-point-is-g-point (g y))

    ii : f-point (g y) â†’ Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
    ii (xâ‚€ , (0 , p) , u) = have p âˆ¶ xâ‚€ â‰¡ g y
                            so have (y , (p â»Â¹)) âˆ¶ fiber g xâ‚€
                               which-is-impossible-by (u âˆ¶ Â¬ fiber g xâ‚€)
    ii (xâ‚€ , (succ n , p) , u) = a , b
     where
      q : f (((g âˆ˜ f) ^ n) xâ‚€) â‰¡ y
      q = have p âˆ¶ ((g âˆ˜ f) ^ (succ n)) xâ‚€  â‰¡ g y
                 âˆ¶ g (f (((g âˆ˜ f) ^ n) xâ‚€)) â‰¡ g y
          so-apply embeddings-are-lc g g-is-emb

      a : fiber f y
      a = ((g âˆ˜ f) ^ n) xâ‚€ , q

      b : Â¬ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
      b = assume Î³ âˆ¶ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
          then (have Î³ xâ‚€ n refl âˆ¶ fiber g xâ‚€
                which-is-impossible-by (u âˆ¶ Â¬ fiber g xâ‚€))

    iii : Â¬Â¬ (Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x)
    iii = double-contrapositive ii i

    iv : is-prop (Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x)
    iv = have f-is-emb y âˆ¶ is-prop (fiber f y)
         so-apply subtype-of-prop-is-prop prâ‚ (prâ‚-lc (Î» {Ïƒ} â†’ negations-are-props fe))

    v : Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
    v = double-negation-elim excluded-middle _ iv iii

\end{code}

With this we are ready to show that h is a split surjection. The idea
is that, given y : Y, we check whether g y is a g-point or not, and if
it is we map it to g y, and otherwise we map y to the point x : X
given by the above claim. But then, of course, we also need to argue
that this works. As above, we use the auxiliary function H for that
purpose.

\begin{code}
  h-split-surjection : (y : Y) â†’ Î£ x ê‰ X , h x â‰¡ y
  h-split-surjection y = x , p
   where
    a : decidable (is-g-point (g y)) â†’ Î£ x ê‰ X , ((d : decidable (is-g-point x)) â†’ H x d â‰¡ y)
    a (inl Î³) = g y , Ïˆ
     where
      Ïˆ : (d : decidable (is-g-point (g y))) â†’ H (g y) d â‰¡ y
      Ïˆ (inl Î³') = H (g y) (inl Î³') â‰¡âŸ¨ by-definition âŸ©
                   gâ»Â¹ (g y) Î³'     â‰¡âŸ¨ gâ»Â¹-is-linv y Î³' âŸ©
                   y                âˆ
      Ïˆ (inr Î½)  = have Î½ âˆ¶ Â¬ is-g-point (g y)
                   which-contradicts (Î³ âˆ¶ is-g-point (g y))
    a (inr Î½) = x , Ïˆ
     where
      w : Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
      w = have Î½ âˆ¶ Â¬ is-g-point (g y)
          so-apply claim y

      x : X
      x = fiber-point (prâ‚ w)

      p : f x â‰¡ y
      p = fiber-identification (prâ‚ w)

      Ïˆ : (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
      Ïˆ (inl Î³) = have Î³ âˆ¶ is-g-point x
                  which-is-impossible-by (prâ‚‚ w âˆ¶ Â¬ is-g-point x)
      Ïˆ (inr Î½) = H x (inr Î½) â‰¡âŸ¨ by-definition âŸ©
                  f x         â‰¡âŸ¨ p âŸ©
                  y           âˆ

    b : Î£ x ê‰ X , ((d : decidable (is-g-point x)) â†’ H x d â‰¡ y)
    b = a (Î´ (g y))

    x : X
    x = prâ‚ b

    p : h x â‰¡ y
    p = h x       â‰¡âŸ¨ by-construction âŸ©
        H x (Î´ x) â‰¡âŸ¨ prâ‚‚ b (Î´ x) âŸ©
        y         âˆ

\end{code}

And because left-cancellable split surjections are equivalences, we
are done:

\begin{code}

  ğ’½ : X â‰ƒ Y
  ğ’½ = h , lc-split-surjections-are-equivs h h-lc h-split-surjection

\end{code}

We record the following special case:

\begin{code}

EM-gives-Cantor-SchrÃ¶der-Bernsteinâ‚€ : Fun-Ext
                                    â†’ EM ğ“¤â‚€
                                    â†’ Cantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¤â‚€
EM-gives-Cantor-SchrÃ¶der-Bernsteinâ‚€ = EM-gives-Cantor-SchrÃ¶der-Bernstein

\end{code}

If the type X in the proof is connected, then every map of X into a
set is constant. In particular, the property of being a g-point is
constant, because the type of truth values is a set (assuming
univalence for subsingletons). Hence, by excluded middle, it is
constantly true or constantly false, and so h = gâ»Â¹ or h = f, which
means that one of the embeddings f and g is already an equivalence.

Mike Shulman observed that this is true even without excluded middle:
If X is connected and we have an embedding g : Y â†’ X and any function
at all f : X â†’ Y, then g is an equivalence. In fact, for any x : X, we
have âˆ¥ g (f (x)) = x âˆ¥ since X is connected; thus g is (non-split)
surjective. But a surjective embedding is an equivalence.

\begin{code}

module CSB-for-connected-types-without-EM (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt public
 open import UF-Connected pt
 open import UF-ImageAndSurjection
 open ImageAndSurjection pt

\end{code}

We say that X is weakly connected if âˆ¥ x â‰¡ x' âˆ¥ for all x and x' in X,
and that it is connected if additionally âˆ¥ X âˆ¥ is pointed.

\begin{code}

 lemma : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) (g : Y â†’ X)
       â†’ is-wconnected X â†’ is-embedding g â†’ is-equiv g
 lemma f g w e = surjective-embeddings-are-equivs g e s
  where
   a : âˆ€ x â†’ âˆ¥ g (f (x)) â‰¡ x âˆ¥
   a x = w (g (f x)) x

   s : is-surjection g
   s x = âˆ¥âˆ¥-functor (Î» p â†’ (f x , p)) (a x)

 cCSB : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ is-wconnected Y â†’ CSB X Y
 cCSB  {ğ“¤} {ğ“¥} {X} {Y} w ((f , i) , (g , _)) = Î³
  where
   Î³ : X â‰ƒ Y
   Î³ = f , lemma g f w i

\end{code}

Of course, we can instead assume that X is wconnected:

\begin{code}

 cCSB' : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ is-wconnected X â†’ CSB X Y
 cCSB' w e = â‰ƒ-sym (cCSB w (prâ‚‚ e , prâ‚ e))

\end{code}

Another direct corollary is that weakly connected types are Dedekind
finite:

\begin{code}

 wconnected-types-are-Dedekind-finite : {X : ğ“¤ Ì‡ }
                                      â†’ is-wconnected X
                                      â†’ (f : X â†’ X) â†’ is-embedding f â†’ is-equiv f
 wconnected-types-are-Dedekind-finite w f = lemma f f w

\end{code}


APPENDIX I
----------

The above is an attempt to make the proof more readable and match the
blog post. Here is a more concise version of the above in a more
direct Agda style which some will prefer (and which could be made even
more concise by avoiding auxiliary definitions used for the purpose of
indicating types explicitly).

\begin{code}

EM-gives-Cantor-SchrÃ¶der-Bernstein' : Fun-Ext
                                    â†’ EM (ğ“¤ âŠ” ğ“¥)
                                    â†’ Cantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥
EM-gives-Cantor-SchrÃ¶der-Bernstein' {ğ“¤} {ğ“¥} fe excluded-middle {X} {Y} ((f , f-is-emb) , (g , g-is-emb)) = ğ’½
 where
  is-g-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  is-g-point x = (xâ‚€ : X) (n : â„•) â†’ ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x â†’ fiber g xâ‚€

  g-is-invertible-at-g-points : (x : X) (Î³ : is-g-point x) â†’ fiber g x
  g-is-invertible-at-g-points x Î³ = Î³ x 0 refl

  gâ»Â¹ : (x : X) â†’ is-g-point x â†’ Y
  gâ»Â¹ x Î³ = fiber-point (g-is-invertible-at-g-points x Î³)

  gâ»Â¹-is-rinv : (x : X) (Î³ : is-g-point x) â†’ g (gâ»Â¹ x Î³) â‰¡ x
  gâ»Â¹-is-rinv x Î³ = fiber-identification (g-is-invertible-at-g-points x Î³)

  gâ»Â¹-is-linv : (y : Y) (Î³ : is-g-point (g y)) â†’ gâ»Â¹ (g y) Î³ â‰¡ y
  gâ»Â¹-is-linv y Î³ = embeddings-are-lc g g-is-emb (gâ»Â¹-is-rinv (g y) Î³)

  Î± : (x : X) â†’ is-g-point (g (f x)) â†’ is-g-point x
  Î± x Î³ xâ‚€ n p = Î³ xâ‚€ (succ n) (ap (g âˆ˜ f) p)

  f-gâ»Â¹-disjoint-images : (x x' : X)
                        â†’ Â¬ is-g-point x
                        â†’ (Î³ : is-g-point x')
                        â†’ f x â‰¢ gâ»Â¹ x' Î³
  f-gâ»Â¹-disjoint-images x x' Î½ Î³ p = ğŸ˜-elim (v Î³)
   where
    q = g (f x)      â‰¡âŸ¨ ap g p âŸ©
        g (gâ»Â¹ x' Î³) â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³ âŸ©
        x'           âˆ

    u : Â¬ is-g-point (g (f x))
    u = contrapositive (Î± x) Î½

    v : Â¬ is-g-point x'
    v = transport (- â†¦ Â¬ is-g-point -) q u

  being-g-point-is-prop : (x : X) â†’ is-prop (is-g-point x)
  being-g-point-is-prop x = Î â‚ƒ-is-prop fe (Î» xâ‚€ _ _ â†’ g-is-emb xâ‚€)

  Î´ : (x : X) â†’ decidable (is-g-point x)
  Î´ x = excluded-middle (is-g-point x) (being-g-point-is-prop x)

  H : (x : X) â†’ decidable (is-g-point x) â†’ Y
  H x (inl Î³) = gâ»Â¹ x Î³
  H x (inr _) = f x

  h : X â†’ Y
  h x = H x (Î´ x)

  h-lc : left-cancellable h
  h-lc {x} {x'} = l (Î´ x) (Î´ x')
   where
    l : (d : decidable (is-g-point x)) (d' : decidable (is-g-point x')) â†’ H x d â‰¡ H x' d' â†’ x â‰¡ x'
    l (inl Î³) (inl Î³') p = x             â‰¡âŸ¨ (gâ»Â¹-is-rinv x Î³)â»Â¹ âŸ©
                           g (gâ»Â¹ x Î³)   â‰¡âŸ¨ ap g p âŸ©
                           g (gâ»Â¹ x' Î³') â‰¡âŸ¨ gâ»Â¹-is-rinv x' Î³' âŸ©
                           x'            âˆ
    l (inl Î³) (inr Î½') p = ğŸ˜-elim (f-gâ»Â¹-disjoint-images x' x  Î½' Î³ (p â»Â¹))
    l (inr Î½) (inl Î³') p = ğŸ˜-elim (f-gâ»Â¹-disjoint-images x  x' Î½  Î³' p)
    l (inr Î½) (inr Î½') p = embeddings-are-lc f f-is-emb p

  f-point : (x : X) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  f-point x = Î£ xâ‚€ ê‰ X , (Î£ n ê‰ â„• , ((g âˆ˜ f) ^ n) xâ‚€ â‰¡ x) Ã— Â¬ fiber g xâ‚€

  non-f-point-is-g-point : (x : X) â†’ Â¬ f-point x â†’ is-g-point x
  non-f-point-is-g-point x Î½ xâ‚€ n p =
   Cases (excluded-middle (fiber g xâ‚€) (g-is-emb xâ‚€))
    (Î» (Ïƒ :   fiber g xâ‚€) â†’ Ïƒ)
    (Î» (u : Â¬ fiber g xâ‚€) â†’ ğŸ˜-elim (Î½ (xâ‚€ , (n , p) , u)))

  claim : (y : Y) â†’ Â¬ is-g-point (g y) â†’ Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
  claim y Î½ = v
   where
   i : Â¬Â¬ f-point (g y)
   i = contrapositive (non-f-point-is-g-point (g y)) Î½

   ii : f-point (g y) â†’ Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
   ii (xâ‚€ , (0      , p) , u) = ğŸ˜-elim (u (y , (p â»Â¹)))
   ii (xâ‚€ , (succ n , p) , u) = a , b
    where
     q : f (((g âˆ˜ f) ^ n) xâ‚€) â‰¡ y
     q = embeddings-are-lc g g-is-emb p

     a : fiber f y
     a = ((g âˆ˜ f) ^ n) xâ‚€ , q

     b : Â¬ is-g-point (((g âˆ˜ f) ^ n) xâ‚€)
     b Î³ = ğŸ˜-elim (u (Î³ xâ‚€ n refl))

   iii : Â¬Â¬ (Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x)
   iii = double-contrapositive ii i

   iv : is-prop (Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x)
   iv = subtype-of-prop-is-prop prâ‚ (prâ‚-lc (Î» {Ïƒ} â†’ negations-are-props fe)) (f-is-emb y)

   v : Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
   v = double-negation-elim excluded-middle _ iv iii

  h-split-surjection : (y : Y) â†’ Î£ x ê‰ X , h x â‰¡ y
  h-split-surjection y = x , p
   where
    a : decidable (is-g-point (g y)) â†’ Î£ x ê‰ X , ((d : decidable (is-g-point x)) â†’ H x d â‰¡ y)
    a (inl Î³) = g y , Ïˆ
     where
      Ïˆ : (d : decidable (is-g-point (g y))) â†’ H (g y) d â‰¡ y
      Ïˆ (inl Î³') = gâ»Â¹-is-linv y Î³'
      Ïˆ (inr Î½)  = ğŸ˜-elim (Î½ Î³)
    a (inr Î½) = x , Ïˆ
     where
      w : Î£ (x , p) ê‰ fiber f y , Â¬ is-g-point x
      w = claim y Î½

      x : X
      x = fiber-point (prâ‚ w)

      Ïˆ : (d : decidable (is-g-point x)) â†’ H x d â‰¡ y
      Ïˆ (inl Î³) = ğŸ˜-elim (prâ‚‚ w Î³)
      Ïˆ (inr Î½) = fiber-identification (prâ‚ w)

    b : Î£ x ê‰ X , ((d : decidable (is-g-point x)) â†’ H x d â‰¡ y)
    b = a (Î´ (g y))

    x : X
    x = prâ‚ b

    p : h x â‰¡ y
    p = h x       â‰¡âŸ¨ by-construction âŸ©
        H x (Î´ x) â‰¡âŸ¨ prâ‚‚ b (Î´ x) âŸ©
        y         âˆ

  ğ’½ : X â‰ƒ Y
  ğ’½ = h , lc-split-surjections-are-equivs h h-lc h-split-surjection

\end{code}

APPENDIX II
-----------

Added 17th Feb 2020. A stronger result is added below, 18th Feb 2020,
with a technically and conceptually simpler proof. (But 19th Feb 2020
below shows that this 17th Feb 2020 argument has some merits.)

Coming back to part 1, we consider what follows if we assume CSB for
types with decidable equality (which are necessarily sets) only. Such
types are called discrete.

We adapt an argument in Johnstone's Sketches of an Elephant Volume 2
(Lemma D.4.1.12).

See also van Atten
https://www.sciencedirect.com/science/article/pii/S0019357718303276
for BKSâº defined below (strong Brouwer-Kripke Schema) and the fact
that together with Markov Principle it implies excluded middle
(attributed to Moschovakis).

The terminology "Rosolini-data" is in connection with the Rosolini
dominance from synthetic domain theory and topology.

\begin{code}

Rosolini-data : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
Rosolini-data {ğ“¤} P = Î£ A ê‰ (â„• â†’ ğ“¤ Ì‡ ) , ((n : â„•) â†’ decidable (A n))
                                      Ã— is-prop (Î£ A)
                                      Ã— (P â‡” Î£ A)

\end{code}

Notice this is data on P rather than property of P because multiple
A's apply to the same P, when P holds.

Notice also that we don't need to require that each A n is a
proposition, as this is automatic because â„• is a set:

\begin{code}

is-prop-total-gives-is-prop-each : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ )
                                 â†’ is-set X
                                 â†’ is-prop (Î£ A) â†’ (x : X) â†’ is-prop (A x)
is-prop-total-gives-is-prop-each A j i x a a' = t
 where
  q : (x , a) â‰¡ (x , a')
  q = i (x , a) (x , a')

  t = a                        â‰¡âŸ¨ by-definition âŸ©
      transport A refl       a â‰¡âŸ¨ ap (- â†¦ transport A - a) (j refl (ap prâ‚ q)) âŸ©
      transport A (ap prâ‚ q) a â‰¡âŸ¨ from-Î£-â‰¡' q âŸ©
      a'                       âˆ

\end{code}

Here a typal, rather than propositional, version of BKSâº, which is
data-valued rather than propositionally valued, arises.

\begin{code}

dBKSâº : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
dBKSâº ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ Rosolini-data P

\end{code}

It is convenient to work with the following formulation of Markov
Principle that avoids âˆƒ (and hence propositional truncations), which
is easily seen to be equivalent to the traditional formulation using âˆƒ
(using the fact that unique choice just holds (trivially) in HoTT/UF).

\begin{code}

MP : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
MP ğ“¤ = (A : â„• â†’ ğ“¤ Ì‡ ) â†’ ((n : â„•) â†’ decidable (A n)) â†’ is-prop (Î£ A) â†’ Â¬Â¬ Î£ A â†’ Î£ A

\end{code}

The following, which derives double negation elimination from dBKSâº
and MP, is formulated and proved in pure (spartan) MLTT:

\begin{code}

dBKSâº-and-MP-give-DNE : dBKSâº ğ“¤ â†’ MP ğ“¤ â†’ DNE ğ“¤
dBKSâº-and-MP-give-DNE {ğ“¤} bks mp P i = Î³ (bks P i)
 where
  Î³ : (Î£ A ê‰ (â„• â†’ ğ“¤ Ì‡ ) , ((n : â„•) â†’ decidable (A n)) Ã— is-prop (Î£ A) Ã— (P â‡” Î£ A))
    â†’ Â¬Â¬ P â†’ P
  Î³ (A , d , j , f , g) = Î´
   where
    f' : Â¬Â¬ P â†’ Â¬Â¬ Î£ A
    f' = double-contrapositive f

    h : Â¬Â¬ Î£ A â†’ Î£ A
    h = mp A d j

    Î´ : Â¬Â¬ P â†’ P
    Î´ = g âˆ˜ h âˆ˜ f'

\end{code}

But the following, which derives excluded middle, needs function
extensionality:

\begin{code}

dBKSâº-and-MP-give-EM : funext ğ“¤ ğ“¤â‚€ â†’ dBKSâº ğ“¤ â†’ MP ğ“¤ â†’ EM ğ“¤
dBKSâº-and-MP-give-EM fe bks MP = DNE-gives-EM fe (dBKSâº-and-MP-give-DNE bks MP)

\end{code}

So dBKSâº "almost" gives excluded middle in some sense.

We now show that CSB for discrete types gives dBKSâº:

\begin{code}

blemma : (P : ğ“¤ Ì‡ ) {X : ğ“¥ Ì‡ }
       â†’ is-set X
       â†’ is-prop P
       â†’ X â‰ƒ P + X
       â†’ Î£ A ê‰ (X â†’ ğ“¤ âŠ” ğ“¥ Ì‡ ) , ((x : X) â†’ decidable (A x)) Ã— is-prop (Î£ A) Ã— (P â‡” Î£ A)
blemma {ğ“¤} {ğ“¥ } P {X} j i (f , (s , Î·) , (r , Îµ)) = A , d , l , (Ï† , Î³)
 where
  A : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
  A x = Î£ p ê‰ P , f x â‰¡ inl p

  d : (x : X) â†’ decidable (A x)
  d x = equality-cases (f x)
         (Î» (p : P) (u : f x â‰¡ inl p) â†’ inl (p , u))
         (Î» (y : X) (v : f x â‰¡ inr y) â†’ inr (Î» (a , u) â†’ +disjoint (inl a â‰¡âŸ¨ u â»Â¹ âŸ©
                                                                    f x   â‰¡âŸ¨ v âŸ©
                                                                    inr y âˆ)))

  k : (x : X) â†’ is-prop (A x)
  k x = Î£-is-prop i (Î» p â†’ +-is-set P X (props-are-sets i) j)

  l : is-prop (Î£ A)
  l (x , p , u) (x' , p' , u') = t
   where
    q : x â‰¡ x'
    q = equivs-are-lc f ((s , Î·) , (r , Îµ)) (f x    â‰¡âŸ¨ u âŸ©
                                             inl p  â‰¡âŸ¨ ap inl (i p p') âŸ©
                                             inl p' â‰¡âŸ¨ u' â»Â¹ âŸ©
                                             f x'   âˆ)

    t : x , p , u â‰¡ x' , p' , u'
    t = to-subtype-â‰¡ k q

  Ï† : P â†’ Î£ A
  Ï† p = s (inl p) , p , Î· (inl p)

  Î³ : Î£ A â†’ P
  Î³ (x , p , u) = p

rlemma : (P : ğ“¤ Ì‡ )
       â†’ is-prop P
       â†’ â„• â‰ƒ P + â„•
       â†’ Rosolini-data P
rlemma P = blemma P â„•-is-set

discrete-Cantor-SchrÃ¶der-Bernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
discrete-Cantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ is-discrete X â†’ is-discrete Y â†’ CSB X Y

econstruction-â„• : (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ (â„• â†ª P + â„•) Ã— (P + â„• â†ª â„•)
econstruction-â„• P i = econstruction P zero succ
                       â„•-is-set i
                       (â„•-is-discrete zero)
                       (Î» (x : â„•) (p : zero â‰¡ succ x) â†’ positive-not-zero x (p â»Â¹))
                       succ-lc

dlemma : (P : ğ“¥ Ì‡ )
       â†’ discrete-Cantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥
       â†’ is-prop P
       â†’ â„• â‰ƒ P + â„•
dlemma P csb i = csb â„•-is-discrete
                  (+-is-discrete (props-are-discrete i) â„•-is-discrete)
                  (econstruction-â„• P i)

discrete-CSB-gives-dBKSâº : discrete-Cantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥ â†’ dBKSâº ğ“¥
discrete-CSB-gives-dBKSâº csb P i = Î³
 where
  e : â„• â‰ƒ P + â„•
  e = dlemma P csb i

  Î³ : Rosolini-data P
  Î³ = rlemma P i e

\end{code}

Added 18th Feb 2020. We make the 17 Feb 2020 delopment sharper, at the
expense of assuming propositional extensionality (univalence for
propositions).

If we have a uniform way to get an equivalence â„• â‰ƒ P + â„• for any
proposition P, given by a function

 Ï† : (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ â„• â‰ƒ P + â„•,

then we can use Ï† to decide P for any proposition P. To see this,
first consider P = ğŸ™, and let x be the natural number that is mapped
to inl * by the equivalence given by Ï†. Then, for an arbitrary
proposition P, if the equivalence maps x to inl p for some p, we have
that P holds. Otherwise, if it maps x to inr y for some y : â„•, then P
can't hold, for if it did we would have p : P, and hence P â‰¡ ğŸ™ by
propositional extensionality, and the equivalence would have to map x
to inl p, which is different from the value inr y of the equivalence
at x.

There is nothing that depends on the nature of the specific type â„• in
the above argument, and hence we formulate this uniformity lemma with
arbitrary types X and Y, although we will apply it to X = Y = â„•.  In
order to simplify the calculational details of this proof, we work
with the type T of true propositions, which is contractible with
center of contraction ğŸ™.

\begin{code}

ulemma : funext ğ“¤ ğ“¤
       â†’ propext ğ“¤
       â†’ {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ }
       â†’ ((P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ X â‰ƒ P + Y)
       â†’ EM ğ“¤
ulemma {ğ“¤} fe pe {X} {Y} Ï† = em
 where
  T : ğ“¤ âº Ì‡
  T = Î£ P ê‰ ğ“¤ Ì‡ , is-prop P Ã— P

  c : (t : T) â†’ (ğŸ™ , ğŸ™-is-prop , â‹†) â‰¡ t
  c = ğŸ™-is-true-props-center fe pe

  f : T â†’ X
  f (P , i , p) = âŒœ Ï† P i âŒâ»Â¹ (inl p)

  x : X
  x = f (ğŸ™ , ğŸ™-is-prop , â‹†)

  Î½ : (P : ğ“¤ Ì‡ ) (i : is-prop P) (y : Y) â†’ âŒœ Ï† P i âŒ x â‰¡ inr y â†’ Â¬ P
  Î½ P i y r p = Î³
   where
    a : x â‰¡ f (P , i , p)
    a = ap f (c (P , i , p))

    b = inr y                           â‰¡âŸ¨ r â»Â¹ âŸ©
        âŒœ Ï† P i âŒ x                     â‰¡âŸ¨ ap âŒœ Ï† P i âŒ a âŸ©
        âŒœ Ï† P i âŒ (f (P , i , p))       â‰¡âŸ¨ by-definition âŸ©
        âŒœ Ï† P i âŒ (âŒœ Ï† P i âŒâ»Â¹ (inl p)) â‰¡âŸ¨ â‰ƒ-sym-is-rinv (Ï† P i) (inl p) âŸ©
        inl p                           âˆ

    Î³ : ğŸ˜
    Î³ = +disjoint' b

  em : (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ P + Â¬ P
  em P i = equality-cases (âŒœ Ï† P i âŒ x)
           (Î» (p : P) (l : âŒœ Ï† P i âŒ x â‰¡ inl p) â†’ inl p)
           (Î» (y : Y) (r : âŒœ Ï† P i âŒ x â‰¡ inr y) â†’ inr (Î½ P i y r))

discrete-CSB-gives-EM : funext ğ“¥ ğ“¥
                      â†’ propext ğ“¥
                      â†’ discrete-Cantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥
                      â†’ EM ğ“¥
discrete-CSB-gives-EM {ğ“¥} fe pe csb = ulemma fe pe Ï†
 where
  Ï† : (P : ğ“¥ Ì‡ ) â†’ is-prop P â†’ â„• â‰ƒ P + â„•
  Ï† P = dlemma P csb

\end{code}

Thus, in particular, decidable equality is not enough to get a
constructive version of CSB. Even with decidable equality of the given
types, one still needs full excluded middle.

Discussion
----------

The Pradic-Brown argument, although it requires a non-discrete type to
work, has the advantage that if we weaken the statement of CSB to say
that an unspecified (rather than designated) equivalence exists, for
any two given embeddings in opposite directions,

    (X â†ª Y) Ã— (Y â†ª X) â†’ âˆ¥ X â‰ƒ Y âˆ¥.

one still gets excluded middle, as already remarked above. And it is
also nice and clear and short.

Our argument doesn't work with this weakening of the hypothesis, as in
this case it is no longer possible to define the function Ï† in the
proof (without choice, which is stronger than what we want to prove,
namely excluded middle) to apply the uniformity lemma. The reason is
that Pradic and Brown use only one instance of CSB, for a given
proposition, whereas we use a family of instances.

In any case, in the other direction, excluded middle does give CSB
with a designated equivalence in the conclusion, as previously shown
above.

Added 19th Feb 2020: In light of the above discussion, notice that the
17th Feb 2020 development has its merits, after all, compared to the
18th Feb 2020 development. We don't get excluded middle if we weaken
CSB, but we do get BKSâº.

\begin{code}

module discrete-wCSB-gives-BKSâº (pt : propositional-truncations-exist) where

\end{code}

We open the following module only to have access to the definition of
wCSB:

\begin{code}

 open wCSB-still-gives-EM pt

 discrete-wCantor-SchrÃ¶der-Bernstein : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
 discrete-wCantor-SchrÃ¶der-Bernstein ğ“¤ ğ“¥ = {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ is-discrete X â†’ is-discrete Y â†’ wCSB X Y

\end{code}

We now consider the propositional version of BKSâº:

\begin{code}

 is-Rosolini : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
 is-Rosolini P = âˆ¥ Rosolini-data P âˆ¥

 BKSâº : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
 BKSâº ğ“¤ = (P : ğ“¤ Ì‡ ) â†’ is-prop P â†’ is-Rosolini P

 discrete-wCSB-gives-BKSâº : discrete-wCantor-SchrÃ¶der-Bernstein ğ“¤â‚€ ğ“¥ â†’ BKSâº ğ“¥
 discrete-wCSB-gives-BKSâº w P i = Î³
  where
   s : âˆ¥ â„• â‰ƒ P + â„• âˆ¥
   s = w â„•-is-discrete (+-is-discrete (props-are-discrete i) â„•-is-discrete) (econstruction-â„• P i)

   Î³ : is-Rosolini P
   Î³ = âˆ¥âˆ¥-functor (rlemma P i) s

\end{code}

Notice that BKSâº also implies excluded middle in the presence of MP,
because EM is a proposition (in any case, this was already observed by
Moschovakis, as discussed above).
