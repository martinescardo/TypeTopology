Tom de Jong, 31 May 2019
Updated comments on 21 June 2022.
Added examples at the end on 22 December 2022.

The denotational semantics of PCF based on pointed directed complete posets.

The flag --lossy-unification significantly speeds up the
typechecking of the line âŸ¦ S {Ï} {Ïƒ} {Ï„} âŸ§â‚‘ = Sáµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ï âŸ§ âŸ¦ Ïƒ âŸ§ âŸ¦ Ï„ âŸ§ below.
(https://agda.readthedocs.io/en/latest/language/lossy-unification.html)


We consider the combinatory version of PCF here. This development was extended
to PCF with variables and Î»-abstraction by Brendan Hart in a final year project
supervised by MartÃ­n EscardÃ³ and myself. Notably, Brendan's extension contains
an Agda formalization of soundness and computational adequacy.

Brendan's code, using a previous version of our library, can be found
here: https://github.com/BrendanHart/Investigating-Properties-of-PCF.

The repository also contains Brendan's report describing the project:
https://github.com/BrendanHart/Investigating-Properties-of-PCF/blob/master/InvestigatingPropertiesOfPCFInAgda.pdf.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan
open import UF.PropTrunc
open import UF.FunExt
open import UF.Subsingletons

module PCF.Combinatory.ScottModelOfPCF
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (pe : propext ğ“¤â‚€)
       where

open PropositionalTruncation pt

open import Naturals.Properties
open import UF.DiscreteAndSeparated

open import PCF.Combinatory.PCF pt
open import DomainTheory.Basics.Dcpo pt fe ğ“¤â‚€
open import DomainTheory.Basics.Exponential pt fe ğ“¤â‚€
open import DomainTheory.Basics.LeastFixedPoint pt fe
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤â‚€
open import DomainTheory.Basics.Pointed pt fe ğ“¤â‚€

open import PCF.Combinatory.PCFCombinators pt fe ğ“¤â‚€
open IfZeroDenotationalSemantics pe

open import DomainTheory.Lifting.LiftingSet pt fe ğ“¤â‚€ pe

open import Lifting.Lifting ğ“¤â‚€
open import Lifting.Monad ğ“¤â‚€ hiding (Î¼)

âŸ¦_âŸ§ : type â†’ DCPOâŠ¥ {ğ“¤â‚} {ğ“¤â‚}
âŸ¦ Î¹ âŸ§     = ğ“›-DCPOâŠ¥ â„•-is-set
âŸ¦ Ïƒ â‡’ Ï„ âŸ§ = âŸ¦ Ïƒ âŸ§ âŸ¹áµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ï„ âŸ§

âŸ¦_âŸ§â‚‘ : {Ïƒ : type} (t : PCF Ïƒ) â†’ âŸª âŸ¦ Ïƒ âŸ§ âŸ«
âŸ¦ Zero âŸ§â‚‘            = Î· zero
âŸ¦ Succ âŸ§â‚‘            = ğ“›Ì‡ succ , ğ“›Ì‡-continuous â„•-is-set â„•-is-set succ
âŸ¦ Pred âŸ§â‚‘            = ğ“›Ì‡ pred , ğ“›Ì‡-continuous â„•-is-set â„•-is-set pred
âŸ¦ ifZero âŸ§â‚‘          = â¦…ifZeroâ¦†
âŸ¦ Fix {Ïƒ} âŸ§â‚‘         = Î¼ âŸ¦ Ïƒ âŸ§
âŸ¦ K {Ïƒ} {Ï„} âŸ§â‚‘       = Káµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ïƒ âŸ§ âŸ¦ Ï„ âŸ§
âŸ¦ S {Ï} {Ïƒ} {Ï„} âŸ§â‚‘   = Sáµˆá¶œáµ–áµ’âŠ¥ âŸ¦ Ï âŸ§ âŸ¦ Ïƒ âŸ§ âŸ¦ Ï„ âŸ§
âŸ¦ _Â·_ {Ïƒ} {Ï„} s t âŸ§â‚‘ = [ âŸ¦ Ïƒ âŸ§ â» ,  âŸ¦ Ï„ âŸ§ â» ]âŸ¨ âŸ¦ s âŸ§â‚‘ âŸ© âŸ¦ t âŸ§â‚‘

\end{code}

Because Agda is a computational system, we can use it to directly compute the
value of terms in the model. We showcase a few examples illustrating this, as
suggested by Andrej Bauer during my viva on 20 December 2022.

\begin{code}

private
 tâ‚ : PCF Î¹
 tâ‚ = âŒœ 7 âŒ

 recall-the-interpretation-of-Î¹ : âŸª âŸ¦ Î¹ âŸ§ âŸ« ï¼ ğ“› â„•
 recall-the-interpretation-of-Î¹ = refl

 âŸ¦tâ‚âŸ§-is-a-triple-representing-a-partial-element : âŸ¦ tâ‚ âŸ§â‚‘
                                                 ï¼ ğŸ™ , (Î» _ â†’ 7) , ğŸ™-is-prop
 âŸ¦tâ‚âŸ§-is-a-triple-representing-a-partial-element = refl

 compute-the-value-of-âŸ¦tâ‚âŸ§ : value âŸ¦ tâ‚ âŸ§â‚‘ â‹† ï¼ 7
 compute-the-value-of-âŸ¦tâ‚âŸ§ = refl


 tâ‚‚ : PCF Î¹
 tâ‚‚ = Pred Â· (Pred Â· (Succ Â· âŒœ 3 âŒ))

 âŸ¦tâ‚‚âŸ§-is-a-triple-representing-a-partial-element : âŸ¦ tâ‚‚ âŸ§â‚‘
                                                 ï¼ ğŸ™ , (Î» _ â†’ 2) , ğŸ™-is-prop
 âŸ¦tâ‚‚âŸ§-is-a-triple-representing-a-partial-element = refl

  -- We let Agda compute the witness (indicated by _) that âŸ¦ tâ‚‚ âŸ§â‚‘ is total.
 compute-the-value-of-âŸ¦tâ‚‚âŸ§ : value âŸ¦ tâ‚‚ âŸ§â‚‘ _ ï¼ 2
 compute-the-value-of-âŸ¦tâ‚‚âŸ§ = refl

\end{code}

By computational adequacy (see the comments at the top of this file) and the
computation above, the term tâ‚‚ reduces to the numeral âŒœ 2 âŒ in PCF.

\begin{code}

 -- tâ‚ƒ encodes the program [Î» x . (if (0 == x) then 2 else (pred 5)) 3]
 tâ‚ƒ : PCF Î¹
 tâ‚ƒ = ifZero Â· âŒœ 2 âŒ Â· (Pred Â· âŒœ 5 âŒ) Â· âŒœ 3 âŒ

 -- Notice how the extent of the partial element is no longer given by ğŸ™ but, as
 -- a consequence of the constructions in our model, by the product ğŸ™ Ã— ğŸ™.
 --
 -- We let Agda compute the witness (indicated by _) that the type ğŸ™ Ã— ğŸ™ is a
 -- proposition.
 âŸ¦tâ‚ƒâŸ§-is-a-triple-representing-a-partial-element : âŸ¦ tâ‚ƒ âŸ§â‚‘
                                                 ï¼ (ğŸ™ Ã— ğŸ™) , (Î» _ â†’ 4) , _
 âŸ¦tâ‚ƒâŸ§-is-a-triple-representing-a-partial-element = refl

 compute-the-value-of-âŸ¦tâ‚ƒâŸ§ : value âŸ¦ tâ‚ƒ âŸ§â‚‘ _ ï¼ 4
 compute-the-value-of-âŸ¦tâ‚ƒâŸ§ = refl

\end{code}

Next we show two examples using the S and K combinators. We first construct the
identity function I on an arbitrary type Ïƒ of PCF using the well-known
definition I = S Â· K Â· K.

\begin{code}

 I : {Ïƒ : type} â†’ PCF (Ïƒ â‡’ Ïƒ)
 I {Ïƒ} = (S {Ïƒ} {Ïƒ â‡’ Ïƒ} {Ïƒ}) Â· K Â· K

 tâ‚„ : PCF Î¹
 tâ‚„ = I Â· âŒœ 7 âŒ

 compute-the-value-of-âŸ¦tâ‚„âŸ§ : value âŸ¦ tâ‚„ âŸ§â‚‘ _ ï¼ 7
 compute-the-value-of-âŸ¦tâ‚„âŸ§ = refl

 tâ‚… : PCF Î¹
 tâ‚… = (I Â· Succ) Â· âŒœ 11 âŒ

 compute-the-value-of-âŸ¦tâ‚…âŸ§ : value âŸ¦ tâ‚… âŸ§â‚‘ _ ï¼ 12
 compute-the-value-of-âŸ¦tâ‚…âŸ§ = refl

\end{code}

Finally, here are two examples that use the Fix combinator where Agda cannot
normalise the term within reasonable time, which is why these lines are
commented out.

\begin{code}
 tâ‚† : PCF Î¹
 tâ‚† = Fix Â· (K Â· âŒœ 0 âŒ)

 -- The value of âŸ¦ tâ‚† âŸ§ is 0, but the computation takes an unreasonable amount
 -- of time.
 --
 -- compute-the-value-of-âŸ¦tâ‚†âŸ§ : value âŸ¦ tâ‚† âŸ§â‚‘ _ = 0
 -- compute-the-value-of-âŸ¦tâ‚†âŸ§ = refl

 tâ‚‡ : PCF Î¹
 tâ‚‡ = Fix Â· (I {Î¹})

 -- The interpretation of tâ‚‡ is equal to âŠ¥, because it is the least fixed point
 -- of the identity on âŸ¦ Î¹ âŸ§, but Agda cannot normalise (is-defined (âŸ¦ tâ‚‡ âŸ§â‚‘) in
 -- reasonable time.
 --
 -- âŸ¦tâ‚‡âŸ§-is-not-defined : Â¬ (is-defined âŸ¦ tâ‚‡ âŸ§â‚‘)
 -- âŸ¦tâ‚‡âŸ§-is-not-defined = {!!}

\end{code}
