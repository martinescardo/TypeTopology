Negation (and emptiness).

\begin{code}

{-# OPTIONS --safe --without-K #-}

module MLTT.Negation where

open import MLTT.Universes
open import MLTT.Empty
open import MLTT.Id
open import MLTT.Pi
open import MLTT.Plus
open import MLTT.Sigma

private
 _â†”_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 A â†” B = (A â†’ B) Ã— (B â†’ A)

Â¬_ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬ A = A â†’ ğŸ˜ {ğ“¤â‚€}

\end{code}

Notice that decidability is not a univalent proposition in general,
but nevertheless we use "is" in our chosen terminology, against a
convention adopted in some quarters that says that "is" should be used
only for concepts that are propositions.

\begin{code}

is-decidable : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-decidable A = A + Â¬ A

_â‰ _ : {X : ğ“¤ Ì‡ } â†’ (x y : X) â†’ ğ“¤ Ì‡
x â‰  y = Â¬ (x ï¼ y)

has-two-distinct-points : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
has-two-distinct-points X = Î£ (x , y) ê‰ X Ã— X , (x â‰  y)

has-three-distinct-points : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
has-three-distinct-points X = Î£ (x , y , z) ê‰ X Ã— X Ã— X , (x â‰  y) Ã— (y â‰  z) Ã— (z â‰  x)

â‰ -sym : {X : ğ“¤ Ì‡ } â†’ {x y : X} â†’ x â‰  y â†’ y â‰  x
â‰ -sym u r = u (r â»Â¹)

is-empty : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-empty = Â¬_

Â¬Â¬_ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬Â¬ A = Â¬ (Â¬ A)

Â¬Â¬Â¬_ : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬Â¬Â¬ A = Â¬ (Â¬Â¬ A)

is-nonempty : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-nonempty = Â¬Â¬_

dual : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (R : ğ“¦ Ì‡ ) â†’ (X â†’ Y) â†’ (Y â†’ R) â†’ (X â†’ R)
dual R f p = p âˆ˜ f

contrapositive : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ Â¬ B â†’ Â¬ A
contrapositive = dual _

double-contrapositive : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ Â¬Â¬ A â†’ Â¬Â¬ B
double-contrapositive = contrapositive âˆ˜ contrapositive

Â¬Â¬-functor : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ Â¬Â¬ A â†’ Â¬Â¬ B
Â¬Â¬-functor = double-contrapositive

Â¬Â¬-kleisli : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ Â¬Â¬ B) â†’ Â¬Â¬ A â†’ Â¬Â¬ B
Â¬Â¬-kleisli f Ï• h = Ï• (Î» a â†’ f a h)

Â¬Â¬-intro : {A : ğ“¤ Ì‡ } â†’ A â†’ Â¬Â¬ A
Â¬Â¬-intro x u = u x

three-negations-imply-one : {A : ğ“¤ Ì‡ } â†’ Â¬Â¬Â¬ A â†’ Â¬ A
three-negations-imply-one = contrapositive Â¬Â¬-intro

dne' : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ (Â¬Â¬ B â†’ B) â†’ Â¬Â¬ A â†’ B
dne' f h Ï• = h (Î» g â†’ Ï• (Î» a â†’ g (f a)))

dne : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ Â¬ B) â†’ Â¬Â¬ A â†’ Â¬ B
dne f Ï• b = Ï• (Î» a â†’ f a b)

double-negation-unshift : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ } â†’ Â¬Â¬ ((x : X) â†’ A x) â†’ (x : X) â†’ Â¬Â¬ (A x)
double-negation-unshift f x g = f (Î» h â†’ g (h x))

dnu : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ Â¬Â¬ (A Ã— B) â†’ Â¬Â¬ A Ã— Â¬Â¬ B
dnu Ï† = (Â¬Â¬-functor prâ‚ Ï†) , (Â¬Â¬-functor prâ‚‚ Ï†)

und : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ Â¬Â¬ A Ã— Â¬Â¬ B â†’ Â¬Â¬ (A Ã— B)
und (Ï† , Î³) w = Î³ (Î» y â†’ Ï† (Î» x â†’ w (x , y)))

Â¬Â¬-stable : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Â¬Â¬-stable A = Â¬Â¬ A â†’ A

Â¬-is-Â¬Â¬-stable : {A : ğ“¤ Ì‡ } â†’ Â¬Â¬-stable (Â¬ A)
Â¬-is-Â¬Â¬-stable = three-negations-imply-one

Î -is-Â¬Â¬-stable : {A : ğ“¤ Ì‡ } {B : A â†’ ğ“¥ Ì‡ }
               â†’ ((a : A) â†’ Â¬Â¬-stable (B a))
               â†’ Â¬Â¬-stable (Î  B)
Î -is-Â¬Â¬-stable f Ï• a = f a (Î» v â†’ Ï• (Î» g â†’ v (g a)))

â†’-is-Â¬Â¬-stable : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
               â†’ Â¬Â¬-stable B
               â†’ Â¬Â¬-stable (A â†’ B)
â†’-is-Â¬Â¬-stable f = Î -is-Â¬Â¬-stable (Î» _ â†’ f)

Ã—-is-Â¬Â¬-stable : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
               â†’ Â¬Â¬-stable A
               â†’ Â¬Â¬-stable B
               â†’ Â¬Â¬-stable (A Ã— B)
Ã—-is-Â¬Â¬-stable f g Ï• = f (Î» v â†’ Ï• (Î» (a , b) â†’ v a)) ,
                       g (Î» v â†’ Ï• (Î» (a , b) â†’ v b))

negation-of-implication :  {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
                        â†’ Â¬ (A â†’ B)
                        â†’ Â¬Â¬ A Ã— Â¬ B
negation-of-implication u = (Î» v â†’ u (Î» a â†’ ğŸ˜-elim (v a))) ,
                            (Î» b â†’ u (Î» a â†’ b))

negation-of-implication-converse :  {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
                                 â†’ Â¬Â¬ A Ã— Â¬ B
                                 â†’ Â¬ (A â†’ B)
negation-of-implication-converse (u , v) f = u (Î» a â†’ v (f a))

Double-negation-of-implicationâ† : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
                                  {R : ğ“¦ Ì‡ } {S : ğ“£ Ì‡ } {T : ğ“£' Ì‡ }
                                â†’ (((A â†’ B) â†’ T) â†’ S)
                                â†’ (((A â†’ S) â†’ R) Ã— (B â†’ T)) â†’ R
Double-negation-of-implicationâ† f g = prâ‚ g (Î» a â†’ f (Î» h â†’ prâ‚‚ g (h a)))

Double-negation-of-implicationâ†’ : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
                                  (R : ğ“¦ Ì‡ ) {S : ğ“£ Ì‡ } {T : ğ“£' Ì‡ } {U : ğ“£' Ì‡ }
                                â†’ (S â†’ B)
                                â†’ ((((A â†’ S) â†’ T) Ã— (B â†’ T)) â†’ U)
                                â†’ ((A â†’ B) â†’ T) â†’ U
Double-negation-of-implicationâ†’ R k f g = f ((Î» h â†’ g (Î» a â†’ k (h a))) ,
                                             (Î» b â†’ g (Î» a â†’ b)))

double-negation-of-implicationâ† : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ Â¬Â¬ (A â†’ B) â†’ Â¬ (Â¬Â¬ A Ã— Â¬ B)
double-negation-of-implicationâ† = Double-negation-of-implicationâ†

double-negation-of-implicationâ†’ : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ Â¬ (Â¬Â¬ A Ã— Â¬ B) â†’ Â¬Â¬ (A â†’ B)
double-negation-of-implicationâ†’ f g = Double-negation-of-implicationâ†’ (ğŸ˜ {ğ“¤â‚€}) ğŸ˜-elim f g

not-equivalent-to-own-negation' : {A : ğ“¤ Ì‡ } {R : ğ“¥ Ì‡ } â†’ (A â†” (A â†’ R)) â†’ R
not-equivalent-to-own-negation' (f , g) = f (g (Î» a â†’ f a a)) (g (Î» a â†’ f a a))

not-equivalent-to-own-negation : {A : ğ“¤ Ì‡ } â†’ Â¬ (A â†” Â¬ A)
not-equivalent-to-own-negation = not-equivalent-to-own-negation'

not-Î£-implies-Î -not : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                    â†’ Â¬ (Î£ x ê‰ X , A x)
                    â†’ (x : X) â†’ Â¬ (A x)
not-Î£-implies-Î -not = curry

Î -not-implies-not-Î£ : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                    â†’ ((x : X) â†’ Â¬ (A x))
                    â†’ Â¬ (Î£ x ê‰ X , A x)
Î -not-implies-not-Î£ = uncurry

Î -implies-not-Î£-not : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                    â†’ ((x : X) â†’ A x)
                    â†’ Â¬ (Î£ x ê‰ X , Â¬ (A x))
Î -implies-not-Î£-not f (x , Î½) = Î½ (f x)

not-Î -not-not-implies-not-not-Î£-not : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                                    â†’ Â¬ ((x : X) â†’ Â¬Â¬ (A x))
                                    â†’ Â¬Â¬ (Î£ x ê‰ X , Â¬ (A x))
not-Î -not-not-implies-not-not-Î£-not = contrapositive not-Î£-implies-Î -not

not-Î -implies-not-not-Î£ : {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ }
                        â†’ ((x : X) â†’ Â¬Â¬-stable (A x))
                        â†’ Â¬ ((x : X) â†’ A x)
                        â†’ Â¬Â¬ (Î£ x ê‰ X , Â¬ (A x))
not-Î -implies-not-not-Î£ f g h = g (Î» x â†’ f x (Î» u â†’ h (x , u)))

\end{code}

Notation to try to make proofs readable:

\begin{code}

contradiction : ğ“¤â‚€ Ì‡
contradiction = ğŸ˜

have_which-is-impossible-by_ : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
                             â†’ A â†’ (A â†’ ğŸ˜ {ğ“¤â‚€}) â†’ B
have a which-is-impossible-by Î½ = ğŸ˜-elim (Î½ a)


have_which-contradicts_ : {A : ğ“¤ Ì‡ } {B : ğ“¦ Ì‡ }
                        â†’ (A â†’ ğŸ˜ {ğ“¤â‚€}) â†’ A â†’ B
have Î½ which-contradicts a = ğŸ˜-elim (Î½ a)

\end{code}

Fixities:

\begin{code}

infix  50 Â¬_
infix  50 Â¬Â¬_
infix  50 Â¬Â¬Â¬_
infix  0 _â‰ _

\end{code}
