Identity type.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module MLTT.Id where

open import MLTT.Universes
open import MLTT.Pi

open import MLTT.Identity-Type renaming (_ï¼_ to infix 0 _ï¼_) public

ğ“»ğ“®ğ’»ğ“µ : {X : ğ“¤ Ì‡ } (x : X) â†’ x ï¼ x
ğ“»ğ“®ğ’»ğ“µ x = refl {_} {_} {x}

Id : {X : ğ“¤ Ì‡ } â†’ X â†’ X â†’ ğ“¤ Ì‡
Id = _ï¼_

Jbased : {X : ğ“¤ Ì‡ } (x : X) (A : (y : X) â†’ x ï¼ y â†’ ğ“¥ Ì‡ )
       â†’ A x refl â†’ (y : X) (r : x ï¼ y) â†’ A y r
Jbased x A b .x refl = b

J : {X : ğ“¤ Ì‡ } (A : (x y : X) â†’ x ï¼ y â†’ ğ“¥ Ì‡ )
  â†’ ((x : X) â†’ A x x refl) â†’ {x y : X} (r : x ï¼ y) â†’ A x y r
J A f {x} {y} = Jbased x (A x) (f x) y

private

 transport' : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) {x y : X}
            â†’ x ï¼ y â†’ A x â†’ A y
 transport' A {x} {y} p a = Jbased x (Î» y p â†’ A y) a y p


transport : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) {x y : X}
          â†’ x ï¼ y â†’ A x â†’ A y
transport A refl = id

lhs : {X : ğ“¤ Ì‡ } {x y : X} â†’ x ï¼ y â†’ X
lhs {ğ“¤} {X} {x} {y} p = x

rhs : {X : ğ“¤ Ì‡ } {x y : X} â†’ x ï¼ y â†’ X
rhs {ğ“¤} {X} {x} {y} p = y

_âˆ™_ : {X : ğ“¤ Ì‡ } {x y z : X} â†’ x ï¼ y â†’ y ï¼ z â†’ x ï¼ z
p âˆ™ q = transport (lhs p ï¼_) q p

_â»Â¹ : {X : ğ“¤ Ì‡ } â†’ {x y : X} â†’ x ï¼ y â†’ y ï¼ x
p â»Â¹ = transport (_ï¼ lhs p) p refl

ap : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) {x x' : X} â†’ x ï¼ x' â†’ f x ï¼ f x'
ap f p = transport (Î» - â†’ f (lhs p) ï¼ f -) p refl

transportâ»Â¹ : {X : ğ“¤ Ì‡ } (A : X â†’ ğ“¥ Ì‡ ) {x y : X} â†’ x ï¼ y â†’ A y â†’ A x
transportâ»Â¹ B p = transport B (p â»Â¹)

module _ {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ } where

 infix  4 _âˆ¼_

 _âˆ¼_ :  Î  A â†’ Î  A â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 f âˆ¼ g = âˆ€ x â†’ f x ï¼ g x

 âˆ¼-refl : {f : Î  A} â†’ f âˆ¼ f
 âˆ¼-refl x = refl

 âˆ¼-trans : {f g h : Î  A} â†’ f âˆ¼ g â†’ g âˆ¼ h â†’ f âˆ¼ h
 âˆ¼-trans h k x = h x âˆ™ k x

 âˆ¼-sym : {f g : Î  A} â†’ f âˆ¼ g â†’ g âˆ¼ f
 âˆ¼-sym h x = (h x)â»Â¹

 âˆ¼-ap : {E : ğ“¦ Ì‡ } (F : E â†’ Î  A) {e e' : E} â†’ e ï¼ e' â†’ F e âˆ¼ F e'
 âˆ¼-ap F p x = ap (Î» - â†’ F - x) p

\end{code}

Notations to make some proofs more readable:

\begin{code}

by-definition : {X : ğ“¤ Ì‡ } {x : X} â†’ x ï¼ x
by-definition = refl

by-construction : {X : ğ“¤ Ì‡ } {x : X} â†’ x ï¼ x
by-construction = refl

by-assumption : {X : ğ“¤ Ì‡ } {x : X} â†’ x ï¼ x
by-assumption = refl

\end{code}

Standard syntax for equality chain reasoning:

\begin{code}

_ï¼âŸ¨_âŸ©_ : {X : ğ“¤ Ì‡ } (x : X) {y z : X} â†’ x ï¼ y â†’ y ï¼ z â†’ x ï¼ z
_ ï¼âŸ¨ p âŸ© q = p âˆ™ q

_âˆ : {X : ğ“¤ Ì‡ } (x : X) â†’ x ï¼ x
_âˆ _ = refl


module _ {X : ğ“¤ Ì‡ } {A : X â†’ ğ“¥ Ì‡ } where

 _âˆ¼âŸ¨_âŸ©_ : (f : Î  A) {g h : Î  A} â†’ f âˆ¼ g â†’ g âˆ¼ h â†’ f âˆ¼ h
 _ âˆ¼âŸ¨ p âŸ© q = âˆ¼-trans p q

 _âˆ¼âˆ : (f : Î  A) â†’ f âˆ¼ f
 _âˆ¼âˆ _ = âˆ¼-refl

 infix  1 _âˆ¼âˆ
 infixr 0 _âˆ¼âŸ¨_âŸ©_

\end{code}

Fixities:

\begin{code}

infix  3  _â»Â¹
infix  1 _âˆ
infixr 0 _ï¼âŸ¨_âŸ©_
infixl 2 _âˆ™_

\end{code}
