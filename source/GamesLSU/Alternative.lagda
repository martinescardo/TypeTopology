Martin Escardo, Paulo Oliva, 9th July 2024.

Alternative, equivalent, inductive definition of the type Game of
games, which may have some practical advantages, such as more modular
definitions of games.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (J)

module GamesLSU.Alternative (R : Type) where

open import UF.Equiv
open import UF.FunExt

open import GamesLSU.TypeTrees
open import GamesLSU.FiniteHistoryDependent R
             renaming (Game to Game' ;
                       game to game')

open import MonadOnTypesLSU.K

open K-definitions R

data Game : Typeâ‚ where
 leaf   : R â†’ Game
 branch : (X : Type) â†’ K X â†’ (X â†’ Game) â†’ Game

leaf' : R â†’ Game'
leaf' r = game' [] (Î» âŸ¨âŸ© â†’ r) âŸ¨âŸ©

branch' : (X : Type) â†’ K X â†’ (X â†’ Game') â†’ Game'
branch' X Ï• Gf = game' (X âˆ· (game-tree âˆ˜ Gf))
                       (Î» (x :: xs) â†’ payoff-function (Gf x) xs)
                       (Ï• :: (quantifier-tree âˆ˜ Gf))

from-Game : Game â†’ Game'
from-Game (leaf r)        = leaf' r
from-Game (branch X Ï• Gf) = branch' X Ï• (from-Game âˆ˜ Gf)

\end{code}

We need the curried version h of the conversion function to-Game
defined below to convince the termination checker that the following
recursion does terminate.

\begin{code}

to-Game : Game' â†’ Game
to-Game (game' Xt q Ï•t) = h Xt q Ï•t
 where
  h : (Xt : ğ‘») â†’ (Path Xt â†’ R) â†’ ğ“š Xt â†’ Game
  h []       q âŸ¨âŸ©        = leaf (q âŸ¨âŸ©)
  h (X âˆ· Xf) q (Ï• :: Ï•f) = branch X Ï• (Î» x â†’ h (Xf x) (subpred q x) (Ï•f x))

\end{code}

The equations we would have liked to use to define the function
to-Game are the following:

\begin{code}

to-Game-base
 : (q : Path [] â†’ R)
 â†’ to-Game (game' [] q âŸ¨âŸ©) ï¼ leaf (q âŸ¨âŸ©)
to-Game-base q = refl

to-Game-step
 : (X : Type) (Xf : X â†’ ğ‘») (Ï• : K X) (Ï•f : (x : X) â†’ ğ“š (Xf x)) (q : Path (X âˆ· Xf) â†’ R)
 â†’ to-Game (game' (X âˆ· Xf) q (Ï• :: Ï•f))
 ï¼ branch X Ï• (Î» x â†’ to-Game (game' (Xf x) (subpred q x) (Ï•f x)))
to-Game-step X Xf Ï• Ï•f q = refl

\end{code}

We also have the following equivalent definitional equalities
expressed in terms of leaf' and branch':

\begin{code}

to-Game-base' : (r : R) â†’ to-Game (leaf' r) ï¼ leaf r
to-Game-base' r = refl

module _
         (X : Type)
         (Xf : X â†’ ğ‘»)
         (Ï• : K X)
         (Ï•f : (x : X) â†’ ğ“š (Xf x))
         (q : Path (X âˆ· Xf) â†’ R)
       where

 subgame : X â†’ Game'
 subgame x = game' (Xf x) (subpred q x) (Ï•f x)

 to-Game-step' : to-Game (branch' X Ï• subgame) ï¼ branch X Ï• (to-Game âˆ˜ subgame)
 to-Game-step' = refl

\end{code}

The above conversion functions are mutually inverse and so the types
Game and Game' are equivalent, assuming function extensionality.

\begin{code}

module _ (fe : Fun-Ext) where

 from-to-Game : from-Game âˆ˜ to-Game âˆ¼ id
 from-to-Game (game' Xt q Ï•t) = h Xt q Ï•t
  where
   h : (Xt : ğ‘»)
       (q : Path Xt â†’ R)
       (Ï•t : ğ“š Xt)
     â†’ from-Game (to-Game (game' Xt q Ï•t)) ï¼ game' Xt q Ï•t
   h []       q âŸ¨âŸ©       = refl
   h (X âˆ· Xf) q (Ï• :: Ï•f) =
    from-Game (to-Game (game' (X âˆ· Xf) q (Ï• :: Ï•f))) ï¼âŸ¨reflâŸ©
    from-Game (branch X Ï• (to-Game âˆ˜ Gf))            ï¼âŸ¨reflâŸ©
    branch' X Ï• Hf                                   ï¼âŸ¨ I âŸ©
    branch' X Ï• Gf                                   ï¼âŸ¨reflâŸ©
    game' (X âˆ· Xf) q (Ï• :: Ï•f)                       âˆ
    where
     Gf Hf : X â†’ Game'
     Gf x = subgame X Xf Ï• Ï•f q x
     Hf x = from-Game (to-Game (Gf x))

     IH : Hf âˆ¼ Gf
     IH x = h (Xf x) (subpred q x) (Ï•f x)

     I : branch' X Ï• Hf ï¼ branch' X Ï• Gf
     I = ap (branch' X Ï•) (dfunext fe IH)

 to-from-Game : to-Game âˆ˜ from-Game âˆ¼ id
 to-from-Game (leaf x)        = refl
 to-from-Game (branch X Ï• Gf) =
  to-Game (from-Game (branch X Ï• Gf))    ï¼âŸ¨reflâŸ©
  to-Game (branch' X Ï• (from-Game âˆ˜ Gf)) ï¼âŸ¨reflâŸ©
  branch X Ï• (to-Game âˆ˜ from-Game âˆ˜ Gf)  ï¼âŸ¨ I âŸ©
  branch X Ï• Gf                          âˆ
  where
   I = ap (branch X Ï•) (dfunext fe (to-from-Game âˆ˜ Gf))

 Game-is-equiv-to-Game' : Game â‰ƒ Game'
 Game-is-equiv-to-Game' = qinveq from-Game (to-Game , to-from-Game , from-to-Game)

\end{code}
