Ian Ray, started: 2023-09-12 - updated: 2024-02-05.

A Sup Lattice L is a set with a partial order â‰¤ that has suprema of 'small'
types. We will use three universe parameters: ğ“¤ for the carrier, ğ“¦ for the
order values and ğ“¥ for the families which have suprema.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.Equiv
open import UF.FunExt
open import UF.HedbergApplications
open import UF.Logic
open import UF.Powerset-MultiUniverse
open import UF.PropTrunc
open import UF.Retracts
open import UF.Sets
open import UF.SubtypeClassifier
open import UF.Size

module OrderedTypes.SupLattice
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
       where

open import Locales.Frame pt fe hiding (âŸ¨_âŸ© ; join-of)
open import Slice.Family
open import UF.ImageAndSurjection pt

open AllCombinators pt fe
open PropositionalTruncation pt

\end{code}

We commence by defining sup lattices.

\begin{code}

module _ (ğ“¤ ğ“£ ğ“¥ : Universe) where

 sup-lattice-data : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“£ âº âŠ” ğ“¥ âº Ì‡
 sup-lattice-data A = (A â†’ A â†’ Î© ğ“£) Ã— (Fam ğ“¥ A â†’ A)

 is-sup-lattice : {A : ğ“¤ Ì‡ } â†’ sup-lattice-data A â†’ ğ“¤ âŠ” ğ“£ âŠ” ğ“¥ âº Ì‡
 is-sup-lattice {A} (_â‰¤_ , â‹_) = is-partial-order A _â‰¤_ Ã— suprema
  where
   open Joins _â‰¤_
   suprema : ğ“¤ âŠ” ğ“£ âŠ” ğ“¥ âº Ì‡
   suprema = (U : Fam ğ“¥ A) â†’ ((â‹ U) is-lub-of U) holds

 sup-lattice-structure : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ âº âŠ” ğ“£ âº Ì‡
 sup-lattice-structure A = Î£ d ê‰ (sup-lattice-data A) , is-sup-lattice d

 Sup-Lattice : (ğ“¤ âŠ” ğ“£ âŠ” ğ“¥) âº Ì‡
 Sup-Lattice = Î£ A ê‰ ğ“¤ Ì‡ , sup-lattice-structure A

\end{code}

Now we give some naming conventions which will be useful.

\begin{code}

âŸ¨_âŸ© : Sup-Lattice ğ“¤ ğ“£ ğ“¥ â†’ ğ“¤ Ì‡
âŸ¨ A , rest âŸ© = A

order-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ (âŸ¨ L âŸ© â†’ âŸ¨ L âŸ© â†’ Î© ğ“£)
order-of (A , (_â‰¤_ , â‹_) , rest) = _â‰¤_

syntax order-of L x y = x â‰¤âŸ¨ L âŸ© y

join-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ Fam ğ“¥ âŸ¨ L âŸ© â†’ âŸ¨ L âŸ©
join-of (A , (_â‰¤_ , â‹_) , rest) = â‹_

syntax join-of L U = â‹âŸ¨ L âŸ© U

partial-orderedness-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
                       â†’ is-partial-order âŸ¨ L âŸ© (order-of L)
partial-orderedness-of (A , (_â‰¤_ , â‹_) , order , is-lub-of) = order

reflexivity-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ is-reflexive (order-of L) holds
reflexivity-of L = prâ‚ (prâ‚ (partial-orderedness-of L))

antisymmetry-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ is-antisymmetric (order-of L)
antisymmetry-of L = prâ‚‚ (partial-orderedness-of L)

transitivity-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ is-transitive (order-of L) holds
transitivity-of L = prâ‚‚ (prâ‚ (partial-orderedness-of L))

join-is-lub-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
               â†’ (U : Fam ğ“¥ âŸ¨ L âŸ©)
               â†’ ((order-of L) Joins.is-lub-of join-of L U) U holds
join-is-lub-of (A , (_â‰¤_ , â‹_) , order , suprema) = suprema

join-is-upper-bound-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
                       â†’ (U : Fam ğ“¥ âŸ¨ L âŸ©)
                       â†’ ((order-of L) Joins.is-an-upper-bound-of
                          join-of L U) U holds
join-is-upper-bound-of L U = prâ‚ (join-is-lub-of L U)

join-is-least-upper-bound-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
                             â†’ (U : Fam ğ“¥ âŸ¨ L âŸ©)
                             â†’ ((u' , _) : Joins.upper-bound (order-of L) U)
                             â†’ (order-of L (join-of L U) u') holds
join-is-least-upper-bound-of L U = prâ‚‚ (join-is-lub-of L U)

sethood-of : (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) â†’ is-set âŸ¨ L âŸ©
sethood-of L =
 type-with-prop-valued-refl-antisym-rel-is-set
  (Î» x â†’ Î» y â†’ order-of L x y holds)
  (Î» x â†’ Î» y â†’ holds-is-prop (order-of L x y))
  (Î» x â†’ reflexivity-of L x)
  (Î» x â†’ Î» y â†’ antisymmetry-of L)

\end{code}

We now define monotone endomaps on a sup-lattice and specify monotone endomaps
as a special case.

\begin{code}

module _ where

 is-monotone : {ğ“¤ ğ“¤' ğ“£ ğ“£' ğ“¥ ğ“¥' : Universe}
             â†’ (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥) (M : Sup-Lattice ğ“¤' ğ“£' ğ“¥')
             â†’ (f : âŸ¨ L âŸ© â†’ âŸ¨ M âŸ©)
             â†’ ğ“¤ âŠ” ğ“£ âŠ” ğ“£' Ì‡
 is-monotone L M f = (x y : âŸ¨ L âŸ©)
                   â†’ (x â‰¤âŸ¨ L âŸ© y) holds
                   â†’ (f x â‰¤âŸ¨ M âŸ© f y) holds

 is-monotone-endomap : {ğ“¤ ğ“£ ğ“¥ : Universe}
                     â†’ (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
                     â†’ (f : âŸ¨ L âŸ© â†’ âŸ¨ L âŸ©)
                     â†’ ğ“¤ âŠ” ğ“£ Ì‡
 is-monotone-endomap L f = is-monotone L L f

\end{code}

We now show that when one subset contains another the join of their total
spaces are ordered as expected.

\begin{code}

module _
        {ğ“¤ ğ“£ ğ“¥ : Universe}
        (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
        {A : ğ“¥ Ì‡ }
        (m : A â†’ âŸ¨ L âŸ©)
       where

 open Joins (order-of L)

 joins-preserve-containment : {P : ğ“Ÿ {ğ“¥} A} {Q : ğ“Ÿ {ğ“¥} A}
                            â†’ P âŠ† Q
                            â†’ ((â‹âŸ¨ L âŸ© ã€ m , P ã€‘)
                             â‰¤âŸ¨ L âŸ© (â‹âŸ¨ L âŸ© ã€ m , Q ã€‘)) holds
 joins-preserve-containment {P} {Q} C =
  (join-is-least-upper-bound-of L ã€ m , P ã€‘)
   (â‹âŸ¨ L âŸ© ã€ m , Q ã€‘ ,
    (Î» (b , b-in-P) â†’ (join-is-upper-bound-of L ã€ m , Q ã€‘)
                      (b , C b b-in-P)))

\end{code}

We now show if a type is small and has a map to the carrier then it has a join.

\begin{code}

module _
        {ğ“¤ ğ“£ ğ“¥ ğ“¦ : Universe}
        (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
        {T : ğ“¦ Ì‡ }
        (m : T â†’ âŸ¨ L âŸ©)
        (T-is-small : T is ğ“¥ small)
       where
 private
  T' : ğ“¥ Ì‡
  T' = (resized T) T-is-small

  T'-â‰ƒ-T : T' â‰ƒ T
  T'-â‰ƒ-T = resizing-condition T-is-small

  T'-to-T : T' â†’ T
  T'-to-T = âŒœ T'-â‰ƒ-T âŒ

  T'-to-T-is-equiv : is-equiv T'-to-T
  T'-to-T-is-equiv = âŒœ T'-â‰ƒ-T âŒ-is-equiv

  T-to-T' : T â†’ T'
  T-to-T' =  âŒœ T'-â‰ƒ-T âŒâ»Â¹

  T'-to-T-has-section : has-section T'-to-T
  T'-to-T-has-section = equivs-have-sections T'-to-T T'-to-T-is-equiv

  T'-to-T-is-section : is-section T'-to-T
  T'-to-T-is-section = equivs-are-sections T'-to-T T'-to-T-is-equiv

  section-T'-to-T : T'-to-T âˆ˜ T-to-T' âˆ¼ id
  section-T'-to-T = section-equation T'-to-T T'-to-T-has-section

  retraction-T'-to-T : T-to-T' âˆ˜ T'-to-T âˆ¼ id
  retraction-T'-to-T = inverses-are-retractions' T'-â‰ƒ-T

  T'-inclusion : T' â†’ âŸ¨ L âŸ©
  T'-inclusion = m âˆ˜ T'-to-T

  s : âŸ¨ L âŸ©
  s = â‹âŸ¨ L âŸ© (T' , T'-inclusion)

 open Joins (order-of L)

 sup-of-small-fam-is-lub : (s is-lub-of (T , m)) holds
 sup-of-small-fam-is-lub = (I , III)
  where
   I : (s is-an-upper-bound-of (T , m)) holds
   I t = II
    where
     II : (m t â‰¤âŸ¨ L âŸ© s) holds
     II = transport (Î» - â†’ (m - â‰¤âŸ¨ L âŸ© s) holds)
                    (section-T'-to-T t)
                    (join-is-upper-bound-of L (T' , T'-inclusion) (T-to-T' t))
   III : ((u , _) : upper-bound (T , m)) â†’ (s â‰¤âŸ¨ L âŸ© u) holds
   III (u , is-upbnd-T) = IV
    where
     IV : (s â‰¤âŸ¨ L âŸ© u) holds
     IV = join-is-least-upper-bound-of
            L (T' , T'-inclusion) (u , Î» i â†’ is-upbnd-T (T'-to-T i))

\end{code}

We now show that reindexing families along a surjection preserves the supremum.

\begin{code}

module _
        {ğ“¤ ğ“£ ğ“¥ ğ“¦ ğ“¦' : Universe}
        (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
        {T : ğ“¦ Ì‡ }
        {T' : ğ“¦' Ì‡ }
        (e : T' â†  T)
        (m : T â†’ âŸ¨ L âŸ©)
       where

 open Joins (order-of L)

 reindexing-along-surj-ï¼-sup : (s s' : âŸ¨ L âŸ©)
                              â†’ (s is-lub-of (T , m)) holds
                              â†’ (s' is-lub-of (T' , m âˆ˜ âŒ e âŒŸ)) holds
                              â†’ s ï¼ s'
 reindexing-along-surj-ï¼-sup
  s s' (is-upbnd , is-least-upbnd) (is-upbnd' , is-least-upbnd') =
   antisymmetry-of L I IV
  where
   I : (s â‰¤âŸ¨ L âŸ© s') holds
   I = is-least-upbnd (s' , Î» t â†’ III t (âŒâŒŸ-is-surjection e t))
    where
     II : (t : T)
        â†’ Î£ t' ê‰ T' , âŒ e âŒŸ t' ï¼ t
        â†’ (m t â‰¤âŸ¨ L âŸ© s') holds
     II t (t' , path) =
       transport (Î» z â†’ (m z â‰¤âŸ¨ L âŸ© s') holds) path (is-upbnd' t')
     III : (t : T)
         â†’ (Æ t' ê‰ T' , âŒ e âŒŸ t' ï¼ t) holds
         â†’ (m t â‰¤âŸ¨ L âŸ© s') holds
     III t = âˆ¥âˆ¥-rec (holds-is-prop (m t â‰¤âŸ¨ L âŸ© s')) (II t)
   IV : (s' â‰¤âŸ¨ L âŸ© s) holds
   IV = is-least-upbnd' (s , Î» t' â†’ is-upbnd (âŒ e âŒŸ t'))

\end{code}

We can also show that reindexing along an equivalence preserves the supremum.
This follows from the previous result as any equivalence can be demoted to a
surjection.

\begin{code}

module _
        {ğ“¤ ğ“£ ğ“¥ ğ“¦ ğ“¦' : Universe}
        (L : Sup-Lattice ğ“¤ ğ“£ ğ“¥)
        {T : ğ“¦ Ì‡ }
        {T' : ğ“¦' Ì‡ }
        (e : T' â‰ƒ T)
        (m : T â†’ âŸ¨ L âŸ©)
       where

 open Joins (order-of L)

 reindexing-along-equiv-ï¼-sup : (s s' : âŸ¨ L âŸ©)
                               â†’ (s is-lub-of (T , m)) holds
                               â†’ (s' is-lub-of (T' , m âˆ˜ âŒœ e âŒ )) holds
                               â†’ s ï¼ s'
 reindexing-along-equiv-ï¼-sup =
  reindexing-along-surj-ï¼-sup
   L (âŒœ e âŒ , equivs-are-surjections âŒœ e âŒ-is-equiv) m

\end{code}
