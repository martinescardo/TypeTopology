Kelton OBrien, 31 May 2024

A proof that the Axiom of Choice implies Zorn's Lemma, as well as
relevant definitions.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import Ordinals.Equivalence
open import Ordinals.Type
open import Ordinals.Underlying
open import UF.Base
open import UF.Choice
open import UF.ClassicalLogic
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Logic
open import UF.Powerset-MultiUniverse
open import UF.PropTrunc
open import UF.Size
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier
open import UF.UA-FunExt
open import UF.Univalence
open import UF.UniverseEmbedding

module OrderedTypes.ZornsLemma
 {ğ“¤ ğ“£ : Universe}
 (pt : propositional-truncations-exist)
 (ua : Univalence)
 {X : ğ“¤ Ì‡ }
 (_â‰ª_ : X â†’ X â†’ ğ“£ Ì‡ )
  where

open PropositionalTruncation pt
open import Ordinals.BuraliForti ua
open import Ordinals.OrdinalOfOrdinals ua
open import UF.ImageAndSurjection pt

fe : FunExt
fe = Univalence-gives-FunExt ua
pe : Prop-Ext
pe = Univalence-gives-Prop-Ext ua
pe' : PropExt
pe' ğ“¤ = pe {ğ“¤}
fe' : Fun-Ext
fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open UF.Choice.ExcludedMiddle pt fe
open UF.Choice.Univalent-Choice fe pt
open UF.Choice.choice-functions pt pe' fe
open UF.Logic.Existential pt
open UF.Logic.Universal fe'
open import OrderedTypes.Poset fe'
open import Ordinals.WellOrderingTaboo fe' pe
open inhabited-subsets pt

open PosetAxioms
open ClassicalWellOrder pt

_â‹˜_ : X â†’ X â†’ (ğ“¤ âŠ” ğ“£) Ì‡
a â‹˜ b =  a â‰ª b Ã— (a â‰  b)

is-chain : {ğ“¥ : Universe} â†’ (Y : ğ“Ÿ {ğ“¥} X ) â†’  (ğ“¤ âŠ” ğ“£ âŠ” ğ“¥) Ì‡
is-chain Y = (x y : X) â†’ x âˆˆ Y â†’ y âˆˆ Y â†’ (x â‰ª y âˆ¨ y â‰ª x)

has-maximal-element-strong :  (ğ“¤ âŠ” ğ“£) Ì‡
has-maximal-element-strong = Î£ x ê‰ X , ((y : X) â†’ x â‰ª y â†’ x ï¼ y)

has-maximal-element : (ğ“¤ âŠ” ğ“£) Ì‡
has-maximal-element = âˆƒ x ê‰ X , ((y : X) â†’ x â‰ª y â†’ x ï¼ y)

all-chains-have-upper-bound : {ğ“¥ : Universe} â†’ (ğ“¤ âŠ” ğ“£ âŠ” (ğ“¥ âº)) Ì‡
all-chains-have-upper-bound {ğ“¥} =
 (Y : ğ“Ÿ {ğ“¥} X) â†’ (is-chain Y) â†’ Î£ x ê‰ X , (âˆ€ y â†’ y âˆˆ Y â†’ y â‰ª x)

\end{code}

The following is a formalization ofthe standard transfinite-induction-based
proof of Zorn's lemma. The closest informal analog to the proof can be found
here: https://math.stackexchange.com/a/2889205.
The primary differences between this proof and that proof are just ones of
how sets are treated (notice the lack of a particular poset P),
additional detail in things that are glossed over in the formal proof,
(e.g the proof that the chain allfbs is a chain is not given).
The proof idea is the same.

We do a proof by contradiction (which is why we have that double negation),
where we assume that even though all chains have a least upper bound,
the set at hand does not have a maximal element.
These assumptions allow us to construct a function `f' from the ordinals to X,
which is a set, which preserves order, is therefore left-cancellable, and again
in turn an equivalence. `f' being order preserving relies on the fact
that there is no maximal element, as otherwise `f' would (as defined)
map many ordinals to the maximal element.  An equivalence between the
ordinals and a set is impossible, so there must (not-not) be a
maximum.

While this is the core of the proof logic, the final result to be used
elsewhere is found below, and takes in more reasonable inputs.

\begin{code}

choice-function-gives-zorns-lemma
 : Excluded-Middle
 â†’ poset-axioms (_â‰ª_)
 â†’ all-chains-have-upper-bound
 â†’ (Î£ Îµ ê‰ (ğ“Ÿ {ğ“£ âŠ” ğ“¤} X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A))
 â†’ has-maximal-element

choice-function-gives-zorns-lemma
 lem
 (X-is-set , â‰ª-prop , â‰ª-refl , â‰ª-trans , â‰ª-antisym )
 chains
 (Îµ , Îµ-behaviour) =
  (Â¬Â¬Î£â†’âˆƒ pt dn (Î» no-max â†’ absurd no-max))
   where
    dn : {ğ“¥ : Universe} â†’ DNE ğ“¥
    dn {ğ“¥} = EM-gives-DNE lem

    eq-is-Â¬Â¬-stable : {x : X} {y : X} â†’  Â¬Â¬-stable (x ï¼ y)
    eq-is-Â¬Â¬-stable {x} {y} Â¬Â¬x=y = dn (x ï¼ y) (X-is-set) Â¬Â¬x=y

    â‰ª-is-Â¬Â¬-stable : {x : X} {y : X} â†’ Â¬Â¬-stable (x â‰ª y)
    â‰ª-is-Â¬Â¬-stable {x} {y} Â¬Â¬ll = dn (x â‰ª y) (â‰ª-prop x y) Â¬Â¬ll

    â‹˜-prop : (a : X) â†’ (b : X) â†’ is-prop (a â‹˜ b)
    â‹˜-prop a b = Ã—-is-prop (â‰ª-prop a b) (negations-are-props fe')

    â‰ª-trans-â‹˜ : (x y z : X) â†’  x â‰ª y â†’ y â‹˜ z â†’ x â‹˜ z
    â‰ª-trans-â‹˜ x y z xâ‰ªy (yâ‰ªz , yâ‰ z) =
     â‰ª-trans x y z xâ‰ªy yâ‰ªz ,
      Î» x=z â†’ yâ‰ z ((â‰ª-antisym y z) yâ‰ªz (transport (Î» q â†’ q â‰ª y) x=z xâ‰ªy))

    g : (Î± : Ordinal ğ“¤) â†’ ( âŸ¨ Î± âŸ© â†’ X) â†’ X
    g Î± s = Îµ  â… x ê‰ X âˆ£ â±¯ a ê‰ âŸ¨ Î± âŸ© , (( s a â‹˜ x ) , â‹˜-prop (s a) x ) â†

    f : Ordinal ğ“¤ â†’ X
    f = transfinite-recursion-on-OO X g

    A : Ordinal ğ“¤ â†’ ğ“Ÿ { (ğ“¤ âŠ” ğ“£)} X
    A Î± = â… x ê‰ X âˆ£ â±¯ a ê‰ âŸ¨ Î± âŸ© , (f (Î± â†“ a) â‹˜ x , â‹˜-prop (f (Î± â†“ a)) x ) â†

    f-behaviour : (Î± : Ordinal ğ“¤) â†’ f Î± ï¼ Îµ (A Î±)
    f-behaviour = transfinite-recursion-on-OO-behaviour X g

    âŠ²-is-classical-well-order : is-classical-well-order (_âŠ²_ {ğ“¤})
    âŠ²-is-classical-well-order =
      inductive-well-order-is-classical _âŠ²_ lem âŠ²-is-well-order

    âŠ²-is-trichotomous : {a b : Ordinal ğ“¤} â†’ (a âŠ² b) + (a ï¼ b) + (b âŠ² a)
    âŠ²-is-trichotomous {a} {b} = prâ‚ (( prâ‚ (prâ‚‚ âŠ²-is-classical-well-order)) a b)

    f-preserves-order : Â¬ has-maximal-element-strong
                      â†’ (Î± Î² : Ordinal ğ“¤)
                      â†’ Î² âŠ² Î±
                      â†’ f Î² â‹˜ f Î±
    f-preserves-order no-max = transfinite-induction-on-OO P (v no-max)
     where
      P : Ordinal ğ“¤ â†’ (ğ“¤ âº âŠ” ğ“£) Ì‡
      P Î± = âˆ€ Î² â†’ Î² âŠ² Î± â†’ f Î² â‹˜ f Î±
      v : Â¬ has-maximal-element-strong
        â†’ (Î± : Ordinal ğ“¤)
        â†’ ((a : âŸ¨ Î± âŸ©) â†’ P (Î± â†“ a))
        â†’ P Î±
      v no-max Î± s Î² (a , Î²=Î±â†“a) =
       transportâ»Â¹ (Î» q â†’ f q â‹˜ f Î±) Î²=Î±â†“a
        (transportâ»Â¹ (Î» q â†’ f (Î± â†“ a) â‹˜ q) (f-behaviour Î±) (step a))
       where
        allfb : ğ“Ÿ  X
        allfb = â… x ê‰ X âˆ£ Æ Î²' , ((Î²' âŠ² Î± ) Ã— (x ï¼ (f Î²'))) â†

        untrunc-works : (x y : X)
                      â†’ (Î£ Î²' ê‰ Ordinal ğ“¤ , ((Î²' âŠ² Î±) Ã— (x ï¼ f Î²')))
                      â†’ (Î£ Î²'' ê‰ Ordinal ğ“¤ , ((Î²'' âŠ² Î±) Ã— (y ï¼ f Î²'')))
                      â†’ x â‰ª y + y â‰ª x
        untrunc-works
         x
         y
         (Î²' , ((b' , Î²'=Î±â†“b') , x=fÎ²'))
         (Î²'' , ((b'' , Î²''=Î±â†“b'') , y=fÎ²''))
         = cases less (cases equal more) âŠ²-is-trichotomous
         where
          less : Î²'' âŠ² Î²' â†’ (x â‰ª y + y â‰ª x)
          less b''<b' =
           inr (transportâ»Â¹ (Î» q â†’ y â‰ª q) x=fÎ²'
            (transportâ»Â¹ (Î» q â†’ q â‰ª f Î²') y=fÎ²''
              (prâ‚ (transportâ»Â¹ (Î» q â†’ f Î²'' â‹˜ f q) Î²'=Î±â†“b' ((s b') Î²''
               (transport (Î» q â†’ Î²'' âŠ² q) Î²'=Î±â†“b' b''<b'))))))
          equal : Î²'' ï¼ Î²' â†’ (x â‰ª y + y â‰ª x)
          equal b''=b' =
           inr (transport (Î» q â†’ y â‰ª q)
            (transportâ»Â¹ (Î» q â†’ y ï¼ q) x=fÎ²'
             (transportâ»Â¹ (Î» q â†’ q ï¼ f Î²') y=fÎ²'' (ap f b''=b'))) (â‰ª-refl y))

          more : Î²' âŠ² Î²'' â†’ (x â‰ª y + y â‰ª x)
          more b'<b'' =
            inl (transportâ»Â¹ (Î» q â†’ x â‰ª q) y=fÎ²''
             (transportâ»Â¹ (Î» q â†’ q â‰ª f Î²'') x=fÎ²'
              (prâ‚ (transportâ»Â¹ (Î» q â†’ f Î²' â‹˜ f q) Î²''=Î±â†“b''
               ((s b'') Î²' (transport (Î» q â†’ Î²' âŠ² q) Î²''=Î±â†“b'' b'<b''))))))
        has-max-neg : (x : X) â†’ Â¬Â¬ (Î£ y ê‰ X , Â¬ (x â‰ª y â†’ x ï¼ y))
        has-max-neg x =
         not-Î -implies-not-not-Î£ (Î» z â†’ â†’-is-Â¬Â¬-stable eq-is-Â¬Â¬-stable)
          ((not-Î£-implies-Î -not no-max) x)

        allfb-is-chain :  is-chain allfb
        allfb-is-chain x y = âˆ¥âˆ¥-functorâ‚‚ (untrunc-works x y)

        allfb-has-ub : Î£ x ê‰ X , (âˆ€ y â†’ y âˆˆ allfb â†’ y â‰ª x)
        allfb-has-ub = chains allfb allfb-is-chain

        ub : X
        ub = prâ‚ allfb-has-ub

        ub-is-ub : âˆ€ y â†’ y âˆˆ allfb â†’ y â‰ª ub
        ub-is-ub = prâ‚‚ allfb-has-ub

        ub-is-strict'
         : (Î£ q ê‰ X , (Â¬ ( ub â‰ª q â†’ ub ï¼ q)))
         â†’ Î£ z ê‰ X , (âˆ€ y â†’ y âˆˆ allfb â†’ y â‹˜ z)
        ub-is-strict' (q , foo) =  q ,
         Î» y yin â†’ â‰ª-trans-â‹˜ y ub q (ub-is-ub y yin)
          ((â‰ª-is-Â¬Â¬-stable (prâ‚ (negation-of-implication foo)))
           , (prâ‚‚ (negation-of-implication foo)))

        ub-is-strict : âˆƒ z ê‰ X , (âˆ€ y â†’ y âˆˆ allfb â†’ y â‹˜ z)
        ub-is-strict = (âˆ¥âˆ¥-functor ub-is-strict') (Â¬Â¬Î£â†’âˆƒ pt dn (has-max-neg ub))

        AÎ±-inhabited' :  Î£ x ê‰ X , (âˆ€ y â†’ y âˆˆ allfb â†’ y â‹˜ x)
                         â†’  Î£ x ê‰ X , ((i : âŸ¨ Î± âŸ©) â†’ f (Î± â†“ i) â‹˜ x)
        AÎ±-inhabited' (x , ylt) =
         x ,  Î» i â†’  ylt (f (Î± â†“ i))  âˆ£  (Î± â†“ i) ,  ( ( i , refl )) , refl âˆ£

        AÎ±-inhabited :  âˆƒ x ê‰ X , (âˆ€ y â†’ y âˆˆ allfb â†’ y â‹˜ x) â†’ is-inhabited (A Î±)
        AÎ±-inhabited =  âˆ¥âˆ¥-functor AÎ±-inhabited'

        step : (a : âŸ¨ Î± âŸ©) â†’  (f (Î± â†“ a) â‹˜ Îµ (A Î±))
        step a = (Îµ-behaviour (A Î±) (AÎ±-inhabited (ub-is-strict ))) a

    f-is-injective : Â¬ has-maximal-element-strong
                   â†’ (a b : Ordinal ğ“¤)
                   â†’ a â‰  b â†’ f a â‰  f b
    f-is-injective no-max a b aâ‰ b =
     cases (less a b) (cases (equal a b aâ‰ b) (more a b)) âŠ²-is-trichotomous
     where
      less : (a b : Ordinal ğ“¤) â†’ a âŠ² b â†’ f a â‰  f b
      less a b a<b = prâ‚‚ (f-preserves-order no-max b a a<b)

      more : (a b : Ordinal ğ“¤) â†’ b âŠ² a â†’ f a â‰  f b
      more a b b<a = â‰ -sym (prâ‚‚ (f-preserves-order no-max a b b<a))

      equal : (a b : Ordinal ğ“¤) â†’ a â‰  b â†’ a ï¼ b â†’ f a â‰  f b
      equal a b aâ‰ b a=b = unique-from-ğŸ˜ (aâ‰ b a=b)

    f-is-left-cancellable
     : {a : Ordinal ğ“¤}
     â†’ {b : Ordinal ğ“¤}
     â†’ Â¬ has-maximal-element-strong
     â†’ f a ï¼ f b
     â†’ a ï¼ b
    f-is-left-cancellable {a} {b} no-max p =
     dn (a ï¼ b) (the-type-of-ordinals-is-a-set (ua ğ“¤) fe')
                 ((contrapositive (f-is-injective no-max a b)) (Â¬Â¬-intro p))

    f-is-embedding : Â¬ has-maximal-element-strong â†’ is-embedding f
    f-is-embedding no-max = lc-maps-into-sets-are-embeddings f
                             (f-is-left-cancellable no-max)
                             X-is-set

    X' : ğ“¤ âº Ì‡
    X' = image f

    f' : Ordinal ğ“¤ â†’ X'
    f' = corestriction f

    f'-is-equiv : Â¬ has-maximal-element-strong â†’ is-equiv f'
    f'-is-equiv no-max = corestriction-of-embedding-is-equivalence f
                          (f-is-embedding no-max)

    B : X â†’ ğ“¤ âº Ì‡
    B x = x âˆˆimage f

    B-is-prop : (x : X) â†’ is-prop (B x)
    B-is-prop x = being-in-the-image-is-prop x f

    Ï : Propositional-Resizing
    Ï = EM-gives-PR lem

    C : X â†’ ğ“¤ Ì‡
    C x = resize Ï (B x) (B-is-prop x)

    Ï„ : Nat C B
    Ï„ x = from-resize Ï (B x) (B-is-prop x)

    Ï„-is-equiv : (x : X) â†’ is-equiv (Ï„ x)
    Ï„-is-equiv x = from-resize-is-equiv Ï (B x) (B-is-prop x)

    X'' : ğ“¤ Ì‡
    X'' = Î£ x ê‰ X , C x

    e : Â¬ has-maximal-element-strong â†’ X'' â‰ƒ Ordinal ğ“¤
    e no-max = X''       â‰ƒâŸ¨ NatÎ£ Ï„ , NatÎ£-is-equiv C B Ï„ Ï„-is-equiv âŸ©
        X'        â‰ƒâŸ¨ â‰ƒ-sym (f' , f'-is-equiv no-max) âŸ©
        Ordinal ğ“¤ â– 

    the-type-of-ordinals-is-small : Â¬ has-maximal-element-strong
                                  â†’ is-small (Ordinal ğ“¤)
    the-type-of-ordinals-is-small no-max = X'' , (e no-max)

    absurd : Â¬ has-maximal-element-strong â†’ ğŸ˜
    absurd no-max = the-type-of-ordinals-is-large
                     (the-type-of-ordinals-is-small no-max)

axiom-of-choice-implies-zorns-lemma
 : Axiom-of-Choice
 â†’ poset-axioms (_â‰ª_)
 â†’ all-chains-have-upper-bound
 â†’ has-maximal-element

axiom-of-choice-implies-zorns-lemma ac (X-is-set , axioms-rest) = III
 where
  em : Excluded-Middle
  em = Choice-gives-Excluded-Middle pe' ac

  lifted-cf
   : âˆ¥ Lift (ğ“¤ âŠ” ğ“£) X âˆ¥
   â†’ âˆƒ Îµ ê‰ (ğ“Ÿ (Lift (ğ“¤ âŠ” ğ“£) X) â†’ (Lift (ğ“¤ âŠ” ğ“£) X))
         , ((A : ğ“Ÿ (Lift (ğ“¤ âŠ” ğ“£) X)) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
  lifted-cf =
   Choice-gives-Choiceâ‚„ ac (Lift (ğ“¤ âŠ” ğ“£) X) (Lift-is-set (ğ“¤ âŠ” ğ“£) X X-is-set)

  lower-cf
   : Î£ Îµ ê‰ (ğ“Ÿ (Lift (ğ“¤ âŠ” ğ“£) X) â†’ (Lift (ğ“¤ âŠ” ğ“£) X))
         , ((A : ğ“Ÿ (Lift (ğ“¤ âŠ” ğ“£) X)) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
   â†’  Î£ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
  lower-cf (Ïµ , f) = (Ïµ' , f')
   where
    Ïµ' : ğ“Ÿ X â†’ X
    Ïµ' S = lower (Ïµ (S âˆ˜ lower))
    inhab-trans : {A' : ğ“Ÿ X} â†’ is-inhabited A' â†’ is-inhabited (A' âˆ˜ lower)
    inhab-trans {A'} isA' =
     isA' >>= Î» isA'' â†’
      âˆ£ lift (ğ“¤ âŠ” ğ“£) (prâ‚ isA'') ,
      transport (Î» q â†’ (A' q) holds) (Îµ-Lift (ğ“¤ âŠ” ğ“£) (prâ‚ isA'')) (prâ‚‚ isA'')âˆ£
    f' : (A' : ğ“Ÿ X) â†’ is-inhabited A' â†’ Ïµ' A' âˆˆ A'
    f' A' A'-inhab = (f (A' âˆ˜ lower) (inhab-trans {A'} A'-inhab))

  choice-function : âˆ¥ X âˆ¥
                  â†’ âˆƒ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
  choice-function isX =  âˆ¥âˆ¥-functor
                          lower-cf
                          (lifted-cf (âˆ¥âˆ¥-functor (lift (ğ“¤ âŠ” ğ“£)) isX))

  I' : all-chains-have-upper-bound
     â†’ âˆƒ Îµ ê‰ (ğ“Ÿ X â†’ X) , ((A : ğ“Ÿ X) â†’ is-inhabited A â†’ Îµ A âˆˆ A)
     â†’ has-maximal-element
  I' chains = âˆ¥âˆ¥-rec
               (âˆƒ-is-prop)
               (choice-function-gives-zorns-lemma em
                 (X-is-set , axioms-rest)
                 chains)

  I : all-chains-have-upper-bound â†’ âˆ¥ X âˆ¥ â†’ has-maximal-element
  I chains-have-ub z = I' chains-have-ub (choice-function z)

  empty-has-no-ub : Â¬ âˆ¥ X âˆ¥ â†’ Â¬ (all-chains-have-upper-bound {ğ“¥})
  empty-has-no-ub Î½  chains =
   Î½ âˆ£ prâ‚ (chains âˆ… Î» x y xin yin â†’  unique-from-ğŸ˜ (Î½ âˆ£ x âˆ£)) âˆ£

  II : all-chains-have-upper-bound  â†’  Â¬ âˆ¥ X âˆ¥ â†’ has-maximal-element
  II chains-have-ub Î½ = unique-from-ğŸ˜ ((empty-has-no-ub Î½) chains-have-ub)

  III : all-chains-have-upper-bound â†’ has-maximal-element
  III chains-have-ub =
   cases (I chains-have-ub) (II chains-have-ub) (em âˆ¥ X âˆ¥ âˆ¥âˆ¥-is-prop)

\end{code}
