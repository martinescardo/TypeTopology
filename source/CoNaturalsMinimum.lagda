Martin Escardo, 12th September 2018.

We define the minimum function on â„•âˆ by corecursion as defined in the
module CoNaturals. The lack of definitional equalities make some proof
lengthy.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt

module CoNaturalsMinimum (fe : âˆ€ U V â†’ funext U V) where

open import Two
open import GenericConvergentSequence
open import CoNaturals fe
open import UF-Base

\end{code}


\begin{code}

private
 Îº : â„•âˆ Ã— â„•âˆ â†’ ğŸ™ {Uâ‚€} + â„•âˆ Ã— â„•âˆ
 Îº (u , v) = ğŸš-Cases (positivity u)
              (inl *)
              (ğŸš-Cases (positivity v)
                (inl *)
                (inr (Pred u , Pred v)))

min : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
min = â„•âˆ-corec Îº

\end{code}

The defining equations of min are:

\begin{code}

min-eqâ‚€ : âˆ€ v   â†’ min (Zero , v) â‰¡ Zero
min-eqâ‚ : âˆ€ u   â†’ min (Succ u , Zero) â‰¡ Zero
min-eqâ‚‚ : âˆ€ u v â†’ min (Succ u , Succ v) â‰¡ Succ (min (u , v))

min-eqâ‚€ = Î» v   â†’ Coalg-morphism-Zero Îº (Zero , v) * refl
min-eqâ‚ = Î» u   â†’ Coalg-morphism-Zero Îº (Succ u , Zero) * refl
min-eqâ‚‚ = Î» u v â†’ Coalg-morphism-Succ Îº (Succ u , Succ v) (u , v) refl

\end{code}

Using the equations min-eqâ‚€ and min-eqâ‚‚, we have that the function

Î» u â†’ min (u , u) is an algebra homomorphism from P to P, where
P : â„•âˆ â†’ ğŸ™ + â„•âˆ is the final coalgebra constructed in the module
CoNaturals) and hence is equal to the identity:

\begin{code}

min-idempotent : âˆ€ u â†’ min (u , u) â‰¡ u
min-idempotent u = ap (Î» - â†’ - u) h-is-corec
 where
  h : â„•âˆ â†’ â„•âˆ
  h u = min (u , u)
  h-homomorphism : is-homomorphism PRED h
  h-homomorphism = dfunext (fe Uâ‚€ Uâ‚€) Î» u â†’ Ï† u (Zero-or-Succ (fe Uâ‚€ Uâ‚€) u)
   where
    Ï† : (u : â„•âˆ) â†’ (u â‰¡ Zero) + (u â‰¡ Succ (Pred u)) â†’ PRED (h u) â‰¡ ğŸ™+ h (PRED u)
    Ï† u (inl r) =
      PRED (min (u , u))       â‰¡âŸ¨ ap (Î» - â†’ PRED (min (- , -))) r âŸ©
      PRED (min (Zero , Zero)) â‰¡âŸ¨ ap PRED (min-eqâ‚€ Zero) âŸ©
      PRED Zero                â‰¡âŸ¨ ğŸ™+id-is-id (PRED Zero) â»Â¹ âŸ©
      ğŸ™+ h (PRED Zero)         â‰¡âŸ¨ ap (Î» - â†’ ğŸ™+ h (PRED -)) (r â»Â¹)  âŸ©
      ğŸ™+ h (PRED u)            âˆ
    Ï† u (inr s) =
      PRED (min (u , u))                         â‰¡âŸ¨ ap (Î» - â†’ PRED (min (- , -))) s âŸ©
      PRED (min (Succ (Pred u) , Succ (Pred u))) â‰¡âŸ¨ ap PRED (min-eqâ‚‚ (Pred u) (Pred u)) âŸ©
      PRED (Succ (min (Pred u , Pred u)))        â‰¡âŸ¨ refl âŸ©
      inr (min (Pred u , Pred u))                â‰¡âŸ¨ refl âŸ©
      ğŸ™+ h (PRED (Succ (Pred u)))                â‰¡âŸ¨ ap (Î» - â†’ ğŸ™+ h (PRED -)) (s â»Â¹) âŸ©
      ğŸ™+ h (PRED u)                              âˆ
  h-is-corec : h â‰¡ id
  h-is-corec = homomorphism-uniqueness PRED h id h-homomorphism id-homomorphism


\end{code}

The following equation is derived from the above equations min-eqâ‚€ and
min-eqâ‚ by cases on whether u is positive:

\begin{code}

eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ :
    (h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ)
  â†’ (âˆ€ v   â†’ h (Zero , v) â‰¡ Zero)
  â†’ (âˆ€ u   â†’ h (Succ u , Zero) â‰¡ Zero)
  â†’ (âˆ€ u   â†’ h (u , Zero) â‰¡ Zero)
eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ h eqâ‚€ eqâ‚ u = Î³ u (positivity u) refl
 where
  Î³ : âˆ€ u n â†’ positivity u â‰¡ n â†’ h (u , Zero) â‰¡ Zero
  Î³ u â‚€ r = b âˆ™ a
   where
    a : u â‰¡ Zero
    a = is-Zero-equal-Zero (fe Uâ‚€ Uâ‚€) r
    b : h (u , Zero) â‰¡ u
    b = back-transport (Î» - â†’ h (- , Zero) â‰¡ -) a (eqâ‚€ Zero)
  Î³ u â‚ r = back-transport (Î» - â†’ h (- , Zero) â‰¡ Zero) a (eqâ‚ (Pred u))
   where
    a : u â‰¡ Succ (Pred u)
    a = not-Zero-is-Succ (fe Uâ‚€ Uâ‚€) (positive-is-not-Zero r)

min-eqâ‚ƒ : âˆ€ u â†’ min (u , Zero) â‰¡ Zero
min-eqâ‚ƒ = eqâ‚ƒ-from-eqâ‚€-and-eqâ‚ min min-eqâ‚€ min-eqâ‚

\end{code}

Conversely, if a function satisfies the above equations, then it is a
coalgebra homomorphism and hence is equal to â„•âˆ-corec p.

\begin{code}

equations-characterize-homomorphisms :
    (h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ)
  â†’ (âˆ€ v   â†’ h (Zero , v) â‰¡ Zero)
  â†’ (âˆ€ u   â†’ h (Succ u , Zero) â‰¡ Zero)
  â†’ (âˆ€ u v â†’ h (Succ u , Succ v) â‰¡ Succ (h (u , v)))
  â†’ is-homomorphism Îº h
equations-characterize-homomorphisms h eqâ‚€ eqâ‚ eqâ‚‚ = dfunext (fe Uâ‚€ Uâ‚€) Î³
  where
   Î³ : (w : â„•âˆ Ã— â„•âˆ) â†’ PRED (h w) â‰¡ ğŸ™+ h (Îº w)
   Î³ (u , v) = Ï† u (Zero-or-Succ (fe Uâ‚€ Uâ‚€) u)
    where
     Ï† : (u : â„•âˆ) â†’ (u â‰¡ Zero) + (u â‰¡ Succ(Pred u)) â†’ PRED (h (u , v)) â‰¡ ğŸ™+ h (Îº (u , v))
     Ï† u (inl q) =
       PRED (h (u , v))       â‰¡âŸ¨ ap (Î» - â†’ PRED (h (- , v))) q âŸ©
       PRED (h (Zero , v))    â‰¡âŸ¨ ap PRED (eqâ‚€ v) âŸ©
       PRED Zero              â‰¡âŸ¨ refl âŸ©
       inl *                  â‰¡âŸ¨ refl âŸ©
       ğŸ™+ h (inl *)           â‰¡âŸ¨ refl âŸ©
       ğŸ™+ h (Îº (Zero , v))    â‰¡âŸ¨ ap (Î» - â†’ ğŸ™+ h (Îº (- , v))) (q â»Â¹) âŸ©
       ğŸ™+ h (Îº (u , v))       âˆ
     Ï† u (inr r) = Ïˆ v (Zero-or-Succ (fe Uâ‚€ Uâ‚€) v)
      where
       Ïˆ : (v : â„•âˆ) â†’ (v â‰¡ Zero) + (v â‰¡ Succ(Pred v)) â†’ PRED (h (u , v)) â‰¡ ğŸ™+ h (Îº (u , v))
       Ïˆ v (inl z) =
         PRED (h (u , v))                   â‰¡âŸ¨ ap (Î» - â†’ PRED (h -)) (Ã—-â‰¡ r z)  âŸ©
         PRED (h (Succ (Pred u) , Zero))    â‰¡âŸ¨ ap PRED (eqâ‚ (Pred u)) âŸ©
         PRED Zero                          â‰¡âŸ¨ refl âŸ©
         ğŸ™+ h (inl *)                       â‰¡âŸ¨ refl âŸ©
         ğŸ™+ h (Îº (Succ (Pred u) , Zero))    â‰¡âŸ¨ ap (Î» - â†’ ğŸ™+ h (Îº -)) ((Ã—-â‰¡ r z)â»Â¹) âŸ©
         ğŸ™+ h (Îº (u , v))                   âˆ
       Ïˆ v (inr t) =
         PRED (h (u , v))                         â‰¡âŸ¨ ap (Î» - â†’ PRED (h -)) (Ã—-â‰¡ r t)  âŸ©
         PRED (h (Succ (Pred u) , Succ (Pred v))) â‰¡âŸ¨ ap PRED (eqâ‚‚ (Pred u) (Pred v)) âŸ©
         PRED (Succ (h (Pred u , Pred v)))        â‰¡âŸ¨ refl âŸ©
         inr (h (Pred u , Pred v))                â‰¡âŸ¨ refl âŸ©
         ğŸ™+ h (inr (Pred u , Pred v))             â‰¡âŸ¨ refl âŸ©
         ğŸ™+ h (Îº (Succ (Pred u) , Succ (Pred v))) â‰¡âŸ¨ ap (Î» - â†’ ğŸ™+ h (Îº -)) (Ã—-â‰¡ r t â»Â¹) âŸ©
         ğŸ™+ h (Îº (u , v))                         âˆ

\end{code}

To prove that min is commutative, we show that the following function
h is also a coalgebra homomorphism and hence equal to â„•âˆ-corec p:

\begin{code}

min-commutative : âˆ€ u v â†’ min (u , v) â‰¡ min (v , u)
min-commutative u v = h (v , u)          â‰¡âŸ¨ ap (Î» - â†’ - (v , u)) h-is-corec âŸ©
                      â„•âˆ-corec Îº (v , u) âˆ
 where
  h : â„•âˆ Ã— â„•âˆ â†’ â„•âˆ
  h (u , v) = min (v , u)
  h-homomorphism : is-homomorphism Îº h
  h-homomorphism = equations-characterize-homomorphisms h h-eqâ‚€ h-eqâ‚ h-eqâ‚‚
   where
    h-eqâ‚€ : (v : â„•âˆ) â†’ min (v , Zero) â‰¡ Zero
    h-eqâ‚€ v = min-eqâ‚ƒ v
    h-eqâ‚ : (u : â„•âˆ) â†’ min (Zero , Succ u) â‰¡ Zero
    h-eqâ‚ u = min-eqâ‚€ (Succ u)
    h-eqâ‚‚ : (u v : â„•âˆ) â†’ min (Succ v , Succ u) â‰¡ Succ (min (v , u))
    h-eqâ‚‚ u v = min-eqâ‚‚ v u
  h-is-corec : h â‰¡ â„•âˆ-corec Îº
  h-is-corec = homomorphism-uniqueness Îº h (â„•âˆ-corec Îº) h-homomorphism (â„•âˆ-corec-homomorphism Îº)

\end{code}

The following two facts invert the equations that characterize min:

\begin{code}

minâ‚€ : âˆ€ u v â†’ min (u , v) â‰¡ Zero â†’ (u â‰¡ Zero) + (v â‰¡ Zero)
minâ‚ : âˆ€ u v w â†’ min (u , v) â‰¡ Succ w â†’ (u â‰¡ Succ (Pred u))
                                      Ã— (v â‰¡ Succ (Pred v))
                                      Ã— (w â‰¡ min (Pred u , Pred v))

\end{code}

And here are their constructions:

\begin{code}

minâ‚€ u v r = h (Zero-or-Succ (fe Uâ‚€ Uâ‚€) u) (Zero-or-Succ (fe Uâ‚€ Uâ‚€) v)
 where
  h : (u â‰¡ Zero) + (u â‰¡ Succ(Pred u)) â†’ (v â‰¡ Zero) + (v â‰¡ Succ(Pred v))
    â†’ (u â‰¡ Zero) + (v â‰¡ Zero)
  h (inl uâ‚€) d = inl uâ‚€
  h (inr uâ‚) (inl vâ‚€) = inr vâ‚€
  h (inr uâ‚) (inr vâ‚) = ğŸ˜-elim (Zero-not-Succ (Zero        â‰¡âŸ¨ r â»Â¹ âŸ©
                                               min (u , v) â‰¡âŸ¨ s âŸ©
                                               Succ _      âˆ))
   where
    s : min (u , v) â‰¡ Succ (min (Pred u , Pred v))
    s = min (u , v)                          â‰¡âŸ¨ ap (Î» - â†’ min (- , v)) uâ‚ âŸ©
        min (Succ (Pred u) , v)              â‰¡âŸ¨ ap (Î» - â†’ min (Succ (Pred u) , -)) vâ‚ âŸ©
        min (Succ (Pred u) ,  Succ (Pred v)) â‰¡âŸ¨ min-eqâ‚‚ (Pred u) (Pred v) âŸ©
        Succ (min (Pred u , Pred v))         âˆ


minâ‚ u v w r = h (Zero-or-Succ (fe Uâ‚€ Uâ‚€) u) (Zero-or-Succ (fe Uâ‚€ Uâ‚€) v)
 where
  h : (u â‰¡ Zero) + (u â‰¡ Succ(Pred u)) â†’ (v â‰¡ Zero) + (v â‰¡ Succ(Pred v)) â†’ _
  h (inl uâ‚€) d =
    ğŸ˜-elim (Zero-not-Succ (Zero           â‰¡âŸ¨ (min-eqâ‚€ v)â»Â¹ âŸ©
                           min (Zero , v) â‰¡âŸ¨ ap (Î» - â†’ min (- , v)) (uâ‚€ â»Â¹) âŸ©
                           min (u , v)    â‰¡âŸ¨ r âŸ©
                           Succ w         âˆ))
  h (inr uâ‚) (inl vâ‚€) =
   ğŸ˜-elim (Zero-not-Succ (Zero           â‰¡âŸ¨ (min-eqâ‚ƒ u)â»Â¹ âŸ©
                          min (u , Zero) â‰¡âŸ¨ ap (Î» - â†’ min (u , -)) (vâ‚€ â»Â¹) âŸ©
                          min (u , v)    â‰¡âŸ¨ r âŸ©
                          Succ w         âˆ))
  h (inr uâ‚) (inr vâ‚) = uâ‚ , vâ‚ ,
   Succ-lc (Succ w                              â‰¡âŸ¨ r â»Â¹ âŸ©
            min (u , v)                         â‰¡âŸ¨ ap (Î» - â†’ min (- , v)) uâ‚  âŸ©
            min (Succ (Pred u) , v)             â‰¡âŸ¨ ap (Î» - â†’ min (Succ (Pred u) , -)) vâ‚ âŸ©
            min (Succ (Pred u) , Succ (Pred v)) â‰¡âŸ¨ min-eqâ‚‚ (Pred u) (Pred v) âŸ©
            Succ (min (Pred u , Pred v))        âˆ)

\end{code}
