Anna Williams, 17 October 2025

Definition of functor

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (_âˆ˜_ ; id)

open import Categories.Type

module Categories.Functor where

\end{code}

We define a functor from precategory A to precategory B as is usual.
This includes:
- Fobj, which is a map from objects of A to objects of B
- Fhom, which is a map from homomorphisms of A to homomorphisms of B

with the following structure
- Fhom (id A) = id (Fobj B)
- Fhom (g âˆ˜ f) = (Fhom g) âˆ˜ (Fhom f)

\begin{code}

record Functor (A : Precategory ğ“¤ ğ“¥) (B : Precategory ğ“¦ ğ“£)
 : (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¦ âŠ” ğ“£) Ì‡  where
 field
  Fobj : obj A â†’ obj B
  Fhom : {a b : obj A} â†’ hom {{A}} a b â†’ hom {{B}} (Fobj a) (Fobj b)
  id-pres : (a : obj A) â†’ Fhom (id {{A}} {a}) ï¼ id {{B}} {Fobj a}
  distrib : {a b c : obj A}
            (g : hom {{A}} b c)
            (f : hom {{A}} a b)
          â†’ Fhom (g âˆ˜[ A ] f) ï¼ (Fhom g) âˆ˜[ B ] (Fhom f)

open Functor {{...}} public

\end{code}

We now define functor composition in the expected way.

\begin{code}

_Fâˆ˜_ : {A : Precategory ğ“¤ ğ“¥}
       {B : Precategory ğ“¦ ğ“£}
       {C : Precategory ğ“¤' ğ“¥'}
       (G : Functor B C)
       (F : Functor A B)
     â†’ Functor A C
_Fâˆ˜_ {_} {_} {_} {_} {_} {_} {A} {B} {C} G F = record {
                                                Fobj = fobj ;
                                                Fhom = fhom ;
                                                id-pres = id-pres' ;
                                                distrib = distrib'
                                               }
 where
  fobj : obj A â†’ obj C
  fobj x = Fobj {{G}} (Fobj {{F}} x)

  fhom : {a b : obj A} â†’ hom {{A}} a b â†’ hom {{C}} (fobj a) (fobj b)
  fhom h = Fhom {{G}} (Fhom {{F}} h)

  id-pres' : (a : obj A) â†’ Fhom {{G}} (Fhom {{F}} (id {{A}})) ï¼ id {{C}}
  id-pres' a = Fhom {{G}} (Fhom {{F}} (id {{A}})) ï¼âŸ¨ i âŸ©
               Fhom {{G}} (id {{B}})              ï¼âŸ¨ ii âŸ©
               id {{C}}                           âˆ
   where
    i = ap (Fhom {{G}}) (id-pres {{F}} a)
    ii = id-pres {{G}} (Fobj {{F}} a)

  distrib' : {a b c : obj A}
             (g : hom {{A}} b c)
             (f : hom {{A}} a b)
           â†’ fhom (g âˆ˜[ A ] f) ï¼ (fhom g) âˆ˜[ C ] (fhom f)
  distrib' g f = fhom (g âˆ˜[ A ] f)                             ï¼âŸ¨ i  âŸ©
                 Fhom {{G}} (Fhom {{F}} g âˆ˜[ B ] Fhom {{F}} f) ï¼âŸ¨ ii âŸ©
                 (fhom g) âˆ˜[ C ] (fhom f)                      âˆ
   where
    i = ap (Fhom {{G}}) (distrib {{F}} g f)
    ii = distrib {{G}} (Fhom {{F}} g) (Fhom {{F}} f)

\end{code}
