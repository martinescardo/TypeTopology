Jon Sterling, started 16th Dec 2022

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module Categories.NaturalTransformation where

open import MLTT.Spartan
open import UF.FunExt
open import UF.Base
open import UF.Equiv
open import UF.Lower-FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Equiv-FunExt

open import Categories.Category
open import Categories.Functor

module natural-transformation (ğ“’ : precategory ğ“¤ ğ“¥) (ğ““ : precategory ğ“¤' ğ“¥') where
 private
  module ğ“’ = precategory ğ“’
  module ğ““ = precategory ğ““

 open functor-of-precategories ğ“’ ğ““

 module _ (F G : functor) where
  private
   module F = functor F
   module G = functor G

  transf : ğ“¤ âŠ” ğ“¥' Ì‡
  transf = (A : ğ“’.ob) â†’ ğ““.hom (F.ob A) (G.ob A)

  transf-is-set : (fe : funext ğ“¤ ğ“¥') â†’ is-set transf
  transf-is-set fe =
   Î -is-set fe Î» _ â†’
   ğ““.hom-is-set (F.ob _) (G.ob _)

  is-natural : transf â†’ ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥' Ì‡
  is-natural Î± =
   (A B : ğ“’.ob) (f : ğ“’.hom A B)
   â†’ ğ““.seq (F.hom f) (Î± B) ï¼ ğ““.seq (Î± A) (G.hom f)

  nat-transf : ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥' Ì‡
  nat-transf = Î£ Î± ê‰ transf , is-natural Î±

  module _ (fe0 : funext ğ“¤ (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥')) (fe1 : funext ğ“¥ ğ“¥')  where
    private
     fe2 : funext ğ“¤ (ğ“¥ âŠ” ğ“¥')
     fe2 = lower-funext ğ“¤ ğ“¤ fe0

     fe3 : funext ğ“¤ ğ“¥'
     fe3 = lower-funext ğ“¤ (ğ“¤ âŠ” ğ“¥) fe0

    being-natural-is-prop : {Î± : transf} â†’ is-prop (is-natural Î±)
    being-natural-is-prop =
     Î -is-prop fe0 Î» _ â†’
     Î -is-prop fe2 Î» _ â†’
     Î -is-prop fe1 Î» _ â†’
     ğ““.hom-is-set _ _

    nat-transf-is-set : is-set nat-transf
    nat-transf-is-set =
     Î£-is-set (transf-is-set fe3) Î» _ â†’
     props-are-sets being-natural-is-prop

    module _ {Î± Î² : nat-transf} where
     to-nat-transf-ï¼ : prâ‚ Î± ï¼ prâ‚ Î² â†’ Î± ï¼ Î²
     to-nat-transf-ï¼ h = to-Î£-ï¼ (h , being-natural-is-prop _ _)

  -- TODO : characterize identity type

 module _ (F : functor) where
  private module F = functor F
  transf-idn : transf F F
  transf-idn A = ğ““.idn (F.ob A)

  abstract
   transf-idn-natural : is-natural F F transf-idn
   transf-idn-natural A B f =
    ğ““.seq (F.hom f) (ğ““.idn _) ï¼âŸ¨ ğ““.idn-R _ _ _ âŸ©
    F.hom f ï¼âŸ¨ ğ““.idn-L _ _ _ â»Â¹ âŸ©
    ğ““.seq (ğ““.idn _) (F.hom f) âˆ

  nat-transf-idn : nat-transf F F
  nat-transf-idn = transf-idn , transf-idn-natural

 module _ (F G H : functor) where
  private
   module F = functor F
   module G = functor G
   module H = functor H

  module _ (Î± : transf F G) (Î² : transf G H) where
   transf-seq : transf F H
   transf-seq A = ğ““.seq (Î± A) (Î² A)

   module _ (Î±-nat : is-natural F G Î±) (Î²-nat : is-natural G H Î²) where
    abstract
     transf-seq-natural : is-natural F H transf-seq
     transf-seq-natural A B f =
      ğ““.seq (F.hom f) (ğ““.seq (Î± B) (Î² B))
       ï¼âŸ¨ ğ““.assoc _ _ _ _ _ _ _ âŸ©
      ğ““.seq (ğ““.seq (F.hom f) (Î± B)) (Î² B)
       ï¼âŸ¨ ap (Î» x â†’ ğ““.seq x (Î² B)) (Î±-nat _ _ _) âŸ©
      ğ““.seq (ğ““.seq (Î± A) (G.hom f)) (Î² B)
       ï¼âŸ¨ ğ““.assoc _ _ _ _ _ _ _ â»Â¹ âŸ©
      ğ““.seq (Î± A) (ğ““.seq (G.hom f) (Î² B))
       ï¼âŸ¨ ap (ğ““.seq (Î± A)) (Î²-nat _ _ _) âŸ©
      ğ““.seq (Î± A) (ğ““.seq (Î² A) (H.hom f))
       ï¼âŸ¨ ğ““.assoc _ _ _ _ _ _ _ âŸ©
      ğ““.seq (ğ““.seq (Î± A) (Î² A)) (H.hom f) âˆ

  nat-transf-seq : nat-transf F G  â†’ nat-transf G H â†’ nat-transf F H
  prâ‚ (nat-transf-seq Î± Î²) = transf-seq (prâ‚ Î±) (prâ‚ Î²)
  prâ‚‚ (nat-transf-seq Î± Î²) = transf-seq-natural (prâ‚ Î±) (prâ‚ Î²) (prâ‚‚ Î±) (prâ‚‚ Î²)

 module _ (F G : functor) (Î± : transf F G) (fe : funext ğ“¤ ğ“¥') where
  transf-idn-L : transf-seq F F G (transf-idn F) Î± ï¼ Î±
  transf-idn-L =
   dfunext fe Î» _ â†’
   ğ““.idn-L _ _ _

  transf-idn-R : transf-seq F G G Î± (transf-idn G) ï¼ Î±
  transf-idn-R =
   dfunext fe Î» _ â†’
   ğ““.idn-R _ _ _

 module _
  (F G H I : functor)
  (Î± : transf F G)
  (Î² : transf G H)
  (Î³ : transf H I)
  (fe : funext ğ“¤ ğ“¥')
  where
  transf-assoc
   : transf-seq F G I Î± (transf-seq G H I Î² Î³)
   ï¼ transf-seq F H I (transf-seq F G H Î± Î²) Î³
  transf-assoc =
   dfunext fe Î» _ â†’
   ğ““.assoc _ _ _ _ _ _ _

 module nat-transf-laws (fe0 : funext ğ“¤ (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥')) (fe1 : funext ğ“¥ ğ“¥') where
  private
   fe2 : funext ğ“¤ ğ“¥'
   fe2 = lower-funext ğ“¤ (ğ“¤ âŠ” ğ“¥) fe0

  module _ (F G : functor) (Î± : nat-transf F G) where
   nat-transf-idn-L : nat-transf-seq F F G (nat-transf-idn F) Î± ï¼ Î±
   nat-transf-idn-L =
    to-nat-transf-ï¼ F G fe0 fe1
     (transf-idn-L F G (prâ‚ Î±) fe2)

   nat-transf-idn-R : nat-transf-seq F G G Î± (nat-transf-idn G) ï¼ Î±
   nat-transf-idn-R =
    to-nat-transf-ï¼ F G fe0 fe1
     (transf-idn-R F G (prâ‚ Î±) fe2)

  module _ (F G H I : functor) (Î± : nat-transf F G) (Î² : nat-transf G H) (Î³ : nat-transf H I) where
   nat-transf-assoc
    : nat-transf-seq F G I Î± (nat-transf-seq G H I Î² Î³)
    ï¼ nat-transf-seq F H I (nat-transf-seq F G H Î± Î²) Î³
   nat-transf-assoc =
    to-nat-transf-ï¼ F I fe0 fe1
     (transf-assoc F G H I (prâ‚ Î±) (prâ‚ Î²) (prâ‚ Î³) fe2)

 module functor-category where
  structure : category-structure (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¤' âŠ” ğ“¥') (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥')
  structure = functor , nat-transf , nat-transf-idn , nat-transf-seq

  module _ (fe0 : funext ğ“¤ (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥')) (fe1 : funext ğ“¥ ğ“¥') where
   axioms : precategory-axioms structure
   axioms =
    let open nat-transf-laws fe0 fe1 in
    (Î» F G â†’ nat-transf-is-set F G fe0 fe1) ,
    nat-transf-idn-L ,
    nat-transf-idn-R ,
    nat-transf-assoc

   precat : precategory (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¤' âŠ” ğ“¥') (ğ“¤ âŠ” ğ“¥ âŠ” ğ“¥')
   precat = structure , axioms

\end{code}
