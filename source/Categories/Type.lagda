Anna Williams, 17 October 2025

Definitions of:
 * wild category
 * pre category
 * category

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (_âˆ˜_ ; id)
open import Notation.UnderlyingType
open import UF.Base
open import UF.Equiv hiding (_â‰…_ ; _â‰…âŸ¨_âŸ©_)
open import UF.Equiv-FunExt
open import UF.FunExt
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties

module Categories.Type where

\end{code}

We start by defining the notion of a wild category.
This consists of the usual components of a category,
which is as follows:

- A collection of objects, obj
- For each pair of objects, A B : obj, a type of homorphism between A and B
- For each object A : obj, an identity homorphism (id A) : hom A A
- A composition operation, âˆ˜, which for objects A B C : obj
  and homorphisms f : hom A B, g : hom B C gives a new homomorphism
  g âˆ˜ f : hom A C

with the following axioms

- left-id: For objects A B : obj and morphism f : hom A B, f âˆ˜ (id A) ï¼ f
- right-id: For objects A B : obj and morphism f : hom A B, (id B) âˆ˜ f ï¼ f
- associativity: For objects A B C D : obj and morphisms f : hom A B,
                 g : hom B C, h : hom C D, we have h âˆ˜ (g âˆ˜ f) ï¼ (h âˆ˜ g) âˆ˜ f

\begin{code}

record WildCategory (ğ“¤ ğ“¥ : Universe) : (ğ“¤ âŠ” ğ“¥)âº Ì‡  where
 constructor wildcat-make
 field
  obj : ğ“¤ Ì‡
  hom : obj â†’ obj â†’ ğ“¥Â Ì‡
  id : {a : obj} â†’ hom a a
  
  _âˆ˜_ : {a b c : obj} â†’ hom b c â†’ hom a b â†’ hom a c
  
  left-id : {a b : obj} â†’ (f : hom a b) â†’ id âˆ˜ f ï¼ f
  
  right-id : {a b : obj} â†’ (f : hom a b) â†’ f âˆ˜ id ï¼ f
  
  assoc : {a b c d : obj}
          (f : hom a b)
          (g : hom b c)
          (h : hom c d)
        â†’ h âˆ˜ (g âˆ˜ f) ï¼ (h âˆ˜ g) âˆ˜ f

\end{code}

We add instance argument versions of each field, apart from
obj, which we make explicit. We also add a syntax definition
for composition where the precategory cannot be inferred.

\begin{code}

open WildCategory {{...}} public hiding (obj)

obj : (W : WildCategory ğ“¤ ğ“¥) â†’ ğ“¤ Ì‡
obj = WildCategory.obj

wildcat-comp-explicit : (W : WildCategory ğ“¤ ğ“¥)
          {a b c : obj W}
          â†’ hom {{W}} b c
          â†’ hom {{W}} a b
          â†’ hom {{W}} a c
wildcat-comp-explicit W g f = _âˆ˜_{{W}} g f

syntax wildcat-comp-explicit P g f = g âˆ˜âŸ¨ P âŸ© f

infixr 5 wildcat-comp-explicit

wildcat-comp : {{W : WildCategory ğ“¤ ğ“¥}}
               {a b c : obj W}
             â†’ hom b c
             â†’ hom a b
             â†’ hom a c
wildcat-comp g f = g âˆ˜ f

infixr 5 wildcat-comp

\end{code}

An isomorphism in a category consists of a homomorphism f : hom a b
and some "inverse" homomorphism g : hom b a, such that g âˆ˜ f = (id a)
and f âˆ˜ g ï¼ (id b).

We first define the property of being an isomorphism and then define
the type of isomorphisms between objects of a wild category.

\begin{code}

is-iso : {{ W : WildCategory ğ“¤ ğ“¥ }} {a b : obj W} (f : hom a b) â†’ ğ“¥ Ì‡ 
is-iso {_} {_} {a} {b} f = Î£ inv ê‰ hom b a , (inv âˆ˜ f ï¼ id) Ã— (f âˆ˜ inv ï¼ id)

inv : {{ W : WildCategory ğ“¤ ğ“¥ }}
      {a b : obj W}
      {f : hom a b}
    â†’ is-iso f
    â†’ hom b a
inv iso = prâ‚ iso

l-inv : {{ W : WildCategory ğ“¤ ğ“¥ }}
            {a b : obj W}
            {f : hom {{W}} a b}
            (iso : is-iso f)
          â†’ inv iso âˆ˜ f ï¼ id 
l-inv iso = prâ‚ (prâ‚‚ iso)

r-inv : {{ W : WildCategory ğ“¤ ğ“¥ }}
            {a b : obj W}
            {f : hom a b}
            (iso : is-iso f)
          â†’ f âˆ˜ inv iso ï¼ id
r-inv iso = prâ‚‚ (prâ‚‚ iso)

_â‰…_ : {{ W : WildCategory ğ“¤ ğ“¥ }} (a b : obj W) â†’ ğ“¥ Ì‡
a â‰… b = Î£ f ê‰ hom a b , is-iso f

wildcat-iso-explicit : (W : WildCategory ğ“¤ ğ“¥)
              (a b : obj W)
            â†’ ğ“¥ Ì‡
wildcat-iso-explicit W a b = _â‰…_ {{W}} a b

syntax wildcat-iso-explicit W a b = a â‰…âŸ¨ W âŸ© b

\end{code}

We can now define the notion of a precategory. This is a wild category
where the type homomorphisms between two objects is a set. This can be
shown to be a proposition.

\begin{code}

is-precategory : (W : WildCategory ğ“¤ ğ“¥) â†’ (ğ“¤ âŠ” ğ“¥) Ì‡
is-precategory W = (a b : obj W) â†’ is-set (hom {{W}} a b)

being-precat-is-prop : (fe : Fun-Ext)
                       (W : WildCategory ğ“¤ ğ“¥)
                     â†’ is-prop (is-precategory W)
being-precat-is-prop fe W p q = Î -is-prop fe
                                 (Î» a â†’ Î -is-prop fe
                                  (Î» b â†’ being-set-is-prop fe)) _ _

PreCategory : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
PreCategory ğ“¤ ğ“¥ = Î£ W ê‰ WildCategory ğ“¤ ğ“¥ , is-precategory W

\end{code}

We also define the corresponding projections from a precategory.

\begin{code}

instance
  underlying-wildcategory-of-precategory
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (PreCategory ğ“¤ ğ“¥) (WildCategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-wildcategory-of-precategory}} (P , _) = P

hom-is-set : {{P : PreCategory ğ“¤ ğ“¥}}
             {a b : obj âŸ¨ P âŸ©}
           â†’ is-set (hom {{âŸ¨ P âŸ©}} a b)
hom-is-set {{_ , p}} {a} {b} = p a b

\end{code}

We now show that in a precategory, for any given homomorphism, being an
isomorphism is a (mere) proposition. We argue that inverses are unique,
and then since the type of homomorphisms between two objects is a set,
equality between any two homomorphisms is a proposition, so our left and
right inverse equalities are a proposition.

\begin{code}

inv-is-lc : {{P : PreCategory ğ“¤ ğ“¥}}
            {a b : obj âŸ¨ P âŸ©}
            {f : hom {{âŸ¨ P âŸ©}} a b}
            (x y : is-iso {{âŸ¨ P âŸ©}} f)
          â†’ inv {{âŸ¨ P âŸ©}} x ï¼ inv {{âŸ¨ P âŸ©}} y
          â†’ x ï¼ y
inv-is-lc {{P}} x y refl = apâ‚‚ (Î» l r â†’ inv {{âŸ¨ P âŸ©}} x , l , r) l-eq r-eq
 where
  l-eq : l-inv {{âŸ¨ P âŸ©}} x ï¼ l-inv {{âŸ¨ P âŸ©}} y
  l-eq = hom-is-set (l-inv {{âŸ¨ P âŸ©}} x) (l-inv {{âŸ¨ P âŸ©}} y)

  r-eq : r-inv {{âŸ¨ P âŸ©}} x ï¼ r-inv {{âŸ¨ P âŸ©}} y
  r-eq = hom-is-set (r-inv {{âŸ¨ P âŸ©}} x) (r-inv {{âŸ¨ P âŸ©}} y)

being-iso-is-prop : {{P : PreCategory ğ“¤ ğ“¥}}
                    {a b : obj âŸ¨ P âŸ©}
                    (f : hom {{âŸ¨ P âŸ©}} a b)
                  â†’ is-prop (is-iso {{âŸ¨ P âŸ©}} f)
being-iso-is-prop {{P}} {a} {b} f x y = inv-is-lc x y (inverse-eq {{âŸ¨ P âŸ©}} x y)
 where  
  inverse-eq : {{W : WildCategory _ _}}
               {a b : obj W}
               {f : hom a b}
               (x y : is-iso f)
             â†’ inv x ï¼ inv y
  inverse-eq {{W}} {a} {b} {f} x y = inv x               ï¼âŸ¨ i âŸ©
                                     inv x âˆ˜ id          ï¼âŸ¨ ii âŸ©
                                     inv x âˆ˜ (f âˆ˜ inv y) ï¼âŸ¨ iii âŸ©
                                     (inv x âˆ˜ f) âˆ˜ inv y ï¼âŸ¨ iv âŸ©
                                     id âˆ˜ inv y          ï¼âŸ¨ v âŸ©
                                     inv y               âˆ
   where
    i   = (right-id (inv x))â»Â¹
    ii  = ap (Î» - â†’ inv x âˆ˜ -) (r-inv y)â»Â¹
    iii = assoc _ _ _
    iv  = ap (Î» - â†’ - âˆ˜ inv y) (l-inv x)
    v   = left-id (inv y)

\end{code}

Following this, we can see that the type of isomorphisms is a set.

\begin{code}

isomorphism-type-is-set : {{P : PreCategory ğ“¤ ğ“¥}}
                          {a b : obj âŸ¨ P âŸ©}
                        â†’ is-set (a â‰…âŸ¨ âŸ¨ P âŸ© âŸ© b)
isomorphism-type-is-set {{P}} = Î£-is-set hom-is-set
                                 (Î» f â†’ props-are-sets (being-iso-is-prop f))

\end{code}

We wish to combine the similar notions of equivalence,
namely the internal equality: a ï¼ b and isomorphisms a â‰… b.

We can in fact show that if a ï¼ b, then a â‰… b. This is because if
a ï¼ b, then by path induction we need to show that a â‰… a. This can
easily be constructed as follows. This map is typically called id-to-iso

\begin{code}

id-to-iso : {{ W : WildCategory ğ“¤ ğ“¥ }}
            (a b : obj W )
          â†’ a ï¼ b
          â†’ a â‰…âŸ¨ W âŸ© b
id-to-iso a b refl = id , (id , id-comp-id-is-id , id-comp-id-is-id)
 where
  id-comp-id-is-id : id âˆ˜ id ï¼ id
  id-comp-id-is-id = left-id id
\end{code}

To bring into alignment the two different forms of equality, we define a
category to be a precategory where identification is equivalent to isomorphism.
That is the above map is an equivalence.

\begin{code}

is-category : (P : PreCategory ğ“¤ ğ“¥) â†’ (ğ“¤ âŠ” ğ“¥) Ì‡ 
is-category P = (a b : obj âŸ¨ P âŸ©) â†’ is-equiv (id-to-iso {{âŸ¨ P âŸ©}} a b)

being-cat-is-prop : (fe : Fun-Ext)
                    (P : PreCategory ğ“¤ ğ“¥)
                  â†’ is-prop (is-category P)
being-cat-is-prop fe P x y = Î â‚‚-is-prop fe I _ _
 where
  I : (a b : obj âŸ¨ P âŸ©) â†’ is-prop (is-equiv (id-to-iso {{âŸ¨ P âŸ©}} a b))
  I a b e e' = being-equiv-is-prop (Î» x y â†’ fe {x} {y})
                                      (id-to-iso {{âŸ¨ P âŸ©}} a b) e e'

Category : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥ )âº Ì‡
Category ğ“¤ ğ“¥ = Î£ P ê‰ PreCategory ğ“¤ ğ“¥ , is-category P

\end{code}

Projections from category.

\begin{code}

instance
  underlying-precategory-of-category
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (Category ğ“¤ ğ“¥) (PreCategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-precategory-of-category}} (P , _) = P

  underlying-wildcategory-of-category
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (Category ğ“¤ ğ“¥) (WildCategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-wildcategory-of-category}} ((W , _) , _) = W


id-to-iso-is-equiv : (C : Category ğ“¤ ğ“¥)
                   â†’ is-category âŸ¨ C âŸ©
id-to-iso-is-equiv C = prâ‚‚ C

\end{code}

We can now show that the objects of any category are 1-types. This is because
equality between objects is given exactly by isomorphism, which we have shown
forms a set.

\begin{code}

cat-objs-are-1-types : (A : Category ğ“¤ ğ“¥) â†’ (a b : obj âŸ¨ A âŸ©) â†’ is-set (a ï¼ b)
cat-objs-are-1-types A a b = equiv-to-set id-equiv-iso
                                          (isomorphism-type-is-set {{âŸ¨ A âŸ©}})
 where
  id-equiv-iso : (a ï¼ b) â‰ƒ (a â‰…âŸ¨ âŸ¨ A âŸ© âŸ© b)
  id-equiv-iso = id-to-iso {{âŸ¨ A âŸ©}} a b , id-to-iso-is-equiv A a b

\end{code}
