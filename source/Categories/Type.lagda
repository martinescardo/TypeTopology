Anna Williams, 17 October 2025

Definitions of:
 * precategory
 * category

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan hiding (_âˆ˜_ ; id)
open import Notation.UnderlyingType
open import UF.Base
open import UF.Embeddings
open import UF.Equiv hiding (_â‰…_ ; _â‰…âŸ¨_âŸ©_)
open import UF.Equiv-FunExt
open import UF.FunExt
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties

module Categories.Type (fe : Fun-Ext) where

\end{code}

We start by defining the notion of a wild category.
This consists of the usual components of a (set theoretic) category,
which is as follows:

- A collection of objects, obj
- For each pair of objects, A B : obj, a type of homorphism between A and B
- For each object A : obj, an identity homorphism (id A) : hom A A
- A composition operation, âˆ˜, which for objects A B C : obj
  and homorphisms f : hom A B, g : hom B C gives a new homomorphism
  g âˆ˜ f : hom A C

with the following axioms

- left-id: For objects A B : obj and morphism f : hom A B, f âˆ˜ (id A) ï¼ f
- right-id: For objects A B : obj and morphism f : hom A B, (id B) âˆ˜ f ï¼ f
- associativity: For objects A B C D : obj and morphisms f : hom A B,
                 g : hom B C, h : hom C D, we have h âˆ˜ (g âˆ˜ f) ï¼ (h âˆ˜ g) âˆ˜ f

\begin{code}

record WildCategory (ğ“¤ ğ“¥ : Universe) : (ğ“¤ âŠ” ğ“¥)âº Ì‡  where
 constructor make
 field
  obj : ğ“¤ Ì‡
  hom : obj â†’ obj â†’ ğ“¥Â Ì‡
  id : {a : obj} â†’ hom a a
  
  _âˆ˜_ : {a b c : obj} â†’ hom b c â†’ hom a b â†’ hom a c
  
  left-id : {a b : obj} â†’ (f : hom a b) â†’ id âˆ˜ f ï¼ f
  
  right-id : {a b : obj} â†’ (f : hom a b) â†’ f âˆ˜ id ï¼ f
  
  assoc : {a b c d : obj}
          {f : hom a b}
          {g : hom b c}
          {h : hom c d}
        â†’ h âˆ˜ (g âˆ˜ f) ï¼ (h âˆ˜ g) âˆ˜ f

\end{code}

We add instance argument versions of each field, apart from
obj, which we make explicit. We also add a syntax definition
for composition where the precategory cannot be inferred.

\begin{code}

open WildCategory {{...}} public hiding (obj)

obj : (W : WildCategory ğ“¤ ğ“¥) â†’ ğ“¤ Ì‡
obj = WildCategory.obj

wildcat-comp : (W : WildCategory ğ“¤ ğ“¥)
          {a b c : obj W}
          â†’ hom {{W}} b c
          â†’ hom {{W}} a b
          â†’ hom {{W}} a c
wildcat-comp W g f = _âˆ˜_{{W}} g f

syntax wildcat-comp P g f = g âˆ˜âŸ¨ P âŸ© f

infixl 5 wildcat-comp

\end{code}

An isomorphism in a category consists of a homomorphism f : hom a b
and some "inverse" homomorphism g : hom b a, such that g âˆ˜ f = (id a)
and f âˆ˜ g ï¼ (id b).

We first define the type of a given homomorphism being an isomorphism,
then we define the type of isomorphism between objects of a wild category.

\begin{code}

is-iso : {{ W : WildCategory ğ“¤ ğ“¥ }} {a b : obj W} (f : hom a b) â†’ ğ“¥ Ì‡ 
is-iso {_} {_} {a} {b} f = Î£ inv ê‰ hom b a , (inv âˆ˜ f ï¼ id) Ã— (f âˆ˜ inv ï¼ id)

inv : {{ W : WildCategory ğ“¤ ğ“¥ }}
      {a b : obj W}
      {f : hom a b}
    â†’ is-iso f
    â†’ hom b a
inv iso = prâ‚ iso

l-inverse : {{ W : WildCategory ğ“¤ ğ“¥ }}
            {a b : obj W}
            {f : hom {{W}} a b}
            (iso : is-iso f)
          â†’ (inv iso) âˆ˜ f ï¼ id 
l-inverse iso = prâ‚ (prâ‚‚ iso)

r-inverse : {{ W : WildCategory ğ“¤ ğ“¥ }}
            {a b : obj W}
            {f : hom a b}
            (iso : is-iso f)
          â†’ f âˆ˜ (inv iso) ï¼ id
r-inverse iso = prâ‚‚ (prâ‚‚ iso)

is-inverse : {{ W : WildCategory ğ“¤ ğ“¥ }}
            {a b : obj W}
            {f : hom a b}
            (iso : is-iso f)
          â†’ ((inv iso) âˆ˜ f ï¼ id) Ã— (f âˆ˜ (inv iso) ï¼ id)
is-inverse = prâ‚‚

mk-iso : {{ W : WildCategory ğ“¤ ğ“¥ }}
         {a b : obj W}
         {f : hom a b}
         (inv : hom b a)
       â†’ (inv âˆ˜ f ï¼ id)
       â†’ (f âˆ˜ inv ï¼ id)
       â†’ is-iso f
mk-iso inv l-id r-id = (inv , l-id , r-id)

_â‰…_ : {{ W : WildCategory ğ“¤ ğ“¥ }} (a b : obj W) â†’ ğ“¥ Ì‡
a â‰… b = Î£ f ê‰ hom a b , is-iso f

wildcat-iso : (W : WildCategory ğ“¤ ğ“¥)
              (a b : obj W)
            â†’ ğ“¥ Ì‡
wildcat-iso W a b = _â‰…_ {{W}} a b

syntax wildcat-iso W a b = a â‰…âŸ¨ W âŸ© b

\end{code}

We now define the notion of a precategory

\begin{code}

is-precategory : (W : WildCategory ğ“¤ ğ“¥) â†’ (ğ“¤ âŠ” ğ“¥) Ì‡
is-precategory W = (a b : obj W) â†’ is-set (hom {{W}} a b)

being-precategory-is-prop : (W : WildCategory ğ“¤ ğ“¥) â†’ is-prop (is-precategory W)
being-precategory-is-prop W p q = Î -is-prop fe
                                   (Î» a â†’ Î -is-prop fe
                                    (Î» b â†’ being-set-is-prop fe)) _ _

Precategory : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥)âº Ì‡
Precategory ğ“¤ ğ“¥ = Î£ W ê‰ WildCategory ğ“¤ ğ“¥ , is-precategory W

\end{code}

We also define the corresponding projections from a precategory.

\begin{code}

instance
  underlying-wildcategory-of-precategory
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (Precategory ğ“¤ ğ“¥) (WildCategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-wildcategory-of-precategory}} (P , _) = P

hom-is-set : {{P : Precategory ğ“¤ ğ“¥}}
             {a b : obj âŸ¨ P âŸ©}
           â†’ is-set (hom {{âŸ¨ P âŸ©}} a b)
hom-is-set {{_ , p}} {a} {b} = p a b

\end{code}

We now show that for a given homomorphism, being an isomorphism is a
(mere) proposition. We argue that inverses are unique, and then since
the type of homomorphisms between two objects is a set, equality between
any two homomorphisms is a proposition, so our left and right inverse
equalities are a proposition.

\begin{code}

inv-is-lc : {{P : Precategory ğ“¤ ğ“¥}}
            {a b : obj âŸ¨ P âŸ©}
            {f : hom {{âŸ¨ P âŸ©}} a b}
            (x y : is-iso {{âŸ¨ P âŸ©}} f)
          â†’ inv {{âŸ¨ P âŸ©}} x ï¼ inv {{âŸ¨ P âŸ©}} y
          â†’ x ï¼ y
inv-is-lc {{P}} x y refl = apâ‚‚ (mk-iso {{âŸ¨ P âŸ©}} (inv {{âŸ¨ P âŸ©}} x)) l-eq r-eq
 where
  l-eq : l-inverse {{âŸ¨ P âŸ©}} x ï¼ l-inverse {{âŸ¨ P âŸ©}} y
  l-eq = hom-is-set (l-inverse {{âŸ¨ P âŸ©}} x) (l-inverse {{âŸ¨ P âŸ©}} y)

  r-eq : r-inverse {{âŸ¨ P âŸ©}} x ï¼ r-inverse {{âŸ¨ P âŸ©}} y
  r-eq = hom-is-set (r-inverse {{âŸ¨ P âŸ©}} x) (r-inverse {{âŸ¨ P âŸ©}} y)

being-iso-is-prop : {{P : Precategory ğ“¤ ğ“¥}}
                    {a b : obj âŸ¨ P âŸ©}
                    (f : hom {{âŸ¨ P âŸ©}} a b)
                  â†’ is-prop (is-iso {{âŸ¨ P âŸ©}} f)
being-iso-is-prop {{P}} {a} {b} f x y = inv-is-lc x y inverse-eq
 where  
  inverse-eq : inv {{âŸ¨ P âŸ©}} x ï¼ inv {{âŸ¨ P âŸ©}} y
  inverse-eq = xâ»Â¹                               ï¼âŸ¨ i âŸ©
               xâ»Â¹ âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© (id {{âŸ¨ P âŸ©}})     ï¼âŸ¨ ii âŸ©
               xâ»Â¹ âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© (f âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© yâ»Â¹) ï¼âŸ¨ iii âŸ©
               (xâ»Â¹ âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© f) âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© yâ»Â¹ ï¼âŸ¨ iv âŸ©
               (id {{âŸ¨ P âŸ©}}) âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© yâ»Â¹     ï¼âŸ¨ v âŸ©
               yâ»Â¹ âˆ
   where
    xâ»Â¹ = inv {{âŸ¨ P âŸ©}} x
    yâ»Â¹ = inv {{âŸ¨ P âŸ©}} y

    i = (right-id {{âŸ¨ P âŸ©}} xâ»Â¹)â»Â¹
    ii = ap (Î» y â†’ xâ»Â¹ âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© y) ((r-inverse {{âŸ¨ P âŸ©}} y)â»Â¹)
    iii = assoc {{âŸ¨ P âŸ©}}
    iv = ap (Î» x â†’ x âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© yâ»Â¹) (l-inverse {{âŸ¨ P âŸ©}} x)
    v = left-id {{âŸ¨ P âŸ©}} yâ»Â¹

\end{code}

We now argue that this means that the type of isomorphisms is a set.
This follows from the fact that being an isomorphism is a proposition.

\begin{code}

isomorphism-type-is-set : {{P : Precategory ğ“¤ ğ“¥}}
                          {a b : obj âŸ¨ P âŸ©}
                        â†’ is-set (a â‰…âŸ¨ âŸ¨ P âŸ© âŸ© b)
isomorphism-type-is-set {{P}} = Î£-is-set hom-is-set
                                 (Î» f â†’ props-are-sets (being-iso-is-prop f))

\end{code}

We wish to combine the similar notions of equivalence,
namely the internal equality: a ï¼ b and isomorphisms a â‰… b.

We can in fact show that if a ï¼ b, then a â‰… b. This is because if
a ï¼ b, then by path induction we need to show that a â‰… a. This is
simple as we can form an isomophism with the identity homomorphism.

\begin{code}

id-to-iso : {{ P : Precategory ğ“¤ ğ“¥ }}
            (a b : obj âŸ¨ P âŸ© )
          â†’ a ï¼ b
          â†’ a â‰…âŸ¨ âŸ¨ P âŸ© âŸ© b
id-to-iso {{P}} a b refl = id {{âŸ¨ P âŸ©}} , iso
 where
  iso : is-iso {{âŸ¨ P âŸ©}} (id {{âŸ¨ P âŸ©}})
  iso = (mk-iso {{âŸ¨ P âŸ©}} (id {{âŸ¨ P âŸ©}}) id-comp-id-is-id id-comp-id-is-id)
   where
    id-comp-id-is-id : id {{âŸ¨ P âŸ©}} âˆ˜âŸ¨ âŸ¨ P âŸ© âŸ© id {{âŸ¨ P âŸ©}} ï¼ id {{âŸ¨ P âŸ©}}
    id-comp-id-is-id = left-id {{âŸ¨ P âŸ©}} (id {{âŸ¨ P âŸ©}})
\end{code}

To bring into alignment the two different forms of equality, we define a
category to be a precategory where identification is equivalent to isomorphism.

\begin{code}

is-category : (P : Precategory ğ“¤ ğ“¥) â†’ (ğ“¤ âŠ” ğ“¥) Ì‡ 
is-category P = (a b : obj âŸ¨ P âŸ©) â†’ is-equiv (id-to-iso {{P}} a b)

being-category-is-prop : (P : Precategory ğ“¤ ğ“¥) â†’ is-prop (is-category P)
being-category-is-prop P x y = Î -is-prop fe (Î» x â†’ Î -is-prop fe (I x)) _ _
 where
  I : (a b : obj âŸ¨ P âŸ©) â†’ is-prop (is-equiv (id-to-iso {{P}} a b))
  I a b e e' = being-equiv-is-prop (Î» x y â†’ fe {x} {y})
                                    (id-to-iso {{P}} a b) e e'

Category : (ğ“¤ ğ“¥ : Universe) â†’ (ğ“¤ âŠ” ğ“¥ )âº Ì‡
Category ğ“¤ ğ“¥ = Î£ P ê‰ Precategory ğ“¤ ğ“¥ , is-category P

\end{code}

Projections from category.

\begin{code}

instance
  underlying-precategory-of-category
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (Category ğ“¤ ğ“¥) (Precategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-precategory-of-category}} (P , _) = P

  underlying-wildcategory-of-category
   : {ğ“¤ ğ“¥ : Universe}
   â†’ Underlying-Type (Category ğ“¤ ğ“¥) (WildCategory ğ“¤ ğ“¥)
  âŸ¨_âŸ© {{underlying-wildcategory-of-category}} ((W , _) , _) = W


id-to-iso-is-equiv : (C : Category ğ“¤ ğ“¥)
                   â†’ is-category âŸ¨ C âŸ©
id-to-iso-is-equiv C = prâ‚‚ C

\end{code}

We can now show that the objects of any category is a 1-type. This is because
equality between objects is exactly isomorphism, which we know is a set.

\begin{code}

cat-objs-are-1-types : (A : Category ğ“¤ ğ“¥) â†’ (a b : obj âŸ¨ A âŸ©) â†’ is-set (a ï¼ b)
cat-objs-are-1-types A a b = equiv-to-set id-equiv-iso
                                          (isomorphism-type-is-set {{âŸ¨ A âŸ©}})
 where
  id-equiv-iso : (a ï¼ b) â‰ƒ (a â‰…âŸ¨ âŸ¨ A âŸ© âŸ© b)
  id-equiv-iso = id-to-iso {{âŸ¨ A âŸ©}} a b , id-to-iso-is-equiv A a b

\end{code}
