Fredrik Bakke, April 2025

Perfect images
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

\begin{code}

{-# OPTIONS --safe --without-K #-}

module CantorSchroederBernstein.PerfectImages where

open import MLTT.Spartan
open import TypeTopology.CompactTypes
open import TypeTopology.DenseMapsProperties
open import UF.Equiv
open import UF.EquivalenceExamples
open import NotionsOfDecidability.Complemented
open import UF.NotNotStablePropositions
open import UF.Subsingletons

\end{code}

We introduce the concept of perfect images as used by KÃ¶nig in his argument
(1906) for the Cantorâ€“SchrÃ¶derâ€“Bernstein theorem, and study logical properties
of it. In CantorSchroederBernstein.CSB-WLPO we use these properties to give a
generalization of the Cantorâ€“SchrÃ¶derâ€“Bernstein theorem assuming the weak
limited principle of omniscience.

Definition.
Given maps f : X â†’ Y and g : Y â†’ X, then an element x : X is said to be a
"perfect image" of g relative to f, if for every natural number n and every
preimage xâ‚€ of x under (g âˆ˜ f)â¿, i.e., (g âˆ˜ f)â¿ xâ‚€ = x, then xâ‚€ has a further
preimage under g.

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) (g : Y â†’ X) where

 is-perfect-image-at : X â†’ â„• â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-perfect-image-at x n =
   ((xâ‚€ , _) : fiber ((g âˆ˜ f) ^ n) x) â†’ fiber g xâ‚€

 is-perfect-image : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-perfect-image x = (n : â„•) â†’ is-perfect-image-at x n

\end{code}

The map g has a section on its perfect image.

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 has-section-on-perfect-image : (x : X) â†’ is-perfect-image f g x â†’ fiber g x
 has-section-on-perfect-image x Ï = Ï 0 (x , refl)

 inverse-on-perfect-image : (x : X) â†’ is-perfect-image f g x â†’ Y
 inverse-on-perfect-image x Ï = prâ‚ (has-section-on-perfect-image x Ï)

 inverse-on-perfect-image-is-section : (x : X) (Ï : is-perfect-image f g x)
                                     â†’ g (inverse-on-perfect-image x Ï) ï¼ x
 inverse-on-perfect-image-is-section x Ï = prâ‚‚ (has-section-on-perfect-image x Ï)

 inverse-on-perfect-image-is-retraction : left-cancellable g
                                        â†’ (y : Y)
                                        â†’ (Ï : is-perfect-image f g (g y))
                                        â†’ inverse-on-perfect-image (g y) Ï ï¼ y
 inverse-on-perfect-image-is-retraction lc-g y Ï =
  lc-g (inverse-on-perfect-image-is-section (g y) Ï)

\end{code}

If g (f x) is a perfect image then so is x.

\begin{code}

 previous-perfect-image-at : (x : X) (n : â„•)
                           â†’ is-perfect-image-at f g (g (f x)) (succ n)
                           â†’ is-perfect-image-at f g x n
 previous-perfect-image-at x n Ï (xâ‚€ , p) = Ï (xâ‚€ , ap (g âˆ˜ f) p)

 previous-perfect-image : (x : X)
                        â†’ is-perfect-image f g (g (f x))
                        â†’ is-perfect-image f g x
 previous-perfect-image x Ï n = previous-perfect-image-at x n (Ï (succ n))

\end{code}

The perfect image of g relative to f is disjoint from the image of f.

\begin{code}

 perfect-image-is-disjoint : (x xâ‚€ : X)
                           â†’ Â¬ is-perfect-image f g x
                           â†’ (Ï : is-perfect-image f g xâ‚€)
                           â†’ f x â‰  inverse-on-perfect-image xâ‚€ Ï
 perfect-image-is-disjoint x xâ‚€ nÏ Ï p = v Ï
  where
   q : g (f x) ï¼ xâ‚€
   q = ap g p âˆ™ inverse-on-perfect-image-is-section xâ‚€ Ï

   s : Â¬ is-perfect-image f g (g (f x))
   s = contrapositive (previous-perfect-image x) nÏ

   v : Â¬ is-perfect-image f g xâ‚€
   v = transport (Î» x' â†’ Â¬ is-perfect-image f g x') q s

\end{code}

Double negation elimination on nonperfect fibers.

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } (f : X â†’ Y) (g : Y â†’ X) where

 is-nonperfect-image : X â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 is-nonperfect-image x =
  Î£ xâ‚€ ê‰ X , Î£ n ê‰ â„• , (((g âˆ˜ f) ^ n) xâ‚€ ï¼ x) Ã— (Â¬ fiber g xâ‚€)

 has-nonperfect-fiber : Y â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 has-nonperfect-fiber y =
  Î£ (xâ‚€ , _) ê‰ fiber f y , Â¬ is-perfect-image f g xâ‚€

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 double-negation-elimination-on-perfect-image : is-Â¬Â¬-stable-map g
                                              â†’ (x : X)
                                              â†’ Â¬ is-nonperfect-image f g x
                                              â†’ is-perfect-image f g x
 double-negation-elimination-on-perfect-image Â¬Â¬elim-g x nÏ n (xâ‚€ , p) =
  Â¬Â¬elim-g xâ‚€ (Î» xâ‚ â†’ nÏ (xâ‚€ , n , p , xâ‚))

\end{code}

If g y is not a perfect image, then f has a fiber over y, f x ï¼ y, that is not
a perfect image of g.

Note that in the formalization we assume that g is Â¬Â¬-stable and
left-cancellable, though this implies that g is an embedding if negations are
propositions, which is for instance true if function extensionality holds.

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 not-perfect-images-are-not-not-nonperfect
  : is-Â¬Â¬-stable-map g
  â†’ (y : Y)
  â†’ Â¬ is-perfect-image f g (g y)
  â†’ Â¬Â¬ is-nonperfect-image f g (g y)
 not-perfect-images-are-not-not-nonperfect Â¬Â¬elim-g y nÏ nÎ¼ =
   nÏ (double-negation-elimination-on-perfect-image Â¬Â¬elim-g (g y) nÎ¼)

 nonperfect-images-have-nonperfect-fibers
  : left-cancellable g
  â†’ (y : Y)
  â†’ is-nonperfect-image f g (g y)
  â†’ has-nonperfect-fiber f g y
 nonperfect-images-have-nonperfect-fibers lc-g y (xâ‚€ , zero , p , v) =
  ğŸ˜-elim (v (y , (p â»Â¹)))
 nonperfect-images-have-nonperfect-fibers lc-g y (xâ‚€ , succ n , p , v) =
  (((g âˆ˜ f) ^ n) xâ‚€ , lc-g p) , Î» Î¼ â†’ v (Î¼ n (xâ‚€ , refl))

 not-perfect-images-dont-not-have-nonperfect-fibers
  : is-Â¬Â¬-stable-map g
  â†’ left-cancellable g
  â†’ (y : Y)
  â†’ Â¬ is-perfect-image f g (g y)
  â†’ Â¬Â¬ has-nonperfect-fiber f g y
 not-perfect-images-dont-not-have-nonperfect-fibers Â¬Â¬elim-g lc-g y nÏ nnÎ¼ =
  not-perfect-images-are-not-not-nonperfect Â¬Â¬elim-g y nÏ
   (nnÎ¼ âˆ˜ nonperfect-images-have-nonperfect-fibers lc-g y)

\end{code}

Double negation elimination on nonperfect fibers.

We introduce the concept of a Â¬Â¬-compact type and show that if the map f has
Â¬Â¬-compact fibers (e.g. if f is a complemented embedding) then the nonperfect
fibers of g are Â¬Â¬-stable.

Definition.
A type A is "Â¬Â¬-compact" if for every family of types B : A â†’ ğ“¤ that satisfy
double negation elimination

 (x : A) â†’ Â¬Â¬ B x â†’ B x,

the dependent sum (Î£ (a ê‰ A), B a) again satisfies double negation elimination.

\begin{code}

is-Â¬Â¬-Compact' : ğ“¤ Ì‡  â†’ {ğ“¥ : Universe} â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
is-Â¬Â¬-Compact' {ğ“¤} A {ğ“¥} =
 (B : A â†’ ğ“¥ Ì‡ ) â†’ ((x : A) â†’ Â¬Â¬ B x â†’ B x) â†’ Â¬Â¬ Î£ B â†’ Î£ B

is-Â¬Â¬-Compact'-map : {X : ğ“¤ Ì‡ }
                   â†’ {Y : ğ“¥ Ì‡ }
                   â†’ (X â†’ Y)
                   â†’ {ğ“¦ : Universe}
                   â†’ ğ“¤ âŠ” ğ“¥ âŠ” (ğ“¦ âº) Ì‡
is-Â¬Â¬-Compact'-map {ğ“¤} {ğ“¥} {X} {Y} f {ğ“¦} =
 each-fiber-of f (Î» T â†’ is-Â¬Â¬-Compact' T {ğ“¦})

decidable-types-with-double-negation-dense-equality-are-Â¬Â¬-Compact'
 : {A : ğ“¤ Ì‡ }
 â†’ is-decidable A
 â†’ ((x y : A) â†’ Â¬Â¬ (x ï¼ y))
 â†’ is-Â¬Â¬-Compact' A {ğ“¦}
decidable-types-with-double-negation-dense-equality-are-Â¬Â¬-Compact' d H =
  cases
   (Î» x B Â¬Â¬elim nnab â†’
    x ,
    Â¬Â¬elim x (Î» nb â†’ nnab (Î» (x' , b') â†’ H x' x (Î» p â†’ nb (transport B p b')))))
   (Î» nx B Â¬Â¬elim nnab â†’ ğŸ˜-elim (nnab (Î» (x , b) â†’ nx x))) d

is-Â¬Â¬-Compact : ğ“¤ Ì‡  â†’ {ğ“¥ : Universe} â†’ ğ“¤ âŠ” (ğ“¥ âº) Ì‡
is-Â¬Â¬-Compact {ğ“¤} A {ğ“¥} =
 (B : A â†’ Î©Â¬Â¬ ğ“¥) â†’ Â¬Â¬ (Î£ a ê‰ A , (B a holdsÂ¬Â¬)) â†’ Î£ a ê‰ A , (B a holdsÂ¬Â¬)

Â¬Â¬-Compact-types-are-Â¬Â¬-stable : {A : ğ“¤ Ì‡ }
                               â†’ is-Â¬Â¬-Compact A {ğ“¤}
                               â†’ Â¬Â¬-stable A
Â¬Â¬-Compact-types-are-Â¬Â¬-stable Î± nna =
 prâ‚ (Î± (Î» _ â†’ ((ğŸ™ , ğŸ™-is-prop) , Î» _ â†’ â‹†)) (Â¬Â¬-functor (Î» a â†’ a , â‹†) nna))

Â¬Â¬-Compact'-types-are-Â¬Â¬-stable : {A : ğ“¤ Ì‡ }
                                â†’ is-Â¬Â¬-Compact' A {ğ“¤}
                                â†’ Â¬Â¬-stable A
Â¬Â¬-Compact'-types-are-Â¬Â¬-stable Î± nna =
 prâ‚ (Î± (Î» _ â†’ ğŸ™) (Î» _ _ â†’ â‹†) (Â¬Â¬-functor (Î» a â†’ a , â‹†) nna))

\end{code}

If f has Â¬Â¬-compact fibers then the nonperfect fibers of g are Â¬Â¬-stable.

\begin{code}

module _ {X  : ğ“¤ Ì‡ }
         {Y  : ğ“¥ Ì‡ }
         {f  : X â†’ Y}
         {g  : Y â†’ X}
         (Î±f : is-Â¬Â¬-Compact'-map f)
       where

 nonperfect-fibers-are-Â¬Â¬-stable' : (y : Y)
                                  â†’ Â¬Â¬-stable (has-nonperfect-fiber f g y)
 nonperfect-fibers-are-Â¬Â¬-stable' y =
  Î±f y (Î» (x , p) â†’ Â¬ is-perfect-image f g x) (Î» _ â†’ three-negations-imply-one)

 module _ (Â¬Â¬elim-g : is-Â¬Â¬-stable-map g)
          (lc-g     : left-cancellable g)
          (y        : Y)
          (nÏ       : Â¬ is-perfect-image f g (g y))
        where

  not-perfect-images-have-nonperfect-fibers' : has-nonperfect-fiber f g y
  not-perfect-images-have-nonperfect-fibers' =
   nonperfect-fibers-are-Â¬Â¬-stable' y
    (not-perfect-images-dont-not-have-nonperfect-fibers Â¬Â¬elim-g lc-g y nÏ)

  underlying-fiber-of-nonperfect-fiber-of-not-perfect-image' : fiber f y
  underlying-fiber-of-nonperfect-fiber-of-not-perfect-image' =
   prâ‚ (not-perfect-images-have-nonperfect-fibers')

  element-in-nonperfect-fiber-of-not-perfect-image' : X
  element-in-nonperfect-fiber-of-not-perfect-image' =
   prâ‚ (underlying-fiber-of-nonperfect-fiber-of-not-perfect-image')

  compute-element-in-nonperfect-fiber-of-not-perfect-image'
   : f element-in-nonperfect-fiber-of-not-perfect-image' ï¼ y
  compute-element-in-nonperfect-fiber-of-not-perfect-image' =
   prâ‚‚ (underlying-fiber-of-nonperfect-fiber-of-not-perfect-image')

  nonperfect-fiber-of-not-perfect-image-is-not-perfect'
   : Â¬ is-perfect-image f g element-in-nonperfect-fiber-of-not-perfect-image'
  nonperfect-fiber-of-not-perfect-image-is-not-perfect' =
   prâ‚‚ not-perfect-images-have-nonperfect-fibers'

\end{code}

Complementedness of the perfect image.

Finally, we need conditions under which the perfect image is complemented. For
this purpose we consider maps with Î -compact fibers. This class includes
complemented embeddings, but is much larger. For instance, the fibers are only
weakly complemented in general, and can have multiple distinct elements. For
example, the (infinite) type of conatural numbers â„•âˆ is Î -compact.

\begin{code}

is-Î -Compact-map : {X : ğ“¤ Ì‡ }
                 â†’ {Y : ğ“¥ Ì‡ }
                 â†’ (X â†’ Y)
                 â†’ {ğ“¦ : Universe}
                 â†’ ğ“¤ âŠ” ğ“¥ âŠ” (ğ“¦ âº) Ì‡
is-Î -Compact-map {ğ“¤} {ğ“¥} {X} {Y} f {ğ“¦} =
 each-fiber-of f (Î» T â†’ is-Î -Compact T {ğ“¦})

id-is-Î -Compact-map : {X : ğ“¤ Ì‡ } â†’ is-Î -Compact-map (ğ‘–ğ‘‘ X) {ğ“¦}
id-is-Î -Compact-map {ğ“¤} {ğ“¦} {X} x =
 Î£-Compact-types-are-Î -Compact
  (fiber (ğ‘–ğ‘‘ X) x)
  (singletons-are-Compact (id-is-vv-equiv X x))

âˆ˜-is-Î -Compact-map : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {Z : ğ“¦ Ì‡ } {f : X â†’ Y} {g : Y â†’ Z}
                   â†’ is-Î -Compact-map f {ğ“¤'}
                   â†’ is-Î -Compact-map g {ğ“¤ âŠ” ğ“¥ âŠ” ğ“¤'}
                   â†’ is-Î -Compact-map (g âˆ˜ f) {ğ“¤'}
âˆ˜-is-Î -Compact-map {ğ“¤} {ğ“¥} {ğ“¦} {ğ“¤'} {X} {Y} {Z} {f} {g} Î±f Î±g z =
 equiv-Î -Compact'
  (â‰ƒ-sym (fiber-of-composite f g z))
  (Î£-is-Î -Compact (Î±g z) (Î±f âˆ˜ prâ‚))

iterate-is-Î -Compact-map : {X : ğ“¤ Ì‡ } {f : X â†’ X}
                         â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¦}
                         â†’ (n : â„•) â†’ is-Î -Compact-map (f ^ n) {ğ“¦}
iterate-is-Î -Compact-map Î±f zero =
 id-is-Î -Compact-map
iterate-is-Î -Compact-map Î±f (succ n) =
 âˆ˜-is-Î -Compact-map (iterate-is-Î -Compact-map Î±f n) Î±f

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 perfect-images-at-are-decidable' : is-Î -Compact-map (g âˆ˜ f) {ğ“¤ âŠ” ğ“¥}
                                  â†’ is-complemented-map g
                                  â†’ (x : X) (n : â„•)
                                  â†’ is-decidable (is-perfect-image-at f g x n)
 perfect-images-at-are-decidable' Î±gf dg x n =
  iterate-is-Î -Compact-map Î±gf n x
   (Î» (y , _) â†’ fiber g y)
   (Î» (x , _) â†’ dg x)

 perfect-images-at-are-decidable : is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                                 â†’ is-Î -Compact-map g {ğ“¤ âŠ” ğ“¥}
                                 â†’ is-Â¬Â¬-stable-map g
                                 â†’ (x : X) (n : â„•)
                                 â†’ is-decidable (is-perfect-image-at f g x n)
 perfect-images-at-are-decidable Î±f Î±g Â¬Â¬elim-g =
  perfect-images-at-are-decidable'
   (âˆ˜-is-Î -Compact-map Î±f Î±g)
   (Î» z â†’
    Â¬Â¬-stable-weakly-decidable-types-are-decidable
     (Î -Compact-types-have-decidable-negations (Î±g z))
     (Â¬Â¬elim-g z))

 perfect-images-are-complemented-assuming-WLPO : is-Î -Compact â„• {ğ“¤ âŠ” ğ“¥}
                                               â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                                               â†’ is-Î -Compact-map g {ğ“¤ âŠ” ğ“¥}
                                               â†’ is-Â¬Â¬-stable-map g
                                               â†’ is-complemented
                                                  (is-perfect-image f g)
 perfect-images-are-complemented-assuming-WLPO wlpo Î±f Î±g Â¬Â¬elim-g x =
  wlpo
   (is-perfect-image-at f g x)
   (perfect-images-at-are-decidable Î±f Î±g Â¬Â¬elim-g x)

\end{code}

References
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
 - KÃ¶nig, 1906. Sur la thÃ©orie des ensembles.
   https://gallica.bnf.fr/ark:/12148/bpt6k30977.image.f110.langEN

 - Fredrik Bakke, 2025. The Cantorâ€“SchrÃ¶derâ€“Bernstein theorem in âˆ-Topoi.
   https://hott-uf.github.io/2025/slides/Bakke.pdf (slides)
