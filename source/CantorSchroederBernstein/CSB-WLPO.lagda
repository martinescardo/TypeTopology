Fredrik Bakke, April 2025

The Cantor-SchrÃ¶der-Bernstein theorem assuming WLPO in Agda
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

We prove a generalization of the Cantor-SchrÃ¶der-Bernstein theorem assuming
the weak limited principle of omniscience.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module CantorSchroederBernstein.CSB-WLPO where

open import CantorSchroederBernstein.PerfectImages

open import MLTT.Plus-Properties
open import MLTT.Spartan
open import Taboos.WLPO
open import TypeTopology.CompactTypes
open import TypeTopology.DenseMapsProperties
open import UF.Embeddings
open import UF.Equiv
open import NotionsOfDecidability.Complemented
open import NotionsOfDecidability.Decidable
open import UF.Retracts

\end{code}

In this file we consider a generalization of the Cantorâ€“SchrÃ¶derâ€“Bernstein (CSB)
theorem assuming the weak limited principle of omniscience (WLPO), based on
KÃ¶nig's argument (1906).

 Theorem.
 Assuming WLPO, then for every pair of types X and Y, if there are complemented
 embeddings X â†ª Y and Y â†ª X, then X is equivalent to Y.

In particular, we do not assume function extensionality.

This theorem can be viewed as a generalization of the Cantorâ€“SchrÃ¶derâ€“Bernstein
theorem to arbitrary non-topological âˆ-topoi, since, under the assumption of the
law of excluded middle (LEM), every embedding is complemented. On the other
hand, It was shown by Pradic and Brown (2022) that the Cantorâ€“SchrÃ¶derâ€“Bernstein
theorem in its most naÃ¯ve form implies the law of excluded middle:

 If it is true that for every pair of sets X and Y, if X injects into Y and Y
 injects into X then X and Y are in bijection, then the law of excluded middle
 holds.

Hence we also know that in the absence of the law of excluded middle, the
hypotheses of the theorem must be strengthened.

Construction
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

For our formalization we import a series of properties of perfect images from
which the construction is straightforward.

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 CSB-construction-map'
  : (x : X) â†’ is-decidable (is-perfect-image f g x) â†’ Y
 CSB-construction-map' x (inl Î³) = inverse-on-perfect-image x Î³
 CSB-construction-map' x (inr nÎ³) = f x

 CSB-construction-map
  : ((x : X) â†’ is-decidable (is-perfect-image f g x)) â†’ X â†’ Y
 CSB-construction-map D x = CSB-construction-map' x (D x)

 CSB-construction-map-is-left-cancellable'
  : (lc-f : left-cancellable f)
  â†’ {x x' : X}
  â†’ (d : is-decidable (is-perfect-image f g x))
  â†’ (d' : is-decidable (is-perfect-image f g x'))
  â†’ CSB-construction-map' x d ï¼ CSB-construction-map' x' d'
  â†’ x ï¼ x'
 CSB-construction-map-is-left-cancellable' lc-f {x} {x'} (inl Ï) (inl Ï') p =
  x                                     ï¼âŸ¨ inverse-on-perfect-image-is-section x Ï â»Â¹ âŸ©
  g (inverse-on-perfect-image x Ï)      ï¼âŸ¨ ap g p âŸ©
  g (CSB-construction-map' x' (inl Ï')) ï¼âŸ¨ inverse-on-perfect-image-is-section x' Ï' âŸ©
  x'                                    âˆ
 CSB-construction-map-is-left-cancellable' lc-f {x} {x'} (inl Ï) (inr nÏ') p =
  ğŸ˜-elim (perfect-image-is-disjoint x' x nÏ' Ï (p â»Â¹))
 CSB-construction-map-is-left-cancellable' lc-f {x} {x'} (inr nÏ) (inl Ï') p =
  ğŸ˜-elim (perfect-image-is-disjoint x x' nÏ Ï' p)
 CSB-construction-map-is-left-cancellable' lc-f {x} {x'} (inr nÏ) (inr nÏ') =
  lc-f

 CSB-construction-map-is-left-cancellable
  : left-cancellable f
  â†’ (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ left-cancellable (CSB-construction-map D)
 CSB-construction-map-is-left-cancellable lc-f D {x} {x'} =
  CSB-construction-map-is-left-cancellable' lc-f (D x) (D x')

\end{code}

We compute how the constructed map behaves on the perfect image and its
complement.

\begin{code}

module _ {X        : ğ“¤ Ì‡ }
         {Y        : ğ“¥ Ì‡ }
         {f        : X â†’ Y}
         {g        : Y â†’ X}
         (Â¬Â¬elim-g : is-Â¬Â¬-stable-map g)
         (lc-g     : left-cancellable g)
         (Î±f       : is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥})
       where

 CSB-construction-map-on-perfect-image
  : (y : Y)
  â†’ (Î³ : is-perfect-image f g (g y))
  â†’ (d : is-decidable (is-perfect-image f g (g y)))
  â†’ CSB-construction-map' (g y) d ï¼ y
 CSB-construction-map-on-perfect-image y Î³ (inl v') =
  inverse-on-perfect-image-is-retraction lc-g y v'
 CSB-construction-map-on-perfect-image y Î³ (inr v) =
  ğŸ˜-elim (v Î³)

 CSB-construction-map-on-not-perfect-image
  : (y : Y)
  â†’ (nÎ³ : Â¬ is-perfect-image f g (g y))
  â†’ (d : is-decidable
          (is-perfect-image f g
           (element-in-nonperfect-fiber-of-not-perfect-image'
             Î±f Â¬Â¬elim-g lc-g y nÎ³)))
  â†’ CSB-construction-map'
     (element-in-nonperfect-fiber-of-not-perfect-image'
       Î±f Â¬Â¬elim-g lc-g y nÎ³)
     (d)
    ï¼ y
 CSB-construction-map-on-not-perfect-image y nÎ³ (inl v) =
  ğŸ˜-elim
   (nonperfect-fiber-of-not-perfect-image-is-not-perfect'
     Î±f Â¬Â¬elim-g lc-g y nÎ³ v)
 CSB-construction-map-on-not-perfect-image y nÎ³ (inr _) =
  compute-element-in-nonperfect-fiber-of-not-perfect-image'
   Î±f Â¬Â¬elim-g lc-g y nÎ³

\end{code}

The construction is an equivalence.

\begin{code}

 CSB-construction-inverse-map'
  : (y : Y) â†’ is-decidable (is-perfect-image f g (g y)) â†’ X
 CSB-construction-inverse-map' y (inl _) =
  g y
 CSB-construction-inverse-map' y (inr nÎ³) =
  element-in-nonperfect-fiber-of-not-perfect-image' Î±f Â¬Â¬elim-g lc-g y nÎ³

 CSB-construction-map-is-retraction'
  : (y : Y)
  â†’ (d : is-decidable (is-perfect-image f g (g y)))
  â†’ (d' : is-decidable
           (is-perfect-image f g (CSB-construction-inverse-map' y d)))
  â†’ CSB-construction-map' (CSB-construction-inverse-map' y d) d' ï¼ y
 CSB-construction-map-is-retraction' y (inl Î³) =
  CSB-construction-map-on-perfect-image y Î³
 CSB-construction-map-is-retraction' y (inr nÎ³) =
  CSB-construction-map-on-not-perfect-image y nÎ³

 CSB-construction-inverse-map
  : ((y : Y) â†’ is-decidable (is-perfect-image f g (g y))) â†’ Y â†’ X
 CSB-construction-inverse-map D y =
  CSB-construction-inverse-map' y (D y)

 CSB-construction-map-is-retraction
  : (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ CSB-construction-map D âˆ˜ CSB-construction-inverse-map (D âˆ˜ g) âˆ¼ id
 CSB-construction-map-is-retraction D y =
   CSB-construction-map-is-retraction' y
    (D (g y))
    (D (CSB-construction-inverse-map (D âˆ˜ g) y))

 CSB-construction-map-has-section
  : (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ has-section (CSB-construction-map D)
 CSB-construction-map-has-section D =
  (CSB-construction-inverse-map (D âˆ˜ g) , CSB-construction-map-is-retraction D)

 CSB-construction-retract
   : ((x : X) â†’ is-decidable (is-perfect-image f g x)) â†’ retract Y of X
 CSB-construction-retract D =
  (CSB-construction-map D , CSB-construction-map-has-section D)

 CSB-construction-is-equiv
  : left-cancellable f
  â†’ (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ is-equiv (CSB-construction-map D)
 CSB-construction-is-equiv lc-f D =
  lc-retractions-are-equivs
   (CSB-construction-map D)
   (CSB-construction-map-is-left-cancellable lc-f D)
   (CSB-construction-map-has-section D)

 CSB-construction-equiv
  : left-cancellable f
  â†’ ((x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ X â‰ƒ Y
 CSB-construction-equiv lc-f D =
  (CSB-construction-map D , CSB-construction-is-equiv lc-f D)

\end{code}

Note in particular that the above definition gives us a fully constructive
version of KÃ¶nig's argument:

 Proposition.
 Given maps f : X â†’ Y and g : Y â†’ X such that

  1. g is left cancellable and Â¬Â¬-stable,
  2. f is left cancellable and has Â¬Â¬-compact fibers
  3. the perfect image of g relative to f is complemented

 then X â‰ƒ Y.

Now, if WLPO holds and f and g are complemented embeddings we can show that the
perfect image is always complemented, hence we can apply the above proposition
and derive our main result.

\begin{code}

module _ (wlpo : is-Î -Compact â„• {ğ“¤ âŠ” ğ“¥})
         {X    : ğ“¤ Ì‡ }
         {Y    : ğ“¥ Ì‡ }
         {f    : X â†’ Y}
         {g    : Y â†’ X}
       where

 private
   lemma : is-complemented-map g
         â†’ is-embedding g
         â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
         â†’ (x : X) â†’ is-decidable (is-perfect-image f g x)
   lemma  cg emb-g Î²f =
    perfect-images-are-complemented-assuming-WLPO wlpo Î²f
     (Î» y â†’
      Î£-Compact-types-are-Î -Compact
       (fiber g y)
       (compact-types-are-Compact
        (decidable-propositions-are-compact (fiber g y) (emb-g y) (cg y))))
     (Î» y â†’ Â¬Â¬-elim (cg y))

 CSB-retract-assuming-WLPO' : is-complemented-map g
                            â†’ is-embedding g
                            â†’ is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥}
                            â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                            â†’ retract Y of X
 CSB-retract-assuming-WLPO' cg emb-g Î±f Î²f =
  CSB-construction-retract
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î±f)
   (lemma cg emb-g Î²f)

 CSB-equiv-assuming-WLPO' : is-complemented-map g
                          â†’ is-embedding g
                          â†’ is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥}
                          â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                          â†’ left-cancellable f
                          â†’ X â‰ƒ Y
 CSB-equiv-assuming-WLPO' cg emb-g Î±f Î²f lc-f =
  CSB-construction-equiv
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î±f)
   (lc-f)
   (lemma cg emb-g Î²f)

\end{code}

In the preceding definition the three assumptions on f are equivalent to f being
a complemented embedding. We formalize that they at least follow from the
latter.

\begin{code}

 CSB-equiv-assuming-WLPO : is-complemented-map g
                         â†’ is-embedding g
                         â†’ is-complemented-map f
                         â†’ is-embedding f
                         â†’ X â‰ƒ Y
 CSB-equiv-assuming-WLPO cg emb-g cf emb-f =
  CSB-construction-equiv
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î» y â†’
    decidable-types-with-double-negation-dense-equality-are-Â¬Â¬-Compact'
     (cf y)
     (Î» p q â†’ Â¬Â¬-intro (emb-f y p q)))
   (embeddings-are-lc f emb-f)
   (lemma cg emb-g
    (Î» y â†’
     Î£-Compact-types-are-Î -Compact
      (fiber f y)
      (compact-types-are-Compact
       (decidable-propositions-are-compact (fiber f y) (emb-f y) (cf y)))))

\end{code}

Finally, to dispell all doubt, we instantiate the previous theorem at the
traditional phrasing of WLPO.

\begin{code}

is-Î -compact : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
is-Î -compact X = (p : X â†’ ğŸš) â†’ is-decidable ((x : X) â†’ p x ï¼ â‚)

Î -compact-types-are-Î -Compact : {X : ğ“¤ Ì‡ } â†’ is-Î -compact X â†’ is-Î -Compact X {ğ“¥}
Î -compact-types-are-Î -Compact {ğ“¤} {ğ“¥} {X} H A Î´ =
 +functor
  (Î» na  x â†’ cases
              (id)
              (ğŸ˜-elim âˆ˜ characteristic-map-propertyâ‚ Â¬A Â¬Î´ x (na x))
              (Î´ x))
  (Î» nna f â†’ nna
              (Î» x â†’ characteristic-map-propertyâ‚-back Â¬A Â¬Î´ x
                      (Â¬Â¬-intro (f x))))
  (H (characteristic-map Â¬A Â¬Î´))
  where
   Â¬A : X â†’ ğ“¥ Ì‡
   Â¬A x = Â¬ (A x)

   Â¬Î´ : is-complemented Â¬A
   Â¬Î´ x = decidable-types-are-closed-under-negations (Î´ x)

CSB-equiv-assuming-traditional-WLPO : WLPO-traditional
                                    â†’ {X : ğ“¤ Ì‡ }
                                    â†’ {Y : ğ“¥ Ì‡ }
                                    â†’ {f : X â†’ Y}
                                    â†’ {g : Y â†’ X}
                                    â†’ is-complemented-map g
                                    â†’ is-embedding g
                                    â†’ is-complemented-map f
                                    â†’ is-embedding f
                                    â†’ X â‰ƒ Y
CSB-equiv-assuming-traditional-WLPO wlpo =
 CSB-equiv-assuming-WLPO (Î -compact-types-are-Î -Compact wlpo)

\end{code}

References
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
 - KÃ¶nig, 1906. Sur la thÃ©orie des ensembles.
   https://gallica.bnf.fr/ark:/12148/bpt6k30977.image.f110.langEN

 - Pradic & Brown, 2022. Cantor-Bernstein implies Excluded Middle.
   https://arxiv.org/abs/1904.09193

 - Bakke, 2025. The Cantorâ€“SchrÃ¶derâ€“Bernstein theorem in âˆ-Topoi.
   https://hott-uf.github.io/2025/slides/Bakke.pdf (slides)
