Fredrik Bakke, April 2025

The Cantor-SchrÃ¶der-Bernstein assuming WLPO in Agda
-------------------------------------------------------------------------

We prove a generalization of the Cantor-SchrÃ¶der-Bernstein theorem assuming
WLPO. We do not assume function extensionality.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module CantorSchroederBernstein.CSB-WLPO where

open import CantorSchroederBernstein.PerfectImages

open import CoNaturals.Type
open import MLTT.Plus-Properties
open import MLTT.Spartan
open import Naturals.Properties
open import TypeTopology.CompactTypes
open import TypeTopology.DenseMapsProperties
open import TypeTopology.Density
open import TypeTopology.GenericConvergentSequenceCompactness
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.ClassicalLogic
open import NotionsOfDecidability.Complemented
open import NotionsOfDecidability.Decidable
open import UF.FunExt
open import UF.Lower-FunExt
open import UF.PropTrunc
open import UF.SubtypeClassifier
open import UF.Retracts
open import UF.Sets
open import UF.NotNotStablePropositions
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import CantorSchroederBernstein.CSB

\end{code}

Using our lemmas about perfect images, the construction is straight forward!

\begin{code}

module _ {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X} where

 map-construction : (x : X) â†’ is-decidable (is-perfect-image f g x) â†’ Y
 map-construction x (inl Î³) = inverse-on-perfect-image x Î³
 map-construction x (inr nÎ³) = f x

 map-construction-CSB :
  ((x : X) â†’ is-decidable (is-perfect-image f g x)) â†’ X â†’ Y
 map-construction-CSB D x = map-construction x (D x)

 map-construction-is-left-cancellable :
  (lc-f : left-cancellable f) â†’
  {x x' : X}
  (d : is-decidable (is-perfect-image f g x))
  (d' : is-decidable (is-perfect-image f g x')) â†’
  map-construction x d ï¼ map-construction x' d' â†’ x ï¼ x'
 map-construction-is-left-cancellable lc-f {x} {x'} (inl Ï) (inl Ï') p =
  inverse-on-perfect-image-is-section x Ï â»Â¹
  âˆ™ ap g p
  âˆ™ inverse-on-perfect-image-is-section x' Ï'
 map-construction-is-left-cancellable lc-f {x} {x'} (inl Ï) (inr nÏ') p =
  ğŸ˜-elim (perfect-image-is-disjoint x' x nÏ' Ï (p â»Â¹))
 map-construction-is-left-cancellable lc-f {x} {x'} (inr nÏ) (inl Ï') p =
  ğŸ˜-elim (perfect-image-is-disjoint x x' nÏ Ï' p)
 map-construction-is-left-cancellable lc-f {x} {x'} (inr nÏ) (inr nÏ') = lc-f

 map-construction-CSB-is-left-cancellable :
  left-cancellable f â†’
  (D : (x : X) â†’ is-decidable (is-perfect-image f g x)) â†’
  left-cancellable (map-construction-CSB D)
 map-construction-CSB-is-left-cancellable lc-f D {x} {x'} =
  map-construction-is-left-cancellable lc-f (D x) (D x')

\end{code}

Computations with the construction.

\begin{code}

module _
 {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X}
 (Â¬Â¬elim-g : is-Â¬Â¬-stable-map g)
 (lc-g : left-cancellable g)
 (Î±f : is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥})
 where

 compute-construction-on-perfect-image :
  (y : Y) â†’
  (Î³ : is-perfect-image f g (g y)) â†’
  (d : is-decidable (is-perfect-image f g (g y))) â†’
  map-construction (g y) d ï¼ y
 compute-construction-on-perfect-image y Î³ (inl v') =
  inverse-on-perfect-image-is-retraction lc-g y v'
 compute-construction-on-perfect-image y Î³ (inr v) = ğŸ˜-elim (v Î³)

 compute-construction-on-not-perfect-image :
  (y : Y) â†’
  (nÎ³ : Â¬ is-perfect-image f g (g y)) â†’
  (d :
    is-decidable
      ( is-perfect-image f g
       ( element-in-nonperfect-fiber-of-not-perfect-image' Î±f Â¬Â¬elim-g lc-g y nÎ³))) â†’
  map-construction
    (element-in-nonperfect-fiber-of-not-perfect-image' Î±f Â¬Â¬elim-g lc-g y nÎ³)
    (d) ï¼
  y
 compute-construction-on-not-perfect-image y nÎ³ (inl v) =
  ğŸ˜-elim (nonperfect-fiber-of-not-perfect-image-is-not-perfect' Î±f Â¬Â¬elim-g lc-g y nÎ³ v)
 compute-construction-on-not-perfect-image y nÎ³ (inr _) =
  compute-element-in-nonperfect-fiber-of-not-perfect-image' Î±f Â¬Â¬elim-g lc-g y nÎ³
\end{code}

The construction is an equivalence.

\begin{code}
 inverse-construction :
   (y : Y) â†’ is-decidable (is-perfect-image f g (g y)) â†’ X
 inverse-construction y (inl _) = g y
 inverse-construction y (inr nÎ³) =
   element-in-nonperfect-fiber-of-not-perfect-image' Î±f Â¬Â¬elim-g lc-g y nÎ³

 construction-is-retraction :
  (y : Y)
  (d : is-decidable (is-perfect-image f g (g y))) â†’
  (d' : is-decidable (is-perfect-image f g (inverse-construction y d))) â†’
  map-construction (inverse-construction y d) d' ï¼ y
 construction-is-retraction y (inl Î³) =
   compute-construction-on-perfect-image y Î³
 construction-is-retraction y (inr nÎ³) =
   compute-construction-on-not-perfect-image y nÎ³

 inverse-construction-CSB :
  ((y : Y) â†’ is-decidable (is-perfect-image f g (g y))) â†’ Y â†’ X
 inverse-construction-CSB D y = inverse-construction y (D y)

 is-section-inverse-construction-CSB :
  (D : (x : X) â†’ is-decidable (is-perfect-image f g x)) â†’
  map-construction-CSB D âˆ˜ inverse-construction-CSB (D âˆ˜ g) âˆ¼ id
 is-section-inverse-construction-CSB D y =
   construction-is-retraction y
    (D (g y))
    (D (inverse-construction-CSB (D âˆ˜ g) y))

 map-construction-CSB-has-section
  : (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ has-section (map-construction-CSB D)
 map-construction-CSB-has-section D =
  ( inverse-construction-CSB (D âˆ˜ g) ,
    is-section-inverse-construction-CSB D)

 retract-CSB :
  ((x : X) â†’ is-decidable (is-perfect-image f g x)) â†’ retract Y of X
 retract-CSB D =
  ( map-construction-CSB D , map-construction-CSB-has-section D)

 construction-CSB-is-equiv :
  left-cancellable f â†’
  (D : (x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ is-equiv (map-construction-CSB D)
 construction-CSB-is-equiv lc-f D =
  lc-retractions-are-equivs (map-construction-CSB D)
   (map-construction-CSB-is-left-cancellable lc-f D)
   (map-construction-CSB-has-section D)

 equiv-CSB :
  left-cancellable f
  â†’ ((x : X) â†’ is-decidable (is-perfect-image f g x))
  â†’ X â‰ƒ Y
 equiv-CSB lc-f D = (map-construction-CSB D , construction-CSB-is-equiv lc-f D)

\end{code}

This gives us the conclusion we want.

\begin{code}

module _
 (wlpo : is-Î -Compact â„• {ğ“¤ âŠ” ğ“¥})
 {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {f : X â†’ Y} {g : Y â†’ X}
 where

 private
   lemma : is-complemented-map g
         â†’ is-embedding g
         â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
         â†’ (x : X) â†’ is-decidable (is-perfect-image f g x)
   lemma  cg emb-g Î²f =
    perfect-images-are-complemented-assuming-WLPO wlpo Î²f
     (Î» y â†’
      Î£-Compact-types-are-Î -Compact
       (fiber g y)
       (compact-types-are-Compact
        (decidable-propositions-are-compact (fiber g y) (emb-g y) (cg y))))
     (Î» y â†’ Â¬Â¬-elim (cg y))

 retract-CSB-assuming-WLPO : is-complemented-map g
                           â†’ is-embedding g
                           â†’ is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥}
                           â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                           â†’ retract Y of X
 retract-CSB-assuming-WLPO cg emb-g Î±f Î²f =
  retract-CSB
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î±f)
   (lemma cg emb-g Î²f)

 equiv-CSB-assuming-WLPO : is-complemented-map g
                         â†’ is-embedding g
                         -- The following three imply $f$ is a complemented embedding
                         â†’ is-Â¬Â¬-Compact'-map f {ğ“¤ âŠ” ğ“¥}
                         â†’ is-Î -Compact-map f {ğ“¤ âŠ” ğ“¥}
                         â†’ left-cancellable f
                         â†’ X â‰ƒ Y
 equiv-CSB-assuming-WLPO cg emb-g Î±f Î²f lc-f =
  equiv-CSB
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î±f)
   (lc-f)
   (lemma cg emb-g Î²f)

 equiv-CSB-assuming-WLPO' : is-complemented-map g
                          â†’ is-embedding g
                          â†’ is-complemented-map f
                          â†’ is-embedding f
                          â†’ X â‰ƒ Y
 equiv-CSB-assuming-WLPO' cg emb-g cf emb-f =
  equiv-CSB
   (Î» y â†’ Â¬Â¬-elim (cg y))
   (embeddings-are-lc g emb-g)
   (Î» y â†’
    decidable-types-with-double-negation-dense-equality-are-Â¬Â¬-Compact'
     (cf y)
     (Î» p q â†’ Â¬Â¬-intro (emb-f y p q)))
   (embeddings-are-lc f emb-f)
   (lemma cg emb-g
    (Î» y â†’
     Î£-Compact-types-are-Î -Compact
      (fiber f y)
      (compact-types-are-Compact
       (decidable-propositions-are-compact (fiber f y) (emb-f y) (cf y)))))

\end{code}
