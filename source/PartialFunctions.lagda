Martin Escardo 25th October 2018.

Only lifting for the moment. We will then discuss partial functions
(cf. my former student Cory Knapp's PhD thesis).

We focuse, to begin with, on the fact that the canonical map into the
lifting is an embedding, which is easy for sets, but seems hard for
arbitrary types.

\begin{code}

{-# OPTIONS --without-K --exact-split #-}

open import SpartanMLTT

module PartialFunctions where

open import UF-Base
open import UF-Subsingletons hiding (âŠ¥)
open import UF-Embedding
open import UF-FunExt
open import UF-Equiv
open import UF-Subsingletons-FunExt
open import UF-Retracts

module _ {V : Universe} where

 ğ“› : âˆ€ {U} â†’ U Ì‡ â†’ U âŠ” V â€² Ì‡
 ğ“› {U} X = Î£ \(P : V Ì‡) â†’ is-prop P Ã— (P â†’ X)

 Î· : âˆ€ {U} {X : U Ì‡} â†’ X â†’ ğ“› X
 Î· x = ğŸ™ , ğŸ™-is-prop , (Î» _ â†’ x)

\end{code}

Our strategy to show that Î· is an embedding is to exhibit it as the
composite of two embeddings (the first of which is actually an
equivalence).

\begin{code}

 ğ“œ : âˆ€ {U} â†’ U Ì‡ â†’ U âŠ” V â€² Ì‡
 ğ“œ {U} X = Î£ \(P : V Ì‡) â†’ is-singleton P Ã— (P â†’ X)

 Î¼ : âˆ€ {U} {X : U Ì‡} â†’ X â†’ ğ“œ X
 Î¼ x = ğŸ™ , ğŸ™-is-singleton , (Î» _ â†’ x)

 Î¶ : âˆ€ {U} (X : U Ì‡) (P : V Ì‡) â†’ is-singleton P Ã— (P â†’ X) â†’ is-prop P Ã— (P â†’ X)
 Î¶ X P (i , Ï†) = is-singleton-is-prop i , Ï†

 ğ“œ-to-ğ“› : âˆ€ {U} (X : U Ì‡) â†’ ğ“œ X â†’ ğ“› X
 ğ“œ-to-ğ“› X = NatÎ£ (Î¶ X)

 Î·-composite : funext V V â†’ âˆ€ {U} â†’ funext U (V â€² âŠ” U)
             â†’ {X : U Ì‡} â†’ Î· â‰¡ ğ“œ-to-ğ“› X âˆ˜ Î¼
 Î·-composite fe fe' {X} = dfunext fe' h
  where
   h : (x : X) â†’ (ğŸ™ , ğŸ™-is-prop ,                             Î» _ â†’ x)
               â‰¡ (ğŸ™ , is-singleton-is-prop (ğŸ™-is-singleton) , Î» _ â†’ x)
   h x = to-Î£-â‰¡ (refl , to-Ã—-â‰¡ (is-prop-is-prop fe _ _) refl)

\end{code}

The fact that ğ“œ-to-ğ“› is an embedding can be proved by obtaining it as
a combination of maps that we already know to be embeddings, using
Ã—-embedding, maps-of-props-are-embeddings, id-is-embedding, and
NatÎ£-embedding.:

\begin{code}

 Î¶-is-embedding : funext V V â†’ âˆ€ {U} (X : U Ì‡) (P : V Ì‡) â†’ is-embedding (Î¶ X P)
 Î¶-is-embedding fe X P = Ã—-embedding
                           is-singleton-is-prop
                           id
                           (maps-of-props-are-embeddings
                              is-singleton-is-prop
                              (is-prop-is-singleton fe)
                              (is-prop-is-prop fe))
                           id-is-embedding

 ğ“œ-to-ğ“›-is-embedding : funext V V â†’ âˆ€ {U} (X : U Ì‡)
                     â†’ is-embedding (ğ“œ-to-ğ“› X)
 ğ“œ-to-ğ“›-is-embedding fe {U} X = NatÎ£-is-embedding
                                  (Î» P â†’ is-singleton P Ã— (P â†’ X))
                                  (Î» P â†’ is-prop P Ã— (P â†’ X))
                                  (Î¶ X)
                                  (Î¶-is-embedding fe X)
\end{code}

That Î¼ is an equivalence corresponds to the fact that the lifting of a
type X with respect to the dominance is-singleton is equivalent to X
itself.

\begin{code}

 Î¼-is-equiv : propext V â†’ funext V V â†’ âˆ€ {U} â†’ funext V U
            â†’ {X : U Ì‡} â†’ is-equiv (Î¼ {U} {X})
 Î¼-is-equiv pe fe {U} fe' {X} = qinv-is-equiv Î¼ (Î½ , (Î½Î¼ , Î¼Î½))
  where
   Î½ : âˆ€ {U} {X : U Ì‡} â†’ ğ“œ X â†’ X
   Î½ (P , i , Ï†) = Ï† (is-singleton-pointed i)
   Î½Î¼ : âˆ€ {U} {X : U Ì‡} (x : X) â†’ Î½ (Î¼ x) â‰¡ x
   Î½Î¼ x = refl
   Î¼Î½ : (m : ğ“œ X) â†’ Î¼ (Î½ m) â‰¡ m
   Î¼Î½ (P , i , Ï†) = to-Î£-â‰¡ (t , s)
    where
     t : ğŸ™ â‰¡ P
     t = pe ğŸ™-is-prop (is-singleton-is-prop i) (Î» _ â†’ is-singleton-pointed i) (Î» p â†’ *)
     u : transport (Î» - â†’ - â†’ X) t (Î» _ â†’ Ï† (is-singleton-pointed i)) â‰¡ Ï†
     u = transport (Î» - â†’ - â†’ X) t (Î» _ â†’ Ï† (is-singleton-pointed i))
             â‰¡âŸ¨ transport-is-pre-comp t (Î» _ â†’ Ï† (is-singleton-pointed i)) âŸ©
         (Î» _ â†’ Ï† (is-singleton-pointed i))
             â‰¡âŸ¨ dfunext fe' (Î» p â†’ ap Ï† (is-singleton-is-prop i (is-singleton-pointed i) p)) âŸ©
         Ï†   âˆ
     s : transport (Î» - â†’ is-singleton - Ã— (- â†’ X)) t (ğŸ™-is-singleton , (Î» _ â†’ Ï† (is-singleton-pointed i)))
       â‰¡ i , Ï†
     s = transport (Î» - â†’ is-singleton - Ã— (- â†’ X)) t (ğŸ™-is-singleton , (Î» _ â†’ Ï† (is-singleton-pointed i)))
              â‰¡âŸ¨ transport-Ã— is-singleton (Î» - â†’ - â†’ X) t âŸ©
         transport is-singleton t ğŸ™-is-singleton , transport (Î» - â†’ - â†’ X) t (Î» _ â†’ Ï† (is-singleton-pointed i))
              â‰¡âŸ¨ to-Ã—-â‰¡ (is-prop-is-singleton fe _ i) u âŸ©
         i , Ï† âˆ

 Î¼-is-embedding : propext V â†’ funext V V â†’ âˆ€ {U} â†’ funext V U
                â†’ {X : U Ì‡} â†’ is-embedding (Î¼ {U} {X})
 Î¼-is-embedding pe fe fe' = is-equiv-is-embedding Î¼ (Î¼-is-equiv pe fe fe')

\end{code}

Finally, Î· is an embedding because it is equal to the composition of
two embeddings:

\begin{code}

 Î·-is-embedding : propext V â†’ funext V V â†’ âˆ€ {U} â†’ funext V U â†’ funext U (V â€² âŠ” U)
                â†’ {X : U Ì‡} â†’ is-embedding (Î· {U} {X})
 Î·-is-embedding pe fe fe' fe'' {X} =
   back-transport
    is-embedding
    (Î·-composite fe fe'')
   (comp-embedding (Î¼-is-embedding pe fe fe') (ğ“œ-to-ğ“›-is-embedding fe X))

\end{code}

We now give meaningful names to the projections:

\begin{code}

 is-defined : âˆ€ {U} {X : U Ì‡} â†’ ğ“› X â†’ V Ì‡
 is-defined (P , i , Ï†) = P

 is-defined-is-prop : âˆ€ {U} {X : U Ì‡} (l : ğ“›  X) â†’ is-prop (is-defined l)
 is-defined-is-prop (P , i , Ï†) = i

 value : âˆ€ {U} {X : U Ì‡} (l : ğ“›  X) â†’ is-defined l â†’ X
 value (P , i , Ï†) = Ï†

\end{code}

Next we show that for any l : ğ“› X,

 fiber Î· l â‰ƒ is-defined l,

using the fact that the fiber is a proposition by virtue of Î· being an
embedding.

For this purpose, it is convenient to work with the information
"Order" on ğ“› X, which will really be a (partial) order only when X is
a set:

\begin{code}

 _âŠ‘_ : âˆ€ {U} {X : U Ì‡} â†’ ğ“› X â†’ ğ“› X â†’ U âŠ” V Ì‡
 l âŠ‘ m = Î£ \(f : is-defined l â†’ is-defined m) â†’ (d : is-defined l) â†’ value l d â‰¡ value m (f d)

\end{code}

If X is a set, then ğ“› X is a poset under _âŠ‘_ (TODO). In the general
case, it should be some sort of univalent âˆ-category with
hom-âˆ-groupoids x âŠ‘ y.

\begin{code}

 âŠ‘-id : âˆ€ {U} {X : U Ì‡} (l : ğ“› X) â†’ l âŠ‘ l
 âŠ‘-id (P , i , Ï†) = id , (Î» x â†’ refl)

 âŠ‘-id' : âˆ€ {U} {X : U Ì‡} (l m : ğ“› X) â†’ l â‰¡ m â†’ l âŠ‘ m
 âŠ‘-id' l m p = transport (Î» - â†’ l âŠ‘ -) p (âŠ‘-id l)

 âŠ‘-âˆ˜ : âˆ€ {U} {X : U Ì‡} (l m n : ğ“› X)
     â†’ l âŠ‘ m â†’ m âŠ‘ n â†’ l âŠ‘ n
 âŠ‘-âˆ˜ l m n (f , Î´) (g , Îµ) = g âˆ˜ f , (Î» d â†’ Î´ d âˆ™ Îµ (f d))

 âŠ‘-anti : âˆ€ {U} â†’ propext V â†’ funext V V â†’ funext V U
        â†’ {X : U Ì‡} {l m : ğ“› X}
        â†’ (l âŠ‘ m) Ã— (m âŠ‘ l) â†’ l â‰¡ m
 âŠ‘-anti pe fe fe' {X} {Q , j , Î³} {P , i , Ï†} ((f , Î´) , (g , Îµ)) = e
  where
   a : Q â‰¡ P
   a = pe j i f g
   b : Idtofun (a â»Â¹) â‰¡ g
   b = dfunext fe (Î» p â†’ j (Idtofun (a â»Â¹) p) (g p))
   c : transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)
     â‰¡ (transport is-prop a j , transport (Î» - â†’ - â†’ X) a Î³)
   c = transport-Ã— is-prop (Î» - â†’ - â†’ X) a
   d : prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)) â‰¡ Ï†
   d = prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³))
             â‰¡âŸ¨ ap prâ‚‚ c âŸ©
       transport (Î» - â†’ - â†’ X) a Î³
             â‰¡âŸ¨ transport-is-pre-comp a Î³ âŸ©
       Î³ âˆ˜ Idtofun (a â»Â¹)
             â‰¡âŸ¨ ap (Î» - â†’ Î³ âˆ˜ -) b âŸ©
       Î³ âˆ˜ g
             â‰¡âŸ¨ (dfunext fe' Îµ)â»Â¹ âŸ©
       Ï†     âˆ
   e : Q , j , Î³ â‰¡ P , i , Ï†
   e = to-Î£-â‰¡ (a , to-Ã—-â‰¡ (is-prop-is-prop fe _ i) d)

\end{code}

We haven't used Î´ in the above proof. But we could use it instead of
Îµ, by definiting Îµ' from Î´ as follows, and then using (dfunext fe' Îµ')
instead of (dfunext fe' Îµ)â»Â¹ in the above proof:

\begin{code}

   Îµ' : (p : P) â†’ Î³ (g p) â‰¡ Ï† p
   Îµ' p = Î´ (g p) âˆ™ ap Ï† (i (f (g p)) p)

\end{code}

We can now establish the promised fact:

\begin{code}

 Î·-fiber-same-as-is-defined :
     propext V â†’ funext V V â†’ âˆ€ {U} â†’ funext V U â†’ funext U (V â€² âŠ” U)
   â†’ {X : U Ì‡} (l : ğ“› X)
   â†’ (Î£ \(x : X) â†’ Î· x â‰¡ l) â‰ƒ is-defined l
 Î·-fiber-same-as-is-defined pe fe fe' fe'' {X} l = f l , ((g l , fg) , (g l , gf))
  where
   f : (l : ğ“› X) â†’ fiber Î· l â†’ is-defined l
   f (.ğŸ™ , .ğŸ™-is-prop , .(Î» _ â†’ x)) (x , refl) = *
   g : (l : ğ“› X) â†’ is-defined l â†’ fiber Î· l
   g (P , i , Ï†) p = Ï† p , âŠ‘-anti pe fe fe' (a , b)
    where
     a : Î· (Ï† p) âŠ‘ (P , i , Ï†)
     a = (Î» _ â†’ p) , (Î» _ â†’ refl)
     b : (P , i , Ï†) âŠ‘ Î· (Ï† p)
     b = (Î» _ â†’ *) , (Î» q â†’ ap Ï† (i q p))
   fg : (d : is-defined l) â†’ f l (g l d) â‰¡ d
   fg d = is-defined-is-prop l (f l (g l d)) d
   gf : (z : fiber Î· l) â†’ g l (f l z) â‰¡ z
   gf z = Î·-is-embedding pe fe fe' fe'' l (g l (f l z)) z

\end{code}

They can't be equal, in the absence of cumulativity (and propositional
resizing), as the lhs lives in a universe higher than the rhs, and
equality is well-typed only for elements of the same type (here of the
same universe). This can be seen by adding type annotations to the
formulation of the above equivalence:

\begin{code}

 private
  Î·-fiber-same-as-is-defined' :
       propext V â†’ funext V V â†’ âˆ€ {U} â†’ funext V U â†’ funext U (V â€² âŠ” U)
    â†’ {X : U Ì‡} (l : ğ“› X)
    â†’ (fiber Î· l âˆ¶ V â€² âŠ” U Ì‡) â‰ƒ (is-defined l âˆ¶ V Ì‡)
  Î·-fiber-same-as-is-defined' = Î·-fiber-same-as-is-defined

\end{code}

For no choice of universes U and V can we have V ' âŠ” U to coincide
with V. However, for dominances other than is-prop, then it will be
possible to have the equality beyween the fiber of l and the
definedness of l.

TODO: Could the map (anti l m) be an equivalence? No. We should
instead have an equivalence (l âŠ‘ m) Ã— (m âŠ‘ l) â†’ (l â‰¡ m) Ã— (l â‰¡ m),
reflecting the fact that there were two candidate proofs for the
equality, as discussed above.

But it is probably better to have the following version of âŠ‘-anti,
which should be an equivalence for each l and m:

\begin{code}

 âŠ‘-anti' : âˆ€ {U} â†’ propext V â†’ funext V V â†’ funext V U
        â†’ {X : U Ì‡} (l m : ğ“› X) â†’ (l âŠ‘ m) Ã— (is-defined m â†’ is-defined l) â†’ l â‰¡ m
 âŠ‘-anti' pe fe fe' {X} (Q , j , Î³) (P , i , Ï†) ((f , Î´) , g) = e
  where
   Îµ' : (p : P) â†’ Î³ (g p) â‰¡ Ï† p
   Îµ' p = Î´ (g p) âˆ™ ap Ï† (i (f (g p)) p)
   a : Q â‰¡ P
   a = pe j i f g
   b : Idtofun (a â»Â¹) â‰¡ g
   b = dfunext fe (Î» p â†’ j (Idtofun (a â»Â¹) p) (g p))
   c : transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)
     â‰¡ (transport is-prop a j , transport (Î» - â†’ - â†’ X) a Î³)
   c = transport-Ã— is-prop (Î» - â†’ - â†’ X) a
   d : prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³)) â‰¡ Ï†
   d = prâ‚‚ (transport (Î» - â†’ is-prop - Ã— (- â†’ X)) a (j , Î³))
             â‰¡âŸ¨ ap prâ‚‚ c âŸ©
       transport (Î» - â†’ - â†’ X) a Î³
             â‰¡âŸ¨ transport-is-pre-comp a Î³ âŸ©
       Î³ âˆ˜ Idtofun (a â»Â¹)
             â‰¡âŸ¨ ap (Î» - â†’ Î³ âˆ˜ -) b âŸ©
       Î³ âˆ˜ g
             â‰¡âŸ¨ dfunext fe' Îµ' âŸ©
       Ï†     âˆ
   e : Q , j , Î³ â‰¡ P , i , Ï†
   e = to-Î£-â‰¡ (a , to-Ã—-â‰¡ (is-prop-is-prop fe _ i) d)


 âŠ‘-anti'-inverse : âˆ€ {U}  {X : U Ì‡} (l m : ğ“› X)
                 â†’ l â‰¡ m â†’ (l âŠ‘ m) Ã— (is-defined m â†’ is-defined l)
 âŠ‘-anti'-inverse l .l refl = âŠ‘-id l , id

 Î·-maximal : âˆ€ {U} {X : U Ì‡} (x : X) (l : ğ“› X) â†’ Î· x âŠ‘ l â†’ l âŠ‘ Î· x
 Î·-maximal x (P , i , Î³) (f , Î´) = (Î» p â†’ *) , (Î» p â†’ ap Î³ (i p (f *)) âˆ™ (Î´ *)â»Â¹)

 âŠ¥ : âˆ€ {U} {X : U Ì‡} â†’ ğ“› X
 âŠ¥ = ğŸ˜ , ğŸ˜-is-prop , unique-from-ğŸ˜

 âŠ¥-least : âˆ€ {U} {X : U Ì‡} (x : X) â†’ âŠ¥ âŠ‘ Î· x
 âŠ¥-least x = unique-from-ğŸ˜ , Î» z â†’ unique-from-ğŸ˜ z


 Î·-â‰¡-gives-âŠ‘ : âˆ€ {U} {X : U Ì‡} {x y : X} â†’ x â‰¡ y â†’ Î· x âŠ‘ Î· y
 Î·-â‰¡-gives-âŠ‘ {U} {X} {x} {y} p = id , (Î» d â†’ p)

 Î·-âŠ‘-gives-â‰¡ : âˆ€ {U} {X : U Ì‡} {x y : X} â†’ Î· x âŠ‘ Î· y â†’ x â‰¡ y
 Î·-âŠ‘-gives-â‰¡ (f , Î´) = Î´ *

 Î·-â‰¡-gives-âŠ‘-is-equiv : âˆ€ {U} â†’ funext V V â†’ funext V U
                      â†’ {X : U Ì‡} {x y : X}
                      â†’ is-equiv (Î·-â‰¡-gives-âŠ‘ {U} {X} {x} {y})
 Î·-â‰¡-gives-âŠ‘-is-equiv {U} fe fe' {X} {x} {y} = qinv-is-equiv Î·-â‰¡-gives-âŠ‘ (Î·-âŠ‘-gives-â‰¡ , Î± , Î²)
  where
   Î± : {x y : X} (p : x â‰¡ y) â†’  Î·-âŠ‘-gives-â‰¡ (Î·-â‰¡-gives-âŠ‘ p) â‰¡ p
   Î± p = refl

   Î² : {x y : X} (q : Î· x âŠ‘ Î· y) â†’ Î·-â‰¡-gives-âŠ‘ (Î·-âŠ‘-gives-â‰¡ q) â‰¡ q
   Î² (f , Î´) = to-Ã—-â‰¡ (dfunext fe (Î» x â†’ ğŸ™-is-prop x (f x)))
                      (dfunext fe' (Î» x â†’ ap Î´ (ğŸ™-is-prop * x)))


{- TODO
âŠ‘-directed-complete : âˆ€ {U} {X I : U Ì‡} (l : I â†’ ğ“› X)
                    â†’ ((i j : I) â†’ Î£ \(k : I) â†’ (l i âŠ‘ l k) Ã— (l j âŠ‘ l k))
                    â†’ Î£ \(m : ğ“› X) â†’ ((i : I) â†’ l i âŠ‘ m)
                                   Ã— ((n : ğ“› X) â†’ ((i : I) â†’ l i âŠ‘ n) â†’ m âŠ‘ n)
âŠ‘-directed-complete = {!!}
-}

\end{code}

We should also do least fixed points of continuous maps.
