Martin Escardo, 16 Dec 2016

Equivalent copy of the natural numbers with logarithmic-size elements.

We use a modification of binary notation to avoid leading zeros and
hence multiple representations of the same number.

The isomorphic copy is formally constructed from 0 iterating the
functions L(n)=2n+1 and R(n)=2n+2.

As an application, we construct a pairing function and hence an
equivalence â„• Ã— â„• â‰ƒ â„•.

\begin{code}

module BinaryNaturals where

open import SpartanMLTT renaming (_+_ to _âˆ”_)
open import UF-Equiv
open import UF-Base
open import UF-EquivalenceExamples

\end{code}

The doubling function n â†¦ 2n:

\begin{code}

double : â„• â†’ â„•
double zero    = zero
double(succ n) = succ(succ(double n))

\end{code}

The functions n â†¦ 2n+1 and n â†¦ 2n+2:

\begin{code}

L : â„• â†’ â„•
L n = succ(double n)

R : â„• â†’ â„•
R n = succ(L n)

\end{code}

Modified binary rendering of the natural numbers and its native
induction principle:

\begin{code}

data ğ”¹ : Uâ‚€ Ì‡ where
 zero : ğ”¹
 l    : ğ”¹ â†’ ğ”¹
 r    : ğ”¹ â†’ ğ”¹

\end{code}

The successor function n â†¦ n+1 on ğ”¹:

\begin{code}

Succ : ğ”¹ â†’ ğ”¹
Succ zero  = l zero
Succ(l m)  = r m
Succ(r m)  = l(Succ m)

\end{code}

Conversion between the two renderings:

\begin{code}

unary : ğ”¹ â†’ â„•
unary zero = zero
unary(l m) = L(unary m)
unary(r m) = R(unary m)

binary : â„• â†’ ğ”¹
binary zero    = zero
binary(succ n) = Succ(binary n)

\end{code}

The size of a (binary) number and the base 2 logarithm of a (unary)
number. We have lg2 ((2^n)-1) = n (TODO).

\begin{code}

size : ğ”¹ â†’ â„•
size zero  = zero
size (l m) = succ (size m)
size (r m) = succ (size m)

lg2 : â„• â†’ â„•
lg2 n = size(binary n)

\end{code}

Next we show that the functions binary and unary are mutually
inverse, after we formulate and prove some lemmas for that.

First some commutation properties:

\begin{code}

ldiagram : âˆ€ n â†’ binary(L n) â‰¡ l(binary n)
ldiagram zero    = refl
ldiagram(succ n) = ap (Î» - â†’ Succ(Succ -)) (ldiagram n)

rdiagram : âˆ€ n â†’ binary(R n) â‰¡ r(binary n)
rdiagram zero    = refl
rdiagram(succ n) = ap (Î» - â†’ Succ(Succ -)) (rdiagram n)

sdiagram : âˆ€ m â†’ unary(Succ m) â‰¡ succ(unary m)
sdiagram zero = refl
sdiagram(l m) = refl
sdiagram(r m) = ap L (sdiagram m)

\end{code}

The functions unary and binary are mutually inverse:

\begin{code}

unary-binary : âˆ€ n â†’ unary(binary n) â‰¡ n
unary-binary zero    = refl
unary-binary(succ n) = g
 where
  IH : unary(binary n) â‰¡ n
  IH = unary-binary n
  a : succ(unary(binary n)) â‰¡ succ n
  a = ap succ IH
  g : unary(Succ(binary n)) â‰¡ succ n
  g = sdiagram(binary n) âˆ™ a

binary-unary : âˆ€ m â†’ binary(unary m) â‰¡ m
binary-unary zero = refl
binary-unary(l m) = g
 where
  IH : binary(unary m) â‰¡ m
  IH = binary-unary m
  a : l(binary(unary m)) â‰¡ l m
  a = ap l IH
  g : binary(unary(l m)) â‰¡ l m
  g = ldiagram(unary m) âˆ™ a
binary-unary(r m) = g
 where
  IH : binary(unary m) â‰¡ m
  IH = binary-unary m
  a : r(binary(unary m)) â‰¡ r m
  a = ap r IH
  g : binary(unary(r m)) â‰¡ r m
  g = rdiagram(unary m) âˆ™ a

binary-equiv : ğ”¹ â‰ƒ â„•
binary-equiv = qinveq unary (binary , binary-unary , unary-binary)

\end{code}

Induction principles induced by the equivalences:

\begin{code}

â„•-induction : {A : â„• â†’ U Ì‡}
            â†’ A zero
            â†’ (âˆ€ n â†’ A n â†’ A(succ n))
            â†’ âˆ€ n â†’ A n
â„•-induction base step zero     = base
â„•-induction base step (succ n) = step n (â„•-induction base step n)

ğ”¹-induction : {B : ğ”¹ â†’ U Ì‡}
            â†’ B zero
            â†’ (âˆ€ m â†’ B m â†’ B(l m))
            â†’ (âˆ€ m â†’ B m â†’ B(r m))
            â†’ âˆ€ m â†’ B m
ğ”¹-induction base stepl stepr zero  = base
ğ”¹-induction base stepl stepr (l m) = stepl m (ğ”¹-induction base stepl stepr m)
ğ”¹-induction base stepl stepr (r m) = stepr m (ğ”¹-induction base stepl stepr m)

unary-induction-on-ğ”¹ : {B : ğ”¹ â†’ U Ì‡}
                     â†’ B zero
                     â†’ (âˆ€ n â†’ B n â†’ B(Succ n))
                     â†’ âˆ€ n â†’ B n
unary-induction-on-ğ”¹ {U} {B} base step = g
 where
  A : â„• â†’ U Ì‡
  A n = B (binary n)
  base' : A zero
  base' = base
  step' : (n : â„•) â†’ A n â†’ A (succ n)
  step' n = step (binary n)
  a : âˆ€ n â†’ A n
  a = â„•-induction base' step'
  b : âˆ€ m â†’ B(binary(unary m))
  b m = a (unary m)
  g : âˆ€ m â†’ B m
  g m = transport B (binary-unary m) (b m)

binary-induction-on-â„• : {A : â„• â†’ U Ì‡}
                     â†’ A zero
                     â†’ (âˆ€ n â†’ A n â†’ A(L n))
                     â†’ (âˆ€ n â†’ A n â†’ A(R n))
                     â†’ âˆ€ n â†’ A n
binary-induction-on-â„• {U} {A} base stepl stepr = g
 where
  B : ğ”¹ â†’ U Ì‡
  B m = A (unary m)
  base' : B zero
  base' = base
  stepl' : (m : ğ”¹) â†’ B m â†’ B (l m)
  stepl' m = stepl (unary m)
  stepr' : (m : ğ”¹) â†’ B m â†’ B (r m)
  stepr' m = stepr (unary m)
  b : âˆ€ m â†’ B m
  b = ğ”¹-induction base' stepl' stepr'
  a : âˆ€ n â†’ A(unary(binary n))
  a n = b (binary n)
  g : âˆ€ n â†’ A n
  g n = transport A (unary-binary n) (a n)

\end{code}

Some operations performed directly in modified binary, for the sake of
efficiency, with their correctness verified.

The doubling function n â†¦ 2n:

\begin{code}

Double : ğ”¹ â†’ ğ”¹
Double zero = zero
Double(l m) = r(Double m)
Double(r m) = Succ(Succ(r(Double m)))

Double-lemma : âˆ€ m â†’ Succ(Succ(Double m)) â‰¡ Double(Succ m)
Double-lemma zero = refl
Double-lemma(l m) = refl
Double-lemma(r m) = ap r (Double-lemma m)

ddiagram : âˆ€ n â†’ binary(double n) â‰¡ Double(binary n)
ddiagram zero    = refl
ddiagram(succ n) = g
 where
  IH : binary(double n) â‰¡ Double(binary n)
  IH = ddiagram n
  a : Succ(Succ(binary(double n))) â‰¡ Succ(Succ(Double(binary n)))
  a = ap (Î» - â†’ Succ(Succ -)) IH
  g : binary(double(succ n)) â‰¡ Double(binary(succ n))
  g = a âˆ™ Double-lemma(binary n)

\end{code}

Now addition, with a faster version in binary:

\begin{code}

_+_ : â„• â†’ â„• â†’ â„•
x + zero = x
x + succ y = succ(x + y)

_+â™­_ : ğ”¹ â†’ ğ”¹ â†’ ğ”¹
x    +â™­ zero = x
zero +â™­ l y  = l y
l x  +â™­ l y  = r(x +â™­ y)
r x  +â™­ l y  = l(Succ(x +â™­ y))
zero +â™­ r y  = r y
l x  +â™­ r y  = l(Succ(x +â™­ y))
r x  +â™­ r y  = r(Succ(x +â™­ y))

+â™­-lemma : âˆ€ m n â†’ Succ(m +â™­ n) â‰¡ m +â™­ Succ n
+â™­-lemma zero   zero = refl
+â™­-lemma (l m)  zero = refl
+â™­-lemma (r m)  zero = refl
+â™­-lemma zero  (l n) = refl
+â™­-lemma (l m) (l n) = refl
+â™­-lemma (r m) (l n) = refl
+â™­-lemma zero  (r n) = refl
+â™­-lemma (l m) (r n) = ap r (+â™­-lemma m n)
+â™­-lemma (r m) (r n) = ap (Î» - â†’ l(Succ -)) (+â™­-lemma m n)

+diagram : âˆ€ m n â†’ binary(m + n) â‰¡ binary m +â™­ binary n
+diagram m zero     = refl
+diagram m (succ n) = g
 where
  IH : binary(m + n) â‰¡ binary m +â™­ binary n
  IH = +diagram m n
  a : Succ(binary(m + n)) â‰¡ Succ(binary m +â™­ binary n)
  a = ap Succ IH
  g : Succ(binary(m + n)) â‰¡ binary m +â™­ Succ(binary n)
  g = a âˆ™ +â™­-lemma (binary m) (binary n)

\end{code}

Even faster binary addition (linear time).  We define the following
operations with the following specifications:

\begin{code}

_+â‚€_ _+â‚_ _+â‚‚_ : ğ”¹ â†’ ğ”¹ â†’ ğ”¹
Succâ‚‚          : ğ”¹ â†’ ğ”¹

+â‚€-spec    : âˆ€ x y â†’ x +â‚€ y â‰¡ x +â™­ y
+â‚-spec    : âˆ€ x y â†’ x +â‚ y â‰¡ Succ(x +â™­ y)
+â‚‚-spec    : âˆ€ x y â†’ x +â‚‚ y â‰¡ Succ(Succ(x +â™­ y))
Succâ‚‚-spec : âˆ€ x â†’  Succâ‚‚ x â‰¡ Succ(Succ x)

\end{code}

Definitions:

\begin{code}

x    +â‚€ zero = x
zero +â‚€ l y  = l y
l x  +â‚€ l y  = r(x +â‚€ y)
r x  +â‚€ l y  = l(x +â‚ y)
zero +â‚€ r y  = r y
l x  +â‚€ r y  = l(x +â‚ y)
r x  +â‚€ r y  = r(x +â‚ y)

x    +â‚ zero = Succ x
zero +â‚ l y  = r y
l x  +â‚ l y  = l(x +â‚ y)
r x  +â‚ l y  = r(x +â‚ y)
zero +â‚ r y  = l(Succ y)
l x  +â‚ r y  = r(x +â‚ y)
r x  +â‚ r y  = l(x +â‚‚ y)

x    +â‚‚ zero = Succâ‚‚ x
zero +â‚‚ l y  = l(Succ y)
l x  +â‚‚ l y  = r(x +â‚ y)
r x  +â‚‚ l y  = l(x +â‚‚ y)
zero +â‚‚ r y  = r(Succ y)
l x  +â‚‚ r y  = l(x +â‚‚ y)
r x  +â‚‚ r y  = r(x +â‚‚ y)

Succâ‚‚ zero  = r zero
Succâ‚‚ (l x) = l(Succ x)
Succâ‚‚ (r x) = r(Succ x)

\end{code}

Correctness proof:

\begin{code}

+â‚€-spec x zero      = refl
+â‚€-spec zero (l y)  = refl
+â‚€-spec (l x) (l y) = ap r (+â‚€-spec x y)
+â‚€-spec (r x) (l y) = ap l (+â‚-spec x y)
+â‚€-spec zero (r y)  = refl
+â‚€-spec (l x) (r y) = ap l (+â‚-spec x y)
+â‚€-spec (r x) (r y) = ap r (+â‚-spec x y)

+â‚-spec x zero      = refl
+â‚-spec zero (l y)  = refl
+â‚-spec (l x) (l y) = ap l (+â‚-spec x y)
+â‚-spec (r x) (l y) = ap r (+â‚-spec x y)
+â‚-spec zero (r y)  = refl
+â‚-spec (l x) (r y) = ap r (+â‚-spec x y)
+â‚-spec (r x) (r y) = ap l (+â‚‚-spec x y)

+â‚‚-spec x zero      = Succâ‚‚-spec x
+â‚‚-spec zero (l y)  = refl
+â‚‚-spec (l x) (l y) = ap r (+â‚-spec x y)
+â‚‚-spec (r x) (l y) = ap l (+â‚‚-spec x y)
+â‚‚-spec zero (r y)  = refl
+â‚‚-spec (l x) (r y) = ap l (+â‚‚-spec x y)
+â‚‚-spec (r x) (r y) = ap r (+â‚‚-spec x y)

Succâ‚‚-spec zero  = refl
Succâ‚‚-spec (l x) = refl
Succâ‚‚-spec (r x) = refl

\end{code}

Now multiplication.

\begin{code}

_â‹†_ : â„• â†’ â„• â†’ â„•
m â‹† zero = zero
m â‹† succ n = m â‹† n + m -- m(n+1) = mn + m

_â‹†â™­_ : ğ”¹ â†’ ğ”¹ â†’ ğ”¹
m â‹†â™­ zero = zero
m â‹†â™­ l n = Double(m â‹†â™­ n) +â™­ m
m â‹†â™­ r n = Double(m â‹†â™­ n +â™­ m)

_â‹†â‚_ : ğ”¹ â†’ ğ”¹ â†’ ğ”¹
m    â‹†â‚ zero = zero
zero â‹†â‚ l n  = zero
l m  â‹†â‚ l n  = l(Double(m â‹†â‚ n) +â‚€ m +â‚€ n) -- (2m+1)(2n+1) = 4mn + 2m + 2n + 1 = 2(2mn+m+n)+1
r m  â‹†â‚ l n  = r(Double(m â‹†â‚ n +â‚€ n) +â‚€ m) -- (2m+2)(2n+1) = 4mn + 2m + 4n + 2 = 2(2(mn+n)+m)+2
zero â‹†â‚ r n  = zero
l m  â‹†â‚ r n  = r(Double(m â‹†â‚ n +â‚€ m) +â‚€ n)
r m  â‹†â‚ r n  = Double(Double(m â‹†â‚ n +â‚€ (m +â‚ n))) -- (2m+2)(2n+2) = 4mn + 4m + 4n + 4 = 4(mn + m + n + 1)

\end{code}

We need a proof for multiplication. Here is feeble evidence for the
moment, in the form of an experiment:

\begin{code}

test : unary(binary 172 â‹†â‚ binary 133) â‰¡ 172 â‹† 133
test = refl

\end{code}

Faster double, by specializing addition x â†¦ x + x:

\begin{code}

doubleâ‚€ doubleâ‚ doubleâ‚‚ : ğ”¹ â†’ ğ”¹
doubleâ‚€-spec : âˆ€ x â†’ doubleâ‚€ x â‰¡ x +â‚€ x
doubleâ‚-spec : âˆ€ x â†’ doubleâ‚ x â‰¡ x +â‚ x
doubleâ‚‚-spec : âˆ€ x â†’ doubleâ‚‚ x â‰¡ x +â‚‚ x

\end{code}

Can be understood as transducer with three states:

\begin{code}

doubleâ‚€ zero = zero
doubleâ‚€(l x) = r(doubleâ‚€ x)
doubleâ‚€(r x) = r(doubleâ‚ x)

doubleâ‚ zero = l zero
doubleâ‚(l x) = l(doubleâ‚ x)
doubleâ‚(r x) = l(doubleâ‚‚ x)

doubleâ‚‚ zero = r zero
doubleâ‚‚(l x) = r(doubleâ‚ x)
doubleâ‚‚(r x) = r(doubleâ‚‚ x)

doubleâ‚€-spec zero = refl
doubleâ‚€-spec (l x) = ap r (doubleâ‚€-spec x)
doubleâ‚€-spec (r x) = ap r (doubleâ‚-spec x)

doubleâ‚-spec zero = refl
doubleâ‚-spec (l x) = ap l (doubleâ‚-spec x)
doubleâ‚-spec (r x) = ap l (doubleâ‚‚-spec x)

doubleâ‚‚-spec zero = refl
doubleâ‚‚-spec (l x) = ap r (doubleâ‚-spec x)
doubleâ‚‚-spec (r x) = ap r (doubleâ‚‚-spec x)

\end{code}

We get a pairing function as follows, using a rather minimal amount of
arithmetic (14th July 2018):

We use binary notation to simplify the definition. An alternative
would be to work with the usual unary notation, using binary
induction. However, this would prevent us from using pattern matching,
which gives a more intuitive definition.

\begin{code}

first' : ğ”¹ â†’ â„•
first' zero = zero
first' (l b) = succ (first' b)
first' (r b) = zero

second' : ğ”¹ â†’ ğ”¹
second' zero = zero
second' (l b) = second' b
second' (r b) = Succ b

pair' : â„• â†’ â„• â†’ ğ”¹
pair' zero zero = zero
pair' (succ n) zero = l(pair' n zero)
pair' zero (succ k) = r (binary k)
pair' (succ n) (succ k) = l (pair' n (succ k))

pair'-claim : (n k : â„•) â†’ pair' (succ n) k â‰¡ l(pair' n k)
pair'-claim n zero = refl
pair'-claim n (succ k) = refl

first'-lemma : (n k : â„•) â†’ first'(pair' n k) â‰¡ n
first'-lemma zero zero = refl
first'-lemma zero (succ k) = refl
first'-lemma (succ n) zero = ap succ (first'-lemma n zero)
first'-lemma (succ n) (succ k) = ap succ (first'-lemma n (succ k))

second'-lemma : (n k : â„•) â†’ second'(pair' n k) â‰¡ binary k
second'-lemma zero zero = refl
second'-lemma zero (succ k) = refl
second'-lemma (succ n) zero = second'-lemma n zero
second'-lemma (succ n) (succ k) = second'-lemma n (succ k)

pair'-lemma : (b : ğ”¹) â†’ pair' (first' b) (unary(second' b)) â‰¡ b
pair'-lemma zero = refl
pair'-lemma (l b) = Î³
 where
  IH : pair' (first' b) (unary (second' b)) â‰¡ b
  IH = pair'-lemma b
  c : pair' (succ (first' b)) (unary (second' b)) â‰¡ l (pair' (first' b) (unary (second' b)))
  c = pair'-claim (first' b) (unary (second' b))
  Î³ : pair' (succ (first' b)) (unary (second' b)) â‰¡ l b
  Î³ = c âˆ™ ap l IH
pair'-lemma (r b) = Î³
 where
  p : r (binary (unary b)) â‰¡ r b
  p = ap r (binary-unary b)
  q : pair' zero (succ(unary b)) â‰¡ r b
  q = p
  Î³ : pair' zero (unary (Succ b)) â‰¡ r b
  Î³ = back-transport (Î» - â†’ pair' zero - â‰¡ r b) (sdiagram b) q

pair : â„• Ã— â„• â†’ â„•
pair (n , k) = unary(pair' n k)

first second : â„• â†’ â„•
first = first' âˆ˜ binary
second = unary âˆ˜ second' âˆ˜ binary

first-pair : (n k : â„•) â†’ first (pair (n , k)) â‰¡ n
first-pair n k = back-transport
                  (Î» - â†’ first' - â‰¡ n)
                  (binary-unary (pair' n k))
                  (first'-lemma n k)

second-pair : (n k : â„•) â†’ second (pair (n , k)) â‰¡ k
second-pair n k = back-transport
                   (Î» - â†’ unary (second' -) â‰¡ k)
                   (binary-unary (pair' n k))
                   (ap unary (second'-lemma n k) âˆ™ unary-binary k)

riap : â„• â†’ â„• Ã— â„•
riap m = (first m , second m)

pair-riap : (m : â„•) â†’ pair(riap m) â‰¡ m
pair-riap m = ap unary (pair'-lemma (binary m)) âˆ™ unary-binary m

riap-pair : (z : â„• Ã— â„•) â†’ riap(pair z) â‰¡ z
riap-pair (n , k) = to-Ã—-â‰¡ (first-pair n k) (second-pair n k)

pairing : â„• Ã— â„• â‰ƒ â„•
pairing = qinveq pair  (riap , riap-pair , pair-riap)

\end{code}

We now show that â„• + â„• â‰ƒ â„• (July 2018).

\begin{code}

â„•-plus-ğŸ™ : â„• âˆ” ğŸ™ â‰ƒ â„•
â„•-plus-ğŸ™ = qinveq f (g , Îµ , Î·)
 where
  f : â„• âˆ” ğŸ™ {Uâ‚€} â†’ â„•
  f (inl n) = succ n
  f (inr *) = zero
  g : â„• â†’ â„• âˆ” ğŸ™
  g zero = inr *
  g (succ n) = inl n
  Î· : (n : â„•) â†’ f (g n) â‰¡ n
  Î· zero = refl
  Î· (succ n) = refl
  Îµ : (z : â„• âˆ” ğŸ™) â†’ g (f z) â‰¡ z
  Îµ (inl n) = refl
  Îµ (inr *) = refl

two-ğ”¹-plus-ğŸ™ : ğ”¹ âˆ” ğ”¹ âˆ” ğŸ™ â‰ƒ ğ”¹
two-ğ”¹-plus-ğŸ™ = qinveq f (g , Îµ , Î·)
 where
  f : ğ”¹ âˆ” ğ”¹ âˆ” ğŸ™ {Uâ‚€} â†’ ğ”¹
  f (inl b) = l b
  f (inr (inl b)) = r b
  f (inr (inr *)) = zero
  g : ğ”¹ â†’ ğ”¹ âˆ” ğ”¹ âˆ” ğŸ™
  g zero = inr (inr *)
  g (l b) = inl b
  g (r b) = inr (inl b)
  Î· : (b : ğ”¹) â†’ f (g b) â‰¡ b
  Î· zero = refl
  Î· (l b) = refl
  Î· (r b) = refl
  Îµ : (z : ğ”¹ âˆ” ğ”¹ âˆ” ğŸ™) â†’ g (f z) â‰¡ z
  Îµ (inl b) = refl
  Îµ (inr (inl b)) = refl
  Îµ (inr (inr *)) = refl

two-â„•-plus-ğŸ™ : â„• âˆ” â„• âˆ” ğŸ™ â‰ƒ â„•
two-â„•-plus-ğŸ™ =
    â„• âˆ” (â„• âˆ” ğŸ™)    â‰ƒâŸ¨ +-cong (â‰ƒ-sym binary-equiv) (Ap+ ğŸ™ (â‰ƒ-sym binary-equiv)) âŸ©
    ğ”¹ âˆ” (ğ”¹ âˆ” ğŸ™)  â‰ƒâŸ¨ two-ğ”¹-plus-ğŸ™ âŸ©
    ğ”¹             â‰ƒâŸ¨ binary-equiv âŸ©
    â„• â– 

two-â„• : â„• âˆ” â„• â‰ƒ â„•
two-â„• =
   â„• âˆ” â„•        â‰ƒâŸ¨ Ap+ â„• (â‰ƒ-sym â„•-plus-ğŸ™) âŸ©
   (â„• âˆ” ğŸ™) âˆ” â„•  â‰ƒâŸ¨ +comm âŸ©
   â„• âˆ” â„• âˆ” ğŸ™    â‰ƒâŸ¨ two-â„•-plus-ğŸ™ âŸ©
   â„• â– 

\end{code}

The following examples show that these equivalences compute:

\begin{code}

module examples where

 example-riap : riap 17 â‰¡ (1 , 4)
 example-riap = refl

 example-pair : pair (5 , 6) â‰¡ 415
 example-pair = refl

 ex17 : eqtofun (â‰ƒ-sym two-â„•) 17 â‰¡ inr 8
 ex17 = refl

 ex18 : eqtofun (â‰ƒ-sym two-â„•) 18 â‰¡ inl 9
 ex18 = refl

\end{code}

And finally the fixities assumed above:

\begin{code}

infixl 6 _+_
infixl 7 _â‹†_
infixl 6 _+â™­_
infixl 7 _â‹†â™­_
infixl 6 _+â‚_
infixl 6 _+â‚€_
infixl 7 _â‹†â‚_

\end{code}
