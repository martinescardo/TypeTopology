Martin Escardo, 20th August 2018

We consider type and subtype classifiers, and discuss an obvious
generalization which is left undone for the moment.

 * (Î£ \(X : ğ“¤ Ì‡) â†’ X â†’ Y) â‰ƒ (Y â†’ ğ“¤ Ì‡)
 * (Î£ \(X : ğ“¤ Ì‡) â†’ X â†ª Y) â‰ƒ (Y â†’ Î© ğ“¤)

\begin{code}

{-# OPTIONS --without-K --exact-split #-}

module UF-Classifiers where

open import SpartanMLTT
open import UF-Subsingletons
open import UF-Equiv
open import UF-EquivalenceExamples
open import UF-Equiv-FunExt
open import UF-Base
open import UF-Univalence
open import UF-UA-FunExt
open import UF-FunExt
open import UF-Embedding

module type-classifier
        {ğ“¤ : Universe}
        (fe' : funext ğ“¤ (ğ“¤ âº))
        (ua : is-univalent ğ“¤)
        (Y : ğ“¤ Ì‡)
       where

 Ï‡ : (Î£ \(X : ğ“¤ Ì‡) â†’ X â†’ Y)  â†’ (Y â†’ ğ“¤ Ì‡)
 Ï‡ (X , f) = fiber f

 T : (Y â†’ ğ“¤ Ì‡) â†’ Î£ \(X : ğ“¤ Ì‡) â†’ X â†’ Y
 T A = Î£ A , prâ‚

 Ï‡T : (A : Y â†’ ğ“¤ Ì‡) â†’ Ï‡(T A) â‰¡ A
 Ï‡T A = dfunext fe' Î³
  where
   f : âˆ€ y â†’ (Î£ \(Ïƒ : Î£ A) â†’ prâ‚ Ïƒ â‰¡ y) â†’ A y
   f y ((.y , a) , refl) = a
   g : âˆ€ y â†’ A y â†’ Î£ \(Ïƒ : Î£ A) â†’ prâ‚ Ïƒ â‰¡ y
   g y a = (y , a) , refl
   fg : âˆ€ y a â†’ f y (g y a) â‰¡ a
   fg y a = refl
   gf : âˆ€ y Ïƒ â†’ g y (f y Ïƒ) â‰¡ Ïƒ
   gf y ((.y , a) , refl) = refl
   Î³ : âˆ€ y â†’ (Î£ \(Ïƒ : Î£ A) â†’ prâ‚ Ïƒ â‰¡ y) â‰¡ A y
   Î³ y = eqtoid ua _ _ (f y , ((g y , fg y) , (g y , gf y)))

 transport-map : {X X' Y : ğ“¤ Ì‡} (e : X â‰ƒ X') (g : X â†’ Y)
             â†’ transport (Î» - â†’ - â†’ Y) (eqtoid ua X X' e) g
             â‰¡ g âˆ˜ eqtofun (â‰ƒ-sym e)

 transport-map {X} {X'} {Y} e g = Ï„ (eqtoid ua X X' e) refl
  where
   Ï„ : (p : X â‰¡ X')
     â†’ p â‰¡ eqtoid ua X X' e
     â†’ transport (Î» - â†’ - â†’ Y) p g â‰¡ g âˆ˜ eqtofun (â‰ƒ-sym e)
   Ï„ refl q = ap (Î» h â†’ g âˆ˜ h) s
    where
     r : idtoeq X X refl â‰¡ e
     r = ap (idtoeq X X) q âˆ™ idtoeq-eqtoid ua X X e
     s : id â‰¡ eqtofun (â‰ƒ-sym e)
     s = ap (Î» - â†’ eqtofun (â‰ƒ-sym -)) r

 TÏ‡ : (Ïƒ : Î£ \(X : ğ“¤ Ì‡) â†’ X â†’ Y) â†’ T(Ï‡ Ïƒ) â‰¡ Ïƒ
 TÏ‡ (X , f) = to-Î£-â‰¡ (eqtoid ua _ _ (graph-domain-equiv f) ,
                       transport-map (graph-domain-equiv f) prâ‚)

 Ï‡-is-equivalence : is-equiv Ï‡
 Ï‡-is-equivalence = (T , Ï‡T) , (T , TÏ‡)

 classification-equivalence : (Î£ \(X : ğ“¤ Ì‡) â†’ X â†’ Y) â‰ƒ (Y â†’ ğ“¤ Ì‡)
 classification-equivalence = Ï‡ , Ï‡-is-equivalence


module subtype-classifier
        {ğ“¤ : Universe}
        (fe' : funext ğ“¤ (ğ“¤ âº))
        (ua : is-univalent ğ“¤)
        (Y : ğ“¤ Ì‡)
       where

 fe : funext ğ“¤ ğ“¤
 fe = funext-from-univalence ua

 Ï‡ : (Î£ \(X : ğ“¤ Ì‡) â†’ X â†ª Y)  â†’ (Y â†’ Î© ğ“¤)
 Ï‡ (X , f , i) y = fiber f y , i y

 T : (Y â†’ Î© ğ“¤) â†’ Î£ \(X : ğ“¤ Ì‡) â†’ X â†ª Y
 T P = (Î£ \(y : Y) â†’ P y holds) , prâ‚ , prâ‚-embedding (Î» y â†’ holds-is-prop (P y))

 Ï‡T : (P : Y â†’ Î© ğ“¤) â†’ Ï‡(T P) â‰¡ P
 Ï‡T P = dfunext fe' Î³
  where
   f : âˆ€ y â†’ Ï‡ (T P) y holds â†’ P y holds
   f y ((.y , h) , refl) = h
   g : âˆ€ y â†’ P y holds â†’ Ï‡ (T P) y holds
   g y h = (y , h) , refl
   Î³ : (y : Y) â†’ Ï‡ (T P) y â‰¡ P y
   Î³ y = PropExt-from-univalence ua (f y) (g y)

 transport-embedding : {X X' Y : ğ“¤ Ì‡} (e : X â‰ƒ X') (g : X â†’ Y) (i : is-embedding g)
                    â†’ transport (Î» - â†’ - â†ª Y) (eqtoid ua X X' e) (g , i)
                    â‰¡ g âˆ˜ eqtofun (â‰ƒ-sym e) , comp-embedding
                                                 (equivs-are-embeddings (eqtofun (â‰ƒ-sym e))
                                                                        (eqtofun-is-an-equiv (â‰ƒ-sym e))) i
 transport-embedding {X} {X'} {Y} e g i = Ï„ (eqtoid ua X X' e) refl
  where
   Ï„ : (p : X â‰¡ X')
     â†’ p â‰¡ eqtoid ua X X' e
     â†’ transport (Î» - â†’ - â†ª Y) p (g , i)
     â‰¡ g âˆ˜ eqtofun (â‰ƒ-sym e) , comp-embedding
                                  (equivs-are-embeddings (eqtofun (â‰ƒ-sym e))
                                                         (eqtofun-is-an-equiv (â‰ƒ-sym e))) i
   Ï„ refl q = to-Î£-â‰¡ (ap (Î» h â†’ g âˆ˜ h) s ,
                      being-embedding-is-a-prop fe fe (g âˆ˜ eqtofun (â‰ƒ-sym e)) _ _)
    where
     r : idtoeq X X refl â‰¡ e
     r = ap (idtoeq X X) q âˆ™ idtoeq-eqtoid ua X X e
     s : id â‰¡ eqtofun (â‰ƒ-sym e)
     s = ap (Î» - â†’ eqtofun (â‰ƒ-sym -)) r

 TÏ‡ : (Ïƒ : Î£ \(X : ğ“¤ Ì‡) â†’ X â†ª Y) â†’ T(Ï‡ Ïƒ) â‰¡ Ïƒ
 TÏ‡ (X , f , i) = to-Î£-â‰¡ (eqtoid ua _ _ (graph-domain-equiv f) ,
                          (transport-embedding (graph-domain-equiv f) prâ‚ (prâ‚-embedding i)
                         âˆ™ to-Î£-â‰¡' (being-embedding-is-a-prop fe fe f _ _)))

 Ï‡-is-equivalence : is-equiv Ï‡
 Ï‡-is-equivalence = (T , Ï‡T) , (T , TÏ‡)

 classification-equivalence : (Î£ \(X : ğ“¤ Ì‡) â†’ X â†ª Y) â‰ƒ (Y â†’ Î© ğ“¤)
 classification-equivalence = Ï‡ , Ï‡-is-equivalence

\end{code}

TODO. Consider a property "green" of types, and call a map green if
its fibers are all green. Then the maps of Y into green types should
correspond to the green maps X â†’ Y. This generalizes the above
situation. In particular, the case green = contractible is of interest
and describes a previously known situation. Another example is that
surjections X â†’ Y are in bijection with families
Y â†’ Î£ (Z : ğ“¤ Ì‡) â†’ âˆ¥ Z âˆ¥), that is, families of inhabited types. It is
not necessary that "green" is proposition valued. It can be universe
valued in general. And then of course retractions X â†’ Y are in
bijections with families of pointed types.
