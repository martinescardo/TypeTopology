Martin Escardo 2012.

We investigate coinduction and corecursion on â„•âˆ, the generic
convergent sequence.

We show that the set â„•âˆ satisfies the following universal property for
a suitable coalgebra PRED : â„•âˆ â†’ ğŸ™ + â„•âˆ, where ğŸ™ is the singleton type
with an element *.

For every type X and every Îº : X â†’ ğŸ™ + X there is a unique h : X â†’ â„•âˆ
such that

                        Îº
             X ------------------> ğŸ™ + X
             |                       |
             |                       |
          h  |                       | ğŸ™ + h
             |                       |
             |                       |
             v                       v
             â„•âˆ -----------------> ğŸ™ + â„•âˆ
                       PRED

The maps Îº and PRED are called coalgebras for the functor ğŸ™ + (-),
and the above diagram says that h is a coalgebra morphism from p to
PRED.

In equational form, this is

             PRED âˆ˜ h â‰¡ (ğŸ™ + h) âˆ˜ Îº,

which can be considered as a corecursive definition of h.  The map
PRED (a sort of predecessor function) is an isomorphism with
inverse SUCC (a sort of successor function). This follows from
Lambek's Lemma once the above universal property is established, but
we actually need to know this first in order to prove the universal
property.

             SUCC : ğŸ™ + â„•âˆ â†’ â„•âˆ
             SUCC (inâ‚€ *) = Zero
             SUCC (inâ‚ u) = Succ u

Using this fact, the above corecursive definition of h is equivalent
to:

             h â‰¡ SUCC âˆ˜ (ğŸ™ + h) âˆ˜ Îº

or

             h(x) â‰¡ SUCC((ğŸ™ + h)(Îº x)).

Now Îº x is either of the form inâ‚€ * or inâ‚ x' for a unique x' : X, and
hence the above equation amounts to

             h(x) â‰¡ Zero,           if Îº x â‰¡ inâ‚€ *,
             h(x) â‰¡ Succ (h x'),    if Îº x â‰¡ inâ‚ x',

once we know the definition of ğŸ™ + h. This shows more clearly how the
diagram can be considered as a (co)recursive definition of h, and
indicates how h may be constructed.

In order to show that any two functions that make the above diagram
commute are equal, that is, that the above two conditional equations
uniquely determine h, we develop a coinduction principle based on
bisimulations. This gives a technique for establishing equalities on
â„•âˆ.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import UF-FunExt

module CoNaturals (fe : FunExt) where

open import SpartanMLTT
open import GenericConvergentSequence
open import CanonicalMapNotation

open import Two-Properties
open import Plus-Properties

private
 feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
 feâ‚€ = fe ğ“¤â‚€ ğ“¤â‚€

Zero' : ğŸ™ + â„•âˆ
Zero' = inl {ğ“¤â‚€} {ğ“¤â‚€} â‹†

Pred' : â„•âˆ â†’ ğŸ™ + â„•âˆ
Pred' u = inr {ğ“¤â‚€} {ğ“¤â‚€} (Pred u)

PRED : â„•âˆ â†’ ğŸ™ + â„•âˆ
PRED u = ğŸš-Cases (positivity u) Zero' (Pred' u)

PRED-Zero : PRED Zero â‰¡ Zero'
PRED-Zero = refl

PRED-Succ : (u : â„•âˆ) â†’ PRED(Succ u) â‰¡ inr u
PRED-Succ u = ap inr Pred-Succ

SUCC : ğŸ™ {ğ“¤â‚€} + â„•âˆ â†’ â„•âˆ
SUCC(inl â‹†) = Zero
SUCC(inr u) = Succ u

PRED-SUCC : {y : ğŸ™ + â„•âˆ} â†’ PRED(SUCC y) â‰¡ y
PRED-SUCC{inl â‹†} = refl
PRED-SUCC{inr u} = refl

SUCC-lc : {y z : ğŸ™ + â„•âˆ} â†’ SUCC y â‰¡ SUCC z â†’ y â‰¡ z
SUCC-lc {y} {z} r = y             â‰¡âŸ¨ PRED-SUCC â»Â¹ âŸ©
                    PRED (SUCC y) â‰¡âŸ¨ ap PRED r âŸ©
                    PRED (SUCC z) â‰¡âŸ¨ PRED-SUCC âŸ©
                    z             âˆ

SUCC-PRED : {u : â„•âˆ} â†’ SUCC(PRED u) â‰¡ u
SUCC-PRED {u} = ğŸš-equality-cases lâ‚€ lâ‚
 where
  lâ‚€ : positivity u â‰¡ â‚€ â†’ SUCC(PRED u) â‰¡ u
  lâ‚€ r = SUCC(PRED u) â‰¡âŸ¨ ap SUCC câ‚€ âŸ©
         Zero         â‰¡âŸ¨ (is-Zero-equal-Zero feâ‚€ r)â»Â¹ âŸ©
         u            âˆ
    where
     câ‚€ : PRED u â‰¡ Zero'
     câ‚€ = ap (ğŸš-cases Zero' (Pred' u)) r

  lâ‚ : positivity u â‰¡ â‚ â†’ SUCC(PRED u) â‰¡ u
  lâ‚ r = SUCC (PRED u) â‰¡âŸ¨ ap SUCC câ‚€ âŸ©
         Succ (Pred u) â‰¡âŸ¨ (not-Zero-is-Succ feâ‚€ câ‚)â»Â¹ âŸ©
         u             âˆ

   where
     câ‚€ : PRED u â‰¡ Pred' u
     câ‚€ = ap (ğŸš-cases Zero' (Pred' u)) r
     câ‚ : u â‰¢ Zero
     câ‚ s = equal-â‚€-different-from-â‚(ap positivity s) r

PRED-lc : {u v : â„•âˆ} â†’ PRED u â‰¡ PRED v â†’ u â‰¡ v
PRED-lc {u} {v} r = u             â‰¡âŸ¨ SUCC-PRED â»Â¹ âŸ©
                    SUCC (PRED u) â‰¡âŸ¨ ap SUCC r âŸ©
                    SUCC (PRED v) â‰¡âŸ¨ SUCC-PRED âŸ©
                    v             âˆ

ğŸ™+ : {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } â†’ (X â†’ Y) â†’ ğŸ™ + X â†’ ğŸ™ + Y
ğŸ™+ f (inl s) = inl {ğ“¤â‚€} s
ğŸ™+ f (inr x) = inr(f x)

ğŸ™+id-is-id : {X : ğ“¤ Ì‡ } â†’ ğŸ™+ id âˆ¼ id {ğ“¤} {ğŸ™ + X}
ğŸ™+id-is-id {ğ“¤} {X} (inl â‹†) = refl
ğŸ™+id-is-id {ğ“¤} {X} (inr x) = refl

is-homomorphism : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸ™ + X) â†’ (X â†’ â„•âˆ) â†’ ğ“¤ Ì‡
is-homomorphism c h = (PRED âˆ˜ h â‰¡ (ğŸ™+ h) âˆ˜ c)

id-homomorphism : is-homomorphism PRED id
id-homomorphism = dfunext feâ‚€ (Î» u â†’ (ğŸ™+id-is-id (PRED u))â»Â¹)

coalg-mophismâ†’ : {X : ğ“¤ Ì‡ } (Îº : X â†’ ğŸ™ + X) (h : X â†’ â„•âˆ)
               â†’ is-homomorphism Îº h
               â†’ h â‰¡ SUCC âˆ˜ (ğŸ™+ h) âˆ˜ Îº
coalg-mophismâ†’ {ğ“¤} Îº h a = dfunext (fe ğ“¤ ğ“¤â‚€)
                             (Î» x â†’ h x               â‰¡âŸ¨ SUCC-PRED â»Â¹ âŸ©
                                    SUCC (PRED (h x)) â‰¡âŸ¨ ap (Î» - â†’ SUCC(- x)) a âŸ©
                                    SUCC (ğŸ™+ h (Îº x)) âˆ)

coalg-mophismâ† : {X : ğ“¤ Ì‡ } (Îº : X â†’ ğŸ™ + X) (h : X â†’ â„•âˆ)
               â†’ h â‰¡ SUCC âˆ˜ (ğŸ™+ h) âˆ˜ Îº
               â†’ is-homomorphism Îº h
coalg-mophismâ† {ğ“¤} Îº h b = dfunext (fe ğ“¤ ğ“¤â‚€)
                            (Î» x â†’ PRED (h x)                 â‰¡âŸ¨ ap (Î» - â†’ PRED(- x)) b âŸ©
                                   PRED ((SUCC âˆ˜ ğŸ™+ h âˆ˜ Îº) x) â‰¡âŸ¨ PRED-SUCC âŸ©
                                   ğŸ™+ h (Îº x)                 âˆ)

homomorphism-existence : {X : ğ“¤ Ì‡ } (Îº : X â†’ ğŸ™ + X)
                       â†’ Î£ h ê‰ (X â†’ â„•âˆ), is-homomorphism Îº h
homomorphism-existence {ğ“¤} {X} Îº = h , dfunext (fe ğ“¤ ğ“¤â‚€) h-spec
 where
  q : ğŸ™ + X â†’ ğŸ™ + X
  q(inl s) = inl s
  q(inr x) = Îº x

  Q : â„• â†’ ğŸ™ + X â†’ ğŸ™ + X
  Q 0 z = z
  Q(succ n) z = q(Q n z)

  E : ğŸ™ + X â†’ ğŸš
  E(inl s) = â‚€
  E(inr x) = â‚

  hl : (z : ğŸ™ + X) â†’ E(q z) â‰¡ â‚ â†’ E z â‰¡ â‚
  hl (inl s) r = r
  hl (inr x) r = refl

  h : X â†’ â„•âˆ
  h x = (Î» i â†’ E(Q(succ i) (inr x))) ,
        (Î» i â†’ â‰¤â‚‚-criterion (hl(Q(succ i) (inr x))))

  h-spec : (x : X) â†’ PRED(h x) â‰¡ (ğŸ™+ h)(Îº x)
  h-spec x = equality-cases (Îº x) lâ‚€ lâ‚
   where
    lâ‚€ : (s : ğŸ™) â†’ Îº x â‰¡ inl s â†’ PRED(h x) â‰¡ (ğŸ™+ h)(Îº x)
    lâ‚€ â‹† r = PRED (h x) â‰¡âŸ¨ ap PRED c âŸ©
             PRED Zero  â‰¡âŸ¨ PRED-Zero âŸ©
             Zero'      â‰¡âŸ¨ (ap (ğŸ™+ h) r)â»Â¹ âŸ©
             ğŸ™+ h (Îº x) âˆ
     where
      c : h x â‰¡ Zero
      c = is-Zero-equal-Zero feâ‚€ (ap E r)


    lâ‚ : (x' : X) â†’ Îº x â‰¡ inr x' â†’ PRED(h x) â‰¡ (ğŸ™+ h)(Îº x)
    lâ‚ x' r = PRED (h x) â‰¡âŸ¨ ap PRED câ‚… âŸ©
              inr (h x') â‰¡âŸ¨ (ap (ğŸ™+ h) r)â»Â¹ âŸ©
              ğŸ™+ h (Îº x) âˆ
     where
      câ‚ : (n : â„•) â†’ q(Q n (inr x)) â‰¡ Q n (Îº x)
      câ‚ 0 = refl
      câ‚ (succ n) = ap q (câ‚ n)
      câ‚‚ : (n : â„•) â†’ q(Q n (inr x)) â‰¡ Q n (inr x')
      câ‚‚ n = q (Q n (inr x)) â‰¡âŸ¨ câ‚ n âŸ©
             Q n (Îº x)       â‰¡âŸ¨ ap (Q n) r âŸ©
             Q n (inr x')    âˆ
      câ‚ƒ : (n : â„•) â†’ E(q(Q n (inr x))) â‰¡ E(Q n (inr x'))
      câ‚ƒ n = ap E (câ‚‚ n)
      câ‚„ : (i : â„•) â†’ Î¹ (h x) i â‰¡ Î¹ (Succ (h x')) i
      câ‚„ 0  = câ‚ƒ 0
      câ‚„ (succ i) = câ‚ƒ(succ i)
      câ‚… : h x â‰¡ Succ (h x')
      câ‚… = â„•âˆ-to-â„•â†’ğŸš-lc feâ‚€ (dfunext feâ‚€ câ‚„)

â„•âˆ-corec  : {X : ğ“¤ Ì‡ } â†’ (X â†’ ğŸ™ + X) â†’ (X â†’ â„•âˆ)
â„•âˆ-corec c = prâ‚(homomorphism-existence c)

â„•âˆ-corec-homomorphism : {X : ğ“¤ Ì‡ } (Îº : X â†’ ğŸ™ + X)
                      â†’ is-homomorphism Îº (â„•âˆ-corec Îº)
â„•âˆ-corec-homomorphism Îº = prâ‚‚(homomorphism-existence Îº)

\end{code}

We now discuss coinduction. We first define bisimulations.

\begin{code}

â„•âˆ-bisimulation :(â„•âˆ â†’ â„•âˆ â†’ ğ“¤ Ì‡ ) â†’ ğ“¤ Ì‡
â„•âˆ-bisimulation R = (u v : â„•âˆ) â†’ R u v
                                â†’ (positivity u â‰¡ positivity v)
                                Ã—  R (Pred u) (Pred v)

â„•âˆ-coinduction : (R : â„•âˆ â†’ â„•âˆ â†’ ğ“¤ Ì‡ )
               â†’ â„•âˆ-bisimulation R
               â†’ (u v : â„•âˆ) â†’ R u v â†’ u â‰¡ v
â„•âˆ-coinduction R b u v r = â„•âˆ-to-â„•â†’ğŸš-lc feâ‚€ (dfunext feâ‚€ (l u v r))
 where
  l : (u v : â„•âˆ) â†’ R u v â†’ (i : â„•) â†’ Î¹ u i â‰¡ Î¹ v i
  l u v r 0 =  prâ‚(b u v r)
  l u v r (succ i) = l (Pred u) (Pred v) (prâ‚‚(b u v r)) i

\end{code}

To be able to use it for our purpose, we need to investigate
coalgebra homomorphisms in more detail.

\begin{code}

coalg-morphism-Zero : {X : ğ“¤ Ì‡ } (Îº : X â†’  ğŸ™ + X) (h : X â†’ â„•âˆ)
                    â†’ is-homomorphism Îº h
                    â†’ (x : X) (s : ğŸ™) â†’ Îº x â‰¡ inl s â†’ h x â‰¡ Zero
coalg-morphism-Zero p h a x â‹† Îº = h x               â‰¡âŸ¨ SUCC-PRED â»Â¹ âŸ©
                                  SUCC (PRED (h x)) â‰¡âŸ¨ ap SUCC c âŸ©
                                  SUCC (inl â‹†)      âˆ
 where
  c : PRED(h x) â‰¡ inl â‹†
  c = PRED (h x) â‰¡âŸ¨ ap (Î» - â†’ - x) a âŸ©
      ğŸ™+ h (p x) â‰¡âŸ¨ ap (ğŸ™+ h) Îº âŸ©
      inl â‹†      âˆ

Coalg-morphism-Zero : {X : ğ“¤ Ì‡ } (Îº : X â†’  ğŸ™ + X)
                    â†’ (x : X) (s : ğŸ™) â†’ Îº x â‰¡ inl s â†’ â„•âˆ-corec Îº x â‰¡ Zero
Coalg-morphism-Zero Îº = coalg-morphism-Zero Îº (â„•âˆ-corec Îº) (â„•âˆ-corec-homomorphism Îº)

coalg-morphism-Succ : {X : ğ“¤ Ì‡ }
                      (Îº : X â†’  ğŸ™ + X) (h : X â†’ â„•âˆ)
                    â†’ is-homomorphism Îº h
                    â†’ (x x' : X) â†’ Îº x â‰¡ inr x' â†’ h x â‰¡ Succ (h x')
coalg-morphism-Succ Îº h a x x' q = h x               â‰¡âŸ¨ SUCC-PRED â»Â¹ âŸ©
                                   SUCC (PRED (h x)) â‰¡âŸ¨ ap SUCC c âŸ©
                                   SUCC (inr (h x')) âˆ
 where
  c : PRED(h x) â‰¡ inr(h x')
  c = PRED (h x) â‰¡âŸ¨ ap (Î» - â†’ - x) a âŸ©
      ğŸ™+ h (Îº x) â‰¡âŸ¨ ap (ğŸ™+ h) q âŸ©
      inr (h x') âˆ

Coalg-morphism-Succ : {X : ğ“¤ Ì‡ } (Îº : X â†’  ğŸ™ + X)
                    â†’ (x x' : X) â†’ Îº x â‰¡ inr x' â†’ â„•âˆ-corec Îº x â‰¡ Succ (â„•âˆ-corec Îº x')
Coalg-morphism-Succ Îº = coalg-morphism-Succ Îº (â„•âˆ-corec Îº) (â„•âˆ-corec-homomorphism Îº)

\end{code}

The following two technical lemmas are used to construct a
bisimulation:

\begin{code}

coalg-morphism-positivity : {X : ğ“¤ Ì‡ }
                            (Îº : X â†’  ğŸ™ + X) (f g : X â†’ â„•âˆ)
                          â†’ is-homomorphism Îº f
                          â†’ is-homomorphism Îº g
                          â†’ (x : X) â†’ positivity(f x) â‰¡ positivity(g x)
coalg-morphism-positivity {ğ“¤} {X} Îº f g a b x = equality-cases (Îº x) lâ‚€ lâ‚
 where
  lâ‚€ : (s : ğŸ™) â†’ Îº x â‰¡ inl s â†’ positivity(f x) â‰¡ positivity(g x)
  lâ‚€ s q = positivity (f x) â‰¡âŸ¨ ap positivity(coalg-morphism-Zero Îº f a x s q) âŸ©
           â‚€                â‰¡âŸ¨ (ap positivity(coalg-morphism-Zero Îº g b x s q))â»Â¹ âŸ©
           positivity (g x) âˆ

  lâ‚ : (x' : X) â†’ Îº x â‰¡ inr x' â†’ positivity(f x) â‰¡ positivity(g x)
  lâ‚ x' q = positivity (f x)         â‰¡âŸ¨ ap positivity(coalg-morphism-Succ Îº f a x x' q) âŸ©
            positivity (Succ (f x')) â‰¡âŸ¨ (ap positivity(coalg-morphism-Succ Îº g b x x' q))â»Â¹ âŸ©
            positivity (g x)         âˆ

coalg-morphism-Pred : {X : ğ“¤ Ì‡ }
                      (Îº : X â†’  ğŸ™ + X) (f g : X â†’ â„•âˆ)
                    â†’ is-homomorphism Îº f
                    â†’ is-homomorphism Îº g
                    â†’ (x : X) (u v : â„•âˆ)
                    â†’ u â‰¡ f x
                    â†’ v â‰¡ g x
                    â†’ Î£ x' ê‰ X , (Pred u â‰¡ f x') Ã— (Pred v â‰¡ g x')
coalg-morphism-Pred {ğ“¤} {X} Îº f g a b x u v d e =
 equality-cases (Îº x) lâ‚€ lâ‚
 where
  lâ‚€ : (s : ğŸ™) â†’ Îº x â‰¡ inl s
     â†’ Î£ x' ê‰ X , (Pred u â‰¡ f x') Ã—  (Pred v â‰¡ g x')
  lâ‚€ s q = x , (l f a u d , l g b v e)
   where
    l : (h : X â†’ â„•âˆ) â†’ PRED âˆ˜ h â‰¡ (ğŸ™+ h) âˆ˜ Îº
      â†’ (u : â„•âˆ) â†’ u â‰¡ h x â†’ Pred u â‰¡ h x
    l h a u d = Pred u â‰¡âŸ¨ câ‚ âŸ©
                Zero   â‰¡âŸ¨ câ‚€ â»Â¹ âŸ©
                h x    âˆ
     where
      câ‚€ : h x â‰¡ Zero
      câ‚€ = coalg-morphism-Zero Îº h a x s q
      câ‚ : Pred u â‰¡ Zero
      câ‚ = ap Pred (u    â‰¡âŸ¨ d âŸ©
                    h x  â‰¡âŸ¨ câ‚€ âŸ©
                    Zero âˆ)

  lâ‚ : (x' : X) â†’ Îº x â‰¡ inr x'
     â†’ Î£ x' ê‰ X , (Pred u â‰¡ f x') Ã— (Pred v â‰¡ g x')
  lâ‚ x' q = x' , (l f a u d , l g b v e)
   where
    l : (h : X â†’ â„•âˆ) â†’ PRED âˆ˜ h â‰¡ (ğŸ™+ h) âˆ˜ Îº
      â†’ (u : â„•âˆ) â†’ u â‰¡ h x â†’ Pred u â‰¡ h x'
    l h a u d = Pred u     â‰¡âŸ¨ ap Pred d âŸ©
                Pred (h x) â‰¡âŸ¨ ap Pred(coalg-morphism-Succ Îº h a x x' q) âŸ©
                h x'       âˆ

\end{code}

We are finally able to prove the uniqueness of coalgebra homomorphisms
from Îº to PRED.

\begin{code}

homomorphism-uniqueness : {X : ğ“¤ Ì‡ }
                          (Îº : X â†’ ğŸ™ + X) (f g : X â†’ â„•âˆ)
                        â†’ is-homomorphism Îº f
                        â†’ is-homomorphism Îº g
                        â†’ f â‰¡ g
homomorphism-uniqueness {ğ“¤} {X} Îº f g a b = dfunext (fe ğ“¤ ğ“¤â‚€) l
 where
  R : â„•âˆ â†’ â„•âˆ â†’ ğ“¤ Ì‡
  R u v = Î£ x ê‰ X , (u â‰¡ f x) Ã— (v â‰¡ g x)

  r : (x : X) â†’ R (f x) (g x)
  r x = (x , refl , refl)

  R-positivity : (u v : â„•âˆ) â†’ R u v â†’ positivity u â‰¡ positivity v
  R-positivity u v (x , c , d) = positivity u     â‰¡âŸ¨ ap positivity c âŸ©
                                 positivity (f x) â‰¡âŸ¨ coalg-morphism-positivity Îº f g a b x âŸ©
                                 positivity (g x) â‰¡âŸ¨ ap positivity (d â»Â¹) âŸ©
                                 positivity v âˆ

  R-Pred : (u v : â„•âˆ) â†’ R u v â†’ R (Pred u) (Pred v)
  R-Pred u v (x , c , d) =
   (prâ‚ l , prâ‚(prâ‚‚ l) , prâ‚‚(prâ‚‚ l))
   where
    l : Î£ x' ê‰ X , (Pred u â‰¡ f x') Ã— (Pred v â‰¡ g x')
    l = coalg-morphism-Pred Îº f g a b x u v c d

  R-bisimulation : â„•âˆ-bisimulation R
  R-bisimulation u v r = (R-positivity u v r) , (R-Pred u v r)

  l : f âˆ¼ g
  l x = â„•âˆ-coinduction R R-bisimulation (f x) (g x) (r x)

\end{code}

Putting existence and uniqueness together, we get that PRED is the final
coalgebra, as claimed:

\begin{code}

PRED-is-the-final-coalgebra : {X : ğ“¤ Ì‡ }
  â†’ (Îº : X â†’ ğŸ™ + X) â†’ Î£! h ê‰ (X â†’ â„•âˆ ), is-homomorphism Îº h
PRED-is-the-final-coalgebra Îº = homomorphism-existence Îº , homomorphism-uniqueness Îº

\end{code}

There is more formalization work to do (2017): By now we know that Î£!
(a form of unique existence) is better captured by the contractibility
of a Î£ type (added 13th July 2018):

\begin{code}

open import UF-Base
open import UF-Subsingletons
open import UF-Subsingletons-FunExt

PRED-is-the-homotopy-final-coalgebra : {X : ğ“¤ Ì‡ } (Îº : X â†’ ğŸ™ + X)
                                     â†’ âˆƒ! h ê‰ (X â†’ â„•âˆ), is-homomorphism Îº h
PRED-is-the-homotopy-final-coalgebra {ğ“¤} {X} Îº = homomorphism-existence Îº , Î³
 where
  Î³ : (e : Î£ h' ê‰ (X â†’ â„•âˆ), is-homomorphism Îº h') â†’ homomorphism-existence Îº â‰¡ e
  Î³ (h' , r) = to-Î£-â‰¡
                (homomorphism-uniqueness Îº (â„•âˆ-corec Îº) h' (â„•âˆ-corec-homomorphism Îº) r ,
                 Î -is-set (fe ğ“¤ ğ“¤â‚€)
                   (Î» _ â†’ +-is-set ğŸ™ â„•âˆ
                           (props-are-sets ğŸ™-is-prop)
                           (â„•âˆ-is-set feâ‚€)) _ _)

\end{code}
