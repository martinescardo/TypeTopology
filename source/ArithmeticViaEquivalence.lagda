Arithmetic via equivalence
--------------------------

MartÃ­n HÃ¶tzel EscardÃ³

Originally 10 July 2014, modified 10 Oct 2017, 22 March 2018.

This is a literate proof in univalent mathematics, in Agda notation.

We have that 3+3+3+3+3 = 5+5+5, or 5Ã—3 = 3Ã—5, and more generally

  mÃ—n = nÃ—m.

This can of course be proved by induction. A more appealing pictorial
proof is

  ooo
  ooo         ooooo
  ooo    =    ooooo
  ooo         ooooo
  ooo

where "o" is an pebble. We just rotate the grid of pebbles, or swap
the coordinates, and doing this doesn't change the number of pebbles.

How can this proof be formally rendered, as faithfully as possible to
the intuition?

We first define an interpretation function Fin : â„• â†’ Uâ‚€ of numbers as
sets (in the universe Uâ‚€) by

 (1) Fin   0  = ğŸ˜,          where ğŸ˜ is the empty set,
 (2) Fin(n+1) = Fin n + ğŸ™,  where ğŸ™ is the singleton set, 

Then Fin is a semiring homomorphism:

 (3) Fin(m + n) â‰ƒ Fin m + Fin n, where "+" in the rhs is disjoint union, 
 (4) Fin 1 â‰ƒ ğŸ™,
 (5) Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n, where "Ã—" in the rhs is cartesian product,

It is also left-cancellable:

 (6) Fin m â‰ƒ Fin n â†’ m = n.

But instead of proving (3)-(5) after defining addition and
multiplication, we prove that

 (3') For every m,n:â„• there is k:â„• with Fin k â‰ƒ Fin m + Fin n.
 (5') For every m,n:â„• there is k:â„• with Fin k â‰ƒ Fin m Ã— Fin n. 

We then define addition and multiplication on â„• from (3') and (5'),
from which (3) and (5) follow tautologically.

This relies on type arithmetic. To prove (3'), we use the trivial
equivalences
 
 X â‰ƒ X + ğŸ˜,
 (X + Y) + ğŸ™ â‰ƒ X + (Y + ğŸ™),

mimicking the definition of addition on â„• in Peano arithmetic (but
with the equations written backwards).

To prove (4), we use the equivalence

 ğŸ˜ + ğŸ™ â‰ƒ ğŸ™

To prove (5'), we use the equivalences

 ğŸ˜ â‰ƒ X Ã— ğŸ˜,
 X Ã— Y + X â‰ƒ X Ã— (Y + ğŸ™),

mimicking the definition of multiplication on â„• in Peano arithmetic
(again backwards).

To prove the cancellation property (6), we use the cancellation
property

 X + ğŸ™ â‰ƒ Y + ğŸ™ â†’ X â‰ƒ Y,

mimicking the cancellation property of the successor function on â„•.
(This is the only combinatorial argument here.)

Now, relying on the equivalence

 X Ã— Y â‰ƒ Y Ã— X,

which corresponds to the rotation of the grid of pebbles, we can prove
m Ã— n = n Ã— m as follows:

 Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n   by (5)
            â‰ƒ Fin n Ã— Fin m   by  X Ã— Y â‰ƒ Y Ã— X,
            â‰ƒ Fin(n Ã— m)      by (5),

and so 

 m Ã— n â‰ƒ n Ã— m                by (6).

Similarly we can prove, of course,

 m + n â‰ƒ n + m

using (3) and the equivalence

 X + Y â‰ƒ Y + X.

Among all these constructions, we use induction on â„• only in

  * the definition (1-2) of the function Fin : â„• â†’ Uâ‚€,

  * the existence (3')-(5') of addition and multiplication, and

  * the left-cancellability (6) of Fin.

With this, induction is not needed to prove that addition and
multiplication are commutative.

Side-remark, to be explored in a future version. From the equivalence

 (5) Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n

we get two maps

     f : Fin(m Ã— n) â†’ Fin m,
     g : Fin(m Ã— n) â†’ Fin n,

which we didn't define explicitly or combinatorially.

21st March 2018 remark: After doing this, I found this article by Tim
Gowers:

    Why is multiplication commutative?
    https://www.dpmms.cam.ac.uk/~wtg10/commutative.html

which says very much the same as above (implemented below in univalent
foundations in Agda notation).

\begin{code}

{-# OPTIONS --without-K --exact-split #-}

open import SpartanMLTT hiding (_+'_)
open import UF-FunExt

module ArithmeticViaEquivalence (fe : âˆ€ U V â†’ FunExt U V) where

open import UF-Equiv
open import UF-EquivalenceExamples
open import PlusOneLC

\end{code}

1st definition by induction. From a natural number n, get a finite set
with n elements. This can be considered as an interpretation function,
which defines the meaning of numbers as types.

\begin{code}

Fin : â„• â†’ Uâ‚€ Ì‡
Fin zero     = ğŸ˜
Fin (succ n) = Fin n + ğŸ™

\end{code}

We have zero and successor for finite sets, with the following types:

\begin{code}

fzero : {n : â„•} â†’ Fin(succ n)
fzero = inr *

fsucc : {n : â„•} â†’ Fin n â†’ Fin(succ n)
fsucc = inl

\end{code}

2nd definition by induction. Existence of addition:

\begin{code}

+construction : (m n : â„•) â†’ Î£ \(k : â„•) â†’ Fin k â‰ƒ Fin m + Fin n
+construction m zero = m , ğŸ˜-rneutral
+construction m (succ n) = g
  where
    IH : Î£ \(k : â„•) â†’ Fin k â‰ƒ Fin m + Fin n
    IH = +construction m n
    k : â„•
    k = prâ‚ IH
    Ï† : Fin k â‰ƒ Fin m + Fin n
    Ï† = prâ‚‚ IH
    Ï†+ğŸ™ : Fin(succ k) â‰ƒ Fin m + Fin (succ n)
    Ï†+ğŸ™ =  Fin k + ğŸ™          â‰ƒâŸ¨ Ap+ ğŸ™ Ï† âŸ©
           (Fin m + Fin n) + ğŸ™ â‰ƒâŸ¨ +assoc âŸ©
           (Fin m + Fin n + ğŸ™) â–  
    g : Î£ \(k' : â„•) â†’ Fin k' â‰ƒ Fin m + Fin (succ n)
    g = succ k , Ï†+ğŸ™

\end{code}

The construction gives an addition function by projection:

\begin{code}

_+'_ : â„• â†’ â„• â†’ â„•
m +' n = prâ‚(+construction m n)

\end{code}

The construction also shows that its satisfies the usual
characterizing equations from Peano arithmetic:

\begin{code}

+base : {m : â„•} â†’ m +' zero â‰¡ m
+base = refl

+step : {m n : â„•} â†’ m +' (succ n) â‰¡ succ(m +' n)
+step = refl

\end{code}

Tautologically, we get that Fin : â„• â†’ Uâ‚€ is an
addition-homomorphism:

\begin{code}

Fin+homo' : (m n : â„•) â†’ Fin(m +' n) â‰ƒ Fin m + Fin n
Fin+homo' m n = prâ‚‚(+construction m n)

\end{code}

3rd and last definition by induction. The function Fin : â„• â†’ Uâ‚€ is
left-cancellable:

\begin{code}

Fin-lc : (m n : â„•) â†’ Fin m â‰ƒ Fin n â†’ m â‰¡ n
Fin-lc zero zero p = refl
Fin-lc (succ m) zero p = ğŸ˜-elim (eqtofun _ _ p fzero)
Fin-lc zero (succ n) p = ğŸ˜-elim (eqtofun _ _ (â‰ƒ-sym p) fzero)
Fin-lc (succ m) (succ n) p = ap succ r
 where
  IH : Fin m â‰ƒ Fin n â†’ m â‰¡ n
  IH = Fin-lc m n
  remark : Fin m + ğŸ™ â‰ƒ Fin n + ğŸ™
  remark = p
  q : Fin m â‰ƒ Fin n
  q = +ğŸ™-cancellable fe p
  r : m â‰¡ n
  r = IH q 

\end{code}

This uses the non-trivial construction +ğŸ™-cancellable defined in the
module PlusOneLC.lagda.

With this, no further induction is needed to prove commutativity of
addition:

\begin{code}

+'-comm : (m n : â„•) â†’ m +' n â‰¡ n +' m
+'-comm m n = Fin-lc (m +' n) (n +' m) p
 where
  p : Fin(m +' n) â‰ƒ Fin(n +' m)
  p =  Fin (m +' n)  â‰ƒâŸ¨ Fin+homo' m n âŸ©
       Fin m + Fin n â‰ƒâŸ¨ +comm  âŸ©
       Fin n + Fin m â‰ƒâŸ¨ â‰ƒ-sym (Fin+homo' n m) âŸ©
       Fin (n +' m) â– 

\end{code}

We now repeat this story for multiplication:

\begin{code}

Ã—construction : (m n : â„•) â†’ Î£ \(k : â„•) â†’ Fin k â‰ƒ Fin m Ã— Fin n
Ã—construction m zero = zero , Ã—ğŸ˜
Ã—construction m (succ n) = g
  where
    IH : Î£ \(k : â„•) â†’ Fin k â‰ƒ Fin m Ã— Fin n
    IH = Ã—construction m n
    k : â„•
    k = prâ‚ IH
    Ï† : Fin k â‰ƒ Fin m Ã— Fin n
    Ï† = prâ‚‚ IH
    Ï†' : Fin (k +' m) â‰ƒ Fin m Ã— (Fin n + ğŸ™)
    Ï†' = Fin (k +' m)          â‰ƒâŸ¨ Fin+homo' k m âŸ©
         Fin k + Fin m         â‰ƒâŸ¨ Ap+ (Fin m) Ï† âŸ©
         Fin m Ã— Fin n + Fin m â‰ƒâŸ¨ ğŸ™distr âŸ©
         Fin m Ã— (Fin n + ğŸ™) â– 
    g : Î£ \(k' : â„•) â†’ Fin k' â‰ƒ Fin m Ã— Fin (succ n)
    g = (k +' m) , Ï†'

_Ã—'_ : â„• â†’ â„• â†’ â„•
m Ã—' n = prâ‚(Ã—construction m n)

Ã—base : {m : â„•} â†’ m Ã—' zero â‰¡ zero
Ã—base = refl

Ã—step : {m n : â„•} â†’ m Ã—' (succ n) â‰¡ m Ã—' n +' m
Ã—step = refl

FinÃ—homo : (m n : â„•) â†’ Fin(m Ã—' n) â‰ƒ Fin m Ã— Fin n
FinÃ—homo m n = prâ‚‚(Ã—construction m n)

Ã—-comm : (m n : â„•) â†’ m Ã—' n â‰¡ n Ã—' m
Ã—-comm m n = Fin-lc (m Ã—' n) (n Ã—' m) Ï†
 where
  Ï† : Fin(m Ã—' n) â‰ƒ Fin(n Ã—' m)
  Ï† = Fin (m Ã—' n)  â‰ƒâŸ¨ FinÃ—homo m n âŸ©
      Fin m Ã— Fin n â‰ƒâŸ¨ Ã—comm âŸ©
      Fin n Ã— Fin m â‰ƒâŸ¨ â‰ƒ-sym (FinÃ—homo n m) âŸ©
      Fin (n Ã—' m) â– 

\end{code}

\begin{code}

infixl 20 _+'_ 
infixl 22 _Ã—'_ 

\end{code}
