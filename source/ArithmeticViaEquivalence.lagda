Arithmetic via equivalence
--------------------------

MartÃ­n HÃ¶tzel EscardÃ³

Originally 10 July 2014, modified 10 Oct 2017, 22 March 2018, 25 Nov 2019.

This is a literate proof in univalent mathematics, in Agda
notation. Although the concepts of univalent mathematics are used, the
univalence axiom is not needed.

We have that 3+3+3+3+3 = 5+5+5, or 5Ã—3 = 3Ã—5, and more generally

  mÃ—n = nÃ—m.

This can of course be proved by induction. A more appealing pictorial
proof is

  ooo
  ooo         ooooo
  ooo    =    ooooo
  ooo         ooooo
  ooo

where "o" is a pebble. We just rotate the grid of pebbles, or swap
the coordinates, and doing this doesn't change the number of pebbles.

How can this proof be formally rendered, as faithfully as possible to
the intuition?

We first define an interpretation function Fin : â„• â†’ ğ“¤â‚€ of numbers as
sets (in the universe ğ“¤â‚€) by

 (1) Fin   0  = ğŸ˜,          where ğŸ˜ is the empty set,
 (2) Fin(n+1) = Fin n + ğŸ™,  where ğŸ™ is the singleton set,

Then Fin is a semiring homomorphism:

 (3) Fin(m + n) â‰ƒ Fin m + Fin n, where "+" in the rhs is disjoint union,
 (4) Fin 1 â‰ƒ ğŸ™,
 (5) Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n, where "Ã—" in the rhs is cartesian product,

It is also left-cancellable:

 (6) Fin m â‰ƒ Fin n â†’ m = n.

Two boxes with the same number of pebbles are counted by same number.

But instead of proving (3)-(5) after defining addition and
multiplication, we prove that

 (3') For every m,n:â„• there is k:â„• with Fin k â‰ƒ Fin m + Fin n.
 (5') For every m,n:â„• there is k:â„• with Fin k â‰ƒ Fin m Ã— Fin n.

We then define addition and multiplication on â„• from (3') and (5'),
from which (3) and (5) follow tautologically.

This relies on type arithmetic. To prove (3'), we use the trivial
bijections, or *equivalences* in the terminology of univalent
mathematics,

 X â‰ƒ X + ğŸ˜,
 (X + Y) + ğŸ™ â‰ƒ X + (Y + ğŸ™),

mimicking the definition of addition on â„• in Peano arithmetic (but
with the equations written backwards).

To prove (4), we use the equivalence

 ğŸ˜ + ğŸ™ â‰ƒ ğŸ™

To prove (5'), we use the equivalences

 ğŸ˜ â‰ƒ X Ã— ğŸ˜,
 X Ã— Y + X â‰ƒ X Ã— (Y + ğŸ™),

mimicking the definition of multiplication on â„• in Peano arithmetic
(again backwards).

To prove the cancellation property (6), we use the cancellation
property

 X + ğŸ™ â‰ƒ Y + ğŸ™ â†’ X â‰ƒ Y,

mimicking the cancellation property of the successor function on â„•.
(This is the only combinatorial argument here.)

Now, relying on the equivalence

 X Ã— Y â‰ƒ Y Ã— X,

which corresponds to the rotation of the grid of pebbles, we can prove
m Ã— n = n Ã— m as follows:

 Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n   by (5)
            â‰ƒ Fin n Ã— Fin m   by  X Ã— Y â‰ƒ Y Ã— X,
            â‰ƒ Fin(n Ã— m)      by (5),

and so

 m Ã— n â‰ƒ n Ã— m                by (6).

Similarly we can prove, of course,

 m + n â‰ƒ n + m

using (3) and the equivalence

 X + Y â‰ƒ Y + X.

Among all these constructions, we use induction on â„• only in

  * the definition (1-2) of the function Fin : â„• â†’ ğ“¤â‚€,

  * the existence (3')-(5') of addition and multiplication, and

  * the left-cancellability (6) of Fin.

With this, induction is not needed to prove that addition and
multiplication are commutative.

Side-remark, to be explored in a future version. From the equivalence

 (5) Fin(m Ã— n) â‰ƒ Fin m Ã— Fin n

we get two maps

     f : Fin(m Ã— n) â†’ Fin m,
     g : Fin(m Ã— n) â†’ Fin n,

which we didn't define explicitly or combinatorially.

21st March 2018 remark: After doing this, I found this article by Tim
Gowers:

    Why is multiplication commutative?
    https://www.dpmms.cam.ac.uk/~wtg10/commutative.html

which says very much the same as above (implemented below in univalent
foundations in Agda notation).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT hiding (_^_)

module ArithmeticViaEquivalence where

open import UF-Subsingletons
open import UF-Equiv
open import UF-EquivalenceExamples
open import Fin
open import Fin-Properties

\end{code}

The 1st definition by induction is that of the function Fin defined in
the module Fin imported above, namely

  Fin   0  = ğŸ˜,
  Fin(n+1) = Fin n + ğŸ™.

From a natural number n, get a finite set Fin n with n elements. This
can be considered as an interpretation function, which defines the
meaning of numbers as types.

2nd definition by induction. Existence of addition:

\begin{code}

+construction : (m n : â„•) â†’ Î£ k ê‰ â„• , Fin k â‰ƒ Fin m + Fin n
+construction m zero = m , ğŸ˜-rneutral
+construction m (succ n) = g
  where
    IH : Î£ k ê‰ â„• , Fin k â‰ƒ Fin m + Fin n
    IH = +construction m n
    k : â„•
    k = prâ‚ IH
    Ï† : Fin k â‰ƒ Fin m + Fin n
    Ï† = prâ‚‚ IH

    Ï†' =  Fin k + ğŸ™           â‰ƒâŸ¨ Ap+ ğŸ™ Ï† âŸ©
         (Fin m + Fin n) + ğŸ™  â‰ƒâŸ¨ +assoc âŸ©
         (Fin m + Fin n + ğŸ™)  â– 

    g : Î£ k' ê‰ â„• , Fin k' â‰ƒ Fin m + Fin (succ n)
    g = succ k , Ï†'

\end{code}

The construction gives an addition function by projection:

\begin{code}

_+'_ : â„• â†’ â„• â†’ â„•
m +' n = prâ‚(+construction m n)

infixl 20 _+'_

\end{code}

The construction also shows that its satisfies the usual
characterizing equations from Peano arithmetic:

\begin{code}

+base : {m : â„•} â†’ m +' zero â‰¡ m
+base = refl

+step : {m n : â„•} â†’ m +' (succ n) â‰¡ succ(m +' n)
+step = refl

\end{code}

Tautologically, we get that Fin : â„• â†’ ğ“¤â‚€ is an
addition-homomorphism:

\begin{code}

Fin+homo : (m n : â„•) â†’ Fin(m +' n) â‰ƒ Fin m + Fin n
Fin+homo m n = prâ‚‚(+construction m n)

\end{code}

The 3rd and last use of induction is for the left-cancellability of
Fin : â„• â†’ ğ“¤â‚€, which is in the module Fin.

With this, no further induction is needed to prove commutativity of
addition:

\begin{code}

+'-comm : (m n : â„•) â†’ m +' n â‰¡ n +' m
+'-comm m n = Fin-lc (m +' n) (n +' m)
 (Fin (m +' n)   â‰ƒâŸ¨ Fin+homo m n âŸ©
  Fin m + Fin n  â‰ƒâŸ¨ +comm âŸ©
  Fin n + Fin m  â‰ƒâŸ¨ â‰ƒ-sym (Fin+homo n m) âŸ©
  Fin (n +' m)   â– )

\end{code}

Exercise. Associativity without induction.

We now repeat this story for multiplication:

\begin{code}

Ã—construction : (m n : â„•) â†’ Î£ k ê‰ â„• , Fin k â‰ƒ Fin m Ã— Fin n
Ã—construction m zero = zero , Ã—ğŸ˜
Ã—construction m (succ n) = g
  where
    IH : Î£ k ê‰ â„• , Fin k â‰ƒ Fin m Ã— Fin n
    IH = Ã—construction m n
    k : â„•
    k = prâ‚ IH
    Ï† : Fin k â‰ƒ Fin m Ã— Fin n
    Ï† = prâ‚‚ IH

    Ï†' = Fin (k +' m)          â‰ƒâŸ¨ Fin+homo k m âŸ©
         Fin k + Fin m         â‰ƒâŸ¨ Ap+ (Fin m) Ï† âŸ©
         Fin m Ã— Fin n + Fin m â‰ƒâŸ¨ ğŸ™distr âŸ©
         Fin m Ã— (Fin n + ğŸ™)   â– 

    g : Î£ k' ê‰ â„• , Fin k' â‰ƒ Fin m Ã— Fin (succ n)
    g = (k +' m) , Ï†'

_Ã—'_ : â„• â†’ â„• â†’ â„•
m Ã—' n = prâ‚(Ã—construction m n)

infixl 22 _Ã—'_

Ã—base : {m : â„•} â†’ m Ã—' zero â‰¡ zero
Ã—base = refl

Ã—step : {m n : â„•} â†’ m Ã—' (succ n) â‰¡ m Ã—' n +' m
Ã—step = refl

FinÃ—homo : (m n : â„•) â†’ Fin(m Ã—' n) â‰ƒ Fin m Ã— Fin n
FinÃ—homo m n = prâ‚‚(Ã—construction m n)

Ã—'-comm : (m n : â„•) â†’ m Ã—' n â‰¡ n Ã—' m
Ã—'-comm m n = Fin-lc (m Ã—' n) (n Ã—' m)
 (Fin (m Ã—' n)   â‰ƒâŸ¨ FinÃ—homo m n âŸ©
  Fin m Ã— Fin n  â‰ƒâŸ¨ Ã—comm âŸ©
  Fin n Ã— Fin m  â‰ƒâŸ¨ â‰ƒ-sym (FinÃ—homo n m) âŸ©
  Fin (n Ã—' m)   â– )

\end{code}

Exercise. Associativity without induction.

Added 30th August 2018: Exponentiation. Requires one more induction
and function extensionality.

\begin{code}

open import UF-FunExt

module exponentiation-and-factorial (fe : FunExt) where

 feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
 feâ‚€ = fe ğ“¤â‚€ ğ“¤â‚€

 â†’construction : (m n : â„•) â†’ Î£ k ê‰ â„• , Fin k â‰ƒ (Fin m â†’ Fin n)
 â†’construction zero n = succ zero ,
                        (ğŸ˜ + ğŸ™        â‰ƒâŸ¨ ğŸ˜-lneutral âŸ©
                         ğŸ™            â‰ƒâŸ¨ ğŸ˜â†’ feâ‚€ âŸ©
                        (ğŸ˜ â†’ Fin n)   â– )
 â†’construction (succ m) n = g
  where
   IH : Î£ k ê‰ â„• , Fin k â‰ƒ (Fin m â†’ Fin n)
   IH = â†’construction m n
   k : â„•
   k = prâ‚ IH
   Ï† : Fin k â‰ƒ (Fin m â†’ Fin n)
   Ï† = prâ‚‚ IH

   Ï†' = Fin (k Ã—' n)                   â‰ƒâŸ¨ FinÃ—homo k n âŸ©
        Fin k Ã— Fin n                  â‰ƒâŸ¨ Ã—-cong Ï† (ğŸ™â†’ feâ‚€) âŸ©
       (Fin m â†’ Fin n) Ã— (ğŸ™ â†’ Fin n)   â‰ƒâŸ¨ â‰ƒ-sym (+â†’ feâ‚€) âŸ©
       (Fin m + ğŸ™ â†’ Fin n)             â– 

   g : Î£ k' ê‰ â„• , Fin k' â‰ƒ (Fin (succ m) â†’ Fin n)
   g = k Ã—' n , Ï†'

 _^_ : â„• â†’ â„• â†’ â„•
 n ^ m = prâ‚(â†’construction m n)

 infixl 23 _^_

 ^base : {n : â„•} â†’ n ^ zero â‰¡ succ zero
 ^base = refl

 ^step : {m n : â„•} â†’ n ^ (succ m) â‰¡ (n ^ m) Ã—' n
 ^step = refl

 Fin^homo : (m n : â„•) â†’ Fin(n ^ m) â‰ƒ (Fin m â†’ Fin n)
 Fin^homo m n = prâ‚‚(â†’construction m n)

\end{code}

 Then, without the need for induction, we get the exponential laws:

\begin{code}

 ^+homo : (k m n : â„•) â†’ k ^ (m +' n) â‰¡ (k ^ m) Ã—' (k ^ n)
 ^+homo k m n = Fin-lc (k ^ (m +' n)) (k ^ m Ã—' k ^ n)
  (Fin (k ^ (m +' n))                â‰ƒâŸ¨ Fin^homo (m +' n) k âŸ©
  (Fin (m +' n) â†’ Fin k)             â‰ƒâŸ¨ â†’cong feâ‚€ feâ‚€ (Fin+homo m n) (â‰ƒ-refl (Fin k)) âŸ©
  (Fin m + Fin n â†’ Fin k)            â‰ƒâŸ¨ +â†’ feâ‚€  âŸ©
  (Fin m â†’ Fin k) Ã— (Fin n â†’ Fin k)  â‰ƒâŸ¨ Ã—-cong (â‰ƒ-sym (Fin^homo m k)) (â‰ƒ-sym (Fin^homo n k)) âŸ©
   Fin (k ^ m) Ã— Fin (k ^ n)         â‰ƒâŸ¨ â‰ƒ-sym (FinÃ—homo (k ^ m) (k ^ n)) âŸ©
   Fin (k ^ m Ã—' k ^ n)              â– )

 iterated^ : (k m n : â„•) â†’ k ^ (m Ã—' n) â‰¡ (k ^ n) ^ m
 iterated^ k m n = Fin-lc (k ^ (m Ã—' n)) (k ^ n ^ m)
    (Fin (k ^ (m Ã—' n))        â‰ƒâŸ¨ Fin^homo (m Ã—' n) k âŸ©
    (Fin (m Ã—' n) â†’ Fin k)     â‰ƒâŸ¨ â†’cong feâ‚€ feâ‚€ (FinÃ—homo m n) (â‰ƒ-refl (Fin k)) âŸ©
    (Fin m Ã— Fin n â†’ Fin k)    â‰ƒâŸ¨ curry-uncurry fe âŸ©
    (Fin m â†’ (Fin n â†’ Fin k))  â‰ƒâŸ¨ â†’cong feâ‚€ feâ‚€ (â‰ƒ-refl (Fin m)) (â‰ƒ-sym (Fin^homo n k)) âŸ©
    (Fin m â†’ Fin (k ^ n))      â‰ƒâŸ¨ â‰ƒ-sym (Fin^homo m (k ^ n)) âŸ©
     Fin (k ^ n ^ m)           â– )

\end{code}

Added 25t November 2019: Numerical factorial from the type theoretical
factorial, which also uses function extensionality (which is not
actually necessary - see the comments in the module UF-Factorial).

\begin{code}

 open import UF-Factorial fe public

 !construction : (n : â„•) â†’ Î£ k ê‰ â„• , Fin k â‰ƒ Aut (Fin n)
 !construction zero = 1 ,
                      (Fin 1          â‰ƒâŸ¨ â‰ƒ-refl (Fin 1) âŸ©
                       ğŸ˜ + ğŸ™          â‰ƒâŸ¨ ğŸ˜-lneutral âŸ©
                       ğŸ™              â‰ƒâŸ¨ factorial-base âŸ©
                       Aut (Fin zero) â– )
 !construction (succ n) = g
  where
   IH : Î£ k ê‰ â„• , Fin k â‰ƒ Aut(Fin n)
   IH = !construction n
   k : â„•
   k = prâ‚ IH
   Ï† : Fin k â‰ƒ Aut(Fin n)
   Ï† = prâ‚‚ IH

   Ï†' = Fin (succ n Ã—' k)         â‰ƒâŸ¨ FinÃ—homo (succ n) k âŸ©
        Fin (succ n) Ã— Fin k      â‰ƒâŸ¨ Ã—-cong (â‰ƒ-refl (Fin (succ n))) Ï† âŸ©
        (Fin n + ğŸ™) Ã— Aut (Fin n) â‰ƒâŸ¨ discrete-factorial (Fin n) Fin-is-discrete âŸ©
        Aut (Fin n + ğŸ™)           â– 

   g : Î£ k' ê‰ â„• , Fin k' â‰ƒ Aut (Fin (succ n))
   g = succ n Ã—' k , Ï†'

\end{code}

Geometric definition of the factorial function:

\begin{code}

 _! : â„• â†’ â„•
 n ! = prâ‚ (!construction n)

 infix 100 _!

\end{code}

The following are theorems rather than definitions:

\sbegin{code}

 !-base : 0 ! â‰¡ 1
 !-base = refl

 !-step : (n : â„•) â†’ (n +' 1)! â‰¡ (n +' 1) Ã—' n !
 !-step n = refl

\end{code}

Added 8th December 2019. Some corollaries.

Recall that a type X is finite if there is n : â„• with X â‰ƒ Fin n.

\begin{code}

open import UF-PropTrunc

module _ (pt : propositional-truncations-exist)
         (fe : FunExt)
         {ğ“¤ : Universe}
         {X : ğ“¤ Ì‡ }
         where

 open finiteness pt
 open exponentiation-and-factorial fe
 open import UF-Equiv-FunExt

 Aut-finite : is-finite X â†’ is-finite (Aut X)
 Aut-finite (n , Î±) = n ! , Î³
  where
   Î´ : X â‰ƒ Fin n â†’ Aut X â‰ƒ Fin (n !)
   Î´ d = Aut X       â‰ƒâŸ¨ â‰ƒ-cong fe d d âŸ©
         Aut (Fin n) â‰ƒâŸ¨ â‰ƒ-sym (prâ‚‚ (!construction n)) âŸ©
         Fin (n !)   â– 
   Î³ : âˆ¥ Aut X â‰ƒ Fin (n !) âˆ¥
   Î³ = âˆ¥âˆ¥-functor Î´ Î±

 module _ {ğ“¥ : Universe} {Y : ğ“¥ Ì‡ } where

  +finite : is-finite X â†’ is-finite Y â†’ is-finite (X + Y)
  Ã—finite : is-finite X â†’ is-finite Y â†’ is-finite (X Ã— Y)
  â†’finite : is-finite X â†’ is-finite Y â†’ is-finite (X â†’ Y)

  +finite (m , Î±) (n , Î²) = m +' n , Î³
   where
    Î´ : X â‰ƒ Fin m â†’ Y â‰ƒ Fin n â†’ X + Y â‰ƒ Fin (m +' n)
    Î´ d e = X + Y         â‰ƒâŸ¨ +cong d e âŸ©
            Fin m + Fin n â‰ƒâŸ¨ â‰ƒ-sym (prâ‚‚ (+construction m n)) âŸ©
            Fin (m +' n)  â– 
    Î³ : âˆ¥ X + Y â‰ƒ Fin (m +' n) âˆ¥
    Î³ = âˆ¥âˆ¥-functorâ‚‚ Î´ Î± Î²

  Ã—finite (m , Î±) (n , Î²) = m Ã—' n , Î³
   where
    Î´ : X â‰ƒ Fin m â†’ Y â‰ƒ Fin n â†’ X Ã— Y â‰ƒ Fin (m Ã—' n)
    Î´ d e = X Ã— Y         â‰ƒâŸ¨ Ã—-cong d e âŸ©
            Fin m Ã— Fin n â‰ƒâŸ¨ â‰ƒ-sym (prâ‚‚ (Ã—construction m n)) âŸ©
            Fin (m Ã—' n)  â– 
    Î³ : âˆ¥ X Ã— Y â‰ƒ Fin (m Ã—' n) âˆ¥
    Î³ = âˆ¥âˆ¥-functorâ‚‚ Î´ Î± Î²

  â†’finite (m , Î±) (n , Î²) = n ^ m , Î³
   where
    Î´ : X â‰ƒ Fin m â†’ Y â‰ƒ Fin n â†’ (X â†’ Y) â‰ƒ Fin (n ^ m)
    Î´ d e = (X â†’ Y)         â‰ƒâŸ¨ â†’cong (fe ğ“¤â‚€ ğ“¤â‚€) (fe ğ“¤ ğ“¥) d e âŸ©
            (Fin m â†’ Fin n) â‰ƒâŸ¨ â‰ƒ-sym (prâ‚‚ (â†’construction m n)) âŸ©
            Fin (n ^ m)     â– 
    Î³ : âˆ¥ (X â†’ Y) â‰ƒ Fin (n ^ m) âˆ¥
    Î³ = âˆ¥âˆ¥-functorâ‚‚ Î´ Î± Î²

\end{code}

We have accounted for the type constructors +, Ã—, â†’, and â‰ƒ (and hence
â‰¡ if we assume univalence). The last two types to account for in our
spartan MLTT are Î  and Î£.

\begin{code}

open import UF-PropIndexedPiSigma

Î£construction : (n : â„•) (a : Fin n â†’ â„•)
              â†’ Î£ k ê‰ â„• , Fin k â‰ƒ (Î£ i ê‰ Fin n , Fin (a i))
Î£construction 0 a = 0 , (Fin 0                    â‰ƒâŸ¨ â‰ƒ-refl _ âŸ©
                         ğŸ˜                        â‰ƒâŸ¨ â‰ƒ-sym (prop-indexed-sum-zero id) âŸ©
                         (Î£ i ê‰ ğŸ˜ , Fin (a i)) â– )
Î£construction (succ n) a = g
 where
  IH : Î£ k ê‰ â„• , Fin k â‰ƒ (Î£ i ê‰ Fin n , Fin (a (suc i)))
  IH = Î£construction n (Î» i â†’ a (suc i))
  k : â„•
  k = prâ‚ IH
  Ï† : Fin k â‰ƒ (Î£ i ê‰ Fin n , Fin (a (suc i)))
  Ï† = prâ‚‚ IH
  Ï†' = Fin (a ğŸ +' k)                                                      â‰ƒâŸ¨ i âŸ©
       Fin (a ğŸ) + Fin k                                                   â‰ƒâŸ¨ ii âŸ©
       Fin k + Fin (a ğŸ)                                                   â‰ƒâŸ¨ iii âŸ©
       (Î£ i ê‰ Fin n , Fin (a (suc i))) + (Î£ i ê‰ ğŸ™ , Fin (a (inr i))) â‰ƒâŸ¨ iv âŸ©
      (Î£ i ê‰ Fin n + ğŸ™ , Fin (a i))                                     â– 
   where
    i   = prâ‚‚ (+construction (a ğŸ) k)
    ii  = +comm
    iii = +cong Ï† (â‰ƒ-sym (prop-indexed-sum ğŸ™-is-prop â‹†))
    iv  = Î£+distr (Fin n) ğŸ™ (Î» i â†’ Fin (a i))

  g : Î£ k' ê‰ â„• , Fin k' â‰ƒ (Î£ i ê‰ Fin (succ n) , Fin (a i))
  g = a ğŸ +' k , Ï†'

\end{code}

The numerical sum:

\begin{code}

âˆ‘ : {n : â„•} â†’ (Fin n â†’ â„•) â†’ â„•
âˆ‘ {n} a = prâ‚ (Î£construction n a)

\end{code}

Which is characterized by its usual inductive definition:

\begin{code}

âˆ‘-base : (a : Fin 0 â†’ â„•)
       â†’ âˆ‘ a â‰¡ 0
âˆ‘-base a = refl

âˆ‘-step : {n : â„•} (a : Fin (succ n) â†’ â„•)
       â†’ âˆ‘ a â‰¡ a ğŸ +' âˆ‘ (a âˆ˜ suc)
âˆ‘-step {n} a = refl

\end{code}

For Î  we need function extensionality:

\begin{code}

module _ (fe : funext ğ“¤â‚€ ğ“¤â‚€) where

 Î construction : (n : â„•) (a : Fin n â†’ â„•)
               â†’ Î£ k ê‰ â„• , Fin k â‰ƒ (Î  i ê‰ Fin n , Fin (a i))
 Î construction 0 a = 1 , (Fin 1                     â‰ƒâŸ¨ i âŸ©
                          ğŸ˜ + ğŸ™                     â‰ƒâŸ¨ ii âŸ©
                          ğŸ™                         â‰ƒâŸ¨ iii âŸ©
                          (Î  i ê‰ ğŸ˜ , Fin (a i))     â‰ƒâŸ¨ iv âŸ©
                          (Î  i ê‰ Fin 0 , Fin (a i)) â– )
  where
   i   = â‰ƒ-refl _
   ii  = ğŸ˜-lneutral
   iii = â‰ƒ-sym (prop-indexed-product-one fe id)
   iv  = â‰ƒ-refl _

 Î construction (succ n) a = g
  where
   IH : Î£ k ê‰ â„• , Fin k â‰ƒ (Î  i ê‰ Fin n , Fin (a (suc i)))
   IH = Î construction n (Î» i â†’ a (suc i))
   k : â„•
   k = prâ‚ IH
   Ï† : Fin k â‰ƒ (Î  i ê‰ Fin n , Fin (a (suc i)))
   Ï† = prâ‚‚ IH
   Ï†' = Fin (a ğŸ Ã—' k)                                                      â‰ƒâŸ¨ i âŸ©
        Fin (a ğŸ) Ã— Fin k                                                   â‰ƒâŸ¨ ii âŸ©
        Fin k Ã— Fin (a ğŸ)                                                   â‰ƒâŸ¨ iii âŸ©
        (Î  i ê‰ Fin n , Fin (a (suc i))) Ã— (Î  i ê‰ ğŸ™ , Fin (a (inr i))) â‰ƒâŸ¨ iv âŸ©
        (Î  i ê‰ Fin n + ğŸ™ , Fin (a i))                                    â– 
    where
     i   = prâ‚‚ (Ã—construction (a ğŸ) k)
     ii  = Ã—comm
     iii = Ã—-cong Ï† (â‰ƒ-sym (prop-indexed-product fe ğŸ™-is-prop â‹†))
     iv  = Î Ã—+ fe

   g : Î£ k' ê‰ â„• , Fin k' â‰ƒ (Î  i ê‰ Fin (succ n) , Fin (a i))
   g = a ğŸ Ã—' k , Ï†'

 âˆ : {n : â„•} â†’ (Fin n â†’ â„•) â†’ â„•
 âˆ {n} a = prâ‚ (Î construction n a)

 âˆ-base : (a : Fin 0 â†’ â„•)
        â†’ âˆ a â‰¡ 1
 âˆ-base a = refl

 âˆ-step : {n : â„•} (a : Fin (succ n) â†’ â„•)
        â†’ âˆ a â‰¡ a ğŸ Ã—' âˆ (a âˆ˜ suc)
 âˆ-step {n} a = refl

\end{code}

In order to avoid the use of the commutativity of + and Ã— to get the
natural inductive constructions of âˆ‘ and âˆ, it would have been better
to have defined Fin(succ n) = ğŸ™ + Fin n. In retrospect, this
definition seems more natural in general.

Todo: Corollary. If X is a type and A is an X-indexed family of types,
and if X is finite and A x is finite for every x : X, then the types Î£ A
and Î  A are finite.
