By Martin Escardo, 2 September 2011.

Modified in December 2011 assuming function extensionality (which is
not used directly in this module, but instead in
GenericConvergentSequence).

We prove that the generic convergent sequence â„•âˆ is compact, or
searchable, which amounts to Theorem-3Â·6 of the paper

   https://www.cs.bham.ac.uk/~mhe/papers/omniscient-journal-revised.pdf
   http://www.cs.bham.ac.uk/~mhe/.talks/dagstuhl2011/omniscient.pdf

(Continuity axioms and the fan principle are not assumed.)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-FunExt

module ConvergentSequenceCompact (fe : funext ğ“¤â‚€ ğ“¤â‚€) where

open import Two-Properties
open import UF-PropTrunc
open import GenericConvergentSequence
open import DiscreteAndSeparated
open import CanonicalMapNotation
open import CompactTypes

\end{code}

We recall the main notions defined in the above imported modules:

\begin{code}

private
 module recall {X : ğ“¤ Ì‡ } where

  recallâ‚€ : compactâˆ™ X    â‰¡ (Î  p ê‰ (X â†’ ğŸš) , Î£ xâ‚€ ê‰ X , (p xâ‚€ â‰¡ â‚ â†’ Î  x ê‰ X , p x â‰¡ â‚))
  recallâ‚ : compact  X    â‰¡ (Î  p ê‰ (X â†’ ğŸš) , (Î£ x ê‰ X , p x â‰¡ â‚€) + (Î  x ê‰ X , p x â‰¡ â‚))
  recallâ‚‚ : is-discrete X â‰¡ ((x y : X) â†’ (x â‰¡ y) + (x â‰¢ y))

  recallâ‚€ = by-definition
  recallâ‚ = by-definition
  recallâ‚‚ = by-definition

\end{code}

This is the main theorem proved in this module.

\begin{code}

â„•âˆ-compactâˆ™ : compactâˆ™ â„•âˆ
â„•âˆ-compactâˆ™ p = a , Lemma
 where
  Î± : â„• â†’ ğŸš
  Î± 0       = p (Î¹ 0)
  Î± (succ n) = minğŸš (Î± n) (p (Î¹ (succ n)))

  d : is-decreasing Î±
  d n = Lemma[minabâ‰¤â‚‚a] {Î± n}

  a : â„•âˆ
  a = (Î± , d)

  Daggerâ‚€ : (n : â„•) â†’ a â‰¡ Î¹ n â†’ p (Î¹ n) â‰¡ â‚€
  Daggerâ‚€ 0 r =  p (Î¹ 0)   â‰¡âŸ¨ refl âŸ©
                 Î± 0       â‰¡âŸ¨ ap (Î» - â†’ Î¹ - 0) r âŸ©
                 Î¹ (Î¹ 0) 0 â‰¡âŸ¨ refl âŸ©
                 â‚€         âˆ

  Daggerâ‚€ (succ n) r = p (Î¹ (succ n))          â‰¡âŸ¨ w â»Â¹ âŸ©
                       Î± (succ n)              â‰¡âŸ¨ ap (Î» - â†’ Î¹ - (succ n)) r âŸ©
                       Î¹ (Î¹ (succ n)) (succ n) â‰¡âŸ¨ Î¹-diagonalâ‚€ n âŸ©
                       â‚€                       âˆ
   where
    t = Î± n              â‰¡âŸ¨ ap (Î» - â†’ Î¹ - n) r â€¯âŸ©
        Î¹ (Î¹ (succ n)) n â‰¡âŸ¨ Î¹-diagonalâ‚ n âŸ©
        â‚                âˆ

    w = Î± (succ n)              â‰¡âŸ¨ ap (Î» - â†’ minğŸš - (p (Î¹ (succ n)))) t âŸ©
        minğŸš â‚ (p (Î¹ (succ n))) â‰¡âŸ¨ refl âŸ©
        p (Î¹ (succ n))          âˆ

  Daggerâ‚ : a â‰¡ âˆ â†’ (n : â„•) â†’ p (Î¹ n) â‰¡ â‚
  Daggerâ‚ r 0 = p (Î¹ 0) â‰¡âŸ¨ refl âŸ©
                Î± 0     â‰¡âŸ¨ ap (Î» - â†’ Î¹ - 0) r âŸ©
                Î¹ âˆ 0   â‰¡âŸ¨ refl âŸ©
                â‚       âˆ
  Daggerâ‚ r (succ n) = p (Î¹ (succ n)) â‰¡âŸ¨ w â»Â¹ âŸ©
                       Î± (succ n)     â‰¡âŸ¨ ap (Î» - â†’ Î¹ - (succ n)) r âŸ©
                       Î¹ âˆ (succ n)   â‰¡âŸ¨ refl âŸ©
                       â‚              âˆ
   where
    s : Î± n â‰¡ â‚
    s = ap (Î» - â†’ Î¹ - n) r

    w : Î± (succ n) â‰¡ p (Î¹ (succ n))
    w = Î± (succ n)              â‰¡âŸ¨ ap (Î» - â†’ minğŸš - (p (Î¹ (succ n)))) s âŸ©
        minğŸš â‚ (p (Î¹ (succ n))) â‰¡âŸ¨ refl âŸ©
        p (Î¹ (succ n))          âˆ

  Lemmaâ‚€ : (n : â„•) â†’ a â‰¡ Î¹ n â†’ p a â‰¡ â‚€
  Lemmaâ‚€ n t = p a     â‰¡âŸ¨ ap p t âŸ©
               p (Î¹ n) â‰¡âŸ¨ Daggerâ‚€ n t âŸ©
               â‚€       âˆ

  Claimâ‚€ : p a â‰¡ â‚ â†’ (n : â„•) â†’ a â‰¢ Î¹ n
  Claimâ‚€ r n s = equal-â‚-different-from-â‚€ r (Lemmaâ‚€ n s)

  Claimâ‚ : p a â‰¡ â‚ â†’ a â‰¡ âˆ
  Claimâ‚ r = not-finite-is-âˆ fe (Claimâ‚€ r)

  Claimâ‚‚ : p a â‰¡ â‚ â†’ (n : â„•) â†’ p (Î¹ n) â‰¡ â‚
  Claimâ‚‚ r = Daggerâ‚ (Claimâ‚ r)

  Claimâ‚ƒ : p a â‰¡ â‚ â†’ p âˆ â‰¡ â‚
  Claimâ‚ƒ r = p âˆ â‰¡âŸ¨ (ap p (Claimâ‚ r))â»Â¹ âŸ©
             p a â‰¡âŸ¨ r âŸ©
             â‚   âˆ

  Lemma : p a â‰¡ â‚ â†’ (v : â„•âˆ) â†’ p v â‰¡ â‚
  Lemma r = â„•âˆ-ğŸš-density fe (Claimâ‚‚ r) (Claimâ‚ƒ r)

\end{code}

Corollaries:

\begin{code}

â„•âˆ-compact : compact â„•âˆ
â„•âˆ-compact = compactâˆ™-gives-compact â„•âˆ-compactâˆ™

â„•âˆ-Compact : Compact â„•âˆ {ğ“¤}
â„•âˆ-Compact = compact-gives-Compact â„•âˆ-compact

â„•âˆâ†’â„•-is-discrete : is-discrete (â„•âˆ â†’ â„•)
â„•âˆâ†’â„•-is-discrete = compact-discrete-discrete fe â„•âˆ-compact (Î» u â†’ â„•-is-discrete)

â„•âˆâ†’ğŸš-is-discrete : is-discrete (â„•âˆ â†’ ğŸš)
â„•âˆâ†’ğŸš-is-discrete = compact-discrete-discrete fe â„•âˆ-compact (Î» u â†’ ğŸš-is-discrete)

module _ (fe' : FunExt) (pt : propositional-truncations-exist) where

 open import WeaklyCompactTypes fe' pt

 â„•âˆ-is-âˆƒ-compact : âˆƒ-compact â„•âˆ
 â„•âˆ-is-âˆƒ-compact = compact-types-are-âˆƒ-compact â„•âˆ-compact

 â„•âˆ-is-Î -compact : Î -compact â„•âˆ
 â„•âˆ-is-Î -compact = âˆƒ-compact-gives-Î -compact â„•âˆ-is-âˆƒ-compact

\end{code}
