Martin Escardo, December 2012, based on earlier work, circa 2010.

Searchable ordinals via squashed sums (without using the Cantor space). 

We can define plenty of searchable sets by transfinitely iterating
squashed sums. These are countable sums with an added limit point at
infinity (see the module SquashedSum).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-} 

open import UF-FunExt

module SearchableOrdinals (fe : âˆ€ U V â†’ funext U V) where

open import SpartanMLTT
open import SquashedSum (fe)
open import SearchableTypes

\end{code}

We use ordinal encodings, or ordinal expressions, that are slightly
different from the traditional "Brouwer ordinals".

\begin{code}

data OE : Uâ‚€ Ì‡ where
  One           : OE 
  Add           : OE â†’ OE â†’ OE
  Mul           : OE â†’ OE â†’ OE
  Sum-plus-One  : (â„• â†’ OE) â†’ OE 

\end{code}

The above are searchable-ordinal codes or expressions.

(The empty ordinal is excluded because it is not searchable. It is
merely exhaustible or omniscient (see the module Searchable for a
partial discussion of this). The reason why including the empty
ordinal causes insurmountable problems is discussed in research papers.)

The decoding function (or semantic interpretation, or evaluation
function) is this, with valued on topped ordinals, where an ordinal is
a type equipped with a proposition-valued, well-founded, transitive
and extensional relation (and such a type is automatically a
set). "Topped" means that there is a top element in the order

This version of the function is from 1st July 2018 (the original
version considered only the underlying set of the ordinal and didn't
construct the order as this was work in progress):

\begin{code}

open import Ordinals fe

ord : OE â†’ TOrd
ord           One    = ğŸ™Âº
ord      (Add Î± Î²)   = ord Î± +Âº ord Î² 
ord      (Mul Î± Î²)   = ord Î± Ã—Âº  ord Î² 
ord (Sum-plus-One Î±) = âˆ‘Â¹ \(i : â„•) â†’ ord(Î± i)

usearchable-ord : (Î± : OE) â†’ usearchable(ord Î±)
usearchable-ord           One  = ğŸ™-usearchable
usearchable-ord      (Add Î± Î²) = +usearchable (ord Î±) (ord Î²)
                                   (usearchable-ord Î±)
                                   (usearchable-ord Î²)
usearchable-ord      (Mul Î± Î²) = Ã—usearchable (ord Î±) (ord Î²)
                                   (usearchable-ord Î±)
                                   (usearchable-ord Î²) 
usearchable-ord (Sum-plus-One Î±) = âˆ‘Â¹-usearchable
                                       (Î» i â†’ ord (Î± i))
                                       (Î» i â†’ usearchable-ord(Î± i))

\end{code}

Classically, the squashed sum is the ordinal sum plus 1, and we have a
semantics with this interpretation, which gives ordinals with discrete
underlying sets. Moreover, there a function maps the underlying set of
the discrete version to the underlying set of the above version.

\begin{code}

{- TODO. Requires more work in other modules.
ord' : OE â†’ TOrd
ord'           One    = ğŸ™Âº
ord'      (Add Î± Î²)   = ord' Î± +Âº ord' Î² 
ord'      (Mul Î± Î²)   = ord' Î± Ã—Âº  ord' Î² 
ord' (Sum-plus-One Î±) = {!!} -- âˆ‘â‚ (Î» i â†’ ord'(Î± i')

udiscrete-ord' : (Î± : OE) â†’ udiscrete(ord' Î±)
udiscrete-ord'           One  = ğŸ™-udiscrete
udiscrete-ord'      (Add Î± Î²) = +udiscrete (ord' Î±) (ord' Î²) (udiscrete-ord' Î±) (udiscrete-ord' Î²)
udiscrete-ord'      (Mul Î± Î²) = Ã—udiscrete (ord' Î±) (ord' Î²) (udiscrete-ord' Î±) (udiscrete-ord' Î²) 
udiscrete-ord' (Sum-plus-One Î±) = {!!} -- âˆ‘â‚-udiscrete (Î» i â†’ ord (Î± i)) (Î» i â†’ udiscrete-ord'(Î± i))

ord'-ord : (Î± : OE) â†’ âŸª ord' Î± âŸ« â†’ âŸª ord Î± âŸ«
ord'-ord One = id
ord'-ord (Add Î± Î²) c = {!!}
ord'-ord (Mul Î± Î²) = {!!}
ord'-ord (Sum-plus-One Î±) = {!!} 
-}

\end{code}

Brouwer ordinal codes can be mapped to searchable ordinal codes, so
that the meaning is not necessarily preserved, but so that it is
bigger or equal.

\begin{code}

open import OrdinalCodes

brouwer-to-oe : B â†’ OE
brouwer-to-oe    Z  = One
brouwer-to-oe (S Î±) = Add One (brouwer-to-oe Î±)
brouwer-to-oe (L Î±) = Sum-plus-One(Î» i â†’ brouwer-to-oe(Î± i))

\end{code}

Relatively "small" example: a type which amounts to the ordinal Îµâ‚€ in set theory:

\begin{code}

Îµâ‚€-ordinal : TOrd
Îµâ‚€-ordinal = ord(brouwer-to-oe B-Îµâ‚€)

searchable-Îµâ‚€-ordinal : usearchable Îµâ‚€-ordinal
searchable-Îµâ‚€-ordinal = usearchable-ord(brouwer-to-oe B-Îµâ‚€)

\end{code}

We can go much higher using the work of many people, including Hancock
and Setzer.

To do: prove that these searchable types are really ordinals in the
sense of the paper "Infinite sets that satisfy the principle of
omniscience in all varieties of constructive mathematics".

That is: they are linearly ordered (in a suitable constructive sense),
and every decidable inhabited subset as a least element (found using
the selection function that exists by searchability). This is proved
in that paper for subsets of the Cantor space. This file constructs
the same ordinals but without having them inside the Cantor space, but
the proof (omitted here for the moment) is essentially the same.
