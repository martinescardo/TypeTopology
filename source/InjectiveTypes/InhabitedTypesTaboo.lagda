Tom de Jong & MartÃ­n EscardÃ³, 8 September 2023.

Formalising a discussion of 7 September.


As explained in InjectiveTypes.CounterExamples:
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
We already know that if excluded middle holds then all pointed types are
algebraicly injective, and that the converse also holds.

So we can't really give an example of any type which is not algebraicly
injective, other than the empty type. The best we can hope is to derive a
constructive taboo, rather than a contradiction, from the assumption that a type
of interest would be injective.

Most types one encounters in practice are "not" injective in the above sense.
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

We consider here the type ğ•€ of inhabited types: ğ•€ = Î£ X ê‰ ğ“¤ Ì‡ , âˆ¥ X âˆ¥ and show
that the following are equivalent:
(1) ğ•€ is injective.
(2) ğ•€ is a retract of ğ“¤.
(3) All propositions are projective:
      (P : ğ“¤ Ì‡  ) (Y : P â†’ ğ“¤ Ì‡  ) â†’ is-prop P
                                â†’ ((p : P) â†’ âˆ¥ Y p âˆ¥)
                                â†’ âˆ¥ (p : P) â†’ Y p âˆ¥.
(4) Every type has unspecified split support:
      (X : ğ“¤ Ì‡  ) â†’ âˆ¥ âˆ¥ X âˆ¥ â†’ X âˆ¥.

The equivalence of (3) and (4) was shown in [Theorem 7.7, 1].

As noted in [1], if Y p in statement (3) is a two-element set, then this is
known as the "world's simplest axiom of choice", which fails in some toposes, as
shown in [2].

Also notice that (3) (and thus, (4)) follows from excluded middle.

TODO: Explain how this gives an example of an injective Sigma-type whose index
      type is not injective.


References
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
[1] Nicolai Kraus, MartÃ­n EscardÃ³, Thierry Coquand and Thorsten Altenkirch.
    Notions of Anonymous Existence in Martin-LÃ¶f Type Theory.
    Logical Methods in Computer Science 13(1), pp. 1â”€36, 2017.
    https://doi.org/10.23638/LMCS-13(1:15)2017

[2] M. P. Fourman and A. Å Äedrov.
    The â€œworld's simplest axiom of choiceâ€ fails.
    Manuscripta Math 38, pp. 325â€“332, 1982.
    https://doi.org/10.1007/BF01170929

\begin{code}

{-# OPTIONS --safe --without-K --exact-split #-}

open import MLTT.Spartan
open import UF.Univalence
open import UF.PropTrunc

module InjectiveTypes.InhabitedTypesTaboo
        (pt : propositional-truncations-exist)
        (ua : Univalence)
        (ğ“¤ : Universe)
       where

open PropositionalTruncation pt

open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

 pe : PropExt
 pe = Univalence-gives-PropExt ua

 pe' : Prop-Ext
 pe' {ğ“¤} = pe ğ“¤

open import InjectiveTypes.Blackboard fe

ğ•€ : ğ“¤ âº Ì‡
ğ•€ = Î£ X ê‰ ğ“¤ Ì‡  , âˆ¥ X âˆ¥

Propositions-Are-Projective : ğ“¤ âº Ì‡
Propositions-Are-Projective = (P : ğ“¤ Ì‡  ) (Y : P â†’ ğ“¤ Ì‡  )
                            â†’ is-prop P
                            â†’ ((p : P) â†’ âˆ¥ Y p âˆ¥)
                            â†’ âˆ¥ ((p : P) â†’ Y p) âˆ¥

Unspecified-Split-Support : ğ“¤ âº Ì‡
Unspecified-Split-Support = (X : ğ“¤ Ì‡  ) â†’ âˆ¥ (âˆ¥ X âˆ¥ â†’ X) âˆ¥

unspecified-split-support-gives-retract : Unspecified-Split-Support
                                        â†’ retract ğ•€ of (ğ“¤ Ì‡  )
unspecified-split-support-gives-retract uss = Ï , Ïƒ , ÏÏƒ
 where
  Ïƒ : ğ•€ â†’ ğ“¤ Ì‡
  Ïƒ = prâ‚
  Ï  : ğ“¤ Ì‡ â†’ ğ•€
  Ï X = (âˆ¥ X âˆ¥ â†’ X) , uss X
  ÏÏƒ : Ï âˆ˜ Ïƒ âˆ¼ id
  ÏÏƒ (X , s) = to-subtype-ï¼ (Î» Y â†’ âˆ¥âˆ¥-is-prop)
                             (eqtoid (ua ğ“¤) (âˆ¥ X âˆ¥ â†’ X) X e)
   where
    e = (âˆ¥ X âˆ¥ â†’ X) â‰ƒâŸ¨ I âŸ©
        (ğŸ™{ğ“¤} â†’ X)    â‰ƒâŸ¨ â‰ƒ-sym (ğŸ™â†’ fe') âŸ©
        X             â– 
     where
      I = â†’cong'' fe' fe' (idtoeq âˆ¥ X âˆ¥ ğŸ™ II)
       where
        II : âˆ¥ X âˆ¥ ï¼ ğŸ™
        II = holds-gives-equal-ğŸ™ pe' âˆ¥ X âˆ¥ âˆ¥âˆ¥-is-prop s

retract-gives-injectivity : retract ğ•€ of (ğ“¤ Ì‡ )
                          â†’ ainjective-type ğ•€ ğ“¤ ğ“¤
retract-gives-injectivity ret = retract-of-ainjective ğ•€ (ğ“¤ Ì‡ ) inj ret
 where
  inj : ainjective-type (ğ“¤ Ì‡ ) ğ“¤ ğ“¤
  inj = universes-are-ainjective-Î  (ua ğ“¤)

flabbiness-gives-projective-propositions : aflabby ğ•€ ğ“¤
                                         â†’ Propositions-Are-Projective
flabbiness-gives-projective-propositions Ï• P Y P-is-prop Y-inh = I
 where
  f : P â†’ ğ•€
  f p = (Y p , Y-inh p)
  ext : Î£ X ê‰ ğ•€ , ((p : P) â†’ X ï¼ f p)
  ext = Ï• P P-is-prop f
  X : ğ“¤ Ì‡
  X = prâ‚ (prâ‚ ext)
  s : âˆ¥ X âˆ¥
  s = prâ‚‚ (prâ‚ ext)
  ext-property : (p : P) â†’ (X , s) ï¼ (Y p , Y-inh p)
  ext-property = prâ‚‚ ext
  ext-property' : (p : P) â†’ X ï¼ Y p
  ext-property' p = ap prâ‚ (ext-property p)

  II : X â†’ (p : P) â†’ Y p
  II x p = idtofun X (Y p) (ext-property' p) x

  I : âˆ¥ ((p : P) â†’ Y p) âˆ¥
  I = âˆ¥âˆ¥-functor II s

injectivity-gives-projective-propositions : ainjective-type ğ•€ ğ“¤ ğ“¤
                                          â†’ Propositions-Are-Projective
injectivity-gives-projective-propositions inj =
 flabbiness-gives-projective-propositions (ainjective-types-are-aflabby ğ•€ inj)

projective-propositions-gives-unspecified-split-support :
 Propositions-Are-Projective â†’ Unspecified-Split-Support
projective-propositions-gives-unspecified-split-support pap X =
 pap âˆ¥ X âˆ¥ (Î» _ â†’ X) âˆ¥âˆ¥-is-prop id

\end{code}

The above allows us to give an alternative (w.r.t. [1]), non-direct proof of the
following:

\begin{code}

unspecified-split-support-gives-projective-propositions :
 Unspecified-Split-Support â†’ Propositions-Are-Projective
unspecified-split-support-gives-projective-propositions uss =
 injectivity-gives-projective-propositions
  (retract-gives-injectivity
    (unspecified-split-support-gives-retract uss))

\end{code}
