Tom de Jong & MartÃ­n EscardÃ³, 8 & 10 September 2023.

Formalising a discussion of 7 September.


As explained in InjectiveTypes.CounterExamples:

â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

We already know that if excluded middle holds then all pointed types are
algebraically injective, and that the converse also holds.

So we can't really give an example of any type which is not algebraicxally
injective, other than the empty type. The best we can hope for is to derive a
constructive taboo, rather than a contradiction, from the assumption that a type
of interest would be injective.

Most types one encounters in practice are "not" injective in the above sense. We
can also say "not all types are injective in general", as there are some
âˆ-toposes which do satisfy excluded middle, as well as some âˆ-toposes which
don't, and we intend TypeTopology to apply to all âˆ-toposes, except when special
assumptions are made.

â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

Here we consider the type Inh  of inhabited types defined by

 Inh = Î£ X ê‰ ğ“¤ Ì‡ , âˆ¥ X âˆ¥

and show that the following are equivalent:

(1) Inh is injective.
(2) Inh is a retract of ğ“¤.
(3) All propositions are projective:
      (P : ğ“¤ Ì‡ ) (Y : P â†’ ğ“¤ Ì‡ ) â†’ is-prop P
                                â†’ ((p : P) â†’ âˆ¥ Y p âˆ¥)
                                â†’ âˆ¥ (p : P) â†’ Y p âˆ¥.
(4) Every type has unspecified split support:
      (X : ğ“¤ Ì‡ ) â†’ âˆ¥ âˆ¥ X âˆ¥ â†’ X âˆ¥.

The equivalence of (3) and (4) was shown in [Theorem 7.7, 1].

As noted in [1], if Y p in statement (3) is a two-element set, then this is
known as the "world's simplest axiom of choice", which fails in some toposes, as
shown in [2].

Also notice that (3) (and thus, (4)) follows from excluded middle.


It is noteworthy that this yields an example of an injective Î£-type whose index
type is "not" injective, as follows: The type of pointed types
  ğ“¤âˆ™ = Î£ X ê‰ ğ“¤ Ì‡ , X
is injective, as proved in InjectiveTypes.MathematicalStructures, and is
(equivalent) to the Î£-type
  Î£ I ê‰ Inh , prâ‚ Inh,
indexed over the "non"-injective type Inh.


To illustrate the constructive differences between the propositional truncation
and the double negation, we point out that the type of non-empty types *is*
injective, as shown in InjectiveTypes.NonEmptyTypes.


References
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
[1] Nicolai Kraus, MartÃ­n EscardÃ³, Thierry Coquand and Thorsten Altenkirch.
    Notions of Anonymous Existence in Martin-LÃ¶f Type Theory.
    Logical Methods in Computer Science 13(1), pp. 1â”€36, 2017.
    https://doi.org/10.23638/LMCS-13(1:15)2017

[2] M. P. Fourman and A. Å Äedrov.
    The â€œworld's simplest axiom of choiceâ€ fails.
    Manuscripta Math 38, pp. 325â€“332, 1982.
    https://doi.org/10.1007/BF01170929

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan
open import UF.Univalence
open import UF.PropTrunc

module InjectiveTypes.InhabitedTypesTaboo
        (pt : propositional-truncations-exist)
        (ua : Univalence)
        (ğ“¤ : Universe)
       where

open PropositionalTruncation pt


open import UF.Base
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Retracts
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

 pe : PropExt
 pe = Univalence-gives-PropExt ua

 pe' : Prop-Ext
 pe' {ğ“¤} = pe ğ“¤

open import InjectiveTypes.Blackboard fe
open import InjectiveTypes.MathematicalStructures ua

\end{code}

We define the type of inhabited of types (in a fixed, but arbitrary universe ğ“¤).
For convenience we also write Inh for this type in this file.

\begin{code}

Inhabited : ğ“¤ âº Ì‡
Inhabited = Î£ X ê‰ ğ“¤ Ì‡ , âˆ¥ X âˆ¥

private
 Inh : ğ“¤ âº Ì‡
 Inh = Inhabited

âŸ¨_âŸ© : Inh â†’ ğ“¤ Ì‡
âŸ¨_âŸ© = prâ‚

\end{code}

We define the two (equivalent) choice principles, labelled (3) and (4) at the
top of this file.

\begin{code}

Propositions-Are-Projective : ğ“¤ âº Ì‡
Propositions-Are-Projective = (P : ğ“¤ Ì‡ ) (Y : P â†’ ğ“¤ Ì‡ )
                            â†’ is-prop P
                            â†’ ((p : P) â†’ âˆ¥ Y p âˆ¥)
                            â†’ âˆ¥ ((p : P) â†’ Y p) âˆ¥

Unspecified-Split-Support : ğ“¤ âº Ì‡
Unspecified-Split-Support = (X : ğ“¤ Ì‡ ) â†’ âˆ¥ (âˆ¥ X âˆ¥ â†’ X) âˆ¥

\end{code}

We now prove the equivalence of statemements (1)â”€(4) and summarise the chain of
implications at the end.

\begin{code}

unspecified-split-support-gives-retract : Unspecified-Split-Support
                                        â†’ retract Inh of (ğ“¤ Ì‡ )
unspecified-split-support-gives-retract uss = Ï , Ïƒ , ÏÏƒ
 where
  Ïƒ : Inh â†’ ğ“¤ Ì‡
  Ïƒ = âŸ¨_âŸ©
  Ï  : ğ“¤ Ì‡ â†’ Inh
  Ï X = (âˆ¥ X âˆ¥ â†’ X) , uss X
  ÏÏƒ : Ï âˆ˜ Ïƒ âˆ¼ id
  ÏÏƒ (X , s) = to-subtype-ï¼ (Î» Y â†’ âˆ¥âˆ¥-is-prop)
                             (eqtoid (ua ğ“¤) (âˆ¥ X âˆ¥ â†’ X) X e)
   where
    e = (âˆ¥ X âˆ¥ â†’ X) â‰ƒâŸ¨ I âŸ©
        (ğŸ™{ğ“¤} â†’ X)    â‰ƒâŸ¨ â‰ƒ-sym (ğŸ™â†’ fe') âŸ©
        X             â– 
     where
      I = â†’cong'' fe' fe' (idtoeq âˆ¥ X âˆ¥ ğŸ™ II)
       where
        II : âˆ¥ X âˆ¥ ï¼ ğŸ™
        II = holds-gives-equal-ğŸ™ pe' âˆ¥ X âˆ¥ âˆ¥âˆ¥-is-prop s

retract-gives-injectivity : retract Inh of (ğ“¤ Ì‡ )
                          â†’ ainjective-type Inh ğ“¤ ğ“¤
retract-gives-injectivity ret = retract-of-ainjective Inh (ğ“¤ Ì‡ ) inj ret
 where
  inj : ainjective-type (ğ“¤ Ì‡ ) ğ“¤ ğ“¤
  inj = universes-are-ainjective-Î  (ua ğ“¤)

flabbiness-gives-projective-propositions : aflabby Inh ğ“¤
                                         â†’ Propositions-Are-Projective
flabbiness-gives-projective-propositions Ï• P Y P-is-prop Y-inh = I
 where
  f : P â†’ Inh
  f p = (Y p , Y-inh p)
  ext : Î£ X ê‰ Inh , ((p : P) â†’ X ï¼ f p)
  ext = Ï• P P-is-prop f
  X : ğ“¤ Ì‡
  X = prâ‚ (prâ‚ ext)
  s : âˆ¥ X âˆ¥
  s = prâ‚‚ (prâ‚ ext)
  ext-property : (p : P) â†’ (X , s) ï¼ (Y p , Y-inh p)
  ext-property = prâ‚‚ ext
  ext-property' : (p : P) â†’ X ï¼ Y p
  ext-property' p = ap âŸ¨_âŸ© (ext-property p)

  II : X â†’ (p : P) â†’ Y p
  II x p = idtofun X (Y p) (ext-property' p) x

  I : âˆ¥ ((p : P) â†’ Y p) âˆ¥
  I = âˆ¥âˆ¥-functor II s

injectivity-gives-projective-propositions : ainjective-type Inh ğ“¤ ğ“¤
                                          â†’ Propositions-Are-Projective
injectivity-gives-projective-propositions inj =
 flabbiness-gives-projective-propositions (ainjective-types-are-aflabby Inh inj)

projective-propositions-gives-unspecified-split-support :
 Propositions-Are-Projective â†’ Unspecified-Split-Support
projective-propositions-gives-unspecified-split-support pap X =
 pap âˆ¥ X âˆ¥ (Î» _ â†’ X) âˆ¥âˆ¥-is-prop id

\end{code}

For convenience, we provide a summary of the chain of implications:

\begin{code}

summary : (Unspecified-Split-Support â†’ retract Inh of (ğ“¤ Ì‡ ))
        Ã— (retract Inh of (ğ“¤ Ì‡ ) â†’ ainjective-type Inh ğ“¤ ğ“¤)
        Ã— (ainjective-type Inh ğ“¤ ğ“¤ â†’ Propositions-Are-Projective)
        Ã— (Propositions-Are-Projective â†’ Unspecified-Split-Support)
summary = unspecified-split-support-gives-retract
        , retract-gives-injectivity
        , injectivity-gives-projective-propositions
        , projective-propositions-gives-unspecified-split-support

\end{code}

Observe that the concatenation of the first three implications yields an
alternative (w.r.t. [1]), non-direct proof of the following:

\begin{code}

unspecified-split-support-gives-projective-propositions :
 Unspecified-Split-Support â†’ Propositions-Are-Projective
unspecified-split-support-gives-projective-propositions uss =
 injectivity-gives-projective-propositions
  (retract-gives-injectivity
    (unspecified-split-support-gives-retract uss))

\end{code}

Finally, we recall that the type ğ“¤âˆ™ of pointed types *is* injective
and record that ğ“¤âˆ™ is equivalent to the Î£-type

  Î£ I ê‰ Inh , âŸ¨ I âŸ©,

which is indexed over the "non"-injective type Inh.

Hence this gives an example of an injective Î£-type whose indexing type
is not necessarily injective.

\begin{code}

private
 ğ“¤âˆ™ : ğ“¤ âº Ì‡
 ğ“¤âˆ™ = Î£ X ê‰ ğ“¤ Ì‡ , X

ğ“¤âˆ™-is-injective : ainjective-type ğ“¤âˆ™ ğ“¤ ğ“¤
ğ“¤âˆ™-is-injective = ainjectivity-of-type-of-pointed-types

ğ“¤âˆ™-as-Î£-type-over-Inh : ğ“¤âˆ™ â‰ƒ (Î£ I ê‰ Inh , âŸ¨ I âŸ©)
ğ“¤âˆ™-as-Î£-type-over-Inh = ğ“¤âˆ™                    â‰ƒâŸ¨ Î£-cong e âŸ©
                        (Î£ X ê‰ ğ“¤ Ì‡ , âˆ¥ X âˆ¥ Ã— X) â‰ƒâŸ¨ â‰ƒ-sym Î£-assoc âŸ©
                        (Î£ I ê‰ Inh , âŸ¨ I âŸ©)    â– 
 where
  e : (X : ğ“¤ Ì‡ ) â†’ X â‰ƒ âˆ¥ X âˆ¥ Ã— X
  e X = qinveq f (g , Î· , Îµ)
   where
    f : X â†’ âˆ¥ X âˆ¥ Ã— X
    f x = âˆ£ x âˆ£ , x
    g : âˆ¥ X âˆ¥ Ã— X â†’ X
    g = prâ‚‚
    Î· : g âˆ˜ f âˆ¼ id
    Î· x = refl
    Îµ : f âˆ˜ g âˆ¼ id
    Îµ (s , x) = to-Ã—-ï¼ (âˆ¥âˆ¥-is-prop âˆ£ x âˆ£ s) refl

example-of-injective-sum-whose-index-type-may-not-be-injective
 : Î£ X ê‰ (ğ“¤ âº) Ì‡
 , Î£ Y ê‰ (X â†’ ğ“¤ Ì‡ )
 , ainjective-type (Î£ Y) ğ“¤ ğ“¤
 Ã— (ainjective-type X ğ“¤ ğ“¤ â†’ Propositions-Are-Projective)
example-of-injective-sum-whose-index-type-may-not-be-injective
 = Inh , âŸ¨_âŸ© , A-ainj , injectivity-gives-projective-propositions
 where
  A = Î£ I ê‰ Inh , âŸ¨ I âŸ©

  A-ainj : ainjective-type A ğ“¤ ğ“¤
  A-ainj = equiv-to-ainjective
            A
            ğ“¤âˆ™
            ğ“¤âˆ™-is-injective
            (â‰ƒ-sym ğ“¤âˆ™-as-Î£-type-over-Inh)

\end{code}
