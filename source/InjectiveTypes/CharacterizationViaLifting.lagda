Martin Escardo and Tom de Jong 7th - 21st November 2024.

We improve the universe levels of Lemma 14 of [1], which corresponds
to `embedding-retract` from InjectiveTypes/Blackboard.

We use this to remove resizing assumption of Theorem 51 of [1], which
characterizes the algebraically injective types as the retracts of the
algebras of the lifiting monad (also known as the partial-map
classifier monad.

[1] M.H. EscardÃ³. Injective type in univalent mathematics.
    https://doi.org/10.1017/S0960129520000225

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module InjectiveTypes.CharacterizationViaLifting (fe : FunExt) where

open import InjectiveTypes.Blackboard fe
open import InjectiveTypes.OverSmallMaps fe
open import MLTT.Spartan
open import UF.Equiv
open import UF.Size
open import UF.Subsingletons

private
 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

\end{code}

We first improve the universe levels of Blackboard.ainjectivity-of-Lifting.

\begin{code}

open import UF.Univalence

module ainjectivity-of-Lifting'
        (ğ“£ : Universe)
        (ua : is-univalent ğ“£)
       where

 private
  pe : propext ğ“£
  pe = univalence-gives-propext ua

 open ainjectivity-of-Lifting ğ“£

 open import Lifting.UnivalentWildCategory ğ“£
 open import UF.Retracts

 Î·-is-small-map : {X : ğ“¤ Ì‡ } â†’ (Î· âˆ¶ (X â†’ ğ“› X)) is ğ“£ small-map
 Î·-is-small-map {ğ“¤} {X} l = is-defined l ,
                            â‰ƒ-sym (Î·-fiber-same-as-is-defined X pe fe' fe' fe' l)

\end{code}

The following improves the universe levels of Lemma 50 of [1].

\begin{code}

 ainjective-is-retract-of-free-ğ“›-algebra' : ({ğ“¤} ğ“¥ {ğ“¦} : Universe)
                                            (D : ğ“¤ Ì‡ )
                                          â†’ ainjective-type D (ğ“£ âŠ” ğ“¥) ğ“¦
                                          â†’ retract D of (ğ“› D)
 ainjective-is-retract-of-free-ğ“›-algebra' {ğ“¤} ğ“¥ D =
  embedding-retract' ğ“¥ D (ğ“› D) Î·
   (Î·-is-embedding' ğ“¤ D ua fe')
   Î·-is-small-map

 ainjectives-in-terms-of-free-ğ“›-algebras'
  : (D : ğ“¤ Ì‡ ) â†’ ainjective-type D ğ“£ ğ“£ â†” (Î£ X ê‰ ğ“¤ Ì‡ , retract D of (ğ“› X))
 ainjectives-in-terms-of-free-ğ“›-algebras' {ğ“¤} D = a , b
  where
   a : ainjective-type D ğ“£ ğ“£ â†’ Î£ X ê‰ ğ“¤ Ì‡ , retract D of (ğ“› X)
   a i = D , ainjective-is-retract-of-free-ğ“›-algebra' ğ“£ D i

   b : (Î£ X ê‰ ğ“¤ Ì‡ , retract D of (ğ“› X)) â†’ ainjective-type D ğ“£ ğ“£
   b (X , r) = retract-of-ainjective D (ğ“› X) (free-ğ“›-algebra-ainjective ua X) r

\end{code}

A particular case of interest that arises in practice is the following.

\begin{code}

 ainjectives-in-terms-of-free-ğ“›-algebrasâº
  : (D : ğ“£ âº Ì‡ ) â†’ ainjective-type D ğ“£ ğ“£ â†” (Î£ X ê‰ ğ“£ âº Ì‡ , retract D of (ğ“› X))
 ainjectives-in-terms-of-free-ğ“›-algebrasâº
  = ainjectives-in-terms-of-free-ğ“›-algebras'

 _ : {X : ğ“£ âº Ì‡ } â†’ type-of (ğ“› X) ï¼ ğ“£ âº Ì‡
 _ = refl

\end{code}

The following removes the resizing assumption of Theorem 51 of [1].

\begin{code}

 ainjectives-in-terms-of-ğ“›-algebras
  : (D : ğ“¤ Ì‡ ) â†’ ainjective-type D ğ“£ ğ“£ â†” (Î£ A ê‰ ğ“£ âº âŠ” ğ“¤ Ì‡ , ğ“›-alg A Ã— retract D of A)
 ainjectives-in-terms-of-ğ“›-algebras {ğ“¤} D = a , b
  where
   a : ainjective-type D ğ“£ ğ“£ â†’ (Î£ A ê‰ ğ“£ âº âŠ” ğ“¤ Ì‡ , ğ“›-alg A Ã— retract D of A)
   a i = ğ“› D ,
         ğ“›-algebra-gives-alg (free-ğ“›-algebra ua D) ,
         ainjective-is-retract-of-free-ğ“›-algebra' ğ“£ D i

   b : (Î£ A ê‰ ğ“£ âº âŠ” ğ“¤ Ì‡ , ğ“›-alg A Ã— retract D of A) â†’ ainjective-type D ğ“£ ğ“£
   b (A , Î± , Ï) = retract-of-ainjective D A (ğ“›-alg-ainjective pe A Î±) Ï

\end{code}

Particular case of interest:

\begin{code}

 ainjectives-in-terms-of-ğ“›-algebrasâº
  : (D : ğ“£ âº Ì‡ ) â†’ ainjective-type D ğ“£ ğ“£ â†” (Î£ A ê‰ ğ“£ âº Ì‡ , ğ“›-alg A Ã— retract D of A)
 ainjectives-in-terms-of-ğ“›-algebrasâº = ainjectives-in-terms-of-ğ“›-algebras

\end{code}
