Martin Escardo, 16th August 2023

We give a sufficient condition for types of mathematical structures,
such as pointed types, âˆ-magmas, monoids, groups, etc. to be
algebraically injective. We use algebraic flabbiness as our main tool.

This file is subsumed by [1] and [2], but it is still important for
both the sake of motivation and the fact that is includes useful
discussion, which probably should be read before reading [1] and [2].

[1] InjectiveTypes.Sigma
[2] InjectiveTypes.MathematicalStructuresMoreGeneral

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.Univalence

module InjectiveTypes.MathematicalStructures (ua : Univalence) where

open import UF.FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import InjectiveTypes.Blackboard fe
open import MLTT.Spartan
open import Taboos.Decomposability ua
open import UF.Base
open import UF.Equiv
open import UF.ExcludedMiddle
open import UF.PropIndexedPiSigma
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.SubtypeClassifier

\end{code}

We already know the following, but here is a short direct proof.

\begin{code}

universes-are-aflabby-Î  : aflabby (ğ“¤ Ì‡ ) ğ“¤
universes-are-aflabby-Î  {ğ“¤} P P-is-prop A = Î  A , I
 where
  X : ğ“¤  Ì‡
  X = Î  A

  I : (p : P) â†’ Î  A ï¼ A p
  I = Î» p â†’ eqtoid (ua ğ“¤) (Î  A) (A p) (prop-indexed-product fe' P-is-prop p)

universes-are-injective-Î  : ainjective-type (ğ“¤ Ì‡ ) ğ“¤ ğ“¤
universes-are-injective-Î  {ğ“¤} = aflabby-types-are-ainjective (ğ“¤ Ì‡ )
                                  universes-are-aflabby-Î 

universes-are-aflabby-Î£ : aflabby (ğ“¤ Ì‡ ) ğ“¤
universes-are-aflabby-Î£ {ğ“¤} P P-is-prop A = Î£ A , I
 where
  X : ğ“¤  Ì‡
  X = Î£ A

  I : (p : P) â†’ Î£ A ï¼ A p
  I = Î» p â†’ eqtoid (ua ğ“¤) (Î£ A) (A p) (prop-indexed-sum P-is-prop p)

\end{code}

We now want to show that several types of mathematical structures are
(algebraically) injective, or, equivalently, (algebraically) flabby.

We work with an arbitrary S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ and want to show that Î£ S is
flabby. E.g. for âˆ-magmas, we will have S X = X â†’ X â†’ X.

Let f : P â†’ Î£ S be a "partial element" where P is a proposition. Then
f is of the form

 f h = A h , B h

with A : P â†’ ğ“¤ Ì‡ and B : (h : P) â†’ S (A h).

We need to construct a (total) element (X , s) of Î£ S, with s : S X ,
such that for all h : P we have that (X , s) = (A h , B h).

This forces X = A h for any h : P. We have a fiberwise equivalence

 Ï€ : (h : P) â†’ Î  A â‰ƒ A h

By univalence, Ï€ induces a fiberwise identification

 Ï• : (h : P) â†’ Î  A ï¼ A h.

Hence we can take X to be Î  A.

To construct s, we need an assumption on S.

Roughly, our assumption is that S is closed under proposition-indexed
products, in the sense that from an element of the type
(h : P) â†’ S (A h) we can get an element of the type S (Î  A).

More precisely, we always have a map

 Ï : S (Î  A) â†’ ((h : P) â†’ S (A h))

in the opposite direction, and we stipulate that it is an equivalence
for any proposition P and any type family A of types indexed by P.

With this assumption, we can let the element s be the inverse of Ï
applied to B.

Remark. With regards to the discussion in the introduction of this
file, it is actually enough to require that Ï is has a section.

\begin{code}

module _ (S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ ) where

 treq : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y
 treq {X} {Y} ğ•— = transport S (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We don't need this fact explicitly, but it is worth keeping it in
mind:

\begin{code}

 treq-is-equiv : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y) â†’ is-equiv (treq ğ•—)
 treq-is-equiv {X} {Y} ğ•— = transports-are-equivs (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We now define "canonical maps" Ï€, Ï• and Ï parametrized by a
proposition p and family A indexed by p.

\begin{code}

 module canonical-map
         (p : Î© ğ“¤)
         (A : p holds â†’ ğ“¤ Ì‡)
         where

  hp : is-prop (p holds)
  hp = holds-is-prop p

  Ï€ : (h : p holds) â†’ Î  A â‰ƒ A h
  Ï€ = prop-indexed-product fe' hp

  remark-Ï€ : (h : p holds) (Î± : Î  A)
           â†’ âŒœ Ï€ h âŒ Î± ï¼ Î± h
  remark-Ï€ h Î± = refl

  remark-Ï€â»Â¹ : (h : p holds) (a : A h)
             â†’ âŒœ Ï€ h âŒâ»Â¹ a ï¼ Î» h' â†’ transport A (hp h h') a
  remark-Ï€â»Â¹ h a = refl

  Ï• : (h : p holds) â†’ Î  A ï¼ A h
  Ï• h = eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)

  Ï : S (Î  A) â†’ ((h : p holds) â†’ S (A h))
  Ï s h = treq (Ï€ h) s

  remark-Ï : (s : S (Î  A)) (h : p holds)
           â†’ Ï s h ï¼ transport S (eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)) s
  remark-Ï s h = refl

\end{code}

Our assumption on S is that the map

  Ï p A : S (Î  A) â†’ ((h : p holds) â†’ S (A h))

is an equivalence for every p and A.

\begin{code}

 closed-under-prop-Î  : ğ“¤ âº âŠ” ğ“¥ Ì‡
 closed-under-prop-Î  = (p : Î© ğ“¤)
                       (A : p holds â†’ ğ“¤ Ì‡)
                     â†’ is-equiv (Ï p A)
  where
   open canonical-map

\end{code}

And the main lemma, under this assumption, is that Î¡ S is algebraically
flabby with with respect to the universe ğ“¤.

\begin{code}

 aflabbiness-of-type-of-structured-types : closed-under-prop-Î 
                                         â†’ aflabby (Î£ S) ğ“¤
 aflabbiness-of-type-of-structured-types Ï-is-equiv = I
  where
   I : aflabby (Î£ S) ğ“¤
   I P P-is-prop f = (Î  A , s) , II
    where
     p : Î© ğ“¤
     p = (P , P-is-prop)

     have-f : p holds â†’ Î£ S
     have-f = f

     A : p holds â†’ ğ“¤ Ì‡
     A = prâ‚ âˆ˜ f

     open canonical-map p A

     e : S (Î  A) â‰ƒ ((h : p holds) â†’ S (A h))
     e = Ï , Ï-is-equiv p A

     g : (h : P) â†’ S (A h)
     g = prâ‚‚ âˆ˜ f

     s : S (Î  A)
     s = âŒœ e âŒâ»Â¹ g

     II : (h : p holds) â†’ Î  A , s ï¼ f h
     II h = Î  A , s   ï¼âŸ¨ to-Î£-ï¼ (Ï• h , III) âŸ©
            A h , g h ï¼âŸ¨ refl âŸ©
            f h       âˆ
      where
       III = transport S (Ï• h) s ï¼âŸ¨ refl âŸ©
             âŒœ e âŒ s h           ï¼âŸ¨ refl âŸ©
             âŒœ e âŒ (âŒœ e âŒâ»Â¹ g) h ï¼âŸ¨ IV âŸ©
             g h                 âˆ
        where
         IV = ap (Î» - â†’ - h) (inverses-are-sections' e g)

\end{code}

It follows that the type Î£ S is algebraically injective if S is closed
under proposition-indexed products, which is our main theorem.

\begin{code}

 ainjectivity-of-type-of-structures : closed-under-prop-Î 
                                    â†’ ainjective-type (Î£ S) ğ“¤ ğ“¤
 ainjectivity-of-type-of-structures = aflabby-types-are-ainjective (Î£ S)
                                      âˆ˜ aflabbiness-of-type-of-structured-types

\end{code}

Our assumption of closure under proposition-indexed products may be
difficult to check directly, because it involves transport along an
identification induced by an equivalence by univalence.

In practice, however, we are often able to construct T and T-refl
below, for S of interest, without using transport.

\begin{code}

 module _ (T      : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y)
          (T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id)
        where

\end{code}

The point is that any such T can be equivalently expressed as a
transport and hence we may apply the above theorem, but it may be
easier to check closure under products using T rather than transport
(see examples below).

\begin{code}

  transport-eqtoid : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y)
                   â†’ T ğ•— âˆ¼ treq ğ•—
  transport-eqtoid {X} {Y} ğ•— s = JEq (ua ğ“¤) X A I Y ğ•—
   where
    A : (Y : ğ“¤ Ì‡) (ğ•— : X â‰ƒ Y) â†’ ğ“¥ Ì‡
    A Y ğ•— = T ğ•— s ï¼ treq ğ•— s

    I : A X (â‰ƒ-refl X)
    I = T (â‰ƒ-refl X) s                                ï¼âŸ¨ T-refl s âŸ©
        s                                             ï¼âŸ¨ refl âŸ©
        transport S refl s                            ï¼âŸ¨ II âŸ©
        transport S (eqtoid (ua ğ“¤) X X (â‰ƒ-refl X)) s  ï¼âŸ¨ refl âŸ©
        treq (â‰ƒ-refl X) s                             âˆ
      where
       II = (ap (Î» - â†’ transport S - s) (eqtoid-refl (ua ğ“¤) X))â»Â¹

\end{code}

Hence our condition on S formulated with transports can be
equivalently formulated with T:

\begin{code}

  module canonical-map'
          (p : Î© ğ“¤)
          (A : p holds â†’ ğ“¤ Ì‡)
          where

   open canonical-map p A public

   Ï„ : S (Î  A) â†’ (h : p holds) â†’ S (A h)
   Ï„ s h = T (Ï€ h) s

   Ï-and-Ï„-agree : Ï âˆ¼ Ï„
   Ï-and-Ï„-agree s =
    Ï s                                                     ï¼âŸ¨ refl âŸ©
    (Î» h â†’ transport S (eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)) s) ï¼âŸ¨ I âŸ©
    (Î» h â†’ T (Ï€ h) s)                                       ï¼âŸ¨ refl âŸ©
    Ï„ s                                                     âˆ
    where
     I = dfunext fe' (Î» h â†’ (transport-eqtoid (Ï€ h) s)â»Â¹)

  closed-under-prop-Î ' : ğ“¤ âº âŠ” ğ“¥ Ì‡
  closed-under-prop-Î ' = (p : Î© ğ“¤)
                         (A : p holds â†’ ğ“¤ Ì‡)
                       â†’ is-equiv (Ï„ p A)
   where
    open canonical-map'

  Î -closure-criterion : closed-under-prop-Î '
                      â†’ closed-under-prop-Î 
  Î -closure-criterion Ï„-is-equiv p A =
   equiv-closed-under-âˆ¼ Ï„ Ï (Ï„-is-equiv p A) Ï-and-Ï„-agree
   where
    open canonical-map' p A

  Î -closure-criterion-converse : closed-under-prop-Î 
                               â†’ closed-under-prop-Î '
  Î -closure-criterion-converse Ï-is-equiv p A =
   equiv-closed-under-âˆ¼ Ï Ï„ (Ï-is-equiv p A) (âˆ¼-sym Ï-and-Ï„-agree)
   where
    open canonical-map' p A

\end{code}

Example. The type of pointed types is algebraically injective.

\begin{code}

Pointed-type : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Pointed-type ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , X

Pointed : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Pointed X = X

Pointed-is-closed-under-prop-Î  : closed-under-prop-Î  (Pointed {ğ“¤})
Pointed-is-closed-under-prop-Î  {ğ“¤} =
  Î -closure-criterion Pointed T T-refl c
 where
  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ X â†’ Y
  T = âŒœ_âŒ

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl x = refl

  c : closed-under-prop-Î ' Pointed T T-refl
  c p A = id-is-equiv (Î  A)

ainjectivity-of-type-of-pointed-types : ainjective-type (Pointed-type ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-type-of-pointed-types {ğ“¤} =
 ainjectivity-of-type-of-structures Pointed Pointed-is-closed-under-prop-Î 

\end{code}

Example. The type of âˆ-magmas is algebraically injective. The proof is
an entirely routine application of the above general theorem after we
guess what T should be.

\begin{code}

âˆ-Magma : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magma ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X)

âˆ-Magma-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magma-structure = Î» X â†’ X â†’ X â†’ X

âˆ-Magma-structure-is-closed-under-prop-Î  : closed-under-prop-Î 
                                            (âˆ-Magma-structure {ğ“¤})
âˆ-Magma-structure-is-closed-under-prop-Î  {ğ“¤} =
 Î -closure-criterion S T T-refl Ï„-is-equiv
 where
  S = âˆ-Magma-structure

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y
  T ğ•— _Â·_ = Î» y y' â†’ âŒœ ğ•— âŒ (âŒœ ğ•— âŒâ»Â¹ y Â· âŒœ ğ•— âŒâ»Â¹ y')

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl _Â·_ = dfunext fe' (Î» x â†’ dfunext fe' (Î» x' â†’ refl))

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡)
         where

   open canonical-map' S T T-refl p A

   Ï„â»Â¹ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ï„â»Â¹ g Î± Î² h = g h (âŒœ Ï€ h âŒ Î±) (âŒœ Ï€ h âŒ Î²)

   Î· : Ï„â»Â¹ âˆ˜ Ï„ âˆ¼ id
   Î· _Â·_ = dfunext fe' (Î» Î± â†’ dfunext fe' (I Î±))
    where
     I : âˆ€ Î± Î² â†’ Ï„â»Â¹ (Ï„ _Â·_) Î± Î² ï¼ Î± Â· Î²
     I Î± Î² =
      (Ï„â»Â¹ âˆ˜ Ï„) _Â·_ Î± Î²                                                ï¼âŸ¨ refl âŸ©
      (Î» h â†’ âŒœ Ï€ h âŒ  (âŒœ Ï€ h âŒâ»Â¹ (âŒœ Ï€ h âŒ Î±) Â· âŒœ Ï€ h âŒâ»Â¹ (âŒœ Ï€ h âŒ Î²))) ï¼âŸ¨ II âŸ©
      (Î» h â†’ âŒœ Ï€ h âŒ (Î± Â· Î²))                                          ï¼âŸ¨ refl âŸ©
      (Î» h â†’ (Î± Â· Î²) h)                                                ï¼âŸ¨ refl âŸ©
      Î± Â· Î²                                                            âˆ
      where
       II = dfunext fe' (Î» h â†’
             apâ‚‚ (Î» -â‚ -â‚‚ â†’ (-â‚ Â· -â‚‚) h)
                 (inverses-are-retractions' (Ï€ h) Î±)
                 (inverses-are-retractions' (Ï€ h) Î²))

   Îµ : Ï„ âˆ˜ Ï„â»Â¹ âˆ¼ id
   Îµ g =
    Ï„ (Ï„â»Â¹ g)                                                       ï¼âŸ¨ refl âŸ©
    (Î» h a b â†’ g h (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ a)) (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ b))) ï¼âŸ¨ I âŸ©
    (Î» h a b â†’ g h a b)                                             ï¼âŸ¨ refl âŸ©
    g                                                               âˆ
     where
      I = dfunext fe' (Î» h â†’ dfunext fe' (Î» a â†’ dfunext fe' (Î» b â†’
           apâ‚‚ (g h)
               (inverses-are-sections' (Ï€ h) a)
               (inverses-are-sections' (Ï€ h) b))))

   Ï„-is-equiv : is-equiv Ï„
   Ï„-is-equiv = qinvs-are-equivs Ï„ (Ï„â»Â¹ , Î· , Îµ)

ainjectivity-of-âˆ-Magma : ainjective-type (âˆ-Magma ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magma {ğ“¤} =
 ainjectivity-of-type-of-structures
  âˆ-Magma-structure
  âˆ-Magma-structure-is-closed-under-prop-Î 

\end{code}

A corollary is that the type âˆ-Magma ğ“¤ doesn't have any non-trivial
decidable property unless weak excluded middle holds.

\begin{code}

decomposition-of-âˆ-Magma-gives-WEM : decomposition (âˆ-Magma ğ“¤) â†’ WEM ğ“¤
decomposition-of-âˆ-Magma-gives-WEM {ğ“¤} =
 decomposition-of-ainjective-type-gives-WEM
  (âˆ-Magma ğ“¤)
  ainjectivity-of-âˆ-Magma

\end{code}

The same is true for the type of pointed types, of course, and for any
injective type.

We now want to consider more examples, such as monoids and groups. For
that purpose, we write combinators, like in UF.SIP, to show that
mathematical structures constructed from standard building blocks,
such as the above, form injective types.

\begin{code}

closure-under-prop-Î -Ã— :
      {ğ“¤ ğ“¥â‚ ğ“¥â‚‚ : Universe}
      {Sâ‚ : ğ“¤ Ì‡ â†’ ğ“¥â‚ Ì‡ } {Sâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¥â‚‚ Ì‡ }
    â†’ closed-under-prop-Î  Sâ‚
    â†’ closed-under-prop-Î  Sâ‚‚
    â†’ closed-under-prop-Î  (Î» X â†’ Sâ‚ X Ã— Sâ‚‚ X)

closure-under-prop-Î -Ã— {ğ“¤} {ğ“¥â‚} {ğ“¥â‚‚} {Sâ‚} {Sâ‚‚}
                       Ïâ‚-is-equiv Ïâ‚‚-is-equiv = Ï-is-equiv
 where
  S : ğ“¤ Ì‡ â†’ ğ“¥â‚ âŠ” ğ“¥â‚‚ Ì‡
  S X = Sâ‚ X Ã— Sâ‚‚ X

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡)
         where

   open canonical-map S  p A using (Ï ; Ï•)
   open canonical-map Sâ‚ p A renaming (Ï to Ïâ‚) using ()
   open canonical-map Sâ‚‚ p A renaming (Ï to Ïâ‚‚) using ()

   Ïâ‚â»Â¹ : ((h : p holds) â†’ Sâ‚ (A h)) â†’ Sâ‚ (Î  A)
   Ïâ‚â»Â¹ = inverse Ïâ‚ (Ïâ‚-is-equiv p A)

   Ïâ‚‚â»Â¹ : ((h : p holds) â†’ Sâ‚‚ (A h)) â†’ Sâ‚‚ (Î  A)
   Ïâ‚‚â»Â¹ = inverse Ïâ‚‚ (Ïâ‚‚-is-equiv p A)

   Ïâ»Â¹ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïâ»Â¹ Î± = Ïâ‚â»Â¹ (Î» h â†’ prâ‚ (Î± h)) , Ïâ‚‚â»Â¹ (Î» h â†’ prâ‚‚ (Î± h))

   Î· : Ïâ»Â¹ âˆ˜ Ï âˆ¼ id
   Î· (sâ‚ , sâ‚‚) =
    Ïâ»Â¹ (Ï (sâ‚ , sâ‚‚))                                         ï¼âŸ¨ refl âŸ©
    Ïâ»Â¹ (Î» h â†’ transport S (Ï• h) (sâ‚ , sâ‚‚))                   ï¼âŸ¨ I âŸ©
    Ïâ»Â¹ (Î» h â†’ transport Sâ‚ (Ï• h) sâ‚ , transport Sâ‚‚ (Ï• h) sâ‚‚) ï¼âŸ¨ refl âŸ©
    Ïâ‚â»Â¹ (Ïâ‚ sâ‚) , Ïâ‚‚â»Â¹ (Ïâ‚‚ sâ‚‚)                               ï¼âŸ¨ II âŸ©
    (sâ‚ , sâ‚‚)                                                 âˆ
     where
      I  = ap Ïâ»Â¹ (dfunext fe' (Î» h â†’ transport-Ã— Sâ‚ Sâ‚‚ (Ï• h)))
      II = apâ‚‚ _,_
              (inverses-are-retractions Ïâ‚ (Ïâ‚-is-equiv p A) sâ‚)
              (inverses-are-retractions Ïâ‚‚ (Ïâ‚‚-is-equiv p A) sâ‚‚)

   Îµ : Ï âˆ˜ Ïâ»Â¹ âˆ¼ id
   Îµ Î± = dfunext fe' I
    where
     Î±â‚ = Î» h â†’ prâ‚ (Î± h)
     Î±â‚‚ = Î» h â†’ prâ‚‚ (Î± h)

     I : Ï (Ïâ»Â¹ Î±) âˆ¼ Î±
     I h =
      Ï (Ïâ»Â¹ Î±) h                                                 ï¼âŸ¨ refl âŸ©
      transport S (Ï• h) (Ïâ‚â»Â¹ Î±â‚ , Ïâ‚‚â»Â¹ Î±â‚‚)                       ï¼âŸ¨ II âŸ©
      transport Sâ‚ (Ï• h) (Ïâ‚â»Â¹ Î±â‚) , transport Sâ‚‚ (Ï• h) (Ïâ‚‚â»Â¹ Î±â‚‚) ï¼âŸ¨ refl âŸ©
      Ïâ‚ (Ïâ‚â»Â¹ Î±â‚) h , Ïâ‚‚ (Ïâ‚‚â»Â¹ Î±â‚‚) h                             ï¼âŸ¨ III âŸ©
      Î±â‚ h , Î±â‚‚ h                                                 ï¼âŸ¨ refl âŸ©
      Î± h                                                         âˆ
       where
        II  = transport-Ã— Sâ‚ Sâ‚‚ (Ï• h)
        III = apâ‚‚ _,_
                 (ap (Î» - â†’ - h)
                     (inverses-are-sections Ïâ‚ (Ïâ‚-is-equiv p A) Î±â‚))
                 (ap (Î» - â†’ - h)
                     (inverses-are-sections Ïâ‚‚ (Ïâ‚‚-is-equiv p A) Î±â‚‚))

   Ï-is-equiv : is-equiv Ï
   Ï-is-equiv = qinvs-are-equivs Ï (Ïâ»Â¹ , Î· , Îµ)

\end{code}

Example. The type of pointed âˆ-magmas is injective.

\begin{code}

open import UF.SIP-Examples
open monoid

âˆ-Magmaâˆ™ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magmaâˆ™ ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X) Ã— X

âˆ-Magmaâˆ™-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magmaâˆ™-structure = monoid-structure

âˆ-Magmaâˆ™-structure-closed-under-Î  : closed-under-prop-Î  (âˆ-Magmaâˆ™-structure {ğ“¤})
âˆ-Magmaâˆ™-structure-closed-under-Î  =
 closure-under-prop-Î -Ã—
  âˆ-Magma-structure-is-closed-under-prop-Î 
  Pointed-is-closed-under-prop-Î 

ainjectivity-of-âˆ-Magmaâˆ™ : ainjective-type (âˆ-Magmaâˆ™ ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magmaâˆ™ {ğ“¤} =
 ainjectivity-of-type-of-structures
  âˆ-Magmaâˆ™-structure
  âˆ-Magmaâˆ™-structure-closed-under-Î 

\end{code}

We now want to add axioms to e.g. pointed âˆ-magmas to get monoids and
conclude that the type of monoids is injective.

\begin{code}

closure-under-prop-Î -with-axioms
 : (S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ )
   (Ï-is-equiv : closed-under-prop-Î  S)
   (axioms : (X : ğ“¤ Ì‡ ) â†’ S X â†’ ğ“¦ Ì‡ )
   (axioms-are-prop-valued : (X : ğ“¤ Ì‡) (s : S X) â†’ is-prop (axioms X s))
   (axioms-closed-under-prop-Î  :
          (p : Î© ğ“¤ )
          (A : p holds â†’ ğ“¤ Ì‡ )
        â†’ (Î± : (h : p holds) â†’ S (A h))
        â†’ ((h : p holds) â†’ axioms (A h) (Î± h))
        â†’ axioms (Î  A) (inverse (canonical-map.Ï S p A) (Ï-is-equiv p A) Î±))
 â†’ closed-under-prop-Î  (Î» X â†’ Î£ s ê‰ S X , axioms X s)
closure-under-prop-Î -with-axioms {ğ“¤} {ğ“¥} {ğ“¦}
                                 S
                                 Ï-is-equiv
                                 axioms
                                 axioms-are-prop-valued
                                 axioms-closed-under-prop-Î  = Ïâ‚-is-equiv
   where
    Sâ‚ : ğ“¤ Ì‡ â†’ ğ“¥ âŠ” ğ“¦ Ì‡
    Sâ‚ X = Î£ s ê‰ S X , axioms X s

    module _ (p : Î© ğ“¤)
             (A : p holds â†’ ğ“¤ Ì‡)
           where

     open canonical-map S  p A using (Ï ; Ï•)
     open canonical-map Sâ‚ p A renaming (Ï to Ïâ‚) using ()

     Ïâ»Â¹ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
     Ïâ»Â¹ = inverse Ï (Ï-is-equiv p A)

     Ïâ‚â»Â¹ : ((h : p holds) â†’ Sâ‚ (A h)) â†’ Sâ‚ (Î  A)
     Ïâ‚â»Â¹ Î± = Ïâ»Â¹ (Î» h â†’ prâ‚ (Î± h)) ,
              axioms-closed-under-prop-Î  p A
               (Î» h â†’ prâ‚ (Î± h))
               (Î» h â†’ prâ‚‚ (Î± h))

     Î· : Ïâ‚â»Â¹ âˆ˜ Ïâ‚ âˆ¼ id
     Î· (s , a) =
      Ïâ‚â»Â¹ (Ïâ‚ (s , a))                       ï¼âŸ¨ refl âŸ©
      Ïâ‚â»Â¹ (Î» h â†’ transport Sâ‚ (Ï• h) (s , a)) ï¼âŸ¨ I âŸ©
      Ïâ‚â»Â¹ (Î» h â†’ transport S (Ï• h) s , _)    ï¼âŸ¨ refl âŸ©
      (Ïâ»Â¹ (Î» h â†’ transport S (Ï• h) s) , _)   ï¼âŸ¨ refl âŸ©
      (Ïâ»Â¹ (Ï s) , _)                         ï¼âŸ¨ II âŸ©
      (s , a)                                 âˆ
       where
        I = ap Ïâ‚â»Â¹ (dfunext fe' (Î» h â†’ transport-Î£ S axioms (A h) (Ï• h) s))
        II = to-subtype-ï¼
              (axioms-are-prop-valued (Î  A))
              (inverses-are-retractions Ï (Ï-is-equiv p A) s)

     Îµ : Ïâ‚ âˆ˜ Ïâ‚â»Â¹ âˆ¼ id
     Îµ Î± = dfunext fe' I
      where
       Î±â‚ = Î» h â†’ prâ‚ (Î± h)
       Î±â‚‚ = Î» h â†’ prâ‚‚ (Î± h)

       I : Ïâ‚ (Ïâ‚â»Â¹ Î±) âˆ¼ Î±
       I h =
        Ïâ‚ (Ïâ‚â»Â¹ Î±) h                    ï¼âŸ¨ refl âŸ©
        Ïâ‚ (Ïâ»Â¹ Î±â‚ , _) h                ï¼âŸ¨ refl âŸ©
        transport Sâ‚ (Ï• h) (Ïâ»Â¹ Î±â‚ , _)  ï¼âŸ¨ II âŸ©
        (transport S (Ï• h) (Ïâ»Â¹ Î±â‚) , _) ï¼âŸ¨ refl âŸ©
        (Ï (Ïâ»Â¹ Î±â‚) h , _)               ï¼âŸ¨ III âŸ©
        (Î±â‚ h , Î±â‚‚ h)                    ï¼âŸ¨ refl âŸ©
        Î± h                              âˆ
         where
          II  = transport-Î£ S axioms (A h) (Ï• h) (Ïâ»Â¹ Î±â‚)
          III = to-subtype-ï¼
                 (axioms-are-prop-valued (A h))
                 (ap (Î» - â†’ - h) (inverses-are-sections Ï (Ï-is-equiv p A) Î±â‚))

     Ïâ‚-is-equiv : is-equiv Ïâ‚
     Ïâ‚-is-equiv = qinvs-are-equivs Ïâ‚ (Ïâ‚â»Â¹ , Î· , Îµ)

\end{code}

The above requires that the structures are closed under
proposition-indexed products with the pointwise operations (where the
operations are specified very abstractly by a structure operator S).
But in many cases of interest, of course, such as monoids and groups,
we have closure under arbitrary products under the pointwise
operations. By the above, the type of any mathematical structure that
is closed under arbitrary products is injective.

Example. The type of monoids is injective. We just have to check that
the monoid axioms are closed under Î .

\begin{code}

Monoid-is-closed-under-prop-Î 
 : closed-under-prop-Î  {ğ“¤} (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
Monoid-is-closed-under-prop-Î  {ğ“¤} = V
 where
  open canonical-map monoid-structure

  Ïâ»Â¹ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡)
      â†’ ((h : p holds) â†’ monoid-structure (A h)) â†’ monoid-structure (Î  A)
  Ïâ»Â¹ p A = inverse (Ï p A) (âˆ-Magmaâˆ™-structure-closed-under-Î  p A)

  axioms-closed-under-prop-Î 
    : (p : Î© ğ“¤)
      (A : p holds â†’ ğ“¤ Ì‡)
      (Î± : (h : p holds) â†’ monoid-structure (A h))
      (F : (h : p holds) â†’ monoid-axioms (A h) (Î± h))
    â†’ monoid-axioms (Î  A) (Ïâ»Â¹ p A Î±)
  axioms-closed-under-prop-Î  p A Î± F = I , II , III , IV
   where
    _Â·_ : Î  A â†’ Î  A â†’ Î  A
    f Â· g = Î» h â†’ prâ‚ (Î± h) (f h) (g h)

    e : Î  A
    e h = prâ‚‚ (Î± h)

    Ïâ»Â¹-remark : Ïâ»Â¹ p A Î± ï¼ (_Â·_ , e)
    Ïâ»Â¹-remark = refl

    I : is-set (Î  A)
    I = Î -is-set fe' (Î» h â†’
         case F h of
          Î» (Ah-is-set , ln , rn , assoc) â†’ Ah-is-set)

    II : left-neutral e _Â·_
    II f = dfunext fe' (Î» h â†’
            case F h of
             Î» (Ah-is-set , ln , rn , assoc) â†’ ln (f h))

    III : right-neutral e _Â·_
    III g = dfunext fe' (Î» h â†’
             case F h of
              Î» (Ah-is-set , ln , rn , assoc) â†’ rn (g h))

    IV : associative _Â·_
    IV f g k = dfunext fe' (Î» h â†’
                case F h of
                 Î» (Ah-is-set , ln , rn , assoc) â†’ assoc (f h) (g h) (k h))

  V : closed-under-prop-Î  {ğ“¤} (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
  V =  closure-under-prop-Î -with-axioms
        monoid-structure
        âˆ-Magmaâˆ™-structure-closed-under-Î 
        monoid-axioms
        (monoid-axioms-is-prop fe')
        axioms-closed-under-prop-Î 

ainjectivity-of-Monoid : ainjective-type (Monoid {ğ“¤}) ğ“¤ ğ“¤
ainjectivity-of-Monoid {ğ“¤} =
 ainjectivity-of-type-of-structures
  (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
  Monoid-is-closed-under-prop-Î 

\end{code}

TODO. It is easy to add further axioms to monoids to get groups, and
then show that the type of groups is injective using the above
technique. I expect this to be entirely routine as the example of
monoids.

TODO. More techniques are needed to show that the type of 1-categories
would be injective. This is more interesting.

NB. The type Ordinal ğ“¤ of well-ordered sets in ğ“¤ is also injective,
but for a different reason.

TODO. The type of posets should be injective, but with a different
proof. May the proof for the type of ordinals can be adapted (check).
