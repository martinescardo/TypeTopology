Martin Escardo, 16th August 2023

We give conditions for types of mathematical structures, such as
pointed types, âˆ-magmas, monoids and groups to be algebraically
injective. We use algebraic flabbiness as our main tool.

\begin{code}

{-# OPTIONS --safe --without-K --exact-split --lossy-unification #-}

open import UF.Univalence

module InjectiveTypes.MathematicalStructures (ua : Univalence) where

open import UF.FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import InjectiveTypes.Blackboard fe
open import MLTT.Spartan
open import Taboos.Decomposability ua
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.Equiv-FunExt
open import UF.EquivalenceExamples
open import UF.ExcludedMiddle
open import UF.PropIndexedPiSigma
open import UF.Size
open import UF.Subsingletons

\end{code}

We already know the following, but here is a short direct proof.

\begin{code}

universes-are-flabby-Î  : aflabby (ğ“¤ Ì‡ ) ğ“¤
universes-are-flabby-Î  {ğ“¤} P P-is-prop A = Î  A , I
 where
  X : ğ“¤  Ì‡
  X = Î  A

  I : (p : P) â†’ Î  A ï¼ A p
  I = Î» p â†’ eqtoid (ua ğ“¤) (Î  A) (A p) (prop-indexed-product fe' P-is-prop p)

universes-are-injective-Î  : ainjective-type (ğ“¤ Ì‡ ) ğ“¤ ğ“¤
universes-are-injective-Î  {ğ“¤} = aflabby-types-are-ainjective (ğ“¤ Ì‡ )
                                  universes-are-flabby-Î 

\end{code}

We now want to show that several types of mathematical structures are
(algebraically) injective, or, equivalently, (algebraically) flabby.

We work with an arbitrary S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ and want to show that Î£ S is
flabby.

Let f : P â†’ Î£ S be a "partial element" where P is a proposition. Then
f is of the form

 f h = A h , g h

with A : P â†’ ğ“¤ Ì‡ and g : (h : P) â†’ S (A h).

We need to construct a (total) element (X , s) of Î£ S, with s : S X ,
such that for all h : P we have that (X , s) = (A h , g h).

This forces X = A h for any h : P. We have an equivalence

 Ï€ : (h : P) â†’ Î  A â‰ƒ A h

By, univalence, Ï€ induces a fiberwise identification

 Ï• : (h : P) â†’ Î  A ï¼ A h.

Hence we can take X to be Î  A.

To construct s, we need an assumption on S.

Roughly, our assumption is that S is closed under prop-indexed
products, in the sense that from an element of the type
(h : P) â†’ S (A h) we can get an element of the type S (Î  A).

More precisely, we always have a map

 Ïƒ : S (Î  A) â†’ ((h : P) â†’ S (A h))

in the opposite direction. We stipulate that it is an equivalence for
any proposition P and any type family A : P â†’ ğ“¤ Ì‡.

With this assumption, we can let s be the inverse of Ïƒ applied to g.

\begin{code}

module _ (S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ ) where

 treq : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y
 treq {X} {Y} ğ•— = transport S (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We don't need this fact explicitly, but it is worth keeping it in
mind:

\begin{code}

 treq-is-equiv : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y) â†’ is-equiv (treq ğ•—)
 treq-is-equiv {X} {Y} ğ•— = transports-are-equivs (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We now define auxiliary functions Ï€, Ï• and Ïƒ parametrized by a
proposition p and family A indexed by p.

Because we deliberately use short, general purpose symbols, we place
these definitions in a module that needs to be opened when we want to
use this notation.

\begin{code}

 module notation
         (p : Î© ğ“¤)
         (A : p holds â†’ ğ“¤ Ì‡)
         where

  hp : is-prop (p holds)
  hp = holds-is-prop p

  Ï€ : (h : p holds) â†’ Î  A â‰ƒ A h
  Ï€ = prop-indexed-product fe' hp

  remarkâ‚€ : (h : p holds) (Î± : Î  A) â†’ âŒœ Ï€ h âŒ Î± ï¼ Î± h
  remarkâ‚€ h Î± = refl

  Ï• : (h : p holds) â†’ Î  A ï¼ A h
  Ï• h = eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)

  Ïƒ : S (Î  A) â†’ ((h : p holds) â†’ S (A h))
  Ïƒ s h = treq (Ï€ h) s

  remarkâ‚ : (s : S (Î  A)) (h : p holds)
          â†’ Ïƒ s h ï¼ transport S (eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)) s
  remarkâ‚ s h = refl

\end{code}

Our assumption on S is that the map

  Ïƒ p A : S (Î  A) â†’ ((h : p holds) â†’ S (A h))

is an equivalence for every p and A.

\begin{code}

 closed-under-prop-Î  : ğ“¤ âº âŠ” ğ“¥ Ì‡
 closed-under-prop-Î  = (p : Î© ğ“¤)
                       (A : p holds â†’ ğ“¤ Ì‡)
                      â†’ is-equiv (Ïƒ p A)
  where
   open notation

\end{code}

And the main lemma, under this assumption, is that Î£ S is algebraically
flabby with with respect to the universe ğ“¤.

\begin{code}

 aflabbiness-of-type-of-structures : closed-under-prop-Î 
                                   â†’ aflabby (Î£ S) ğ“¤
 aflabbiness-of-type-of-structures Ïƒ-is-equiv = I
  where
   I : aflabby (Î£ S) ğ“¤
   I P P-is-prop f = (Î  A , s) , II
    where
     p : Î© ğ“¤
     p = (P , P-is-prop)

     have-f : p holds â†’ Î£ S
     have-f = f

     A : p holds â†’ ğ“¤ Ì‡
     A = prâ‚ âˆ˜ f

     open notation p A

     e : S (Î  A) â‰ƒ ((h : p holds) â†’ S (A h))
     e = Ïƒ , Ïƒ-is-equiv p A

     g : (h : P) â†’ S (A h)
     g = prâ‚‚ âˆ˜ f

     s : S (Î  A)
     s = âŒœ e âŒâ»Â¹ g

     II : (h : p holds) â†’ Î  A , s ï¼ f h
     II h = Î  A , s   ï¼âŸ¨ to-Î£-ï¼ (Ï• h , III) âŸ©
            A h , g h ï¼âŸ¨ refl âŸ©
            f h       âˆ
      where
       III = transport S (Ï• h) s ï¼âŸ¨ refl âŸ©
             âŒœ e âŒ s h           ï¼âŸ¨ refl âŸ©
             âŒœ e âŒ (âŒœ e âŒâ»Â¹ g) h ï¼âŸ¨ IV âŸ©
             g h âˆ
        where
         IV = ap (Î» - â†’ - h) (inverses-are-sections âŒœ e âŒ âŒœ e âŒ-is-equiv g)

\end{code}

It follows that the type Î£ S is algebraically injective if S is closed
under prop-indexed products, which is our main theorem.

\begin{code}

 ainjectivity-of-type-of-structures : closed-under-prop-Î 
                                    â†’ ainjective-type (Î£ S) ğ“¤ ğ“¤
 ainjectivity-of-type-of-structures = aflabby-types-are-ainjective (Î£ S)
                                      âˆ˜ aflabbiness-of-type-of-structures

\end{code}

Our assumption of closure under prop-indexed products may be difficult
to check directly, because it involves transport along an
identification induced by an equivalence by univalence.

In practice, we are often able to construct T and T-refl below, for S
of interest, without using transport.

\begin{code}

 module _ (T      : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y)
          (T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id)
        where

\end{code}

The point is that any such T can be equivalently expressed as a
transport and hence we may apply the above theorem, but it may be
easier to check closure under products using T rather than transport
(see examples below).

\begin{code}

  transport-eqtoid : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y)
                   â†’ T ğ•— âˆ¼ transport S (eqtoid (ua ğ“¤) X Y ğ•—)
  transport-eqtoid {X} {Y} ğ•— s = JEq (ua ğ“¤) X A I Y ğ•—
   where
    A : (Y : ğ“¤ Ì‡) (ğ•— : X â‰ƒ Y) â†’ ğ“¥ Ì‡
    A Y ğ•— = T ğ•— s ï¼ transport S (eqtoid (ua ğ“¤) X Y ğ•—) s

    I : A X (â‰ƒ-refl X)
    I = T (â‰ƒ-refl X) s                                ï¼âŸ¨ II âŸ©
        s                                             ï¼âŸ¨ refl âŸ©
        transport S refl s                            ï¼âŸ¨ III âŸ©
        transport S (eqtoid (ua ğ“¤) X X (â‰ƒ-refl X)) s  âˆ
      where
       II   = T-refl s
       III  = (ap (Î» - â†’ transport S - s) (eqtoid-refl (ua ğ“¤) X))â»Â¹

\end{code}

Hence our condition on S formulated with transports can be
equivalently formulated with T:

\begin{code}

  module notation'
          (p : Î© ğ“¤)
          (A : p holds â†’ ğ“¤ Ì‡)
          where

   open notation p A public

   Ï„ : S (Î  A) â†’ (h : p holds) â†’ S (A h)
   Ï„ s h = T (Ï€ h) s

   Ïƒ-and-Ï„-agree : Ïƒ âˆ¼ Ï„
   Ïƒ-and-Ï„-agree s =
    Ïƒ s                                                       ï¼âŸ¨ refl âŸ©
    ((Î» h â†’ transport S (eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ h)) s)) ï¼âŸ¨ I âŸ©
    (Î» h â†’ T (Ï€ h) s)                                         ï¼âŸ¨ refl âŸ©
    Ï„ s                                                       âˆ
    where
     I = dfunext fe' (Î» h â†’ (transport-eqtoid (Ï€ h) s)â»Â¹)

  closed-under-prop-Î ' : ğ“¤ âº âŠ” ğ“¥ Ì‡
  closed-under-prop-Î ' = (p : Î© ğ“¤)
                         (A : p holds â†’ ğ“¤ Ì‡)
                       â†’ is-equiv (Ï„ p A)
   where
    open notation'

  Î -closure-criterion : closed-under-prop-Î '
                      â†’ closed-under-prop-Î 
  Î -closure-criterion Ï„-is-equiv p A =
   equiv-closed-under-âˆ¼ (Ï„ p A) (Ïƒ p A) (Ï„-is-equiv p A) (Ïƒ-and-Ï„-agree p A)
   where
    open notation'

  Î -closure-criterion-converse : closed-under-prop-Î 
                               â†’ closed-under-prop-Î '
  Î -closure-criterion-converse Ïƒ-is-equiv p A =
   equiv-closed-under-âˆ¼ (Ïƒ p A) (Ï„ p A) (Ïƒ-is-equiv p A) (âˆ¼-sym (Ïƒ-and-Ï„-agree p A))
   where
    open notation'

\end{code}

Example: The type of pointed types is algebraically injective.

\begin{code}

Pointed-Type : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Pointed-Type ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , X

Pointed : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Pointed X = X

Pointed-is-closed-under-prop-Î  : closed-under-prop-Î  (Pointed {ğ“¤})
Pointed-is-closed-under-prop-Î  {ğ“¤} =
  Î -closure-criterion Pointed T T-refl c
 where
  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ X â†’ Y
  T = âŒœ_âŒ

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl x = refl

  c : closed-under-prop-Î ' Pointed T T-refl
  c p A = id-is-equiv (Î  A)

ainjectivity-of-type-of-pointed-types : ainjective-type (Pointed-Type ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-type-of-pointed-types {ğ“¤} =
 ainjectivity-of-type-of-structures Pointed Pointed-is-closed-under-prop-Î 

\end{code}

Example: The type of âˆ-magmas is algebraically injective. The proof is
a bit long, but it is an entirely routine application of the above
general theorem.

\begin{code}

âˆ-Magma : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magma ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X)

âˆ-Magma-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magma-structure X = X â†’ X â†’ X

âˆ-Magma-structure-is-closed-under-prop-Î  : closed-under-prop-Î  (âˆ-Magma-structure {ğ“¤})
âˆ-Magma-structure-is-closed-under-prop-Î  {ğ“¤} =
 Î -closure-criterion S T T-refl Ï„-is-equiv
 where
  S = âˆ-Magma-structure

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y
  T ğ•— _Â·_ = Î» y y' â†’ âŒœ ğ•— âŒ (âŒœ ğ•— âŒâ»Â¹ y Â· âŒœ ğ•— âŒâ»Â¹ y')

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl _Â·_ = dfunext fe' (Î» x â†’ dfunext fe' (Î» x' â†’ refl))

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡)
         where

   open notation' S T T-refl p A

   Ï„â»Â¹ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ï„â»Â¹ g Î± Î² h = g h (âŒœ Ï€ h âŒ Î±) (âŒœ Ï€ h âŒ Î²)

   Î· : Ï„â»Â¹ âˆ˜ Ï„ âˆ¼ id
   Î· _Â·_ = dfunext fe' (Î» Î± â†’ dfunext fe' (I Î±))
    where
     I : âˆ€ Î± Î² â†’ Ï„â»Â¹ (Ï„ _Â·_) Î± Î² ï¼ Î± Â· Î²
     I Î± Î² =
      (Ï„â»Â¹ âˆ˜ Ï„) _Â·_ Î± Î²                                                ï¼âŸ¨ refl âŸ©
      (Î» h â†’ âŒœ Ï€ h âŒ  (âŒœ Ï€ h âŒâ»Â¹ (âŒœ Ï€ h âŒ Î±) Â· âŒœ Ï€ h âŒâ»Â¹ (âŒœ Ï€ h âŒ Î²))) ï¼âŸ¨ II âŸ©
      (Î» h â†’ âŒœ Ï€ h âŒ (Î± Â· Î²))                                          ï¼âŸ¨ refl âŸ©
      (Î» h â†’ (Î± Â· Î²) h)                                                ï¼âŸ¨ refl âŸ©
      Î± Â· Î²                                                            âˆ
      where
       II = dfunext fe' (Î» h â†’
             apâ‚‚ (Î» -â‚ -â‚‚ â†’ (-â‚ Â· -â‚‚) h)
                 (inverses-are-retractions (âŒœ Ï€ h âŒ) âŒœ Ï€ h âŒ-is-equiv Î±)
                 (inverses-are-retractions (âŒœ Ï€ h âŒ) âŒœ Ï€ h âŒ-is-equiv Î²))

   Îµ : Ï„ âˆ˜ Ï„â»Â¹ âˆ¼ id
   Îµ g =
    Ï„ (Ï„â»Â¹ g)                                                     ï¼âŸ¨ refl âŸ©
    (Î» h a b â†’ g h (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ a)) (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ b))) ï¼âŸ¨ I âŸ©
    (Î» h a b â†’ g h a b)                                             ï¼âŸ¨ refl âŸ©
    g                                                               âˆ
     where
      I = dfunext fe' (Î» h â†’ dfunext fe' (Î» a â†’ dfunext fe' (Î» b â†’
           apâ‚‚ (g h)
               (inverses-are-sections (âŒœ Ï€ h âŒ) âŒœ Ï€ h âŒ-is-equiv a)
               (inverses-are-sections (âŒœ Ï€ h âŒ) âŒœ Ï€ h âŒ-is-equiv b))))

   Ï„-is-equiv : is-equiv Ï„
   Ï„-is-equiv = qinvs-are-equivs Ï„  (Ï„â»Â¹ , Î· , Îµ)

ainjectivity-of-âˆ-Magma : ainjective-type (âˆ-Magma ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magma {ğ“¤} =
 ainjectivity-of-type-of-structures
  âˆ-Magma-structure
  âˆ-Magma-structure-is-closed-under-prop-Î 

\end{code}

A corollary is that the type âˆ-Magma ğ“¤ doesn't have any decidable
property unless weak excluded middle holds.

\begin{code}

decomposition-of-âˆ-Magma-gives-WEM : decomposition (âˆ-Magma ğ“¤) â†’ WEM ğ“¤
decomposition-of-âˆ-Magma-gives-WEM {ğ“¤} =
 decomposition-of-ainjective-type-gives-WEM
  (âˆ-Magma ğ“¤)
  ainjectivity-of-âˆ-Magma

\end{code}

The same is true for the type of pointed types, of course.

We now want to consider more examples, such as monoids, groups and
1-categories. For that purpose, write combinators, like in UF.SIP, to
show that mathematical structures constructed from standard building
blocks, such as the above, form injective types.

\begin{code}

variable
 ğ“¥â‚ ğ“¥â‚‚ : Universe

closed-under-prop-Î -Ã— :
      {Sâ‚ : ğ“¤ Ì‡ â†’ ğ“¥â‚ Ì‡ } {Sâ‚‚ : ğ“¤ Ì‡ â†’ ğ“¥â‚‚ Ì‡ }
    â†’ closed-under-prop-Î  Sâ‚
    â†’ closed-under-prop-Î  Sâ‚‚
    â†’ closed-under-prop-Î  (Î» X â†’ Sâ‚ X Ã— Sâ‚‚ X)

closed-under-prop-Î -Ã— {ğ“¤} {ğ“¥â‚} {ğ“¥â‚‚} {Sâ‚} {Sâ‚‚} Ïƒâ‚-is-equiv Ïƒâ‚‚-is-equiv = Ïƒ-is-equiv
 where
  S : ğ“¤ Ì‡ â†’ ğ“¥â‚ âŠ” ğ“¥â‚‚ Ì‡
  S X = Sâ‚ X Ã— Sâ‚‚ X

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡)
         where

   open notation S  p A using (Ïƒ ; Ï•)
   open notation Sâ‚ p A renaming (Ïƒ to Ïƒâ‚) using ()
   open notation Sâ‚‚ p A renaming (Ïƒ to Ïƒâ‚‚) using ()

   Ïƒâ‚â»Â¹ : ((h : p holds) â†’ Sâ‚ (A h)) â†’ Sâ‚ (Î  A)
   Ïƒâ‚â»Â¹ = inverse Ïƒâ‚ (Ïƒâ‚-is-equiv p A)

   Ïƒâ‚‚â»Â¹ : ((h : p holds) â†’ Sâ‚‚ (A h)) â†’ Sâ‚‚ (Î  A)
   Ïƒâ‚‚â»Â¹ = inverse Ïƒâ‚‚ (Ïƒâ‚‚-is-equiv p A)

   Ïƒâ»Â¹ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïƒâ»Â¹ Î± = Ïƒâ‚â»Â¹ (Î» h â†’ prâ‚ (Î± h)) , Ïƒâ‚‚â»Â¹ (Î» h â†’ prâ‚‚ (Î± h))

   Î· : Ïƒâ»Â¹ âˆ˜ Ïƒ âˆ¼ id
   Î· (sâ‚ , sâ‚‚) =
    Ïƒâ»Â¹ (Ïƒ (sâ‚ , sâ‚‚))                                         ï¼âŸ¨ refl âŸ©
    Ïƒâ»Â¹ (Î» h â†’ transport S (Ï• h) (sâ‚ , sâ‚‚))                   ï¼âŸ¨ I âŸ©
    Ïƒâ»Â¹ (Î» h â†’ transport Sâ‚ (Ï• h) sâ‚ , transport Sâ‚‚ (Ï• h) sâ‚‚) ï¼âŸ¨ refl âŸ©
    Ïƒâ‚â»Â¹ (Ïƒâ‚ sâ‚) , Ïƒâ‚‚â»Â¹ (Ïƒâ‚‚ sâ‚‚)                               ï¼âŸ¨ II âŸ©
    (sâ‚ , sâ‚‚)                                                 âˆ
     where
      I  = ap Ïƒâ»Â¹ (dfunext fe' (Î» h â†’ transport-Ã— Sâ‚ Sâ‚‚ (Ï• h)))
      II = apâ‚‚ _,_
              (inverses-are-retractions Ïƒâ‚ (Ïƒâ‚-is-equiv p A) sâ‚)
              (inverses-are-retractions Ïƒâ‚‚ (Ïƒâ‚‚-is-equiv p A) sâ‚‚)

   Îµ : Ïƒ âˆ˜ Ïƒâ»Â¹ âˆ¼ id
   Îµ Î± = dfunext fe' I
    where
     Î±â‚ = Î» h â†’ prâ‚ (Î± h)
     Î±â‚‚ = Î» h â†’ prâ‚‚ (Î± h)

     I : Ïƒ (Ïƒâ»Â¹ Î±) âˆ¼ Î±
     I h =
      Ïƒ (Ïƒâ»Â¹ Î±) h                                                 ï¼âŸ¨ refl âŸ©
      transport S (Ï• h) (Ïƒâ‚â»Â¹ Î±â‚ , Ïƒâ‚‚â»Â¹ Î±â‚‚)                       ï¼âŸ¨ II âŸ©
      transport Sâ‚ (Ï• h) (Ïƒâ‚â»Â¹ Î±â‚) , transport Sâ‚‚ (Ï• h) (Ïƒâ‚‚â»Â¹ Î±â‚‚) ï¼âŸ¨ refl âŸ©
      Ïƒâ‚ (Ïƒâ‚â»Â¹ Î±â‚) h , Ïƒâ‚‚ (Ïƒâ‚‚â»Â¹ Î±â‚‚) h                             ï¼âŸ¨ III âŸ©
      Î±â‚ h , Î±â‚‚ h                                                 ï¼âŸ¨ refl âŸ©
      Î± h                                                         âˆ
       where
        II  = transport-Ã— Sâ‚ Sâ‚‚ (Ï• h)
        III = apâ‚‚ _,_
                 (ap (Î» - â†’ - h)
                     (inverses-are-sections Ïƒâ‚ (Ïƒâ‚-is-equiv p A) Î±â‚))
                 (ap (Î» - â†’ - h)
                     (inverses-are-sections Ïƒâ‚‚ (Ïƒâ‚‚-is-equiv p A) Î±â‚‚))

   Ïƒ-is-equiv : is-equiv Ïƒ
   Ïƒ-is-equiv = qinvs-are-equivs Ïƒ (Ïƒâ»Â¹ , Î· , Îµ)

\end{code}

Example. The type of pointed âˆ-magmas is injective.

\begin{code}

âˆ-Magmaâˆ™ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magmaâˆ™ ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , X Ã— (X â†’ X â†’ X)

ainjectivity-of-âˆ-Magmaâˆ™ : ainjective-type (âˆ-Magmaâˆ™ ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magmaâˆ™ {ğ“¤} =
 ainjectivity-of-type-of-structures
  (Î» X â†’ X Ã— (X â†’ X â†’ X))
  (closed-under-prop-Î -Ã—
    Pointed-is-closed-under-prop-Î 
    âˆ-Magma-structure-is-closed-under-prop-Î )

\end{code}

To to be continued with more "combinators" and examples.
