Martin Escardo, August 2023

More about injectivity.

\begin{code}

{-# OPTIONS --safe --without-K  #-}

open import UF.FunExt

module InjectiveTypes.OverSmallMaps (fe : FunExt) where

open import InjectiveTypes.Blackboard fe
open import MLTT.Spartan
open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.Size
open import UF.Subsingletons

private
 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

\end{code}

Added 3rd August 2023. Extensions over small embeddings induced by
algebraic flabbiness. The point is to reduce size without resizing
axioms. An application is in Taboos.Decomposability.

\begin{code}

module _ (D : ğ“¤ Ì‡ )
         (D-is-flabby : aflabby D ğ“£)
         {X : ğ“¥ Ì‡ }
         {Y : ğ“¦ Ì‡ }
         (j : X â†’ Y)
         (j-is-embedding : is-embedding j)
         (j-small : j is ğ“£ small-map)
         (f : X â†’ D)
       where

 private
  R : Y â†’ ğ“£ Ì‡
  R y = resized (fiber j y) (j-small y)

  Ï : (y : Y) â†’ R y â‰ƒ fiber j y
  Ï y = resizing-condition (j-small y)

  R-is-prop : (y : Y) â†’ is-prop (R y)
  R-is-prop y = equiv-to-prop (Ï y) (j-is-embedding y)

  e : (y : Y) â†’ Î£ d ê‰ D , ((r : R y) â†’ d ï¼ f (prâ‚ (âŒœ Ï y âŒ r)))
  e y = D-is-flabby (R y) (R-is-prop y) (Î» r â†’ f (prâ‚ (âŒœ Ï y âŒ r)))

 sflabby-extension : (Y â†’ D)
 sflabby-extension y = prâ‚ (e y)

 sflabby-extension-property : sflabby-extension âˆ˜ j âˆ¼ f
 sflabby-extension-property x =
  sflabby-extension (j x)                 ï¼âŸ¨ I âŸ©
  f (prâ‚ (âŒœ Ï (j x) âŒ (âŒœ Ï (j x) âŒâ»Â¹ w))) ï¼âŸ¨ II âŸ©
  f (prâ‚ w)                               ï¼âŸ¨ refl âŸ©
  f x                                     âˆ
  where
   w : fiber j (j x)
   w = x , refl

   I  = prâ‚‚ (e (j x)) (âŒœ Ï (j x) âŒâ»Â¹ w)
   II = ap (f âˆ˜ prâ‚) (â‰ƒ-sym-is-rinv (Ï (j x)) w)

 aflabbiness-gives-injectivity-over-small-maps : Î£ f' ê‰ (Y â†’ D) , f' âˆ˜ j âˆ¼ f
 aflabbiness-gives-injectivity-over-small-maps = sflabby-extension ,
                                                 sflabby-extension-property
\end{code}

An extension property for injective types, with more general universes
and less general embeddings.

\begin{code}

ainjectivity-over-small-maps : {ğ“¤ ğ“¥ ğ“¦ ğ“£â‚€ ğ“£â‚ ğ“£â‚‚ : Universe}
                             â†’ (D : ğ“¤ Ì‡ )
                             â†’ ainjective-type D (ğ“£â‚€ âŠ” ğ“£â‚) ğ“£â‚‚
                             â†’ {X : ğ“¥ Ì‡ } {Y : ğ“¦ Ì‡ }
                               (j : X â†’ Y)
                             â†’ is-embedding j
                             â†’ j is ğ“£â‚€ small-map
                             â†’ (f : X â†’ D) â†’ Î£ f' ê‰ (Y â†’ D) , f' âˆ˜ j âˆ¼ f
ainjectivity-over-small-maps {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£â‚€} {ğ“£â‚} {ğ“£â‚‚} D D-ainj =
 aflabbiness-gives-injectivity-over-small-maps D
  (aflabbiness-resizingâ‚ {ğ“¤} {ğ“£â‚€} {ğ“£â‚} D (ainjective-types-are-aflabby D D-ainj))

\end{code}
