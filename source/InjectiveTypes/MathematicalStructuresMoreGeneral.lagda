Martin Escardo, 16th August 2023

This file improves InjectiveTypes.MathematicalStructuresMoreGeneral at
the cost of being harder to understand, with the benefit of at the
same time being more general and allowing shorter proofs. It relies on
the file InjectiveTypes.Sigma.

We give a sufficient condition for types of mathematical structures,
such as pointed types, âˆ-magmas, monoids, groups, etc. to be
algebraically injective. We use algebraic flabbiness as our main tool.

There is already enough discussion in the files
InjectiveTypes.MathematicalStructure and InjectiveTypes.Sigma, which
we will not repeat here. But we still add some remarks.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.Univalence

module InjectiveTypes.MathematicalStructuresMoreGeneral (ua : Univalence) where

open import UF.FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import InjectiveTypes.Blackboard fe
open import InjectiveTypes.Sigma fe
open import MLTT.Spartan
open import Taboos.Decomposability ua
open import UF.Base
open import UF.Equiv
open import UF.ExcludedMiddle
open import UF.PropIndexedPiSigma
open import UF.Retracts
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier

\end{code}

We already know the following, but here is a short direct proof.

\begin{code}

universes-are-aflabby-Î  : aflabby (ğ“¤ Ì‡ ) ğ“¤
universes-are-aflabby-Î  {ğ“¤} P P-is-prop A = Î  A , I
 where
  X : ğ“¤  Ì‡
  X = Î  A

  I : (p : P) â†’ Î  A ï¼ A p
  I = Î» p â†’ eqtoid (ua ğ“¤) (Î  A) (A p) (prop-indexed-product fe' P-is-prop p)

universes-are-injective-Î  : ainjective-type (ğ“¤ Ì‡ ) ğ“¤ ğ“¤
universes-are-injective-Î  {ğ“¤} = aflabby-types-are-ainjective (ğ“¤ Ì‡ )
                                  universes-are-aflabby-Î 

universes-are-aflabby-Î£ : aflabby (ğ“¤ Ì‡ ) ğ“¤
universes-are-aflabby-Î£ {ğ“¤} P P-is-prop A = Î£ A , I
 where
  X : ğ“¤  Ì‡
  X = Î£ A

  I : (p : P) â†’ Î£ A ï¼ A p
  I = Î» p â†’ eqtoid (ua ğ“¤) (Î£ A) (A p) (prop-indexed-sum P-is-prop p)

module _ (S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ ) where

 treq : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y
 treq {X} {Y} ğ•— = transport S (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We don't need this fact explicitly, but it is worth keeping it in
mind:

\begin{code}

 treq-is-equiv : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y) â†’ is-equiv (treq ğ•—)
 treq-is-equiv {X} {Y} ğ•— = transports-are-equivs (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

We now assume flabbiness data for the universe ğ“¤, which later will
choose to be e.g. one of the above two, we record something proved in
InjectiveTypes.Sigma specialized to our situation.

\begin{code}

 module _ (Ï• : aflabby (ğ“¤ Ì‡ ) ğ“¤) where

  aflabbiness-of-type-of-structured-types : technical-condition S Ï•
                                          â†’ aflabby (Î£ S) ğ“¤
  aflabbiness-of-type-of-structured-types = Î£-is-aflabby S Ï•


  ainjectivity-of-type-of-structures : technical-condition S Ï•
                                       â†’ ainjective-type (Î£ S) ğ“¤ ğ“¤
  ainjectivity-of-type-of-structures = aflabby-types-are-ainjective (Î£ S)
                                       âˆ˜ aflabbiness-of-type-of-structured-types

\end{code}

The main additional work in this file on top of InjectiveTypes.Sigma
is to make it easier to work with the technical condition for the
purpose of injectivity of types of mathematical structures.

We work with hypothetical T and T-refl with the following types.

\begin{code}

 module _ (T      : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y)
          (T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id)
        where

\end{code}

The point is that any such T can be equivalently expressed as a
transport and hence we may apply the above theorem, but it may be
easier to check the technical condition using T rather than transport
(see examples below).

\begin{code}

  T-is-treq : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y)
            â†’ T ğ•— âˆ¼ treq ğ•—
  T-is-treq {X} {Y} ğ•— s = JEq (ua ğ“¤) X A I Y ğ•—
   where
    A : (Y : ğ“¤ Ì‡) (ğ•— : X â‰ƒ Y) â†’ ğ“¥ Ì‡
    A Y ğ•— = T ğ•— s ï¼ treq ğ•— s

    I : A X (â‰ƒ-refl X)
    I = T (â‰ƒ-refl X) s                                ï¼âŸ¨ T-refl s âŸ©
        s                                             ï¼âŸ¨ refl âŸ©
        transport S refl s                            ï¼âŸ¨ II âŸ©
        transport S (eqtoid (ua ğ“¤) X X (â‰ƒ-refl X)) s  ï¼âŸ¨ refl âŸ©
        treq (â‰ƒ-refl X) s                             âˆ
      where
       II = (ap (Î» - â†’ transport S - s) (eqtoid-refl (ua ğ“¤) X))â»Â¹

\end{code}

We introduce names for the canonical maps induced by Î - and
Î£-flabbiness structure on ğ“¤.

\begin{code}

  ÏÎ  : (p : Î© ğ“¤)
       (A : p holds â†’ ğ“¤ Ì‡)
     â†’ S (Î  A) â†’ ((h : p holds) â†’ S (A h))
  ÏÎ  p A s h = T (prop-indexed-product fe' (holds-is-prop p) h) s

  ÏÎ£ : (p : Î© ğ“¤)
       (A : p holds â†’ ğ“¤ Ì‡)
     â†’ S (Î£ A) â†’ ((h : p holds) â†’ S (A h))
  ÏÎ£ p A s h = T (prop-indexed-sum (holds-is-prop p) h) s

\end{code}

In our applications, we will apply Î -flabbiness structure, and it will
be easier to check technical-condition-Î  than technical-condition S
universes-are-aflabby-Î 

\begin{code}

  technical-condition-Î  : ğ“¤ âº âŠ” ğ“¥ Ì‡
  technical-condition-Î  = (p : Î© ğ“¤)
                          (A : p holds â†’ ğ“¤ Ì‡)
                        â†’ has-section (ÏÎ  p A)

  Î -lemma : technical-condition-Î 
          â†’ technical-condition S universes-are-aflabby-Î 
  Î -lemma t p A = II
   where
    Ï€ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) (h : p holds) â†’ Î  A â‰ƒ A h
    Ï€ p A = prop-indexed-product fe' (holds-is-prop p)

    I : ÏÎ  p A âˆ¼ Ï S universes-are-aflabby-Î  p A
    I s =
     ÏÎ  p A s                                                    ï¼âŸ¨ refl âŸ©
     (Î» h â†’ T (Ï€ p A h) s)                                       ï¼âŸ¨ Iâ‚€ âŸ©
     (Î» h â†’ transport S (eqtoid (ua ğ“¤) (Î  A) (A h) (Ï€ p A h)) s) ï¼âŸ¨ refl âŸ©
          Ï S universes-are-aflabby-Î  p A s                      âˆ
     where
      Iâ‚€ = dfunext fe' (Î» h â†’ T-is-treq (Ï€ p A h) s)

    II : has-section (Ï S universes-are-aflabby-Î  p A)
    II = has-section-closed-under-âˆ¼ (ÏÎ  p A) _ (t p A) (âˆ¼-sym I)

\end{code}

We could have proved Î -lemma as follows, but then it wouldn't "compute
enough" for the purposes of e.g. Monoid-Î -condition.

\begin{code}

  Î -lemma' : technical-condition-Î 
           â†’ technical-condition S universes-are-aflabby-Î 
  Î -lemma' t p A = transport has-section I II
   where
    I : ÏÎ  p A ï¼ Ï S universes-are-aflabby-Î  p A
    I = dfunext fe' (Î» s â†’
        dfunext fe' (Î» h â†’
          ap (Î» - â†’ - (prop-indexed-product fe' (holds-is-prop p) h) s)
             (dfunext fe' (Î» ğ•— â†’ dfunext fe' (T-is-treq ğ•—)))))

    II : has-section (ÏÎ  p A)
    II = t p A

  technical-condition-Î£ : ğ“¤ âº âŠ” ğ“¥ Ì‡
  technical-condition-Î£ = (p : Î© ğ“¤)
                          (A : p holds â†’ ğ“¤ Ì‡)
                        â†’ has-section (ÏÎ£ p A)

  Î£-lemma : technical-condition-Î£
          â†’ technical-condition S universes-are-aflabby-Î£
  Î£-lemma t p A = transport has-section I II
   where
    I : ÏÎ£ p A ï¼ Ï S universes-are-aflabby-Î£ p A
    I = dfunext fe' (Î» s â†’
        dfunext fe' (Î» h â†’
          ap (Î» - â†’ - (prop-indexed-sum (holds-is-prop p) h) s)
             (dfunext fe' (Î» ğ•— â†’ dfunext fe' (T-is-treq ğ•—)))))

    II : has-section (ÏÎ£ p A)
    II = t p A

\end{code}

Because at the moment we are not applying the Î£-flabbiness structure
of the universe, we haven't bothered to produce a version of Î£-lemma
with better computational properties, but this may be needed in the
future (TODO).

Example. The type of pointed types is algebraically injective. We use
the Î -flabbiness of the universe.

\begin{code}

Pointed-type : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Pointed-type ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , X

Pointed : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Pointed X = X

Pointed-Î -condition : technical-condition
                       (Pointed {ğ“¤})
                        universes-are-aflabby-Î 
Pointed-Î -condition {ğ“¤} = Î -lemma Pointed T T-refl c
 where
  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ X â†’ Y
  T = âŒœ_âŒ

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl x = refl

  c : technical-condition-Î  (Î» X â†’ X) T T-refl
  c p A = equivs-have-sections id (id-is-equiv (Î  A))

ainjectivity-of-type-of-pointed-types : ainjective-type (Pointed-type ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-type-of-pointed-types {ğ“¤} =
 ainjectivity-of-type-of-structures
  Pointed
  universes-are-aflabby-Î 
  Pointed-Î -condition

\end{code}

Example. The type of âˆ-magmas is algebraically injective. The proof is
an entirely routine application of the above general theorem after we
guess what T should be.

\begin{code}

âˆ-Magma : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magma ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X)

âˆ-Magma-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magma-structure = Î» X â†’ X â†’ X â†’ X

âˆ-Magma-structure-Î -condition : technical-condition
                                 (âˆ-Magma-structure {ğ“¤})
                                 universes-are-aflabby-Î 
âˆ-Magma-structure-Î -condition {ğ“¤} =
 Î -lemma S T T-refl ÏÎ -has-section
 where
  S = âˆ-Magma-structure

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y
  T ğ•— _Â·_ = Î» y y' â†’ âŒœ ğ•— âŒ (âŒœ ğ•— âŒâ»Â¹ y Â· âŒœ ğ•— âŒâ»Â¹ y')

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl _Â·_ = dfunext fe' (Î» x â†’ dfunext fe' (Î» x' â†’ refl))

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡)
         where

   Ï€ : (h : p holds) â†’ Î  A â‰ƒ A h
   Ï€ = prop-indexed-product fe' (holds-is-prop p)

   Ïƒ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïƒ g Î± Î² h = g h (âŒœ Ï€ h âŒ Î±) (âŒœ Ï€ h âŒ Î²)

   r : S (Î  A) â†’ ((h : p holds) â†’ S (A h))
   r = ÏÎ  S T T-refl p A

   ÏÏƒ : r âˆ˜ Ïƒ âˆ¼ id
   ÏÏƒ g =
    r (Ïƒ g)                                                         ï¼âŸ¨ refl âŸ©
    (Î» h a b â†’ g h (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ a)) (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ b))) ï¼âŸ¨ I âŸ©
    (Î» h a b â†’ g h a b)                                             ï¼âŸ¨ refl âŸ©
    g                                                               âˆ
     where
      I = dfunext fe' (Î» h â†’ dfunext fe' (Î» a â†’ dfunext fe' (Î» b â†’
           apâ‚‚ (g h)
               (inverses-are-sections' (Ï€ h) a)
               (inverses-are-sections' (Ï€ h) b))))

   ÏÎ -has-section : has-section (ÏÎ  S T T-refl p A)
   ÏÎ -has-section = Ïƒ , ÏÏƒ

ainjectivity-of-âˆ-Magma : ainjective-type (âˆ-Magma ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magma {ğ“¤} =
 ainjectivity-of-type-of-structures
  âˆ-Magma-structure
  universes-are-aflabby-Î 
  âˆ-Magma-structure-Î -condition

\end{code}

A corollary is that the type âˆ-Magma ğ“¤ doesn't have any non-trivial
decidable property unless weak excluded middle holds.

\begin{code}

decomposition-of-âˆ-Magma-gives-WEM : decomposition (âˆ-Magma ğ“¤) â†’ WEM ğ“¤
decomposition-of-âˆ-Magma-gives-WEM {ğ“¤} =
 decomposition-of-ainjective-type-gives-WEM
  (âˆ-Magma ğ“¤)
  ainjectivity-of-âˆ-Magma

\end{code}

The same is true for the type of pointed types, of course, and for any
injective type.

Example. The type of pointed âˆ-magmas is injective.

\begin{code}

open import UF.SIP-Examples
open monoid

âˆ-Magmaâˆ™ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magmaâˆ™ ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X) Ã— X

âˆ-Magmaâˆ™-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magmaâˆ™-structure = monoid-structure

âˆ-Magmaâˆ™-structure-Î -condition : technical-condition
                                  (âˆ-Magmaâˆ™-structure {ğ“¤})
                                  universes-are-aflabby-Î 
âˆ-Magmaâˆ™-structure-Î -condition =
 technical-condition-Ã—
  universes-are-aflabby-Î 
  âˆ-Magma-structure-Î -condition
  Pointed-Î -condition

ainjectivity-of-âˆ-Magmaâˆ™ : ainjective-type (âˆ-Magmaâˆ™ ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magmaâˆ™ {ğ“¤} =
 ainjectivity-of-type-of-structures
  âˆ-Magmaâˆ™-structure
  universes-are-aflabby-Î 
  âˆ-Magmaâˆ™-structure-Î -condition

\end{code}

Example. The type of monoids is injective. We just have to check that
the monoid axioms are closed under Î .

\begin{code}

Monoid-Î -condition : technical-condition {ğ“¤ âº}
                      (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
                      universes-are-aflabby-Î 
Monoid-Î -condition {ğ“¤} =
 technical-condition-with-axioms
  universes-are-aflabby-Î 
  monoid-structure
  âˆ-Magmaâˆ™-structure-Î -condition
  monoid-axioms
  (monoid-axioms-is-prop fe')
  axioms-Î -condition
 where
  Ïƒ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡)
    â†’ ((h : p holds) â†’ monoid-structure (A h)) â†’ monoid-structure (Î  A)
  Ïƒ p A = section-of
           (Ï monoid-structure universes-are-aflabby-Î  p A)
           (âˆ-Magmaâˆ™-structure-Î -condition p A)

  axioms-Î -condition
    : (p : Î© ğ“¤)
      (A : p holds â†’ ğ“¤ Ì‡)
      (Î± : (h : p holds) â†’ monoid-structure (A h))
      (F : (h : p holds) â†’ monoid-axioms (A h) (Î± h))
    â†’ monoid-axioms (Î  A) (Ïƒ p A Î±)
  axioms-Î -condition p A Î± F = I , II , III , IV
   where
    _Â·_ : Î  A â†’ Î  A â†’ Î  A
    f Â· g = Î» h â†’ prâ‚ (Î± h) (f h) (g h)

    e : Î  A
    e h = prâ‚‚ (Î± h)

    Ïƒ-remark : Ïƒ p A Î± ï¼ (_Â·_ , e)
    Ïƒ-remark = refl

    I : is-set (Î  A)
    I = Î -is-set fe' (Î» h â†’
         case F h of
          Î» (Ah-is-set , ln , rn , assoc) â†’ Ah-is-set)

    II : left-neutral e _Â·_
    II f = dfunext fe' (Î» h â†’
            case F h of
             Î» (Ah-is-set , ln , rn , assoc) â†’ ln (f h))

    III : right-neutral e _Â·_
    III g = dfunext fe' (Î» h â†’
             case F h of
              Î» (Ah-is-set , ln , rn , assoc) â†’ rn (g h))

    IV : associative _Â·_
    IV f g k = dfunext fe' (Î» h â†’
                case F h of
                 Î» (Ah-is-set , ln , rn , assoc) â†’ assoc (f h) (g h) (k h))

ainjectivity-of-Monoid : ainjective-type (Monoid {ğ“¤}) ğ“¤ ğ“¤
ainjectivity-of-Monoid {ğ“¤} =
 ainjectivity-of-type-of-structures
  (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
  universes-are-aflabby-Î 
  Monoid-Î -condition

\end{code}

TODO. It is easy to add further axioms to monoids to get groups, and
then show that the type of groups is injective using the above
technique. I expect this to be entirely routine as the example of
monoids.

TODO. More techniques are needed to show that the type of 1-categories
would be injective. This is more interesting.

NB. The type Ordinal ğ“¤ of well-ordered sets in ğ“¤ is also injective,
but for a different reason.

TODO. The type of posets should be injective, but with a different
proof. Maybe the proof for the type of ordinals can be adapted (check).
