Martin Escardo, 16th August 2023, with more improvements 18th June 2025.

Injectivity of types of mathematical structures, such as pointed
types, âˆ-magmas, magmas, monoids, groups etc.

We give a sufficient condition for types of mathematical structures to
be injective, and we apply it to examples such as the above.

This file generalizes InjectiveTypes.MathematicalStructures at the
cost of perhaps being harder to understand. It relies on the file
InjectiveTypes.Sigma, which also arises as a generalization of the
above original file.

Added 5 November 2025 by Tom de Jong: The type of metric spaces is
injective and this relies on the generalizations developed here. This
is the first example that make uses of the added generality of this file.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

import InjectiveTypes.MathematicalStructures -- For comparison only.

open import UF.Univalence

\end{code}

We assume univalence (and hence function extensionality, which,
follows from it), but no other HoTT/UF extensions, not even
propositional truncations.

\begin{code}

module InjectiveTypes.MathematicalStructuresMoreGeneral
        (ua : Univalence)
       where

open import UF.FunExt
open import UF.UA-FunExt

private
 fe : FunExt
 fe = Univalence-gives-FunExt ua

 fe' : Fun-Ext
 fe' {ğ“¤} {ğ“¥} = fe ğ“¤ ğ“¥

open import InjectiveTypes.Blackboard fe
open import InjectiveTypes.Sigma fe
open import MLTT.Spartan
open import Taboos.Decomposability fe
open import UF.Base
open import UF.Equiv
open import UF.ClassicalLogic
open import UF.PropIndexedPiSigma
open import UF.Retracts
open import UF.Sets
open import UF.Sets-Properties
open import UF.SubtypeClassifier

\end{code}

It is convenient to work with the following definition of (algebraic)
flabbiness of a universe, which uses equivalence of types rather than
equality.

\begin{code}

Flabby : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Flabby ğ“¤ = Î£ â¨† ê‰ ((p : Î© ğ“¤) â†’ (p holds â†’ ğ“¤ Ì‡ ) â†’ ğ“¤ Ì‡ )
                , ((p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) (h : p holds) â†’ â¨† p A â‰ƒ A h)

\end{code}

In the presence of univalence we can convert to the usual definition,
and we can always convert in the other direction, but in this file we
need the first one only.

\begin{code}

to-aflabby : Flabby ğ“¤ â†’ aflabby (ğ“¤ Ì‡ ) ğ“¤
to-aflabby {ğ“¤} (â¨† , e) P i A =
 â¨† (P , i) A , (Î» h â†’ eqtoid (ua ğ“¤) _ _ (e (P , i) A h))

from-afabbly : aflabby (ğ“¤ Ì‡ ) ğ“¤ â†’ Flabby ğ“¤
from-afabbly {ğ“¤} aflab =
 aflabby-extension aflab ,
 (Î» p A h â†’ idtoeq _ _ (aflabby-extension-property aflab p A h))

\end{code}

We already know that universes are flabby in two ways, using â¨† := Î 
and â¨† := Î£, but we give constructions that they are Flabby without
univalence, and hence have better computational behaviour, which will
simplify the applications we have in mind.

If the index type is a proposition, then the projection out of a
Î -type is an equivalence.

\begin{code}

Î -ğ•¡ğ•£ğ• ğ•› : (p : Î© ğ“¤) {A : p holds â†’ ğ“¤ Ì‡ } (h : p holds)
      â†’ Î  A â‰ƒ A h
Î -ğ•¡ğ•£ğ• ğ•› p h = Î -proj h , Î -proj-is-equiv h fe' (holds-is-prop p)

universes-are-Flabby-Î  : Flabby ğ“¤
universes-are-Flabby-Î  = (Î» p A â†’ Î  A) ,
                         (Î» p A â†’ Î -ğ•¡ğ•£ğ• ğ•› p)

universes-are-flabby-Î  : aflabby (ğ“¤  Ì‡) ğ“¤
universes-are-flabby-Î  = to-aflabby universes-are-Flabby-Î 

Î£-ğ•šğ•Ÿ : (p : Î© ğ“¤) {A : p holds â†’ ğ“¤ Ì‡ } (h : p holds)
    â†’ A h â‰ƒ Î£ A
Î£-ğ•šğ•Ÿ p h = Î£-in h , Î£-in-is-equiv h (holds-is-prop p)

universes-are-Flabby-Î£ : Flabby ğ“¤
universes-are-Flabby-Î£ = (Î» p A â†’ Î£ A) ,
                         (Î» p A h â†’ â‰ƒ-sym (Î£-ğ•šğ•Ÿ p h))

universes-are-flabby-Î£ : aflabby (ğ“¤  Ì‡) ğ“¤
universes-are-flabby-Î£ = to-aflabby universes-are-Flabby-Î£

\end{code}

We now work with an arbitrary notion S of structure on ğ“¤. E.g. for
monoids we will take S X := X â†’ X â†’ X, the type of the multiplication
operation.

\begin{code}

module _ (S : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ ) where

\end{code}

By the results of InjectiveTypes.Sigma, we get that Î£ S is aflabby in
two ways, assuming the compatibility condition for the flabbiness
data.

\begin{code}

 module _ (Ï• : aflabby (ğ“¤ Ì‡ ) ğ“¤) where

  aflabbiness-of-type-of-structured-types : compatibility-data S Ï•
                                          â†’ aflabby (Î£ S) ğ“¤
  aflabbiness-of-type-of-structured-types = Î£-is-aflabby S Ï•

  ainjectivity-of-type-of-structures : compatibility-data S Ï•
                                     â†’ ainjective-type (Î£ S) ğ“¤ ğ“¤
  ainjectivity-of-type-of-structures = aflabby-types-are-ainjective (Î£ S)
                                       âˆ˜ aflabbiness-of-type-of-structured-types

\end{code}

We will apply this to get our desired examples with Ï• taken to be the
above canonical Î -flabby structure on the universe in most cases, and
at least one with the canonical Î£-flabby structure.

Next we want to simplify working with compatibility data (as defined
in the module InjectiveTypes.Sigma), where we avoid transports by
working with the following function treq and suitable choices of T and
T-refl in the examples below. Notice that the definition of treq uses
univalence. The point of T and T-refl below is that they won't use
univalence in our examples of interest, so that they will have a
better computational behaviour than treq.

\begin{code}

 treq : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y
 treq {X} {Y} ğ•— = transport S (eqtoid (ua ğ“¤) X Y ğ•—)

\end{code}

The main additional work in this file on top of InjectiveTypes.Sigma
is to make it easier to work with the compatibility condition for the
purpose of injectivity of types of mathematical structures.

We work with hypothetical T and T-refl with the following types.

\begin{code}

 module _ (T      : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y)
          (T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id)
        where

\end{code}

The point is that any such T can be equivalently expressed as a
transport and hence we may apply the theorems of the imported file
InjectiveTypes.Sigma, but it may be easier to check the compatibility
condition using T rather than transport (see examples below).

\begin{code}

  T-is-treq : {X Y : ğ“¤ Ì‡ } (ğ•— : X â‰ƒ Y)
            â†’ T ğ•— âˆ¼ treq ğ•—
  T-is-treq {X} {Y} ğ•— s = JEq (ua ğ“¤) X A I Y ğ•—
   where
    A : (Y : ğ“¤ Ì‡ ) (ğ•— : X â‰ƒ Y) â†’ ğ“¥ Ì‡
    A Y ğ•— = T ğ•— s ï¼ treq ğ•— s

    I : A X (â‰ƒ-refl X)
    I = T (â‰ƒ-refl X) s                                ï¼âŸ¨ T-refl s âŸ©
        s                                             ï¼âŸ¨reflâŸ©
        transport S refl s                            ï¼âŸ¨ II âŸ©
        transport S (eqtoid (ua ğ“¤) X X (â‰ƒ-refl X)) s  ï¼âŸ¨reflâŸ©
        treq (â‰ƒ-refl X) s                             âˆ
      where
       II = (ap (Î» - â†’ transport S - s) (eqtoid-refl (ua ğ“¤) X))â»Â¹

\end{code}

In order to be able to apply the results of InjectiveTypes.Sigma, we
perform the following construction. That file requires compatibility
data of a certain kind, which we reduce to compatibility of another
kind, which will be easier to produce in our sample applications.

\begin{code}

  module compatibility-data-construction (Ï•@(â¨† , Îµ) : Flabby ğ“¤) where

   derived-Ï : (p : Î© ğ“¤)
               (A : p holds â†’ ğ“¤ Ì‡ )
             â†’ S (â¨† p A) â†’ ((h : p holds) â†’ S (A h))
   derived-Ï p A s h = T (Îµ p A h) s

   compatibility-data-for-derived-Ï : ğ“¤ âº âŠ” ğ“¥ Ì‡
   compatibility-data-for-derived-Ï = (p : Î© ğ“¤)
                                      (A : p holds â†’ ğ“¤ Ì‡ )
                                    â†’ has-section (derived-Ï p A)

   construction : compatibility-data-for-derived-Ï
                â†’ compatibility-data S (to-aflabby Ï•)
   construction t p A = III
    where

     II : derived-Ï p A âˆ¼ Ï S (to-aflabby Ï•) p A
     II s =
      derived-Ï p A s                                     ï¼âŸ¨reflâŸ©
      (Î» h â†’ T (Îµ p A h) s)                               ï¼âŸ¨ Iâ‚€ âŸ©
      (Î» h â†’ treq (Îµ p A h) s)                            ï¼âŸ¨reflâŸ©
      (Î» h â†’ transport S (eqtoid (ua ğ“¤) _ _ (Îµ p A h)) s) ï¼âŸ¨reflâŸ©
      Ï S (to-aflabby Ï•) p A s                           âˆ
      where
       Iâ‚€ = dfunext fe' (Î» h â†’ T-is-treq (Îµ p A h) s)

     III : has-section (Ï S (to-aflabby Ï•) p A)
     III = has-section-closed-under-âˆ¼ (derived-Ï p A) _ (t p A) (âˆ¼-sym II)

\end{code}

This completes the construction, but we record that the section map of
the above construction is literally the same as that of the
hypothesis t.

\begin{code}

     _ = section-map (Ï S (to-aflabby Ï•) p A) III  ï¼âŸ¨reflâŸ©
         section-map (derived-Ï p A) (t p A)        âˆ

\end{code}

What is necessarily different is the proof that this map is a
section. In fact, it is different in the strong sense that the
comparison for equality doesn't even make sense - it wouldn't even
typecheck.

A way to verify this in Agda is to try to supply the following naive
definition.

   construction' : compatibility-data-for-derived-Ï
                 â†’ compatibility-data S (to-aflabby Ï•)
   construction' t = t -- Doesn't type check (of course).

We can sensibly have only that the *section map* of the construction
agrees with the given section map, which is what we have already
observed in the above proof, but record again with full type
information, outside the above proof.

\begin{code}

   construction-fact : (p : Î© ğ“¤)
                       (A : p holds â†’ ğ“¤ Ì‡)
                       (t : compatibility-data-for-derived-Ï)
                     â†’ section-map (Ï S (to-aflabby Ï•) p A) (construction t p A)
                     ï¼ section-map (derived-Ï p A)         (t p A)
   construction-fact p A t = refl

\end{code}

This fact about the construction will be rather useful in practice,
for the applications we have in mind.

We can specialize this to the Î  and Î£ flabbiness structures discussed
above, to get the following.

\begin{code}

  module _ where

   open compatibility-data-construction universes-are-Flabby-Î 

   ÏÎ  : (p : Î© ğ“¤)
        (A : p holds â†’ ğ“¤ Ì‡ )
      â†’ S (Î  A) â†’ ((h : p holds) â†’ S (A h))
   ÏÎ  = derived-Ï

   compatibility-data-Î  : ğ“¤ âº âŠ” ğ“¥ Ì‡
   compatibility-data-Î  = (p : Î© ğ“¤)
                          (A : p holds â†’ ğ“¤ Ì‡ )
                        â†’ has-section (ÏÎ  p A)

   Î -construction : compatibility-data-Î 
                  â†’ compatibility-data S universes-are-flabby-Î 
   Î -construction = construction

\end{code}

We use the following definitional equality a number of times (and we
try to record this explicitly when we do so).

\begin{code}

   _ : ÏÎ  ï¼ Î» p A s h â†’ T (Î -ğ•¡ğ•£ğ• ğ•› p h) s
   _ = refl

\end{code}

For most examples below, we only need the above functions ÏÎ ,
compatibility-data-Î  and Î -construction, but at least one of them uses
their Î£ versions defined below.

\begin{code}

  module _ where

   open compatibility-data-construction universes-are-Flabby-Î£

   ÏÎ£ : (p : Î© ğ“¤)
        (A : p holds â†’ ğ“¤ Ì‡ )
      â†’ S (Î£ A) â†’ ((h : p holds) â†’ S (A h))
   ÏÎ£ = derived-Ï

   compatibility-data-Î£ : ğ“¤ âº âŠ” ğ“¥ Ì‡
   compatibility-data-Î£ = (p : Î© ğ“¤)
                          (A : p holds â†’ ğ“¤ Ì‡ )
                        â†’ has-section (ÏÎ£ p A)

   Î£-construction : compatibility-data-Î£
                  â†’ compatibility-data S universes-are-flabby-Î£
   Î£-construction = construction

\end{code}

Example. The type of pointed types is algebraically injective. We use
the Î -flabbiness of the universe.

\begin{code}

Pointed-type : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Pointed-type ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , X

Pointed : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
Pointed X = X

Pointed-Î -data : compatibility-data (Pointed {ğ“¤}) universes-are-flabby-Î 
Pointed-Î -data {ğ“¤} = Î -construction Pointed T T-refl c
 where
  S = Pointed

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ X â†’ Y
  T = âŒœ_âŒ

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl x = refl

  _ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) â†’ ÏÎ  S T T-refl p A ï¼ ğ‘–ğ‘‘ (S (Î  A))
  _ = Î» p A â†’ refl

  c : compatibility-data-Î  S T T-refl
  c p A = equivs-have-sections id (id-is-equiv (Î  A))

\end{code}

Hence we conclude that the type of pointed types is ainjective.

\begin{code}

ainjectivity-of-type-of-pointed-types : ainjective-type (Pointed-type ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-type-of-pointed-types =
 ainjectivity-of-type-of-structures
  Pointed
  universes-are-flabby-Î 
  Pointed-Î -data

\end{code}

Example. The type of âˆ-magmas is algebraically injective. The proof is
an entirely routine application of the above general theorem after we
guess what T should be.

\begin{code}

âˆ-Magma : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magma ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X)

âˆ-magma-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-magma-structure = Î» X â†’ X â†’ X â†’ X

âˆ-Magma-structure-Î -data : compatibility-data
                            (âˆ-magma-structure {ğ“¤})
                            universes-are-flabby-Î 
âˆ-Magma-structure-Î -data {ğ“¤} =
 Î -construction S T T-refl ÏÎ -has-section
 where
  S = âˆ-magma-structure

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y
  T ğ•— _Â·_ = Î» y y' â†’ âŒœ ğ•— âŒ (âŒœ ğ•— âŒâ»Â¹ y Â· âŒœ ğ•— âŒâ»Â¹ y')

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl _Â·_ = refl

  module _ (p : Î© ğ“¤)
           (A : p holds â†’ ğ“¤ Ì‡ )
         where

   Ï€ : (h : p holds) â†’ Î  A â‰ƒ A h
   Ï€ = Î -ğ•¡ğ•£ğ• ğ•› p

   r : S (Î  A) â†’ ((h : p holds) â†’ S (A h))
   r _Â·_ h a b = âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ a Â· âŒœ Ï€ h âŒâ»Â¹ b)

   _ : r ï¼ ÏÎ  S T T-refl p A
   _ = refl -- Which is crucial for the proof below to work.

   Ïƒ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïƒ g Î± Î² h = g h (âŒœ Ï€ h âŒ Î±) (âŒœ Ï€ h âŒ Î²)

   rÏƒ : r âˆ˜ Ïƒ âˆ¼ id
   rÏƒ g =
    r (Ïƒ g)                                                         ï¼âŸ¨reflâŸ©
    (Î» h a b â†’ g h (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ a)) (âŒœ Ï€ h âŒ (âŒœ Ï€ h âŒâ»Â¹ b))) ï¼âŸ¨ II âŸ©
    (Î» h a b â†’ g h a b)                                             ï¼âŸ¨reflâŸ©
    g                                                               âˆ
     where
      II = dfunext fe' (Î» h â†’
           dfunext fe' (Î» a â†’
           dfunext fe' (Î» b â†’
            apâ‚‚ (g h)
             (inverses-are-sections' (Ï€ h) a)
             (inverses-are-sections' (Ï€ h) b))))

   ÏÎ -has-section : has-section (ÏÎ  S T T-refl p A)
   ÏÎ -has-section = Ïƒ , rÏƒ

ainjectivity-of-âˆ-Magma : ainjective-type (âˆ-Magma ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magma =
 ainjectivity-of-type-of-structures
  âˆ-magma-structure
  universes-are-flabby-Î 
  âˆ-Magma-structure-Î -data

\end{code}

A corollary is that the type âˆ-Magma ğ“¤ doesn't have any non-trivial
decidable property unless weak excluded middle holds.

\begin{code}

decomposition-of-âˆ-Magma-gives-WEM : decomposition (âˆ-Magma ğ“¤) â†’ typal-WEM ğ“¤
decomposition-of-âˆ-Magma-gives-WEM {ğ“¤} =
 decomposition-of-ainjective-type-gives-WEM
  (univalence-gives-propext (ua ğ“¤))
  (âˆ-Magma ğ“¤)
  ainjectivity-of-âˆ-Magma

\end{code}

The same is true for the type of pointed types, of course, and for any
injective type.

Example. The type of pointed âˆ-magmas is injective.

\begin{code}

open import UF.SIP-Examples
open monoid

âˆ-Magmaâˆ™ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
âˆ-Magmaâˆ™ ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ X) Ã— X

âˆ-Magmaâˆ™-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
âˆ-Magmaâˆ™-structure = monoid-structure

âˆ-Magmaâˆ™-structure-Î -data : compatibility-data
                             (âˆ-Magmaâˆ™-structure {ğ“¤})
                             universes-are-flabby-Î 
âˆ-Magmaâˆ™-structure-Î -data =
 compatibility-data-Ã—
  universes-are-flabby-Î 
  âˆ-Magma-structure-Î -data
  Pointed-Î -data

ainjectivity-of-âˆ-Magmaâˆ™ : ainjective-type (âˆ-Magmaâˆ™ ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-âˆ-Magmaâˆ™ =
 ainjectivity-of-type-of-structures
  âˆ-Magmaâˆ™-structure
  universes-are-flabby-Î 
  âˆ-Magmaâˆ™-structure-Î -data

\end{code}

Example. The type of monoids is injective. We just have to check that
the monoid axioms are closed under Î .

\begin{code}

Monoid-Î -data : compatibility-data {ğ“¤ âº}
                 (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
                 universes-are-flabby-Î 
Monoid-Î -data {ğ“¤} =
 compatibility-data-with-axioms
  universes-are-flabby-Î 
  monoid-structure
  âˆ-Magmaâˆ™-structure-Î -data
  monoid-axioms
  (monoid-axioms-is-prop fe')
  axioms-Î -data
 where
  Ïƒ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡ )
    â†’ ((h : p holds) â†’ monoid-structure (A h)) â†’ monoid-structure (Î  A)
  Ïƒ p A = section-map
           (Ï monoid-structure universes-are-flabby-Î  p A)
           (âˆ-Magmaâˆ™-structure-Î -data p A)

  axioms-Î -data
   : (p : Î© ğ“¤)
     (A : p holds â†’ ğ“¤ Ì‡ )
     (Î± : (h : p holds) â†’ monoid-structure (A h))
     (F : (h : p holds) â†’ monoid-axioms (A h) (Î± h))
   â†’ monoid-axioms (Î  A) (Ïƒ p A Î±)
  axioms-Î -data p A Î± F = I , II , III , IV
   where
    _*_ : {h : p holds} â†’ A h â†’ A h â†’ A h
    _*_ {h} = prâ‚ (Î± h)

    _Â·_ : Î  A â†’ Î  A â†’ Î  A
    (f Â· g) h = f h * g h

    e : Î  A
    e h = prâ‚‚ (Î± h)

    _ : Ïƒ p A Î± ï¼ (_Â·_ , e)
    _ = refl -- Which is crucial for the proof below to work.

    I : is-set (Î  A)
    I = Î -is-set fe' (Î» h â†’
         case F h of
          Î» (Ah-is-set , ln , rn , assoc) â†’ Ah-is-set)

    II : left-neutral e _Â·_
    II f = dfunext fe' (Î» h â†’
            case F h of
             Î» (Ah-is-set , ln , rn , assoc) â†’ ln (f h))

    III : right-neutral e _Â·_
    III g = dfunext fe' (Î» h â†’
             case F h of
              Î» (Ah-is-set , ln , rn , assoc) â†’ rn (g h))

    IV : associative _Â·_
    IV f g k = dfunext fe' (Î» h â†’
                case F h of
                 Î» (Ah-is-set , ln , rn , assoc) â†’ assoc (f h) (g h) (k h))

ainjectivity-of-Monoid : ainjective-type (Monoid {ğ“¤}) ğ“¤ ğ“¤
ainjectivity-of-Monoid {ğ“¤} =
 ainjectivity-of-type-of-structures
  (Î» X â†’ Î£ s ê‰ monoid-structure X , monoid-axioms X s)
  universes-are-flabby-Î 
  Monoid-Î -data

\end{code}

It is easy to add further axioms to monoids to get groups, and then
show that the type of groups is injective using the above
technique. This is just as routine as the example of monoids. All one
needs to do is to show that the group axioms are closed under
prop-indexed products.

TODO. Maybe implement this.

NB. The type Ordinal ğ“¤ of well-ordered sets in ğ“¤ is also injective,
but for different reasons, two of them given in two different modules.

Added 20th June 2025. The type of all families in a universe is
injective.

\begin{code}

Fam : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Fam ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ ğ“¤ Ì‡)

Fam-structure : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
Fam-structure {ğ“¤} X = X â†’ ğ“¤ Ì‡

open import UF.EquivalenceExamples
open import UF.Subsingletons

Fam-Î -data : compatibility-data (Fam-structure {ğ“¤}) universes-are-flabby-Î 
Fam-Î -data {ğ“¤} = Î -construction Fam-structure T T-refl c
 where
  S = Fam-structure

  T : {X Y : ğ“¤ Ì‡} â†’ X â‰ƒ Y â†’ (X â†’ ğ“£ Ì‡ ) â†’ (Y â†’ ğ“£ Ì‡ )
  T ğ•— R = Î» y â†’ R (âŒœ ğ•— âŒâ»Â¹ y)

  T-refl : {X : ğ“¤ Ì‡} â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl R = refl

  module _ (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) where

   r :  S (Î  A) â†’ ((h : p holds) â†’ S (A h))
   r s h a = s (âŒœ Î -ğ•¡ğ•£ğ• ğ•› p h âŒâ»Â¹ a)

   _ : ÏÎ  S T T-refl p A ï¼ r
   _ = refl

   Ïƒ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïƒ g f = (h : p holds) â†’ g h (f h)

   rÏƒ : r âˆ˜ Ïƒ âˆ¼ id
   rÏƒ g = dfunext fe' (Î» h â†’ dfunext fe' (II h))
    where
     module _ (h : p holds) (a : A h) where

      Ï€ : Î  A â‰ƒ A h
      Ï€ = Î -ğ•¡ğ•£ğ• ğ•› p h

      I = ((h' : p holds) â†’ g h' (âŒœ Ï€ âŒâ»Â¹ a h')) â‰ƒâŸ¨ Iâ‚€ âŸ©
          (p holds â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h))          â‰ƒâŸ¨ Iâ‚ âŸ©
          (ğŸ™ â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h))                â‰ƒâŸ¨ Iâ‚‚ âŸ©
          g h (âŒœ Ï€ âŒâ»Â¹ a h)                      â– 
        where
         Iâ‚€ = Î -cong fe' fe'
               (Î» h' â†’ transport (Î» - â†’ g - (âŒœ Ï€ âŒâ»Â¹ a -))
                                 (holds-is-prop p h' h) ,
                       transports-are-equivs (holds-is-prop p h' h))
         Iâ‚ = Î -change-of-variable-â‰ƒ {ğ“¤} {ğ“¤} fe
               (Î» _ â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h))
               (logically-equivalent-props-are-equivalent
                 (holds-is-prop p) ğŸ™-is-prop unique-to-ğŸ™ (Î» _ â†’ h))
         Iâ‚‚ = â‰ƒ-sym (ğŸ™â†’ fe')

      II = r (Ïƒ g) h a                            ï¼âŸ¨reflâŸ©
           Ïƒ g (âŒœ Ï€ âŒâ»Â¹ a)                        ï¼âŸ¨reflâŸ©
           ((h' : p holds) â†’ g h' (âŒœ Ï€ âŒâ»Â¹ a h')) ï¼âŸ¨ IIâ‚€ âŸ©
           g h (âŒœ Ï€ âŒâ»Â¹ a h)                      ï¼âŸ¨reflâŸ©
           g h (âŒœ Ï€ âŒ (âŒœ Ï€ âŒâ»Â¹ a))                ï¼âŸ¨ IIâ‚ âŸ©
           g h a                                  âˆ
            where
             IIâ‚€  = eqtoid (ua ğ“¤) _ _ I
             IIâ‚ = ap (g h) (inverses-are-sections' Ï€ a)

  c :  compatibility-data-Î  Fam-structure T T-refl
  c p A = Ïƒ p A , rÏƒ p A

ainjectivity-of-Fam : ainjective-type (Fam ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-Fam =
 ainjectivity-of-type-of-structures
  Fam-structure
  universes-are-flabby-Î 
  Fam-Î -data

\end{code}

A corollary is that the type of all functions in a universe is injective.

\begin{code}

open import UF.Classifiers

ainjectivity-of-type-of-all-functions
 : ainjective-type (Î£ X ê‰ ğ“¤ Ì‡ , Î£ Y ê‰ ğ“¤ Ì‡ , (X â†’ Y)) ğ“¤ ğ“¤
ainjectivity-of-type-of-all-functions {ğ“¤}
 = transport
    (Î» - â†’ ainjective-type - ğ“¤ ğ“¤)
    (eqtoid (ua (ğ“¤ âº)) _ _ (â‰ƒ-sym I))
    ainjectivity-of-Fam
 where
  open classifier-single-universe ğ“¤

  I = (Î£ X ê‰ ğ“¤ Ì‡ , Î£ Y ê‰ ğ“¤ Ì‡ , (X â†’ Y)) â‰ƒâŸ¨ Î£-flip âŸ©
      (Î£ Y ê‰ ğ“¤ Ì‡ , Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ Y)) â‰ƒâŸ¨ Î£-cong (classification (ua ğ“¤) fe') âŸ©
      (Î£ Y ê‰ ğ“¤ Ì‡ , (Y â†’ ğ“¤ Ì‡))           â‰ƒâŸ¨by-definitionâŸ©
      Fam ğ“¤                           â– 

\end{code}

The type of all type-valued relations, or multigraphs, in a universe
is injective. The proof is the binary version of the above unary proof.

\begin{code}

Graph : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Graph ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , (X â†’ X â†’ ğ“¤ Ì‡)

graph-structure : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
graph-structure {ğ“¤} X = X â†’ X â†’ ğ“¤ Ì‡


Graph-Î -data : compatibility-data (graph-structure {ğ“¤}) universes-are-flabby-Î 
Graph-Î -data {ğ“¤} =
 Î -construction graph-structure T T-refl c
 where
  S = graph-structure

  T : {X Y : ğ“¤ Ì‡} â†’ X â‰ƒ Y â†’ (X â†’ X â†’ ğ“£ Ì‡ ) â†’ (Y â†’ Y â†’ ğ“£ Ì‡ )
  T ğ•— R y y' = R (âŒœ ğ•— âŒâ»Â¹ y) (âŒœ ğ•— âŒâ»Â¹ y')

  T-refl : {X : ğ“¤ Ì‡} â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl R = refl

  module _ (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) where

   r :  S (Î  A) â†’ ((h : p holds) â†’ S (A h))
   r s h a a' = s (âŒœ Î -ğ•¡ğ•£ğ• ğ•› p h âŒâ»Â¹ a) (âŒœ Î -ğ•¡ğ•£ğ• ğ•› p h âŒâ»Â¹ a')

   _ : r ï¼ ÏÎ  S T T-refl p A
   _ = refl

   Ïƒ : ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
   Ïƒ g f f' = (h : p holds) â†’ g h (f h) (f' h)

   rÏƒ : r âˆ˜ Ïƒ âˆ¼ id
   rÏƒ g = dfunext fe' (Î» h â†’
          dfunext fe' (Î» a â†’
          dfunext fe' (Î» a' â†’ II h a a')))
    where
     module _ (h : p holds) (a a' : A h) where

      Ï€ : Î  A â‰ƒ A h
      Ï€ = Î -ğ•¡ğ•£ğ• ğ•› p h

      I = ((h' : p holds) â†’ g h' (âŒœ Ï€ âŒâ»Â¹ a h') (âŒœ Ï€ âŒâ»Â¹ a' h')) â‰ƒâŸ¨ Iâ‚€ âŸ©
          (p holds â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h) (âŒœ Ï€ âŒâ»Â¹ a' h))           â‰ƒâŸ¨ Iâ‚ âŸ©
          (ğŸ™ â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h) (âŒœ Ï€ âŒâ»Â¹ a' h))                 â‰ƒâŸ¨ Iâ‚‚ âŸ©
          g h (âŒœ Ï€ âŒâ»Â¹ a h) (âŒœ Ï€ âŒâ»Â¹ a' h)                       â– 
        where
         Iâ‚€ = Î -cong fe' fe'
               (Î» h' â†’ transport (Î» - â†’ g - (âŒœ Ï€ âŒâ»Â¹ a -) (âŒœ Ï€ âŒâ»Â¹ a' -))
                                 (holds-is-prop p h' h) ,
                       transports-are-equivs (holds-is-prop p h' h))
         Iâ‚ = Î -change-of-variable-â‰ƒ {ğ“¤} {ğ“¤} fe
               (Î» _ â†’ g h (âŒœ Ï€ âŒâ»Â¹ a h) (âŒœ Ï€ âŒâ»Â¹ a' h))
               (logically-equivalent-props-are-equivalent
                 (holds-is-prop p) ğŸ™-is-prop unique-to-ğŸ™ (Î» _ â†’ h))
         Iâ‚‚ = â‰ƒ-sym (ğŸ™â†’ fe')

      II = r (Ïƒ g) h a a'                                         ï¼âŸ¨reflâŸ©
           Ïƒ g (âŒœ Ï€ âŒâ»Â¹ a) (âŒœ Ï€ âŒâ»Â¹ a')                           ï¼âŸ¨reflâŸ©
           ((h' : p holds) â†’ g h' (âŒœ Ï€ âŒâ»Â¹ a h') (âŒœ Ï€ âŒâ»Â¹ a' h')) ï¼âŸ¨ IIâ‚€ âŸ©
           g h (âŒœ Ï€ âŒâ»Â¹ a h) (âŒœ Ï€ âŒâ»Â¹ a' h)                       ï¼âŸ¨reflâŸ©
           g h (âŒœ Ï€ âŒ (âŒœ Ï€ âŒâ»Â¹ a)) (âŒœ Ï€ âŒ (âŒœ Ï€ âŒâ»Â¹ a'))           ï¼âŸ¨ IIâ‚ âŸ©
           g h a a'                                               âˆ
            where
             IIâ‚€  = eqtoid (ua ğ“¤) _ _ I
             IIâ‚ = apâ‚‚ (g h)
                       (inverses-are-sections' Ï€ a)
                       (inverses-are-sections' Ï€ a')

  c :  compatibility-data-Î  graph-structure T T-refl
  c p A = Ïƒ p A , rÏƒ p A

ainjectivity-of-Graph : ainjective-type (Graph ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-Graph =
 ainjectivity-of-type-of-structures
  graph-structure
  universes-are-flabby-Î 
  Graph-Î -data

\end{code}

As a consequence, we get the injectivity of the type of posets.

\begin{code}

poset-axioms : (X : ğ“¤ Ì‡ ) â†’ graph-structure X â†’ ğ“¤ Ì‡
poset-axioms X _â‰¤_ = is-set X
                   Ã— ((x y : X) â†’ is-prop (x â‰¤ y))
                   Ã— reflexive     _â‰¤_
                   Ã— transitive    _â‰¤_
                   Ã— antisymmetric _â‰¤_

Poset : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
Poset ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡ , Î£ s ê‰ graph-structure X , poset-axioms X s

open import UF.Subsingletons-FunExt

poset-axioms-is-prop : (X : ğ“¤ Ì‡ ) (s : graph-structure X)
                     â†’ is-prop (poset-axioms X s)
poset-axioms-is-prop X _â‰¤_ = prop-criterion I
 where
  I : poset-axioms X _â‰¤_ â†’ is-prop (poset-axioms X _â‰¤_)
  I (s , pv , r , t , a) =
   Ã—â‚…-is-prop
    (being-set-is-prop fe')
    (Î â‚‚-is-prop fe' (Î» x y â†’ being-prop-is-prop fe'))
    (Î -is-prop fe' (Î» x â†’ pv x x))
    (Î â‚…-is-prop fe' (Î» x _ z _ _ â†’ pv x z))
    (Î â‚„-is-prop fe' (Î» _ _ _ _ â†’ s))

Poset-Î -data : compatibility-data {ğ“¤ âº}
                 (Î» X â†’ Î£ s ê‰ graph-structure X , poset-axioms X s)
                 universes-are-flabby-Î 
Poset-Î -data {ğ“¤} =
 compatibility-data-with-axioms
  universes-are-flabby-Î 
  graph-structure
  Graph-Î -data
  poset-axioms
  poset-axioms-is-prop
  axioms-Î -data
 where
  Ïƒ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡ )
    â†’ ((h : p holds) â†’ graph-structure (A h)) â†’ graph-structure (Î  A)
  Ïƒ p A = section-map
           (Ï graph-structure universes-are-flabby-Î  p A)
           (Graph-Î -data p A)

  axioms-Î -data
   : (p : Î© ğ“¤)
     (A : p holds â†’ ğ“¤ Ì‡ )
     (Î± : (h : p holds) â†’ graph-structure (A h))
     (F : (h : p holds) â†’ poset-axioms (A h) (Î± h))
   â†’ poset-axioms (Î  A) (Ïƒ p A Î±)
  axioms-Î -data p A Î± F = I , II , III , IV , V
   where
    _âŠ‘_ : {h : p holds} â†’ A h â†’ A h â†’ ğ“¤ Ì‡
    _âŠ‘_ {h} = Î± h

    _â‰¤_ : Î  A â†’ Î  A â†’ ğ“¤ Ì‡
    f â‰¤ g = (h : p holds) â†’ f h âŠ‘ g h

    _ : Ïƒ p A Î± ï¼ _â‰¤_
    _ = refl -- Which is crucial for the proof below to work.

    I : is-set (Î  A)
    I = Î -is-set fe' (Î» h â†’
         case F h of
          Î» (s , pv , r , t , a) â†’ s)

    II : (f g : Î  A) â†’ is-prop (f â‰¤ g)
    II f g = Î -is-prop fe' (Î» h â†’
              case F h of
               Î» (s , pv , r , t , a) â†’ pv (f h) (g h))

    III : reflexive _â‰¤_
    III f h =
     case F h of
      Î» (s , pv , r , t , a) â†’ r (f h)

    IV : transitive _â‰¤_
    IV fâ‚€ fâ‚ fâ‚‚ l m h =
     case F h of
      Î» (s , pv , r , t , a) â†’ t (fâ‚€ h) (fâ‚ h) (fâ‚‚ h) (l h) (m h)

    V : antisymmetric _â‰¤_
    V fâ‚€ fâ‚ l m = dfunext fe' (Î» h â†’
                   case F h of
                    Î» (s , pv , r , t , a) â†’ a (fâ‚€ h) (fâ‚ h) (l h) (m h))

ainjectivity-of-Poset : ainjective-type (Poset ğ“¤) ğ“¤ ğ“¤
ainjectivity-of-Poset {ğ“¤} =
 ainjectivity-of-type-of-structures
  (Î» X â†’ Î£ s ê‰ graph-structure X , poset-axioms X s)
  universes-are-flabby-Î 
  Poset-Î -data

\end{code}

Notice that, just as in the case for monoids, the proof amounts to
showing that posets are closed under prop-indexed products. Using the
same idea, it is straightforward to show that the types of dcpos,
continuous dcpos, suplattices, frames etc. are all injective. (Notice
that this is different from e.g. saying that the underlying type of a
dcpos is injective, which is also true and is proved in another
module.)

TODO. Maybe implement (some of) these examples.

TODO. More techniques are needed to show that the type of 1-categories
would be injective. A category can be seen as a graph equipped with
operations (identity and composition) satisfying properties (identity
laws, associativity, univalence).

Added 24 July 2025 by Tom de Jong.

In InjectiveTypes.InhabitedTypesTaboo we showed that the type of nonempty types
is injective by exhibiting it as a retract of the universe. In line with the
condition from InjectiveTypes.Subtypes, the argument there shows that a type is
nonempty if and only if it is a fixed point of the map X â†¦ (Â¬Â¬ X â†’ X).

Here is an alternative proof, using that
   (Î  (p : P) , Â¬Â¬ A p)   â†’   Â¬Â¬ Î  (p : P) , A p
holds when P is a proposition.

\begin{code}

Nonempty-Î -data : compatibility-data (is-nonempty {ğ“¤}) universes-are-flabby-Î 
Nonempty-Î -data {ğ“¤} = Î -construction is-nonempty T T-refl c
 where
  S = is-nonempty

  T : {X Y : ğ“¤ Ì‡ } â†’ (X â‰ƒ Y) â†’ S X â†’ S Y
  T e = Â¬Â¬-functor âŒœ e âŒ

  T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
  T-refl x = refl

  Ïƒ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡ )
    â†’ ((h : p holds) â†’ S (A h)) â†’ S (Î  A)
  Ïƒ p A Ï† Î½ = III
   where
    I : (h : p holds) â†’ Â¬ A h
    I h a = Î½ (Î» h' â†’ transport A (holds-is-prop p h h') a)

    II : Â¬ (p holds)
    II h = Ï† h (I h)

    III : ğŸ˜
    III = Î½ (Î» h â†’ ğŸ˜-elim (II h))

  c : compatibility-data-Î  S T T-refl
  c p A = Ïƒ p A , (Î» Ï† â†’ dfunext fe' (Î» h â†’ negations-are-props fe' _ _))

ainjectivity-of-type-of-nonempty-types
 : ainjective-type (Î£ X ê‰ ğ“¤ Ì‡ , is-nonempty X) ğ“¤ ğ“¤
ainjectivity-of-type-of-nonempty-types =
 ainjectivity-of-type-of-structures
  is-nonempty
  universes-are-flabby-Î 
  Nonempty-Î -data

\end{code}

Added 5 November 2025 by Tom de Jong.

All previous examples used the Î -flabbiness structure on the universe. In what
follows we put the extra generality of our machinery to good use by instead
employing the Î£-flabbiness structure to prove that the type of metric spaces is
injective.

As a first step we show that the collection of types with an R-valued relation
(for an arbitrary type R, later taken to be â„) to be injective.
We denote this type by Graph' as it generalizes the type Graph of graphs defined
above. Indeed, the injectivity proof mirrors the above construction for Graph.

\begin{code}

open import UF.Subsingletons-Properties

module _ (R : ğ“¥ Ì‡ ) where

 Graph' : (ğ“¤ : Universe) â†’ ğ“¤ âº âŠ” ğ“¥ Ì‡
 Graph' ğ“¤ = Î£ X ê‰ ğ“¤ Ì‡  , (X â†’ X â†’ R)

 graph'-structure : ğ“¤ Ì‡  â†’ ğ“¥ âŠ” ğ“¤ Ì‡
 graph'-structure X = (X â†’ X â†’ R)

 Graph'-Î£-data
  : compatibility-data (graph'-structure {ğ“¤}) universes-are-flabby-Î£
 Graph'-Î£-data {ğ“¤} =
  Î£-construction S T T-refl c
   where
    S = graph'-structure

    T : {X Y : ğ“¤ Ì‡ } â†’ X â‰ƒ Y â†’ S X â†’ S Y
    T ğ•— Î¼ y y' = Î¼ (âŒœ ğ•— âŒâ»Â¹ y) (âŒœ ğ•— âŒâ»Â¹ y')

    T-refl : {X : ğ“¤ Ì‡ } â†’ T (â‰ƒ-refl X) âˆ¼ id
    T-refl R = refl

    module _ (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡) where

     r :  S (Î£ A) â†’ ((h : p holds) â†’ S (A h))
     r Î¼ h a a' = Î¼ (h , a) (h , a')

     _ : r ï¼ ÏÎ£ S T T-refl p A
     _ = refl

     Ïƒ : ((h : p holds) â†’ S (A h)) â†’ S (Î£ A)
     Ïƒ g (h , a) (h' , a') =
      g h (âŒœ Î£-ğ•šğ•Ÿ p {A} h âŒâ»Â¹ (h , a)) (âŒœ Î£-ğ•šğ•Ÿ p h âŒâ»Â¹ (h' , a'))

     rÏƒ : r âˆ˜ Ïƒ âˆ¼ id
     rÏƒ g = dfunext fe' (Î» h â†’
            dfunext fe' (Î» a â†’
            dfunext fe' (Î» a' â†’
            apâ‚‚ (g h) (transport-over-prop (holds-is-prop p))
                      (transport-over-prop (holds-is-prop p)))))

    c : compatibility-data-Î£ S T T-refl
    c p A = Ïƒ p A , rÏƒ p A

 ainjectivity-of-Graph'
  : ainjective-type (Graph' ğ“¤) ğ“¤ ğ“¤
 ainjectivity-of-Graph' =
  ainjectivity-of-type-of-structures
   graph'-structure
   universes-are-flabby-Î£
   Graph'-Î£-data

\end{code}

We now take R = â„, the type of Dedekind reals, and additionally impose the
axioms of a metric space.

This mirrors the above construction for the type of posets.

\begin{code}

open import UF.PropTrunc

module _
        (pt : propositional-truncations-exist)
       where

 open PropositionalTruncation pt

 private
  pe : PropExt
  pe = Univalence-gives-PropExt ua

  pe' : Prop-Ext
  pe' {ğ“¤} = pe ğ“¤

 open import DedekindReals.Addition fe' pe' pt
  renaming (_+_ to _+â„_) hiding (_-_)
 open import DedekindReals.Order fe' pe' pt
 open import DedekindReals.Type fe' pe' pt
 open import MetricSpaces.StandardDefinition fe' pe' pt

 Metric-Space-Î£-data : compatibility-data {(ğ“¤ âŠ” ğ“¤â‚) âº}
                        (Î» M â†’ Î£ d ê‰ (M â†’ M â†’ â„) , metric-axioms M d)
                        universes-are-flabby-Î£
 Metric-Space-Î£-data =
  compatibility-data-with-axioms
   universes-are-flabby-Î£
   (graph'-structure â„)
   (Graph'-Î£-data â„)
   metric-axioms
   metric-axioms-is-prop
   axioms-Î£-data
  where
   Ïƒ : (p : Î© ğ“¤) (A : p holds â†’ ğ“¤ Ì‡ )
     â†’ ((h : p holds) â†’ graph'-structure â„ (A h))
     â†’ graph'-structure â„ (Î£ A)
   Ïƒ p A = section-map
            (Ï (graph'-structure â„) universes-are-flabby-Î£ p A)
            (Graph'-Î£-data â„ p A)

   axioms-Î£-data
    : (p : Î© ğ“¤)
      (A : p holds â†’ ğ“¤ Ì‡ )
      (Î± : (h : p holds) â†’ graph'-structure â„ (A h))
      (F : (h : p holds) â†’ metric-axioms (A h) (Î± h))
    â†’ metric-axioms (Î£ A) (Ïƒ p A Î±)
   axioms-Î£-data p A Î± F = I , II , III
    where
     dâ‚š : {h : p holds} â†’ A h â†’ A h â†’ â„
     dâ‚š {h} = Î± h

     dâ‚š-reflexive : {h : p holds} â†’ reflexivity (A h) dâ‚š
     dâ‚š-reflexive {h} = prâ‚ (F h)

     dâ‚š-symmetric : {h : p holds} â†’ symmetry (A h) dâ‚š
     dâ‚š-symmetric {h} = prâ‚ (prâ‚‚ (F h))

     dâ‚š-triangle-inequality : {h : p holds} â†’ triangle-inequality (A h) dâ‚š
     dâ‚š-triangle-inequality {h} = prâ‚‚ (prâ‚‚ (F h))

     i : {h h' : p holds} â†’ h ï¼ h'
     i = holds-is-prop p _ _

     Ï„ : {h h' : p holds} â†’ A h â†’ A h'
     Ï„ = transport A i

     d : Î£ A â†’ Î£ A â†’ â„
     d (hâ‚ , aâ‚) (hâ‚‚ , aâ‚‚) = Î± hâ‚ (Ï„ aâ‚) (Ï„ aâ‚‚)

     lemma : {hâ‚ hâ‚‚ : p holds} {aâ‚ : A hâ‚} {aâ‚‚ : A hâ‚‚}
             (eâ‚ : hâ‚‚ ï¼ hâ‚) (eâ‚‚ : hâ‚ ï¼ hâ‚)
             (eâ‚ƒ : hâ‚‚ ï¼ hâ‚‚) (eâ‚„ : hâ‚ ï¼ hâ‚‚)
           â†’ Î± hâ‚ (transport A eâ‚ aâ‚‚) (transport A eâ‚‚ aâ‚)
             ï¼ Î± hâ‚‚ (transport A eâ‚ƒ aâ‚‚) (transport A eâ‚„ aâ‚)
     lemma {hâ‚} {hâ‚‚} {aâ‚} {aâ‚‚} refl eâ‚‚ eâ‚ƒ eâ‚„ =
      apâ‚‚ (Î± hâ‚‚)
          ((transport-over-prop' (holds-is-prop p) eâ‚ƒ) â»Â¹)
          (ap (Î» - â†’ transport A - aâ‚)
              (props-are-sets (holds-is-prop p) eâ‚‚ eâ‚„))

     dâ‚š-equals-d : {hâ‚ hâ‚‚ : p holds} {aâ‚ : A hâ‚} {aâ‚‚ : A hâ‚‚}
                 â†’ dâ‚š (Ï„ aâ‚) (Ï„ aâ‚‚) ï¼ d (hâ‚ , aâ‚) (hâ‚‚ , aâ‚‚)
     dâ‚š-equals-d = refl

     dâ‚š-equals-d-left : {hâ‚ hâ‚‚ : p holds} {aâ‚ : A hâ‚} {aâ‚‚ : A hâ‚‚}
                      â†’ dâ‚š (Ï„ aâ‚) aâ‚‚ ï¼ d (hâ‚ , aâ‚) (hâ‚‚ , aâ‚‚)
     dâ‚š-equals-d-left = lemma i refl i i

     dâ‚š-equals-d-right : {hâ‚ hâ‚‚ : p holds} {aâ‚ : A hâ‚} {aâ‚‚ : A hâ‚‚}
                       â†’ dâ‚š aâ‚ (Ï„ aâ‚‚) ï¼ d (hâ‚ , aâ‚) (hâ‚‚ , aâ‚‚)
     dâ‚š-equals-d-right = lemma refl refl i refl

     _ : Ïƒ p A Î± ï¼ d
     _ = refl -- Which is crucial for the proof below to work.

     I : reflexivity (Î£ A) (Ïƒ p A Î±)
     I x@(hâ‚ , a) y@(hâ‚‚ , a') = Iâ‚ , Iâ‚‚
      where
       Iâ‚ : d x y ï¼ 0â„ â†’ x ï¼ y
       Iâ‚ e = to-Î£-ï¼ (i , lr-implication (dâ‚š-reflexive (Ï„ a) a')
                                          (dâ‚š-equals-d-left âˆ™ e))
       Iâ‚‚ : x ï¼ y â†’ d x y ï¼ 0â„
       Iâ‚‚ refl = rl-implication (dâ‚š-reflexive (Ï„ a) (Ï„ a)) refl

     II : symmetry (Î£ A) d
     II (hâ‚ , aâ‚) (hâ‚‚ , aâ‚‚) =
      dâ‚š {hâ‚} (Ï„ aâ‚) (Ï„ aâ‚‚) ï¼âŸ¨ dâ‚š-symmetric (Ï„ aâ‚) (Ï„ aâ‚‚) âŸ©
      dâ‚š {hâ‚} (Ï„ aâ‚‚) (Ï„ aâ‚) ï¼âŸ¨ lemma i i i i âŸ©
      dâ‚š {hâ‚‚} (Ï„ aâ‚‚) (Ï„ aâ‚) âˆ

     III : triangle-inequality (Î£ A) (Ïƒ p A Î±)
     III x@(hâ‚ , aâ‚) y@(hâ‚‚ , aâ‚‚) z@(hâ‚ƒ , aâ‚ƒ) = IIIâ‚‚
      where
       IIIâ‚ : dâ‚š {hâ‚‚} (Ï„ aâ‚) aâ‚‚ +â„ dâ‚š {hâ‚‚} aâ‚‚ (Ï„ aâ‚ƒ) â‰¤â„ dâ‚š {hâ‚‚} (Ï„ aâ‚) (Ï„ aâ‚ƒ)
       IIIâ‚ = dâ‚š-triangle-inequality (Ï„ aâ‚) aâ‚‚ (Ï„ aâ‚ƒ)
       IIIâ‚‚ : d x y +â„ d y z â‰¤â„ d x z
       IIIâ‚‚ = transportâ‚ƒ (Î» râ‚ râ‚‚ râ‚ƒ â†’ râ‚ +â„ râ‚‚ â‰¤â„ râ‚ƒ)
              dâ‚š-equals-d-left dâ‚š-equals-d-right (lem i i) IIIâ‚
        where
         lem : (eâ‚ : hâ‚ ï¼ hâ‚‚) (eâ‚‚ : hâ‚ƒ ï¼ hâ‚‚)
             â†’ dâ‚š {hâ‚‚} (transport A eâ‚ aâ‚) (transport A eâ‚‚ aâ‚ƒ)
               ï¼ d (hâ‚ , aâ‚) (hâ‚ƒ , aâ‚ƒ)
         lem refl refl = lemma refl refl i i

 ainjectivity-of-Metric-Space
  : ainjective-type (Metric-Space (ğ“¤â‚ âŠ” ğ“¤)) (ğ“¤â‚ âŠ” ğ“¤) (ğ“¤â‚ âŠ” ğ“¤)
 ainjectivity-of-Metric-Space {ğ“¤} =
  ainjectivity-of-type-of-structures
   (Î» M â†’ Î£ d ê‰ (M â†’ M â†’ â„) , metric-axioms M d)
   universes-are-flabby-Î£
   (Metric-Space-Î£-data {ğ“¤})

\end{code}
