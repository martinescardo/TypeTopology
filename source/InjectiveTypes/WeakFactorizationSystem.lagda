Tom de Jong, 22 May 2025
A weak factorization system of embeddings and fiberwise injective maps

An anonymous reviewer of our TYPES abstract [1] suggested that some of our
results could be generalized to weak factorization systems. Here we consider a
factorization system based on embeddings and maps whose fibers are all injective
types. We are also thinking about the connections to *algebraic* weak
factorization systems.

[1] Tom de Jong and MartÄ±Ìn HÃ¶tzel EscardÃ³.
    "Examples and counter-examples of injective types in univalent mathematics".
    Abstract for the 31st International Conference on Types for Proofs and
    Programs (TYPES). 2025.
    url: https://msp.cis.strath.ac.uk/types2025/abstracts/TYPES2025_paper6.pdf.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module InjectiveTypes.WeakFactorizationSystem
        (fe : FunExt)
       where

open import InjectiveTypes.Blackboard fe

open import MLTT.Spartan

open import UF.Base
open import UF.Embeddings
open import UF.Equiv
open import UF.EquivalenceExamples
open import UF.IdEmbedding
open import UF.Univalence

\end{code}

We define a fiberwise algebraically injective map to be one whose fibers are all
algebraically injective types.

NB. It may be that fiberwise flabbiness is more convenient to work with.

\begin{code}

fiberwise-ainjective : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ } â†’ (A â†’ B) â†’ (ğ“¦ ğ“£ : Universe)
                     â†’ ((ğ“¦ âŠ” ğ“£) âº âŠ” ğ“¤ âŠ” ğ“¥) Ì‡
fiberwise-ainjective f ğ“¦ ğ“£ =
 each-fiber-of f (Î» F â†’ ainjective-type F ğ“¦ ğ“£)

\end{code}

Any map can be factored as an embedding followed by a fiberwise algebraically
injective map.

\begin{code}

embedding-fiberwise-ainjective-factorization
 : {A : ğ“¤ Ì‡ } {B : ğ“¥ Ì‡ }
 â†’ is-univalent (ğ“¤ âŠ” ğ“¥)
 â†’ (f : A â†’ B)
 â†’ Î£ X ê‰ (ğ“¤ âŠ” ğ“¥)âº Ì‡  ,
   Î£ l ê‰ (A â†’ X) ,
   Î£ r ê‰ (X â†’ B) , (f ï¼ r âˆ˜ l)
                 Ã— is-embedding l
                 Ã— fiberwise-ainjective r (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
embedding-fiberwise-ainjective-factorization {ğ“¤} {ğ“¥} {A} {B} ua f =
 X , l , r , refl , l-is-embedding ua , r-fiberwise-ainjective ua
  where
   X : (ğ“¤ âŠ” ğ“¥) âº Ì‡
   X = Î£ b ê‰ B , (fiber f b â†’ (ğ“¤ âŠ” ğ“¥) Ì‡ )

   Î¹ : (Y : ğ“¤' Ì‡ ) â†’ Y â†’ (Y â†’ ğ“¤' Ì‡ )
   Î¹ Y = Id

   Î¹-is-embedding : is-univalent ğ“¤' â†’ (Y : ğ“¤' Ì‡ ) â†’ is-embedding (Î¹ Y)
   Î¹-is-embedding ua _ = UA-Id-embedding ua fe

   l : A â†’ X
   l = NatÎ£ (Î» b â†’ Î¹ (fiber f b)) âˆ˜ âŒœ domain-is-total-fiber f âŒ

   l-is-embedding : is-univalent (ğ“¤ âŠ” ğ“¥) â†’ is-embedding l
   l-is-embedding ua =
    âˆ˜-is-embedding
     (equivs-are-embeddings' (domain-is-total-fiber f))
     (NatÎ£-is-embedding
       (fiber f)
       (Î» b â†’ fiber f b â†’ ğ“¤ âŠ” ğ“¥ Ì‡ )
       (Î» b â†’ Î¹ (fiber f b))
       (Î» b â†’ Î¹-is-embedding ua (fiber f b)))

   r : X â†’ B
   r = prâ‚

   r-fiberwise-ainjective : is-univalent (ğ“¤ âŠ” ğ“¥)
                          â†’ fiberwise-ainjective r (ğ“¤ âŠ” ğ“¥) (ğ“¤ âŠ” ğ“¥)
   r-fiberwise-ainjective ua b =
    equiv-to-ainjective
     (fiber r b)
     (fiber f b â†’ ğ“¤ âŠ” ğ“¥ Ì‡ )
     (power-of-ainjective (universes-are-ainjective-Î ' ua))
     (prâ‚-fiber-equiv b)

\end{code}

We have (specified) diagonal lifts of embeddings against fiberwise algebraically
injective maps.

We consider a commutative square with j an embedding and r fiberwise
algebraically injective and we look to find diagonal filler: a map e : Y â†’ D
making the resulting triangles commute.

       f
  X ------> D
  |       ^ |
  |      /  |
j |  âˆƒe /   | r
  |    /    |
  |   /     |
  v  /      v
  Y ------> E
       g

Note that in our case we have a designated filler.

\begin{code}

module lifting-problem
        {X : ğ“¤ Ì‡ } {Y : ğ“¥ Ì‡ } {D : ğ“¦ Ì‡ } {E : ğ“£ Ì‡ }
        (j : X â†’ Y) (f : X â†’ D) (r : D â†’ E) (g : Y â†’ E)
        (j-is-embedding : is-embedding j)
        (r-fiberwise-ainjective : fiberwise-ainjective r (ğ“¤ âŠ” ğ“¥) ğ“£')
        -- NB. The last universe parameter is arbitrary.
        (comm-sq : r âˆ˜ f âˆ¼ g âˆ˜ j)
       where

 lifting-problem-has-a-solution : Î£ eÂ ê‰ (Y â†’ D) , (e âˆ˜ j âˆ¼ f) Ã— (r âˆ˜ e âˆ¼ g)
 lifting-problem-has-a-solution = e , e-upper-triangle , e-lower-triangle
  where
   module _ (y : Y) where

    fÌ… : fiber j y â†’ fiber r (g y)
    fÌ… (x , e) = (f x , (r (f x) ï¼âŸ¨ comm-sq x âŸ©
                        g (j x) ï¼âŸ¨ ap g e âŸ©
                        g y     âˆ))

    ğ•– : Î£ e ê‰ fiber r (g y) , ((p : fiber j y) â†’ e ï¼ fÌ… p)
    ğ•– = ainjective-types-are-aflabby
         (fiber r (g y))
         (r-fiberwise-ainjective (g y))
         (fiber j y)
         (j-is-embedding y)
         fÌ…

    eÊ¸ = prâ‚ ğ•–
    eÊ¸-extends : (p : fiber j y) â†’ eÊ¸ ï¼ fÌ… p
    eÊ¸-extends = prâ‚‚ ğ•–

   e : Y â†’ D
   e y = prâ‚ (eÊ¸ y)

   e-lower-triangle : r âˆ˜ e âˆ¼ g
   e-lower-triangle y = prâ‚‚ (eÊ¸ y)

   e-upper-triangle : e âˆ˜ j âˆ¼ f
   e-upper-triangle x = ap prâ‚ I
    where
     I : eÊ¸ (j x) ï¼ fÌ… (j x) (x , refl)
     I = eÊ¸-extends (j x) (x , refl)

\end{code}

In the above it is important to work with *algebraically* injective types: if we
instead had assumed that each fiber of r is only injective, then we would have
gotten for each y : Y an unspecified element of D only, which, in the absence of
choice, fails to produce a function.

Finally, since propositions and injective types are closed under retracts and
because retractions of maps induce retractions of their fibers [HoTTBook,
Lemma 4.7.3], the embeddings and the fiberwise injective maps are closed under
retracts, so that by the "retract argument" [Rie14, Lemma 11.2.3], the
embeddings and fiberwise injective maps indeed give rise to a weak factorization
system.

TODO. Formalize this and, as a preliminary, retracts of maps.

[Rie14] Emily Riehl. Categorical Homotopy Theory.
        New Mathematical Monographs 24.
        Cambridge University Press, 2014.
        doi: 10.1017/ CBO9781107261457.
        url: https://math.jhu.edu/~eriehl/cathtpy.pdf.