Martin Escardo, 23 June 2025.

The following paper has a notion of flabbiness in the internal
language of a 1-topos.

[1] Ingo Blechschmidt (2018). Flabby and injective objects in toposes.
    https://arxiv.org/abs/1810.12708

This definition of flabbiness uses the two notions of "subterminal"
and "subsingleton" subsets, as defined in e.g.

[2] Kock, A. (1991). Algebras for the partial map classifier
    monad. In: Carboni, A., Pedicchio, M.C., Rosolini, G. (eds)
    Category Theory. Lecture Notes in Mathematics, vol 1488. Springer,
    Berlin, Heidelberg. https://doi.org/10.1007/BFb0084225

We show that the notion of flabbiness defined in [1] coincides with
ours for types that are sets, or 1-types, in the sense of HoTT/UF.

*Terminological warning.* Sometimes we use, in names of functions, and
in discussions, the word "set" to refer to "subset", to e.g. avoid
awkward names such as "is-subterminal-subset".

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt

module InjectiveTypes.AlternativeFlabbiness
        (fe : Fun-Ext)
       where

open import InjectiveTypes.Structure
open import InjectiveTypes.Blackboard
open import MLTT.Spartan
open import UF.Embeddings
open import UF.Equiv
open import UF.Powerset
open import UF.PropTrunc
open import UF.Sets
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier

\end{code}

The references [1] and [2] work with the following two concepts, in
the internal language of an elementary 1-topos.

(1) A set K : ğ“Ÿ X is *subterminal* if and only if any two elements of
    K are equal.

(2) A set K : ğ“Ÿ X is a *subsingleton* if there is xâ‚€ : X with K âŠ† {xâ‚€}.

In our more general setting of HoTT/UF, which can be considered as an
internal language for âˆ-toposes, the singleton {x} can be formed if X
is a set, or 1-type, in the sense of HoTT/UF (and if and only if xâ‚€ is
homotopy isolated, meaning that the equality xâ‚€ = x is a proposition
for every x : X).

But K âŠ† {xâ‚€}, in their setting, amounts to the implication
x âˆˆ K â†’ x ï¼ xâ‚€, and so that we can circumvent this obstacle.

(2') A set K : ğ“Ÿ X is a *subsingleton* if there is xâ‚€ : X such that
     x âˆˆ K implies x ï¼ xâ‚€ for all x : X.

In the setting of [1] and [2], conditions (2) and (2') are equivalent,
and only (2') makes sense in our setting for an arbitrary type X,
which is what we adopt below.

(However, in any case, we will eventually need to assume that X is a
1-type, as the internal definition of flabbiness is tailored for
1-toposes.)

We have that (1) is property if X is a set, and the above
reformulation (2') of (2) is always a proposition.

To begin with, we will work with the following notion, which is data
rather than property.

(3) *Singleton data* for a set K : ğ“Ÿ X consists of a designated point
     xâ‚€ : X such that x âˆˆ K implies x = xâ‚€ for all x : X.

The difference between (2) and (3) is that in (2) the point xâ‚€ merely
exists, but in (3) it is part of the data.

We begin by formally discussing (1) and (3), leaving (2) for later.

\begin{code}

module _ {X : ğ“¤ Ì‡ } (K : ğ“Ÿ X) where

 subterminal-set : ğ“¤ Ì‡
 subterminal-set = (x y : X) â†’ x âˆˆ K â†’ y âˆˆ K â†’ x ï¼ y

\end{code}

Notice that the above is strictly speak data unless X is a set.

\begin{code}

 being-subterminal-set-is-prop
  : is-set X
  â†’ is-prop subterminal-set
 being-subterminal-set-is-prop X-is-set
  = Î â‚„-is-prop fe (Î» _ _ _ _ â†’ X-is-set)

 subsingleton-set-data : ğ“¤ Ì‡
 subsingleton-set-data = Î£ xâ‚€ ê‰ X , ((x : X) â†’ x âˆˆ K â†’ x ï¼ xâ‚€)

\end{code}

As observed in [1], subsingleton sets are subterminal. We also have
the following, replacing the subsigleton property by subsingleton
data.

\begin{code}

 sets-with-subsingleton-data-are-subterminal
  : subsingleton-set-data
  â†’ subterminal-set
 sets-with-subsingleton-data-are-subterminal (xâ‚€ , Ï•) x y i j
  = Ï• x i âˆ™ (Ï• y j)â»Â¹

\end{code}

We make the converse construction, which isn't always possible, into
an alternative definition of flabby structure.

\begin{code}

flabby-structure' : ğ“¤ Ì‡ â†’ ğ“¤ âº Ì‡
flabby-structure' X = (K : ğ“Ÿ X)
                    â†’ subterminal-set K
                    â†’ subsingleton-set-data K

\end{code}

The following two observations are not used directly in our
discussion, but may be enlightening. They say that the total space
ğ•‹ K := Î£ x ê‰ X , x âˆˆ K of the subset K of X is a proposition, assuming
that K is subterminal, or, in particular, that it is equipped with
subsingleton data.

\begin{code}

module _ {X : ğ“¤ Ì‡ } (K : ğ“Ÿ X) where

 subterminals-have-propositional-total-space
  : subterminal-set K
  â†’ is-prop (ğ•‹ K)
 subterminals-have-propositional-total-space s (x , m) (y , n)
  = to-subtype-ï¼ (âˆˆ-is-prop K) (s x y m n)

 types-with-subsubgleton-data-have-propositional-total-space
  : subsingleton-set-data K
  â†’ is-prop (ğ•‹ K)
 types-with-subsubgleton-data-have-propositional-total-space s
  = subterminals-have-propositional-total-space
     (sets-with-subsingleton-data-are-subterminal K s)

\end{code}

We now show that we can construct flabby structure from the
alternative flabby structure, and conversely.

The first direction requires X to be a 1-type, or set.

\begin{code}

module _ {X : ğ“¤ Ì‡ } where

 flabby-structure'-gives-flabby-structure
   : is-set X
   â†’ flabby-structure' X
   â†’ flabby-structure X ğ“¤
 flabby-structure'-gives-flabby-structure X-is-set a = â¨† , e
  where
   module _ (P : Î© ğ“¤) (f : P holds â†’ X) where

    K : ğ“Ÿ X
    K x = fiber f x ,
          maps-of-props-into-sets-are-embeddings f (holds-is-prop P) X-is-set x

    I : subterminal-set K
    I x y (p , d) (q , e) =
     x   ï¼âŸ¨ d â»Â¹ âŸ©
     f p ï¼âŸ¨ ap f (holds-is-prop P p q) âŸ©
     f q ï¼âŸ¨ e âŸ©
     y   âˆ

    II : subsingleton-set-data K
    II = a K I

    â¨† : X
    â¨† = prâ‚ II

    _ : (x : X) â†’ fiber f x â†’ x ï¼ â¨†
    _ = prâ‚‚ II

    e : (p : P holds) â†’ â¨† ï¼ f p
    e p = (prâ‚‚ II (f p) (p , refl))â»Â¹

\end{code}

The converse doesn't require X to a 1-type.

\begin{code}

 flabby-structure-gives-flabby-structure'
  : flabby-structure X ğ“¤
  â†’ flabby-structure' X
 flabby-structure-gives-flabby-structure' (â¨† , e) K K-subterminal = xâ‚€ , I
  where
   P : Î© ğ“¤
   P = (Î£ x ê‰ X , x âˆˆ K) ,
       (Î» {(x , m) (y , n) â†’ to-subtype-ï¼
                              (âˆˆ-is-prop K)
                              (K-subterminal x y m n)})

   f : P holds â†’ X
   f = prâ‚

   xâ‚€ : X
   xâ‚€ = â¨† P f

   I : (x : X) â†’ x âˆˆ K â†’ x ï¼ xâ‚€
   I x m = (e P f (x , m))â»Â¹

\end{code}

We discuss the truncated version now, which is what is relevant for
the comparison with the reference [1], as discussed above.

We have already defined the notions (1) and (3) above, and it remains
to define the notion (2), which we call is-subsingleton-set. For that
purpose, we need assume that propositional truncations exist, so that
we have the existential quantifier âˆƒ available.

\begin{code}

 module _
          (pt : propositional-truncations-exist)
        where

  open PropositionalTruncation pt
  open injective (Î» ğ“¤ ğ“¥ â†’ fe {ğ“¤} {ğ“¥}) pt

  module _ (K : ğ“Ÿ X) where

   is-subsingleton-set : ğ“¤ Ì‡
   is-subsingleton-set = âˆƒ xâ‚€ ê‰ X , ((x : X) â†’ x âˆˆ K â†’ x ï¼ xâ‚€)

   being-subsingleton-set-is-prop : is-prop is-subsingleton-set
   being-subsingleton-set-is-prop = âˆƒ-is-prop

\end{code}

As observed in [1], subsingleton sets are subterminal. In our more
general setting, we need to assume that X is a 1-type to reach the
same conclusion.

\begin{code}

   subsingleton-sets-are-subterminal
    : is-set X
    â†’ is-subsingleton-set
    â†’ subterminal-set K
   subsingleton-sets-are-subterminal X-is-set =
    âˆ¥âˆ¥-rec
     (being-subterminal-set-is-prop K X-is-set)
     (sets-with-subsingleton-data-are-subterminal K)

\end{code}

And the following is the internal definition of flabbiness proposed in [1],
as a converse of the above fact.

\begin{code}

  flabby' : ğ“¤ âº Ì‡
  flabby' = (K : ğ“Ÿ {ğ“¤} X)
          â†’ subterminal-set K
          â†’ is-subsingleton-set K

\end{code}

In this repository we have our own internal definition of flabbiness
of a type X, called fabby, which says that for every proposiiton P and
function f : P â†’ X, there exists x : X such that x = f p for all p : P.

We now show that this is equivalent to the definition given in [1],
where the first direction assumes that X is a set.

Notice that this is a logical equivalence, as stated, but also a typal
equivalence because the two notions of flabbiness are property.

\begin{code}

  flabby'-gives-flabby : is-set X â†’ flabby' â†’ flabby X ğ“¤
  flabby'-gives-flabby X-is-set Ï•' P P-is-prop f = IV
   where
    K : ğ“Ÿ X
    K x = fiber f x ,
          maps-of-props-into-sets-are-embeddings f P-is-prop X-is-set x

    I : subterminal-set K
    I x y (p , d) (q , e) =
     x   ï¼âŸ¨ d â»Â¹ âŸ©
     f p ï¼âŸ¨ ap f (P-is-prop p q) âŸ©
     f q ï¼âŸ¨ e âŸ©
     y   âˆ

    II : is-subsingleton-set K
    II = Ï•' K I

    III : (Î£ xâ‚€ ê‰ X , ((x : X) â†’ x âˆˆ K â†’ x ï¼ xâ‚€))
        â†’ (Î£ x ê‰ X , ((p : P) â†’ x ï¼ f p))
    III (xâ‚€ , Î±) = xâ‚€ , (Î» p â†’ (Î± (f p) (p , refl))â»Â¹)

    IV : âˆƒ x ê‰ X , ((p : P) â†’ x ï¼ f p)
    IV = âˆ¥âˆ¥-functor III II

  flabby-gives-flabby' : flabby X ğ“¤ â†’ flabby'
  flabby-gives-flabby' Ï• K K-subterminal = II
   where
    P : Î© ğ“¤
    P = (Î£ x ê‰ X , x âˆˆ K) ,
        (Î» {(x , m) (y , n) â†’ to-subtype-ï¼
                               (âˆˆ-is-prop K)
                               (K-subterminal x y m n)})

    f : P holds â†’ X
    f = prâ‚

    I : âˆƒ xâ‚€ ê‰ X , ((p : P holds) â†’ xâ‚€ ï¼ prâ‚ p)
    I = Ï• (P holds) (holds-is-prop P) f

    II : âˆƒ xâ‚€ ê‰ X , ((x : X) â†’ x âˆˆ K â†’ x ï¼ xâ‚€)
    II = âˆ¥âˆ¥-functor (Î» (xâ‚€ , e) â†’ xâ‚€ , (Î» x m â†’ (e (x , m))â»Â¹)) I

\end{code}

So, at least for sets, this justifies our internal definition of
flabbiness used in this repository. Perhaps an âˆ-topos theorist can
chime in and discuss whether our proposed internal definition does
correspond to any external definition of flabbiness discussed in the
âˆ-topos literature.
