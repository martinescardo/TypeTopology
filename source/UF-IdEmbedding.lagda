Martin Escardo, 2015, formalized December 2017.

Id : X â†’ (X â†’ U) is an embedding assuming functional extensionality,
and either univalence or K, in fact the Yoneda Embedding.

The Id-fiber of A:Xâ†’ğ“¤ Ì‡ says that A is representable, which is
equivalent to the contractibility of Î£A, which is a
proposition. (Hence the injective types are the retracts of the
exponential powers of the universe.)

This works as follows in outline:

If A : X â†’ ğ“¤ Ì‡ then the Id-fiber of A is Î£ x ê‰ X , Id x â‰¡ A.

If the pair (x,p) is in the fiber for x : X and p : Id x = A, then

   ap Î£ p : Î£ (Id x) = Î£ A,

and hence, being equal to a contractible type, the type Î£ A is
contractible.

Next we have (*)

 A x â‰ƒ Nat (Id x) A             (yoneda)
     = (y : X) â†’ Id x y â†’ A y   (definition)
     â‰ƒ (y : X) â†’ Id x y â‰ƒ A y   (because Î£ A is contractible (Yoneda corollary))
     â‰ƒ (y : X) â†’ Id x y â‰¡ A y   (by univalence)
     â‰ƒ Id x â‰¡ A                 (by function extensionality)

Applying Î£ to both sides, Î£ A â‰ƒ (Î£ x ê‰ X , Id x â‰¡ A), and because
the type Î£ A is contractible so is Î£ x ê‰ X , Id x â‰¡ A, which shows
that the map Id : X â†’ (X â†’ U) is an embedding.

2017:

This relies on univalence. But less than that suffices
(https://groups.google.com/forum/#!topic/homotopytypetheory/bKti7krHM-c)

First, Evan Cavallo showed that it is enough to assume funext and that
the canonical map X â‰¡ Y â†’ X â‰ƒ Y is an embedding. Then, using this idea
and the above proof outline, we further generalized this to assume
that the canonical map X â‰¡ Y â†’ (X â†’ Y) is left-cancellable (which is
much weaker than assuming that it is an embedding).

This is what we record next (9th December 2017), using the original
idea (*) in the weakened form discussed above.

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

module UF-IdEmbedding where

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-FunExt
open import UF-Lower-FunExt
open import UF-Equiv
open import UF-Equiv-FunExt
open import UF-Embeddings
open import UF-Yoneda
open import UF-LeftCancellable
open import UF-Univalence
open import UF-EquivalenceExamples

\end{code}

The Id Embedding Lemma. The idea is to show that the type
T := Î£ x ê‰ X , Id x â‰¡ A is a proposition by showing that there is a
left-cancellable map from it to a proposition, namely the contractible
type Î£ A.

\begin{code}

Id-Embedding-Lemma : FunExt
                   â†’ {X : ğ“¤ Ì‡ }
                   â†’ ((x y : X) (A : X â†’ ğ“¤ Ì‡ )
                   â†’ left-cancellable (idtofun (Id x y) (A y)))
                   â†’ is-embedding(Id {ğ“¤} {X})
Id-Embedding-Lemma {ğ“¤} fe {X} iflc A (xâ‚€ , pâ‚€) = h (xâ‚€ , pâ‚€)
 where
  T = Î£ x ê‰ X , Id x â‰¡ A
  q : Î£ (Id xâ‚€) â‰¡ Î£ A
  q = ap Î£ pâ‚€
  c : âˆƒ! A
  c = yoneda-nat (singleton-type xâ‚€) is-singleton (singleton-types-are-singletons xâ‚€) (Î£ A) q
  fâ‚€ : (x : X) â†’ Id x â‰¡ A â†’ (y : X) â†’ Id x y â‰¡ A y
  fâ‚€ x = happly
  fâ‚ : (x : X) â†’ ((y : X) â†’ Id x y â‰¡ A y) â†’ Nat (Id x) A
  fâ‚ x = NatÎ  (Î» y â†’ idtofun (Id x y) (A y))
  fâ‚‚ : (x : X) â†’ Nat (Id x) A â†’ A x
  fâ‚‚ x = yoneda-elem x A
  f : (x : X) â†’ Id x â‰¡ A â†’ A x
  f x = fâ‚‚ x âˆ˜ fâ‚ x âˆ˜ fâ‚€ x
  fâ‚€-lc : (x : X) â†’ left-cancellable(fâ‚€ x)
  fâ‚€-lc x = happly-lc (fe ğ“¤ (ğ“¤ âº)) (Id x) A
  fâ‚-lc : (x : X) â†’ left-cancellable(fâ‚ x)
  fâ‚-lc x = g
    where
      l : âˆ€ {Ï† Ï†'} â†’ fâ‚ x Ï† â‰¡ fâ‚ x Ï†' â†’ (x : X) â†’ Ï† x â‰¡ Ï†' x
      l {Ï†} {Ï†'} = NatÎ -lc (Î» y â†’ idtofun (Id x y) (A y)) (Î» y â†’ iflc x y A)
      g : âˆ€ {Ï† Ï†'} â†’ fâ‚ x Ï† â‰¡ fâ‚ x Ï†' â†’ Ï† â‰¡ Ï†'
      g p = dfunext (fe ğ“¤ (ğ“¤ âº)) (l p)
  fâ‚‚-lc : (x : X) â†’ left-cancellable(fâ‚‚ x)
  fâ‚‚-lc x {Î·} {Î·'} p = dfunext (fe ğ“¤ ğ“¤) (Î» y â†’ dfunext (fe ğ“¤ ğ“¤) (l y))
    where
      l : Î· â‰ˆ Î·'
      l = yoneda-elem-lc Î· Î·' p
  f-lc : (x : X) â†’ left-cancellable(f x)
  f-lc x = left-cancellable-closed-under-âˆ˜
               (fâ‚€ x)
               (fâ‚‚ x âˆ˜ fâ‚ x)
               (fâ‚€-lc x)
               (left-cancellable-closed-under-âˆ˜ (fâ‚ x) (fâ‚‚ x) (fâ‚-lc x) (fâ‚‚-lc x))
  g : T â†’ Î£ A
  g = NatÎ£ f
  g-lc : left-cancellable g
  g-lc = NatÎ£-lc f f-lc
  h : is-prop T
  h = left-cancellable-reflects-is-prop g g-lc (singletons-are-props c)

\end{code}

univalence implies that the function Id {ğ“¤} {X} : X â†’ (X â†’ ğ“¤ Ì‡ ) is an embedding.

The map eqtofun is left-cancellable assuming univalence (and function
extensionality, which is a consequence of univalence, but we don't
bother):

\begin{code}

eqtofun-lc : is-univalent ğ“¤
           â†’ FunExt
           â†’ (X Y : ğ“¤ Ì‡ ) â†’ left-cancellable(Eqtofun X Y)
eqtofun-lc ua fe X Y {f , jef} {g , jeg} p = Î³
 where
  q : yoneda-nat f is-equiv jef g p â‰¡ jeg
  q = being-equiv-is-prop fe g _ _
  Î³ : f , jef â‰¡ g , jeg
  Î³ = to-Î£-Id (p , q)

\end{code}

The map idtofun is left-cancellable assuming univalence (and funext):

\begin{code}

is-univalent-idtofun-lc : is-univalent ğ“¤
                        â†’ FunExt
                        â†’ (X Y : ğ“¤ Ì‡ ) â†’ left-cancellable(idtofun X Y)
is-univalent-idtofun-lc  ua fe X Y = left-cancellable-closed-under-âˆ˜
                                        (idtoeq X Y)
                                        (Eqtofun X Y)
                                        (is-univalent-idtoeq-lc ua X Y) (eqtofun-lc ua fe X Y)

UA-Id-embedding : is-univalent ğ“¤
                â†’ FunExt
                â†’ {X : ğ“¤ Ì‡ } â†’ is-embedding(Id {ğ“¤} {X})
UA-Id-embedding {ğ“¤} ua fe {X} = Id-Embedding-Lemma fe
                                            (Î» x y a â†’ is-univalent-idtofun-lc ua fe (Id x y) (a y))

\end{code}

The K axiom and function extensionality together imply that the
function Id : X â†’ (X â†’ U) is an embedding.

\begin{code}

K-Id-embedding' : K-axiom (ğ“¤ âº)
                â†’ FunExt
                â†’ {X : ğ“¤ Ì‡ } â†’ is-embedding(Id {ğ“¤} {X})
K-Id-embedding' {ğ“¤} k fe {X} = Id-Embedding-Lemma fe (K-idtofun-lc k)

\end{code}

But actually function extensionality is not needed for this: K alone suffices.

\begin{code}

Id-lc : {X : ğ“¤ Ì‡ } â†’ left-cancellable (Id {ğ“¤} {X})
Id-lc {ğ“¤} {X} {x} {y} p = idtofun (Id y y) (Id x y) (happly (p â»Â¹) y) refl

K-Id-embedding : K-axiom (ğ“¤ âº) â†’ {X : ğ“¤ Ì‡ } â†’ is-embedding(Id {ğ“¤} {X})
K-Id-embedding {ğ“¤} k {X} = lc-maps-are-embeddings-with-K Id Id-lc k

\end{code}

Added 7th Feb 2019.

\begin{code}

Id-set : {X : ğ“¤ Ì‡ } â†’ is-set X â†’ X â†’ (X â†’ Î© ğ“¤)
Id-set i x y = (x â‰¡ y) , i

Id-set-lc : funext  ğ“¤ (ğ“¤ âº)
          â†’ {X : ğ“¤ Ì‡ } (i : is-set X)
          â†’ left-cancellable (Id-set i)
Id-set-lc fe {X} i {x} {y} e = Id-lc d
 where
  d : Id x â‰¡ Id y
  d = dfunext fe (Î» z â†’ ap prâ‚ (happly e z))

Id-set-is-embedding : funext  ğ“¤ (ğ“¤ âº)
                    â†’ propext ğ“¤
                    â†’ {X : ğ“¤ Ì‡ } (i : is-set X) â†’ is-embedding (Id-set i)
Id-set-is-embedding {ğ“¤} fe pe {X} i = lc-maps-into-sets-are-embeddings
                                        (Id-set i)
                                        (Id-set-lc (lower-funext ğ“¤ (ğ“¤ âº) fe) i)
                                        (Î -is-set fe (Î» x â†’ Î©-is-set (lower-funext ğ“¤ (ğ“¤ âº) fe) pe))
\end{code}
