Tom de Jong
Reboot: 22 January 2021
Earlier version: 18 September 2020

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import Circle.Integers

open import MLTT.Spartan
open import UF.Base
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Sets
open import UF.Subsingletons
open import UF.Subsingletons-Properties

module Circle.Integers-Properties where

â„¤-is-set : is-set â„¤
â„¤-is-set = +-is-set ğŸ™ (â„• + â„•) (props-are-sets ğŸ™-is-prop)
            (+-is-set â„• â„• â„•-is-set â„•-is-set)

succ-â„¤ : â„¤ â†’ â„¤
succ-â„¤ ğŸ              = pos 0
succ-â„¤ (pos n)        = pos (succ n)
succ-â„¤ (neg 0)        = ğŸ
succ-â„¤ (neg (succ n)) = neg n

pred-â„¤ : â„¤ â†’ â„¤
pred-â„¤ ğŸ              = neg 0
pred-â„¤ (pos 0)        = ğŸ
pred-â„¤ (pos (succ n)) = pos n
pred-â„¤ (neg n)        = neg (succ n)

succ-â„¤-is-retraction : succ-â„¤ âˆ˜ pred-â„¤ âˆ¼ id
succ-â„¤-is-retraction ğŸ              = refl
succ-â„¤-is-retraction (pos zero)     = refl
succ-â„¤-is-retraction (pos (succ n)) = refl
succ-â„¤-is-retraction (neg n)        = refl

succ-â„¤-is-section : pred-â„¤ âˆ˜ succ-â„¤ âˆ¼ id
succ-â„¤-is-section ğŸ              = refl
succ-â„¤-is-section (pos n)        = refl
succ-â„¤-is-section (neg zero)     = refl
succ-â„¤-is-section (neg (succ n)) = refl

succ-â„¤-is-equiv : is-equiv succ-â„¤
succ-â„¤-is-equiv = qinvs-are-equivs succ-â„¤
                   (pred-â„¤ , succ-â„¤-is-section , succ-â„¤-is-retraction)

succ-â„¤-â‰ƒ : â„¤ â‰ƒ â„¤
succ-â„¤-â‰ƒ = (succ-â„¤ , succ-â„¤-is-equiv)

pred-â„¤-is-equiv : is-equiv pred-â„¤
pred-â„¤-is-equiv = âŒœâŒ-is-equiv (â‰ƒ-sym succ-â„¤-â‰ƒ)

\end{code}

We will consider iterations of succ-â„¤ and pred-â„¤ in defining addition on â„¤ and
need some lemmas for working with those iterations.

\begin{code}
commute-with-iterated-function : {X : ğ“¤ Ì‡ } (f g : X â†’ X)
                               â†’ f âˆ˜ g âˆ¼ g âˆ˜ f
                               â†’ (n : â„•) â†’ f âˆ˜ (g ^ n) âˆ¼ (g ^ n) âˆ˜ f
commute-with-iterated-function f g h zero     x = refl
commute-with-iterated-function f g h (succ n) x =
 (f âˆ˜ g âˆ˜ (g ^ n)) x ï¼âŸ¨ h ((g ^ n) x) âŸ©
 (g âˆ˜ f âˆ˜ (g ^ n)) x ï¼âŸ¨ ap g (IH x)   âŸ©
 (g âˆ˜ (g ^ n) âˆ˜ f) x âˆ
  where
   IH : f âˆ˜ (g ^ n) âˆ¼ (g ^ n) âˆ˜ f
   IH = commute-with-iterated-function f g h n

commute-with-iterated-functions : {X : ğ“¤ Ì‡ } (f g : X â†’ X)
                                â†’ f âˆ˜ g âˆ¼ g âˆ˜ f
                                â†’ (n m : â„•)
                                â†’ (f ^ n) âˆ˜ (g ^ m) âˆ¼ (g ^ m) âˆ˜ (f ^ n)
commute-with-iterated-functions f g h n m =
 commute-with-iterated-function (f ^ n) g Î³ m
  where
   Î³ : (f ^ n) âˆ˜ g âˆ¼ g âˆ˜ (f ^ n)
   Î³ x = (commute-with-iterated-function g f (Î» y â†’ h y â»Â¹) n x) â»Â¹

iterated-function-is-section : {X : ğ“¤ Ì‡ } (s : X â†’ X) (r : X â†’ X)
                             â†’ r âˆ˜ s âˆ¼ id
                             â†’ (n : â„•) â†’ (r ^ n) âˆ˜ (s ^ n) âˆ¼ id
iterated-function-is-section s r Ï zero     x = refl
iterated-function-is-section s r Ï (succ n) x =
 (r âˆ˜ (r ^ n) âˆ˜ s âˆ˜ (s ^ n)) x ï¼âŸ¨ I   âŸ©
 (r âˆ˜ (r ^ n) âˆ˜ (s ^ n) âˆ˜ s) x ï¼âŸ¨ II  âŸ©
 (r âˆ˜ s) x                     ï¼âŸ¨ Ï x âŸ©
 x                             âˆ
  where
   I  = ap (r ^ (succ n)) (commute-with-iterated-function s s (Î» x â†’ refl) n x)
   II = ap r (iterated-function-is-section s r Ï n (s x))

iterated-function-is-equiv : {X : ğ“¤ Ì‡ } (f : X â†’ X)
                           â†’ is-equiv f
                           â†’ (n : â„•) â†’ is-equiv (f ^ n)
iterated-function-is-equiv f ((s , Îµ) , (r , Î·)) n =
 (((s ^ n) , (iterated-function-is-section s f Îµ n)) ,
  ((r ^ n) , (iterated-function-is-section f r Î· n)))

commute-with-succ-â„¤^n : (f : â„¤ â†’ â„¤)
                      â†’ (f âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ f)
                      â†’ (n : â„•) â†’ f âˆ˜ (succ-â„¤ ^ n) âˆ¼ (succ-â„¤ ^ n) âˆ˜ f
commute-with-succ-â„¤^n f c = commute-with-iterated-function f succ-â„¤ c

commute-with-pred-â„¤ : (f : â„¤ â†’ â„¤)
                    â†’ (f âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ f)
                    â†’ f âˆ˜ pred-â„¤ âˆ¼ pred-â„¤ âˆ˜ f
commute-with-pred-â„¤ f c z = equivs-are-lc succ-â„¤ succ-â„¤-is-equiv Î³
 where
  Î³ : succ-â„¤ (f (pred-â„¤ z)) ï¼ succ-â„¤ (pred-â„¤ (f z))
  Î³ = succ-â„¤ (f (pred-â„¤ z)) ï¼âŸ¨ (c (pred-â„¤ z)) â»Â¹               âŸ©
      f (succ-â„¤ (pred-â„¤ z)) ï¼âŸ¨ ap f (succ-â„¤-is-retraction z)   âŸ©
      f z                   ï¼âŸ¨ (succ-â„¤-is-retraction (f z)) â»Â¹ âŸ©
      succ-â„¤ (pred-â„¤ (f z)) âˆ

succ-â„¤-commutes-with-pred-â„¤ : succ-â„¤ âˆ˜ pred-â„¤ âˆ¼ pred-â„¤ âˆ˜ succ-â„¤
succ-â„¤-commutes-with-pred-â„¤ = commute-with-pred-â„¤ succ-â„¤ (Î» x â†’ refl)

pred-â„¤-commutes-with-succ-â„¤ : pred-â„¤ âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ pred-â„¤
pred-â„¤-commutes-with-succ-â„¤ x = (succ-â„¤-commutes-with-pred-â„¤ x) â»Â¹

commute-with-pred-â„¤^n : (f : â„¤ â†’ â„¤)
                      â†’ (f âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ f)
                      â†’ (n : â„•) â†’ f âˆ˜ (pred-â„¤ ^ n) âˆ¼ (pred-â„¤ ^ n) âˆ˜ f
commute-with-pred-â„¤^n f c =
 commute-with-iterated-function f pred-â„¤ (commute-with-pred-â„¤ f c)

succ-â„¤^n-is-retraction : (n : â„•) â†’ (succ-â„¤ ^ n) âˆ˜ (pred-â„¤ ^ n) âˆ¼ id
succ-â„¤^n-is-retraction =
 iterated-function-is-section pred-â„¤ succ-â„¤ succ-â„¤-is-retraction

succ-â„¤^n-is-section : (n : â„•) â†’ (pred-â„¤ ^ n) âˆ˜ (succ-â„¤ ^ n) âˆ¼ id
succ-â„¤^n-is-section =
 iterated-function-is-section succ-â„¤ pred-â„¤ succ-â„¤-is-section

pos-is-succ-â„¤-iterated : (n : â„•) â†’ pos n ï¼ (succ-â„¤ ^ (succ n)) ğŸ
pos-is-succ-â„¤-iterated zero     = refl
pos-is-succ-â„¤-iterated (succ n) = ap succ-â„¤ (pos-is-succ-â„¤-iterated n)

neg-is-pred-â„¤-iterated : (n : â„•) â†’ neg n ï¼ (pred-â„¤ ^ (succ n)) ğŸ
neg-is-pred-â„¤-iterated zero     = refl
neg-is-pred-â„¤-iterated (succ n) = ap pred-â„¤ (neg-is-pred-â„¤-iterated n)

\end{code}

We are now ready to define addition on â„¤ and prove its basic properties, such as
commutativity.

\begin{code}

_+â„¤_ : â„¤ â†’ â„¤ â†’ â„¤
_+â„¤_ ğŸ       = id
_+â„¤_ (pos n) = (succ-â„¤ ^ (succ n))
_+â„¤_ (neg n) = (pred-â„¤ ^ (succ n))

+â„¤-is-commutative : (x y : â„¤) â†’ x +â„¤ y ï¼ y +â„¤ x
+â„¤-is-commutative ğŸ ğŸ = refl
+â„¤-is-commutative ğŸ (pos m) = pos-is-succ-â„¤-iterated m
+â„¤-is-commutative ğŸ (neg m) = neg-is-pred-â„¤-iterated m
+â„¤-is-commutative (pos n) ğŸ = (pos-is-succ-â„¤-iterated n) â»Â¹
+â„¤-is-commutative (neg n) ğŸ = (neg-is-pred-â„¤-iterated n) â»Â¹
+â„¤-is-commutative (pos n) (pos m) =
 (succ-â„¤ ^ succ n) (pos m)               ï¼âŸ¨ I    âŸ©
 (succ-â„¤ ^ succ n) ((succ-â„¤ ^ succ m) ğŸ) ï¼âŸ¨ II   âŸ©
 (succ-â„¤ ^ succ m) ((succ-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ III  âŸ©
 (succ-â„¤ ^ succ m) (pos n)               ï¼âŸ¨ refl âŸ©
 pos m +â„¤ pos n                          âˆ
  where
   I   = ap (succ-â„¤ ^ (succ n)) (pos-is-succ-â„¤-iterated m)
   II  = commute-with-iterated-functions succ-â„¤ succ-â„¤
          (Î» x â†’ refl) (succ n) (succ m) ğŸ
   III = ap (succ-â„¤ ^ (succ m)) ((pos-is-succ-â„¤-iterated n) â»Â¹)
+â„¤-is-commutative (pos n) (neg m) =
 (succ-â„¤ ^ succ n) (neg m)               ï¼âŸ¨ I   âŸ©
 (succ-â„¤ ^ succ n) ((pred-â„¤ ^ succ m) ğŸ) ï¼âŸ¨ II  âŸ©
 (pred-â„¤ ^ succ m) ((succ-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ III âŸ©
 (pred-â„¤ ^ succ m) (pos n)               ï¼âŸ¨ refl âŸ©
 neg m +â„¤ pos n                          âˆ
  where
   I   = ap (succ-â„¤ ^ succ n) (neg-is-pred-â„¤-iterated m)
   II  = commute-with-iterated-functions succ-â„¤ pred-â„¤
          succ-â„¤-commutes-with-pred-â„¤ (succ n) (succ m) ğŸ
   III = ap (pred-â„¤ ^ succ m) ((pos-is-succ-â„¤-iterated n) â»Â¹)
+â„¤-is-commutative (neg n) (pos m) =
 (pred-â„¤ ^ succ n) (pos m)               ï¼âŸ¨ I    âŸ©
 (pred-â„¤ ^ succ n) ((succ-â„¤ ^ succ m) ğŸ) ï¼âŸ¨ II   âŸ©
 (succ-â„¤ ^ succ m) ((pred-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ III  âŸ©
 (succ-â„¤ ^ succ m) (neg n)               ï¼âŸ¨ refl âŸ©
 pos m +â„¤ neg n                          âˆ
  where
   I   = ap (pred-â„¤ ^ succ n) (pos-is-succ-â„¤-iterated m)
   II  = commute-with-iterated-functions pred-â„¤ succ-â„¤
          pred-â„¤-commutes-with-succ-â„¤ (succ n) (succ m) ğŸ
   III = ap (succ-â„¤ ^ succ m) ((neg-is-pred-â„¤-iterated n) â»Â¹)
+â„¤-is-commutative (neg n) (neg m) =
 (pred-â„¤ ^ succ n) (neg m)               ï¼âŸ¨ I    âŸ©
 (pred-â„¤ ^ succ n) ((pred-â„¤ ^ succ m) ğŸ) ï¼âŸ¨ II   âŸ©
 (pred-â„¤ ^ succ m) ((pred-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ III  âŸ©
 (pred-â„¤ ^ succ m) (neg n)               ï¼âŸ¨ refl âŸ©
 neg m +â„¤ neg n                          âˆ
  where
   I   = ap (pred-â„¤ ^ succ n) (neg-is-pred-â„¤-iterated m)
   II  = commute-with-iterated-functions pred-â„¤ pred-â„¤
          (Î» x â†’ refl) (succ n) (succ m) ğŸ
   III = ap (pred-â„¤ ^ succ m) ((neg-is-pred-â„¤-iterated n) â»Â¹)

\end{code}

Next is negation of integers.

\begin{code}

â”€_ : â„¤ â†’ â„¤
â”€ ğŸ       = ğŸ
â”€ (pos n) = neg n
â”€ (neg n) = pos n

â”€-is-linv : (x : â„¤) â†’ x +â„¤ (â”€ x) ï¼ ğŸ
â”€-is-linv ğŸ = refl
â”€-is-linv (pos n) =
 (succ-â„¤ ^ succ n) (neg n)               ï¼âŸ¨ I  âŸ©
 (succ-â„¤ ^ succ n) ((pred-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ II âŸ©
 ğŸ                                       âˆ
  where
   I  = ap (succ-â„¤ ^ succ n) (neg-is-pred-â„¤-iterated n)
   II = succ-â„¤^n-is-retraction (succ n) ğŸ
â”€-is-linv (neg n) =
 (pred-â„¤ ^ succ n) (pos n)               ï¼âŸ¨ I  âŸ©
 (pred-â„¤ ^ succ n) ((succ-â„¤ ^ succ n) ğŸ) ï¼âŸ¨ II âŸ©
 ğŸ                                       âˆ
  where
   I  = ap (pred-â„¤ ^ succ n) (pos-is-succ-â„¤-iterated n)
   II = succ-â„¤^n-is-section (succ n) ğŸ

â”€-is-involutive : (x : â„¤) â†’ â”€ â”€ x ï¼ x
â”€-is-involutive ğŸ       = refl
â”€-is-involutive (pos n) = refl
â”€-is-involutive (neg n) = refl

â”€-is-rinv : (x : â„¤) â†’ (â”€ x) +â„¤ x ï¼ ğŸ
â”€-is-rinv x = (â”€ x) +â„¤ x ï¼âŸ¨ +â„¤-is-commutative (â”€ x) x âŸ©
              x +â„¤ (â”€ x) ï¼âŸ¨ â”€-is-linv x               âŸ©
              ğŸ          âˆ

\end{code}

Finally, we prove some lemmas on adding/shifting by a fixed integer.

\begin{code}

+â„¤-is-equiv-left : (x : â„¤) â†’ is-equiv (Î» y â†’ x +â„¤ y)
+â„¤-is-equiv-left ğŸ       = id-is-equiv â„¤
+â„¤-is-equiv-left (pos n) = iterated-function-is-equiv succ-â„¤ succ-â„¤-is-equiv (succ n)
+â„¤-is-equiv-left (neg n) = iterated-function-is-equiv pred-â„¤ pred-â„¤-is-equiv (succ n)

+â„¤-is-equiv-right : (y : â„¤) â†’ is-equiv (Î» x â†’ x +â„¤ y)
+â„¤-is-equiv-right y = equiv-closed-under-âˆ¼ (Î» x â†’ y +â„¤ x) (Î» x â†’ x +â„¤ y)
                  (+â„¤-is-equiv-left y) (Î» x â†’ +â„¤-is-commutative x y)

shift-if-commute-with-succ-â„¤ : (f : â„¤ â†’ â„¤)
                             â†’ f âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ f
                             â†’ f âˆ¼ (Î» x â†’ x +â„¤ f ğŸ)
shift-if-commute-with-succ-â„¤ f h ğŸ = refl
shift-if-commute-with-succ-â„¤ f h (pos n) =
 f (pos n)                 ï¼âŸ¨ ap f (pos-is-succ-â„¤-iterated n) âŸ©
 f ((succ-â„¤ ^ succ n) ğŸ)   ï¼âŸ¨ commute-with-iterated-function
                               f succ-â„¤ h (succ n) ğŸ          âŸ©
 (succ-â„¤ ^ (succ n)) (f ğŸ) âˆ
shift-if-commute-with-succ-â„¤ f h (neg n) =
 f (neg n)                 ï¼âŸ¨ ap f (neg-is-pred-â„¤-iterated n)                âŸ©
 f ((pred-â„¤ ^ succ n) ğŸ)   ï¼âŸ¨ commute-with-iterated-function
                               f pred-â„¤ (commute-with-pred-â„¤ f h) (succ n) ğŸ âŸ©
 (pred-â„¤ ^ (succ n)) (f ğŸ) âˆ

left-shift-commutes-with-succ-â„¤ : (x : â„¤)
                                â†’ (Î» y â†’ x +â„¤ y) âˆ˜ succ-â„¤
                                âˆ¼ succ-â„¤ âˆ˜ (Î» y â†’ x +â„¤ y)
left-shift-commutes-with-succ-â„¤ ğŸ y = refl
left-shift-commutes-with-succ-â„¤ (pos n) y =
 (commute-with-succ-â„¤^n succ-â„¤ (Î» _ â†’ refl) (succ n) y) â»Â¹
left-shift-commutes-with-succ-â„¤ (neg n) y =
 (commute-with-pred-â„¤^n succ-â„¤ (Î» _ â†’ refl) (succ n) y) â»Â¹

right-shift-commutes-with-succ-â„¤ : (y : â„¤)
                                 â†’ (Î» x â†’ x +â„¤ y) âˆ˜ succ-â„¤
                                 âˆ¼ succ-â„¤ âˆ˜ (Î» x â†’ x +â„¤ y)
right-shift-commutes-with-succ-â„¤ y x =
 (succ-â„¤ x) +â„¤ y ï¼âŸ¨ +â„¤-is-commutative (succ-â„¤ x) y      âŸ©
 y +â„¤ (succ-â„¤ x) ï¼âŸ¨ left-shift-commutes-with-succ-â„¤ y x âŸ©
 succ-â„¤ (y +â„¤ x) ï¼âŸ¨ ap succ-â„¤ (+â„¤-is-commutative y x)   âŸ©
 succ-â„¤ (x +â„¤ y) âˆ

is-equiv-if-commute-with-succ-â„¤ : (f : â„¤ â†’ â„¤)
                                â†’ f âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ f
                                â†’ is-equiv f
is-equiv-if-commute-with-succ-â„¤ f h =
 equiv-closed-under-âˆ¼ (Î» x â†’ x +â„¤ f ğŸ) f
  (+â„¤-is-equiv-right (f ğŸ)) (shift-if-commute-with-succ-â„¤ f h)

\end{code}
