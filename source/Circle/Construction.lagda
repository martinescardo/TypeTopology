Tom de Jong, 28 January 2021
(Following Bezem, Buchholtz, Grayson and Shulman)

We construct the circle ğ•ŠÂ¹ as the type of â„¤-torsors, following "Construction of
the circle in UniMath" by Bezem, Buchholtz, Grayson and Shulman
(doi:10.1016/j.jpaa.2021.106687). The construction needs univalence of ğ“¤â‚€,
propositional truncations and function extensionality for every two universes.

Rather than proving the induction principle directly as in "Construction of the
circle in UniMath", we prove the induction principle abstractly from the
universal property.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.Base
open import UF.Equiv hiding (_â‰…_)
open import UF.Equiv-FunExt
open import UF.EquivalenceExamples
open import UF.FunExt
open import UF.Lower-FunExt
open import UF.PropTrunc
open import UF.SIP
open import UF.Sets
open import UF.Sets-Properties
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.UA-FunExt
open import UF.Univalence

open import Circle.Integers
open import Circle.Integers-Properties
open import Circle.Integers-SymmetricInduction

module Circle.Construction
        (pt : propositional-truncations-exist)
        (ua : is-univalent ğ“¤â‚€)
       where

feâ‚€ : funext ğ“¤â‚€ ğ“¤â‚€
feâ‚€ = univalence-gives-funext ua

open PropositionalTruncation pt
open sip
open sip-with-axioms

\end{code}

The pointed type of â„¤-torsors base : Tâ„¤. This type is connected (like ğ•ŠÂ¹) almost
by definition.

\begin{code}

Tâ„¤ : ğ“¤â‚ Ì‡
Tâ„¤ = Î£ X ê‰ ğ“¤â‚€ Ì‡ , Î£ f ê‰ (X â†’ X) , âˆ¥ (â„¤ , succ-â„¤) ï¼ (X , f) âˆ¥

base : Tâ„¤
base = (â„¤ , succ-â„¤ , âˆ£ refl âˆ£)

Tâ„¤-is-connected : (X Y : Tâ„¤) â†’ âˆ¥ X ï¼ Y âˆ¥
Tâ„¤-is-connected (X , f , p) (Y , g , q) = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop Ï• p
 where
  Ï• : (â„¤ , succ-â„¤) ï¼ (X , f)
    â†’ âˆ¥ X , f , p ï¼ Y , g , q âˆ¥
  Ï• refl = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop Ïˆ q
   where
    Ïˆ : (â„¤ , succ-â„¤) ï¼ (Y , g)
      â†’ âˆ¥ â„¤ , succ-â„¤ , p ï¼ Y , g , q âˆ¥
    Ïˆ refl = âˆ£ ap âŒœ Î£-assoc âŒ (to-subtype-ï¼ (Î» _ â†’ âˆ¥âˆ¥-is-prop) refl) âˆ£

\end{code}

Next, we wish to define loop : base ï¼ base. To this end, we first characterize
equality of â„¤-torsors, for which we use the Structure Identity Principle.

\begin{code}

_â‰…_ : Tâ„¤ â†’ Tâ„¤ â†’ ğ“¤â‚€ Ì‡
(X , f , _) â‰… (Y , g , _) = Î£ e ê‰ (X â†’ Y) , is-equiv e
                                          Ã— (e âˆ˜ f ï¼ g âˆ˜ e)

Tâ„¤-structure : ğ“¤â‚€ Ì‡ â†’ ğ“¤â‚€ Ì‡
Tâ„¤-structure X = X â†’ X

Tâ„¤â» : ğ“¤â‚ Ì‡
Tâ„¤â» = Î£ X ê‰ ğ“¤â‚€ Ì‡ , Tâ„¤-structure X

sns-data : SNS Tâ„¤-structure ğ“¤â‚€
sns-data = (Î¹ , Ï , Î¸)
 where
  Î¹ : (X Y : Tâ„¤â») â†’ âŸ¨ X âŸ© â‰ƒ âŸ¨ Y âŸ© â†’ ğ“¤â‚€ Ì‡
  Î¹ (X , f) (Y , g) (e , _) = e âˆ˜ f ï¼ g âˆ˜ e
  Ï : (X : Tâ„¤â») â†’ Î¹ X X (â‰ƒ-refl âŸ¨ X âŸ©)
  Ï (X , f) = refl
  h : {X : ğ“¤â‚€ Ì‡ } {f g : Tâ„¤-structure X}
    â†’ canonical-map Î¹ Ï f g âˆ¼ id {ğ“¤â‚€} {f ï¼ g}
  h refl = refl
  Î¸ : {X : ğ“¤â‚€ Ì‡ } (f g : Tâ„¤-structure X)
    â†’ is-equiv (canonical-map Î¹ Ï f g)
  Î¸ f g = equiv-closed-under-âˆ¼ _ _ (id-is-equiv (f ï¼ g)) h

characterization-of-Tâ„¤-ï¼ : (X Y : Tâ„¤)
                         â†’ (X ï¼ Y) â‰ƒ (X â‰… Y)
characterization-of-Tâ„¤-ï¼ =
 characterization-of-ï¼-with-axioms ua
  sns-data
  (Î» X f â†’ âˆ¥ (â„¤ , succ-â„¤) ï¼ (X , f) âˆ¥)
  (Î» X f â†’ âˆ¥âˆ¥-is-prop)

to-Tâ„¤-ï¼ : (X Y : Tâ„¤) â†’ X â‰… Y â†’ X ï¼ Y
to-Tâ„¤-ï¼ X Y = âŒœ characterization-of-Tâ„¤-ï¼ X Y âŒâ»Â¹

loop-â‰… : base â‰… base
loop-â‰… = (succ-â„¤ , succ-â„¤-is-equiv , refl)

loop : base ï¼ base
loop = to-Tâ„¤-ï¼ base base loop-â‰…

\end{code}

Another nice consequence of having characterized the equality of â„¤-torsors (and
the symmetric induction principle of â„¤) is that we can quickly prove that
(base ï¼ base) â‰ƒ â„¤.

\begin{code}

loops-at-base-equivalent-to-â„¤ : (base ï¼ base) â‰ƒ â„¤
loops-at-base-equivalent-to-â„¤ =
 (base ï¼ base)                                            â‰ƒâŸ¨ I   âŸ©
 (Î£ e ê‰ (â„¤ â†’ â„¤) , is-equiv e Ã— (e âˆ˜ succ-â„¤ ï¼ succ-â„¤ âˆ˜ e)) â‰ƒâŸ¨ II  âŸ©
 (Î£ e ê‰ (â„¤ â†’ â„¤) , is-equiv e Ã— (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e)) â‰ƒâŸ¨ III âŸ©
 (Î£ e ê‰ (â„¤ â†’ â„¤) , (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e) Ã— is-equiv e) â‰ƒâŸ¨ IV  âŸ©
 (Î£ e ê‰ (â„¤ â†’ â„¤) , (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e))              â‰ƒâŸ¨ V   âŸ©
 â„¤                                                        â– 
  where
   I   = characterization-of-Tâ„¤-ï¼ base base
   II  = Î£-cong (Î» e â†’ Ã—-cong (â‰ƒ-refl (is-equiv e))
                              (â‰ƒ-funext feâ‚€ (e âˆ˜ succ-â„¤) (succ-â„¤ âˆ˜ e)))
   III = Î£-cong (Î» e â†’ Ã—-comm)
   IV  = Î£-cong Î³
    where
     Î³ : (e : â„¤ â†’ â„¤)
       â†’ (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e) Ã— is-equiv e
       â‰ƒ (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e)
     Î³ e = qinveq prâ‚ (Ï• , Î· , Îµ)
      where
       Ï• : e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e
         â†’ (e âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ e) Ã— is-equiv e
       Ï• c = (c , is-equiv-if-commute-with-succ-â„¤ e c)
       Î· : Ï• âˆ˜ prâ‚ âˆ¼ id
       Î· (i , c) = to-subtype-ï¼
                    (Î» _ â†’ being-equiv-is-prop' feâ‚€ feâ‚€ feâ‚€ feâ‚€ e) refl
       Îµ : prâ‚ âˆ˜ Ï• âˆ¼ id
       Îµ _ = refl
   V   = â„¤-symmetric-induction feâ‚€ (Î» _ â†’ â„¤) (Î» _ â†’ succ-â„¤-â‰ƒ)

âŸ¨_âŸ©â‚‚ : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â†’ âŸ¨ X âŸ©
âŸ¨ (X , f , t) âŸ©â‚‚ = f

_â» : Tâ„¤ â†’ Tâ„¤â»
X â» = âŸ¨ X âŸ© , âŸ¨ X âŸ©â‚‚

Tâ„¤-ï¼-from-Tâ„¤â»-ï¼ : {X Y : Tâ„¤} â†’ X â» ï¼ Y â» â†’ X ï¼ Y
Tâ„¤-ï¼-from-Tâ„¤â»-ï¼ q = ap âŒœ Î£-assoc âŒ (to-subtype-ï¼ (Î» _ â†’ âˆ¥âˆ¥-is-prop) q)

\end{code}

The connectedness of Tâ„¤ gets us the following propositional induction principle,
which allows us to prove some further properties of â„¤-torsors. What's remarkable
(and in my opinion this is the crux of the paper by Bezem et al.) is that this
principle can be used to get the full recursion principle for Tâ„¤.

\begin{code}

Tâ„¤-prop-induction : {ğ“¤ : Universe} {P : Tâ„¤ â†’ ğ“¤ Ì‡ }
                  â†’ ((X : Tâ„¤) â†’ is-prop (P X))
                  â†’ P base
                  â†’ (X : Tâ„¤) â†’ P X
Tâ„¤-prop-induction {ğ“¤} {P} i p (X , f , t) = âˆ¥âˆ¥-rec (i (X , f , t)) Î³ t
 where
  Î³ : (â„¤ , succ-â„¤) ï¼ (X , f) â†’ P (X , f , t)
  Î³ q = transport P (Tâ„¤-ï¼-from-Tâ„¤â»-ï¼ q) p

âŸ¨âŸ©-is-set : (X : Tâ„¤) â†’ is-set âŸ¨ X âŸ©
âŸ¨âŸ©-is-set = Tâ„¤-prop-induction (Î» _ â†’ being-set-is-prop feâ‚€) â„¤-is-set

âŸ¨âŸ©â‚‚-is-equiv : (X : Tâ„¤) â†’ is-equiv âŸ¨ X âŸ©â‚‚
âŸ¨âŸ©â‚‚-is-equiv = Tâ„¤-prop-induction
                (Î» X â†’ being-equiv-is-prop' feâ‚€ feâ‚€ feâ‚€ feâ‚€ âŸ¨ X âŸ©â‚‚)
                succ-â„¤-is-equiv

âŸ¨_âŸ©â‚‚-â‰ƒ : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â‰ƒ âŸ¨ X âŸ©
âŸ¨_âŸ©â‚‚-â‰ƒ X = (âŸ¨ X âŸ©â‚‚ , âŸ¨âŸ©â‚‚-is-equiv X)

âŸ¨_âŸ©â‚‚â»Â¹ : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â†’ âŸ¨ X âŸ©
âŸ¨_âŸ©â‚‚â»Â¹ X = âŒœ âŸ¨ X âŸ©â‚‚-â‰ƒ âŒâ»Â¹

\end{code}

Next we derive the recursion principle following Bezem et al.

\begin{code}

module Tâ„¤-rec
        {A : ğ“¤ Ì‡ }
        (fe : funext ğ“¤ ğ“¤)
       where

 module _
         ((a , p) : Î£ a' ê‰ A , a' ï¼ a')
        where

  -- Bezem, Buchholtz, Grayson
  BBG : (X : Tâ„¤â») â†’ ğ“¤ Ì‡
  BBG (X , f) = Î£ a' ê‰ A , Î£ h ê‰ (X â†’ a ï¼ a') , ((x : X) â†’ h (f x) ï¼ p âˆ™ h x)

  BBG-base : ğ“¤ Ì‡
  BBG-base = BBG (â„¤ , succ-â„¤)

  BBG-base-is-singleton : is-singleton BBG-base
  BBG-base-is-singleton = equiv-to-singleton Ï• (singleton-types-are-singletons a)
   where
    Ï• : BBG-base â‰ƒ singleton-type a
    Ï• = Î£-cong Ïˆ
     where
      Ïˆ : (a' : A)
        â†’ (Î£ h ê‰ (â„¤ â†’ a ï¼ a') , ((z : â„¤) â†’ h (succ-â„¤ z) ï¼ p âˆ™ h z))
        â‰ƒ (a ï¼ a')
      Ïˆ a' = â„¤-symmetric-induction (lower-funext ğ“¤ ğ“¤ fe) (Î» _ â†’ a ï¼ a') (Î» _ â†’ g)
       where
        g : (a ï¼ a') â‰ƒ (a ï¼ a')
        g = ((p âˆ™_) , âˆ™-is-equiv-left p)

  abstract
   BBG-is-singleton : ((X , f , _) : Tâ„¤) â†’ is-singleton (BBG (X , f))
   BBG-is-singleton = Tâ„¤-prop-induction (Î» _ â†’ being-singleton-is-prop fe)
                                        BBG-base-is-singleton

  Tâ„¤-rec : Tâ„¤ â†’ A
  Tâ„¤-rec X = prâ‚ (center (BBG-is-singleton X))

\end{code}

The corresponding computation rule is a bit more work.

\begin{code}

  Tâ„¤-rec-lemmaâ‚ : (X : Tâ„¤) â†’ (âŸ¨ X âŸ©) â†’ a ï¼ Tâ„¤-rec X
  Tâ„¤-rec-lemmaâ‚ X = prâ‚ (prâ‚‚ (center (BBG-is-singleton X)))

  Tâ„¤-rec-lemmaâ‚‚ : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                â†’ Tâ„¤-rec-lemmaâ‚ X (âŸ¨ X âŸ©â‚‚ x) ï¼ p âˆ™ Tâ„¤-rec-lemmaâ‚ X x
  Tâ„¤-rec-lemmaâ‚‚ X = prâ‚‚ (prâ‚‚ (center (BBG-is-singleton X)))

  ap-Tâ„¤-rec-lemma : {X Y : Tâ„¤} (e : X ï¼ Y) (x : âŸ¨ X âŸ©)
                  â†’ ap Tâ„¤-rec e
                  ï¼ (Tâ„¤-rec-lemmaâ‚ X x) â»Â¹
                    âˆ™ (Tâ„¤-rec-lemmaâ‚ Y (âŒœ idtoeq âŸ¨ X âŸ© âŸ¨ Y âŸ© (ap âŸ¨_âŸ© e) âŒ x))
  ap-Tâ„¤-rec-lemma {X} {Y} refl x =
   ap Tâ„¤-rec refl                                     ï¼âŸ¨ refl âŸ©
   refl                                               ï¼âŸ¨ Î³    âŸ©
   (t X x) â»Â¹ âˆ™ (t X x)                               ï¼âŸ¨ refl âŸ©
   (t X x) â»Â¹ âˆ™ (t X (âŒœ idtoeq âŸ¨ X âŸ© âŸ¨ Y âŸ© refl âŒ x)) âˆ
    where
     t : (W : Tâ„¤) â†’ âŸ¨ W âŸ© â†’ a ï¼ Tâ„¤-rec W
     t = Tâ„¤-rec-lemmaâ‚
     Î³ = (left-inverse (t X x)) â»Â¹

  ap-Tâ„¤-rec-loop-lemmaâ‚ : ap Tâ„¤-rec loop
                        ï¼ (Tâ„¤-rec-lemmaâ‚ base ğŸ) â»Â¹ âˆ™ p âˆ™ Tâ„¤-rec-lemmaâ‚ base ğŸ
  ap-Tâ„¤-rec-loop-lemmaâ‚ =
   ap Tâ„¤-rec loop                                            ï¼âŸ¨ I   âŸ©
   (t base ğŸ) â»Â¹ âˆ™ (t base (âŒœ idtoeq â„¤ â„¤ (ap âŸ¨_âŸ© loop) âŒ ğŸ)) ï¼âŸ¨ II  âŸ©
   (t base ğŸ) â»Â¹ âˆ™ (t base (succ-â„¤ ğŸ))                       ï¼âŸ¨ III âŸ©
   (t base ğŸ) â»Â¹ âˆ™ (p âˆ™ t base ğŸ)                            ï¼âŸ¨ IV  âŸ©
   (t base ğŸ) â»Â¹ âˆ™ p âˆ™ t base ğŸ                              âˆ
    where
     t : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â†’ a ï¼ Tâ„¤-rec X
     t = Tâ„¤-rec-lemmaâ‚
     I   = ap-Tâ„¤-rec-lemma loop ğŸ
     III = ap (Î» - â†’ (t base ğŸ) â»Â¹ âˆ™ -) (Tâ„¤-rec-lemmaâ‚‚ base ğŸ)
     IV  = âˆ™assoc (t base ğŸ â»Â¹) p (t base ğŸ) â»Â¹
     II  = ap (Î» - â†’ (t base ğŸ) â»Â¹ âˆ™ (t base (âŒœ - âŒ ğŸ))) Î³
      where
       Î³ : idtoeq â„¤ â„¤ (ap âŸ¨_âŸ© loop) ï¼ succ-â„¤-â‰ƒ
       Î³ =  idtoeq â„¤ â„¤ (ap âŸ¨_âŸ© loop)                        ï¼âŸ¨ I'   âŸ©
            (prâ‚ (Ï• loop)       , prâ‚ (prâ‚‚ (Ï• loop)))       ï¼âŸ¨ refl âŸ©
            (prâ‚ (Ï• (Ïˆ loop-â‰…)) , prâ‚ (prâ‚‚ (Ï• (Ïˆ loop-â‰…)))) ï¼âŸ¨ II'  âŸ©
            (prâ‚ loop-â‰…         , prâ‚ (prâ‚‚ loop-â‰…))         âˆ
             where
              Ï• : base ï¼ base â†’ base â‰… base
              Ï• = âŒœ characterization-of-Tâ„¤-ï¼ base base âŒ
              Ïˆ : base â‰… base â†’ base ï¼ base
              Ïˆ = âŒœ characterization-of-Tâ„¤-ï¼ base base âŒâ»Â¹
              I' = h loop
               where
                h : {X Y : Tâ„¤} (p : X ï¼ Y)
                     â†’ idtoeq âŸ¨ X âŸ© âŸ¨ Y âŸ© (ap âŸ¨_âŸ© p)
                     ï¼ (prâ‚ ( âŒœ characterization-of-Tâ„¤-ï¼ X Y âŒ p) ,
                        prâ‚ (prâ‚‚ (âŒœ characterization-of-Tâ„¤-ï¼ X Y âŒ p)))
                h refl = refl
              II' = ap (Î» - â†’ (prâ‚ - , prâ‚ (prâ‚‚ -))) (Îµ loop-â‰…)
               where
                Îµ : Ï• âˆ˜ Ïˆ âˆ¼ id
                Îµ = inverses-are-sections Ï•
                     (âŒœâŒ-is-equiv (characterization-of-Tâ„¤-ï¼ base base))

  ap-Tâ„¤-rec-loop-lemmaâ‚‚ : ap Tâ„¤-rec loop
                        ï¼ transport (Î» - â†’ - ï¼ -) (Tâ„¤-rec-lemmaâ‚ base ğŸ) p
  ap-Tâ„¤-rec-loop-lemmaâ‚‚ =
   ap Tâ„¤-rec loop                                       ï¼âŸ¨ I  âŸ©
   (Tâ„¤-rec-lemmaâ‚ base ğŸ) â»Â¹ âˆ™ p âˆ™ Tâ„¤-rec-lemmaâ‚ base ğŸ ï¼âŸ¨ II âŸ©
   transport (Î» - â†’ - ï¼ -) (Tâ„¤-rec-lemmaâ‚ base ğŸ) p     âˆ
    where
     I  = ap-Tâ„¤-rec-loop-lemmaâ‚
     II = (transport-along-ï¼ (Tâ„¤-rec-lemmaâ‚ base ğŸ) p) â»Â¹

  Tâ„¤-rec-comp : (Tâ„¤-rec base , ap Tâ„¤-rec loop) ï¼ (a , p)
  Tâ„¤-rec-comp = (to-Î£-ï¼ ((Tâ„¤-rec-lemmaâ‚ base ğŸ) , (ap-Tâ„¤-rec-loop-lemmaâ‚‚ â»Â¹))) â»Â¹

\end{code}

Now we will deviate from Bezem et al. a bit by deriving the universal property
rather than the induction principle. The proof of the universal property uses
lemmas and techniques from Section 4.2 of the paper by Bezem et al.

Above we constructed a map of type Tâ„¤ â†’ A, namely Tâ„¤-rec using the BBG-type. In
what follows we take the reverse route: we start with a map f : Tâ„¤ â†’ A and then
construct something in the BBG-type so that f and Tâ„¤-rec coincide.

First some general lemmas.

\begin{code}

â‰…-comp-Tâ„¤ : (X Y Z : Tâ„¤) â†’ X â‰… Y â†’ Y â‰… Z â†’ X â‰… Z
â‰…-comp-Tâ„¤ X Y Z (e , i , c) (e' , i' , c') =
 (e' âˆ˜ e , âˆ˜-is-equiv-abstract i i' , Ïˆ)
  where
   abstract
    Ïˆ : e' âˆ˜ e âˆ˜ âŸ¨ X âŸ©â‚‚ ï¼ âŸ¨ Z âŸ©â‚‚ âˆ˜ e' âˆ˜ e
    Ïˆ = dfunext feâ‚€ Î³
     where
      Î³ : e' âˆ˜ e âˆ˜ âŸ¨ X âŸ©â‚‚ âˆ¼ âŸ¨ Z âŸ©â‚‚ âˆ˜ e' âˆ˜ e
      Î³ x = e' (e (âŸ¨ X âŸ©â‚‚ x)) ï¼âŸ¨ ap e' (happly c x) âŸ©
            e' (âŸ¨ Y âŸ©â‚‚ (e x)) ï¼âŸ¨ happly c' (e x) âŸ©
            âŸ¨ Z âŸ©â‚‚ (e' (e x)) âˆ

to-ï¼-of-â‰… : (X Y : Tâ„¤) {f g : X â‰… Y}
          â†’ prâ‚ f âˆ¼ prâ‚ g
          â†’ f ï¼ g
to-ï¼-of-â‰… X Y h =
 to-subtype-ï¼
  (Î» f' â†’ Ã—-is-prop (being-equiv-is-prop' feâ‚€ feâ‚€ feâ‚€ feâ‚€ f')
         (equiv-to-prop (â‰ƒ-funext feâ‚€ _ _)
          (Î -is-prop feâ‚€ (Î» _ â†’ âŸ¨âŸ©-is-set Y))))
  (dfunext feâ‚€ h)

to-Tâ„¤-ï¼-comp : (X Y Z : Tâ„¤) (f : X â‰… Y) (g : Y â‰… Z)
             â†’ to-Tâ„¤-ï¼ X Z (â‰…-comp-Tâ„¤ X Y Z f g)
             ï¼ to-Tâ„¤-ï¼ X Y f âˆ™ to-Tâ„¤-ï¼ Y Z g
to-Tâ„¤-ï¼-comp X Y Z f g =
 Ï• X Z (â‰…-comp-Tâ„¤ X Y Z f g)                 ï¼âŸ¨ I    âŸ©
 Ï• X Z (Ïˆ X Z (p âˆ™ q))                       ï¼âŸ¨ II   âŸ©
 p âˆ™ q                                       ï¼âŸ¨ refl âŸ©
 Ï• X Y f âˆ™ Ï• Y Z g                           âˆ
  where
   Ï• : (X Y : Tâ„¤) â†’ X â‰… Y â†’ X ï¼ Y
   Ï• = to-Tâ„¤-ï¼
   Ïˆ : (X Y : Tâ„¤) â†’ X ï¼ Y â†’ X â‰… Y
   Ïˆ X Y = âŒœ characterization-of-Tâ„¤-ï¼ X Y âŒ
   p : X ï¼ Y
   p = Ï• X Y f
   q : Y ï¼ Z
   q = Ï• Y Z g
   II = Î· X Z (p âˆ™ q)
    where
     Î· : (X Y : Tâ„¤) â†’ Ï• X Y âˆ˜ Ïˆ X Y âˆ¼ id
     Î· X Y = inverses-are-retractions (Ïˆ X Y)
              (âŒœâŒ-is-equiv (characterization-of-Tâ„¤-ï¼ X Y))
   I = ap (Ï• X Z) Î³

\end{code}

    The proof below is done with to-ï¼-of-â‰… (rather than directly) for
    type-checking efficiency reasons.

\begin{code}

    where
     Î³ = â‰…-comp-Tâ„¤ X Y Z f g                 ï¼âŸ¨ to-ï¼-of-â‰… X Z w      âŸ©
         â‰…-comp-Tâ„¤ X Y Z (Ïˆ X Y p) (Ïˆ Y Z q) ï¼âŸ¨ (lemma X Y Z p q) â»Â¹ âŸ©
         Ïˆ X Z (p âˆ™ q)                       âˆ
      where
       lemma : (X Y Z : Tâ„¤) (p : X ï¼ Y) (q : Y ï¼ Z)
             â†’ Ïˆ X Z (p âˆ™ q) ï¼ â‰…-comp-Tâ„¤ X Y Z (Ïˆ X Y p) (Ïˆ Y Z q)
       lemma X Y Z refl refl = to-ï¼-of-â‰… X Z (Î» x â†’ refl)
       w : prâ‚ g âˆ˜ prâ‚ f âˆ¼ prâ‚ (Ïˆ Y Z (to-Tâ„¤-ï¼ Y Z g)) âˆ˜ prâ‚ (Ïˆ X Y p)
       w x = v (prâ‚ f x) âˆ™ ap (prâ‚ (Ïˆ Y Z q)) (u x)
        where
         Îµ : (X Y : Tâ„¤) â†’ Ïˆ X Y âˆ˜ Ï• X Y âˆ¼ id
         Îµ X Y = inverses-are-sections (Ïˆ X Y)
                  (âŒœâŒ-is-equiv (characterization-of-Tâ„¤-ï¼ X Y))
         u : prâ‚ f âˆ¼ prâ‚ (Ïˆ X Y p)
         u = happly (ap prâ‚ ((Îµ X Y f) â»Â¹))
         v : prâ‚ g âˆ¼ prâ‚ (Ïˆ Y Z q)
         v = happly (ap prâ‚ ((Îµ Y Z g) â»Â¹))

\end{code}

Now some constructions for the BBG-type. The map Tâ„¤-action appears in Lemma 4.6
of Bezem et al.

\begin{code}

Tâ„¤-action : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â†’ â„¤ â†’ âŸ¨ X âŸ©
Tâ„¤-action X x ğŸ       = x
Tâ„¤-action X x (pos n) = (âŸ¨ X âŸ©â‚‚   ^ (succ n)) x
Tâ„¤-action X x (neg n) = (âŸ¨ X âŸ©â‚‚â»Â¹ ^ (succ n)) x

Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚ : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                            â†’ Tâ„¤-action X (âŸ¨ X âŸ©â‚‚ x)
                            âˆ¼ âŸ¨ X âŸ©â‚‚ âˆ˜ Tâ„¤-action X x
Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚ X x ğŸ       = refl
Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚ X x (pos n) =
 ap âŸ¨ X âŸ©â‚‚ ((commute-with-iterated-function âŸ¨ X âŸ©â‚‚ âŸ¨ X âŸ©â‚‚ (Î» _ â†’ refl) n x) â»Â¹)
Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚ X x (neg n) = Î³
 where
  Î³ : (âŸ¨ X âŸ©â‚‚â»Â¹ ^ (succ n)) (âŸ¨ X âŸ©â‚‚ x) ï¼ âŸ¨ X âŸ©â‚‚ ((âŸ¨ X âŸ©â‚‚â»Â¹ ^ (succ n)) x)
  Î³ = (commute-with-iterated-function âŸ¨ X âŸ©â‚‚ âŸ¨ X âŸ©â‚‚â»Â¹ Ï• (succ n) x) â»Â¹
   where
    Ï• : âŸ¨ X âŸ©â‚‚ âˆ˜ âŸ¨ X âŸ©â‚‚â»Â¹ âˆ¼ âŸ¨ X âŸ©â‚‚â»Â¹ âˆ˜ âŸ¨ X âŸ©â‚‚
    Ï• y = âŸ¨ X âŸ©â‚‚ (âŸ¨ X âŸ©â‚‚â»Â¹ y) ï¼âŸ¨ I  âŸ©
          y                   ï¼âŸ¨ II âŸ©
          âŸ¨ X âŸ©â‚‚â»Â¹ (âŸ¨ X âŸ©â‚‚ y) âˆ
     where
      I  = inverses-are-sections âŸ¨ X âŸ©â‚‚ (âŸ¨âŸ©â‚‚-is-equiv X) y
      II = (inverses-are-retractions âŸ¨ X âŸ©â‚‚ (âŸ¨âŸ©â‚‚-is-equiv X) y) â»Â¹

Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚-ï¼ : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                              â†’ Tâ„¤-action X (âŸ¨ X âŸ©â‚‚ x) ï¼ âŸ¨ X âŸ©â‚‚ âˆ˜ Tâ„¤-action X x
Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚-ï¼ X x = dfunext feâ‚€ (Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚ X x)

Tâ„¤-action-base-is-shift : (x : â„¤) â†’ Tâ„¤-action base x âˆ¼ (Î» y â†’ y +â„¤ x)
Tâ„¤-action-base-is-shift x ğŸ       = refl
Tâ„¤-action-base-is-shift x (pos n) = refl
Tâ„¤-action-base-is-shift x (neg n) = happly (ap (_^ succ n) (ap âŒœ_âŒâ»Â¹ Ï•)) x
 where
  Ï• : âŸ¨ base âŸ©â‚‚-â‰ƒ ï¼ succ-â„¤-â‰ƒ
  Ï• = to-subtype-ï¼ (being-equiv-is-prop' feâ‚€ feâ‚€ feâ‚€ feâ‚€) refl

Tâ„¤-action-is-equiv : (X : Tâ„¤) (x : âŸ¨ X âŸ©) â†’ is-equiv (Tâ„¤-action X x)
Tâ„¤-action-is-equiv =
 Tâ„¤-prop-induction (Î» X â†’ Î -is-prop feâ‚€
                   (Î» x â†’ being-equiv-is-prop' feâ‚€ feâ‚€ feâ‚€ feâ‚€ (Tâ„¤-action X x)))
                   Î³
  where
   Î³ : (x : â„¤) â†’ is-equiv (Tâ„¤-action base x)
   Î³ x = equiv-closed-under-âˆ¼ (Î» y â†’ y +â„¤ x) (Tâ„¤-action base x)
          (+â„¤-is-equiv-right x) (Tâ„¤-action-base-is-shift x)

Tâ„¤-action-is-Tâ„¤-map : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                    â†’ (Tâ„¤-action X x âˆ˜ succ-â„¤ ï¼ âŸ¨ X âŸ©â‚‚ âˆ˜ Tâ„¤-action X x)
Tâ„¤-action-is-Tâ„¤-map = Tâ„¤-prop-induction i Î³
 where
  i : (X : Tâ„¤)
    â†’ is-prop ((x : âŸ¨ X âŸ©) â†’ (Tâ„¤-action X x âˆ˜ succ-â„¤ ï¼ âŸ¨ X âŸ©â‚‚ âˆ˜ Tâ„¤-action X x))
  i X = Î -is-prop feâ‚€
         (Î» x â†’ equiv-to-prop
                 (â‰ƒ-funext feâ‚€ (Tâ„¤-action X x âˆ˜ succ-â„¤) (âŸ¨ X âŸ©â‚‚ âˆ˜ Tâ„¤-action X x))
                 (Î -is-prop feâ‚€ (Î» _ â†’ âŸ¨âŸ©-is-set X)))
  Î³ : (x : â„¤)
    â†’  Tâ„¤-action base x âˆ˜ succ-â„¤ ï¼ succ-â„¤ âˆ˜ Tâ„¤-action base x
  Î³ x = dfunext feâ‚€ h
   where
    h : Tâ„¤-action base x âˆ˜ succ-â„¤ âˆ¼ succ-â„¤ âˆ˜ Tâ„¤-action base x
    h y = Tâ„¤-action base x (succ-â„¤ y) ï¼âŸ¨ I   âŸ©
          (succ-â„¤ y) +â„¤ x             ï¼âŸ¨ II  âŸ©
          succ-â„¤ (y +â„¤ x)             ï¼âŸ¨ III âŸ©
          succ-â„¤ (Tâ„¤-action base x y) âˆ
     where
      I   = Tâ„¤-action-base-is-shift x (succ-â„¤ y)
      II  = right-shift-commutes-with-succ-â„¤ x y
      III = ap succ-â„¤ ((Tâ„¤-action-base-is-shift x y) â»Â¹)

Tâ„¤-action-â‰… : (X : Tâ„¤) (x : âŸ¨ X âŸ©) â†’ base â‰… X
Tâ„¤-action-â‰… X x =
 (Tâ„¤-action X x , Tâ„¤-action-is-equiv X x , Tâ„¤-action-is-Tâ„¤-map X x)

Tâ„¤-action-ï¼ : (X : Tâ„¤) (x : âŸ¨ X âŸ©) â†’ base ï¼ X
Tâ„¤-action-ï¼ X x = to-Tâ„¤-ï¼ base X (Tâ„¤-action-â‰… X x)

Tâ„¤-action-lemma : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                â†’ Tâ„¤-action X (âŸ¨ X âŸ©â‚‚ x)
                ï¼ Tâ„¤-action X x âˆ˜ succ-â„¤
Tâ„¤-action-lemma X x = Tâ„¤-action-commutes-with-âŸ¨âŸ©â‚‚-ï¼ X x
                    âˆ™ (Tâ„¤-action-is-Tâ„¤-map X x) â»Â¹

Tâ„¤-action-ï¼-lemma : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
                  â†’ Tâ„¤-action-ï¼ X (âŸ¨ X âŸ©â‚‚ x) ï¼ loop âˆ™ Tâ„¤-action-ï¼ X x
Tâ„¤-action-ï¼-lemma X x =
 Tâ„¤-action-ï¼ X (âŸ¨ X âŸ©â‚‚ x)                                        ï¼âŸ¨ refl âŸ©
 to-Tâ„¤-ï¼ base X (Tâ„¤-action-â‰… X (âŸ¨ X âŸ©â‚‚ x))                       ï¼âŸ¨ I    âŸ©
 to-Tâ„¤-ï¼ base X (â‰…-comp-Tâ„¤ base base X loop-â‰… (Tâ„¤-action-â‰… X x)) ï¼âŸ¨ II   âŸ©
 to-Tâ„¤-ï¼ base base loop-â‰… âˆ™ to-Tâ„¤-ï¼ base X (Tâ„¤-action-â‰… X x)     ï¼âŸ¨ refl âŸ©
 loop âˆ™ Tâ„¤-action-ï¼ X x                                          âˆ
  where
   I  = ap (to-Tâ„¤-ï¼ base X) Ï•
    where
     Ï• = to-ï¼-of-â‰… base X (happly (Tâ„¤-action-lemma X x))
   II = to-Tâ„¤-ï¼-comp base base X loop-â‰… (Tâ„¤-action-â‰… X x)

\end{code}

Finally, as promised, every map r : Tâ„¤ â†’ A gives rise to an element of the
BBG-type so that r and Tâ„¤-rec coincide.

\begin{code}

module _
        {A : ğ“¤ Ì‡ }
        (r : Tâ„¤ â†’ A)
       where

 BBG-map : (X : Tâ„¤) â†’ âŸ¨ X âŸ© â†’ r base ï¼ r X
 BBG-map X x = ap r (Tâ„¤-action-ï¼ X x)

 BBG-map-lemma : (X : Tâ„¤) (x : âŸ¨ X âŸ©)
               â†’ BBG-map X (âŸ¨ X âŸ©â‚‚ x) ï¼ ap r loop âˆ™ BBG-map X x
 BBG-map-lemma X x = BBG-map X (âŸ¨ X âŸ©â‚‚ x)                      ï¼âŸ¨ refl âŸ©
                     ap r (Tâ„¤-action-ï¼ X (âŸ¨ X âŸ©â‚‚ x))           ï¼âŸ¨ I    âŸ©
                     ap r (loop âˆ™ Tâ„¤-action-ï¼ X x)             ï¼âŸ¨ II   âŸ©
                     ap r loop âˆ™ ap r (Tâ„¤-action-ï¼ X x)        ï¼âŸ¨ refl âŸ©
                     ap r loop âˆ™ BBG-map X x                   âˆ
  where
   I  = ap (ap r) (Tâ„¤-action-ï¼-lemma X x)
   II = ap-âˆ™ r loop (Tâ„¤-action-ï¼ X x)

 module _
         (fe : funext ğ“¤ ğ“¤)
        where

  open Tâ„¤-rec {ğ“¤} {A} fe

  âˆ¼-to-Tâ„¤-rec : r âˆ¼ Tâ„¤-rec (r base , ap r loop)
  âˆ¼-to-Tâ„¤-rec X = ap prâ‚ e
   where
    bâ‚ : BBG (r base , ap r loop) (X â»)
    bâ‚ = (r X , BBG-map X , BBG-map-lemma X)
    bâ‚‚ : BBG (r base , ap r loop) (X â»)
    bâ‚‚ = center (BBG-is-singleton (r base , ap r loop) X)
    e : bâ‚ ï¼ bâ‚‚
    e = singletons-are-props (BBG-is-singleton (r base , ap r loop) X) bâ‚ bâ‚‚

\end{code}

But the above gives us the uniqueness principle for ğ•ŠÂ¹ (Lemma 6.2.8 in the HoTT
Book) which says that any two maps f,g : ğ•ŠÂ¹ â†’ A that agree on base and loop must
coincide. Combined with the recursion principle, this quickly gives us the
universal property.

\begin{code}

Tâ„¤-universal-map : (A : ğ“¤ Ì‡ ) â†’ (Tâ„¤ â†’ A) â†’ Î£ a ê‰ A , a ï¼ a
Tâ„¤-universal-map A f = (f base , ap f loop)

Tâ„¤-universal-property : FunExt
                      â†’ (A : ğ“¤ Ì‡ )
                      â†’ is-equiv (Tâ„¤-universal-map A)
Tâ„¤-universal-property {ğ“¤} fe A = qinvs-are-equivs Ï• (Ïˆ , Î· , Îµ)
 where
  open Tâ„¤-rec {ğ“¤} {A} (fe ğ“¤ ğ“¤)
  Ï• : (Tâ„¤ â†’ A) â†’ (Î£ a ê‰ A , a ï¼ a)
  Ï• f = (f base , ap f loop)
  Ïˆ : (Î£ a ê‰ A , a ï¼ a) â†’ (Tâ„¤ â†’ A)
  Ïˆ (a , p) = Tâ„¤-rec (a , p)
  Î· : Ïˆ âˆ˜ Ï• âˆ¼ id
  Î· f = dfunext (fe ğ“¤â‚ ğ“¤) (Î» X â†’ âˆ¼-to-Tâ„¤-rec f (fe ğ“¤ ğ“¤) X â»Â¹)
  Îµ : Ï• âˆ˜ Ïˆ âˆ¼ id
  Îµ = Tâ„¤-rec-comp

\end{code}

Finally, we use our abstract proof that the universal property implies induction
(which is developed separately in CircleInduction) to derive the induction
principle.

\begin{code}

open import Circle.Induction

module _
        (fe : FunExt)
        (A : Tâ„¤ â†’ ğ“¤ Ì‡ )
        (a : A base)
        (l : transport A loop a ï¼ a)
       where

 open ğ•ŠÂ¹-induction Tâ„¤ base loop (Tâ„¤-universal-property fe) A a l

 Tâ„¤-induction : (x : Tâ„¤) â†’ A x
 Tâ„¤-induction = ğ•ŠÂ¹-induction

 Tâ„¤-induction-comp : (Tâ„¤-induction base , apd Tâ„¤-induction loop)
                   ï¼[ Î£ y ê‰ A base , transport A loop y ï¼ y ] (a , l)
 Tâ„¤-induction-comp = ğ•ŠÂ¹-induction-comp
                      (equiv-to-set loops-at-base-equivalent-to-â„¤ â„¤-is-set)

\end{code}
