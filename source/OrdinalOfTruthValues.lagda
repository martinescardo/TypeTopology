Martin Escardo, 4th October 2018

The ordinal of truth values in a universe ğ“¤.
\begin{code}

{-# OPTIONS --without-K --exact-split --safe --auto-inline #-}

open import SpartanMLTT
open import UF-FunExt
open import UF-Subsingletons renaming (âŠ¤Î© to âŠ¤ ; âŠ¥Î© to âŠ¥)

module OrdinalOfTruthValues
       (fe : FunExt)
       (ğ“¤  : Universe)
       (pe : propext ğ“¤)
       where

open import UF-Subsingletons-FunExt

open import OrdinalArithmetic fe
open import OrdinalsType
open import OrdinalNotions
open import OrdinalsType

Î©â‚’ : Ordinal (ğ“¤ âº)
Î©â‚’ = Î© ğ“¤ , _â‰º_ , pv , w , e , t
 where
  _â‰º_ : Î© ğ“¤ â†’ Î© ğ“¤ â†’ ğ“¤ âº Ì‡
  p â‰º q = (p â‰¡ âŠ¥) Ã— (q â‰¡ âŠ¤)

  pv : is-prop-valued _â‰º_
  pv p q = Ã—-is-prop (Î©-is-set (fe ğ“¤ ğ“¤) pe) (Î©-is-set (fe ğ“¤ ğ“¤) pe)

  w : is-well-founded _â‰º_
  w p = step s
   where
    t : (q : Î© ğ“¤) â†’  q â‰º âŠ¥ â†’ is-accessible _â‰º_ q
    t âŠ¥ (refl , b) = ğŸ˜-elim (âŠ¥-is-not-âŠ¤ b)

    âŠ¥-accessible : is-accessible _â‰º_ âŠ¥
    âŠ¥-accessible = step t

    s : (q : Î© ğ“¤) â†’ q â‰º p â†’ is-accessible _â‰º_ q
    s âŠ¥ (refl , b) = âŠ¥-accessible

  e : is-extensional _â‰º_
  e p q f g = Î©-ext pe (fe ğ“¤ ğ“¤) Ï† Ïˆ
   where
    Ï† : p â‰¡ âŠ¤ â†’ q â‰¡ âŠ¤
    Ï† a = prâ‚‚ (f âŠ¥ (refl , a))

    Ïˆ : q â‰¡ âŠ¤ â†’ p â‰¡ âŠ¤
    Ïˆ b = prâ‚‚ (g âŠ¥ (refl , b))

  t : is-transitive _â‰º_
  t p q r (a , _) (_ , b) = a , b

âŠ¥-is-least : is-least Î©â‚’ âŠ¥
âŠ¥-is-least (P , i) (ğŸ˜ , ğŸ˜-is-prop) (refl , q) = ğŸ˜-elim (equal-âŠ¤-is-true ğŸ˜ ğŸ˜-is-prop q)

âŠ¤-is-largest : is-largest Î©â‚’ âŠ¤
âŠ¤-is-largest (.ğŸ™ , .ğŸ™-is-prop) (.ğŸ˜ , .ğŸ˜-is-prop) (refl , refl) = refl , refl

Â¬Â¬-dense-is-largest' : (p q : Î© ğ“¤)
                     â†’ Â¬Â¬ (p holds)
                     â†’ (q â‰¾âŸ¨ Î©â‚’ âŸ© p)
Â¬Â¬-dense-is-largest' .âŠ¥ .âŠ¤ f (refl , refl) = f ğŸ˜-elim

open import UF-Univalence

module _ (ua : Univalence) where

 open import OrdinalOfOrdinals ua

 ğŸšâ‚’-leq-Î©â‚’ : ğŸšâ‚’ {ğ“¤} âŠ´ Î©â‚’
 ğŸšâ‚’-leq-Î©â‚’ = f , i , p
  where
   f : ğŸ™ + ğŸ™ â†’ Î© ğ“¤
   f (inl â‹†) = âŠ¥
   f (inr â‹†) = âŠ¤

   i : is-initial-segment ğŸšâ‚’ Î©â‚’ f
   i (inl â‹†) .âŠ¥ (refl , e) = ğŸ˜-elim (âŠ¥-is-not-âŠ¤ e)
   i (inr â‹†) .âŠ¥ (refl , _) = inl â‹† , â‹† , refl

   p : is-order-preserving ğŸšâ‚’ Î©â‚’ f
   p (inl â‹†) (inr x) â‹† = refl , refl
   p (inr â‹†) (inl x) l = ğŸ˜-elim l
   p (inr â‹†) (inr x) l = ğŸ˜-elim l

\end{code}

Notice also that being a least element is not in general decidable
because in this example being a least element amounts to being false,
and deciding falsity is equivalent to weak excluded middle.
