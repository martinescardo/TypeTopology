Ayberk Tosun, 12 September 2023

Split out from the now-deprecated `CompactRegular` module.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan hiding (ğŸš)
open import UF.PropTrunc
open import UF.FunExt
open import UF.UA-FunExt
open import UF.Size

module Locales.WellInside (pt : propositional-truncations-exist)
                          (fe : Fun-Ext)
                          (sr : Set-Replacement pt)                where

\end{code}

Importation of foundational UF stuff.

\begin{code}

open import Slice.Family
open import UF.Subsingletons
open import UF.SubtypeClassifier
open import UF.Logic

open import Locales.Frame       pt fe
open import Locales.Complements pt fe

open AllCombinators pt fe
open PropositionalTruncation pt

\end{code}

\begin{code}

well-insideâ‚€ : (F : Frame ğ“¤ ğ“¥ ğ“¦) â†’ âŸ¨ F âŸ© â†’ âŸ¨ F âŸ© â†’ ğ“¤  Ì‡
well-insideâ‚€ F U V =
 Î£ W ê‰ âŸ¨ F âŸ© , (U âˆ§[ F ] W ï¼ ğŸ[ F ]) Ã— (V âˆ¨[ F ] W ï¼ ğŸ[ F ])

infix 4 well-insideâ‚€

syntax well-insideâ‚€ F U V = U â‹œâ‚€[ F ] V

\end{code}

Because well inside is not propositional, we have to truncate it to get a
relation.

\begin{code}

well-inside : (F : Frame ğ“¤ ğ“¥ ğ“¦) â†’ âŸ¨ F âŸ© â†’ âŸ¨ F âŸ© â†’ Î© ğ“¤
well-inside F U V = âˆ¥ U â‹œâ‚€[ F ] V âˆ¥Î©

infix 4 well-inside

syntax well-inside F U V = U â‹œ[ F ] V

\end{code}

Proposition. Let `U, V : ğ’ª(X)`. If `U â‹œ V` then `U â‰¤ V`.

\begin{code}

well-inside-implies-below : (F : Frame ğ“¤ ğ“¥ ğ“¦)
                          â†’ (U V : âŸ¨ F âŸ©)
                          â†’ (U â‹œ[ F ] V â‡’ (U â‰¤[ poset-of F ] V)) holds
well-inside-implies-below F U V = âˆ¥âˆ¥-rec (holds-is-prop (U â‰¤[ poset-of F ] V)) Î³
 where
  _âŠ“_ = Î» U V â†’ U âˆ§[ F ] V

  Î³ : U â‹œâ‚€[ F ] V â†’ (U â‰¤[ poset-of F ] V) holds
  Î³ (W , câ‚ , câ‚‚) = connecting-lemmaâ‚‚ F Î´
   where
    Î´ : U ï¼ U âˆ§[ F ] V
    Î´ = U                        ï¼âŸ¨ ğŸ-right-unit-of-âˆ§ F U â»Â¹              âŸ©
        U âŠ“ ğŸ[ F ]               ï¼âŸ¨ ap (U âŠ“_) (câ‚‚ â»Â¹)                     âŸ©
        U âŠ“ (V âˆ¨[ F ] W)         ï¼âŸ¨ binary-distributivity F U V W         âŸ©
        (U âŠ“ V) âˆ¨[ F ] (U âŠ“ W)   ï¼âŸ¨ ap (Î» - â†’ binary-join F (U âŠ“ V) -) câ‚ âŸ©
        (U âŠ“ V) âˆ¨[ F ] ğŸ[ F ]    ï¼âŸ¨ ğŸ-left-unit-of-âˆ¨ F (U âŠ“ V)            âŸ©
        U âŠ“ V                    âˆ

\end{code}

The set `â†‘â†‘(U) â‰¡ { V âˆ£ U â‹œ V }` is upwards-closed.

\begin{code}

â†‘â†‘-is-upwards-closed : (F : Frame ğ“¤ ğ“¥ ğ“¦)
                     â†’ {U V W : âŸ¨ F âŸ©}
                     â†’ (U â‹œ[ F ] V) holds
                     â†’ (V â‰¤[ poset-of F ] W) holds
                     â†’ (U â‹œ[ F ] W) holds
â†‘â†‘-is-upwards-closed F {U} {V} {W} p q =
 âˆ¥âˆ¥-rec (holds-is-prop (U â‹œ[ F ] W)) Î³ p
  where
   open PosetReasoning (poset-of F)

   Î³ : U â‹œâ‚€[ F ] V â†’ (U â‹œ[ F ] W) holds
   Î³ (T , câ‚ , câ‚‚) = âˆ£ T , câ‚ , dâ‚‚ âˆ£
    where
     Î² : (ğŸ[ F ] â‰¤[ poset-of F ] (W âˆ¨[ F ] T)) holds
     Î² = ğŸ[ F ]      ï¼âŸ¨ câ‚‚ â»Â¹                  âŸ©â‚š
         V âˆ¨[ F ] T  â‰¤âŸ¨ âˆ¨[ F ]-left-monotone q âŸ©
         W âˆ¨[ F ] T  â– 

     dâ‚‚ : W âˆ¨[ F ] T ï¼ ğŸ[ F ]
     dâ‚‚ = only-ğŸ-is-above-ğŸ F (W âˆ¨[ F ] T) Î²

\end{code}

The set `â†“â†“(U) â‰¡ { V | V â‹œ U }` is downwards-closed.

\begin{code}

â†“â†“-is-downwards-closed : (F : Frame ğ“¤ ğ“¥ ğ“¦)
                       â†’ {U V W : âŸ¨ F âŸ©}
                       â†’ (V â‹œ[ F ] W) holds
                       â†’ (U â‰¤[ poset-of F ] V) holds
                       â†’ (U â‹œ[ F ] W) holds
â†“â†“-is-downwards-closed F {U} {V} {W} p q = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop Î³ p
 where
  open PosetReasoning (poset-of F)

  Î³ : V â‹œâ‚€[ F ] W â†’ (U â‹œ[ F ] W) holds
  Î³ (T , câ‚ , câ‚‚) = âˆ£ T , (only-ğŸ-is-below-ğŸ F (U âˆ§[ F ] T) Î² , câ‚‚) âˆ£
   where
    Î² : ((U âˆ§[ F ] T) â‰¤[ poset-of F ] ğŸ[ F ]) holds
    Î² = U âˆ§[ F ] T  â‰¤âŸ¨ âˆ§[ F ]-left-monotone q âŸ©
        V âˆ§[ F ] T  ï¼âŸ¨ câ‚                    âŸ©â‚š
        ğŸ[ F ]      â– 

\end{code}
