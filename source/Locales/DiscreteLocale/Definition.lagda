--------------------------------------------------------------------------------
title:          The discrete locale
author:         Ayberk Tosun
date-started:   2024-03-04
date-completed: 2024-03-04
--------------------------------------------------------------------------------

We define the discrete locale (i.e. the frame of opens of the discrete topology)
over a set `X`.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.PropTrunc
open import UF.FunExt
open import UF.Subsingletons

module Locales.DiscreteLocale.Definition
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
       where

open import Locales.DistributiveLattice.Definition fe pt
open import Locales.Frame pt fe
open import MLTT.Spartan
open import Slice.Family
open import UF.Logic
open import UF.Sets
open import UF.Powerset
open import UF.SubtypeClassifier

open AllCombinators pt fe renaming (_âˆ§_ to _âˆ§â‚š_; _âˆ¨_ to _âˆ¨â‚š_)
open PropositionalSubsetInclusionNotation fe
open PropositionalTruncation pt hiding (_âˆ¨_)

\end{code}

We work with a fixed set `X` in this module.

\begin{code}

module DefnOfDiscreteLocale (X : ğ“¤  Ì‡) (Ïƒ : is-set X) where

\end{code}

We start by writing down the poset of subsets of `X`.

\begin{code}

 _âŠ†áµ–_ : ğ“Ÿ X â†’ ğ“Ÿ X â†’ Î© ğ“¤
 P âŠ†áµ– Q = P âŠ†â‚š Q

 infix 30 _âŠ†áµ–_

 âŠ†-partial-order : is-partial-order (ğ“Ÿ X) _âŠ†â‚š_
 âŠ†-partial-order = (âŠ†-refl , âŠ†-trans') , subset-extensionality pe fe

 poset-of-subsets : Poset (ğ“¤ âº) ğ“¤
 poset-of-subsets = ğ“Ÿ X
                  , _âŠ†â‚š_
                  , (âŠ†-refl , âŠ†-trans')
                  , subset-extensionality pe fe

\end{code}

The top element is the full subset, denoted `full`.

\begin{code}

 full-is-top : (P : ğ“Ÿ X) â†’ (P âŠ†â‚š full) holds
 full-is-top I x = Î» _ â†’ â‹†

\end{code}

Binary meets are set intersection.

\begin{code}

 open Meets _âŠ†áµ–_

 âˆ©-gives-glb : ((P , Q) : ğ“Ÿ X Ã— ğ“Ÿ X) â†’ ((P âˆ© Q) is-glb-of (P , Q)) holds
 âˆ©-gives-glb (P , Q) = ((Î» _ â†’ prâ‚) , (Î» _ â†’ prâ‚‚))
                     , Î» (_ , Ï† , Ïˆ) x r â†’ Ï† x r , Ïˆ x r

\end{code}

Small joins are given by set union, defined as:

\begin{code}

 â‹â‚š_ : Fam ğ“¤ (ğ“Ÿ X) â†’ ğ“Ÿ X
 â‹â‚š S = Î» x â†’ Æ k ê‰ index S , x âˆˆ (S [ k ])

 infix 32 â‹â‚š_

 open Joins _âŠ†áµ–_

 â‹â‚š-gives-an-upper-bound : (S : Fam ğ“¤ (ğ“Ÿ X))
                         â†’ ((â‹â‚š S) is-an-upper-bound-of S) holds
 â‹â‚š-gives-an-upper-bound S i _ Î¼ = âˆ£ i , Î¼ âˆ£

 â‹â‚š-is-least : (S : Fam ğ“¤ (ğ“Ÿ X)) (U : ğ“Ÿ X)
             â†’ (U is-an-upper-bound-of S) holds
             â†’ (â‹â‚š S) âŠ† U
 â‹â‚š-is-least S U Ï… x = âˆ¥âˆ¥-rec (holds-is-prop (x âˆˆâ‚š U)) â€ 
  where
   â€  : Î£ i ê‰ index S , x âˆˆ (S [ i ]) â†’ U x holds
   â€  (i , Î¼) = Ï… i x Î¼

 â‹â‚š-gives-lub : (S : Fam ğ“¤ (ğ“Ÿ X)) â†’ ((â‹â‚š S) is-lub-of S) holds
 â‹â‚š-gives-lub S = â‹â‚š-gives-an-upper-bound S
                , Î» { (U , Ï…) â†’ â‹â‚š-is-least S U Ï… }

\end{code}

Finally, the distributivity law.

\begin{code}

 distributivityâ‚š : (P : ğ“Ÿ X) (S : Fam ğ“¤ (ğ“Ÿ X))
                 â†’ P âˆ© (â‹â‚š S) ï¼ â‹â‚š â… P âˆ© Q âˆ£ Q Îµ S â†
 distributivityâ‚š P S = subset-extensionality pe fe â€  â€¡
  where
   â€  : (P âˆ© â‹â‚š S) âŠ†áµ– â‹â‚š â… P âˆ© Q âˆ£ Q Îµ S â† holds
   â€  x (p , e) = âˆ¥âˆ¥-rec âˆ¥âˆ¥-is-prop (Î» { (i , q) â†’ âˆ£ i , (p , q) âˆ£ }) e

   â€¡ : â‹â‚š â… P âˆ© Q âˆ£ Q Îµ S â† âŠ†áµ– (P âˆ© â‹â‚š S) holds
   â€¡ x = âˆ¥âˆ¥-rec (holds-is-prop (x âˆˆâ‚š (P âˆ© â‹â‚š S))) Î³
    where
     Î³ : Î£ i ê‰ index S , x âˆˆ (P âˆ© (S [ i ])) â†’ x âˆˆ (P âˆ© (â‹â‚š S))
     Î³ (i , p , q) = p , âˆ£ i , q âˆ£

\end{code}

We package these up into a frame that we call `frame-of-subsets`.

\begin{code}

 frame-of-subsets-structure : frame-structure ğ“¤ ğ“¤ (ğ“Ÿ X)
 frame-of-subsets-structure = (_âŠ†â‚š_ , full , _âˆ©_ , â‹â‚š_)
                            , âŠ†-partial-order
                            , full-is-top
                            , âˆ©-gives-glb
                            , â‹â‚š-gives-lub
                            , Î» (P , Q) â†’ distributivityâ‚š P Q

 frame-of-subsets : Frame (ğ“¤ âº) ğ“¤ ğ“¤
 frame-of-subsets = ğ“Ÿ X , frame-of-subsets-structure

\end{code}

The discrete locale on set `X` is the locale given by the frame of subsets of
`X`.

\begin{code}

discrete-locale : (X : ğ“¤  Ì‡) â†’ is-set X â†’ Locale (ğ“¤ âº) ğ“¤ ğ“¤
discrete-locale X Ïƒ =
 record
  { âŸ¨_âŸ©â‚—         = ğ“Ÿ X
  ; frame-str-of = DefnOfDiscreteLocale.frame-of-subsets-structure X Ïƒ
  }

\end{code}
