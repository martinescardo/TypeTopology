Ayberk Tosun, 11 September 2023

\begin{code}[hide]

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan hiding (ğŸš)
open import UF.PropTrunc
open import UF.FunExt
open import UF.UA-FunExt

module Locales.Complements (pt : propositional-truncations-exist)
                           (fe : Fun-Ext)                           where

open import Locales.AdjointFunctorTheoremForFrames
open import Locales.Frame pt fe
open import Locales.WayBelowRelation.Definition pt fe
open import Locales.Compactness pt fe
open import Slice.Family
open import UF.Logic
open import UF.SubtypeClassifier
open import UF.Base using (from-Î£-ï¼)

open AllCombinators pt fe
open PropositionalTruncation pt

open import Locales.GaloisConnection pt fe
open import Locales.InitialFrame     pt fe

open Locale

\end{code}

An open `x` in a frame `L` is *clopen* iff it has complement `xâ€²`.

\begin{code}

is-boolean-complement-of : (L : Frame ğ“¤ ğ“¥ ğ“¦) â†’ âŸ¨ L âŸ© â†’ âŸ¨ L âŸ© â†’ Î© ğ“¤
is-boolean-complement-of F xâ€² x =
 (x âˆ§[ F ] xâ€² ï¼[ iss ]ï¼ ğŸ[ F ]) âˆ§ (x âˆ¨[ F ] xâ€² ï¼[ iss ]ï¼ ğŸ[ F ])
  where
   iss = carrier-of-[ poset-of F ]-is-set

\end{code}

\begin{code}

complementation-is-symmetric : (F : Frame ğ“¤ ğ“¥ ğ“¦) (x y : âŸ¨ F âŸ©)
                             â†’ (is-boolean-complement-of F x y
                             â‡’  is-boolean-complement-of F y x) holds
complementation-is-symmetric F x y (Ï† , Ïˆ) = â€  , â€¡
 where
  â€  : x âˆ§[ F ] y ï¼ ğŸ[ F ]
  â€  = x âˆ§[ F ] y ï¼âŸ¨ âˆ§[ F ]-is-commutative x y âŸ© y âˆ§[ F ] x ï¼âŸ¨ Ï† âŸ© ğŸ[ F ] âˆ

  â€¡ : x âˆ¨[ F ] y ï¼ ğŸ[ F ]
  â€¡ = x âˆ¨[ F ] y ï¼âŸ¨ âˆ¨[ F ]-is-commutative x y âŸ© y âˆ¨[ F ] x ï¼âŸ¨ Ïˆ âŸ© ğŸ[ F ] âˆ

\end{code}

\begin{code}

complement-of-meet : (L : Frame ğ“¤ ğ“¥ ğ“¦)
                   â†’ {x y xâ€² yâ€² : âŸ¨ L âŸ©}
                   â†’ is-boolean-complement-of L x xâ€² holds
                   â†’ is-boolean-complement-of L y yâ€² holds
                   â†’ is-boolean-complement-of L (xâ€² âˆ¨[ L ] yâ€²) (x âˆ§[ L ] y) holds
complement-of-meet L {x} {y} {xâ€²} {yâ€²} Ï† Ïˆ = Î² , Î³
 where
  open PosetReasoning (poset-of L)
  F = L

  _âŠ“_ = Î» x y â†’ x âˆ§[ L ] y

  Ï†â‚€ : x âŠ“ xâ€² ï¼ ğŸ[ F ]
  Ï†â‚€ = x âŠ“ xâ€² ï¼âŸ¨ âˆ§[ F ]-is-commutative x xâ€² âŸ© xâ€² âŠ“ x ï¼âŸ¨ prâ‚ Ï† âŸ©  ğŸ[ F ] âˆ

  Ïˆâ‚€ : y âŠ“ yâ€² ï¼ ğŸ[ F ]
  Ïˆâ‚€ = y âŠ“ yâ€² ï¼âŸ¨ âˆ§[ F ]-is-commutative y yâ€² âŸ© yâ€² âŠ“ y  ï¼âŸ¨ prâ‚ Ïˆ âŸ© ğŸ[ F ] âˆ

  Ï†â‚ : x âˆ¨[ F ] xâ€² ï¼ ğŸ[ F ]
  Ï†â‚ = x  âˆ¨[ F ] xâ€² ï¼âŸ¨ âˆ¨[ F ]-is-commutative x xâ€² âŸ©
       xâ€² âˆ¨[ F ] x  ï¼âŸ¨ prâ‚‚ Ï†                      âŸ©
       ğŸ[ F ]       âˆ

  Î² : (x âˆ§[ F ] y) âˆ§[ F ] (xâ€² âˆ¨[ F ] yâ€²) ï¼ ğŸ[ F ]
  Î² = (x âŠ“ y) âŠ“ (xâ€² âˆ¨[ F ] yâ€²)                ï¼âŸ¨ â…  âŸ©
      ((x âŠ“ y) âŠ“ xâ€²) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)    ï¼âŸ¨ â…¡ âŸ©
      ((y âŠ“ x) âŠ“ xâ€²) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)    ï¼âŸ¨ â…¢ âŸ©
      (y âŠ“ (x âŠ“ xâ€²)) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)    ï¼âŸ¨ â…£ âŸ©
      (y âŠ“ ğŸ[ F ]) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)      ï¼âŸ¨ â…¤ âŸ©
      ğŸ[ F ] âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)            ï¼âŸ¨ â…¥ âŸ©
      (x âŠ“ y) âŠ“ yâ€²                            ï¼âŸ¨ â…¦ âŸ©
      x âŠ“ (y âŠ“ yâ€²)                            ï¼âŸ¨ â…§ âŸ©
      x âŠ“ ğŸ[ F ]                              ï¼âŸ¨ â…¨ âŸ©
      ğŸ[ F ]                                  âˆ
       where
        â…  = binary-distributivity F (x âŠ“ y) xâ€² yâ€²
        â…¡ = ap (Î» - â†’ (- âŠ“ xâ€²) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)) (âˆ§[ F ]-is-commutative x y)
        â…¢ = ap (Î» - â†’ - âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)) (âˆ§[ F ]-is-associative y x xâ€² â»Â¹)
        â…£ = ap (Î» - â†’ (y âŠ“ -) âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)) Ï†â‚€
        â…¤ = ap (Î» - â†’ - âˆ¨[ F ] ((x âŠ“ y) âŠ“ yâ€²)) (ğŸ-right-annihilator-for-âˆ§ F y)
        â…¥ = ğŸ-right-unit-of-âˆ¨ F ((x âŠ“ y) âŠ“ yâ€²)
        â…¦ = âˆ§[ F ]-is-associative x y yâ€² â»Â¹
        â…§ = ap (Î» - â†’ x âŠ“ -) Ïˆâ‚€
        â…¨ = ğŸ-right-annihilator-for-âˆ§ F x


  Î³ : (x âŠ“ y) âˆ¨[ F ] (xâ€² âˆ¨[ F ] yâ€²) ï¼ ğŸ[ F ]
  Î³ = (x âŠ“ y) âˆ¨[ F ] (xâ€² âˆ¨[ F ] yâ€²)                                ï¼âŸ¨ â…  âŸ©
      (xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] (x âŠ“ y)                                ï¼âŸ¨ â…¡ âŸ©
      ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] x) âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)   ï¼âŸ¨ â…¢ âŸ©
      ((yâ€² âˆ¨[ F ] xâ€²) âˆ¨[ F ] x) âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)   ï¼âŸ¨ â…£ âŸ©
      (yâ€² âˆ¨[ F ] (xâ€² âˆ¨[ F ] x)) âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)   ï¼âŸ¨ â…¤ âŸ©
      (yâ€² âˆ¨[ F ] ğŸ[ F ]) âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)          ï¼âŸ¨ â…¥ âŸ©
      ğŸ[ F ] âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)                      ï¼âŸ¨ â…¦ âŸ©
      (xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y                                      ï¼âŸ¨ â…§ âŸ©
      xâ€² âˆ¨[ F ] (yâ€² âˆ¨[ F ] y)                                      ï¼âŸ¨ â…¨ âŸ©
      xâ€² âˆ¨[ F ] ğŸ[ F ]                                             ï¼âŸ¨ â…© âŸ©
      ğŸ[ F ]                                                       âˆ
       where
        â€  = âˆ¨[ F ]-is-commutative xâ€² yâ€²
        â€¡ = ğŸ-right-annihilator-for-âˆ¨ F yâ€²
        â…  = âˆ¨[ F ]-is-commutative (x âŠ“ y) (xâ€² âˆ¨[ F ] yâ€²)
        â…¡ = binary-distributivity-op F (xâ€² âˆ¨[ F ] yâ€²) x y
        â…¢ = ap (Î» - â†’ (- âˆ¨[ F ] x) âˆ§[ F ] ((_ âˆ¨[ F ] _) âˆ¨[ F ] y)) â€ 
        â…£ = ap (Î» - â†’ - âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)) (âˆ¨[ F ]-assoc yâ€² xâ€² x)
        â…¤ = ap (Î» - â†’ (yâ€² âˆ¨[ F ] -) âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)) (prâ‚‚ Ï†)
        â…¥ = ap (Î» - â†’ - âˆ§[ F ] ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)) â€¡
        â…¦ = ğŸ-left-unit-of-âˆ§ F ((xâ€² âˆ¨[ F ] yâ€²) âˆ¨[ F ] y)
        â…§ = âˆ¨[ F ]-assoc xâ€² yâ€² y
        â…¨ = ap (Î» - â†’ xâ€² âˆ¨[ F ] -) (prâ‚‚ Ïˆ)
        â…© = ğŸ-right-annihilator-for-âˆ¨ F xâ€²

\end{code}

\begin{code}

frame-homomorphisms-preserve-complements : (F G : Frame ğ“¤ ğ“¥ ğ“¦)
                                         â†’ (h : F â”€fâ†’ G)
                                         â†’ {x xâ€² : âŸ¨ F âŸ©}
                                         â†’ is-boolean-complement-of F xâ€² x holds
                                         â†’ is-boolean-complement-of G (h .prâ‚ x) (h .prâ‚ xâ€²) holds
frame-homomorphisms-preserve-complements F G ğ’½@(h , _ , Î¼) {x} {xâ€²} (Ï† , Ïˆ) = â€  , â€¡
 where
  â€  : (h xâ€²) âˆ§[ G ] (h x) ï¼ ğŸ[ G ]
  â€  = h xâ€² âˆ§[ G ] h x   ï¼âŸ¨ â…  âŸ©
      h (xâ€² âˆ§[ F ] x)   ï¼âŸ¨ â…¡ âŸ©
      h ğŸ[ F ]          ï¼âŸ¨ â…¢ âŸ©
      ğŸ[ G ]            âˆ
       where
        â…  = frame-homomorphisms-preserve-meets F G ğ’½ xâ€² x â»Â¹
        â…¡ = ap h (xâ€² âˆ§[ F ] x   ï¼âŸ¨ âˆ§[ F ]-is-commutative xâ€² x âŸ©
                  x âˆ§[ F ] xâ€²   ï¼âŸ¨ Ï† âŸ©
                  ğŸ[ F ]        âˆ)
        â…¢ = frame-homomorphisms-preserve-bottom F G ğ’½

  â€¡ : h xâ€² âˆ¨[ G ] h x ï¼ ğŸ[ G ]
  â€¡ = h xâ€² âˆ¨[ G ] h x   ï¼âŸ¨ â…  âŸ©
      h (xâ€² âˆ¨[ F ] x)   ï¼âŸ¨ â…¡ âŸ©
      h ğŸ[ F ]          ï¼âŸ¨ â…¢ âŸ©
      ğŸ[ G ]            âˆ
       where
        â…  = frame-homomorphisms-preserve-binary-joins F G ğ’½ xâ€² x â»Â¹
        â…¡ = ap h (xâ€² âˆ¨[ F ] x ï¼âŸ¨ âˆ¨[ F ]-is-commutative xâ€² x âŸ©
                  x âˆ¨[ F ] xâ€² ï¼âŸ¨ Ïˆ âŸ©
                  ğŸ[ F ]      âˆ)
        â…¢ = frame-homomorphisms-preserve-top F G ğ’½

\end{code}
