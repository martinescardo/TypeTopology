---
title:          The SierpiÅ„ski locale and its patch
author:         Ayberk Tosun
date-completed: 2024-02-12
---

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.FunExt
open import UF.Logic
open import MLTT.Spartan hiding (ğŸš)
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Size hiding (is-locally-small)

module Locales.Sierpinski
        (ğ“¤  : Universe)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (sr : Set-Replacement pt) where

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤
open import DomainTheory.Basics.Dcpo    pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import DomainTheory.Basics.Pointed pt fe ğ“¤ renaming (âŠ¥ to âŠ¥âˆ™)
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.Lifting.LiftingSet pt fe ğ“¤ pe
open import DomainTheory.Lifting.LiftingSetAlgebraic pt pe fe ğ“¤
open import Lifting.Lifting ğ“¤
open import Lifting.Miscelanea-PropExt-FunExt ğ“¤ pe fe
open import Lifting.UnivalentPrecategory ğ“¤ (ğŸ™ {ğ“¤})
open import Locales.Frame pt fe hiding (ğŸš; is-directed)
open import Locales.InitialFrame pt fe
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralLocale pt fe
open import Locales.Spectrality.SpectralMap pt fe
open import Locales.Stone pt fe sr
open import Slice.Family
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Subsingletons-Properties
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier

open Locale

open PropositionalTruncation pt

\end{code}

We first define the Sierpinski domain

\begin{code}

ğ•Šğ““âº : DCPO {ğ“¤ âº } {ğ“¤ âº}
ğ•Šğ““âº = ğ“›-DCPO {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

\end{code}

which is locally small and also algebraic:

\begin{code}

ğ•Š-is-locally-small : is-locally-small ğ•Šğ““âº
ğ•Š-is-locally-small = ğ“›-is-locally-small {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

ğ•Šğ““âº-is-algebraic : is-algebraic-dcpo (ğ“›-DCPO {X = ğŸ™ {ğ“¤}} ğŸ™-is-set)
ğ•Šğ““âº-is-algebraic = ğ“›-is-algebraic-dcpo ğŸ™-is-set

\end{code}

Unfortunately, we do not have the required machinery for making a locally small
copy of a dcpo from an extrinsic proof that it is locally small. In hindsight,
it would have been easier for me to work with such extrinsic proofs of local
smallness, but I didn't do this and right now, I don't have the time to migrate
my formalization to this style.

Therefore, I defined the function `ğ“›-DCPOâ»` which directly gives the locally
small copy of the dcpo in consideration. Instead of working with `ğ•Šğ““âº`, I work
with `ğ•Šğ““` instead to circumvent this problem.

\begin{code}

ğ•Šğ““ : DCPO {ğ“¤ âº} {ğ“¤}
ğ•Šğ““ = ğ“›-DCPOâ» {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

\end{code}

These two dcpos are of course order-isomorphic.

\begin{code}

âŠ‘-implies-âŠ‘âº : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y â†’ x âŠ‘âŸ¨ ğ•Šğ““âº âŸ© y
âŠ‘-implies-âŠ‘âº x y p q = âŠ‘-to-âŠ‘' p q

âŠ‘âº-implies-âŠ‘ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““âº âŸ© y â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
âŠ‘âº-implies-âŠ‘ x y p = (Î» q â†’ transport is-defined (p q) q) , Î» _ â†’ refl

\end{code}

The proposition `ğŸ˜` is the bottom element of this dcpo, meaning it can be
made into a pointed dcpo:

\begin{code}

ğ•Šğ““âŠ¥ : DCPOâŠ¥ {ğ“¤ âº} {ğ“¤}
ğ•Šğ““âŠ¥ = ğ•Šğ““ , (ğŸ˜ , (Î» ()) , ğŸ˜-is-prop) , Î» _ â†’ (Î» ()) , Î» ()

\end{code}

The proposition `ğŸ™` is a top element of this dcpo.

\begin{code}

ğŸ™-is-top : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© Î· â‹†
ğŸ™-is-top (P , q) = (Î» _ â†’ â‹†) , Î» _ â†’ refl

\end{code}

Furthermore, the dcpo `ğ•Šğ““` is compact.

\begin{code}

ğ•Šğ““-is-compact : is-compact ğ•Šğ““ (Î· â‹†)
ğ•Šğ““-is-compact I Î± (âˆ£iâˆ£ , upâ») pâ» =
 âˆ¥âˆ¥-rec âˆƒ-is-prop â€  (Î·s-are-compact ğŸ™-is-set â‹† I Î± Î´ p)
  where
   open is-locally-small ğ•Š-is-locally-small

   up : is-semidirected (underlying-order ğ•Šğ““âº) Î±
   up i j = âˆ¥âˆ¥-rec âˆƒ-is-prop â€  (upâ» i j)
    where
     â€  : Î£ k ê‰ I , (Î± i âŠ‘âŸ¨ ğ•Šğ““  âŸ© Î± k) Ã— (Î± j âŠ‘âŸ¨ ğ•Šğ““  âŸ© Î± k)
       â†’ âˆƒ k ê‰ I , (Î± i âŠ‘âŸ¨ ğ•Šğ““âº âŸ© Î± k) Ã— (Î± j âŠ‘âŸ¨ ğ•Šğ““âº âŸ© Î± k)
     â€  (k , p , q) = âˆ£ k , âŠ‘-implies-âŠ‘âº (Î± i) (Î± k) p  , âŠ‘-implies-âŠ‘âº (Î± j) (Î± k) q âˆ£

   Î´ : is-directed (underlying-order ğ•Šğ““âº) Î±
   Î´ = âˆ£iâˆ£ , up

   p : Î· â‹† âŠ‘âŸ¨ ğ•Šğ““âº âŸ© (âˆ (ğ“›-DCPO ğŸ™-is-set) Î´)
   p = âŠ‘-to-âŠ‘' (prâ‚ pâ» , Î» _ â†’ refl)

   â€  : Î£ i ê‰ I , underlying-order (ğ“›-DCPO ğŸ™-is-set) (Î· â‹†) (Î± i)
     â†’ âˆƒ i ê‰ I , Î· â‹† âŠ‘âŸ¨ ğ•Šğ““ âŸ© (Î± i)
   â€  (i , q) = âˆ£ i , âŠ‘âº-implies-âŠ‘ (Î· â‹†) (Î± i) q âˆ£

\end{code}

We define a function for mapping inhabitants of the SierpiÅ„ski dcpo to the type
of propositions:

\begin{code}

to-Î© : âŸ¨ ğ•Šğ““ âŸ©âˆ™ â†’ Î© ğ“¤
to-Î© (P , _ , h) = P , h

\end{code}

Conversely, we define a function mapping every proposition `P : Î© ğ“¤` to the
carrier set of the SierpiÅ„ski dcpo.

\begin{code}

to-ğ•Šğ““ : Î© ğ“¤ â†’  âŸ¨ ğ•Šğ““ âŸ©âˆ™
to-ğ•Šğ““ (P , h) = P , (Î» _ â†’ â‹†) , h

\end{code}

It is obvious that these form an equivalence.

\begin{code}

Î©-equivalent-to-ğ•Š : Î© ğ“¤ â‰ƒ âŸ¨ ğ•Šğ““ âŸ©âˆ™
Î©-equivalent-to-ğ•Š = to-ğ•Šğ““ , ((to-Î© , â€ ) , (to-Î© , â€¡))
 where
  Ïˆ : {A : ğ“¤  Ì‡} â†’ is-prop (A â†’ ğŸ™)
  Ïˆ = Î -is-prop fe (Î» _ â†’ ğŸ™-is-prop)

  Ï‘ : {A : ğ“¤  Ì‡} â†’ is-prop (is-prop A)
  Ï‘ = being-prop-is-prop fe

  â€  : (to-ğ•Šğ““ âˆ˜ to-Î©) âˆ¼ id
  â€  (P , f , h) = to-subtype-ï¼ (Î» _ â†’ Ã—-is-prop Ïˆ Ï‘) refl

  â€¡ : to-Î© âˆ˜ to-ğ•Šğ““ âˆ¼ id
  â€¡ (P , h) = to-subtype-ï¼ (Î» _ â†’ Ï‘) refl

\end{code}

We now proceed to the definition of the SierpiÅ„ski locale.

First, we show that `ğ•Šğ““` has a specified small compact basis.


\begin{code}

open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤

hscb : has-specified-small-compact-basis ğ•Šğ““
hscb = (ğŸ™ {ğ“¤} + ğŸ™ {ğ“¤}) , Î² , Ïƒ
 where
  Î² : ğŸ™ + ğŸ™ â†’ âŸ¨ ğ•Šğ““ âŸ©âˆ™
  Î² (inl â‹†) = âŠ¥âˆ™ (ğ“›-DCPOâŠ¥ ğŸ™-is-set)
  Î² (inr â‹†) = ğŸ™ {ğ“¤} , (Î» { â‹† â†’ â‹† }) , ğŸ™-is-prop

  Î²-is-compact : (b : ğŸ™ + ğŸ™) â†’ is-compact ğ•Šğ““ (Î² b)
  Î²-is-compact (inl â‹†) = âŠ¥-is-compact ğ•Šğ““âŠ¥
  Î²-is-compact (inr â‹†) = ğ•Šğ““-is-compact

  Î²-is-upward-directed : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
                       â†’ is-semidirected (underlying-order ğ•Šğ““) (â†“-inclusion ğ•Šğ““ Î² x)
  Î²-is-upward-directed x (inl â‹† , p) (z , q)  = let
                                                 u = (z , q)
                                                 râ‚ = reflexivity ğ•Šğ““ (Î² (inl â‹†))
                                                 râ‚‚ = reflexivity ğ•Šğ““ (Î² z)
                                                in
                                                 âˆ£ u , âŠ¥-is-least ğ•Šğ““âŠ¥ (Î² z) , râ‚‚ âˆ£
  Î²-is-upward-directed x (inr â‹† , pâ‚) (z , q) = let
                                                 râ‚ = reflexivity ğ•Šğ““ (Î² (inr â‹†))
                                                 râ‚‚ = reflexivity ğ•Šğ““ (Î² (inr â‹†))
                                                in
                                                 âˆ£ (inr â‹† , pâ‚) , râ‚ , ğŸ™-is-top (Î² z) âˆ£

  covering : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ is-sup (underlying-order ğ•Šğ““) x (â†“-inclusion ğ•Šğ““ Î² x)
  covering ğ’«@(P , f , h) = prâ‚‚ , â€ 
   where
    â€  : is-lowerbound-of-upperbounds (underlying-order ğ•Šğ““) (P , f , h) (â†“-inclusion ğ•Šğ““ Î² (P , f , h))
    â€  ğ’«â€²@(Pâ€² , fâ€² , hâ€²) Ï… = â€¡
     where
      â™  : P â†’ ğ’« âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«â€²
      â™  p = transport (Î» - â†’ - âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«â€²) eq (Ï… (inr â‹† , q))
       where
        q : Î² (inr â‹†) âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«
        q = (Î» _ â†’ p) , Î» _ â†’ ğŸ™-is-prop â‹† (f p)

        eq : Î² (inr â‹†) ï¼ ğ’«
        eq = antisymmetry ğ•Šğ““ (Î² (inr â‹†)) ğ’« q (ğŸ™-is-top ğ’«)

      â€¡ : underlying-order ğ•Šğ““ (P , f , h) ğ’«â€²
      â€¡ = (Î» p â†’ prâ‚ (â™  p) p) , Î» p â†’ ğŸ™-is-prop â‹† (f p)

  Ïƒ : is-small-compact-basis ğ•Šğ““ Î²
  Ïƒ = record
       { basis-is-compact = Î²-is-compact
       ; âŠ‘á´®-is-small = Î» x b â†’ (Î² b âŠ‘âŸ¨ ğ•Šğ““ âŸ© x) , â‰ƒ-refl (Î² b âŠ‘âŸ¨ ğ•Šğ““ âŸ© x)
       ; â†“á´®-is-directed = Î» x â†’ âˆ£ inl â‹† , âŠ¥-is-least ğ•Šğ““âŠ¥ x âˆ£ , Î²-is-upward-directed x
       ; â†“á´®-is-sup = covering
       }

\end{code}

Using this compact basis, we define the SierpiÅ„ski locale as the Scott locale of
`ğ•Šğ““`.

\begin{code}

open ScottLocaleConstruction ğ•Šğ““ hscb pe

ğ•Š : Locale (ğ“¤ âº) ğ“¤ ğ“¤
ğ•Š = ScottLocale

\end{code}

The true truth value in the SierpiÅ„ski space i.e. its only nontrivial open.

\begin{code}

open DefnOfScottLocale ğ•Šğ““ ğ“¤ pe

âŠ¤ğ•Š : âŸ¨ ğ’ª ğ•Š âŸ©
âŠ¤ğ•Š = âŠ¤â‚›

\end{code}

We now show that `ğ•Šğ““` is a Scott domain. We have already shown that it is an
algebraic lattice, so it remains to show that it is bounded complete.

\begin{code}

open import DomainTheory.BasesAndContinuity.ScottDomain pt fe ğ“¤

open DefinitionOfBoundedCompleteness

âŠ‘â‚€-implies-âŠ‘ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
             â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
             â†’ (to-Î© x â‰¤[ poset-of (ğŸ-ğ”½ğ•£ğ• pe) ] to-Î© y) holds
âŠ‘â‚€-implies-âŠ‘ _ _ (g , q) p = g p

âŠ‘-implies-âŠ‘â‚€ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
             â†’ (to-Î© x â‰¤[ poset-of (ğŸ-ğ”½ğ•£ğ• pe) ] to-Î© y) holds
             â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
âŠ‘-implies-âŠ‘â‚€ (P , f , h) (Pâ€² , fâ€² , hâ€²) p = p , (Î» _ â†’ ğŸ™-is-prop â‹† â‹†)

ğ•Šğ““-bounded-complete : bounded-complete ğ•Šğ““ holds
ğ•Šğ““-bounded-complete S _ = sup , Ï†
 where
  Sâ‚€ : Fam ğ“¤ (Î© ğ“¤)
  Sâ‚€ = â… to-Î© P âˆ£ P Îµ S â†

  supâ‚€ : Î© ğ“¤
  supâ‚€ = â‹[ (ğŸ-ğ”½ğ•£ğ• pe) ] Sâ‚€

  sup : âŸ¨ ğ•Šğ““ âŸ©âˆ™
  sup = supâ‚€ holds , (Î» _ â†’ â‹†) , âˆƒ-is-prop

  Ï… : is-upperbound (underlying-order ğ•Šğ““) sup (S [_])
  Ï… i = â€  , â€¡
   where
    â€  : is-defined (S [ i ]) â†’ is-defined sup
    â€  p = âˆ£ i , p âˆ£

    â€¡ : value (S [ i ]) âˆ¼ (Î» xâ‚ â†’ value sup (â€  xâ‚))
    â€¡ _ = ğŸ™-is-prop â‹† â‹†

  Ï‘ : is-lowerbound-of-upperbounds (underlying-order ğ•Šğ““) sup (S [_])
  Ï‘ (P , f , h) q = âŠ‘-implies-âŠ‘â‚€ sup (P , f , h) (â‹[ ğŸ-ğ”½ğ•£ğ• pe ]-least Sâ‚€ ((P , h) , (Î» i â†’ prâ‚ (q i))))

  Ï† : is-sup (underlying-order ğ•Šğ““) sup (S [_])
  Ï† = Ï… , Ï‘

\end{code}

Finally, we show that `ğ•Šğ““` trivially satisfies the decidability condition that
we assume in the proof that Scott locales of Scott domains are spectral.

\begin{code}

open import Locales.ScottLocale.ScottLocalesOfScottDomains pt fe sr ğ“¤

ğ•Šğ““-satisfies-dc : decidability-condition ğ•Šğ““
ğ•Šğ““-satisfies-dc ğ’«â‚€@(Pâ‚€ , hâ‚€ , fâ‚€) ğ’«â‚@(Pâ‚ , hâ‚ , fâ‚) Îºc Îºd =
 inl âˆ£ up , â€¡ âˆ£
  where
   up : âŸ¨ ğ•Šğ““ âŸ©âˆ™
   up = to-ğ•Šğ““ (to-Î© ğ’«â‚€ âˆ¨[ ğŸ-ğ”½ğ•£ğ• pe ] to-Î© ğ’«â‚)

   open Joins {A = âŸ¨ ğ•Šğ““ âŸ©âˆ™} (Î» x y â†’ (x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y) , prop-valuedness ğ•Šğ““ x y)

   â€¡ : (up is-an-upper-bound-of (binary-family ğ“¤ ğ’«â‚€ ğ’«â‚)) holds
   â€¡ (inl â‹†) = (Î» p â†’ âˆ£ inl â‹† , p âˆ£) , Î» _ â†’ ğŸ™-is-prop â‹† â‹†
   â€¡ (inr â‹†) = (Î» p â†’ âˆ£ inr â‹† , p âˆ£) , Î» _ â†’ ğŸ™-is-prop â‹† â‹†

\end{code}

From all these, we obtain the fact that `ğ•Š` is a spectral locale.

\begin{code}

ğ•Šğ““-has-least : has-least (underlying-order ğ•Šğ““)
ğ•Šğ““-has-least = (âŠ¥âˆ™ ğ•Šğ““âŠ¥) , âŠ¥-is-least ğ•Šğ““âŠ¥

open SpectralScottLocaleConstruction ğ•Šğ““ ğ•Šğ““-has-least hscb ğ•Šğ““-satisfies-dc ğ•Šğ““-bounded-complete pe

ğ•Š-is-spectralá´° : spectralá´° ğ•Š
ğ•Š-is-spectralá´° = Ïƒá´°

open import Locales.PatchLocale pt fe sr

ğ•Š-is-spectral : is-spectral ğ•Š holds
ğ•Š-is-spectral = spectralá´°-gives-spectrality ğ•Š Ïƒá´°

ğ•Š-has-small-ğ’¦ : has-small-ğ’¦ ğ•Š
ğ•Š-has-small-ğ’¦ = spectralá´°-implies-small-ğ’¦ ğ•Š Ïƒá´°

open SmallPatchConstruction ğ•Š ğ•Š-is-spectralá´° renaming (SmallPatch to Patch-ğ•Š)

\end{code}

We conclude by constructing the patch of SierpiÅ„ski.

\begin{code}

patch-of-ğ•Š : Locale (ğ“¤ âº) ğ“¤ ğ“¤
patch-of-ğ•Š = Patch-ğ•Š

\end{code}

The universal property of Patch then specializes to the following.

\begin{code}

open import Locales.UniversalPropertyOfPatch pt fe sr

open import Locales.PatchProperties pt fe sr

open ClosedNucleus ğ•Š ğ•Š-is-spectral

ump-for-patch-of-ğ•Š : (X : Locale (ğ“¤ âº) ğ“¤ ğ“¤)
                   â†’ is-stone X holds
                   â†’ (ğ’»@(f , _) : X â”€câ†’ ğ•Š)
                   â†’ is-spectral-map ğ•Š X ğ’» holds
                   â†’ âˆƒ! ğ’»â»@(fâ» , _) ê‰ X â”€câ†’ Patch-ğ•Š ,
                      ((U : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ f U ï¼ fâ» â€˜ U â€™)
ump-for-patch-of-ğ•Š = ump-of-patch ğ•Š ğ•Š-is-spectral ğ•Š-has-small-ğ’¦

\end{code}
