---
title:          Equivalence of sharp elements with spectral points
author:         Ayberk Tosun
date-started:   2024-05-22
date-completed: 2024-05-28
dates-updated:  [2024-06-03]
---

This module contains the proof of equivalence between the sharp elements of a
Scott domain and the â€œspectral pointsâ€ of its Scott locale. This equivalence was
conjectured by MartÃ­n EscardÃ³ and proved by Ayberk Tosun on 2024-03-15.

The formalization of the proof was completed on 2024-05-28.

\begin{code}[hide]

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.List hiding ([_])
open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc
open import UF.Size
open import UF.Subsingletons
open import UF.UA-FunExt
open import UF.Univalence

module Locales.LawsonLocale.SharpElementsCoincideWithSpectralPoints
        (ğ“¤  : Universe)
        (ua : Univalence)
        (pt : propositional-truncations-exist)
        (sr : Set-Replacement pt)
       where

private
 fe : Fun-Ext
 fe {ğ“¤} {ğ“¥} = univalence-gives-funext' ğ“¤ ğ“¥ (ua ğ“¤) (ua (ğ“¤ âŠ” ğ“¥))

 pe : Prop-Ext
 pe {ğ“¤} = univalence-gives-propext (ua ğ“¤)

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.CompactBasis pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.ScottDomain pt fe ğ“¤
open import DomainTheory.Basics.Dcpo pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.Topology.ScottTopology pt fe ğ“¤
open import DomainTheory.Topology.ScottTopologyProperties pt fe ğ“¤
open import Locales.Clopen pt fe sr
open import Locales.CompactRegular pt fe using (clopens-are-compact-in-compact-frames)
open import Locales.Compactness pt fe hiding (is-compact)
open import Locales.ContinuousMap.FrameHomomorphism-Definition pt fe
open import Locales.ContinuousMap.FrameHomomorphism-Properties pt fe
open import Locales.Frame pt fe
open import Locales.InitialFrame pt fe hiding (_âŠ‘_)
open import Locales.LawsonLocale.CompactElementsOfPoint ğ“¤ fe pe pt sr
open import Locales.Point.Definition pt fe
open import Locales.Point.SpectralPoint-Definition pt fe
open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.Properties pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfScottDomains pt fe sr ğ“¤
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralMap pt fe
open import Locales.TerminalLocale.Properties pt fe sr
open import NotionsOfDecidability.Decidable
open import NotionsOfDecidability.SemiDecidable fe pe pt
open import Slice.Family
open import UF.Equiv
open import UF.Logic
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier renaming (âŠ¥ to âŠ¥â‚š)

open AllCombinators pt fe
open DefinitionOfScottDomain
open Locale
open PropositionalTruncation pt hiding (_âˆ¨_)

\end{code}

\section{Preliminaries}

We define a version of the predicate `is-compact` that is packaged up with the
proof that it is a proposition.

\begin{code}

is-compactâ‚š : (ğ““ : DCPO {ğ“¤ âº} {ğ“¤}) â†’ âŸ¨ ğ““ âŸ©âˆ™ â†’ Î© (ğ“¤ âº)
is-compactâ‚š ğ““ x = is-compact ğ““ x , being-compact-is-prop ğ““ x

\end{code}

Similarly, we define a version of the predicate `is-decidable` that is packaged
up with the proof that it is a proposition.

\begin{code}

is-decidableâ‚š : Î© ğ“¤ â†’ Î© ğ“¤
is-decidableâ‚š P =
 is-decidable (P holds) , decidability-of-prop-is-prop fe (holds-is-prop P)

\end{code}

\section{Introduction}

We work in a module parameterized by

 - a large and locally small Scott domain `ğ““`,
 - assumed to satisfy the `decidability-condition` which says that upper
   boundedness of its compact elements is a decidable property.

\begin{code}

module Sharp-Element-Spectral-Point-Equivalence
        (ğ““    : DCPO {ğ“¤ âº} {ğ“¤})
        (hl   : has-least (underlying-order ğ““))
        (sd   : is-scott-domain ğ““ holds)
        (dc   : decidability-condition ğ““)
       where

 open Construction ğ““ ua hl sd dc
 open DefinitionOfBoundedCompleteness hiding (_âŠ‘_)

\end{code}

The following is a bit of preparation for the proofs. We open up relevant
modules and define abbreviations for constructions that we use for the sake of
readability and self-containment.

\begin{code}

 ğ’·â‚€ : has-unspecified-small-compact-basis ğ““
 ğ’·â‚€ = prâ‚ sd

\end{code}

We denote by `Scottâ¦…ğ““â¦†` the Scott locale of domain `ğ““`.

\begin{code}

 open SpectralScottLocaleConstructionâ‚‚ ğ““ ua hl sd dc pe renaming (Ïƒâ¦…ğ““â¦† to Scottâ¦…ğ““â¦†)

\end{code}

For the frame of opens of the Scott locale `Scottâ¦…ğ““â¦†`, we reserve the notation
`Ïƒâ¦…ğ““â¦†`. This notation differs from other uses in TypeTopology, but it should be
the standard one and the notation elsewhere should be updated to use this one.

\begin{code}

 Ïƒâ¦…ğ““â¦† : Frame (ğ“¤ âº) ğ“¤ ğ“¤
 Ïƒâ¦…ğ““â¦† = ğ’ª Scottâ¦…ğ““â¦†

 open SpectralScottLocaleConstruction  ğ““ hl hscb dc bc pe hiding (scb; Ïƒá´°)

 open ScottLocaleProperties ğ““ hl hscb pe renaming (âŠ¤-is-compact to Scottâ¦…ğ““â¦†-is-compact)
 open is-small-compact-basis scb
 open structurally-algebraic

 Ïƒá´° : spectralá´° Scottâ¦…ğ““â¦†
 Ïƒá´° = scott-locale-spectralá´°

\end{code}

The family `basis` given below is the basis of the Scott locale of domain `ğ““`.

\begin{code}

 basis : Fam ğ“¤ âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©
 basis = basisâ‚› Scottâ¦…ğ““â¦† Ïƒá´°

 BÏƒ : ğ“¤  Ì‡
 BÏƒ = index basis

 Î²Ïƒ : BÏƒ â†’ âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©
 Î²Ïƒ = basis [_]

 ÎºÏƒ : (i : BÏƒ) â†’ is-compact-open Scottâ¦…ğ““â¦† (Î²Ïƒ i) holds
 ÎºÏƒ = basisâ‚›-consists-of-compact-opens Scottâ¦…ğ““â¦† Ïƒá´°

\end{code}

We define a version of the order of `ğ““` that is packaged up with the proof that
it is a proposition (called `prop-valuedness` in the domain theory development).

\begin{code}

 _âŠ‘_ : âŸ¨ ğ““ âŸ©âˆ™ â†’ âŸ¨ ğ““ âŸ©âˆ™ â†’ Î© ğ“¤
 x âŠ‘ y = (x âŠ‘âŸ¨ ğ““ âŸ© y) , prop-valuedness ğ““ x y

\end{code}

\section{Definition of sharpness}

We now define what it means for an element to be _sharp_, following the work of
de Jong [1].

\begin{code}

 is-sharp : âŸ¨ ğ““ âŸ©âˆ™ â†’ Î© (ğ“¤ âº)
 is-sharp x = â±¯ c ê‰ âŸ¨ ğ““ âŸ©âˆ™ , is-compactâ‚š ğ““ c â‡’ is-decidableâ‚š (c âŠ‘ x)

\end{code}

This definition of the notion of sharpness is a predicate with large truth
values as it quantifies over the compact opens. Because we are working with an
algebraic dcpo, however, we can define a small version which we denote
`is-sharpâ»`.

\begin{code}

 is-sharpâ» : âŸ¨ ğ““ âŸ©âˆ™ â†’ Î© ğ“¤
 is-sharpâ» x = â±¯ i ê‰ index Bğ““ , is-decidableâ‚š ((Bğ““ [ i ]) âŠ‘ x)

\end{code}

These two are equivalent.

\begin{code}

 sharp-implies-sharpâ» : (â±¯ x ê‰ âŸ¨ ğ““ âŸ©âˆ™ , is-sharp x â‡’ is-sharpâ» x) holds
 sharp-implies-sharpâ» x ğ•¤ i = ğ•¤ (Bğ““ [ i ]) (basis-is-compact i)

 sharpâ»-implies-sharp : (â±¯ x ê‰ âŸ¨ ğ““ âŸ©âˆ™ , is-sharpâ» x â‡’ is-sharp x) holds
 sharpâ»-implies-sharp x ğ•¤ c Ï‡ =
  âˆ¥âˆ¥-rec (holds-is-prop (is-decidableâ‚š (c âŠ‘ x))) â€  Î¼
   where
    Î¼ : âˆƒ i ê‰ index Bğ““ , Bğ““ [ i ] ï¼ c
    Î¼ = small-compact-basis-contains-all-compact-elements ğ““ (Bğ““ [_]) scb c Ï‡

    â€  : Î£ i ê‰ index Bğ““ , Bğ““ [ i ] ï¼ c â†’ is-decidableâ‚š (c âŠ‘ x) holds
    â€  (i , p) = transport (Î» - â†’ is-decidableâ‚š (- âŠ‘ x) holds) p (ğ•¤ i)

 sharp-is-equivalent-to-sharpâ» : (â±¯ x ê‰ âŸ¨ ğ““ âŸ©âˆ™ , is-sharp x â‡” is-sharpâ» x) holds
 sharp-is-equivalent-to-sharpâ» x =
  sharp-implies-sharpâ» x , sharpâ»-implies-sharp x

\end{code}

We now define the type `â™¯ğ““` of sharp elements of the Scott domain `ğ““`.

\begin{code}

 â™¯ğ““ : ğ“¤ âº  Ì‡
 â™¯ğ““ = Î£ x ê‰ âŸ¨ ğ““ âŸ©âˆ™ , is-sharp x holds

\end{code}

We usually pattern match on the inhabitants of `â™¯ğ““` to refer to the first
component. But if the need arises, we denote the underlying element of a sharp
element `ğ“` by `â¦… ğ“ â¦†`.

\begin{code}

 â¦…_â¦† : â™¯ğ““ â†’ âŸ¨ ğ““ âŸ©âˆ™
 â¦…_â¦† (x , _) = x

 abstract
  to-sharp-ï¼ : (ğ“ ğ“ : â™¯ğ““) â†’ prâ‚ ğ“ ï¼ prâ‚ ğ“ â†’ ğ“ ï¼ ğ“
  to-sharp-ï¼ ğ“ ğ“ = to-subtype-ï¼ (holds-is-prop âˆ˜ is-sharp)

 open Preliminaries
 open Locale
 open DefnOfScottTopology ğ““ ğ“¤

\end{code}

\section{Characterization of sharp elements}

In this section, we give a characterization of sharp elements which we use
when constructing the equivalence (in the next section).

We define the following predicate expressing that an element `x` has decidable
membership in compact Scott opens.

\begin{code}

 open Properties ğ““

 admits-decidable-membership-in-compact-scott-opens : âŸ¨ ğ““ âŸ©âˆ™ â†’ Î© (ğ“¤ âº)
 admits-decidable-membership-in-compact-scott-opens x =
  â±¯ ğ’¦ ê‰ âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ© , is-compact-open Scottâ¦…ğ““â¦† ğ’¦ â‡’ is-decidableâ‚š (x âˆˆâ‚› ğ’¦)

\end{code}

Every sharp element admits decidable membership in compact Scott opens.

\begin{code}

 sharp-implies-admits-decidable-membership-in-compact-scott-opens
  : (x : âŸ¨ ğ““ âŸ©âˆ™)
  â†’ (is-sharp x â‡’ admits-decidable-membership-in-compact-scott-opens x) holds
 sharp-implies-admits-decidable-membership-in-compact-scott-opens x ğ“ˆğ’½ ğ’¦ ğ•œ =
  âˆ¥âˆ¥-rec (holds-is-prop (is-decidableâ‚š (x âˆˆâ‚› ğ’¦))) (uncurry â€¡) â™¢
   where
    â™¢ : is-basic Scottâ¦…ğ““â¦† ğ’¦ (spectralá´°-implies-directed-basisá´° Scottâ¦…ğ““â¦† Ïƒá´°) holds
    â™¢ = compact-opens-are-basic
         Scottâ¦…ğ““â¦†
         (spectralá´°-implies-directed-basisá´° Scottâ¦…ğ““â¦† Ïƒá´°)
         ğ’¦
         ğ•œ

    lemma : (xs : List (index Bğ““)) â†’ is-decidableâ‚š (x âˆˆâ‚› Î²Ïƒ xs) holds
    lemma []       = inr ğŸ˜-elim
    lemma (i âˆ· is) =
     âˆ¨-preserves-decidability pt (x âˆˆâ‚› â†‘Ë¢[ Î²â‚– i ]) (x âˆˆâ‚› ğœ¸ is) â€  IH
      where
       â€  : is-decidableâ‚š (x âˆˆâ‚› â†‘Ë¢[ Î²â‚– i ]) holds
       â€  = ğ“ˆğ’½ (Î² i) (basis-is-compact i)

       IH : is-decidableâ‚š (x âˆˆâ‚› ğœ¸ is) holds
       IH = lemma is

    â€¡ : (xs : List (index Bğ““)) â†’ Î²Ïƒ xs ï¼ ğ’¦ â†’ is-decidableâ‚š (x âˆˆâ‚› ğ’¦) holds
    â€¡ xs p = transport (Î» - â†’ is-decidableâ‚š (x âˆˆâ‚› -) holds) p (lemma xs)

\end{code}

The converse also holds meaning elements that admit decidable membership in
compact Scott opens are _exactly_ the sharp elements.

\begin{code}

 admits-decidable-membership-in-compact-scott-opens-implies-is-sharp
  : (x : âŸ¨ ğ““ âŸ©âˆ™)
  â†’ admits-decidable-membership-in-compact-scott-opens x holds
  â†’ is-sharp x holds
 admits-decidable-membership-in-compact-scott-opens-implies-is-sharp x Ï† c ğ•œ =
  Ï† â†‘Ë¢[ (c , ğ•œ) ] (principal-filter-is-compactâ‚€ c ğ•œ)

 characterization-of-sharp-elements
  : (x : âŸ¨ ğ““ âŸ©âˆ™)
  â†’ (admits-decidable-membership-in-compact-scott-opens x â‡” is-sharp x) holds
 characterization-of-sharp-elements x = â€  , â€¡
  where
   â€  = admits-decidable-membership-in-compact-scott-opens-implies-is-sharp x
   â€¡ = sharp-implies-admits-decidable-membership-in-compact-scott-opens x

\end{code}

\section{The equivalence}

We now start constructing an equivalence between the type
`Spectral-Point Scottâ¦…ğ““â¦†` and the type `â™¯ğ““`.

This equivalence consists of the maps:

  1. `ğ“…ğ“‰[_] : â™¯ğ““ â†’ Spectral-Point Scottâ¦…ğ““â¦†`, and
  2. `sharp : Spectral-Point Scottâ¦…ğ““â¦† â†’ â™¯ğ““`.

We now construct these maps in this order.

\subsection{Definition of the map `ğ“…ğ“‰`}

We follow our usual convention of distinguishing the preliminary version of the
construction of interest using the subscript `â‚€`, which we then package up with
the proof that it satisfies some property.

\begin{code}

 ptâ‚€[_] : âŸ¨ ğ““ âŸ©âˆ™ â†’ âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ© â†’ Î© ğ“¤
 ptâ‚€[_] x U = x âˆˆâ‚› U

 open FrameHomomorphismProperties (ğ’ª Scottâ¦…ğ““â¦†) (ğŸ-ğ”½ğ•£ğ• pe)
 open FrameHomomorphisms

 pt[_] : â™¯ğ““ â†’ Point Scottâ¦…ğ““â¦†
 pt[_] ğ“@(x , ğ•¤) = ptâ‚€[ x ] , â€ 
  where
   â€¡ : preserves-joins (ğ’ª Scottâ¦…ğ““â¦†) (ğŸ-ğ”½ğ•£ğ• pe) ptâ‚€[ x ] holds
   â€¡ S = â‹[ ğŸ-ğ”½ğ•£ğ• pe ]-upper â… ptâ‚€[ x ] y âˆ£ y Îµ S â†
       , â‹[ ğŸ-ğ”½ğ•£ğ• pe ]-least â… ptâ‚€[ x ] y âˆ£ y Îµ S â†

   â€  : is-a-frame-homomorphism (ğ’ª Scottâ¦…ğ““â¦†) (ğŸ-ğ”½ğ•£ğ• pe) ptâ‚€[ x ] holds
   â€  = refl , (Î» _ _ â†’ refl) , â€¡

 open BottomLemma ğ““ ğ•’ hl

\end{code}

Given any sharp element `ğ“`, the point `pt ğ“` is a spectral map.

\begin{code}

 pt-is-spectral : (ğ“ : â™¯ğ““) â†’ is-spectral-map Scottâ¦…ğ““â¦† (ğŸLoc pe) pt[ ğ“ ] holds
 pt-is-spectral ğ“@(x , ğ“ˆğ’½) ğ’¦@(K , Ïƒ) ğ•œ = decidable-implies-compact pe (x âˆˆâ‚› ğ’¦) â€ 
  where
   â€  : is-decidableâ‚š (x âˆˆâ‚› (K , Ïƒ)) holds
   â€  = sharp-implies-admits-decidable-membership-in-compact-scott-opens x ğ“ˆğ’½ ğ’¦ ğ•œ

 open Notion-Of-Spectral-Point pe

\end{code}

We package `pt[_]` up with this proof of spectrality and denote it `ğ“…ğ“‰[_]`.

\begin{code}

 ğ“…ğ“‰[_] : â™¯ğ““ â†’ Spectral-Point Scottâ¦…ğ““â¦†
 ğ“…ğ“‰[_] ğ“ = to-spectral-point Scottâ¦…ğ““â¦† â„±
  where
   â„± : Spectral-Map (ğŸLoc pe) Scottâ¦…ğ““â¦†
   â„± = pt[ ğ“ ] , pt-is-spectral ğ“

\end{code}

\subsection{Definition of the map `sharp`}

We now define the map `sharp` going in the opposite direction.

\begin{code}

 sharpâ‚€ : Point Scottâ¦…ğ““â¦† â†’ âŸ¨ ğ““ âŸ©âˆ™
 sharpâ‚€ â„± = âˆ ğ““ (ğ’¦-in-point-is-directed â„±)

\end{code}

The following lemma says if `sharp(â„±) âˆˆ ğ”˜` then `U âˆˆ â„±`, for every point `â„±` and
every Scott open `ğ”˜`.

\begin{code}

 open PropertiesAlgebraic ğ““ ğ•’ hiding (is-compactâ‚š)

 sharp-in-scott-open-implies-in-point
  : (ğ”˜ : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©)
  â†’ (â„±@(F , _) : Point Scottâ¦…ğ““â¦†)
  â†’ (sharpâ‚€ â„± âˆˆâ‚› ğ”˜ â‡’ F ğ”˜) holds
 sharp-in-scott-open-implies-in-point ğ”˜ â„±@(F , ğ’½) = â€ 
  where
   open ğ’ªâ‚›á´¿ (to-ğ’ªâ‚›á´¿ ğ”˜)

   â€  : (sharpâ‚€ â„± âˆˆâ‚› ğ”˜ â‡’ F ğ”˜) holds
   â€  p = âˆ¥âˆ¥-rec (holds-is-prop (F ğ”˜)) â€¡ Î³
    where
     â€¡ : Î£ (i , _) ê‰ index (ğ’¦-in-point â„±) , ((Bğ““ [ i ]) âˆˆâ‚› ğ”˜) holds
       â†’ F ğ”˜ holds
     â€¡ ((a , b) , c) = frame-morphisms-are-monotonic F ğ’½ (â†‘Ë¢[ Î²â‚– a ] , ğ”˜) q b
      where
       q : (â†‘Ë¢[ Î²â‚– a ] â‰¤[ poset-of (ğ’ª Scottâ¦…ğ““â¦†) ] ğ”˜) holds
       q x = pred-is-upwards-closed (Bğ““ [ a ]) (Bğ““ [ x ]) c

     Î³ : âˆ¥ Î£ (i , _) ê‰ index (ğ’¦-in-point â„±) , ((Bğ““ [ i ]) âˆˆâ‚› ğ”˜) holds âˆ¥
     Î³ = pred-is-inaccessible-by-dir-joins (ğ’¦-in-pointâ†‘ â„±) p

\end{code}

As an immediate special case of this lemma, we obtain the following.

\begin{code}

 below-sharp-implies-in-point
  : (â„±@(F , _) : Point Scottâ¦…ğ““â¦†)
  â†’ (c : âŸ¨ ğ““ âŸ©âˆ™)
  â†’ (ğ•œ : is-compact ğ““ c)
  â†’ c âŠ‘âŸ¨ ğ““ âŸ© sharpâ‚€ â„±
  â†’ F â†‘Ë¢[ c , ğ•œ ] holds
 below-sharp-implies-in-point â„±@(F , ğ’½) c ğ•œ =
  sharp-in-scott-open-implies-in-point â†‘Ë¢[ ğ”  ] â„±
   where
    ğ”  = (c , ğ•œ)

\end{code}

The converse of this special case also holds. In fact, the converse holds
for _all_ compact Scott opens.

\begin{code}

 in-point-implies-contains-sharpâ‹†
  : (ks : List (index Bğ““))
  â†’ (â„±@(F , _) : Point Scottâ¦…ğ““â¦†)
  â†’ (F (ğœ¸ ks) â‡’ sharpâ‚€ â„± âˆˆâ‚› ğœ¸ ks) holds
 in-point-implies-contains-sharpâ‹† [] â„±@(F , _) p = ğŸ˜-elim â… 
  where
   Ï† : F ğŸ[ ğ’ª Scottâ¦…ğ““â¦† ] holds
   Ï† = transport (Î» - â†’ (F -) holds) (ğœ¸-equal-to-ğœ¸â‚ []) p

   â…¡ : ğŸ[ ğŸ-ğ”½ğ•£ğ• pe ] holds
   â…¡ = transport _holds (frame-homomorphisms-preserve-bottom â„±) Ï†

   â…  : âŠ¥â‚š holds
   â…  = transport (Î» - â†’ - holds) (ğŸ-is-âŠ¥ pe â»Â¹) â…¡

 in-point-implies-contains-sharpâ‹† (k âˆ· ks) â„±@(F , _) p =
  âˆ¥âˆ¥-rec (holds-is-prop ((sharpâ‚€ â„± âˆˆâ‚› ğœ¸ (k âˆ· ks)))) â€¡ (transport _holds â™  p)
   where
    â™  : F (ğœ¸ (k âˆ· ks)) ï¼ F â†‘áµ[ k ] âˆ¨ F (ğœ¸ ks)
    â™  = F (ğœ¸ (k âˆ· ks))                     ï¼âŸ¨ â…  âŸ©
        F (ğœ¸â‚ (k âˆ· ks))                    ï¼âŸ¨ â…¡ âŸ©
        F â†‘áµ[ k ] âˆ¨[ ğŸ-ğ”½ğ•£ğ• pe ] F (ğœ¸â‚ ks)  ï¼âŸ¨ â…¢ âŸ©
        F â†‘áµ[ k ] âˆ¨[ ğŸ-ğ”½ğ•£ğ• pe ] F (ğœ¸ ks)   ï¼âŸ¨ â…£ âŸ©
        F â†‘áµ[ k ] âˆ¨ F (ğœ¸ ks)               âˆ
         where
          â…  = ap F (ğœ¸-equal-to-ğœ¸â‚ (k âˆ· ks))
          â…¡ = frame-homomorphisms-preserve-binary-joins â„± _ _
          â…¢ = ap (Î» - â†’ F â†‘áµ[ k ] âˆ¨[ ğŸ-ğ”½ğ•£ğ• pe ] F -) (ğœ¸-equal-to-ğœ¸â‚ ks â»Â¹)
          â…£ = binary-join-is-disjunction pe (F â†‘áµ[ k ]) (F (ğœ¸ ks))

    â€¡ : F â†‘áµ[ k ] holds + F (ğœ¸ ks) holds â†’ (sharpâ‚€ â„± âˆˆâ‚› ğœ¸ (k âˆ· ks)) holds
    â€¡ (inl p) = âˆ£ inl (âˆ-is-upperbound ğ““ (ğ’¦-in-point-is-directed â„±) (k , p)) âˆ£
    â€¡ (inr q) = âˆ£ inr (in-point-implies-contains-sharpâ‹† ks â„± q) âˆ£

\end{code}

We can reformulate this more concisely to say the same thing for any compact
Scott open `K` since a Scott open is compact iff it is a finite join of
principal filters on compact opens.

\begin{code}

 in-point-implies-contains-sharp
  : (â„±@(F , _) : Point Scottâ¦…ğ““â¦†)
  â†’ (K : âŸ¨ Ïƒâ¦…ğ““â¦† âŸ©)
  â†’ (ğ•œ : is-compact-open Scottâ¦…ğ““â¦† K holds)
  â†’ (F K â‡’ sharpâ‚€ â„± âˆˆâ‚› K) holds
 in-point-implies-contains-sharp â„±@(F , Ïˆ) K ğ•œ Ï‡ =
  âˆ¥âˆ¥-rec (holds-is-prop (sharpâ‚€ â„± âˆˆâ‚› K)) â€  Î³
   where
    â„¬â†‘ : directed-basisá´° (ğ’ª Scottâ¦…ğ““â¦†)
    â„¬â†‘ = spectralá´°-implies-directed-basisá´° Scottâ¦…ğ““â¦† Ïƒá´°

    Î³ : is-basic Scottâ¦…ğ““â¦† K (spectralá´°-implies-directed-basisá´° Scottâ¦…ğ““â¦† Ïƒá´°) holds
    Î³ = compact-opens-are-basic Scottâ¦…ğ““â¦† â„¬â†‘ K ğ•œ

    â€  : Î£ i ê‰ BÏƒ , Î²Ïƒ i ï¼ K â†’ (sharpâ‚€ â„± âˆˆâ‚› K) holds
    â€  (i , p) = transport (Î» - â†’ (sharpâ‚€ â„± âˆˆâ‚› -) holds) p â€¡
     where
      Î¼ : F (ğœ¸ i) holds
      Î¼ = transport (Î» - â†’ F - holds) (p â»Â¹) Ï‡

      â€¡ : (sharpâ‚€ (F , Ïˆ) âˆˆâ‚› Î²Ïƒ i) holds
      â€¡ = in-point-implies-contains-sharpâ‹† i â„± Î¼

\end{code}

We now prove that the map `sharpâ‚€` always gives sharp elements.

\begin{code}

 sharpâ‚€-gives-sharp-elements : (F : Point Scottâ¦…ğ““â¦†)
                             â†’ is-spectral-map Scottâ¦…ğ““â¦† (ğŸLoc pe) F holds
                             â†’ is-sharp (sharpâ‚€ F) holds
 sharpâ‚€-gives-sharp-elements â„±@(F , _) Ïƒ c ğ•œ = cases caseâ‚ caseâ‚‚ Î³
  where
   Ï‡ : is-compact-open (ğŸLoc pe) (F â†‘Ë¢[ c , ğ•œ ]) holds
   Ï‡ = Ïƒ â†‘Ë¢[ c , ğ•œ ] (principal-filter-is-compactâ‚€ c ğ•œ )

   Î³ : is-decidableâ‚š (F â†‘Ë¢[ c , ğ•œ ]) holds
   Î³ = compact-implies-boolean pe (F â†‘Ë¢[ c , ğ•œ ]) Ï‡

   Îº : is-compact-open Scottâ¦…ğ““â¦† â†‘Ë¢[ c , ğ•œ ] holds
   Îº = principal-filter-is-compactâ‚€ c ğ•œ

   caseâ‚ : F â†‘Ë¢[ c , ğ•œ ] holds â†’ is-decidableâ‚š (c âŠ‘ sharpâ‚€ â„±) holds
   caseâ‚ = inl âˆ˜ in-point-implies-contains-sharp â„± â†‘Ë¢[ (c , ğ•œ) ] Îº

   caseâ‚‚ : Â¬ (F â†‘Ë¢[ c , ğ•œ ] holds) â†’ is-decidableâ‚š (c âŠ‘ sharpâ‚€ â„±) holds
   caseâ‚‚ Ï‡ = inr (Ï‡ âˆ˜ below-sharp-implies-in-point â„± c ğ•œ)

\end{code}

We denote by `sharp` the version of `sharpâ‚€` that is packaged up with the proof
that it always gives sharp elements.

\begin{code}

 sharp : Spectral-Point Scottâ¦…ğ““â¦† â†’ â™¯ğ““
 sharp â„± = sharpâ‚€ FÂ· , sharpâ‚€-gives-sharp-elements FÂ· Ïƒ
  where
   open Spectral-Point Scottâ¦…ğ““â¦† â„±
    renaming (point-fn to F; point to FÂ·; point-preserves-compactness to Ïƒ)

\end{code}

\subsection{A useful lemma}

We now prove a lemma which we use when showing that these two maps form an
equivalence.

Given a sharp element `ğ“`, the element `sharp (pt ğ“)` is exactly the join of
the compact approximants of `ğ“`.

\begin{code}

 sharp-equal-to-join-of-covering-family
  : (ğ“ : â™¯ğ““)
  â†’ âˆ ğ““ (â†“á´®â‚›-is-directed â¦… ğ“ â¦†) ï¼ sharpâ‚€ pt[ ğ“ ]
 sharp-equal-to-join-of-covering-family (x , ğ•¤) =
  antisymmetry ğ““ (âˆ ğ““ (â†“á´®â‚›-is-directed x)) (â‹ ğ’¦-in-pointâ†‘ pt[ (x , ğ•¤) ]) â€  â€¡
   where
    Î³ : ((i , _) : â†“á´®â‚› x) â†’ (sharpâ‚€ pt[ x , ğ•¤ ] âˆˆâ‚› â†‘Ë¢[ Î²â‚– i ]) holds
    Î³ (i , q) = in-point-implies-contains-sharp
                 pt[ x , ğ•¤ ]
                 â†‘Ë¢[ Î²â‚– i ]
                 (principal-filter-is-compact i)
                 (âŠ‘á´®â‚›-to-âŠ‘á´® q)

    Î´ : is-upperbound
         (underlying-order ğ““)
         (âˆ ğ““ (â†“á´®â‚›-is-directed x))
         (ğ’¦-in-point pt[ x , ğ•¤ ] [_])
    Î´ (i , q) = âˆ-is-upperbound ğ““ (â†“á´®â‚›-is-directed x) (i , âŠ‘á´®-to-âŠ‘á´®â‚› q)

    â€  : (âˆ ğ““ (â†“á´®â‚›-is-directed x)) âŠ‘âŸ¨ ğ““ âŸ© (â‹ ğ’¦-in-pointâ†‘ pt[ (x , ğ•¤) ])
    â€  = âˆ-is-lowerbound-of-upperbounds
         ğ““
         (â†“á´®â‚›-is-directed x)
         (â‹ ğ’¦-in-pointâ†‘ pt[ x , ğ•¤ ])
         Î³

    â€¡ : ((â‹ ğ’¦-in-pointâ†‘ pt[ (x , ğ•¤) ]) âŠ‘âŸ¨ ğ““ âŸ© âˆ ğ““ (â†“á´®â‚›-is-directed x))
    â€¡ = sup-is-lowerbound-of-upperbounds
         (underlying-order ğ““)
         (â‹-is-sup (ğ’¦-in-pointâ†‘ pt[ (x , ğ•¤) ]))
         (âˆ ğ““ (â†“á´®â‚›-is-directed x))
         Î´

\end{code}

\subsection{The equivalence proof}

The fact that `sharp` is a retraction of `ğ“…ğ“‰[_]` follows easily from the lemma
`sharp-equal-to-join-of-covering-family` above.

\begin{code}

 sharp-cancels-pt : (ğ“ : â™¯ğ““) â†’ sharp ğ“…ğ“‰[ ğ“ ] ï¼ ğ“
 sharp-cancels-pt ğ“ = to-sharp-ï¼ (sharp ğ“…ğ“‰[ ğ“ ]) ğ“ â€ 
  where
   â€  : â¦… sharp ğ“…ğ“‰[ ğ“ ] â¦† ï¼ â¦… ğ“ â¦†
   â€  = â¦… sharp ğ“…ğ“‰[ ğ“ ] â¦†           ï¼âŸ¨ â…  âŸ©
       âˆ ğ““ (â†“á´®â‚›-is-directed â¦… ğ“ â¦†) ï¼âŸ¨ â…¡ âŸ©
       â¦… ğ“ â¦†                       âˆ
        where
         â…  = sharp-equal-to-join-of-covering-family ğ“ â»Â¹
         â…¡ = â†“á´®â‚›-âˆ-ï¼ â¦… ğ“ â¦†

\end{code}

The map `ğ“…ğ“‰[_]` is a retraction of the map `sharp`.

\begin{code}

 pt-cancels-sharp : (â„± : Spectral-Point Scottâ¦…ğ““â¦†) â†’ ğ“…ğ“‰[ sharp â„± ] ï¼ â„±
 pt-cancels-sharp â„± =
  to-spectral-point-ï¼ Scottâ¦…ğ““â¦† ğ“…ğ“‰[ sharp â„± ] â„± (dfunext fe â€ )
   where
    open Spectral-Point Scottâ¦…ğ““â¦† â„± renaming (point-fn to F; point to â„±â‚€)

    â€  : (ğ”˜ : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©) â†’ (sharpâ‚€ â„±â‚€ âˆˆâ‚› ğ”˜) ï¼ F ğ”˜
    â€  ğ”˜@(U , s) = transport (Î» - â†’ (sharpâ‚€ â„±â‚€ âˆˆâ‚› -) ï¼ F -) (q â»Â¹) â€¡
     where
      S : Fam ğ“¤ âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©
      S = covering-familyâ‚› Scottâ¦…ğ““â¦† Ïƒá´° ğ”˜

      q : ğ”˜ ï¼ â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S
      q = basisâ‚›-covers-do-cover-eq Scottâ¦…ğ““â¦† Ïƒá´° ğ”˜

      â€¡â‚ : cofinal-in (ğŸ-ğ”½ğ•£ğ• pe) â… sharpâ‚€ â„±â‚€ âˆˆâ‚› ğ”˜ âˆ£ ğ”˜ Îµ S â† â… F ğ”˜ âˆ£ ğ”˜ Îµ S â† holds
      â€¡â‚ k = âˆ£ k , sharp-in-scott-open-implies-in-point (S [ k ]) â„±â‚€ âˆ£

      â€¡â‚‚ : cofinal-in (ğŸ-ğ”½ğ•£ğ• pe) â… F ğ”˜ âˆ£ ğ”˜ Îµ S â† â… sharpâ‚€ â„±â‚€ âˆˆâ‚› ğ”˜ âˆ£ ğ”˜ Îµ S â† holds
      â€¡â‚‚ (ks , p) = âˆ£ (ks , p) , in-point-implies-contains-sharpâ‹† ks â„±â‚€ âˆ£

      â€¡ : sharpâ‚€ â„±â‚€ âˆˆâ‚› (â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S) ï¼ F (â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S)
      â€¡ = sharpâ‚€ â„±â‚€ âˆˆâ‚› (â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S)               ï¼âŸ¨ refl âŸ©
          ptâ‚€[ sharpâ‚€ â„±â‚€ ] (â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S)           ï¼âŸ¨ â…     âŸ©
          â‹[ ğŸ-ğ”½ğ•£ğ• pe ] â… ptâ‚€[ sharpâ‚€ â„±â‚€ ] ğ”˜ âˆ£ ğ”˜  Îµ S â†  ï¼âŸ¨ refl âŸ©
          â‹[ ğŸ-ğ”½ğ•£ğ• pe ] â… sharpâ‚€ â„±â‚€ âˆˆâ‚› ğ”˜ âˆ£ ğ”˜ Îµ S â†       ï¼âŸ¨ â…¡    âŸ©
          â‹[ ğŸ-ğ”½ğ•£ğ• pe ] â… F ğ”˜ âˆ£ ğ”˜ Îµ S â†                  ï¼âŸ¨ â…¢    âŸ©
          F (â‹[ ğ’ª Scottâ¦…ğ““â¦† ] S)                          âˆ
           where
            â…  = frame-homomorphisms-preserve-all-joinsâ€²
                 (ğ’ª Scottâ¦…ğ““â¦†)
                 (ğŸ-ğ”½ğ•£ğ• pe)
                 pt[ sharp â„± ]
                 S
            â…¡ = bicofinal-implies-same-join
                 (ğŸ-ğ”½ğ•£ğ• pe)
                 â… sharpâ‚€ â„±â‚€ âˆˆâ‚› ğ”˜ âˆ£ ğ”˜ Îµ S â†
                 â… F ğ”˜ âˆ£ ğ”˜ Îµ S â†
                 â€¡â‚
                 â€¡â‚‚
            â…¢ = frame-homomorphisms-preserve-all-joinsâ€²
                 (ğ’ª Scottâ¦…ğ““â¦†)
                 (ğŸ-ğ”½ğ•£ğ• pe)
                 â„±â‚€
                 S â»Â¹

\end{code}

Finally, we conclude this development by giving the equivalence between the
sharp elements and the spectral points.

\begin{code}

 â™¯ğ““-equivalent-to-spectral-points-of-Scottâ¦…ğ““â¦† : â™¯ğ““ â‰ƒ Spectral-Point Scottâ¦…ğ““â¦†
 â™¯ğ““-equivalent-to-spectral-points-of-Scottâ¦…ğ““â¦† = ğ“…ğ“‰[_] , qinvs-are-equivs ğ“…ğ“‰[_] â€ 
  where
   â€  : qinv ğ“…ğ“‰[_]
   â€  = sharp , sharp-cancels-pt , pt-cancels-sharp

\end{code}

\section{Acknowledgements}

I am grateful to Tom de Jong for his comments on a write-up of the proof
formalized in this module.

\section{References}

- [1]: Tom de Jong. *Apartness, sharp elements, and the Scott topology of
  domains*.

  Mathematical Structures in Computer Science, vol. 33, no. 7,
  pp 573-604, August 2023. doi:10.1017/S0960129523000282

  https://doi.org/10.1017/S0960129523000282
