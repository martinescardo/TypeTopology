---
title:          Equivalence of sharp elements and the points of patch
author:         Ayberk Tosun
date-started:   2024-08-04
date-completed: 2024-08-13
---

Joint work with MartÃ­n EscardÃ³.

We prove that the sharp elements of a Scott domain `ğ““` are in bijection with the
points of `Patch(Scott(ğ““))`.

\begin{code}[hide]

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.List hiding ([_])
open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc
open import UF.Size
open import UF.Subsingletons
open import UF.UA-FunExt
open import UF.Univalence

module Locales.LawsonLocale.PointsOfPatch
        (ğ“¤  : Universe)
        (ua : Univalence)
        (pt : propositional-truncations-exist)
        (sr : Set-Replacement pt)
       where

private
 fe : Fun-Ext
 fe {ğ“¤} {ğ“¥} = univalence-gives-funext' ğ“¤ ğ“¥ (ua ğ“¤) (ua (ğ“¤ âŠ” ğ“¥))

 pe : Prop-Ext
 pe {ğ“¤} = univalence-gives-propext (ua ğ“¤)

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.CompactBasis pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.ScottDomain pt fe ğ“¤
open import DomainTheory.Basics.Dcpo pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.Topology.ScottTopology pt fe ğ“¤
open import DomainTheory.Topology.ScottTopologyProperties pt fe ğ“¤
open import Locales.Clopen pt fe sr
open import Locales.CompactRegular pt fe using (clopens-are-compact-in-compact-frames)
open import Locales.Compactness pt fe hiding (is-compact)
open import Locales.ContinuousMap.Definition pt fe
open import Locales.ContinuousMap.FrameHomomorphism-Definition pt fe
open import Locales.ContinuousMap.FrameHomomorphism-Properties pt fe
open import Locales.Frame pt fe
open import Locales.InitialFrame pt fe hiding (_âŠ‘_)
open import Locales.LawsonLocale.CompactElementsOfPoint ğ“¤ fe pe pt sr
open import Locales.LawsonLocale.SharpElementsCoincideWithSpectralPoints ğ“¤ ua pt sr
open import Locales.PatchLocale pt fe sr
open import Locales.PatchProperties pt fe sr
open import Locales.PerfectMaps pt fe
open import Locales.Point.Definition pt fe
open import Locales.Point.SpectralPoint-Definition pt fe pe
open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.Properties pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfScottDomains pt fe sr ğ“¤
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralMap pt fe
open import Locales.Spectrality.SpectralityOfOmega pt fe sr ğ“¤ pe
open import Locales.Stone pt fe sr
open import Locales.StoneImpliesSpectral pt fe sr
open import Locales.TerminalLocale.Properties pt fe sr
open import Locales.UniversalPropertyOfPatch pt fe sr
open import Locales.ZeroDimensionality pt fe sr
open import NotionsOfDecidability.Decidable
open import NotionsOfDecidability.SemiDecidable fe pe pt
open import Slice.Family
open import UF.Base
open import UF.Equiv
open import UF.Logic
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier renaming (âŠ¥ to âŠ¥â‚š)

open AllCombinators pt fe
open DefinitionOfScottDomain
open Locale
open PerfectMaps
open PropositionalTruncation pt hiding (_âˆ¨_)

\end{code}

In the module below, we show that points `ğŸ â†’ Patch(Scott(ğ““))` are in
bijection with spectral points `ğŸ â†’ Scott(ğ““)`. This is done by constructing
an equivalence
```
Point(Patch(Scott(ğ““))) â‰ƒ Spectral-Point(Patch(Scott(ğ““))) â‰ƒ Spectral-Point(Scott(ğ““))
```

We then use this equivalence to show that the sharp elements of `ğ““` are in
bijection with `Point(Patch(Scott(ğ““)))`.

\begin{code}

module sharp-elements-and-points-of-patch
        (ğ““  : DCPO {ğ“¤ âº} {ğ“¤})
        (hl : has-least (underlying-order ğ““))
        (sd : is-scott-domain ğ““ holds)
        (dc : decidability-condition ğ““)
       where

 zd : zero-dimensionalá´° {ğ“¤ âº} (ğ’ª (ğŸLoc pe))
 zd = ğŸ-zero-dimensionalá´° pe

 open SpectralScottLocaleConstructionâ‚‚ ğ““ ua hl sd dc pe renaming (Ïƒâ¦…ğ““â¦† to Scottâ¦…ğ““â¦†)

\end{code}

Some more module opening bureaucracy.

\begin{code}

 open ClosedNucleus Scottâ¦…ğ““â¦† scott-locale-is-spectral
 open ContinuousMaps
 open Epsilon Scottâ¦…ğ““â¦† scott-locale-spectralá´°
 open Notion-Of-Spectral-Point
 open PatchStoneá´° Scottâ¦…ğ““â¦† scott-locale-spectralá´°
 open Preliminaries
 open SmallPatchConstruction Scottâ¦…ğ““â¦† scott-locale-spectralá´°
 open UniversalProperty Scottâ¦…ğ““â¦† (ğŸLoc pe) scott-locale-spectralá´° zd ğŸFrm-is-compact

\end{code}

We define an alias for Patch(Scott(ğ““)).

\begin{code}

 Patchâ¦…Scottâ¦…ğ““â¦†â¦† : Locale (ğ“¤ âº) ğ“¤ ğ“¤
 Patchâ¦…Scottâ¦…ğ““â¦†â¦† = SmallPatch

 Patchâ¦…Scottâ¦…ğ““â¦†â¦†-stoneá´° : stoneá´° Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 Patchâ¦…Scottâ¦…ğ““â¦†â¦†-stoneá´° = patchâ‚›-is-compact , patchâ‚›-zero-dimensionalá´°

\end{code}

We now instantiate the universal property of `Patchâ¦…Scottâ¦…ğ““â¦†â¦†` to points `ğŸ â†’
Scottâ¦…ğ““â¦†`.

\begin{code}

 patch-ump : (ğ“…@(p , _) : ğŸLoc pe â”€câ†’ Scottâ¦…ğ““â¦†)
           â†’ is-spectral-map Scottâ¦…ğ““â¦† (ğŸLoc pe) ğ“… holds
           â†’ let
              open ContinuousMapNotation (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦†
             in
              âˆƒ! ğ’»â» ê‰ ğŸLoc pe â”€câ†’ Patchâ¦…Scottâ¦…ğ““â¦†â¦† ,
               ((U : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©) â†’ p U  ï¼ ğ’»â» â‹†âˆ™ â€˜ U â€™ )
 patch-ump = ump-of-patch
              Scottâ¦…ğ““â¦†
              scott-locale-is-spectral
              scott-locale-has-small-ğ’¦
              (ğŸLoc pe)
              (ğŸ-is-stone pe)

\end{code}

This universal property immediately gives us a map from the spectral points of
`Scottâ¦…ğ““â¦†` into the spectral points of `Patchâ¦…Scottâ¦…ğ““â¦†â¦†`. We call this map
`to-patch-point`.

\begin{code}

 to-patch-point : Spectral-Point Scottâ¦…ğ““â¦† â†’ Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 to-patch-point â„± = to-spectral-point Patchâ¦…Scottâ¦…ğ““â¦†â¦† (ğ“… , â€ )
  where
   open Spectral-Point â„± renaming (point to F; point-preserves-compactness to ğ•¤)
   open continuous-maps-of-stone-locales (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦†

   ğ“… : ğŸLoc pe â”€câ†’ Patchâ¦…Scottâ¦…ğ““â¦†â¦†
   ğ“… = âˆƒ!-witness (patch-ump F ğ•¤)

   â€  : is-spectral-map Patchâ¦…Scottâ¦…ğ““â¦†â¦† (ğŸLoc pe) ğ“… holds
   â€  = continuous-maps-between-stone-locales-are-spectral
        (ğŸ-stoneá´° pe)
        Patchâ¦…Scottâ¦…ğ““â¦†â¦†-stoneá´°
        ğ“…

\end{code}

Recall that the map `Ïµ` denotes the continuous map given by the frame
homomorphism `x â†¦ â€˜ x â€™`.

\begin{code}

 Ïµ-is-a-spectral-map : is-spectral-map Scottâ¦…ğ““â¦† Patchâ¦…Scottâ¦…ğ““â¦†â¦† Ïµ holds
 Ïµ-is-a-spectral-map =
  perfect-maps-are-spectral
   Patchâ¦…Scottâ¦…ğ““â¦†â¦†
   Scottâ¦…ğ““â¦†
   âˆ£ spectralá´°-implies-basisá´° Scottâ¦…ğ““â¦† scott-locale-spectralá´° âˆ£
   Ïµ
   Ïµ-is-a-perfect-map

\end{code}

TODO: The proof above should be placed in a more appropriate place.

We now define the inverse of `to-patch-point`: given a spectral point `ğŸ â†’
Patchâ¦…Scottâ¦…ğ““â¦†â¦†`, we can compose this with `Ïµ : Patchâ¦…Scottâ¦…ğ““â¦†â¦† â†’ Scottâ¦…ğ““â¦†` to
obtain a spectral point `ğŸ â†’ Scottâ¦…ğ““â¦†`. We call this map `to-scott-point`.

\begin{code}

 to-scott-point : Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦† â†’ Spectral-Point Scottâ¦…ğ““â¦†
 to-scott-point ğ“… = to-spectral-point Scottâ¦…ğ““â¦† (ğ“…â‚€ , ğ•¤)
  where
   open Spectral-Point ğ“… renaming (point to ğ“…â‹†)

   ğ“…â‚€ : ğŸLoc pe â”€câ†’ Scottâ¦…ğ““â¦†
   ğ“…â‚€ = cont-comp (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦† Scottâ¦…ğ““â¦† Ïµ ğ“…â‹†

   ğ•¤ : is-spectral-map Scottâ¦…ğ““â¦† (ğŸLoc pe) ğ“…â‚€ holds
   ğ•¤ K Îº = point-preserves-compactness â€˜ K â€™ (Ïµ-is-a-spectral-map K Îº)

\end{code}

We now proceed to show that these maps form a section-retraction pair.

The fact that `to-scott-point` is a retraction of `to-patch-point` follows
directly from the existence part of the universal property.

\begin{code}

 to-scott-point-cancels-to-patch-point : to-scott-point âˆ˜ to-patch-point âˆ¼ id
 to-scott-point-cancels-to-patch-point ğ“… =
  to-spectral-point-ï¼ Scottâ¦…ğ““â¦† (to-scott-point (to-patch-point ğ“…)) ğ“… â€ 
   where
    open Spectral-Point ğ“…
     using ()
     renaming (point to ğ“…â‹†; point-fn to pâ‹†; point-preserves-compactness to ğ•¤)

    ğ“…â€² : Spectral-Point Scottâ¦…ğ““â¦†
    ğ“…â€² = to-scott-point (to-patch-point ğ“…)

    open Spectral-Point ğ“…â€² using () renaming (point to ğ“…â€²â‹†; point-fn to pâ€²â‹†)

    â€¡ : pâ€²â‹† âˆ¼ pâ‹†
    â€¡ U = prâ‚‚ (description (patch-ump ğ“…â‹† ğ•¤)) U â»Â¹

    â€  : pâ€²â‹† ï¼ pâ‹†
    â€  = dfunext fe â€¡

\end{code}

The fact that it is a section follows from the uniqueness.

\begin{code}

 to-patch-point-cancels-to-scott-point : to-patch-point âˆ˜ to-scott-point âˆ¼ id
 to-patch-point-cancels-to-scott-point ğ“… =
  to-spectral-point-ï¼' Patchâ¦…Scottâ¦…ğ““â¦†â¦† ğ“…â€² ğ“… â€ 
   where
    open Spectral-Point ğ“…
     using ()
     renaming (point to ğ“…â‹†; point-fn to pâ‹†; point-preserves-compactness to ğ•¤)
    open ContinuousMapNotation (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦†

    ğ“…â€² : Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
    ğ“…â€² = to-patch-point (to-scott-point ğ“…)

    ğ“…â‚€ : ğŸLoc pe â”€câ†’ Scottâ¦…ğ““â¦†
    ğ“…â‚€ = cont-comp (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦† Scottâ¦…ğ““â¦† Ïµ ğ“…â‹†

    pâ‚€ : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ© â†’ âŸ¨ ğ’ª (ğŸLoc pe) âŸ©
    pâ‚€ = prâ‚ ğ“…â‚€

    ğ•¤â‚€ : is-spectral-map Scottâ¦…ğ““â¦† (ğŸLoc pe) ğ“…â‚€ holds
    ğ•¤â‚€ K Îº = ğ•¤ â€˜ K â€™ (Ïµ-is-a-spectral-map K Îº)

    open Spectral-Point ğ“…â€² using () renaming (point to ğ“…â€²â‹†; point-fn to pâ€²â‹†)

    Ï… : âˆƒ! ğ“…â‚€â» ê‰ ğŸLoc pe â”€câ†’ Patchâ¦…Scottâ¦…ğ““â¦†â¦† ,
         ((U : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©) â†’ pâ‚€ U ï¼ ğ“…â‚€â» â‹†âˆ™ â€˜ U â€™)
    Ï… = patch-ump ğ“…â‚€ ğ•¤â‚€

    r : (U : âŸ¨ ğ’ª Scottâ¦…ğ““â¦† âŸ©) â†’ pâ‚€ U ï¼ pâ‹† â€˜ U â€™
    r = Î» _ â†’ refl

    â€  : ğ“…â€²â‹† ï¼ ğ“…â‹†
    â€  = prâ‚ (from-Î£-ï¼ (âˆƒ!-uniqueness Ï… ğ“…â‹† r))

\end{code}

We package these up into a proof that `to-patch-point` and `to-scott-point` form
an equivalence.

\begin{code}

 to-patch-point-is-invertible : invertible to-patch-point
 to-patch-point-is-invertible = to-scott-point , â€  , â€¡
  where
   â€  : to-scott-point âˆ˜ to-patch-point âˆ¼ id
   â€  = to-scott-point-cancels-to-patch-point

   â€¡ : to-patch-point âˆ˜ to-scott-point âˆ¼ id
   â€¡ = to-patch-point-cancels-to-scott-point

 spectral-points-of-patch-are-equivalent-to-spectral-points-of-scott
  : Spectral-Point Scottâ¦…ğ““â¦† â‰ƒ Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 spectral-points-of-patch-are-equivalent-to-spectral-points-of-scott =
  to-patch-point , qinvs-are-equivs to-patch-point to-patch-point-is-invertible

\end{code}

We now proceed to show that `Pointâ¦…Patchâ¦…Scottâ¦…ğ““â¦†â¦†â¦†` is equivalent to
`Spectral-Pointâ¦…Patchâ¦…Scottâ¦…ğ““â¦†â¦†â¦†`, since all points of `Patchâ¦…Scottâ¦…ğ““â¦†â¦†` are
_automatically_ spectral.

\begin{code}

 forget-spectrality : Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦† â†’ Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 forget-spectrality = Spectral-Point.point

 to-spectral-point-of-patch : Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
                            â†’ Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 to-spectral-point-of-patch ğ“… = to-spectral-point Patchâ¦…Scottâ¦…ğ““â¦†â¦† (ğ“… , ğ•¤)
  where
   open continuous-maps-of-stone-locales (ğŸLoc pe) Patchâ¦…Scottâ¦…ğ““â¦†â¦†

   ğ•¤ : is-spectral-map Patchâ¦…Scottâ¦…ğ““â¦†â¦† (ğŸLoc pe) ğ“… holds
   ğ•¤ = continuous-maps-between-stone-locales-are-spectral
        (ğŸ-stoneá´° pe)
        Patchâ¦…Scottâ¦…ğ““â¦†â¦†-stoneá´°
        ğ“…

 open FrameHomomorphismProperties

 forget-spectrality-is-invertible : invertible forget-spectrality
 forget-spectrality-is-invertible = to-spectral-point-of-patch , â€  , â€¡
  where
   â€  : to-spectral-point-of-patch âˆ˜ forget-spectrality âˆ¼ id
   â€  ğ“…â‚› = to-spectral-point-ï¼ Patchâ¦…Scottâ¦…ğ““â¦†â¦† ğ“… ğ“…â‚› refl
    where
     ğ“… : Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
     ğ“… = to-spectral-point-of-patch (forget-spectrality ğ“…â‚›)

   â€¡ : forget-spectrality âˆ˜ to-spectral-point-of-patch  âˆ¼ id
   â€¡ ğ“… =
    to-frame-homomorphism-ï¼ (ğ’ª Patchâ¦…Scottâ¦…ğ““â¦†â¦†) (ğ’ª (ğŸLoc pe)) ğ“… ğ“…â€² (Î» _ â†’ refl)
     where
      ğ“…â€² : Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
      ğ“…â€² = forget-spectrality (to-spectral-point-of-patch ğ“…)

 spectral-points-of-patch-are-equivalent-to-points-of-patch
  : Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦† â‰ƒ Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 spectral-points-of-patch-are-equivalent-to-points-of-patch =
  forget-spectrality , e
   where
    e : is-equiv forget-spectrality
    e = qinvs-are-equivs forget-spectrality forget-spectrality-is-invertible

\end{code}

We combine these two equivalences to obtain an equivalence between the points of
`Patchâ¦…Scottâ¦…ğ““â¦†â¦†` and spectral points of `Scottâ¦…ğ““â¦†`.

\begin{code}

 points-of-patch-are-spectral-points-of-scott
  : Point Patchâ¦…Scottâ¦…ğ““â¦†â¦† â‰ƒ Spectral-Point Scottâ¦…ğ““â¦†
 points-of-patch-are-spectral-points-of-scott =
  Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†              â‰ƒâŸ¨ â…  âŸ©
  Spectral-Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†     â‰ƒâŸ¨ â…¡ âŸ©
  Spectral-Point Scottâ¦…ğ““â¦†            â– 
   where
    â…  = â‰ƒ-sym spectral-points-of-patch-are-equivalent-to-points-of-patch
    â…¡ = â‰ƒ-sym spectral-points-of-patch-are-equivalent-to-spectral-points-of-scott

\end{code}

Finally, we combine this equivalence with the equivalence between sharp elements
and spectral points.

\begin{code}

 open Sharp-Element-Spectral-Point-Equivalence ğ““ hl sd dc

 points-of-patch-are-the-sharp-elements : â™¯ğ““ â‰ƒ Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†
 points-of-patch-are-the-sharp-elements =
  â™¯ğ““                         â‰ƒâŸ¨ â…  âŸ©
  Spectral-Point Scottâ¦…ğ““â¦†    â‰ƒâŸ¨ â…¡ âŸ©
  Point Patchâ¦…Scottâ¦…ğ““â¦†â¦†      â– 
   where
    â…  = â™¯ğ““-equivalent-to-spectral-points-of-Scottâ¦…ğ““â¦†
    â…¡ = â‰ƒ-sym points-of-patch-are-spectral-points-of-scott

\end{code}
