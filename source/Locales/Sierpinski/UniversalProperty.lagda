--------------------------------------------------------------------------------
title:          Universal property of the SierpiÅ„ski locale
author:         Ayberk Tosun
date-started:   2024-03-06
date-completed: 2024-03-09
--------------------------------------------------------------------------------

In this module, we

  1. define the universal property of SierpiÅ„ski which amounts to the fact that
     it is the free frame on one generator; and
  2. we prove that the Scott locale of the SierpiÅ„ski dcpo satisfies this
     universal property.

This the implementation of a proof sketch communicated to me by MartÃ­n EscardÃ³.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import MLTT.Spartan
open import UF.FunExt
open import UF.PropTrunc
open import UF.Size
open import UF.Subsingletons

module Locales.Sierpinski.UniversalProperty
        (ğ“¤  : Universe)
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (sr : Set-Replacement pt)
       where

open import DomainTheory.Basics.Dcpo pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.Pointed pt fe ğ“¤
open import DomainTheory.Topology.ScottTopology pt fe ğ“¤
open import DomainTheory.Topology.ScottTopologyProperties pt fe
open import Locales.ContinuousMap.Definition pt fe
open import Locales.ContinuousMap.FrameHomomorphism-Definition pt fe
open import Locales.ContinuousMap.FrameHomomorphism-Properties pt fe
open import Locales.DistributiveLattice.Definition fe pt
open import Locales.DistributiveLattice.Ideal pt fe pe
open import Locales.DistributiveLattice.Properties fe pt
open import Locales.Frame pt fe hiding (is-directed)
open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.Properties pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfScottDomains pt fe sr ğ“¤
open import Locales.Sierpinski.Definition ğ“¤ pe pt fe sr
open import Locales.Sierpinski.Properties ğ“¤ pe pt fe sr
open import Locales.SmallBasis pt fe sr
open import MLTT.Fin hiding (ğŸ; ğŸ)
open import MLTT.List hiding ([_])
open import Slice.Family
open import UF.Logic
open import UF.Subsingletons
open import UF.Subsingletons-FunExt
open import UF.SubtypeClassifier

open AllCombinators pt fe renaming (_âˆ§_ to _âˆ§â‚š_; _âˆ¨_ to _âˆ¨â‚š_)
open ContinuousMaps
open DefnOfScottLocale ğ•Šğ““ ğ“¤ pe hiding (âŠ¤â‚›)
open DefnOfScottTopology ğ•Šğ““ ğ“¤
open FrameHomomorphismProperties
open FrameHomomorphisms
open Locale
open PropertiesAlgebraic ğ“¤ ğ•Šğ““ ğ•Šğ““-is-structurally-algebraic
open PropositionalTruncation pt hiding (_âˆ¨_)
open ScottLocaleConstruction ğ•Šğ““ hscb pe


\end{code}

\section{The definition of the universal property}

Given a locale S with a chosen open truth : ğ’ª(S), S satisfies the universal
property of the SierpiÅ„ski locale if

    for any locale X, any open U : ğ’ª(X), there exists a continuous map f : X â†’ S
    unique with the property that f(truth) = U.

In other words, this says that the SierpiÅ„ski locale is the locale whose
defining frame is the free frame on one generator.

\begin{code}

has-the-universal-property-of-sierpinski : (S : Locale (ğ“¤ âº) ğ“¤ ğ“¤)
                                         â†’ âŸ¨ ğ’ª S âŸ©
                                         â†’ ğ“¤ âº âº  Ì‡
has-the-universal-property-of-sierpinski S truth =
 (X : Locale (ğ“¤ âº) ğ“¤ ğ“¤) â†’
  (U : âŸ¨ ğ’ª X âŸ©) â†’
   âˆƒ! (f , _) ê‰ (X â”€câ†’ S) , U ï¼ f truth

\end{code}

\section{The Scott locale of the SierpiÅ„ski dcpo has this universal property}

We denote by `ğ’½` the defining frame homomorphism of the continuous map required
for the universal property.

\begin{code}

universal-property-of-sierpinski : has-the-universal-property-of-sierpinski ğ•Š truth
universal-property-of-sierpinski X U = (ğ’½ , â€ ) , â€¡
 where
  open PosetNotation (poset-of (ğ’ª X))
  open PosetReasoning (poset-of (ğ’ª X))
  open Joins _â‰¤_

\end{code}

We adopt the convention of using ğ”£ğ”¯ğ”ğ”¨ğ”±ğ”²ğ”¯ letters for Scott opens.

The frame homomorphism `h : ğ’ª(ğ•Š) â†’ ğ’ª(X)` is defined as `h(ğ”™) :â‰¡ â‹ (â„±â‚“ ğ”™)` where
`â„±â‚“ ğ”™` denotes the following family.

\begin{code}

  I : âŸ¨ ğ’ª ğ•Š âŸ© â†’ ğ“¤  Ì‡
  I ğ”˜ = (âŠ¤â‚› âˆˆâ‚› ğ”˜) holds + (âŠ¥â‚› âˆˆâ‚› ğ”˜) holds

  Î± : (ğ”™ : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ (âŠ¤â‚› âˆˆâ‚› ğ”™) holds + (âŠ¥â‚› âˆˆâ‚› ğ”™) holds â†’ âŸ¨ ğ’ª X âŸ©
  Î± V (inl _) = U
  Î± V (inr _) = ğŸ[ ğ’ª X ]

  â„±â‚“ : âŸ¨ ğ’ª ğ•Š âŸ© â†’ Fam ğ“¤ âŸ¨ ğ’ª X âŸ©
  â„±â‚“ ğ”™ = (I ğ”™ , Î± ğ”™)

  h : âŸ¨ ğ’ª ğ•Š âŸ© â†’ âŸ¨ ğ’ª X âŸ©
  h ğ”™ = â‹[ ğ’ª X ] â„±â‚“ ğ”™

\end{code}

It is easy to see that this map is monotone.

\begin{code}

  ğ“‚ : is-monotonic (poset-of (ğ’ª ğ•Š)) (poset-of (ğ’ª X)) h holds
  ğ“‚ (Vâ‚ , Vâ‚‚) p = â‹[ ğ’ª X ]-least (I Vâ‚ , Î± Vâ‚) (h Vâ‚‚ , â€ )
   where
    pâ€² : (Vâ‚ âŠ†â‚› Vâ‚‚) holds
    pâ€² = âŠ†â‚–-implies-âŠ†â‚› Vâ‚ Vâ‚‚ p

    â€  : (h Vâ‚‚ is-an-upper-bound-of (I Vâ‚ , Î± Vâ‚)) holds
    â€  (inl Î¼) = U        â‰¤âŸ¨ â‹[ ğ’ª X ]-upper _ (inl (pâ€² âŠ¤â‚› Î¼)) âŸ© h Vâ‚‚ â– 
    â€  (inr Î¼) = ğŸ[ ğ’ª X ] â‰¤âŸ¨ â‹[ ğ’ª X ]-upper _ (inr (pâ€² âŠ¥â‚› Î¼)) âŸ© h Vâ‚‚ â– 

\end{code}

We now prove that it preserves the top element and the binary meets.

\begin{code}

  Ï† : h ğŸ[ ğ’ª ğ•Š ] ï¼ ğŸ[ ğ’ª X ]
  Ï† = only-ğŸ-is-above-ğŸ (ğ’ª X) (h ğŸ[ ğ’ª ğ•Š ]) Î³
   where
    Î³ : (ğŸ[ ğ’ª X ] â‰¤ h ğŸ[ ğ’ª ğ•Š ]) holds
    Î³ = â‹[ ğ’ª X ]-upper ((I ğŸ[ ğ’ª ğ•Š ]) , (Î± ğŸ[ ğ’ª ğ•Š ])) (inr â‹†)

  Ïˆ : preserves-binary-meets (ğ’ª ğ•Š) (ğ’ª X) h holds
  Ïˆ ğ”™ ğ”š = â‰¤-is-antisymmetric (poset-of (ğ’ª X)) Ïˆâ‚ Ïˆâ‚‚
   where
    Ïˆâ‚ : (h (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â‰¤ (h ğ”™ âˆ§[ ğ’ª X ] h ğ”š)) holds
    Ïˆâ‚ = âˆ§[ ğ’ª X ]-greatest
          (h ğ”™)
          (h ğ”š)
          (h (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š))
          (ğ“‚ ((ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) , _) (âˆ§[ ğ’ª ğ•Š ]-lowerâ‚ ğ”™ ğ”š))
          ((ğ“‚ ((ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) , ğ”š) (âˆ§[ ğ’ª ğ•Š ]-lowerâ‚‚ ğ”™ ğ”š)))

    Ï… : (h (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)
          is-an-upper-bound-of
         â… Î± ğ”™ iâ‚ âˆ§[ ğ’ª X ] Î± ğ”š iâ‚‚ âˆ£ (iâ‚ , iâ‚‚) âˆ¶ I ğ”™ Ã— I ğ”š â†) holds
    Ï… (inl pâ‚ , inl pâ‚‚) =
     Î± ğ”™ (inl pâ‚) âˆ§[ ğ’ª X ] Î± ğ”š (inl pâ‚‚)                     ï¼âŸ¨ refl âŸ©â‚š
     U âˆ§[ ğ’ª X ] U                                           ï¼âŸ¨ â…    âŸ©â‚š
     U                                                      â‰¤âŸ¨ â…¡  âŸ©
     â‹[ ğ’ª X ] â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† â– 
      where
       p : (âŠ¤â‚› âˆˆâ‚› (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)) holds
       p = pâ‚ , pâ‚‚

       â…  = âˆ§[ ğ’ª X ]-is-idempotent U â»Â¹
       â…¡ = â‹[ ğ’ª X ]-upper â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† (inl p)
    Ï… (inl qâ‚ , inr pâ‚‚) =
     Î± ğ”™ (inl qâ‚) âˆ§[ ğ’ª X ] Î± ğ”š (inr pâ‚‚)                     ï¼âŸ¨ refl âŸ©â‚š
     U âˆ§[ ğ’ª X ] ğŸ[ ğ’ª X ]                                    ï¼âŸ¨ â…    âŸ©â‚š
     U                                                      â‰¤âŸ¨ â…¡  âŸ©
     â‹[ ğ’ª X ] â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† â– 
      where
       p : (âŠ¤â‚› âˆˆâ‚› (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)) holds
       p = qâ‚ , contains-âŠ¥â‚›-implies-contains-âŠ¤â‚› ğ”š pâ‚‚

       â…  = ğŸ-right-unit-of-âˆ§ (ğ’ª X) U
       â…¡ = â‹[ ğ’ª X ]-upper â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† (inl p)
    Ï… (inr qâ‚ , inl pâ‚‚) =
     Î± ğ”™ (inr qâ‚) âˆ§[ ğ’ª X ] Î± ğ”š (inl pâ‚‚)                     ï¼âŸ¨ refl âŸ©â‚š
     ğŸ[ ğ’ª X ] âˆ§[ ğ’ª X ] U                                    ï¼âŸ¨ â…    âŸ©â‚š
     U                                                      â‰¤âŸ¨ â…¡  âŸ©
     â‹[ ğ’ª X ] â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† â– 
      where
       p : (âŠ¤â‚› âˆˆâ‚› (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)) holds
       p = contains-âŠ¥â‚›-implies-contains-âŠ¤â‚› ğ”™ qâ‚ , pâ‚‚

       â…  = ğŸ-left-unit-of-âˆ§ (ğ’ª X) U
       â…¡ = â‹[ ğ’ª X ]-upper â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† (inl p)
    Ï… (inr qâ‚ , inr qâ‚‚) =
     Î± ğ”™ (inr qâ‚) âˆ§[ ğ’ª X ] Î± ğ”š (inr qâ‚‚)                      ï¼âŸ¨ refl âŸ©â‚š
     ğŸ[ ğ’ª X ] âˆ§[ ğ’ª X ] ğŸ[ ğ’ª X ]                              ï¼âŸ¨ â…     âŸ©â‚š
     ğŸ[ ğ’ª X ]                                                â‰¤âŸ¨  â…¡ âŸ©
     â‹[ ğ’ª X ] â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â†  â– 
      where
       q : (âŠ¥â‚› âˆˆâ‚› (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)) holds
       q = qâ‚ , qâ‚‚

       â…  = âˆ§[ ğ’ª X ]-is-idempotent ğŸ[ ğ’ª X ] â»Â¹
       â…¡ = â‹[ ğ’ª X ]-upper â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â† (inr q)

    Ïˆâ‚‚ : ((h ğ”™ âˆ§[ ğ’ª X ] h ğ”š) â‰¤ h (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š)) holds
    Ïˆâ‚‚ =
     h ğ”™ âˆ§[ ğ’ª X ] h ğ”š                                             ï¼âŸ¨ refl âŸ©â‚š
     h ğ”™ âˆ§[ ğ’ª X ] (â‹[ ğ’ª X ] â„±â‚“ ğ”š)                                 ï¼âŸ¨ â…¡ âŸ©â‚š
     â‹[ ğ’ª X ] â… Î± ğ”™ iâ‚ âˆ§[ ğ’ª X ] Î± ğ”š iâ‚‚ âˆ£ (iâ‚ , iâ‚‚) âˆ¶ I ğ”™ Ã— I ğ”š â†  â‰¤âŸ¨ â…¢ âŸ©
     â‹[ ğ’ª X ] â… Î± (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) i âˆ£ i âˆ¶ I (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â†       ï¼âŸ¨ refl âŸ©â‚š
     h (ğ”™ âˆ§[ ğ’ª ğ•Š ] ğ”š) â– 
      where
       â…¡ = distributivity+ (ğ’ª X) (I ğ”™ , Î± ğ”™) (I ğ”š  , Î± ğ”š)
       â…¢ = â‹[ ğ’ª X ]-least _ (_ , Ï…)

\end{code}

The fact that it satisfies the property `h truth ï¼ U` is quite easy to see.

\begin{code}

  â€ â‚ : (U â‰¤ h truth) holds
  â€ â‚ = U â‰¤âŸ¨ â‹[ ğ’ª X ]-upper _ (inl â‹†) âŸ© h truth â– 

  â€ â‚‚ : (h truth â‰¤ U) holds
  â€ â‚‚ = â‹[ ğ’ª X ]-least (â„±â‚“ truth) (U , Î³)
   where
    Î³ : (U is-an-upper-bound-of (â„±â‚“ truth)) holds
    Î³ (inl â‹†) = â‰¤-is-reflexive (poset-of (ğ’ª X)) U

  â€  : U ï¼ h truth
  â€  = â‰¤-is-antisymmetric (poset-of (ğ’ª X)) â€ â‚ â€ â‚‚

\end{code}

We now proceed to prove that it preserves the joins.

\begin{code}

  open ScottLocaleProperties ğ•Šğ““ ğ•Šğ““-has-least hscb pe

  Ï‘ : (ğ”– : Fam ğ“¤ âŸ¨ ğ’ª ğ•Š âŸ©) â†’ (h (â‹[ ğ’ª ğ•Š ] ğ”–) is-lub-of â… h ğ”˜ âˆ£ ğ”˜ Îµ ğ”– â†) holds
  Ï‘ ğ”– = Ï‘â‚ , Î» { (V , Ï…) â†’ Ï‘â‚‚ V Ï… }
   where
    Ï‘â‚ : (h (â‹[ ğ’ª ğ•Š ] ğ”–) is-an-upper-bound-of â… h ğ”˜ âˆ£ ğ”˜ Îµ ğ”– â†) holds
    Ï‘â‚ i = ğ“‚ (ğ”– [ i ] , â‹[ ğ’ª ğ•Š ] ğ”–) (â‹[ ğ’ª ğ•Š ]-upper ğ”– i)

    Ï‘â‚‚ : (W : âŸ¨ ğ’ª X âŸ©)
       â†’ (W is-an-upper-bound-of â… h ğ”˜ âˆ£ ğ”˜ Îµ ğ”– â†) holds
       â†’ (h (â‹[ ğ’ª ğ•Š ] ğ”–) â‰¤ W) holds
    Ï‘â‚‚ W Ï… = â‹[ ğ’ª X ]-least (â„±â‚“ (â‹[ ğ’ª ğ•Š ] ğ”–)) (W , Î³)
     where
      Î³ : (W is-an-upper-bound-of (â„±â‚“ (â‹[ ğ’ª ğ•Š ] ğ”–))) holds
      Î³ (inl Î¼) = âˆ¥âˆ¥-rec (holds-is-prop (_ â‰¤ _)) â™£ Î¼
       where
        â™£ : (Î£ k ê‰ index ğ”– , (âŠ¤â‚› âˆˆâ‚› (ğ”– [ k ])) holds) â†’ (U â‰¤ W) holds
        â™£ (k , Î¼â‚–) = U           ï¼âŸ¨ â…  âŸ©â‚š
                     h truth     â‰¤âŸ¨ â…¡ âŸ©
                     h (ğ”– [ k ]) â‰¤âŸ¨ â…¢ âŸ©
                     W           â– 
                      where
                       â…  = â€ 
                       â…¡ = ğ“‚ _ (contains-âŠ¤â‚›-implies-above-truth (ğ”– [ k ]) Î¼â‚–)
                       â…¢ = Ï… k
      Î³ (inr Î¼) = âˆ¥âˆ¥-rec (holds-is-prop (_ â‰¤ _)) â™¥ Î¼
       where
        â™¥ : (Î£ k ê‰ index ğ”– , (âŠ¥â‚› âˆˆâ‚› (ğ”– [ k ])) holds) â†’ (ğŸ[ ğ’ª X ] â‰¤ W) holds
        â™¥ (k , Î¼â‚–) =
         ğŸ[ ğ’ª X ]    ï¼âŸ¨ â…  âŸ©â‚š
         h ğŸ[ ğ’ª ğ•Š ]  ï¼âŸ¨ â…¡ âŸ©â‚š
         h (ğ”– [ k ]) â‰¤âŸ¨ â…¢  âŸ©
         W           â– 
          where
           â…  = Ï† â»Â¹
           â…¡ = ap h (contains-bottom-implies-is-ğŸ (ğ”– [ k ]) Î¼â‚–) â»Â¹
           â…¢ = Ï… k

\end{code}

We package these up into a continuous map of locales (recal that `X â”€câ†’ Y`
denotes the type of continuous maps from locale `X` to locale `Y`).

\begin{code}

  ğ’½ : X â”€câ†’ ğ•Š
  ğ’½ = h , Ï† , Ïˆ , Ï‘

\end{code}

Finally, we show that `ğ’½` is determined uniquely by this property.

\begin{code}

  â€¡ : is-central (Î£ (f , _) ê‰ (ğ’ª ğ•Š â”€fâ†’ ğ’ª X) , U ï¼ f truth) (ğ’½ , â€ )
  â€¡ (â„Š@(g , Ï†â‚€ , Ïˆâ‚€ , Ï‘â‚€) , â€ â‚€) =
   to-subtype-ï¼
    (Î» h â†’ carrier-of-[ poset-of (ğ’ª X) ]-is-set)
    (to-frame-homomorphism-ï¼ (ğ’ª ğ•Š) (ğ’ª X) ğ’½ â„Š Î³)
     where
      ğ“‚â€² : is-monotonic (poset-of (ğ’ª ğ•Š)) (poset-of (ğ’ª X)) g holds
      ğ“‚â€² = frame-morphisms-are-monotonic (ğ’ª ğ•Š) (ğ’ª X) g (Ï†â‚€ , Ïˆâ‚€ , Ï‘â‚€)

      Î³â‚ : (ğ”™ : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ (h ğ”™ â‰¤ g ğ”™) holds
      Î³â‚ ğ”™ = â‹[ ğ’ª X ]-least (â„±â‚“ ğ”™) (g ğ”™ , Î²â‚)
       where
        Î²â‚ : (g ğ”™ is-an-upper-bound-of â„±â‚“ ğ”™) holds
        Î²â‚ (inl p) = U ï¼âŸ¨ â€ â‚€ âŸ©â‚š g truth â‰¤âŸ¨ â…¡ âŸ© g ğ”™ â– 
                      where
                       â…¡ = ğ“‚â€² (truth , ğ”™) (contains-âŠ¤â‚›-implies-above-truth ğ”™ p)
        Î²â‚ (inr p) = ğŸ[ ğ’ª X ] ï¼âŸ¨ â…  âŸ©â‚š g ğŸ[ ğ’ª ğ•Š ] ï¼âŸ¨ â…¡ âŸ©â‚š g ğ”™ â– 
                      where
                       â…  = Ï†â‚€ â»Â¹
                       â…¡ = ap g (contains-bottom-implies-is-ğŸ ğ”™ p â»Â¹)

      Î³â‚‚ : (ğ”™ : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ (g ğ”™ â‰¤ h ğ”™) holds
      Î³â‚‚ ğ”™ = g ğ”™                      ï¼âŸ¨ ap g cov âŸ©â‚š
             g (â‹[ ğ’ª ğ•Š ] ğ”–)           ï¼âŸ¨ â€» âŸ©â‚š
             â‹[ ğ’ª X ] â… g ğ”… âˆ£ ğ”… Îµ ğ”– â† â‰¤âŸ¨ â™¢ âŸ©
             h ğ”™                      â– 
       where
        open Joins _âŠ†â‚›_
         renaming (_is-an-upper-bound-of_ to _is-an-upper-bound-ofâ‚›_)

        ğ”– = covering-familyâ‚› ğ•Š ğ•Š-is-spectralá´° ğ”™

        â€» = â‹[ ğ’ª X ]-unique â… g ğ”… âˆ£ ğ”… Îµ ğ”– â† (g (â‹[ ğ’ª ğ•Š ] ğ”–)) (Ï‘â‚€ ğ”–)

        cov : ğ”™ ï¼ â‹[ ğ’ª ğ•Š ] ğ”–
        cov = â‹[ ğ’ª ğ•Š ]-unique ğ”– ğ”™ (basisâ‚›-covers-do-cover ğ•Š ğ•Š-is-spectralá´° ğ”™)

        covâ‚€ : (ğ”™ is-an-upper-bound-ofâ‚› ğ”–) holds
        covâ‚€ bs = âŠ†â‚–-implies-âŠ†â‚›
                   (ğ”– [ bs ])
                   ğ”™
                   (prâ‚ (basisâ‚›-covers-do-cover ğ•Š ğ•Š-is-spectralá´° ğ”™) bs)

        final : (i : index ğ”–) â†’ (g (ğ”– [ i ]) â‰¤ h ğ”™) holds
        final (bs , b) = casesâ‚ƒ caseâ‚ caseâ‚‚ caseâ‚ƒ (basis-trichotomy bs)
         where
          open PosetReasoning poset-of-scott-opensâ‚›
           renaming (_â‰¤âŸ¨_âŸ©_ to _â‰¤âŸ¨_âŸ©â‚›_;
                     _â–  to _â– â‚›;
                     _ï¼âŸ¨_âŸ©â‚š_ to _ï¼âŸ¨_âŸ©â‚›_)

          caseâ‚ : â„¬ğ•Š [ bs ] ï¼ ğŸ[ ğ’ª ğ•Š ]
                â†’ (g (ğ”– [ bs , b ]) â‰¤ h ğ”™) holds
          caseâ‚ q = transport (Î» - â†’ (g (ğ”– [ bs , b ]) â‰¤ h -) holds)
                     r
                     (g (â„¬ğ•Š [ bs ]) â‰¤âŸ¨ ğŸ-is-top (ğ’ª X) (g (â„¬ğ•Š [ bs ])) âŸ©
                     ğŸ[ ğ’ª X ]       ï¼âŸ¨ Ï† â»Â¹ âŸ©â‚š h ğŸ[ ğ’ª ğ•Š ] â– )
           where
            Ï‡ : (ğŸ[ ğ’ª ğ•Š ] âŠ†â‚› (â„¬ğ•Š [ bs ])) holds
            Ï‡ = reflexivity+ poset-of-scott-opensâ‚› (q â»Â¹)

            r : ğŸ[ ğ’ª ğ•Š ] ï¼ ğ”™
            r = âŠ†â‚›-is-antisymmetric
                 (Î» x p â†’ covâ‚€ (bs , b) x (Ï‡ x p))
                 (âŠ¤â‚›-is-top ğ”™)

          caseâ‚‚ : â„¬ğ•Š [ bs ] ï¼ truth
                â†’ (g (ğ”– [ bs , b ]) â‰¤ h ğ”™) holds
          caseâ‚‚ p = g (ğ”– [ bs , b ]) ï¼âŸ¨ refl âŸ©â‚š
                    g (â„¬ğ•Š [ bs ])    ï¼âŸ¨ â…  âŸ©â‚š
                    g truth          ï¼âŸ¨ â…¡ âŸ©â‚š
                    U                â‰¤âŸ¨  â…¢ âŸ©
                    h ğ”™              â– 
           where
            pâ‚€ : (truth âŠ†â‚› (â„¬ğ•Š [ bs ])) holds
            pâ‚€ = reflexivity+ poset-of-scott-opensâ‚› (p â»Â¹)

            Î¶ : (truth âŠ†â‚› ğ”™) holds
            Î¶ P Î¼ = covâ‚€ (bs , b) P (pâ‚€ P Î¼)

            Ï‡ : (âŠ¤â‚› âˆˆâ‚› ğ”™) holds
            Ï‡ = above-truth-implies-contains-âŠ¤â‚› ğ”™ (âŠ†â‚›-implies-âŠ†â‚– truth ğ”™ Î¶)

            â…  = ap g p
            â…¡ = â€ â‚€ â»Â¹
            â…¢ = â‹[ ğ’ª X ]-upper (â„±â‚“ ğ”™) (inl Ï‡)

          caseâ‚ƒ : â„¬ğ•Š [ bs ] ï¼ ğŸ[ ğ’ª ğ•Š ] â†’ (g (ğ”– [ bs , b ]) â‰¤ h ğ”™) holds
          caseâ‚ƒ q = g (ğ”– [ bs , b ]) ï¼âŸ¨ refl   âŸ©â‚š
                    g (â„¬ğ•Š [ bs ])    ï¼âŸ¨ â…  âŸ©â‚š
                    g ğŸ[ ğ’ª ğ•Š ]       ï¼âŸ¨ â…¡ âŸ©â‚š
                    ğŸ[ ğ’ª X ]         â‰¤âŸ¨ â…¢ âŸ©
                    h ğ”™              â– 
                     where
                      â…  = ap g q
                      â…¡ = frame-homomorphisms-preserve-bottom (ğ’ª ğ•Š) (ğ’ª X) â„Š
                      â…¢ = ğŸ-is-bottom (ğ’ª X) (h ğ”™)

        â™¢ : ((â‹[ ğ’ª X ] â… g ğ”… âˆ£ ğ”… Îµ ğ”– â†) â‰¤ h ğ”™) holds
        â™¢ = â‹[ ğ’ª X ]-least â… g ğ”… âˆ£ ğ”… Îµ ğ”– â† (h ğ”™ , final)

      Î³ : (ğ”™ : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ h ğ”™ ï¼ g ğ”™
      Î³ ğ”™ = â‰¤-is-antisymmetric (poset-of (ğ’ª X)) (Î³â‚ ğ”™) (Î³â‚‚ ğ”™)

\end{code}
