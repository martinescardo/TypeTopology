---
title:          Some properties of the SierpiÅ„ski locale
author:         Ayberk Tosun
date-completed: 2024-02-12
---

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.FunExt
open import UF.Logic
open import MLTT.Spartan hiding (ğŸš; â‚€; â‚)
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Size

module Locales.Sierpinski.Properties
        (ğ“¤  : Universe)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (sr : Set-Replacement pt)
       where

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤
open import DomainTheory.Basics.Dcpo    pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import DomainTheory.Basics.Pointed pt fe ğ“¤ renaming (âŠ¥ to âŠ¥âˆ™)
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.Lifting.LiftingSet pt fe ğ“¤ pe
open import DomainTheory.Lifting.LiftingSetAlgebraic pt pe fe ğ“¤
open import DomainTheory.Topology.ScottTopology pt fe ğ“¤
open import Lifting.Construction ğ“¤
open import Lifting.Miscelanea-PropExt-FunExt ğ“¤ pe fe
open import Lifting.UnivalentPrecategory ğ“¤ (ğŸ™ {ğ“¤})
open import Locales.Frame pt fe hiding (is-directed)
open import Locales.InitialFrame pt fe
open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.Properties pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤
open import Locales.Sierpinski.Definition ğ“¤ pe pt fe sr
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralLocale pt fe
open import Locales.Spectrality.SpectralMap pt fe
open import Locales.Stone pt fe sr
open import MLTT.List hiding ([_])
open import Slice.Family
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier

open AllCombinators pt fe
open Locale
open PropositionalTruncation pt

\end{code}

We show that `ğ•Šğ““` is a Scott domain. We have already shown that it is an
algebraic lattice, so it remains to show that it is bounded complete.

\begin{code}

open import DomainTheory.BasesAndContinuity.ScottDomain pt fe ğ“¤

open DefinitionOfBoundedCompleteness

âŠ‘â‚€-implies-âŠ‘ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
             â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
             â†’ (to-Î© x â‰¤[ poset-of (ğŸ-ğ”½ğ•£ğ• pe) ] to-Î© y) holds
âŠ‘â‚€-implies-âŠ‘ _ _ (g , q) p = g p

âŠ‘-implies-âŠ‘â‚€ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
             â†’ (to-Î© x â‰¤[ poset-of (ğŸ-ğ”½ğ•£ğ• pe) ] to-Î© y) holds
             â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
âŠ‘-implies-âŠ‘â‚€ (P , f , h) (Pâ€² , fâ€² , hâ€²) p = p , (Î» _ â†’ ğŸ™-is-prop â‹† â‹†)

ğ•Šğ““-bounded-complete : bounded-complete ğ•Šğ““ holds
ğ•Šğ““-bounded-complete S _ = sup , Ï†
 where
  Sâ‚€ : Fam ğ“¤ (Î© ğ“¤)
  Sâ‚€ = â… to-Î© P âˆ£ P Îµ S â†

  supâ‚€ : Î© ğ“¤
  supâ‚€ = â‹[ (ğŸ-ğ”½ğ•£ğ• pe) ] Sâ‚€

  sup : âŸ¨ ğ•Šğ““ âŸ©âˆ™
  sup = supâ‚€ holds , (Î» _ â†’ â‹†) , âˆƒ-is-prop

  Ï… : is-upperbound (underlying-order ğ•Šğ““) sup (S [_])
  Ï… i = â€  , â€¡
   where
    â€  : is-defined (S [ i ]) â†’ is-defined sup
    â€  p = âˆ£ i , p âˆ£

    â€¡ : value (S [ i ]) âˆ¼ (Î» xâ‚ â†’ value sup (â€  xâ‚))
    â€¡ _ = ğŸ™-is-prop â‹† â‹†

  Ï‘ : is-lowerbound-of-upperbounds (underlying-order ğ•Šğ““) sup (S [_])
  Ï‘ (P , f , h) q = âŠ‘-implies-âŠ‘â‚€ sup (P , f , h) (â‹[ ğŸ-ğ”½ğ•£ğ• pe ]-least Sâ‚€ ((P , h) , (Î» i â†’ prâ‚ (q i))))

  Ï† : is-sup (underlying-order ğ•Šğ““) sup (S [_])
  Ï† = Ï… , Ï‘

\end{code}

Finally, we show that `ğ•Šğ““` trivially satisfies the decidability condition that
we assume in the proof that Scott locales of Scott domains are spectral.

\begin{code}

open import Locales.ScottLocale.ScottLocalesOfScottDomains pt fe sr ğ“¤

ğ•Šğ““-satisfies-dc : decidability-condition ğ•Šğ““
ğ•Šğ““-satisfies-dc ğ’«â‚€@(Pâ‚€ , hâ‚€ , fâ‚€) ğ’«â‚@(Pâ‚ , hâ‚ , fâ‚) Îºc Îºd =
 inl âˆ£ up , â€¡ âˆ£
  where
   up : âŸ¨ ğ•Šğ““ âŸ©âˆ™
   up = to-ğ•Šğ““ (to-Î© ğ’«â‚€ âˆ¨[ ğŸ-ğ”½ğ•£ğ• pe ] to-Î© ğ’«â‚)

   open Joins {A = âŸ¨ ğ•Šğ““ âŸ©âˆ™} (Î» x y â†’ (x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y) , prop-valuedness ğ•Šğ““ x y)

   â€¡ : (up is-an-upper-bound-of (binary-family ğ“¤ ğ’«â‚€ ğ’«â‚)) holds
   â€¡ (inl â‹†) = (Î» p â†’ âˆ£ inl â‹† , p âˆ£) , Î» _ â†’ ğŸ™-is-prop â‹† â‹†
   â€¡ (inr â‹†) = (Î» p â†’ âˆ£ inr â‹† , p âˆ£) , Î» _ â†’ ğŸ™-is-prop â‹† â‹†

\end{code}

From all these, we obtain the fact that `ğ•Š` is a spectral locale, which we call
`ğ•Š-is-spectral` below.

\begin{code}

ğ•Šğ““-has-least : has-least (underlying-order ğ•Šğ““)
ğ•Šğ““-has-least = (âŠ¥âˆ™ ğ•Šğ““âŠ¥) , âŠ¥-is-least ğ•Šğ““âŠ¥

open ScottLocaleConstruction ğ•Šğ““ hscb pe
open SpectralScottLocaleConstruction ğ•Šğ““ ğ•Šğ““-has-least hscb ğ•Šğ““-satisfies-dc ğ•Šğ““-bounded-complete pe
open ScottLocaleProperties ğ•Šğ““ ğ•Šğ““-has-least hscb pe
open DefnOfScottLocale ğ•Šğ““ ğ“¤ pe using (ğ’ªâ‚›-equality; _âŠ†â‚›_)

â„¬ğ•Š : Fam ğ“¤ âŸ¨ ğ’ª ğ•Š âŸ©
â„¬ğ•Š = List (ğŸš ğ“¤) , ğœ¸

principal-filter-on-â‚-is-truth : â†‘áµ[ â‚ ] ï¼ truth
principal-filter-on-â‚-is-truth = â‰¤-is-antisymmetric (poset-of (ğ’ª ğ•Š)) â€  â€¡
 where
  â€ â‚€ : (â†‘áµ[ â‚ ] âŠ†â‚› truth) holds
  â€ â‚€ (P , f , Ï†) (p , _) = p â‹†

  â€  : (â†‘áµ[ â‚ ] âŠ†â‚– truth) holds
  â€  = âŠ†â‚›-implies-âŠ†â‚– â†‘áµ[ â‚ ] truth â€ â‚€

  â€¡â‚€ : (truth âŠ†â‚› â†‘áµ[ â‚ ]) holds
  â€¡â‚€ (P , f , Ï†) p = (Î» x â†’ p) , Î» { _ â†’ ğŸ™-is-prop â‹† â‹† }

  â€¡ : (truth âŠ†â‚– â†‘áµ[ â‚ ]) holds
  â€¡ = âŠ†â‚›-implies-âŠ†â‚– truth â†‘áµ[ â‚ ] â€¡â‚€

ğ•Š-is-spectralá´° : spectralá´° ğ•Š
ğ•Š-is-spectralá´° = Ïƒá´°

open import Locales.PatchLocale pt fe sr

ğ•Š-is-spectral : is-spectral ğ•Š holds
ğ•Š-is-spectral = spectralá´°-gives-spectrality ğ•Š Ïƒá´°

ğ•Š-has-small-ğ’¦ : has-small-ğ’¦ ğ•Š
ğ•Š-has-small-ğ’¦ = spectralá´°-implies-small-ğ’¦ ğ•Š Ïƒá´°

\end{code}

Added on 2024-03-09.

A basic open of the SierpiÅ„ski locale is either `ğŸ`, `truth`, or `ğŸ`. In fact,
because the basic open coincide with the compact opens in spectral locales, a
corollary of this is that these three elements form a basis for SierpiÅ„ski.

\begin{code}

basis-trichotomy : (bs : List (ğŸš ğ“¤))
                 â†’ (ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ]) + (ğœ¸ bs ï¼ truth) + (ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ])
basis-trichotomy []       = inr (inr p)
                             where
                              p : ğœ¸ [] ï¼ ğŸ[ ğ’ª ğ•Š ]
                              p = ğœ¸ []     ï¼âŸ¨ ğœ¸-equal-to-ğœ¸â‚ [] âŸ©
                                  ğœ¸â‚ []    ï¼âŸ¨ refl             âŸ©
                                  ğŸ[ ğ’ª ğ•Š ] âˆ
basis-trichotomy (â‚€ âˆ· bs) = inl p
                             where
                              â…  = ğœ¸-equal-to-ğœ¸â‚ (â‚€ âˆ· bs)
                              â…¡ = ap (Î» - â†’ - âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs) â†‘âŠ¥-is-top
                              â…¢ = ğŸ-left-annihilator-for-âˆ¨ (ğ’ª ğ•Š) (ğœ¸â‚ bs)

                              p : ğœ¸ (â‚€ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ]
                              p = ğœ¸ (â‚€ âˆ· bs)               ï¼âŸ¨ â…     âŸ©
                                  ğœ¸â‚ (â‚€ âˆ· bs)              ï¼âŸ¨ refl âŸ©
                                  â†‘áµ[ â‚€ ] âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs   ï¼âŸ¨ â…¡    âŸ©
                                  ğŸ[ ğ’ª ğ•Š ] âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs  ï¼âŸ¨ â…¢    âŸ©
                                  ğŸ[ ğ’ª ğ•Š ]                 âˆ
basis-trichotomy (â‚ âˆ· bs) = casesâ‚ƒ caseâ‚ caseâ‚‚ caseâ‚ƒ IH
 where
  IH : (ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ]) + (ğœ¸ bs ï¼ truth) + (ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ])
  IH = basis-trichotomy bs

  caseâ‚ : ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ]
        â†’ (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ]) + (ğœ¸ (â‚ âˆ· bs) ï¼ truth) + (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ])
  caseâ‚ q = inl r
   where
    â…¡ = ap
         (Î» - â†’ â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] -)
         (ğœ¸â‚ bs ï¼âŸ¨ ğœ¸-equal-to-ğœ¸â‚ bs â»Â¹ âŸ© ğœ¸ bs ï¼âŸ¨ q âŸ© ğŸ[ ğ’ª ğ•Š ] âˆ )
    â…¢ = ğŸ-right-annihilator-for-âˆ¨ (ğ’ª ğ•Š) â†‘áµ[ â‚ ]

    r : ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ]
    r = ğœ¸ (â‚ âˆ· bs)                ï¼âŸ¨ ğœ¸-equal-to-ğœ¸â‚ (â‚ âˆ· bs) âŸ©
        ğœ¸â‚ (â‚ âˆ· bs)               ï¼âŸ¨ refl âŸ©
        â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs    ï¼âŸ¨ â…¡ âŸ©
        â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] ğŸ[ ğ’ª ğ•Š ] ï¼âŸ¨ â…¢ âŸ©
        ğŸ[ ğ’ª ğ•Š ]                  âˆ

  caseâ‚‚ : ğœ¸ bs ï¼ truth
        â†’ (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ]) + (ğœ¸ (â‚ âˆ· bs) ï¼ truth) + (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ])
  caseâ‚‚ q = inr (inl r)
   where
    â…¡ = ap (Î» - â†’ - âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs) principal-filter-on-â‚-is-truth
    â…¢ = ap (Î» - â†’ truth âˆ¨[ ğ’ª ğ•Š ] -) (ğœ¸-equal-to-ğœ¸â‚ bs â»Â¹)
    â…£ = ap (Î» - â†’ truth âˆ¨[ ğ’ª ğ•Š ] -) q
    â…¤ = âˆ¨[ ğ’ª ğ•Š ]-is-idempotent truth â»Â¹

    r : ğœ¸ (â‚ âˆ· bs) ï¼ truth
    r = ğœ¸ (â‚ âˆ· bs)               ï¼âŸ¨ ğœ¸-equal-to-ğœ¸â‚ (â‚ âˆ· bs) âŸ©
        ğœ¸â‚ (â‚ âˆ· bs)              ï¼âŸ¨ refl                   âŸ©
        â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs   ï¼âŸ¨ â…¡                      âŸ©
        truth âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs     ï¼âŸ¨ â…¢                      âŸ©
        truth âˆ¨[ ğ’ª ğ•Š ] ğœ¸ bs      ï¼âŸ¨ â…£                      âŸ©
        truth âˆ¨[ ğ’ª ğ•Š ] truth     ï¼âŸ¨ â…¤                      âŸ©
        truth                    âˆ

  caseâ‚ƒ : ğœ¸ bs ï¼ ğŸ[ ğ’ª ğ•Š ]
        â†’ (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ]) + (ğœ¸ (â‚ âˆ· bs) ï¼ truth) + (ğœ¸ (â‚ âˆ· bs) ï¼ ğŸ[ ğ’ª ğ•Š ])
  caseâ‚ƒ q = inr (inl r)
   where
    â…  = ğœ¸-equal-to-ğœ¸â‚ (â‚ âˆ· bs)
    â…¡ = ap
         (Î» - â†’ â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] -)
         (ğœ¸â‚ bs ï¼âŸ¨ ğœ¸-equal-to-ğœ¸â‚ bs â»Â¹ âŸ© ğœ¸ bs ï¼âŸ¨ q âŸ© ğŸ[ ğ’ª ğ•Š ] âˆ)
    â…¢ = ğŸ-left-unit-of-âˆ¨ (ğ’ª ğ•Š) â†‘áµ[ â‚ ]
    â…£ = principal-filter-on-â‚-is-truth

    r : ğœ¸ (â‚ âˆ· bs) ï¼ truth
    r = ğœ¸ (â‚ âˆ· bs)                ï¼âŸ¨ â…      âŸ©
        ğœ¸â‚ (â‚ âˆ· bs)               ï¼âŸ¨ refl  âŸ©
        â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] ğœ¸â‚ bs    ï¼âŸ¨ â…¡     âŸ©
        â†‘áµ[ â‚ ] âˆ¨[ ğ’ª ğ•Š ] ğŸ[ ğ’ª ğ•Š ] ï¼âŸ¨ â…¢     âŸ©
        â†‘áµ[ â‚ ]                   ï¼âŸ¨ â…£     âŸ©
        truth                     âˆ

\end{code}

Added on 2024-03-11.

\begin{code}

open DefnOfScottTopology ğ•Šğ““ ğ“¤

contains-âŠ¥â‚›-implies-contains-âŠ¤â‚› : (ğ”˜ : âŸ¨ ğ’ª ğ•Š âŸ©) â†’ (âŠ¥â‚› âˆˆâ‚› ğ”˜ â‡’ âŠ¤â‚› âˆˆâ‚› ğ”˜) holds
contains-âŠ¥â‚›-implies-contains-âŠ¤â‚› ğ”˜ Î¼ = transport (Î» - â†’ (âŠ¤â‚› âˆˆâ‚› -) holds) (q â»Â¹) â‹†
 where
  open ğ’ªâ‚›á´¿

  q : ğ”˜ ï¼ ğŸ[ ğ’ª ğ•Š ]
  q = contains-bottom-implies-is-ğŸ ğ”˜ Î¼

holds-gives-equal-âŠ¤â‚› : (P : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ (P âˆˆâ‚› truth) holds â†’ P ï¼ âŠ¤â‚›
holds-gives-equal-âŠ¤â‚› (P , f , Ï†) p =
 to-subtype-ï¼
  (Î» Q â†’ Ã—-is-prop (Î -is-prop fe (Î» _ â†’ ğŸ™-is-prop)) (being-prop-is-prop fe))
  (holds-gives-equal-ğŸ™ pe P Ï† p)

contains-âŠ¤â‚›-implies-above-truth : (ğ”˜ : âŸ¨ ğ’ª ğ•Š âŸ©)
                                â†’ (âŠ¤â‚› âˆˆâ‚› ğ”˜) holds
                                â†’ (truth â‰¤[ poset-of (ğ’ª ğ•Š) ] ğ”˜) holds
contains-âŠ¤â‚›-implies-above-truth ğ”˜ Î¼â‚œ = âŠ†â‚›-implies-âŠ†â‚– truth ğ”˜ â€ 
 where
  â€  : (truth âŠ†â‚› ğ”˜) holds
  â€  P Î¼â‚š = transport (Î» - â†’ (- âˆˆâ‚› ğ”˜) holds) (holds-gives-equal-âŠ¤â‚› P Î¼â‚š â»Â¹) Î¼â‚œ

\end{code}

Added on 2024-03-11.

If a Scott open `ğ”˜` is above truth, then it obviously contains the true
proposition `âŠ¤â‚›`.

\begin{code}

above-truth-implies-contains-âŠ¤â‚› : (ğ”˜ : âŸ¨ ğ’ª ğ•Š âŸ©)
                                â†’ (truth â‰¤[ poset-of (ğ’ª ğ•Š) ] ğ”˜) holds
                                â†’ (âŠ¤â‚› âˆˆâ‚› ğ”˜) holds
above-truth-implies-contains-âŠ¤â‚› ğ”˜ p = âŠ†â‚–-implies-âŠ†â‚› truth ğ”˜ p âŠ¤â‚› â‹†

\end{code}
