--------------------------------------------------------------------------------
title:          The SierpiÅ„ski locale
author:         Ayberk Tosun
date-completed: 2024-02-12
dates-updated:  [2024-03-09]
--------------------------------------------------------------------------------

This module contains the definition of the SierpiÅ„ski locale as the Scott locale
of the SierpiÅ„ski the dcpo.

In the future, other constructions of the SierpiÅ„ski locale might be added here.

\begin{code}

{-# OPTIONS --safe --without-K --lossy-unification #-}

open import UF.FunExt
open import UF.Logic
open import MLTT.Spartan hiding (ğŸš)
open import UF.PropTrunc
open import UF.Subsingletons
open import UF.Size hiding (is-locally-small)

module Locales.Sierpinski.Definition
        (ğ“¤  : Universe)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (fe : Fun-Ext)
        (sr : Set-Replacement pt) where

open import DomainTheory.BasesAndContinuity.Bases pt fe ğ“¤
open import DomainTheory.BasesAndContinuity.Continuity pt fe ğ“¤
open import DomainTheory.Basics.Dcpo    pt fe ğ“¤ renaming (âŸ¨_âŸ© to âŸ¨_âŸ©âˆ™)
open import DomainTheory.Basics.Miscelanea pt fe ğ“¤
open import DomainTheory.Basics.Pointed pt fe ğ“¤ renaming (âŠ¥ to âŠ¥âˆ™)
open import DomainTheory.Basics.WayBelow pt fe ğ“¤
open import DomainTheory.Lifting.LiftingSet pt fe ğ“¤ pe
open import DomainTheory.Lifting.LiftingSetAlgebraic pt pe fe ğ“¤
open import DomainTheory.Topology.ScottTopology pt fe ğ“¤
open import Lifting.Construction ğ“¤ hiding (âŠ¥)
open import Lifting.Miscelanea-PropExt-FunExt ğ“¤ pe fe
open import Lifting.UnivalentPrecategory ğ“¤ (ğŸ™ {ğ“¤})
open import Locales.Frame pt fe hiding (ğŸš; is-directed)
open import Locales.InitialFrame pt fe
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralLocale pt fe
open import Locales.Spectrality.SpectralMap pt fe
open import Locales.Stone pt fe sr
open import Slice.Family
open import UF.DiscreteAndSeparated
open import UF.Equiv
open import UF.Subsingletons-FunExt
open import UF.Subsingletons-Properties
open import UF.SubtypeClassifier

open Locale
open PropositionalTruncation pt

\end{code}

We first define the Sierpinski dcpo

\begin{code}

ğ•Šğ““âº : DCPO {ğ“¤ âº } {ğ“¤ âº}
ğ•Šğ““âº = ğ“›-DCPO {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

\end{code}

which is locally small and also algebraic:

\begin{code}

ğ•Š-is-locally-small : is-locally-small ğ•Šğ““âº
ğ•Š-is-locally-small = ğ“›-is-locally-small {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

ğ•Šğ““âº-is-algebraic : is-algebraic-dcpo (ğ“›-DCPO {X = ğŸ™ {ğ“¤}} ğŸ™-is-set)
ğ•Šğ““âº-is-algebraic = ğ“›-is-algebraic-dcpo ğŸ™-is-set

\end{code}

Unfortunately, we do not have the required machinery for making a locally small
copy of a dcpo from an extrinsic proof that it is locally small. In hindsight,
it would have been easier to work with such extrinsic proofs of local smallness,
but I didn't do this and right now, I don't have the time to migrate my
formalization to this style.

Therefore, I defined the function `ğ“›-DCPOâ»` which directly gives the locally
small copy of the dcpo in consideration. Instead of working with `ğ•Šğ““âº`, I work
with `ğ•Šğ““` instead to circumvent this problem.

\begin{code}

ğ•Šğ““ : DCPO {ğ“¤ âº} {ğ“¤}
ğ•Šğ““ = ğ“›-DCPOâ» {X = ğŸ™ {ğ“¤}} ğŸ™-is-set

\end{code}

These two dcpos are of course order-isomorphic.

\begin{code}

âŠ‘-implies-âŠ‘âº : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y â†’ x âŠ‘âŸ¨ ğ•Šğ““âº âŸ© y
âŠ‘-implies-âŠ‘âº x y p q = âŠ‘-to-âŠ‘' p q

âŠ‘âº-implies-âŠ‘ : (x y : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““âº âŸ© y â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© y
âŠ‘âº-implies-âŠ‘ x y p = (Î» q â†’ transport is-defined (p q) q) , Î» _ â†’ refl

\end{code}

The proposition `ğŸ˜` is the bottom element of this dcpo, meaning it can be
made into a pointed dcpo:

\begin{code}

ğ•Šğ““âŠ¥ : DCPOâŠ¥ {ğ“¤ âº} {ğ“¤}
ğ•Šğ““âŠ¥ = ğ•Šğ““ , (ğŸ˜ , (Î» ()) , ğŸ˜-is-prop) , Î» _ â†’ (Î» ()) , Î» ()

\end{code}

The proposition `ğŸ™` is the top element of this dcpo.

\begin{code}

ğŸ™-is-top : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ x âŠ‘âŸ¨ ğ•Šğ““ âŸ© Î· â‹†
ğŸ™-is-top (P , q) = (Î» _ â†’ â‹†) , Î» _ â†’ refl

\end{code}

Furthermore, the dcpo `ğ•Šğ““` is compact.

\begin{code}

ğ•Šğ““-is-compact : is-compact ğ•Šğ““ (Î· â‹†)
ğ•Šğ““-is-compact I Î± (âˆ£iâˆ£ , upâ») pâ» =
 âˆ¥âˆ¥-rec âˆƒ-is-prop â€  (Î·s-are-compact ğŸ™-is-set â‹† I Î± Î´ p)
  where
   open is-locally-small ğ•Š-is-locally-small

   up : is-semidirected (underlying-order ğ•Šğ““âº) Î±
   up i j = âˆ¥âˆ¥-rec âˆƒ-is-prop â€  (upâ» i j)
    where
     â€  : Î£ k ê‰ I , (Î± i âŠ‘âŸ¨ ğ•Šğ““  âŸ© Î± k) Ã— (Î± j âŠ‘âŸ¨ ğ•Šğ““  âŸ© Î± k)
       â†’ âˆƒ k ê‰ I , (Î± i âŠ‘âŸ¨ ğ•Šğ““âº âŸ© Î± k) Ã— (Î± j âŠ‘âŸ¨ ğ•Šğ““âº âŸ© Î± k)
     â€  (k , p , q) = âˆ£ k , âŠ‘-implies-âŠ‘âº (Î± i) (Î± k) p  , âŠ‘-implies-âŠ‘âº (Î± j) (Î± k) q âˆ£

   Î´ : is-directed (underlying-order ğ•Šğ““âº) Î±
   Î´ = âˆ£iâˆ£ , up

   p : Î· â‹† âŠ‘âŸ¨ ğ•Šğ““âº âŸ© (âˆ (ğ“›-DCPO ğŸ™-is-set) Î´)
   p = âŠ‘-to-âŠ‘' (prâ‚ pâ» , Î» _ â†’ refl)

   â€  : Î£ i ê‰ I , underlying-order (ğ“›-DCPO ğŸ™-is-set) (Î· â‹†) (Î± i)
     â†’ âˆƒ i ê‰ I , Î· â‹† âŠ‘âŸ¨ ğ•Šğ““ âŸ© (Î± i)
   â€  (i , q) = âˆ£ i , âŠ‘âº-implies-âŠ‘ (Î· â‹†) (Î± i) q âˆ£

\end{code}

We define a function for mapping inhabitants of the SierpiÅ„ski dcpo to the type
of propositions:

\begin{code}

to-Î© : âŸ¨ ğ•Šğ““ âŸ©âˆ™ â†’ Î© ğ“¤
to-Î© (P , _ , h) = P , h

\end{code}

Conversely, we define a function mapping every proposition `P : Î© ğ“¤` to the
carrier set of the SierpiÅ„ski dcpo.

\begin{code}

to-ğ•Šğ““ : Î© ğ“¤ â†’  âŸ¨ ğ•Šğ““ âŸ©âˆ™
to-ğ•Šğ““ (P , h) = P , (Î» _ â†’ â‹†) , h

\end{code}

It is obvious that these form an equivalence.

\begin{code}

Î©-equivalent-to-ğ•Š : Î© ğ“¤ â‰ƒ âŸ¨ ğ•Šğ““ âŸ©âˆ™
Î©-equivalent-to-ğ•Š = to-ğ•Šğ““ , ((to-Î© , â€ ) , (to-Î© , â€¡))
 where
  Ïˆ : {A : ğ“¤  Ì‡} â†’ is-prop (A â†’ ğŸ™)
  Ïˆ = Î -is-prop fe (Î» _ â†’ ğŸ™-is-prop)

  Ï‘ : {A : ğ“¤  Ì‡} â†’ is-prop (is-prop A)
  Ï‘ = being-prop-is-prop fe

  â€  : (to-ğ•Šğ““ âˆ˜ to-Î©) âˆ¼ id
  â€  (P , f , h) = to-subtype-ï¼ (Î» _ â†’ Ã—-is-prop Ïˆ Ï‘) refl

  â€¡ : to-Î© âˆ˜ to-ğ•Šğ““ âˆ¼ id
  â€¡ (P , h) = to-subtype-ï¼ (Î» _ â†’ Ï‘) refl

\end{code}

For convenience we define abbreviations for the copies of `âŠ¤` and `âŠ¥` in `ğ•Šğ““`.

\begin{code}

âŠ¤â‚› : âŸ¨ ğ•Šğ““ âŸ©âˆ™
âŠ¤â‚› = to-ğ•Šğ““ âŠ¤

âŠ¥â‚› : âŸ¨ ğ•Šğ““ âŸ©âˆ™
âŠ¥â‚› = to-ğ•Šğ““ âŠ¥

\end{code}

We now proceed to the definition of the SierpiÅ„ski locale.

First, we show that `ğ•Šğ““` has a specified small compact basis.

\begin{code}

open import Locales.ScottLocale.Definition pt fe ğ“¤
open import Locales.ScottLocale.ScottLocalesOfAlgebraicDcpos pt fe ğ“¤

hscb : has-specified-small-compact-basis ğ•Šğ““
hscb = (ğŸ™ {ğ“¤} + ğŸ™ {ğ“¤}) , Î² , Ïƒ
 where
  Î² : ğŸ™ + ğŸ™ â†’ âŸ¨ ğ•Šğ““ âŸ©âˆ™
  Î² (inl â‹†) = âŠ¥â‚›
  Î² (inr â‹†) = âŠ¤â‚›

  Î²-is-compact : (b : ğŸ™ + ğŸ™) â†’ is-compact ğ•Šğ““ (Î² b)
  Î²-is-compact (inl â‹†) = âŠ¥-is-compact ğ•Šğ““âŠ¥
  Î²-is-compact (inr â‹†) = ğ•Šğ““-is-compact

  Î²-is-upward-directed : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™)
                       â†’ is-semidirected (underlying-order ğ•Šğ““) (â†“-inclusion ğ•Šğ““ Î² x)
  Î²-is-upward-directed x (inl â‹† , p) (z , q)  = let
                                                 u = (z , q)
                                                 râ‚ = reflexivity ğ•Šğ““ (Î² (inl â‹†))
                                                 râ‚‚ = reflexivity ğ•Šğ““ (Î² z)
                                                in
                                                 âˆ£ u , âŠ¥-is-least ğ•Šğ““âŠ¥ (Î² z) , râ‚‚ âˆ£
  Î²-is-upward-directed x (inr â‹† , pâ‚) (z , q) = let
                                                 râ‚ = reflexivity ğ•Šğ““ (Î² (inr â‹†))
                                                 râ‚‚ = reflexivity ğ•Šğ““ (Î² (inr â‹†))
                                                in
                                                 âˆ£ (inr â‹† , pâ‚) , râ‚ , ğŸ™-is-top (Î² z) âˆ£

  covering : (x : âŸ¨ ğ•Šğ““ âŸ©âˆ™) â†’ is-sup (underlying-order ğ•Šğ““) x (â†“-inclusion ğ•Šğ““ Î² x)
  covering ğ’«@(P , f , h) = prâ‚‚ , â€ 
   where
    â€  : is-lowerbound-of-upperbounds (underlying-order ğ•Šğ““) (P , f , h) (â†“-inclusion ğ•Šğ““ Î² (P , f , h))
    â€  ğ’«â€²@(Pâ€² , fâ€² , hâ€²) Ï… = â€¡
     where
      â™  : P â†’ ğ’« âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«â€²
      â™  p = transport (Î» - â†’ - âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«â€²) eq (Ï… (inr â‹† , q))
       where
        q : Î² (inr â‹†) âŠ‘âŸ¨ ğ•Šğ““ âŸ© ğ’«
        q = (Î» _ â†’ p) , Î» _ â†’ ğŸ™-is-prop â‹† (f p)

        eq : Î² (inr â‹†) ï¼ ğ’«
        eq = antisymmetry ğ•Šğ““ (Î² (inr â‹†)) ğ’« q (ğŸ™-is-top ğ’«)

      â€¡ : underlying-order ğ•Šğ““ (P , f , h) ğ’«â€²
      â€¡ = (Î» p â†’ prâ‚ (â™  p) p) , Î» p â†’ ğŸ™-is-prop â‹† (f p)

  Ïƒ : is-small-compact-basis ğ•Šğ““ Î²
  Ïƒ = record
       { basis-is-compact = Î²-is-compact
       ; âŠ‘á´®-is-small = Î» x b â†’ (Î² b âŠ‘âŸ¨ ğ•Šğ““ âŸ© x) , â‰ƒ-refl (Î² b âŠ‘âŸ¨ ğ•Šğ““ âŸ© x)
       ; â†“á´®-is-directed = Î» x â†’ âˆ£ inl â‹† , âŠ¥-is-least ğ•Šğ““âŠ¥ x âˆ£ , Î²-is-upward-directed x
       ; â†“á´®-is-sup = covering
       }

ğ•Šğ““-is-structurally-algebraic : structurally-algebraic ğ•Šğ““
ğ•Šğ““-is-structurally-algebraic =
 structurally-algebraic-if-specified-small-compact-basis ğ•Šğ““ hscb

\end{code}

Using this compact basis, we define the SierpiÅ„ski locale as the Scott locale of
`ğ•Šğ““`.

\begin{code}

open ScottLocaleConstruction ğ•Šğ““ hscb pe

ğ•Š : Locale (ğ“¤ âº) ğ“¤ ğ“¤
ğ•Š = ScottLocale

\end{code}

Added on 2024-03-08.

There are three important opens of the SierpiÅ„ski locale.

````````````````````````````````````````````````````````````````````````````````
    Î©
    |
   {âŠ¤â‚›}
    |
    âˆ…
````````````````````````````````````````````````````````````````````````````````

The top and bottom one are the full subset and the empty subset of `Î©`. We now
define the singleton open lying in the middle. We call this Scott open `truth`.

We first define the subset `âŸ¨ ğ•Šğ““ âŸ© â†’ Î©` underlying this map, which is in fact
just the identity map since given a proposition `P`, `P ï¼ âŠ¤` iff `P` holds.

\begin{code}

open DefnOfScottLocale ğ•Šğ““ ğ“¤ pe

truthâ‚€ : âŸ¨ ğ•Šğ““ âŸ©âˆ™ â†’ Î© ğ“¤
truthâ‚€ (P , _ , i) = (P , i)

\end{code}

We now package this subset up with the proof that it is Scott open.

\begin{code}

open DefnOfScottTopology ğ•Šğ““ ğ“¤

truthâ‚€-is-upward-closed : is-upwards-closed truthâ‚€ holds
truthâ‚€-is-upward-closed U V u (Ï† , _) = Ï† u

trutháµ£ : ğ’ªâ‚›á´¿
trutháµ£ =
 record
  { pred                              = truthâ‚€
  ; pred-is-upwards-closed            = Ï…
  ; pred-is-inaccessible-by-dir-joins = Î¹
  }
  where
   Ï… : is-upwards-closed truthâ‚€ holds
   Ï… U V u (Ï† , _) = Ï† u

   Î¹ : is-inaccessible-by-directed-joins truthâ‚€ holds
   Î¹ U Î¼ = Î¼

truth : âŸ¨ ğ’ª ğ•Š âŸ©
truth = from-ğ’ªâ‚›á´¿ trutháµ£

\end{code}
