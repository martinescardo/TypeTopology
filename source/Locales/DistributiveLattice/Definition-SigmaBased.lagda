--------------------------------------------------------------------------------
title:          Definition of distributive lattices (Î£-based)
author:         Ayberk Tosun
date-started:   2024-05-16
date-completed: 2024-05-17
--------------------------------------------------------------------------------

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.FunExt
open import UF.PropTrunc
open import UF.Sets

module Locales.DistributiveLattice.Definition-SigmaBased
        (fe : Fun-Ext)
        (pt : propositional-truncations-exist)
       where

open import Locales.DistributiveLattice.Definition fe pt
open import Locales.Frame pt fe
open import MLTT.Spartan
open import UF.Base
open import UF.Equiv
open import UF.Logic
open import UF.Powerset-MultiUniverse
open import UF.SubtypeClassifier

open Implication fe

\end{code}

Sigma-based definition of distributive lattices.

\begin{code}

Distributive-Lattice-Data : ğ“¤  Ì‡ â†’ ğ“¤  Ì‡
Distributive-Lattice-Data A = A           -- top element
                            Ã— A           -- bottom element
                            Ã— (A â†’ A â†’ A) -- binary meet
                            Ã— (A â†’ A â†’ A) -- binary join

open AllCombinators pt fe renaming (_âˆ§_ to _âˆ§â‚š_; _âˆ¨_ to _âˆ¨â‚š_)

satisfies-distributive-lattice-laws
 : {A : ğ“¤  Ì‡} â†’ Distributive-Lattice-Data A â†’ ğ“¤  Ì‡
satisfies-distributive-lattice-laws {ğ“¤} {A} (ğŸ , ğŸ , _âˆ§_ , _âˆ¨_) =
 Î£ s ê‰ is-set A , rest s holds
  where

   rest : is-set A â†’ Î© ğ“¤
   rest s =  (â±¯ x y z ê‰ A , x âˆ§ (y âˆ§ z) ï¼â‚š (x âˆ§ y) âˆ§ z)
          âˆ§â‚š (â±¯ x y ê‰ A , x âˆ§ y ï¼â‚š y âˆ§ x)
          âˆ§â‚š (â±¯ x ê‰ A , x âˆ§ ğŸ ï¼â‚š x)
          âˆ§â‚š (â±¯ x ê‰ A , x âˆ§ x ï¼â‚š x)
          âˆ§â‚š (â±¯ x y ê‰ A , x âˆ§ (x âˆ¨ y) ï¼â‚š x)
          âˆ§â‚š (â±¯ x y z ê‰ A , x âˆ¨ (y âˆ¨ z) ï¼â‚š (x âˆ¨ y) âˆ¨ z)
          âˆ§â‚š (â±¯ x y ê‰ A , x âˆ¨ y ï¼â‚š y âˆ¨ x)
          âˆ§â‚š (â±¯ x ê‰ A , x âˆ¨ ğŸ ï¼â‚š x)
          âˆ§â‚š (â±¯ x ê‰ A , x âˆ¨ x ï¼â‚š x)
          âˆ§â‚š (â±¯ x y ê‰ A , x âˆ¨ (x âˆ§ y) ï¼â‚š x)
          âˆ§â‚š (â±¯ x y z ê‰ A , x âˆ§ (y âˆ¨ z) ï¼â‚š (x âˆ§ y) âˆ¨ (x âˆ§ z))
    where
     open Equality s

\end{code}

\begin{code}

Distributive-Lattice-Structure : (A : ğ“¤  Ì‡) â†’ ğ“¤  Ì‡
Distributive-Lattice-Structure A =
 Î£ d ê‰ Distributive-Lattice-Data A , satisfies-distributive-lattice-laws d

\end{code}

We denote the type Î£-version of the type of distributive lattices
`Distributive-Latticeâ‚€` to distinguish it from the record-based version.

\begin{code}

Distributive-Latticeâ‚€ : (ğ“¤ : Universe) â†’ ğ“¤ âº  Ì‡
Distributive-Latticeâ‚€ ğ“¤ = Î£ A ê‰ ğ“¤  Ì‡ , Distributive-Lattice-Structure A

\end{code}

We now prove that this type is equivalent to the record-based version.

We first define the map going from the Î£-based definition to the record-based
definition.

\begin{code}

to-distributive-lattice : (ğ“¤ : Universe)
                        â†’ Distributive-Latticeâ‚€ ğ“¤
                        â†’ DistributiveLattice ğ“¤
to-distributive-lattice ğ“¤ (X , ((ğŸ , ğŸ , _âˆ§_ , _âˆ¨_) , laws)) =
 let
  (X-is-set     , rest) = laws
  (âˆ§-assoc      , rest) = rest
  (âˆ§-comm       , rest) = rest
  (âˆ§-unit       , rest) = rest
  (âˆ§-idempotent , rest) = rest
  (âˆ§-absorptive , rest) = rest
  (âˆ¨-assoc      , rest) = rest
  (âˆ¨-comm       , rest) = rest
  (âˆ¨-unit       , rest) = rest
  (âˆ¨-idempotent , rest) = rest
  (âˆ¨-absorptive , rest) = rest
  distributivity        = rest
 in
  record
   { X               = X
   ; ğŸ               = ğŸ
   ; ğŸ               = ğŸ
   ; _âˆ§_             = _âˆ§_
   ; _âˆ¨_             = _âˆ¨_
   ; X-is-set        = X-is-set
   ; âˆ§-associative   = âˆ§-assoc
   ; âˆ§-commutative   = âˆ§-comm
   ; âˆ§-unit          = âˆ§-unit
   ; âˆ§-idempotent    = âˆ§-idempotent
   ; âˆ§-absorptive    = âˆ§-absorptive
   ; âˆ¨-associative   = âˆ¨-assoc
   ; âˆ¨-commutative   = âˆ¨-comm
   ; âˆ¨-unit          = âˆ¨-unit
   ; âˆ¨-idempotent    = âˆ¨-idempotent
   ; âˆ¨-absorptive    = âˆ¨-absorptive
   ; distributivityáµˆ = distributivity
   }

\end{code}

\begin{code}

to-distributive-latticeâ‚€ : (ğ“¤ : Universe)
                         â†’ DistributiveLattice ğ“¤
                         â†’ Distributive-Latticeâ‚€ ğ“¤
to-distributive-latticeâ‚€ ğ“¤ L = X , d , â€ 
 where
  open DistributiveLattice L

  d : Distributive-Lattice-Data X
  d = ğŸ , ğŸ , _âˆ§_ , _âˆ¨_

  â€  : satisfies-distributive-lattice-laws d
  â€  = X-is-set
    , âˆ§-associative
    , âˆ§-commutative
    , âˆ§-unit
    , âˆ§-idempotent
    , âˆ§-absorptive
    , âˆ¨-associative
    , âˆ¨-commutative
    , âˆ¨-unit
    , âˆ¨-idempotent
    , âˆ¨-absorptive
    , distributivityáµˆ

\end{code}

We now prove that these two maps form an equivalence, which follows trivially
from the definitional equality.

\begin{code}

distributive-latticeâ‚€-equivalent-to-distributive-lattice
 : (ğ“¤ : Universe)
 â†’ Distributive-Latticeâ‚€ ğ“¤ â‰ƒ DistributiveLattice ğ“¤
distributive-latticeâ‚€-equivalent-to-distributive-lattice ğ“¤ =
 to-distributive-lattice ğ“¤ , qinvs-are-equivs (to-distributive-lattice ğ“¤) â€ 
  where
   â€  : qinv (to-distributive-lattice ğ“¤)
   â€  = to-distributive-latticeâ‚€ ğ“¤ , (Î» _ â†’ refl) , (Î» _ â†’ refl)

\end{code}
