---
title:          Properties of the locale of spectra
author:         Ayberk Tosun
date-started:   2024-03-01
dates-updated:  [2024-03-27, 2024-04-08, 2024-04-09, 2024-06-05]
---

We define the spectrum locale over a distributive lattice `L`, the defining
frame of which is the frame of ideals over `L`.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import UF.PropTrunc
open import UF.FunExt
open import UF.Subsingletons
open import UF.Size

module Locales.DistributiveLattice.Spectrum-Properties
        (fe : Fun-Ext)
        (pe : Prop-Ext)
        (pt : propositional-truncations-exist)
        (sr : Set-Replacement pt)
       where

open import Locales.Compactness pt fe
open import Locales.DistributiveLattice.Definition fe pt
open import Locales.DistributiveLattice.Ideal pt fe pe
open import Locales.DistributiveLattice.Ideal-Properties pt fe pe
open import Locales.DistributiveLattice.Properties fe pt
open import Locales.DistributiveLattice.Spectrum fe pe pt
open import Locales.Frame pt fe
open import Locales.SmallBasis pt fe sr
open import Locales.Spectrality.SpectralLocale pt fe
open import MLTT.Fin hiding (ğŸ; ğŸ)
open import MLTT.List hiding ([_])
open import MLTT.Spartan
open import Slice.Family
open import UF.Classifiers
open import UF.Equiv renaming (_â–  to _ğğ„ğƒ)
open import UF.ImageAndSurjection pt
open import UF.Logic
open import UF.Powerset-MultiUniverse hiding (ğ•‹)
open import UF.SubtypeClassifier

open AllCombinators pt fe renaming (_âˆ§_ to _âˆ§â‚š_; _âˆ¨_ to _âˆ¨â‚š_)
open Locale
open PropositionalSubsetInclusionNotation fe
open PropositionalTruncation pt hiding (_âˆ¨_)

\end{code}

We work with a fixed distributive ğ“¤-lattice `L` in this module.

\begin{code}

module Spectrality (L : DistributiveLattice ğ“¤) where

 open DefnOfFrameOfIdeal  L
 open DistributiveLattice L renaming (X-is-set to Ïƒ)
 open IdealNotation L
 open IdealProperties L

\end{code}

We abbreviate the `spectrum` of `L` to `spec-L`.

\begin{code}

 private
  spec-L : Locale (ğ“¤ âº) ğ“¤ ğ“¤
  spec-L = spectrum

\end{code}

The locale `spec-L` is a compact locale.

\begin{code}

 spectrum-is-compact : is-compact spec-L holds
 spectrum-is-compact S Î´ p =
  âˆ¥âˆ¥-rec âˆƒ-is-prop â€  (p ğŸ (ğŸáµˆ-is-top L ğŸ))
   where
    â€  : Î£ xs ê‰ List X , xs â— S Ã— (ğŸ ï¼ join-listáµˆ L xs)
      â†’ âˆƒ i ê‰ index S , (ğŸ[ ğ’ª spec-L ] âŠ†áµ¢ (S [ i ])) holds
    â€  (xs , c , r) = âˆ¥âˆ¥-rec âˆƒ-is-prop â€¡ (finite-subcover S xs Î´ c)
     where
      â€¡ : Î£ k ê‰ index S , join-listáµˆ L xs âˆˆâ± (S [ k ])
        â†’ âˆƒ i ê‰ index S , (ğŸ[ ğ’ª spec-L ] âŠ†áµ¢ (S [ i ])) holds
      â€¡ (k , p) = âˆ£ k , contains-ğŸ-implies-above-ğŸ (S [ k ]) Î¼ âˆ£
       where
        Î¼ : ğŸ âˆˆâ± (S [ k ])
        Î¼ = transport (Î» - â†’ - âˆˆâ± (S [ k ])) (r â»Â¹) p

\end{code}

Added on 2024-03-13.

Every ideal `I` is the join of its principal ideals. We call this join the
_factorization_ of `I` into its join of principal ideals, and we denote by
`factorization` the function implementing this.

\begin{code}

 open PrincipalIdeals L
 open Joins _âŠ†áµ¢_

 factorization : Ideal L â†’ Ideal L
 factorization I = â‹[ ğ’ª spec-L ] principal-ideals-of I

 ideal-equal-to-factorization : (I : Ideal L) â†’ I ï¼ factorization I
 ideal-equal-to-factorization I =
  â‹[ ğ’ª spec-L ]-unique (principal-ideals-of I) I (â€  , â€¡)
   where
    â€  : (I is-an-upper-bound-of (principal-ideals-of I)) holds
    â€  = ideal-is-an-upper-bound-of-its-principal-ideals I

    â€¡ : ((Iáµ¤ , _) : upper-bound (principal-ideals-of I)) â†’ I âŠ†áµ¢ Iáµ¤ holds
    â€¡ (Iáµ¤ , Ï…) =
     ideal-is-lowerbound-of-upperbounds-of-its-principal-ideals I Iáµ¤ Ï…

\end{code}

The family of principal ideals in an ideal is a directed family.

\begin{code}

 factorization-is-directed : (I : Ideal L)
                           â†’ is-directed (ğ’ª spec-L) (principal-ideals-of I) holds
 factorization-is-directed = principal-ideals-of-ideal-form-a-directed-family

\end{code}

Added on 2024-03-27

For every `x : L`, the principal ideal `â†“x` is a compact open of the locale of
spectra.

\begin{code}

 principal-ideal-is-compact : (x : âˆ£ L âˆ£áµˆ) â†’ is-compact-open spec-L (â†“ x) holds
 principal-ideal-is-compact x S Î´ p = âˆ¥âˆ¥-rec âˆƒ-is-prop â€  Î¼
  where
   Î¼ : x âˆˆáµ¢ (â‹[ ğ’ª spec-L ] S) holds
   Î¼ = p x (â‰¤áµˆ-is-reflexive L x)

   â€  : Î£ xs ê‰ List X , xs â— S Ã— (x ï¼ join-listáµˆ L xs)
     â†’ âˆƒ i  ê‰ index S , â†“ x âŠ†áµ¢ (S [ i ]) holds
   â€  (xs , q , râ€²) = âˆ¥âˆ¥-rec âˆƒ-is-prop â€¡ Î²
    where
     Î² : âˆƒ i ê‰ index S , join-listáµˆ L xs âˆˆáµ¢ (S [ i ]) holds
     Î² = finite-subcover S xs Î´ q

     â€¡ : Î£ i ê‰ index S , join-listáµˆ L xs âˆˆáµ¢ (S [ i ]) holds
       â†’ âˆƒ i  ê‰ index S , â†“ x âŠ†áµ¢ (S [ i ]) holds
     â€¡ (i , r) = âˆ£ i , Î³ âˆ£
      where
       open Ideal (S [ i ]) renaming (I-is-downward-closed
                                      to Sáµ¢-is-downward-closed)

       Î³ : (â†“ x âŠ†áµ¢ (S [ i ])) holds
       Î³ y Ï† = Sáµ¢-is-downward-closed y (join-listáµˆ L xs) Ïµ r
        where
         Ïµ : (y â‰¤áµˆ[ L ] join-listáµˆ L xs) holds
         Ïµ = transport (Î» - â†’ (y â‰¤áµˆ[ L ] -) holds) râ€² Ï†

\end{code}

Added on 2024-06-05.

\begin{code}

 â†“â‚–_ : âˆ£ L âˆ£áµˆ â†’ Î£ I ê‰ Ideal L , (is-compact-open spec-L I holds)
 â†“â‚–_ x = â†“ x , principal-ideal-is-compact x

\end{code}

End of addition.

Added on 2024-03-13.

Every ideal has a directed covering family consisting of compact opens.

\begin{code}

 ideal-has-directed-cover-of-compact-opens
  : (I : Ideal L)
  â†’ has-a-directed-cover-of-compact-opens spec-L I holds
 ideal-has-directed-cover-of-compact-opens I =
  âˆ£ principal-ideals-of I , Îº , Î´ , eq âˆ£
   where
    Îº : consists-of-compact-opens spec-L (principal-ideals-of I) holds
    Îº (x , _) =  principal-ideal-is-compact x

    Î´ : is-directed (ğ’ª spec-L) (principal-ideals-of I) holds
    Î´ = principal-ideals-of-ideal-form-a-directed-family I

    eq : I ï¼ â‹[ ğ’ª spec-L ] principal-ideals-of I
    eq = ideal-equal-to-factorization I

\end{code}

Added on 2024-04-08.

We have already proved that every principal ideal is compact. We now prove
the converse of this: every compact ideal is the principal ideal on some
element `x` of the distributive lattice `L`.

\begin{code}

 compact-ideal-is-principal : (I : Ideal L)
                            â†’ is-compact-open spec-L I holds
                            â†’ âˆƒ x ê‰ âˆ£ L âˆ£áµˆ , I ï¼ principal-ideal x
 compact-ideal-is-principal I Îº =
  âˆ¥âˆ¥-rec âˆƒ-is-prop Î³ (Îº (principal-ideals-of I) Î´ câ‚€)
   where
    c : I ï¼ factorization I
    c = ideal-equal-to-factorization I

    câ‚€ : (I âŠ†áµ¢ factorization I) holds
    câ‚€ = reflexivity+ (poset-of (ğ’ª spec-L)) c

    câ‚ : (factorization I âŠ†áµ¢ I) holds
    câ‚ = reflexivity+ (poset-of (ğ’ª spec-L)) (c â»Â¹)

    Î´ : is-directed (ğ’ª spec-L) (principal-ideals-of I) holds
    Î´ = factorization-is-directed I

    Î³ : (Î£ (x , _) ê‰ index (principal-ideals-of I) , (I âŠ†áµ¢ â†“ x) holds)
      â†’ âˆƒ x ê‰ âˆ£ L âˆ£áµˆ , I ï¼ â†“ x
    Î³ ((x , p) , Ï†) = âˆ£ x , â‰¤-is-antisymmetric (poset-of (ğ’ª spec-L)) qâ‚ qâ‚‚ âˆ£
     where
      open Ideal I using (I-is-downward-closed)

      qâ‚ : I âŠ†áµ¢ â†“ x holds
      qâ‚ = Ï†

      qâ‚‚ : â†“ x âŠ†áµ¢ I holds
      qâ‚‚ y Î¼ = I-is-downward-closed y x Î¼ p

\end{code}

Added on 2024-04-08.

The map `â†“(-) : L â†’ Idl(L)` preserves meets.

\begin{code}

 principal-ideal-preserves-meets : (x y : âˆ£ L âˆ£áµˆ)
                                 â†’ â†“ (x âˆ§ y) ï¼ â†“ x âˆ§[ ğ’ª spec-L ] â†“ y
 principal-ideal-preserves-meets x y =
  â‰¤-is-antisymmetric (poset-of (ğ’ª spec-L)) â€  â€¡
   where
    open PosetReasoning (poset-ofáµˆ L)

    â€  : (â†“ (x âˆ§ y) âŠ†áµ¢ (â†“ x âˆ§[ ğ’ª spec-L ] â†“ y)) holds
    â€  z p = â€ â‚ , â€ â‚‚
     where
      â€ â‚ : (z â‰¤áµˆ[ L ] x) holds
      â€ â‚ = z â‰¤âŸ¨ p âŸ© x âˆ§ y â‰¤âŸ¨ âˆ§-is-a-lower-boundâ‚ L x y âŸ© x â– 

      â€ â‚‚ : (z â‰¤áµˆ[ L ] y) holds
      â€ â‚‚ = z â‰¤âŸ¨ p âŸ© x âˆ§ y â‰¤âŸ¨ âˆ§-is-a-lower-boundâ‚‚ L x y âŸ© y â– 

    â€¡ : ((â†“ x âˆ§[ ğ’ª spec-L ] â†“ y) âŠ†áµ¢ â†“ (x âˆ§ y)) holds
    â€¡ = âˆ§-is-greatest L x y

\end{code}

Added on 2024-06-05.

This has probably been written down somewhere else before.

\begin{code}

 principal-ideal-preserves-top : â†“ ğŸ ï¼ ğŸ[ ğ’ª spec-L ]
 principal-ideal-preserves-top = only-ğŸ-is-above-ğŸ (ğ’ª spec-L) (â†“ ğŸ) (Î» _ â†’ id)

 principal-ideal-preserves-bottom : â†“ ğŸ ï¼ ğŸ[ ğ’ª spec-L ]
 principal-ideal-preserves-bottom = only-ğŸ-is-below-ğŸ (ğ’ª spec-L) (â†“ ğŸ) â€ 
  where
   â€  : (â†“ ğŸ â‰¤[ poset-of (ğ’ª spec-L) ] ğŸ[ ğ’ª spectrum ]) holds
   â€  x Î¼ = transport (Î» - â†’ - âˆˆâ± ğŸ[ ğ’ª spectrum ]) (p â»Â¹) ideal-ğŸ-contains-ğŸ
    where
     open Ideal ğŸ[ ğ’ª spectrum ] renaming (I-contains-ğŸ to ideal-ğŸ-contains-ğŸ)

     p : x ï¼ ğŸ
     p = only-ğŸ-is-below-ğŸáµˆ L x Î¼

\end{code}

End of addition

Added on 2024-04-08.

The binary meet of two compact ideals is compact.

\begin{code}

 compacts-of-the-spectrum-are-closed-under-âˆ§
  : compacts-of-[ spec-L ]-are-closed-under-binary-meets holds
 compacts-of-the-spectrum-are-closed-under-âˆ§ Kâ‚ Kâ‚‚ Îºâ‚ Îºâ‚‚ = Îº
  where
   Î¹â‚ : âˆƒ xâ‚ ê‰ âˆ£ L âˆ£áµˆ , Kâ‚ ï¼ â†“ xâ‚
   Î¹â‚ = compact-ideal-is-principal Kâ‚ Îºâ‚

   Î¹â‚‚ : âˆƒ xâ‚‚ ê‰ âˆ£ L âˆ£áµˆ , Kâ‚‚ ï¼ â†“ xâ‚‚
   Î¹â‚‚ = compact-ideal-is-principal Kâ‚‚ Îºâ‚‚

   Îº : is-compact-open spec-L (Kâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚) holds
   Îº =
    âˆ¥âˆ¥-recâ‚‚ (holds-is-prop (is-compact-open spec-L (Kâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚))) â€  Î¹â‚ Î¹â‚‚
     where
      â€  : Î£ xâ‚ ê‰ âˆ£ L âˆ£áµˆ , Kâ‚ ï¼ â†“ xâ‚
        â†’ Î£ xâ‚‚ ê‰ âˆ£ L âˆ£áµˆ , Kâ‚‚ ï¼ â†“ xâ‚‚
        â†’ is-compact-open spec-L (Kâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚) holds
      â€  (xâ‚ , pâ‚) (xâ‚‚ , pâ‚‚) =
       transport (Î» - â†’ is-compact-open spec-L - holds) (q â»Â¹) â€¡
        where
         q : Kâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚ ï¼ â†“ (xâ‚ âˆ§ xâ‚‚)
         q = Kâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚       ï¼âŸ¨ â…  âŸ©
             â†“ xâ‚ âˆ§[ ğ’ª spec-L ] Kâ‚‚     ï¼âŸ¨ â…¡ âŸ©
             â†“ xâ‚ âˆ§[ ğ’ª spec-L ] â†“ xâ‚‚   ï¼âŸ¨ â…¢ âŸ©
             â†“ (xâ‚ âˆ§ xâ‚‚)               âˆ
              where
               â…  = ap (Î» - â†’ - âˆ§[ ğ’ª spec-L ] Kâ‚‚) pâ‚
               â…¡ = ap (Î» - â†’ â†“ xâ‚ âˆ§[ ğ’ª spec-L ] -) pâ‚‚
               â…¢ = principal-ideal-preserves-meets xâ‚ xâ‚‚ â»Â¹

         â€¡ : is-compact-open spec-L (â†“ (xâ‚ âˆ§ xâ‚‚)) holds
         â€¡ = principal-ideal-is-compact (xâ‚ âˆ§ xâ‚‚)

\end{code}

Added on 2024-04-08.

Finally, we package everything up into a proof that the spectrum locale is
spectral.

\begin{code}

 spec-L-is-spectral : is-spectral spec-L holds
 spec-L-is-spectral = (Îº , Î½) , ideal-has-directed-cover-of-compact-opens
  where
   Îº : is-compact spec-L holds
   Îº = spectrum-is-compact

   Î½ : compacts-of-[ spec-L ]-are-closed-under-binary-meets holds
   Î½ = compacts-of-the-spectrum-are-closed-under-âˆ§

\end{code}

Everything after this line has been added on 2024-04-09.

To show that the type of compact ideals is small, we directly construct the
intensional specified basis for `Idl(L)` given by the family `â†“(-) : L â†’ Idl(L)`.

\begin{code}

 â„¬-spec : Fam ğ“¤ âŸ¨ ğ’ª spec-L âŸ©
 â„¬-spec = âˆ£ L âˆ£áµˆ , principal-ideal

 open classifier-single-universe

 â„¬-spec-is-directed-basis : directed-basis-forá´° (ğ’ª spec-L) â„¬-spec
 â„¬-spec-is-directed-basis â„ = ğ•‹ ğ“¤ âˆ£ L âˆ£áµˆ (_âˆˆâ± â„) , â€  , ğ’¹
  where
   c : â„ ï¼ â‹[ ğ’ª spec-L ] â… â†“ x âˆ£ x Îµ ğ•‹ ğ“¤ âˆ£ L âˆ£áµˆ (_âˆˆâ± â„) â†
   c = ideal-equal-to-factorization â„

   â€  : (â„ is-lub-of â… â†“ x âˆ£ x Îµ ğ•‹ ğ“¤ âˆ£ L âˆ£áµˆ (_âˆˆâ± â„) â†) holds
   â€  = transport
        (Î» - â†’ (- is-lub-of â… â†“ x âˆ£ x Îµ ğ•‹ ğ“¤ âˆ£ L âˆ£áµˆ (_âˆˆâ± â„) â†) holds)
        (c â»Â¹)
        (â‹[ ğ’ª spec-L ]-upper _ , â‹[ ğ’ª spec-L ]-least _)

   ğ’¹ : is-directed (ğ’ª spec-L) â… â†“ x âˆ£ x Îµ (ğ•‹ ğ“¤ âˆ£ L âˆ£áµˆ (_âˆˆâ± â„)) â† holds
   ğ’¹ = factorization-is-directed â„

 â„¬-spec-is-basis : basis-forá´° (ğ’ª spec-L) â„¬-spec
 â„¬-spec-is-basis =
  directed-basis-is-basis (ğ’ª spec-L) â„¬-spec â„¬-spec-is-directed-basis

\end{code}

We denote by `ğ’¦-fam` the family corresponding to the subset of compact opens.

\begin{code}

 ğ’¦-fam : Fam (ğ“¤ âº) âŸ¨ ğ’ª spec-L âŸ©
 ğ’¦-fam = ğ•‹ (ğ“¤ âº) âŸ¨ ğ’ª spec-L âŸ© (_holds âˆ˜ is-compact-open spec-L)

\end{code}

We know that the image of `â†“(-) : L â†’ Idl(L)` is equivalent to type of compact
opens of `spec-L`.

\begin{code}

 image-â†“-equiv-to-ğ’¦ : image principal-ideal â‰ƒ ğ’¦ spec-L
 image-â†“-equiv-to-ğ’¦ = basic-iso-to-ğ’¦
                       spec-L
                       (â„¬-spec , â„¬-spec-is-directed-basis)
                       principal-ideal-is-compact

\end{code}

We also know that the image of `â†“(-)` is a ğ“¤-small type.

\begin{code}

 image-of-â†“-is-small : (image principal-ideal) is ğ“¤ small
 image-of-â†“-is-small =
  basic-is-small spec-L (â„¬-spec , â„¬-spec-is-directed-basis) Î³
   where
    open PosetNotation (poset-of (ğ’ª spec-L))

    Î³ : âŸ¨ ğ’ª spec-L âŸ© is-locally ğ“¤ small
    Î³ I J = (I â‰£ J) holds , s , (r , â€ ) , (r , â€¡)
     where
      s : (I â‰£ J) holds â†’ I ï¼ J
      s (pâ‚ , pâ‚‚) = â‰¤-is-antisymmetric (poset-of (ğ’ª spec-L)) pâ‚ pâ‚‚

      r : I ï¼ J â†’ (I â‰£ J) holds
      r p = transport (Î» - â†’ (- â‰£ J) holds) (p â»Â¹) (â‰£-is-reflexive poset-of-ideals J)

      â€  : s âˆ˜ r âˆ¼ id
      â€  p = carrier-of-[ poset-of-ideals ]-is-set (s (r p)) p

      â€¡ : r âˆ˜ s âˆ¼ id
      â€¡ p = holds-is-prop (I â‰£ J) (r (s p)) p

\end{code}

We use the superscript `(-)â»` to denote the small copy of the type `image â†“(-)`.

\begin{code}

 image-â†“â» : ğ“¤  Ì‡
 image-â†“â» = resized (image principal-ideal) image-of-â†“-is-small

\end{code}

From the previous two equivalences, we can conclude that the type of compact
opens of `spec-L` is equivalent to `image-â†“â»`.

\begin{code}

 image-â†“â»-equiv-to-ğ’¦ : image-â†“â» â‰ƒ ğ’¦ spec-L
 image-â†“â»-equiv-to-ğ’¦ = image-â†“â»               â‰ƒâŸ¨ â…  âŸ©
                       image principal-ideal  â‰ƒâŸ¨ â…¡ âŸ©
                       ğ’¦ spec-L               ğğ„ğƒ
                        where
                         â…  = resizing-condition image-of-â†“-is-small
                         â…¡ = image-â†“-equiv-to-ğ’¦

\end{code}

This means that `ğ’¦(spec-L)` is ğ“¤-small.

\begin{code}

 spec-L-has-small-ğ’¦ : has-small-ğ’¦ spec-L
 spec-L-has-small-ğ’¦ = image-â†“â» , image-â†“â»-equiv-to-ğ’¦

\end{code}
