Martin Escardo, 29th January 2019

If univalence holds, then any universe is embedded into any larger universe.

We do this without cumulativity, because it is not available in the
Martin-LoÌˆf type theory that we are working with in Agda.

Moreover, any map f : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡ with f X â‰ƒ X for all X : ğ“¤ Ì‡ is an
embedding, e.g. the map X â†¦ X + ğŸ˜ {ğ“¥}.

(Here the notion of a map being an embedding is stronger than that of
being left-cancellable, and it means that the fibers of the map are
propositions, or subsingletons, as in HoTT/UF.)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UF-Univalence

module UF-UniverseEmbedding where

open import SpartanMLTT
open import UF-Embedding
open import UF-Equiv
open import UF-EquivalenceExamples
open import UF-FunExt
open import UF-Equiv-FunExt
open import UF-UA-FunExt

universe-embedding-criterion : Univalence
                             â†’ (ğ“¤ ğ“¥ : Universe) (f : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡)
                             â†’ ((X : ğ“¤ Ì‡) â†’ f X â‰ƒ X)
                             â†’ is-embedding f
universe-embedding-criterion ua ğ“¤ ğ“¥ f i = embedding-criterion' f Î³
 where
  Î³ : (X X' : ğ“¤ Ì‡) â†’ (f X â‰¡ f X') â‰ƒ (X â‰¡ X')
  Î³ X X' =  (f X â‰¡ f X')  â‰ƒâŸ¨ is-univalent-â‰ƒ (ua (ğ“¤ âŠ” ğ“¥)) (f X) (f X') âŸ©
            (f X â‰ƒ f X')  â‰ƒâŸ¨ Eq-Eq-cong (FunExt-from-univalence ua) (i X) (i X') âŸ©
            (X â‰ƒ X')      â‰ƒâŸ¨ â‰ƒ-sym (is-univalent-â‰ƒ (ua ğ“¤) X X') âŸ©
            (X â‰¡ X')      â– 

\end{code}

For instance, the following function satisfies this condition and
hence is an embedding:

\begin{code}

lift : (ğ“¥ : Universe) â†’ ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
lift ğ“¥ X = X + ğŸ˜ {ğ“¥}

lift-â‰ƒ : (ğ“¥ : Universe) (X : ğ“¤ Ì‡)
              â†’ lift ğ“¥ X â‰ƒ X
lift-â‰ƒ ğ“¥ X = ğŸ˜-rneutral'

lift-is-embedding : Univalence â†’ is-embedding (lift {ğ“¤} ğ“¥)
lift-is-embedding {ğ“¤} {ğ“¥} ua = universe-embedding-criterion ua ğ“¤ ğ“¥ (lift ğ“¥) (lift-â‰ƒ ğ“¥)

\end{code}
