Martin Escardo, 29th January 2019

If univalence holds, then any universe is embedded into any larger universe.

We do this without cumulativity, because it is not available in the
Martin-LoÌˆf type theory that we are working with in Agda.

Moreover, any map f : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡ with f X â‰ƒ X for all X : ğ“¤ Ì‡ is an
embedding, e.g. the map X â†¦ X + ğŸ˜ {ğ“¥}.

(Here the notion of a map being an embedding is stronger than that of
being left-cancellable, and it means that the fibers of the map are
propositions, or subsingletons, as in HoTT/UF.)

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import UF-Univalence

module UF-UniverseEmbedding (ua : Univalence) where

open import SpartanMLTT
open import UF-Base
open import UF-Embedding
open import UF-Equiv
open import UF-EquivalenceExamples
open import UF-FunExt
open import UF-Equiv-FunExt
open import UF-UA-FunExt

\end{code}

The above module assumption "ua" is global univalence (univalence for
all universes), which gives global function extensionality
(extensionality for functions of types of any two universes). We
initially tried to use localized univalence and function
extensionality, and we succeeded, but then, for example, one lemma got
five function extensionality assumptions for various combinations of
universes, which is distracting and cumbersome.

\begin{code}

private
 fe : FunExt
 fe = FunExt-from-univalence ua

 nfe : {X : ğ“¤ Ì‡} {A : X â†’ ğ“¥ Ì‡} {f g : Î  A} â†’ f âˆ¼ g â†’ f â‰¡ g
 nfe {ğ“¤} {ğ“¥} = dfunext (fe ğ“¤ ğ“¥)

\end{code}

We begin with some general results about equivalences, which probably
should be moved to other univalent foundations modules in the future
as they are potentially of wider use, independently of the particular
application developed here.

\begin{code}

inverse-involutive : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f : X â†’ Y) (e : is-equiv f)
                   â†’ inverse (inverse f e) (inverse-is-equiv f e) â‰¡ f
inverse-involutive f e = refl

\end{code}

That the above proof is refl is an accident of our choice of notion of
equivalence as primary.

\begin{code}

â‰ƒ-assoc : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {Z : ğ“¦ Ì‡} {T : ğ“£ Ì‡}
          (Î± : X â‰ƒ Y) (Î² : Y â‰ƒ Z) (Î³ : Z â‰ƒ T)
        â†’ Î± â— (Î² â— Î³) â‰¡ (Î± â— Î²) â— Î³
â‰ƒ-assoc (f , a) (g , b) (h , c) = to-Î£-â‰¡ (p , q)
 where
  p : (h âˆ˜ g) âˆ˜ f â‰¡ h âˆ˜ (g âˆ˜ f)
  p = refl

  d e : is-equiv (h âˆ˜ g âˆ˜ f)
  d = âˆ˜-is-equiv a (âˆ˜-is-equiv b c)
  e = âˆ˜-is-equiv (âˆ˜-is-equiv a b) c

  q : transport is-equiv p d â‰¡ e
  q = being-equiv-is-a-prop fe (h âˆ˜ g âˆ˜ f) _ _

\end{code}

The above proof can be condensed to one line in the style of the
following two proofs, which exploit the fact that the identity map is
a neutral element for ordinary function composition, definitionally:

\begin{code}

â‰ƒ-refl-left : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (Î± : X â‰ƒ Y) â†’ â‰ƒ-refl X â— Î± â‰¡ Î±
â‰ƒ-refl-left Î± = to-Î£-â‰¡ (refl , being-equiv-is-a-prop fe _ _ _)

â‰ƒ-refl-right : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (Î± : X â‰ƒ Y) â†’ Î± â— â‰ƒ-refl Y â‰¡ Î±
â‰ƒ-refl-right Î± = to-Î£-â‰¡ (refl , being-equiv-is-a-prop fe _ _ _)

â‰ƒ-sym-involutive : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (Î± : X â‰ƒ Y) â†’ â‰ƒ-sym (â‰ƒ-sym Î±) â‰¡ Î±
â‰ƒ-sym-involutive (f , a) = to-Î£-â‰¡ (p , being-equiv-is-a-prop fe f _ a)
 where
  p : inverse (inverse f a) (inverse-is-equiv f a) â‰¡ f
  p = inverse-involutive f a

â‰ƒ-Sym : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â‰ƒ Y) â‰ƒ (Y â‰ƒ X)
â‰ƒ-Sym = qinveq â‰ƒ-sym (â‰ƒ-sym , â‰ƒ-sym-involutive , â‰ƒ-sym-involutive)

â‰ƒ-sym-left-inverse : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (Î± : X â‰ƒ Y) â†’ â‰ƒ-sym Î± â— Î± â‰¡ â‰ƒ-refl Y
â‰ƒ-sym-left-inverse (f , e) = to-Î£-â‰¡ (p , being-equiv-is-a-prop fe _ _ _)
 where
  p : f âˆ˜ inverse f e â‰¡ id
  p = nfe (inverse-is-section f e)

â‰ƒ-sym-right-inverse : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (Î± : X â‰ƒ Y) â†’ Î± â— â‰ƒ-sym Î± â‰¡ â‰ƒ-refl X
â‰ƒ-sym-right-inverse (f , e) = to-Î£-â‰¡ (p , being-equiv-is-a-prop fe _ _ _)
 where
  p : inverse f e âˆ˜ f â‰¡ id
  p = nfe (inverse-is-retraction f e)

â‰ƒ-Comp : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {Z : ğ“¦ Ì‡} â†’ X â‰ƒ Y â†’ (Y â‰ƒ Z) â‰ƒ (X â‰ƒ Z)
â‰ƒ-Comp Î± = qinveq (Î± â—_) ((â‰ƒ-sym Î± â—_), p , q)
 where
  p = Î» Î² â†’ â‰ƒ-sym Î± â— (Î± â— Î²) â‰¡âŸ¨ â‰ƒ-assoc (â‰ƒ-sym Î±) Î± Î² âŸ©
            (â‰ƒ-sym Î± â— Î±) â— Î² â‰¡âŸ¨ ap (_â— Î²) (â‰ƒ-sym-left-inverse Î±) âŸ©
            â‰ƒ-refl _ â— Î²      â‰¡âŸ¨ â‰ƒ-refl-left _ âŸ©
            Î²                 âˆ

  q = Î» Î³ â†’ Î± â— (â‰ƒ-sym Î± â— Î³) â‰¡âŸ¨ â‰ƒ-assoc Î± (â‰ƒ-sym Î±) Î³ âŸ©
            (Î± â— â‰ƒ-sym Î±) â— Î³ â‰¡âŸ¨ ap (_â— Î³) (â‰ƒ-sym-right-inverse Î±) âŸ©
            â‰ƒ-refl _ â— Î³      â‰¡âŸ¨ â‰ƒ-refl-left _ âŸ©
            Î³ âˆ

\end{code}

One could be tempted to attempt prove the following by instead
assuming, with the aid of univalence, X â‰¡ A and Y â‰¡ B and then using
identity-type induction. However, in the absence of cumulativity, the
expressions "X â‰¡ A" and "Y â‰¡ B" don't make sense as they are not
well-typed. A similar remark applies to the above development.

\begin{code}

Id-Eq-congruence : (X Y : ğ“¤ Ì‡) (A B : ğ“¥ Ì‡)
                    â†’ X â‰ƒ A â†’ Y â‰ƒ B â†’ (X â‰¡ Y) â‰ƒ (A â‰¡ B)
Id-Eq-congruence {ğ“¤} {ğ“¥} X Y A B Î± Î² =
 (X â‰¡ Y) â‰ƒâŸ¨ is-univalent-â‰ƒ (ua ğ“¤) X Y âŸ©
 (X â‰ƒ Y) â‰ƒâŸ¨ â‰ƒ-Comp (â‰ƒ-sym Î±) âŸ©
 (A â‰ƒ Y) â‰ƒâŸ¨ â‰ƒ-Sym âŸ©
 (Y â‰ƒ A) â‰ƒâŸ¨ â‰ƒ-Comp (â‰ƒ-sym Î²) âŸ©
 (B â‰ƒ A) â‰ƒâŸ¨ â‰ƒ-Sym âŸ©
 (A â‰ƒ B) â‰ƒâŸ¨ â‰ƒ-sym (is-univalent-â‰ƒ (ua ğ“¥) A B) âŸ©
 (A â‰¡ B)  â– 

\end{code}

The terminology for the above construction is perhaps clearer with the
following reformulation of its statement:

\begin{code}

Id-Eq-congruence' : (X Y : ğ“¤ Ì‡) (A B : ğ“¥ Ì‡)
                     â†’ Eq X A â†’ Eq Y B â†’ Eq (Id X Y) (Id A B)
Id-Eq-congruence' = Id-Eq-congruence

\end{code}

With this, we can prove the promised result:

\begin{code}

universe-embedding-criterion : (ğ“¤ ğ“¥ : Universe) (f : ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡)
                             â†’ ((X : ğ“¤ Ì‡) â†’ f X â‰ƒ X)
                             â†’ is-embedding f
universe-embedding-criterion ğ“¤ ğ“¥ f i = embedding-criterion' f Î³
 where
  Î³ : (X X' : ğ“¤ Ì‡) â†’ (f X â‰¡ f X') â‰ƒ (X â‰¡ X')
  Î³ X X' = Id-Eq-congruence (f X) (f X') X X' (i X) (i X')

\end{code}

For instance, the function X â†¦ X + ğŸ˜ is an embedding of the universe ğ“¤
into the universe ğ“¤ âŠ” ğ“¥, where ğŸ˜ is taken to live in the universe ğ“¥:

\begin{code}

module example where

 universe-up : (ğ“¤ ğ“¥ : Universe) â†’ ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
 universe-up ğ“¤ ğ“¥ X = X + ğŸ˜ {ğ“¥}

 universe-up-is-embedding : is-embedding (universe-up ğ“¤ ğ“¥)
 universe-up-is-embedding {ğ“¤} {ğ“¥} = universe-embedding-criterion ğ“¤ ğ“¥
                                      (universe-up ğ“¤ ğ“¥)
                                      (Î» X â†’ ğŸ˜-rneutral' {ğ“¤} {ğ“¥} {X})

\end{code}

But, of course, there are many other naturally occurring embeddings

ğ“¤ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡, such as e.g. X â†¦ X Ã— ğŸ™ {ğ“¥}, or the one provided in the
Agda standard library (called 'Lift'), defined as a record.
