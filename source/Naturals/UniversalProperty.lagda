Tom de Jong (adapted from Martin's MGS lecture notes)
18 September 2020

We show that the type of natural numbers enjoys the universal property of a
natural numbers object. We generalize
https://www.cs.bham.ac.uk/~mhe/HoTT-UF.in-Agda-Lecture-Notes/HoTT-UF-Agda.html#â„•-is-nno
here from nondependent functions to dependent functions.

\begin{code}

{-# OPTIONS --safe --without-K #-}

module Naturals.UniversalProperty where

open import MLTT.NaturalNumbers

open import MLTT.Spartan
open import UF.Base
open import UF.Equiv
open import UF.FunExt
open import UF.Retracts
open import UF.Subsingletons

â„•-induction-retract : funext ğ“¤â‚€ ğ“¤
                    â†’ (Y : â„• â†’ ğ“¤ Ì‡ ) (yâ‚€ : Y 0) (g : (n : â„•) â†’ Y n â†’ Y (succ n))
                    â†’ (Î£ h ê‰ (Î  Y) , (h 0 ï¼ yâ‚€) Ã—
                                     ((n : â„•) â†’ h (succ n) ï¼ g n (h n)))
                    â— (Î£ h ê‰ (Î  Y) , h ï¼ induction yâ‚€ g)
â„•-induction-retract fe Y yâ‚€ g = Î£-retract _ _ Î³
 where
  Î³ : (h : Î  Y)
    â†’ (h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n))
    â— (h ï¼ induction yâ‚€ g)
  Î³ h =  (h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n)) â—âŸ¨ i  âŸ©
         (h âˆ¼ induction yâ‚€ g)                            â—âŸ¨ ii âŸ©
         (h ï¼ induction yâ‚€ g)                            â—€
   where
    ii = â‰ƒ-gives-â— (â‰ƒ-sym (â‰ƒ-funext fe h (induction yâ‚€ g)))
    i  = r , s , Î·
     where
      r : h âˆ¼ induction yâ‚€ g
        â†’ (h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n))
      r H = H 0 , (Î» n â†’ h (succ n)              ï¼âŸ¨ H (succ n)          âŸ©
                         induction yâ‚€ g (succ n) ï¼âŸ¨ refl                âŸ©
                         g n (induction yâ‚€ g n)  ï¼âŸ¨ ap (g n) ((H n) â»Â¹) âŸ©
                         g n (h n)               âˆ)
      s : (h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n))
        â†’ h âˆ¼ induction yâ‚€ g
      s (p , K) 0 = p
      s (p , K) (succ n) = h (succ n)              ï¼âŸ¨ K n                    âŸ©
                           g n (h n)               ï¼âŸ¨ ap (g n) (s (p , K) n) âŸ©
                           g n (induction yâ‚€ g n)  ï¼âŸ¨ refl                   âŸ©
                           induction yâ‚€ g (succ n) âˆ
      Î· : r âˆ˜ s âˆ¼ id
      Î· (p , K) =
       r (s (p , K))                                      ï¼âŸ¨ refl âŸ©
       (p , Î» n â†’ s (p , K) (succ n)
                  âˆ™ (refl âˆ™ ap (g n) ((s (p , K) n) â»Â¹))) ï¼âŸ¨ Ï†    âŸ©
       (p , K)                                            âˆ
         where
          Ï† = ap (p ,_) (dfunext fe Ïˆ)
           where
            Ïˆ : (n : â„•)
              â†’ s (p , K) (succ n) âˆ™ (refl âˆ™ ap (g n) (s (p , K) n â»Â¹))
              ï¼ K n
            Ïˆ n = s (p , K) (succ n)
                    âˆ™ (refl âˆ™ ap (g n) (s (p , K) n â»Â¹))   ï¼âŸ¨ refl âŸ©
                  K n âˆ™ ap (g n) (s (p , K) n)
                    âˆ™ (refl âˆ™ ap (g n) ((s (p , K) n) â»Â¹)) ï¼âŸ¨ I    âŸ©
                  K n âˆ™ ap (g n) (s (p , K) n)
                    âˆ™ ap (g n) ((s (p , K) n) â»Â¹)          ï¼âŸ¨ II   âŸ©
                  K n âˆ™ (ap (g n) (s (p , K) n)
                    âˆ™ ap (g n) ((s (p , K) n) â»Â¹))         ï¼âŸ¨ III  âŸ©
                  K n âˆ™ (ap (g n) (s (p , K) n)
                    âˆ™ (ap (g n) (s (p , K) n)) â»Â¹)         ï¼âŸ¨ IV   âŸ©
                  K n âˆ™ refl                               ï¼âŸ¨ refl âŸ©
                  K n                                      âˆ
             where
              I   = ap (K n âˆ™ ap (g n) (s (p , K) n) âˆ™_)
                     (refl-left-neutral {_} {_} {_} {_}
                       {ap (g n) ((s (p , K) n) â»Â¹)})
              II  = âˆ™assoc
                     (K n)
                     (ap (g n) (s (p , K) n))
                     (ap (g n) ((s (p , K) n) â»Â¹))
              III = ap (Î» - â†’ K n âˆ™ (ap (g n) (s (p , K) n) âˆ™ -))
                     ((ap-sym (g n) (s (p , K) n)) â»Â¹)
              IV  = ap (K n âˆ™_)
                     ((right-inverse (ap (g n) (s (p , K) n))) â»Â¹)

â„•-is-nno-dep : funext ğ“¤â‚€ ğ“¤
             â†’ (Y : â„• â†’ ğ“¤ Ì‡ ) (yâ‚€ : Y 0) (g : (n : â„•) â†’ Y n â†’ Y (succ n))
             â†’ âˆƒ! h ê‰ (Î  Y) , ((h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n)))
â„•-is-nno-dep {ğ“¤} fe Y yâ‚€ g = Î³
 where
  Î³ : is-singleton
       (Î£ h ê‰ (Î  Y) , (h 0 ï¼ yâ‚€) Ã— ((n : â„•) â†’ h (succ n) ï¼ g n (h n)))
  Î³ = retract-of-singleton (â„•-induction-retract fe Y yâ‚€ g)
       (singleton-types'-are-singletons (induction {ğ“¤} {Y} yâ‚€ g))


\end{code}
