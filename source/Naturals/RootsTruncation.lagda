Martin Escardo, early 2013, typed 5th May 2018

We show that the type of roots of a function Œ± : ‚Ñï ‚Üí Z has a
propositional truncation, in pure spartan Martin-L√∂f theory (without
using function extensionality). We also show that if we already have
truncations, we can "exit" the truncation of the set of roots.

The following can be specialized to any type Z with an isolated point
z taken as an abstract zero, including ‚Ñï and ùüö with any of its
points. Recall that a point of a type is called isolated if its
equality with any other point of the type is decidable.

\begin{code}

{-# OPTIONS --safe --without-K #-}

open import MLTT.Spartan

open import UF.DiscreteAndSeparated
open import UF.Base

module Naturals.RootsTruncation
        (ùì§ : Universe)
        (Z : ùì§ Ãá )
        (z : Z)
        (z-is-isolated : is-isolated' z)
       where

open import MLTT.Plus-Properties
open import Naturals.Order
open import Notation.Order
open import UF.Subsingletons
open import UF.KrausLemma
open import UF.Hedberg

\end{code}

We now consider whether there is or there isn't a minimal root
(strictly) bounded by a number k, where a root of Œ± is an n : ‚Ñï with Œ±
n Ôºù z.

\begin{code}

_has-no-root<_ : (‚Ñï ‚Üí Z) ‚Üí ‚Ñï ‚Üí ùì§ Ãá
Œ± has-no-root< k = (n : ‚Ñï) ‚Üí n < k ‚Üí Œ± n ‚â† z

_has-a-minimal-root<_ : (‚Ñï ‚Üí Z) ‚Üí ‚Ñï ‚Üí ùì§ Ãá
Œ± has-a-minimal-root< k = Œ£ m Íûâ ‚Ñï , (Œ± m Ôºù z)
                                     √ó (m < k)
                                     √ó Œ± has-no-root< m

FPO : ‚Ñï ‚Üí (‚Ñï ‚Üí Z) ‚Üí ùì§ Ãá
FPO k Œ± = Œ± has-a-minimal-root< k
        + Œ± has-no-root< k

\end{code}

The above "finite principle of omniscience" is a univalent proposition
using functional extensionality. However, we want to avoid function
extensionality here.

\begin{code}

fpo : ‚àÄ k Œ± ‚Üí FPO k Œ±
fpo zero Œ± = inr (Œª n p ‚Üí ùüò-elim p)
fpo (succ k) Œ± = cases f g (fpo k Œ±)
 where
  f : Œ± has-a-minimal-root< k ‚Üí FPO (succ k) Œ±
  f (m , p , l , œÜ) = inl (m , p , ‚â§-trans (succ m) k (succ k) l (‚â§-succ k) , œÜ)

  g : Œ± has-no-root< k ‚Üí FPO (succ k) Œ±
  g œÜ = cases g‚ÇÄ g‚ÇÅ (z-is-isolated (Œ± k))
   where
    g‚ÇÄ : Œ± k Ôºù z ‚Üí FPO (succ k) Œ±
    g‚ÇÄ p = inl (k , p , ‚â§-refl k , œÜ)

    g‚ÇÅ : Œ± k ‚â† z ‚Üí FPO (succ k) Œ±
    g‚ÇÅ u = inr (bounded-‚àÄ-next (Œª n ‚Üí Œ± n ‚â† z) k u œÜ)

\end{code}

Given any root, we can find a minimal root.

\begin{code}

minimal-root : ‚àÄ Œ± n ‚Üí Œ± n Ôºù z ‚Üí Œ± has-a-minimal-root< (succ n)
minimal-root Œ± n p = Right-fails-gives-left-holds (fpo (succ n) Œ±) g
 where
  g : ¬¨ (Œ± has-no-root< (succ n))
  g œÜ = œÜ n (‚â§-refl n) p

\end{code}

With this we can define a constant endomap on the type of roots, that
given any root finds a minimal root. Notice that the type of roots may
be empty, and still the function is well defined.

\begin{code}

roots : (‚Ñï ‚Üí Z) ‚Üí ùì§ Ãá
roots Œ± = Œ£ n Íûâ ‚Ñï , Œ± n Ôºù z

ŒºœÅ : (Œ± : ‚Ñï ‚Üí Z) ‚Üí roots Œ± ‚Üí roots Œ±
ŒºœÅ Œ± (n , p) = pr‚ÇÅ (minimal-root Œ± n p) , pr‚ÇÅ (pr‚ÇÇ (minimal-root Œ± n p))

ŒºœÅ-root : (Œ± : ‚Ñï ‚Üí Z) ‚Üí roots Œ± ‚Üí ‚Ñï
ŒºœÅ-root Œ± r = pr‚ÇÅ (ŒºœÅ Œ± r)

ŒºœÅ-root-is-root : (Œ± : ‚Ñï ‚Üí Z) (r : roots Œ±) ‚Üí Œ± (ŒºœÅ-root Œ± r) Ôºù z
ŒºœÅ-root-is-root Œ± r = pr‚ÇÇ (ŒºœÅ Œ± r)

ŒºœÅ-root-minimal : (Œ± : ‚Ñï ‚Üí Z) (m : ‚Ñï) (p : Œ± m Ôºù z)
                ‚Üí (n : ‚Ñï) ‚Üí Œ± n Ôºù z ‚Üí ŒºœÅ-root Œ± (m , p) ‚â§ n
ŒºœÅ-root-minimal Œ± m p n q = not-less-bigger-or-equal (ŒºœÅ-root Œ± (m , p)) n (f (¬¨¬¨-intro q))
 where
  f : ¬¨ (Œ± n ‚â† z) ‚Üí ¬¨ (n < ŒºœÅ-root Œ± (m , p))
  f = contrapositive (pr‚ÇÇ(pr‚ÇÇ(pr‚ÇÇ (minimal-root Œ± m p))) n)

ŒºœÅ-constant : (Œ± : ‚Ñï ‚Üí Z) ‚Üí wconstant (ŒºœÅ Œ±)
ŒºœÅ-constant Œ± (n , p) (n' , p') = r
 where
  m m' : ‚Ñï
  m  = ŒºœÅ-root Œ± (n , p)
  m' = ŒºœÅ-root Œ± (n' , p')

  l : m ‚â§ m'
  l = ŒºœÅ-root-minimal Œ± n p m' (ŒºœÅ-root-is-root Œ± (n' , p'))

  l' : m' ‚â§ m
  l' = ŒºœÅ-root-minimal Œ± n' p' m (ŒºœÅ-root-is-root Œ± (n , p))

  q : m Ôºù m'
  q = ‚â§-anti _ _ l l'

  r : ŒºœÅ Œ± (n , p) Ôºù ŒºœÅ Œ± (n' , p')
  r = to-Œ£-Ôºù (q , isolated-Id-is-prop z z-is-isolated _ _ _)

roots-has-prop-truncation : (Œ± : ‚Ñï ‚Üí Z) ‚Üí ‚àÄ ùì• ‚Üí has-prop-truncation ùì• (roots Œ±)
roots-has-prop-truncation Œ± = collapsible-has-prop-truncation (ŒºœÅ Œ± , ŒºœÅ-constant Œ±)

\end{code}

Explicitly (and repeating the construction of roots-has-prop-truncation):

\begin{code}

roots-truncation : (‚Ñï ‚Üí Z) ‚Üí ùì§ Ãá
roots-truncation Œ± = Œ£ r Íûâ roots Œ± , r Ôºù ŒºœÅ Œ± r

roots-truncation-is-prop : (Œ± : ‚Ñï ‚Üí Z) ‚Üí is-prop (roots-truncation Œ±)
roots-truncation-is-prop Œ± = fix-is-prop (ŒºœÅ Œ±) (ŒºœÅ-constant Œ±)

roots-Œ∑ : (Œ± : ‚Ñï ‚Üí Z) ‚Üí roots Œ± ‚Üí roots-truncation Œ±
roots-Œ∑ Œ± = to-fix (ŒºœÅ Œ±) (ŒºœÅ-constant Œ±)

roots-universal : (Œ± : ‚Ñï ‚Üí Z) (P : ùì• Ãá )
                ‚Üí is-prop P ‚Üí (roots Œ± ‚Üí P) ‚Üí roots-truncation Œ± ‚Üí P
roots-universal Œ± P _ f t = f (from-fix (ŒºœÅ Œ±) t)

\end{code}

We can't normally "exit a truncation", but in this special case we can:

\begin{code}

roots-exit-truncation : (Œ± : ‚Ñï ‚Üí Z) ‚Üí roots-truncation Œ± ‚Üí roots Œ±
roots-exit-truncation Œ± = from-fix (ŒºœÅ Œ±)

\end{code}

Of course, if we already have propositional truncations, we can exit
root truncations using the above technique.

\begin{code}

open import UF.PropTrunc

module ExitRootTruncations (pt : propositional-truncations-exist) where

 open PropositionalTruncation pt

 exit-roots-truncation : (Œ± : ‚Ñï ‚Üí Z) ‚Üí (‚àÉ n Íûâ ‚Ñï , Œ± n Ôºù z) ‚Üí Œ£ n Íûâ ‚Ñï , Œ± n Ôºù z
 exit-roots-truncation Œ± = h ‚àò g
  where
   f : (Œ£ n Íûâ ‚Ñï , Œ± n Ôºù z) ‚Üí fix (ŒºœÅ Œ±)
   f = to-fix (ŒºœÅ Œ±) (ŒºœÅ-constant Œ±)

   g : ‚à•(Œ£ n Íûâ ‚Ñï , Œ± n Ôºù z)‚à• ‚Üí fix (ŒºœÅ Œ±)
   g = ‚à•‚à•-rec (fix-is-prop (ŒºœÅ Œ±) (ŒºœÅ-constant Œ±)) f

   h : fix (ŒºœÅ Œ±) ‚Üí Œ£ n Íûâ ‚Ñï , Œ± n Ôºù z
   h = from-fix (ŒºœÅ Œ±)

\end{code}

This says that if there is a root, then we can find one.
