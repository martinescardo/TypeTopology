Martin Escardo, started 5th May 2018

\begin{code}

{-# OPTIONS --without-K --exact-split --safe --no-sized-types --no-guardedness --auto-inline #-}

module Naturals.Order where

open import MLTT.Spartan

open import Ordinals.Notions
open import UF.Subsingletons
open import Naturals.Addition renaming (_+_ to _+'_)
open import Naturals.Properties
open import Notation.Order

_â‰¤â„•_ : â„• â†’ â„• â†’ ğ“¤â‚€ Ì‡
zero â‰¤â„• n        = ğŸ™
succ m â‰¤â„• zero   = ğŸ˜
succ m â‰¤â„• succ n = m â‰¤â„• n

instance
 Order-â„•-â„• : Order â„• â„•
 _â‰¤_ {{Order-â„•-â„•}} = _â‰¤â„•_

â‰¤-is-prop-valued : (m n : â„•) â†’ is-prop (m â‰¤ n)
â‰¤-is-prop-valued zero     n        = ğŸ™-is-prop
â‰¤-is-prop-valued (succ m) zero     = ğŸ˜-is-prop
â‰¤-is-prop-valued (succ m) (succ n) = â‰¤-is-prop-valued m n

open import UF.Base
open import UF.Miscelanea

right-addition-is-embedding : (m n : â„•) â†’ is-prop (Î£ k ê‰ â„• , k +' m ï¼ n)
right-addition-is-embedding zero n (.n , refl) (.n , refl) = refl
right-addition-is-embedding (succ m) zero (k , p) (k' , p') = ğŸ˜-elim (positive-not-zero (k +' m) p)
right-addition-is-embedding (succ m) (succ n) (k , p) (k' , p') = to-Î£-ï¼ (ap prâ‚ IH , â„•-is-set _ _)
 where
  IH : k , succ-lc p ï¼ k' , succ-lc p'
  IH = right-addition-is-embedding m n (k , succ-lc p) (k' , succ-lc p')

subtraction : (m n : â„•) â†’ m â‰¤ n â†’ Î£ k ê‰ â„• , k +' m ï¼ n
subtraction zero n l = n , refl
subtraction (succ m) zero l = ğŸ˜-elim l
subtraction (succ m) (succ n) l = prâ‚ IH , ap succ (prâ‚‚ IH)
 where
  IH : Î£ k ê‰ â„• , k +' m ï¼ n
  IH = subtraction m n l

cosubtraction : (m n : â„•) â†’ (Î£ k ê‰ â„• , k +' m ï¼ n) â†’ m â‰¤ n
cosubtraction zero n (.n , refl) = â‹†
cosubtraction (succ m) zero (k , p) = positive-not-zero (k +' m) p
cosubtraction (succ m) (succ .(k +' m)) (k , refl) = cosubtraction m (k +' m) (k , refl)

zero-least : (n : â„•) â†’ zero â‰¤ n
zero-least n = â‹†

zero-least' : (n : â„•) â†’ Â¬ (succ n â‰¤ zero)
zero-least' n l = l

zero-least'' : (n : â„•) â†’ n â‰¤ zero â†’ n ï¼ zero
zero-least'' zero l = refl

succ-monotone : (m n : â„•) â†’ m â‰¤ n â†’ succ m â‰¤ succ n
succ-monotone m n l = l

succ-order-injective : (m n : â„•) â†’ succ m â‰¤ succ n â†’ m â‰¤ n
succ-order-injective m n l = l

â‰¤-induction : (P : (m n : â„•) (l : m â‰¤ n) â†’ ğ“¤ Ì‡ )
            â†’ ((n : â„•) â†’ P zero n (zero-least n))
            â†’ ((m n : â„•) (l : m â‰¤ n) â†’ P m n l â†’ P (succ m) (succ n) (succ-monotone m n l))
            â†’ (m n : â„•) (l : m â‰¤ n) â†’ P m n l
â‰¤-induction P b f zero n â‹†            = b n
â‰¤-induction P b f (succ m) zero l     = ğŸ˜-elim l
â‰¤-induction P b f (succ m) (succ n) l = f m n l (â‰¤-induction P b f m n l)

succâ‰¤ï¼ : (m n : â„•) â†’ (succ m â‰¤ succ n) ï¼ (m â‰¤ n)
succâ‰¤ï¼ m n = refl

â‰¤-refl : (n : â„•) â†’ n â‰¤ n
â‰¤-refl zero     = â‹†
â‰¤-refl (succ n) = â‰¤-refl n

â‰¤-trans : (l m n : â„•) â†’ l â‰¤ m â†’ m â‰¤ n â†’ l â‰¤ n
â‰¤-trans zero m n p q = â‹†
â‰¤-trans (succ l) zero n p q = ğŸ˜-elim p
â‰¤-trans (succ l) (succ m) zero p q = ğŸ˜-elim q
â‰¤-trans (succ l) (succ m) (succ n) p q = â‰¤-trans l m n p q

â‰¤-anti : (m n : â„•) â†’ m â‰¤ n â†’ n â‰¤ m â†’ m ï¼ n
â‰¤-anti zero zero p q = refl
â‰¤-anti zero (succ n) p q = ğŸ˜-elim q
â‰¤-anti (succ m) zero p q = ğŸ˜-elim p
â‰¤-anti (succ m) (succ n) p q = ap succ (â‰¤-anti m n p q)

â‰¤-succ : (n : â„•) â†’ n â‰¤ succ n
â‰¤-succ zero     = â‹†
â‰¤-succ (succ n) = â‰¤-succ n

unique-least : (n : â„•) â†’ n â‰¤ zero â†’ n ï¼ zero
unique-least zero l = refl
unique-least (succ n) l = ğŸ˜-elim l

â‰¤-split : (m n : â„•) â†’ m â‰¤ succ n â†’ (m â‰¤ n) + (m ï¼ succ n)
â‰¤-split zero n l = inl l
â‰¤-split (succ m) zero l = inr (ap succ (unique-least m l))
â‰¤-split (succ m) (succ n) l = cases inl (inr âˆ˜ (ap succ)) (â‰¤-split m n l)

â‰¤-join : (m n : â„•) â†’ (m â‰¤ n) + (m ï¼ succ n) â†’ m â‰¤ succ n
â‰¤-join m n (inl l) = â‰¤-trans m n (succ n) l (â‰¤-succ n)
â‰¤-join .(succ n) n (inr refl) = â‰¤-refl n

â‰¤-down : (m n : â„•) â†’ m â‰¤ succ n â†’ (m â‰  succ n) â†’ (m â‰¤ n)
â‰¤-down m n l u = cases id (Î» p â†’ ğŸ˜-elim (u p)) (â‰¤-split m n l)

â‰¤-+ : (m n : â„•) â†’ (m â‰¤ m +' n)
â‰¤-+ m zero     = â‰¤-refl m
â‰¤-+ m (succ n) = â‰¤-join m (m +' n) (inl IH)
 where
  IH : m â‰¤ m +' n
  IH = â‰¤-+ m n

â‰¤-+' : (m n : â„•) â†’ (n â‰¤ m +' n)
â‰¤-+' m n = transport (Î» k â†’ n â‰¤ k) Î³ (â‰¤-+ n m)
 where
  Î³ : n +' m ï¼ m +' n
  Î³ = addition-commutativity n m

_<â„•_ : â„• â†’ â„• â†’ ğ“¤â‚€ Ì‡
m <â„• n = succ m â‰¤â„• n

instance
 Strict-Order-â„•-â„• : Strict-Order â„• â„•
 _<_ {{Strict-Order-â„•-â„•}} = _<â„•_

<-succ : (n : â„•) â†’ n < succ n
<-succ = â‰¤-refl

not-less-than-itself : (n : â„•) â†’ Â¬ (n < n)
not-less-than-itself zero l = l
not-less-than-itself (succ n) l = not-less-than-itself n l

not-less-bigger-or-equal : (m n : â„•) â†’ Â¬ (n < m) â†’ n â‰¥ m
not-less-bigger-or-equal zero n u = zero-least n
not-less-bigger-or-equal (succ m) zero = Â¬Â¬-intro (zero-least m)
not-less-bigger-or-equal (succ m) (succ n) = not-less-bigger-or-equal m n

bigger-or-equal-not-less : (m n : â„•) â†’ n â‰¥ m â†’ Â¬ (n < m)
bigger-or-equal-not-less m n l u = not-less-than-itself n (â‰¤-trans (succ n) m n u l)

less-not-bigger-or-equal : (m n : â„•) â†’ m < n â†’ Â¬ (n â‰¤ m)
less-not-bigger-or-equal m n l u = bigger-or-equal-not-less n m u l

bounded-âˆ€-next : (A : â„• â†’ ğ“¤ Ì‡ ) (k : â„•)
               â†’ A k
               â†’ ((n : â„•) â†’ n < k â†’ A n)
               â†’ (n : â„•) â†’ n < succ k â†’ A n
bounded-âˆ€-next A k a Ï† n l = cases f g s
 where
  s : (n < k) + (succ n ï¼ succ k)
  s = â‰¤-split (succ n) k l
  f : n < k â†’ A n
  f = Ï† n
  g : succ n ï¼ succ k â†’ A n
  g p = transportâ»Â¹ A (succ-lc p) a

\end{code}

Added 20th June 2018:

\begin{code}

<-is-prop-valued : (m n : â„•) â†’ is-prop (m < n)
<-is-prop-valued m n = â‰¤-is-prop-valued (succ m) n

<-coarser-than-â‰¤ : (m n : â„•) â†’ m < n â†’ m â‰¤ n
<-coarser-than-â‰¤ m n = â‰¤-trans m (succ m) n (â‰¤-succ m)

<-trans : (l m n : â„•) â†’ l < m â†’ m < n â†’ l < n
<-trans l m n u v = â‰¤-trans (succ l) m n u (<-coarser-than-â‰¤ m n v)

<-split : (m n : â„•) â†’ m < succ n â†’ (m < n) + (m ï¼ n)
<-split m zero     l = inr (unique-least m l)
<-split m (succ n) l = â‰¤-split m n l

regress : (P : â„• â†’ ğ“¤ Ì‡ )
        â†’ ((n : â„•) â†’ P (succ n) â†’ P n)
        â†’ (n m : â„•) â†’ m â‰¤ n â†’ P n â†’ P m
regress P Ï zero m l p = transportâ»Â¹ P (unique-least m l) p
regress P Ï (succ n) m l p = cases (Î» (l' : m â‰¤ n) â†’ IH m l' (Ï n p))
                                   (Î» (r : m ï¼ succ n) â†’ transportâ»Â¹ P r p)
                                   (â‰¤-split m n l)
 where
  IH : (m : â„•) â†’ m â‰¤ n â†’ P n â†’ P m
  IH = regress P Ï n

<-is-well-founded : (m : â„•) â†’ is-accessible _<_ m
<-is-well-founded zero     = step (Î» y l â†’ unique-from-ğŸ˜ l)
<-is-well-founded (succ m) = step (Ï„ (<-is-well-founded m))
 where
  Ï„ : is-accessible _<_ m â†’ (n : â„•) â†’ n < succ m â†’ is-accessible _<_ n
  Ï„ a n u = cases (Î» (v : n < m) â†’ prev _<_ a n v)
                  (Î» (p : n ï¼ m) â†’ transportâ»Â¹ (is-accessible _<_) p a)
                  (<-split n m u)

course-of-values-induction : (P : â„• â†’ ğ“¤ Ì‡ )
                           â†’ ((n : â„•) â†’ ((m : â„•) â†’ m < n â†’ P m) â†’ P n)
                           â†’ (n : â„•) â†’ P n
course-of-values-induction = transfinite-induction _<_ <-is-well-founded

<-is-extensional : is-extensional _<_
<-is-extensional zero     zero     f g = refl
<-is-extensional zero     (succ n) f g = unique-from-ğŸ˜ (g zero (zero-least n))
<-is-extensional (succ m) (zero)   f g = unique-from-ğŸ˜ (f zero (zero-least m))
<-is-extensional (succ m) (succ n) f g = ap succ (â‰¤-anti m n (f m (â‰¤-refl m)) (g n (â‰¤-refl n)))

â„•-ordinal : is-well-order _<_
â„•-ordinal = <-is-prop-valued , <-is-well-founded , <-is-extensional , <-trans

\end{code}

Induction on z, then x, then y:

\begin{code}

â„•-cotransitive : cotransitive _<_
â„•-cotransitive zero     y        zero     l = inr l
â„•-cotransitive (succ x) y        zero     l = inr (â‰¤-trans 1 (succ(succ x)) y â‹† l)
â„•-cotransitive zero     (succ y) (succ z) l = inl (zero-least y)
â„•-cotransitive (succ x) (succ y) (succ z) l = Î³ IH
 where
  IH : (x < z) + (z < y)
  IH = â„•-cotransitive x y z l
  Î³ : (x < z) + (z < y) â†’ (succ x < succ z) + (succ z < succ y)
  Î³ (inl l) = inl (succ-monotone (succ x) z l)
  Î³ (inr r) = inr (succ-monotone (succ z) y r)

\end{code}

Added December 2019.

\begin{code}

open import NotionsOfDecidability.Decidable
open import NotionsOfDecidability.Complemented

â‰¤-decidable : (m n : â„• ) â†’ decidable (m â‰¤ n)
â‰¤-decidable zero     n        = inl (zero-least n)
â‰¤-decidable (succ m) zero     = inr (zero-least' m)
â‰¤-decidable (succ m) (succ n) = â‰¤-decidable m n

<-decidable : (m n : â„• ) â†’ decidable (m < n)
<-decidable m n = â‰¤-decidable (succ m) n

\end{code}

Bounded minimization (added 14th December 2019):

\begin{code}

Î²Î¼ : (A : â„• â†’ ğ“¤ Ì‡ ) â†’ complemented A
  â†’ (k : â„•) â†’ (Î£ m ê‰ â„• , (m < k) Ã— A m Ã— ((n : â„•) â†’ A n â†’ m â‰¤ n))
            + ((n : â„•) â†’ A n â†’ n â‰¥ k)

Î²Î¼ A Î´ 0 = inr (Î» n a â†’ zero-least n)
Î²Î¼ A Î´ (succ k) = Î³
 where
  conclusion = (Î£ m ê‰ â„• , (m < succ k) Ã— A m Ã— ((n : â„•) â†’ A n â†’ m â‰¤ n))
             + ((n : â„•) â†’ A n â†’ n â‰¥ succ k)

  f : (Î£ m ê‰ â„• , (m < k) Ã— A m Ã— ((n : â„•) â†’ A n â†’ m â‰¤ n)) â†’ conclusion
  f (m , l , a , Ï†) = inl (m , <-trans m k (succ k) l (<-succ k) , a , Ï†)
  g : ((n : â„•) â†’ A n â†’ k â‰¤ n) â†’ conclusion
  g Ï† = cases gâ‚€ gâ‚ (Î´ k)
   where
    gâ‚€ : A k â†’ conclusion
    gâ‚€ a = inl (k , â‰¤-refl k , a , Ï†)
    gâ‚ : Â¬ A k â†’ conclusion
    gâ‚ u = inr Ïˆ
     where
      Ïˆ : (n : â„•) â†’ A n â†’ succ k â‰¤ n
      Ïˆ 0 a = ğŸ˜-elim (v a)
       where
        p : k ï¼ 0
        p = zero-least'' k (Ï† 0 a)
        v : Â¬ A 0
        v = transport (Î» - â†’ Â¬ A -) p u
      Ïˆ (succ n) a = III
       where
        I : k â‰¤ succ n
        I = Ï† (succ n) a
        II : k â‰  succ n
        II p = transport (Î» - â†’ Â¬ A -) p u a
        III : k â‰¤ n
        III = â‰¤-down k n I II

  Î³ : conclusion
  Î³ = cases f g (Î²Î¼ A Î´ k)

\end{code}

Given k : â„• with A k, find the least m : â„• with A m, by reduction to
bounded minimization:

\begin{code}

Î£Î¼ : (â„• â†’ ğ“¤ Ì‡ ) â†’ ğ“¤ Ì‡
Î£Î¼ A = Î£ m ê‰ â„• , A m Ã— ((n : â„•) â†’ A n â†’ m â‰¤ n)

least-from-given : (A : â„• â†’ ğ“¤ Ì‡ ) â†’ complemented A â†’ Î£ A â†’ Î£Î¼ A
least-from-given A Î´ (k , a) = Î³
 where
  f : (Î£ m ê‰ â„• , (m < k) Ã— A m Ã— ((n : â„•) â†’ A n â†’ m â‰¤ n)) â†’ Î£Î¼ A
  f (m , l , a' , Ï†) = m , a' , Ï†
  g : ((n : â„•) â†’ A n â†’ k â‰¤ n) â†’ Î£Î¼ A
  g Ï† = k , a , Ï†
  Î³ : Î£Î¼ A
  Î³ = cases f g (Î²Î¼ A Î´ k)

\end{code}

20th November 2020.

\begin{code}

open import Naturals.Addition renaming (_+_ to _âˆ”_)

max : â„• â†’ â„• â†’ â„•
max zero     n        = n
max (succ m) zero     = succ m
max (succ m) (succ n) = succ (max m n)

max-idemp : (x : â„•) â†’ max x x ï¼ x
max-idemp zero     = refl
max-idemp (succ x) = ap succ (max-idemp x)

max-comm : (m n : â„•) â†’ max m n ï¼ max n m
max-comm zero     zero     = refl
max-comm zero     (succ n) = refl
max-comm (succ m) zero     = refl
max-comm (succ m) (succ n) = ap succ (max-comm m n)

max-assoc : (x y z : â„•) â†’ max (max x y) z ï¼ max x (max y z)
max-assoc zero     y        z        = refl
max-assoc (succ x) zero     z        = refl
max-assoc (succ x) (succ y) zero     = refl
max-assoc (succ x) (succ y) (succ z) = ap succ (max-assoc x y z)

max-ordâ†’ : (x y : â„•) â†’ x â‰¤ y â†’ max x y ï¼ y
max-ordâ†’ zero     y        le = refl
max-ordâ†’ (succ x) zero     le = ğŸ˜-elim le
max-ordâ†’ (succ x) (succ y) le = ap succ (max-ordâ†’ x y le)

max-ordâ† : (x y : â„•) â†’ max x y ï¼ y â†’ x â‰¤ y
max-ordâ† zero     y        p = â‹†
max-ordâ† (succ x) zero     p = ğŸ˜-elim (positive-not-zero x p)
max-ordâ† (succ x) (succ y) p = max-ordâ† x y (succ-lc p)

max-â‰¤-upper-bound : (m n : â„•) â†’ m â‰¤ max m n
max-â‰¤-upper-bound zero     n        = â‹†
max-â‰¤-upper-bound (succ m) zero     = â‰¤-refl m
max-â‰¤-upper-bound (succ m) (succ n) = max-â‰¤-upper-bound m n

minus : (m n : â„•) â†’ n â‰¤ m â†’ â„•
minus zero     n        le = zero
minus (succ m) zero     â‹†  = succ m
minus (succ m) (succ n) le = minus m n le

minus-property : (m n : â„•) (le : n â‰¤ m) â†’ minus m n le âˆ” n ï¼ m
minus-property zero     zero     â‹†  = refl
minus-property (succ m) zero     â‹†  = refl
minus-property (succ m) (succ n) le = ap succ (minus-property m n le)

max-minus-property : (m n : â„•) â†’ minus (max m n) m (max-â‰¤-upper-bound m n) âˆ” m ï¼ max m n
max-minus-property m n = minus-property (max m n) m (max-â‰¤-upper-bound m n)

\end{code}

Tom de Jong, 5 November 2021.

\begin{code}

<-trichotomous : (n m : â„•) â†’ n < m + (n ï¼ m) + m < n
<-trichotomous zero     zero     = inr (inl refl)
<-trichotomous zero     (succ m) = inl â‹†
<-trichotomous (succ n) zero     = inr (inr â‹†)
<-trichotomous (succ n) (succ m) = Î³ IH
 where
  Î³ : (n < m) + (n ï¼ m) + (m < n)
    â†’ (succ n < succ m) + (succ n ï¼ succ m) + (succ m < succ n)
  Î³ (inl k)       = inl k
  Î³ (inr (inl e)) = inr (inl (ap succ e))
  Î³ (inr (inr l)) = inr (inr l)

  IH : (n < m) + (n ï¼ m) + (m < n)
  IH = <-trichotomous n m

\end{code}

Added 12/05/2020 by Andrew Sneap.
Following are proofs of common properties of strict and non-strict order of Natural Numbers.

\begin{code}

â‰¤-transâ‚‚ : (x y u v : â„•) â†’ x â‰¤ y â†’ y â‰¤ u â†’ u â‰¤ v â†’ x â‰¤ v
â‰¤-transâ‚‚ x y u v lâ‚ lâ‚‚ = â‰¤-trans x u v I
 where
  I : x â‰¤ u
  I = â‰¤-trans x y u lâ‚ lâ‚‚

<-transâ‚‚ : (x y u v : â„•) â†’ x < y â†’ y < u â†’ u < v â†’ x < v
<-transâ‚‚ x y u v lâ‚ lâ‚‚ = <-trans x u v I
 where
  I : x < u
  I = <-trans x y u lâ‚ lâ‚‚

â‰¤-<-trans : (x y z : â„•) â†’ x â‰¤ y â†’ y < z â†’ x < z
â‰¤-<-trans x y z lâ‚ lâ‚‚ = â‰¤-trans (succ x) (succ y) z lâ‚ lâ‚‚

<-â‰¤-trans : (x y z : â„•) â†’ x < y â†’ y â‰¤ z â†’ x < z
<-â‰¤-trans x y z lâ‚ lâ‚‚ = â‰¤-trans (succ x) y z lâ‚ lâ‚‚

â‰¤-n-monotone-right : (x y z : â„•) â†’ x â‰¤ y â†’ (x +' z) â‰¤ (y +' z)
â‰¤-n-monotone-right x y 0        l = l
â‰¤-n-monotone-right x y (succ n) l = â‰¤-n-monotone-right x y n l

â‰¤-n-monotone-left : (x y z : â„•) â†’ x â‰¤ y â†’ (z +' x) â‰¤ (z +' y)
â‰¤-n-monotone-left x y z l
 = transportâ‚‚ _â‰¤_ (addition-commutativity x z) (addition-commutativity y z) (â‰¤-n-monotone-right x y z l)

â‰¤-adding : (x y u v : â„•) â†’ x â‰¤ y â†’ u â‰¤ v â†’ (x +' u) â‰¤ (y +' v)
â‰¤-adding x y u v lâ‚ lâ‚‚
 = â‰¤-trans (x +' u) (y +' u) (y +' v) (â‰¤-n-monotone-right x y u lâ‚) (â‰¤-n-monotone-left u v y lâ‚‚)

<-succ-monotone : (x y : â„•) â†’ x < y â†’ succ x < succ y
<-succ-monotone x y = id

<-n-monotone-right : (x y z : â„•) â†’ x < y â†’ (x +' z) < (y +' z)
<-n-monotone-right x y  0       l = l
<-n-monotone-right x y (succ z) l = <-n-monotone-right x y z l

<-n-monotone-left : (x y z : â„•) â†’ x < y â†’ (z +' x) < (z +' y)
<-n-monotone-left x y z l
 = transportâ‚‚ _<_ (addition-commutativity x z) (addition-commutativity y z) (<-n-monotone-right x y z l)

<-adding : (x y u v : â„•) â†’ x < y â†’ u < v â†’ (x +' u) < (y +' v)
<-adding x y u v lâ‚ lâ‚‚
 = <-trans (x +' u) (y +' u) (y +' v) (<-n-monotone-right x y u lâ‚) (<-n-monotone-left u v y lâ‚‚)

<-+ : (x y z : â„•) â†’ x < y â†’ x < y +' z
<-+ x y z lâ‚ = â‰¤-trans (succ x) y (y +' z) lâ‚ lâ‚‚
 where
  lâ‚‚ : y â‰¤ y +' z
  lâ‚‚ = â‰¤-+ y z

equal-gives-less-than-or-equal : (x y : â„•) â†’ x ï¼ y â†’ x â‰¤ y
equal-gives-less-than-or-equal x y p = transport (_â‰¤ y) (p â»Â¹) (â‰¤-refl y)

less-than-not-equal : (x y : â„•) â†’ x < y â†’ Â¬ (x ï¼ y)
less-than-not-equal x y r p = less-not-bigger-or-equal x y r (equal-gives-less-than-or-equal y x (p â»Â¹))

less-than-one-is-zero : (x : â„•) â†’ x < 1 â†’ x ï¼ 0
less-than-one-is-zero 0        l = refl
less-than-one-is-zero (succ x) l = ğŸ˜-elim l

not-less-or-equal-is-bigger : (x y : â„•) â†’ Â¬(x â‰¤ y) â†’ y < x
not-less-or-equal-is-bigger 0        y        l = l (zero-least y)
not-less-or-equal-is-bigger (succ x) 0        l = zero-least x
not-less-or-equal-is-bigger (succ x) (succ y) l = not-less-or-equal-is-bigger x y l

â‰¤-dichotomous : (x y : â„•) â†’ (x â‰¤ y) + (y â‰¤ x)
â‰¤-dichotomous zero     y        = inl â‹†
â‰¤-dichotomous (succ x) zero     = inr â‹†
â‰¤-dichotomous (succ x) (succ y) = â‰¤-dichotomous x y

â‰¥-dichotomy : (x y : â„•) â†’ (x â‰¥ y) + (x â‰¤ y)
â‰¥-dichotomy 0        y        = inr (zero-least y)
â‰¥-dichotomy (succ x) 0        = inl (zero-least (succ x))
â‰¥-dichotomy (succ x) (succ y) = â‰¥-dichotomy x y

subtraction' : (x y : â„•) â†’ x < y â†’ Î£ z ê‰ â„• , (z +' x ï¼ y)
subtraction' 0        0        l = ğŸ˜-induction l
subtraction' 0        (succ y) l = (succ y) , refl
subtraction' (succ x) (succ y) l = prâ‚ IH , ap succ (prâ‚‚ IH)
 where
  IH : Î£ z ê‰ â„• , z +' x ï¼ y
  IH = subtraction' x y l

subtraction'' : (x y : â„•) â†’ x < y â†’ Î£ z ê‰ â„• , (succ z +' x ï¼ y)
subtraction'' 0        0        l = ğŸ˜-elim l
subtraction'' 0        (succ y) l = y , refl
subtraction'' (succ x) 0        l = ğŸ˜-elim l
subtraction'' (succ x) (succ y) l = z , ap succ e
 where
  I : Î£ z ê‰ â„• , succ z +' x ï¼ y
  I = subtraction'' x y l

  z : â„•
  z = prâ‚ I

  e : succ z +' x ï¼ y
  e = prâ‚‚ I

order-split : (x y : â„•) â†’ (x < y) + (x â‰¥ y)
order-split 0        0        = inr (zero-least 0)
order-split 0        (succ y) = inl (zero-least (succ y))
order-split (succ x) 0        = inr (zero-least (succ x))
order-split (succ x) (succ y) = order-split x y

least-element-unique : {A : â„• â†’ ğ“¤ Ì‡ } â†’ (Ïƒ : complemented A)
                                     â†’ ((Î± , Î±â‚š) : Î£ k ê‰ â„• , A k Ã— ((z : â„•) â†’ A z â†’ k â‰¤ z))
                                     â†’ ((Î² , Î²â‚š) : Î£ n ê‰ â„• , A n Ã— ((z : â„•) â†’ A z â†’ n â‰¤ z))
                                     â†’ Î± ï¼ Î²
least-element-unique Ïƒ (Î± , Î±â‚€ , Î±â‚) (Î² , Î²â‚€ , Î²â‚) = â‰¤-anti Î± Î² I II
 where
  I : Î± â‰¤ Î²
  I = Î±â‚ Î² Î²â‚€

  II : Î² â‰¤ Î±
  II = Î²â‚ Î± Î±â‚€

least-element-unique' : {A : â„• â†’ ğ“¤ Ì‡ } â†’ (Ïƒ : complemented A)
                                      â†’ (x y : â„•)
                                      â†’ (Î´ : Î£ A) â†’ x ï¼ prâ‚ (least-from-given A Ïƒ Î´) â†’ y ï¼ prâ‚ (least-from-given A Ïƒ Î´)
                                      â†’ x ï¼ y
least-element-unique' Ïƒ x y Î´ eâ‚ eâ‚‚ = eâ‚ âˆ™ eâ‚‚ â»Â¹

\end{code}

The following section provides an algorithm for bounded maximisation
of decidable propositions on Natural numbers, similar to the algorithm
for bounded-minimisation above.

The strategy is simple.


\begin{code}

bounded-maximisation : (A : â„• â†’ ğ“¤ Ì‡ )â†’ complemented A
                     â†’ (k : â„•)
                     â†’ (Î£ m ê‰ â„• , (m < k Ã— A m Ã— ((n : â„•) â†’ n < k â†’ A n â†’ n â‰¤ m))) + ((n : â„•) â†’ A n â†’ n â‰¥ k)
bounded-maximisation A Î´ zero = inr (Î» n _ â†’ zero-least n)
bounded-maximisation A Î´ (succ k) = f (bounded-maximisation A Î´ k)
 where
  conclusion = (Î£ m ê‰ â„• , (m < succ k) Ã— A m Ã— ((n : â„•) â†’ n < succ k â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ n â‰¥ succ k)

  f : (Î£ m ê‰ â„• , (m < k) Ã— A m Ã— ((n : â„•) â†’ n < k â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ n â‰¥ k)
    â†’ conclusion
  f (inl (m , l , a , Ïˆ)) = g (Î´ k)
   where
    g : A k + Â¬ A k â†’ conclusion
    g (inl k-holds) = inl (k , ((<-succ k) , (k-holds , Ïˆ')))
     where
       Ïˆ' : (n : â„•) â†’ n < succ k â†’ A n â†’ n â‰¤ k
       Ïˆ' n z a' = z
    g (inr k-fails) = inl (m , ((<-trans m k (succ k) l (<-succ k)) , a , Ïˆ'))
     where
      Ïˆ' : (n : â„•) â†’ n < succ k â†’ A n â†’ n â‰¤ m
      Ïˆ' n z a' = Ïˆ n (Ï (<-split n k z)) a'
       where
        Ï : (n < k) + (n ï¼ k) â†’ n < k
        Ï (inl r) = r
        Ï (inr r) = ğŸ˜-elim (k-fails (transport (Î» - â†’ A -) r a'))
  f (inr Ï‰) = g (Î´ k)
   where
    g : A k + Â¬ A k â†’ conclusion
    g (inl k-holds) = inl (k , (<-succ k , k-holds , (Î» z l a' â†’ l)))
    g (inr k-fails) = inr Ïˆ
     where
      Ïˆ : (n : â„•) â†’ A n â†’ n â‰¥ succ k
      Ïˆ n n-holds = Ï„ (<-split k n (Ï‰ n n-holds))
       where
        Ï„ : (k < n) + (k ï¼ n) â†’ n â‰¥ succ k
        Ï„ (inr w) = ğŸ˜-elim (k-fails (transport (Î» - â†’ A -) (w â»Â¹) n-holds))
        Ï„ (inl w) = w

bounded-maximisation' : (A : â„• â†’ ğ“¤ Ì‡ )â†’ complemented A
   â†’ (k : â„•)
   â†’ (Î£ m ê‰ â„• , (m â‰¤ k Ã— A m Ã— ((n : â„•) â†’ n â‰¤ k â†’ A n â†’ n â‰¤ m))) + ((n : â„•) â†’ A n â†’ k < n)
bounded-maximisation' A Î´ k = result (bounded-maximisation A Î´ k) (Î´ k)
 where
  result : (Î£ m ê‰ â„• , (m < k) Ã— A m Ã— ((n : â„•) â†’ n < k â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ n â‰¥ k) â†’ A k + Â¬ A k
         â†’ (Î£ m ê‰ â„• , (m â‰¤ k) Ã— A m Ã— ((n : â„•) â†’ n â‰¤ k â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ k < n)
  result (inl z) (inl k-holds) = inl (k , (â‰¤-refl k , (k-holds , (Î» _ t _ â†’ t))))
  result (inr z) (inl k-holds) = inl (k , â‰¤-refl k , k-holds , (Î» _ t _ â†’ t))
  result (inl (m , l , a , Ïˆ)) (inr k-fails) = inl (m , (<-coarser-than-â‰¤ m k l) , a , g)
   where
    g : (n : â„•) â†’ n â‰¤ k â†’ A n â†’ n â‰¤ m
    g n l' a' = Ïˆ n (h (<-split n k l')) a'
     where
      h : (n < k) + (n ï¼ k) â†’ n < k
      h (inl j) = j
      h (inr j) = ğŸ˜-elim (k-fails (transport (Î» - â†’ A -) j a'))
  result (inr z) (inr k-fails) = inr f
   where
    f : (n : â„•) â†’ A n â†’ k < n
    f n a = g (<-split k n (z n a))
     where
      g : (k < n) + (k ï¼ n) â†’ k < n
      g (inl j) = j
      g (inr j) = ğŸ˜-elim (k-fails (transport (Î» - â†’ A -) (j â»Â¹) a))

-- type of maximal element m : â„• such that A m holds, given an upper bound

maximal-element : (A : â„• â†’ ğ“¤ Ì‡ )â†’ (k : â„•) â†’ ğ“¤ Ì‡
maximal-element A k = Î£ m ê‰ â„• , (m < k Ã— A m Ã— ((n : â„•) â†’ n < k â†’ A n â†’ n â‰¤ m))

maximal-element' : (A : â„• â†’ ğ“¤ Ì‡ )â†’ (k : â„•) â†’ ğ“¤ Ì‡
maximal-element' A k = Î£ m ê‰ â„• , (m â‰¤ k Ã— A m Ã— ((n : â„•) â†’ n â‰¤ k â†’ A n â†’ n â‰¤ m))

\end{code}

With above machinery in mind, we can now produce maximal elements of
propositions of Natural Numbers, given some initial Natural Number for
which the property holds. Of course, we must provide an upper bound.

\begin{code}

maximal-from-given : (A : â„• â†’ ğ“¤ Ì‡ )â†’ (b : â„•) â†’ complemented A â†’ Î£ k ê‰ â„• , A k Ã— k < b â†’ maximal-element A b
maximal-from-given A b Î´ (k , a) = f (bounded-maximisation A Î´ b)
 where
  f : (Î£ m ê‰ â„• , (m < b) Ã— A m Ã— ((n : â„•) â†’ n < b â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ n â‰¥ b) â†’ maximal-element A b
  f (inl x) = x
  f (inr x) = ğŸ˜-elim (less-not-bigger-or-equal k b (prâ‚‚ a) (x k (prâ‚ a)))

maximal-from-given' : (A : â„• â†’ ğ“¤ Ì‡ )â†’ (b : â„•) â†’ complemented A â†’ Î£ k ê‰ â„• , A k Ã— k â‰¤ b â†’ maximal-element' A b
maximal-from-given' A b Î´ (k , a , c) = f (bounded-maximisation' A Î´ b)
 where
  f : (Î£ m ê‰ â„• , (m â‰¤ b) Ã— A m Ã— ((n : â„•) â†’ n â‰¤ b â†’ A n â†’ n â‰¤ m)) + ((n : â„•) â†’ A n â†’ b < n) â†’ maximal-element' A b
  f (inr x) = ğŸ˜-elim (bigger-or-equal-not-less k b c (x k a))
  f (inl x) = x

\end{code}

Multiplication preserves non-strict order, and this is proved by induction.

In the base case, it is required to prove that 0 â‰¤ 0 which is true by
definition.  In the inductive case, we need to prove that
m * succ k â‰¤ n * succ k, or by definitional equality m + m * k â‰¤ n + n * k.

By the inductive hypothesis, m * k â‰¤ n * k, and we have that m â‰¤ n, so we
can use the result which says we can combine two order relations into one.

\begin{code}

open import Naturals.Multiplication

multiplication-preserves-order : (m n k : â„•) â†’ m â‰¤ n â†’ m * k â‰¤ n * k
multiplication-preserves-order m n 0        l = zero-least 0
multiplication-preserves-order m n (succ k) l = â‰¤-adding m n (m * k) (n * k) l IH
 where
  IH : m * k â‰¤ n * k
  IH = multiplication-preserves-order m n k l

\end{code}

For strict order, order is only preserved when multiplying by a value
greater than 0.  Again by induction, the base case is trivial since we
are multiplying by 1.  The inductive case is similar to the above
proof.

\begin{code}

multiplication-preserves-strict-order : (m n k : â„•) â†’ m < n â†’ m * succ k < n * succ k
multiplication-preserves-strict-order m n 0        l = l
multiplication-preserves-strict-order m n (succ k) l = <-adding m n (m * succ k) (n * succ k) l (multiplication-preserves-strict-order m n k l)

\end{code}

If x * (y + 1) â‰¤ z, then x â‰¤ z. This is a useful property to have, and
proof follows from x â‰¤ x * y + 1 and transitivity of order.

A similar proof for strict order is sometimes useful.

\begin{code}

product-order-cancellable : (x y z : â„•) â†’ x * (succ y) â‰¤ z â†’ x â‰¤ z
product-order-cancellable x 0        z l = l
product-order-cancellable x (succ y) z l = â‰¤-trans x (x * succ (succ y)) z (â‰¤-+ x (x * succ y)) l

less-than-pos-mult : (x y z : â„•) â†’ x < y â†’ x < y * succ z
less-than-pos-mult x y z l = <-+ x y (y * z) l

\end{code}

{-
course-of-values-induction-modified : (P : â„• â†’ ğ“¤ Ì‡ )
                                    â†’ ((n : â„•) â†’ (Î£ m ê‰ â„• , m < n Ã— (P m â†’ P n)))
                                    â†’ (n : â„•) â†’ P n
course-of-values-induction-modified P step = course-of-values-induction P step'
 where
  step' : (n : â„•) â†’ ((m : â„•) â†’ m < n â†’ P m) â†’ P n
  step' n f with step n
  ... | n , m , ooop = ooop (f n m)
-}
