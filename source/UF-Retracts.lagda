\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module UF-Retracts where

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons

has-section : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
has-section r = Î£ \s â†’ r âˆ˜ s âˆ¼ id

has-retraction : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
has-retraction s = Î£ \r â†’ r âˆ˜ s âˆ¼ id

has-retraction-lc : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (s : X â†’ Y)
                  â†’ has-retraction s â†’ left-cancellable s
has-retraction-lc s (r , rs) {x} {x'} p = (rs x)â»Â¹ âˆ™ ap r p âˆ™ rs x'

retract_of_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
retract Y of X = Î£ \(r : X â†’ Y) â†’ has-section r

retract-of-singleton : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡}
                     â†’ retract Y of X
                     â†’ is-singleton X
                     â†’ is-singleton Y
retract-of-singleton (r , s , rs) (c , Ï†) = r c , (Î» y â†’ ap r (Ï† (s y)) âˆ™ rs y)

retract-of-prop : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡}
                        â†’ retract Y of X
                        â†’ is-prop X
                        â†’ is-prop Y
retract-of-prop (r , s , rs) = subtype-of-prop-is-a-prop s
                                        (has-retraction-lc s (r , rs))
identity-retraction : {X : ğ“¤ Ì‡} â†’ retract X of X
identity-retraction = id , (id , Î» x â†’ refl)

has-section-closed-under-âˆ¼ : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f g : X â†’ Y)
                           â†’ has-section f â†’  g âˆ¼ f  â†’ has-section g
has-section-closed-under-âˆ¼ {ğ“¤} {ğ“¥} {X} {Y} f g (s , fs) h =
 (s , Î» y â†’ g (s y) â‰¡âŸ¨ h (s y) âŸ© f (s y) â‰¡âŸ¨ fs y âŸ© y âˆ)

has-section-closed-under-âˆ¼' : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {f g : X â†’ Y}
                            â†’ has-section f â†’ f âˆ¼ g â†’ has-section g
has-section-closed-under-âˆ¼' ise h = has-section-closed-under-âˆ¼ _ _ ise (Î» x â†’ (h x)â»Â¹)

has-retraction-closed-under-âˆ¼ : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} (f g : X â†’ Y)
                              â†’ has-retraction f â†’  g âˆ¼ f  â†’ has-retraction g
has-retraction-closed-under-âˆ¼ {ğ“¤} {ğ“¥} {X} {Y} f g (r , rf) h = (r , Î» x â†’ r (g x) â‰¡âŸ¨ ap r (h x) âŸ© r (f x) â‰¡âŸ¨ rf x âŸ© x âˆ)

has-retraction-closed-under-âˆ¼' : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {f g : X â†’ Y}
                               â†’ has-retraction f â†’ f âˆ¼ g â†’ has-retraction g
has-retraction-closed-under-âˆ¼' ise h = has-retraction-closed-under-âˆ¼ _ _ ise (Î» x â†’ (h x)â»Â¹)

\end{code}

Surjection expressed in Curry-Howard logic amounts to retraction.

\begin{code}

retraction : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ (f : X â†’ Y) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
retraction f = âˆ€ y â†’ Î£ \x â†’ f x â‰¡ y

retract_Of_ : ğ“¤ Ì‡ â†’ ğ“¥ Ì‡ â†’ ğ“¤ âŠ” ğ“¥ Ì‡
retract Y Of X = Î£ \(f : X â†’ Y) â†’ retraction f

retract-of-retract-Of : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ retract Y of X â†’ retract Y Of X
retract-of-retract-Of {ğ“¤} {ğ“¥} {X} {Y} (f , Ï†)= (f , hass)
 where
  hass : (y : Y) â†’ Î£ \(x : X) â†’ f x â‰¡ y
  hass y = prâ‚ Ï† y , prâ‚‚ Ï† y

retract-Of-retract-of : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} â†’ retract Y Of X â†’ retract Y of X
retract-Of-retract-of {ğ“¤} {ğ“¥} {X} {Y} (f , hass) = (f , Ï†)
 where
  Ï† : Î£ \(s : Y â†’ X) â†’ f âˆ˜ s âˆ¼ id
  Ï† = (Î» y â†’ prâ‚ (hass y)) , Î» y â†’ prâ‚‚ (hass y)

retracts-compose : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {Z : ğ“¦ Ì‡}
                 â†’ retract Y of X â†’ retract Z of Y â†’ retract Z of X
retracts-compose (r , (s , rs)) (r' , (s' , rs')) = r' âˆ˜ r ,
                                                    s âˆ˜ s' ,
                                                    Î» z â†’ ap r' (rs (s' z)) âˆ™ rs' z


Ã—-retract : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : ğ“¦ Ì‡} {B : ğ“£ Ì‡}
          â†’ retract X of A
          â†’ retract Y of B
          â†’ retract (X Ã— Y) of (A Ã— B)
Ã—-retract {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A Ã— B â†’ X Ã— Y
  f (a , b) = (r a , t b)
  g : X Ã— Y â†’ A Ã— B
  g (x , y) = s x , u y
  fg : (z : X Ã— Y) â†’ f (g z) â‰¡ z
  fg (x , y) = to-Ã—-â‰¡ (rs x) (tu y)

+-retract : {X : ğ“¤ Ì‡} {Y : ğ“¦ Ì‡} {A : ğ“¥ Ì‡} {B : ğ“£ Ì‡}
           â†’ retract X of A
           â†’ retract Y of B
           â†’ retract (X + Y) of (A + B)
+-retract {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A + B â†’ X + Y
  f (inl a) = inl(r a)
  f (inr b) = inr(t b)
  g : X + Y â†’ A + B
  g (inl x) = inl(s x)
  g (inr y) = inr(u y)
  fg : (p : X + Y) â†’ f (g p) â‰¡ p
  fg (inl x) = ap inl (rs x)
  fg (inr y) = ap inr (tu y)

+'-retract-of-+ : {X Y : ğ“¤ Ì‡}
           â†’ retract (X +' Y) of (X + Y)
+'-retract-of-+ {ğ“¤} {X} {Y} = f , g , fg
 where
  f : X + Y â†’ X +' Y
  f (inl x) = â‚€ , x
  f (inr y) = â‚ , y
  g : X +' Y â†’ X + Y
  g (â‚€ , x) = inl x
  g (â‚ , y) = inr y
  fg : (z : X +' Y) â†’ f (g z) â‰¡ z
  fg (â‚€ , x) = refl
  fg (â‚ , y) = refl

+'-retract : {X Y : ğ“¤ Ì‡} {A B : ğ“¥ Ì‡}
           â†’ retract X of A
           â†’ retract Y of B
           â†’ retract (X +' Y) of (A +' B)
+'-retract {ğ“¤} {ğ“¥} {X} {Y} {A} {B} (r , s , rs) (t , u , tu) = f , g , fg
 where
  f : A +' B â†’ X +' Y
  f (â‚€ , a) = â‚€ , r a
  f (â‚ , b) = â‚ , t b
  g : X +' Y â†’ A +' B
  g (â‚€ , x) = â‚€ , s x
  g (â‚ , y) = â‚ , u y
  fg : (p : X +' Y) â†’ f (g p) â‰¡ p
  fg (â‚€ , x) = ap (Î» - â†’ (â‚€ , -)) (rs x)
  fg (â‚ , y) = ap (Î» - â†’ (â‚ , -)) (tu y)

Î£-reindex-retract : {X : ğ“¤ Ì‡} {Y : ğ“¥ Ì‡} {A : X â†’ ğ“¦ Ì‡} (r : Y â†’ X)
                  â†’ has-section r â†’ retract (Î£ A) of (Î£ (A âˆ˜ r))
Î£-reindex-retract {ğ“¤} {ğ“¥} {ğ“¦} {X} {Y} {A} r (s , rs) = Î³ , Ï† , Î³Ï†
 where
  Î³ : (Î£ \(y : Y) â†’ A (r y)) â†’ Î£ A
  Î³ (y , a) = (r y , a)
  Ï† : Î£ A â†’ Î£ \(y : Y) â†’ A (r y)
  Ï† (x , a) = (s x , back-transport A (rs x) a)
  Î³Ï† : (Ïƒ : Î£ A) â†’ Î³ (Ï† Ïƒ) â‰¡ Ïƒ
  Î³Ï† (x , a) = to-Î£-â‰¡ (rs x , p)
   where
    p : transport A (rs x) (back-transport A (rs x) a) â‰¡ a
    p = back-and-forth-transport (rs x)

Î£-retract : {X : ğ“¤ Ì‡} (A : X â†’ ğ“¥ Ì‡) (B : X â†’ ğ“¦ Ì‡)
          â†’ ((x : X) â†’ retract (A x) of (B x))
          â†’ retract (Î£ A) of (Î£ B)
Î£-retract {ğ“¤} {ğ“¥} {ğ“¦} {X} A B Ï = NatÎ£ R , NatÎ£ S , rs
 where
  R : (x : X) â†’ B x â†’ A x
  R x = prâ‚(Ï x)
  S : (x : X) â†’ A x â†’ B x
  S x = prâ‚(prâ‚‚(Ï x))
  RS : (x : X) (a : A x) â†’ R x (S x a) â‰¡ a
  RS x = prâ‚‚(prâ‚‚(Ï x))
  rs : (Ïƒ : Î£ A) â†’ NatÎ£ R (NatÎ£ S Ïƒ) â‰¡ Ïƒ
  rs (x , a) = to-Î£-â‰¡' (RS x a)

retract-ğŸ™+ğŸ™-of-ğŸš : retract ğŸ™ + ğŸ™ of ğŸš
retract-ğŸ™+ğŸ™-of-ğŸš = f , (g , fg)
 where
  f : ğŸš â†’ ğŸ™ {ğ“¤â‚€} + ğŸ™ {ğ“¤â‚€}
  f = ğŸš-cases (inl *) (inr *)
  g : ğŸ™ + ğŸ™ â†’ ğŸš
  g = cases (Î» x â†’ â‚€) (Î» x â†’ â‚)
  fg : (x : ğŸ™ + ğŸ™) â†’ f (g x) â‰¡ x
  fg (inl *) = refl
  fg (inr *) = refl

\end{code}

TODO. Several retractions here are actually equivalences. So some code
should be generalized and moved to an equivalences module. Similarly,
some retracts proved here are also shown as equivalences in other
modules, and hence there is some amount of repetition that should be
removed. This is the result of (1) merging initially independent
developments, and (2) work over many years with uncontrolled growth.

\begin{code}

Î£-retractâ‚‚ : {X : ğ“¤ Ì‡} {Y : X â†’ ğ“¥ Ì‡} {A : ğ“¦ Ì‡} {B : ğ“£ Ì‡}
           â†’ retract X of A
           â†’ ((x : X) â†’ retract  (Y x) of B)
           â†’ retract (Î£ Y) of (A Ã— B)
Î£-retractâ‚‚ {ğ“¤} {ğ“¥} {ğ“¦} {ğ“£} {X} {Y} {A} {B} (r , s , rs) R = f , g , gf
 where
  Ï† : (x : X) â†’ B â†’ Y x
  Ï† x = prâ‚ (R x)
  Î³ : (x : X) â†’ Y x â†’ B
  Î³ x = prâ‚ (prâ‚‚ (R x))
  Ï†Î³ : (x : X) â†’ (y : Y x) â†’ Ï† x (Î³ x y) â‰¡ y
  Ï†Î³ x = prâ‚‚ (prâ‚‚ (R x))
  f : A Ã— B â†’ Î£ Y
  f (a , b) = r a , Ï† (r a) b
  g : Î£ Y â†’ A Ã— B
  g (x , y) = s x , Î³ x y
  gf : (z : Î£ Y) â†’ f (g z) â‰¡ z
  gf (x , y) = to-Î£-â‰¡ (rs x , l (rs x))
   where
    l : {x' : X} (p : x' â‰¡ x) â†’ transport Y p (Ï† x' (Î³ x y)) â‰¡ y
    l refl = Ï†Î³ x y

retract-ğŸ™+ğŸ™-of-â„• : retract ğŸ™ + ğŸ™ of â„•
retract-ğŸ™+ğŸ™-of-â„• = r , s , rs
 where
  r : â„• â†’ ğŸ™ + ğŸ™
  r zero = inl *
  r (succ _) = inr *
  s : ğŸ™ + ğŸ™ â†’ â„•
  s (inl *) = zero
  s (inr *) = succ zero
  rs : (z : ğŸ™ {ğ“¤â‚€} + ğŸ™ {ğ“¤â‚€}) â†’ r (s z) â‰¡ z
  rs (inl *) = refl
  rs (inr *) = refl

\end{code}
