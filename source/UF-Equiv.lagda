\begin{code}

{-# OPTIONS --without-K --exact-split #-}

module UF-Equiv where

open import SpartanMLTT
open import UF-Base
open import UF-Subsingletons
open import UF-Retracts

\end{code}

We take Joyal's version of the notion of equivalence as the primary
one because it is more symmetrical.

\begin{code}

is-equiv : {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
is-equiv f = has-section f Ã— has-retraction f

is-equiv-has-section : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                     â†’ is-equiv f â†’ has-section f
is-equiv-has-section f = prâ‚

is-equiv-has-retraction : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                        â†’ is-equiv f â†’ has-retraction f
is-equiv-has-retraction f = prâ‚‚

section-retraction-equiv : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                         â†’ has-section f â†’ has-retraction f â†’ is-equiv f
section-retraction-equiv f hr hs = (hr , hs)

_â‰ƒ_ : U Ì‡ â†’ V Ì‡ â†’ U âŠ” V Ì‡
X â‰ƒ Y = Î£ \(f : X â†’ Y) â†’ is-equiv f

id-is-an-equiv : (X : U Ì‡) â†’ is-equiv (id {U} {X})
id-is-an-equiv X = (id , Î» x â†’ refl) , (id , Î» x â†’ refl)

â‰ƒ-refl : (X : U Ì‡) â†’ X â‰ƒ X
â‰ƒ-refl X = id , id-is-an-equiv X

â‰ƒ-trans : {X : U Ì‡} {Y : V Ì‡} {Z : W Ì‡} â†’ X â‰ƒ Y â†’ Y â‰ƒ Z â†’ X â‰ƒ Z
â‰ƒ-trans {U} {V} {W} {X} {Y} {Z} (f , (g , fg) , (h , hf)) (f' , (g' , fg') , (h' , hf'))  =
  f' âˆ˜ f , (g âˆ˜ g' , fg'') , (h âˆ˜ h' , hf'')
 where
    fg'' : (z : Z) â†’ f' (f (g (g' z))) â‰¡ z
    fg'' z =  ap f' (fg (g' z)) âˆ™ fg' z
    hf'' : (x : X) â†’ h(h'(f'(f x))) â‰¡ x
    hf'' x = ap h (hf' (f x)) âˆ™ hf x

_â‰ƒâŸ¨_âŸ©_ : (X : U Ì‡) {Y : V Ì‡} {Z : W Ì‡} â†’ X â‰ƒ Y â†’ Y â‰ƒ Z â†’ X â‰ƒ Z
_ â‰ƒâŸ¨ d âŸ© e = â‰ƒ-trans d e

_â–  : (X : U Ì‡) â†’ X â‰ƒ X
_â–  = â‰ƒ-refl

Eq : U Ì‡ â†’ V Ì‡ â†’ U âŠ” V Ì‡
Eq = _â‰ƒ_

Eqtofun : (X : U Ì‡) (Y : V Ì‡) â†’ X â‰ƒ Y â†’ X â†’ Y
Eqtofun X Y (f , _) = f

eqtofun : {X : U Ì‡} {Y : V Ì‡} â†’ X â‰ƒ Y â†’ X â†’ Y
eqtofun (f , _) = f

eqtofun-is-an-equiv : {X : U Ì‡} {Y : V Ì‡} (e : X â‰ƒ Y) â†’ is-equiv (eqtofun e)
eqtofun-is-an-equiv = prâ‚‚

back-eqtofun : {X : U Ì‡} {Y : V Ì‡} â†’ X â‰ƒ Y â†’ Y â†’ X
back-eqtofun e = prâ‚ (prâ‚ (prâ‚‚ e))

idtoeq : (X Y : U Ì‡) â†’ X â‰¡ Y â†’ X â‰ƒ Y
idtoeq X Y p = transport (Eq X) p (â‰ƒ-refl X)

idtoeq-traditional : (X Y : U Ì‡) â†’ X â‰¡ Y â†’ X â‰ƒ Y
idtoeq-traditional X _ refl = â‰ƒ-refl X

\end{code}

We would have a definitional equality if we had defined the traditional
one using J(based), but because of the idiosyncracies of Agda, we
don't with the current definition:

\begin{code}

eqtoeq-agreement : (X Y : U Ì‡) (p : X â‰¡ Y)
                 â†’ idtoeq X Y p â‰¡ idtoeq-traditional X Y p
eqtoeq-agreement {U} X _ refl = refl

idtofun : (X Y : U Ì‡) â†’ X â‰¡ Y â†’ X â†’ Y
idtofun X Y p = eqtofun (idtoeq X Y p)

idtofun-agreement : (X Y : U Ì‡) (p : X â‰¡ Y) â†’ idtofun X Y p â‰¡ Idtofun p
idtofun-agreement X Y refl = refl

equiv-closed-under-âˆ¼ : {X : U Ì‡} {Y : V Ì‡} (f g : X â†’ Y) â†’ is-equiv f â†’  g âˆ¼ f  â†’ is-equiv g
equiv-closed-under-âˆ¼ {U} {V} {X} {Y} f g (hass , hasr) h = (has-section-closed-under-âˆ¼ f g hass h) ,
                                                           (has-retraction-closed-under-âˆ¼ f g hasr h)

equiv-closed-under-âˆ¼' : {X : U Ì‡} {Y : V Ì‡} {f g : X â†’ Y} â†’ is-equiv f â†’ f âˆ¼ g â†’ is-equiv g
equiv-closed-under-âˆ¼' ise h = equiv-closed-under-âˆ¼ _ _ ise (Î» x â†’ (h x)â»Â¹)

qinv : {U V : Universe} {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
qinv f = Î£ \g â†’ (g âˆ˜ f âˆ¼ id) Ã— (f âˆ˜ g âˆ¼ id)

equivs-are-qinvs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ is-equiv f â†’ qinv f
equivs-are-qinvs {U} {V} {X} {Y} f ((s , fs) , (r , rf)) = s , (sf , fs)
 where
  sf : (x : X) â†’ s(f x) â‰¡ x
  sf x = s(f x)       â‰¡âŸ¨ (rf (s (f x)))â»Â¹ âŸ©
         r(f(s(f x))) â‰¡âŸ¨ ap r (fs (f x)) âŸ©
         r(f x)       â‰¡âŸ¨ rf x âŸ©
         x            âˆ

qinvs-are-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ qinv f â†’ is-equiv f
qinvs-are-equivs f (g , (gf , fg)) = (g , fg) , (g , gf)

qinveq : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ qinv f â†’ X â‰ƒ Y
qinveq f q = (f , qinvs-are-equivs f q)

â‰ƒ-sym : {X : U Ì‡} {Y : V Ì‡}  â†’ X â‰ƒ Y â†’ Y â‰ƒ X
â‰ƒ-sym {U} {V} {X} {Y} (f , e) = (g , d)
 where
  g : Y â†’ X
  g = prâ‚(equivs-are-qinvs f e)
  q : qinv g
  q = f , prâ‚‚(prâ‚‚(equivs-are-qinvs f e)) , prâ‚(prâ‚‚(equivs-are-qinvs f e))
  d : is-equiv g
  d = qinvs-are-equivs g q

equiv-retract-l : {X : U Ì‡} {Y : V Ì‡} â†’ X â‰ƒ Y â†’ retract X of Y
equiv-retract-l (f , (g , fg) , (h , hf)) = h , f , hf

equiv-retract-r : {X : U Ì‡} {Y : V Ì‡} â†’ X â‰ƒ Y â†’ retract Y of X
equiv-retract-r (f , (g , fg) , (h , hf)) = f , g , fg

Id-retract-l : {X Y : U Ì‡} â†’ X â‰¡ Y â†’ retract X of Y
Id-retract-l p = equiv-retract-l (idtoeq (lhs p) (rhs p) p)

Id-retract-r : {X Y : U Ì‡} â†’ X â‰¡ Y â†’ retract Y of X
Id-retract-r p = equiv-retract-r (idtoeq (lhs p) (rhs p) p)

equiv-to-subsingleton : {X : U Ì‡} {Y : V Ì‡}
                      â†’ Y â‰ƒ X
                      â†’ is-subsingleton X
                      â†’ is-subsingleton Y
equiv-to-subsingleton e = retract-of-subsingleton (equiv-retract-l e)

equiv-to-singleton : {X : U Ì‡} {Y : V Ì‡}
                   â†’ Y â‰ƒ X
                   â†’ is-singleton X
                   â†’ is-singleton Y
equiv-to-singleton e = retract-of-singleton (equiv-retract-l e)

\end{code}

Equivalence of transports.

\begin{code}

transports-are-equivs : {X : U Ì‡} {A : X â†’ V Ì‡} {x y : X} (p : x â‰¡ y)
                   â†’ is-equiv (transport A p)
transports-are-equivs refl = id-is-an-equiv _

back-transports-are-equivs : {X : U Ì‡} {A : X â†’ V Ì‡} {x y : X} (p : x â‰¡ y)
                        â†’ is-equiv (back-transport A p)
back-transports-are-equivs p = transports-are-equivs (p â»Â¹)

\end{code}

\begin{code}

fiber : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ Y â†’ U âŠ” V Ì‡
fiber f y = Î£ \x â†’ f x â‰¡ y

is-vv-equiv : {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
is-vv-equiv f = âˆ€ y â†’ is-singleton (fiber f y)

vv-equivs-are-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                     â†’ is-vv-equiv f â†’ is-equiv f
vv-equivs-are-equivs {U} {V} {X} {Y} f Ï† = (g , fg) , (g , gf)
 where
  Ï†' : (y : Y) â†’ Î£ \(c : Î£ \(x : X) â†’ f x â‰¡ y) â†’ (Ïƒ : Î£ \(x : X) â†’ f x â‰¡ y) â†’ c â‰¡ Ïƒ
  Ï†' = Ï†
  c : (y : Y) â†’ Î£ \(x : X) â†’ f x â‰¡ y
  c y = prâ‚(Ï† y)
  d : (y : Y) â†’ (Ïƒ : Î£ \(x : X) â†’ f x â‰¡ y) â†’ c y â‰¡ Ïƒ
  d y = prâ‚‚(Ï† y)
  g : Y â†’ X
  g y = prâ‚(c y)
  fg : (y : Y) â†’ f (g y) â‰¡ y
  fg y = prâ‚‚(c y)
  e : (x : X) â†’ g(f x) , fg (f x) â‰¡ x , refl
  e x = d (f x) (x , refl)
  gf : (x : X) â†’ g (f x) â‰¡ x
  gf x = ap prâ‚ (e x)

fiber' : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ Y â†’ U âŠ” V Ì‡
fiber' f y = Î£ \x â†’ y â‰¡ f x

fiber-lemma : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) (y : Y) â†’ fiber f y â‰ƒ fiber' f y
fiber-lemma f y = g , (h , gh) , (h , hg)
 where
  g : fiber f y â†’ fiber' f y
  g (x , p) = x , (p â»Â¹)
  h : fiber' f y â†’ fiber f y
  h (x , p) = x , (p â»Â¹)
  hg : âˆ€ Ïƒ â†’ h(g Ïƒ) â‰¡ Ïƒ
  hg (x , refl) = refl
  gh : âˆ€ Ï„ â†’ g(h Ï„) â‰¡ Ï„
  gh (x , refl) = refl

is-hae : {X : U Ì‡} {Y : V Ì‡} â†’ (X â†’ Y) â†’ U âŠ” V Ì‡
is-hae {U} {V} {X} {Y} f = Î£ \(g : Y â†’ X) â†’ Î£ \(Î· : g âˆ˜ f âˆ¼ id) â†’ Î£ \(Îµ : f âˆ˜ g âˆ¼ id)
                            â†’ Î  \(x : X) â†’ ap f (Î· x) â‰¡ Îµ (f x)

haes-are-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                â†’ is-hae f â†’ is-equiv f
haes-are-equivs {U} {V} {X} f (g , Î· , Îµ , Ï„) = qinvs-are-equivs f (g , Î· , Îµ)

id-homotopies-are-natural : {X : U Ì‡} (h : X â†’ X) (Î· : h âˆ¼ id) {x : X}
                          â†’ Î· (h x) â‰¡ ap h (Î· x)
id-homotopies-are-natural h Î· {x} =
   Î· (h x)                         â‰¡âŸ¨ refl âŸ©
   Î· (h x) âˆ™ refl                  â‰¡âŸ¨ ap (Î» - â†’ Î·(h x) âˆ™ -) ((trans-sym' (Î· x))â»Â¹) âŸ©
   Î· (h x) âˆ™ (Î· x âˆ™ (Î· x)â»Â¹)       â‰¡âŸ¨ (assoc (Î· (h x)) (Î· x) (Î· x â»Â¹))â»Â¹ âŸ©
   Î· (h x) âˆ™ Î· x âˆ™ (Î· x)â»Â¹         â‰¡âŸ¨ ap (Î» - â†’ Î· (h x) âˆ™ - âˆ™ (Î· x)â»Â¹) ((ap-id-is-id (Î· x))) âŸ©
   Î· (h x) âˆ™ ap id (Î· x) âˆ™ (Î· x)â»Â¹ â‰¡âŸ¨ homotopies-are-natural' h id Î· {h x} {x} {Î· x} âŸ©
   ap h (Î· x)                      âˆ

qinvs-are-haes : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y) â†’ qinv f â†’ is-hae f
qinvs-are-haes {U} {V} {X} {Y} f (g , (Î· , Îµ)) = g , Î· , Îµ' , Ï„
 where
  Îµ' : f âˆ˜ g âˆ¼ id
  Îµ' y = f (g y)         â‰¡âŸ¨ (Îµ (f (g y)))â»Â¹ âŸ©
         f (g (f (g y))) â‰¡âŸ¨ ap f (Î· (g y)) âŸ©
         f (g y)         â‰¡âŸ¨ Îµ y âŸ©
         y               âˆ

  a : (x : X) â†’ Î· (g (f x)) â‰¡ ap g (ap f (Î· x))
  a x = Î· (g (f x))      â‰¡âŸ¨ id-homotopies-are-natural (g âˆ˜ f) Î·  âŸ©
        ap (g âˆ˜ f) (Î· x)  â‰¡âŸ¨ (ap-ap f g (Î· x))â»Â¹ âŸ©
        ap g (ap f (Î· x)) âˆ

  b : (x : X) â†’ ap f (Î· (g (f x))) âˆ™ Îµ (f x) â‰¡ Îµ (f (g (f x))) âˆ™ ap f (Î· x)
  b x = ap f (Î· (g (f x))) âˆ™ Îµ (f x)         â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ Îµ (f x)) (ap (ap f) (a x)) âŸ©
        ap f (ap g (ap f (Î· x))) âˆ™ Îµ (f x)   â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ Îµ (f x)) (ap-ap g f (ap f (Î· x))) âŸ©
        ap (f âˆ˜ g) (ap f (Î· x)) âˆ™ Îµ (f x)    â‰¡âŸ¨ (homotopies-are-natural (f âˆ˜ g) id Îµ {f (g (f x))} {f x} {ap f (Î· x)})â»Â¹ âŸ©
        Îµ (f (g (f x))) âˆ™ ap id (ap f (Î· x)) â‰¡âŸ¨ ap (Î» - â†’ Îµ (f (g (f x))) âˆ™ -) (ap-ap f id (Î· x)) âŸ©
        Îµ (f (g (f x))) âˆ™ ap f (Î· x)         âˆ

  Ï„ : (x : X) â†’ ap f (Î· x) â‰¡ Îµ' (f x)
  Ï„ x = ap f (Î· x)                                         â‰¡âŸ¨ refl-left-neutral â»Â¹ âŸ©
        refl âˆ™ ap f (Î· x)                                   â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ ap f (Î· x)) ((trans-sym (Îµ (f (g (f x)))))â»Â¹) âŸ©
        (Îµ (f (g (f x))))â»Â¹ âˆ™ Îµ (f (g (f x))) âˆ™ ap f (Î· x)   â‰¡âŸ¨ assoc ((Îµ (f (g (f x))))â»Â¹) (Îµ (f (g (f x)))) (ap f (Î· x)) âŸ©
        (Îµ (f (g (f x))))â»Â¹ âˆ™ (Îµ (f (g (f x))) âˆ™ ap f (Î· x)) â‰¡âŸ¨ ap (Î» - â†’ (Îµ (f (g (f x))))â»Â¹ âˆ™ -) (b x)â»Â¹ âŸ©
        (Îµ (f (g (f x))))â»Â¹ âˆ™ (ap f (Î· (g (f x))) âˆ™ Îµ (f x)) â‰¡âŸ¨ refl âŸ©
        Îµ' (f x)                                            âˆ

equivs-are-haes : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                â†’ is-equiv f â†’ is-hae f
equivs-are-haes f e = qinvs-are-haes f (equivs-are-qinvs f e)

\end{code}

The following could be defined by combining functions we already have,
but a proof by path induction is direct:

\begin{code}

identifications-in-fibers : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                            (y : Y) (x x' : X) (p : f x â‰¡ y) (p' : f x' â‰¡ y)
                          â†’ (Î£ \(Î³ : x â‰¡ x') â†’ ap f Î³ âˆ™ p' â‰¡ p) â†’ (x , p) â‰¡ (x' , p')
identifications-in-fibers f .(f x) x .x refl p' (refl , r) = g
 where
  g : x , refl â‰¡ x , p'
  g = ap (Î» - â†’ (x , -)) (r â»Â¹ âˆ™ refl-left-neutral)

\end{code}

Using this we see that half adjoint equivalences have singleton fibers:

\begin{code}

haes-are-vv-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                   â†’ is-hae f â†’ is-vv-equiv f
haes-are-vv-equivs {U} {V} {X} f (g , Î· , Îµ , Ï„) y = (c , Î» Ïƒ â†’ Î± (prâ‚ Ïƒ) (prâ‚‚ Ïƒ))
 where
  c : fiber f y
  c = (g y , Îµ y)

  Î± : (x : X) (p : f x â‰¡ y) â†’ c â‰¡ (x , p)
  Î± x p = Ï†
   where
    Î³ : g y â‰¡ x
    Î³ = (ap g p)â»Â¹ âˆ™ Î· x
    q : ap f Î³ âˆ™ p â‰¡ Îµ y
    q = ap f Î³ âˆ™ p                         â‰¡âŸ¨ refl âŸ©
        ap f ((ap g p)â»Â¹ âˆ™ Î· x) âˆ™ p         â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ p) (ap-comp f ((ap g p)â»Â¹) (Î· x)) âŸ©
        ap f ((ap g p)â»Â¹) âˆ™ ap f (Î· x) âˆ™ p  â‰¡âŸ¨ ap (Î» - â†’ ap f - âˆ™ ap f (Î· x) âˆ™ p) (ap-sym g p) âŸ©
        ap f (ap g (p â»Â¹)) âˆ™ ap f (Î· x) âˆ™ p â‰¡âŸ¨ ap (Î» - â†’ ap f (ap g (p â»Â¹)) âˆ™ - âˆ™ p) (Ï„ x) âŸ©
        ap f (ap g (p â»Â¹)) âˆ™ Îµ (f x) âˆ™ p    â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ Îµ (f x) âˆ™ p) (ap-ap g f (p â»Â¹)) âŸ©
        ap (f âˆ˜ g) (p â»Â¹) âˆ™ Îµ (f x) âˆ™ p     â‰¡âŸ¨ ap (Î» - â†’ - âˆ™ p) (homotopies-are-natural (f âˆ˜ g) id Îµ {y} {f x} {p â»Â¹})â»Â¹ âŸ©
        Îµ y âˆ™ ap id (p â»Â¹) âˆ™ p              â‰¡âŸ¨ ap (Î» - â†’ Îµ y âˆ™ - âˆ™ p) (ap-id-is-id (p â»Â¹))â»Â¹ âŸ©
        Îµ y âˆ™ p â»Â¹ âˆ™ p                      â‰¡âŸ¨ assoc (Îµ y) (p â»Â¹) p âŸ©
        Îµ y âˆ™ (p â»Â¹ âˆ™ p)                    â‰¡âŸ¨ ap (Î» - â†’ Îµ y âˆ™ -) (trans-sym p) âŸ©
        Îµ y âˆ™ refl â‰¡âŸ¨ refl âŸ©
        Îµ y âˆ

    Ï† : g y , Îµ y â‰¡ x , p
    Ï† = identifications-in-fibers f y (g y) x (Îµ y) p (Î³ , q)

\end{code}

Here are some corollaries:

\begin{code}

qinvs-are-vv-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                 â†’ qinv f â†’ is-vv-equiv f
qinvs-are-vv-equivs f q = haes-are-vv-equivs f (qinvs-are-haes f q)

equivs-are-vv-equivs : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                     â†’ is-equiv f â†’ is-vv-equiv f
equivs-are-vv-equivs f ie = qinvs-are-vv-equivs f (equivs-are-qinvs f ie)

\end{code}

We pause to characterize negation and singletons. Recall that Â¬ X and
is-empty X are synonyms for the function type X â†’ ğŸ˜.

\begin{code}

equiv-can-assume-pointed-codomain : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                                  â†’ (Y â†’ is-vv-equiv f) â†’ is-vv-equiv f
equiv-can-assume-pointed-codomain f Ï† y = Ï† y y

maps-to-ğŸ˜-are-equivs : {X : U Ì‡} (f : Â¬ X) â†’ is-vv-equiv f
maps-to-ğŸ˜-are-equivs f = equiv-can-assume-pointed-codomain f ğŸ˜-elim

negations-are-equiv-to-ğŸ˜ : {X : U Ì‡} â†’ is-empty X â‡” X â‰ƒ ğŸ˜
negations-are-equiv-to-ğŸ˜ = (Î» f â†’ f , vv-equivs-are-equivs f (maps-to-ğŸ˜-are-equivs f)), prâ‚

\end{code}

Then with functional and propositional extensionality, which follow
from univalence, we conclude that Â¬X = (X â‰ƒ 0) = (X â‰¡ 0).

And similarly, with similar a observation:

\begin{code}

singletons-are-equiv-to-ğŸ™ : {X : U Ì‡} â†’ is-singleton X â‡” X â‰ƒ ğŸ™ {V}
singletons-are-equiv-to-ğŸ™ {U} {V} {X} = forth , back
 where
  forth : is-singleton X â†’ X â‰ƒ ğŸ™
  forth (xâ‚€ , Ï†) = unique-to-ğŸ™ , (((Î» _ â†’ xâ‚€) , (Î» x â†’ (ğŸ™-all-* x)â»Â¹)) , ((Î» _ â†’ xâ‚€) , Ï†))
  back : X â‰ƒ ğŸ™ â†’ is-singleton X
  back (f , (s , fs) , (r , rf)) = retract-of-singleton (r , f , rf) ğŸ™-is-singleton

\end{code}

The following again could be defined by combining functions we already
have:

\begin{code}

from-identifications-in-fibers : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
                                 (y : Y) (x x' : X) (p : f x â‰¡ y) (p' : f x' â‰¡ y)
                               â†’ (x , p) â‰¡ (x' , p') â†’ Î£ \(Î³ : x â‰¡ x') â†’ ap f Î³ âˆ™ p' â‰¡ p
from-identifications-in-fibers f .(f x) x .x refl .refl refl = refl , refl

Î·-pif : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
        (y : Y) (x x' : X) (p : f x â‰¡ y) (p' : f x' â‰¡ y)
        (Ïƒ : Î£ \(Î³ : x â‰¡ x') â†’ ap f Î³ âˆ™ p' â‰¡ p)
      â†’ from-identifications-in-fibers f y x x' p p' (identifications-in-fibers f y x x' p p' Ïƒ) â‰¡ Ïƒ
Î·-pif f .(f x) x .x _ refl (refl , refl) = refl

\end{code}

Then the following is a consequence of natural-section-has-retraction,
but also has a direct proof by path induction:

\begin{code}
Îµ-pif : {X : U Ì‡} {Y : V Ì‡} (f : X â†’ Y)
        (y : Y) (x x' : X) (p : f x â‰¡ y) (p' : f x' â‰¡ y)
        (q : (x , p) â‰¡ (x' , p'))
      â†’ identifications-in-fibers f y x x' p p' (from-identifications-in-fibers f y x x' p p' q) â‰¡ q
Îµ-pif f .(f x) x .x refl .refl refl = refl

prâ‚-is-vv-equiv : (X : U Ì‡) (Y : X â†’ V Ì‡)
             â†’ ((x : X) â†’ is-singleton (Y x))
             â†’ is-vv-equiv (prâ‚ {U} {V} {X} {Y})
prâ‚-is-vv-equiv {U} {V} X Y iss x = g
 where
  c : fiber prâ‚ x
  c = (x , prâ‚ (iss x)) , refl
  p : (y : Y x) â†’ prâ‚ (iss x) â‰¡ y
  p = prâ‚‚ (iss x)
  f : (w : Î£ \(Ïƒ : Î£ Y) â†’ prâ‚ Ïƒ â‰¡ x) â†’ c â‰¡ w
  f ((.x , y) , refl) = ap (Î» - â†’ (x , -) , refl) (p y)
  g : is-singleton (fiber prâ‚ x)
  g = c , f

prâ‚-is-vv-equiv-converse : {X : U Ì‡} {A : X â†’ V Ì‡}
                      â†’ is-vv-equiv (prâ‚ {U} {V} {X} {A})
                      â†’ ((x : X) â†’ is-singleton(A x))
prâ‚-is-vv-equiv-converse {U} {V} {X} {A} isv x = retract-of-singleton (r , s , rs) (isv x)
  where
    f : Î£ A â†’ X
    f = prâ‚ {U} {V} {X} {A}
    s : A x â†’ fiber f x
    s a = (x , a) , refl
    r : fiber f x â†’ A x
    r ((x , a) , refl) = a
    rs : (a : A x) â†’ r(s a) â‰¡ a
    rs a = refl

\end{code}

Associativities and precedences.

\begin{code}

infix  0 _â‰ƒ_
infix  1 _â– 
infixr 0 _â‰ƒâŸ¨_âŸ©_

\end{code}
