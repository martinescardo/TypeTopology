Martin Escardo, 4th August 2020. (Going back to 1993 or earlier.)

A construction of the initial binary system in Spartan MLTT, without
HITs or quotients, or extensionality axioms.

A binary system is a type A with distinguished points a b : A and
functions f g : A â†’ A such that

 (1) a = f a,
 (2) b = g b,
 (3) f b = g a.

We don't require the type A to be a set in the sense of univalent
mathematics.

The initial binary system is the closed interval of dyadic rationals
(see below for a picture).

We construct it as a quotient of the following type ğ”¹, with L,R,l,r
corresponding to a,b,f,g, without assuming that our type theory has
quotients - it just happens to have the quotient we want.

\begin{code}

{-# OPTIONS --without-K --safe --exact-split #-}

module InitialBinarySystem where

open import SpartanMLTT

data ğ”¹ : ğ“¤â‚€ Ì‡ where
 L R : ğ”¹
 l r : ğ”¹ â†’ ğ”¹

\end{code}

We want to perform the identifications

 (1) L = l L,
 (2) R = r R,
 (3) l R = r L,

so that l and r become congruences. We call C (for "center") the equal
points of (3).

Here is a pictorial illustration of what we have in mind:


    Left endpoint    center  right endpoint

    L                  C                  R
    [------------------+------------------]  (the dyadic closed interval)
                       |
    [ image of l       |       image of r ]
                       v
          common point of the images


Geometrically, the functions l and r are supposed to be affine
transformations that scale and translate the interval to two smaller
copies with a common overlapping point.

To perform the identifications, we could quotient or use a HIT (higher
inductive type). We instead take a retract, defined by the fixed
points of an idempotent normalization function.

We take the biased choice l R for C before we perform the
identification (3). This will be the canonical, or normal-form
representative of the common point of the images.

More generally, a binary system is a type A with distinguished points
a b : A and functions f g : A â†’ A such that

 (1) a = f a,
 (2) b = g b,
 (3) f b = g a.

What we want to do is to quotient the type ğ”¹, so that the quotient map
is retraction, to get the initial binary system.

\begin{code}

C : ğ”¹
C = l R

is-normal : ğ”¹ â†’ ğ“¤â‚€ Ì‡
is-normal L         = ğŸ™
is-normal R         = ğŸ™
is-normal (l L)     = ğŸ˜
is-normal (l R)     = ğŸ™
is-normal (l (l x)) = is-normal (l x)
is-normal (l (r x)) = is-normal (r x)
is-normal (r L)     = ğŸ˜
is-normal (r R)     = ğŸ˜
is-normal (r (l x)) = is-normal (l x)
is-normal (r (r x)) = is-normal (r x)

\end{code}

The following can be proved directly, but it also follows from the
result proved below that we have an idempotent normalization function
with the normal elements as its fixed points, and from the fact that ğ”¹
has decidable equality (not proved here).

\begin{code}

normality-is-decidable : (x : ğ”¹) â†’ is-normal x + Â¬ is-normal x
normality-is-decidable L         = inl *
normality-is-decidable R         = inl *
normality-is-decidable (l L)     = inr id
normality-is-decidable (l R)     = inl *
normality-is-decidable (l (l x)) = normality-is-decidable (l x)
normality-is-decidable (l (r x)) = normality-is-decidable (r x)
normality-is-decidable (r L)     = inr id
normality-is-decidable (r R)     = inr id
normality-is-decidable (r (l x)) = normality-is-decidable (l x)
normality-is-decidable (r (r x)) = normality-is-decidable (r x)

\end{code}

We don't use the fact that normality is decidable anywhere in this
file, but, because the proof is so easy, we couldn't resist including
it.

To define the normalization function, we define versions ğ• and ğ•£ of l
and r that preserve normality.

\begin{code}

ğ• : ğ”¹ â†’ ğ”¹
ğ• L     = L
ğ• R     = C
ğ• (l x) = l (l x)
ğ• (r x) = l (r x)

ğ•£ : ğ”¹ â†’ ğ”¹
ğ•£ L         = C
ğ•£ R         = R
ğ•£ (l x)     = r (l x)
ğ•£ (r L)     = r C
ğ•£ (r R)     = R
ğ•£ (r (l x)) = r (r (l x))
ğ•£ (r (r x)) = r (r (r x))

\end{code}

The fact that the construction of ğ•£ is not the symmetric version of
that of ğ• (and that it is longer) corresponds to the fact that we made
a biased choice for the normal form of the center C, favouring l.

The preservation proofs are by case analysis without induction:

\begin{code}

ğ•-preserves-normality : (x : ğ”¹) â†’ is-normal x â†’ is-normal (ğ• x)
ğ•-preserves-normality L     i = *
ğ•-preserves-normality R     i = *
ğ•-preserves-normality (l x) i = i
ğ•-preserves-normality (r x) i = i

ğ•£-preserves-normality : (x : ğ”¹) â†’ is-normal x â†’ is-normal (ğ•£ x)
ğ•£-preserves-normality L         * = *
ğ•£-preserves-normality R         * = *
ğ•£-preserves-normality (l R)     * = *
ğ•£-preserves-normality (l (l x)) i = i
ğ•£-preserves-normality (l (r x)) i = i
ğ•£-preserves-normality (r (l x)) i = i
ğ•£-preserves-normality (r (r x)) i = i

\end{code}

The normalization function replaces l and r by their "semantic"
versions ğ• and ğ•£:

\begin{code}

normalize : ğ”¹ â†’ ğ”¹
normalize L     = L
normalize R     = R
normalize (l x) = ğ• (normalize x)
normalize (r x) = ğ•£ (normalize x)

\end{code}

By construction, the result of normalization is normal, with a direct
proof by induction:

\begin{code}

normalize-is-normal : (x : ğ”¹) â†’ is-normal (normalize x)
normalize-is-normal L     = *
normalize-is-normal R     = *
normalize-is-normal (l x) = ğ•-preserves-normality (normalize x) (normalize-is-normal x)
normalize-is-normal (r x) = ğ•£-preserves-normality (normalize x) (normalize-is-normal x)

\end{code}

We now prove that normal points are fixed points of the normalization
function. We need a simple lemma for that purpose, proved by case
analysis.

\begin{code}

ğ•£r-equation : (x : ğ”¹) â†’ is-normal (r x) â†’ ğ•£ (r x) â‰¡ r (r x)
ğ•£r-equation L     i = ğŸ˜-elim i
ğ•£r-equation R     i = ğŸ˜-elim i
ğ•£r-equation (l x) i = refl
ğ•£r-equation (r x) i = refl

\end{code}

To prove that normal points are fixed points of the normalization
function, we need to simultaneously prove two lemmas by induction:

\begin{code}

nfp-lemma-l : (x : ğ”¹) â†’ is-normal (l x) â†’ ğ• (normalize x) â‰¡ l x
nfp-lemma-r : (x : ğ”¹) â†’ is-normal (r x) â†’ ğ•£ (normalize x) â‰¡ r x

nfp-lemma-l L     i = ğŸ˜-elim i
nfp-lemma-l R     * = refl
nfp-lemma-l (l x) i = ap ğ• (nfp-lemma-l x i)
nfp-lemma-l (r x) i = ap ğ• (nfp-lemma-r x i)

nfp-lemma-r L     i = ğŸ˜-elim i
nfp-lemma-r R     i = ğŸ˜-elim i
nfp-lemma-r (l x) i = ap ğ•£ (nfp-lemma-l x i)
nfp-lemma-r (r x) i = ğ•£ (ğ•£ (normalize x)) â‰¡âŸ¨ ap ğ•£ (nfp-lemma-r x i) âŸ©
                      ğ•£ (r x)                 â‰¡âŸ¨ ğ•£r-equation x i                             âŸ©
                      r (r x)                 âˆ
\end{code}

Now the proof of the desired result is by cases (without induction),
using the above two lemmas.

\begin{code}

normals-are-fixed-points : (x : ğ”¹) â†’ is-normal x â†’ normalize x â‰¡ x
normals-are-fixed-points L     * = refl
normals-are-fixed-points R     * = refl
normals-are-fixed-points (l x) i = nfp-lemma-l x i
normals-are-fixed-points (r x) i = nfp-lemma-r x i

\end{code}

We have the following two corollaries:

\begin{code}

fixed-points-are-normal : (x : ğ”¹) â†’ normalize x â‰¡ x â†’ is-normal x
fixed-points-are-normal x p = transport is-normal p (normalize-is-normal x)

normalization-idemp : (x : ğ”¹) â†’ normalize (normalize x) â‰¡ normalize x
normalization-idemp x = normals-are-fixed-points (normalize x) (normalize-is-normal x)

\end{code}

But we actually don't need the normalization procedure to construct
the initial binary system, whose underlying type will be called ğ•„.
However, we will use some of the above machinery.

\begin{code}

ğ•„ = Î£ x ê‰ ğ”¹ , is-normal x

Left Center Right : ğ•„
Left   = L , *
Center = C , *
Right  = R , *

left right : ğ•„ â†’ ğ•„
left  (x , i) = ğ• x , ğ•-preserves-normality x i
right (x , i) = ğ•£ x , ğ•£-preserves-normality x i

ğ•„-eq-l : Left â‰¡ left Left
ğ•„-eq-l = refl

ğ•„-eq-r : Right â‰¡ right Right
ğ•„-eq-r = refl

ğ•„-eq-lr : left Right â‰¡ right Left
ğ•„-eq-lr = refl

ğ•„-eq-lm : left Right â‰¡ Center
ğ•„-eq-lm = refl

ğ•„-eq-rm : right Left â‰¡ Center
ğ•„-eq-rm = refl

\end{code}

We now use the above to show that ğ•„ is the initial binary system.

\begin{code}

binary-system-structure : ğ“¤ Ì‡ â†’ ğ“¤ Ì‡
binary-system-structure A = A Ã— A Ã— (A â†’ A) Ã— (A â†’ A)

binary-system-axioms : (A : ğ“¤ Ì‡ ) â†’ binary-system-structure A â†’ ğ“¤ Ì‡
binary-system-axioms A (a , b , f , g) = (a â‰¡ f a) Ã— (f b â‰¡ g a) Ã— (b â‰¡ g b)

BS : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
BS ğ“¤ = Î£ A ê‰ ğ“¤ Ì‡ , Î£ s ê‰ binary-system-structure A , binary-system-axioms A s

ğ“œ : BS ğ“¤â‚€
ğ“œ = (ğ•„ , (Left , Right , left , right) , (ğ•„-eq-l , ğ•„-eq-lr , ğ•„-eq-r))

open import UF-SIP
open sip

\end{code}

The notion of homomorphism of binary systems is the expected one:

\begin{code}

is-hom : (ğ“ : BS ğ“¤) (ğ“' : BS ğ“¥) â†’ (âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“' âŸ©) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
is-hom (A , (a , b , f , g) , _) (A' , (a' , b' , f' , g') , _) h =
   (h a â‰¡ a')
 Ã— (h b â‰¡ b')
 Ã— (h âˆ˜ f âˆ¼ f' âˆ˜ h)
 Ã— (h âˆ˜ g âˆ¼ g' âˆ˜ h)

\end{code}

In order to show that ğ“œ is the initial binary system, we first prove
(the expected) induction principle for its underlying type ğ•„ (with a
perhaps unexpected proof):

\begin{code}

ğ•„-induction : (P : ğ•„ â†’ ğ“¤ Ì‡ )
            â†’ P Left
            â†’ P Right
            â†’ ((x : ğ•„) â†’ P x â†’ P (left x))
            â†’ ((x : ğ•„) â†’ P x â†’ P (right x))
            â†’ (x : ğ•„) â†’ P x

ğ•„-induction P a b f g (L ,           *) = a
ğ•„-induction P a b f g (R ,           *) = b
ğ•„-induction P a b f g (l R ,         i) = f (R , *) b
ğ•„-induction P a b f g (l (l x) ,     i) = f (l x , i) (ğ•„-induction P a b f g (l x , i))
ğ•„-induction P a b f g (l (r x) ,     i) = f (r x , i) (ğ•„-induction P a b f g (r x , i))
ğ•„-induction P a b f g (r (l R) ,     i) = g (l R , *) (f (R , *) b)
ğ•„-induction P a b f g (r (l (l x)) , i) = g (l (l x) , i) (ğ•„-induction P a b f g (l (l x) , i))
ğ•„-induction P a b f g (r (l (r x)) , i) = g (l (r x) , i) (ğ•„-induction P a b f g (l (r x) , i))
ğ•„-induction P a b f g (r (r (l x)) , i) = g (r (l x) , i) (ğ•„-induction P a b f g (r (l x) , i))
ğ•„-induction P a b f g (r (r (r x)) , i) = g (r (r x) , i) (ğ•„-induction P a b f g (r (r x) , i))

\end{code}

In MLTT, induction principles come with equations. In our case they
are the expected ones. But notice that some of these equations require
(expected) binary-system-like equations in their premises. Only the
first two don't, and they hold by construction:

\begin{code}

ğ•„-induction-eq-Left : (P : ğ•„ â†’ ğ“¤ Ì‡ )
                      (a : P Left)
                      (b : P Right)
                      (f : (x : ğ•„) â†’ P x â†’ P (left x))
                      (g : (x : ğ•„) â†’ P x â†’ P (right x))
                    â†’ ğ•„-induction P a b f g Left â‰¡ a

ğ•„-induction-eq-Left P a b f g = refl

ğ•„-induction-eq-Right : (P : ğ•„ â†’ ğ“¤ Ì‡ )
                      (a : P Left)
                      (b : P Right)
                      (f : (x : ğ•„) â†’ P x â†’ P (left x))
                      (g : (x : ğ•„) â†’ P x â†’ P (right x))
                     â†’ ğ•„-induction P a b f g Right â‰¡ b

ğ•„-induction-eq-Right P a b f g = refl

\end{code}

For the next equation for the induction principle, we need the
assumption a â‰¡ f Left a:

\begin{code}

ğ•„-induction-eq-left : (P : ğ•„ â†’ ğ“¤ Ì‡ )
                      (a : P Left)
                      (b : P Right)
                      (f : (x : ğ•„) â†’ P x â†’ P (left x))
                      (g : (x : ğ•„) â†’ P x â†’ P (right x))
                    â†’ a â‰¡ f Left a
                    â†’ (x : ğ•„) â†’ ğ•„-induction P a b f g (left x) â‰¡ f x (ğ•„-induction P a b f g x)

ğ•„-induction-eq-left P a b f g p (L ,   *) = p
ğ•„-induction-eq-left P a b f g p (R ,   *) = refl
ğ•„-induction-eq-left P a b f g p (l x , i) = refl
ğ•„-induction-eq-left P a b f g p (r x , i) = refl

\end{code}

And for the last equation for the induction principle, we need the two
equations f Right b â‰¡ g Left a and b â‰¡ g Right b as assumptions:

\begin{code}

ğ•„-induction-eq-right : (P : ğ•„ â†’ ğ“¤ Ì‡ )
                      (a : P Left)
                      (b : P Right)
                      (f : (x : ğ•„) â†’ P x â†’ P (left x))
                      (g : (x : ğ•„) â†’ P x â†’ P (right x))
                    â†’ f Right b â‰¡ g Left a
                    â†’ b â‰¡ g Right b
                    â†’ (x : ğ•„) â†’ ğ•„-induction P a b f g (right x) â‰¡ g x (ğ•„-induction P a b f g x)

ğ•„-induction-eq-right P a b f g p q (L ,       *) = p
ğ•„-induction-eq-right P a b f g p q (R ,       *) = q
ğ•„-induction-eq-right P a b f g p q (l R ,     i) = refl
ğ•„-induction-eq-right P a b f g p q (l (l x) , i) = refl
ğ•„-induction-eq-right P a b f g p q (l (r x) , i) = refl
ğ•„-induction-eq-right P a b f g p q (r (l x) , i) = refl
ğ•„-induction-eq-right P a b f g p q (r (r x) , i) = refl

\end{code}

So the complete set of required equational assumptions for the
equations for the induction principle are

 (1) a â‰¡ f Left a,
 (2) b â‰¡ g Right b,
 (3) f Right b â‰¡ g Left a,

which correspond to the equations for binary systems.

As usual, the recursion principle is a particular case of the
induction principle:

\begin{code}

ğ•„-rec : (A : ğ“¤ Ì‡ ) â†’ binary-system-structure A â†’ (ğ•„ â†’ A)
ğ•„-rec A (a , b , f , g) = ğ•„-induction (Î» _ â†’ A) a b (Î» _ â†’ f) (Î» _ â†’ g)

\end{code}

And so are its equations, which amount to the fact that ğ•„-rec
constructs a homomorphism:

\begin{code}

ğ•„-rec-is-hom : (A : ğ“¤ Ì‡ ) (s : binary-system-structure A) (a : binary-system-axioms A s)
              â†’ is-hom ğ“œ (A , s , a) (ğ•„-rec A s)
ğ•„-rec-is-hom A (a , b , f , g) (eql , eqlr , eqr) = i , ii , iii , iv
 where
  i : ğ•„-rec A (a , b , f , g) Left â‰¡ a
  i = ğ•„-induction-eq-Left (Î» _ â†’ A) a b (Î» _ â†’ f) (Î» _ â†’ g)

  ii : ğ•„-rec A (a , b , f , g) Right â‰¡ b
  ii = ğ•„-induction-eq-Right (Î» _ â†’ A) a b (Î» _ â†’ f) (Î» _ â†’ g)

  iii : (x : ğ•„) â†’ ğ•„-rec A (a , b , f , g) (left x) â‰¡ f (ğ•„-rec A (a , b , f , g) x)
  iii = ğ•„-induction-eq-left (Î» _ â†’ A) a b (Î» _ â†’ f) (Î» _ â†’ g) eql

  iv : (x : ğ•„) â†’ ğ•„-rec A (a , b , f , g) (right x) â‰¡ g (ğ•„-rec A (a , b , f , g) x)
  iv = ğ•„-induction-eq-right (Î» _ â†’ A) a b (Î» _ â†’ f) (Î» _ â†’ g) eqlr eqr

\end{code}

We reformulate this as a recursion principle for the binary system ğ“œ:

\begin{code}

ğ“œ-rec : (ğ“ : BS ğ“¤)  â†’ ğ•„ â†’ âŸ¨ ğ“ âŸ©
ğ“œ-rec (A , s , _) = ğ•„-rec A s

\end{code}

Which is then automatically a homomorphism:

\begin{code}

ğ“œ-rec-is-hom : (ğ“ : BS ğ“¤)
             â†’ is-hom ğ“œ ğ“ (ğ“œ-rec ğ“)
ğ“œ-rec-is-hom (A , s , Î±) = ğ•„-rec-is-hom A s Î±

\end{code}

Some boiler plate code to name the projections follows:

\begin{code}

âŸ¨_âŸ©-Left : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ©
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-Left = a


âŸ¨_âŸ©-Right : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ©
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-Right = b


âŸ¨_âŸ©-left : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“ âŸ©
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-left = f


âŸ¨_âŸ©-right : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“ âŸ©
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-right = g


âŸ¨_âŸ©-eql : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ©-Left â‰¡ âŸ¨ ğ“ âŸ©-left âŸ¨ ğ“ âŸ©-Left
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-eql = eql


âŸ¨_âŸ©-eqr : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ©-Right â‰¡ âŸ¨ ğ“ âŸ©-right âŸ¨ ğ“ âŸ©-Right
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-eqr = eqr


âŸ¨_âŸ©-eqlr : (ğ“ : BS ğ“¤) â†’ âŸ¨ ğ“ âŸ©-left âŸ¨ ğ“ âŸ©-Right â‰¡ âŸ¨ ğ“ âŸ©-right âŸ¨ ğ“ âŸ©-Left
âŸ¨ (A , (a , b , f , g) , (eql , eqlr , eqr)) âŸ©-eqlr = eqlr


is-hom-Left : (ğ“ : BS ğ“¤) (ğ“‘ : BS ğ“¥) (h : âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“‘ âŸ©)
            â†’ is-hom ğ“ ğ“‘ h â†’ h (âŸ¨ ğ“ âŸ©-Left) â‰¡ âŸ¨ ğ“‘ âŸ©-Left
is-hom-Left ğ“ ğ“‘ h (i , ii , iii , iv) = i


is-hom-Right : (ğ“ : BS ğ“¤) (ğ“‘ : BS ğ“¥) (h : âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“‘ âŸ©)
             â†’ is-hom ğ“ ğ“‘ h â†’ h (âŸ¨ ğ“ âŸ©-Right) â‰¡ âŸ¨ ğ“‘ âŸ©-Right
is-hom-Right ğ“ ğ“‘ h (i , ii , iii , iv) = ii


is-hom-left : (ğ“ : BS ğ“¤) (ğ“‘ : BS ğ“¥) (h : âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“‘ âŸ©)
            â†’ is-hom ğ“ ğ“‘ h â†’ h âˆ˜ âŸ¨ ğ“ âŸ©-left âˆ¼ âŸ¨ ğ“‘ âŸ©-left âˆ˜ h
is-hom-left ğ“ ğ“‘ h (i , ii , iii , iv) = iii


is-hom-right : (ğ“ : BS ğ“¤) (ğ“‘ : BS ğ“¥) (h : âŸ¨ ğ“ âŸ© â†’ âŸ¨ ğ“‘ âŸ©)
             â†’ is-hom ğ“ ğ“‘ h â†’ h âˆ˜ âŸ¨ ğ“ âŸ©-right âˆ¼ âŸ¨ ğ“‘ âŸ©-right âˆ˜ h
is-hom-right ğ“ ğ“‘ h (i , ii , iii , iv) = iv

\end{code}

This is the end of the boiler plate code.

To conclude that ğ“œ is the initial binary system, it remains to prove
that there is at most one homomorphism from it to any other binary
system.

\begin{code}

ğ“œ-at-most-one-hom : (ğ“ : BS ğ“¤) (h k : ğ•„ â†’ âŸ¨ ğ“ âŸ©)
                  â†’ is-hom ğ“œ ğ“ h
                  â†’ is-hom ğ“œ ğ“ k
                  â†’ h âˆ¼ k
ğ“œ-at-most-one-hom ğ“ h k u v = ğ•„-induction (Î» x â†’ h x â‰¡ k x) i ii iii iv
 where
  i :  h Left â‰¡ k Left
  i = is-hom-Left ğ“œ ğ“ h u âˆ™ (is-hom-Left ğ“œ ğ“ k v)â»Â¹

  ii : h Right â‰¡ k Right
  ii = is-hom-Right ğ“œ ğ“ h u âˆ™ (is-hom-Right ğ“œ ğ“ k v)â»Â¹

  iii : (x : ğ•„) â†’ h x â‰¡ k x â†’ h (left x) â‰¡ k (left x)
  iii x p = h (left x)       â‰¡âŸ¨ is-hom-left ğ“œ ğ“ h u x âŸ©
            âŸ¨ ğ“ âŸ©-left (h x) â‰¡âŸ¨ ap âŸ¨ ğ“ âŸ©-left p âŸ©
            âŸ¨ ğ“ âŸ©-left (k x) â‰¡âŸ¨ (is-hom-left ğ“œ ğ“ k v x)â»Â¹ âŸ©
            k (left x)       âˆ

  iv : (x : ğ•„) â†’ h x â‰¡ k x â†’ h (right x) â‰¡ k (right x)
  iv x p =  h (right x)       â‰¡âŸ¨ is-hom-right ğ“œ ğ“ h u x âŸ©
            âŸ¨ ğ“ âŸ©-right (h x) â‰¡âŸ¨ ap âŸ¨ ğ“ âŸ©-right p âŸ©
            âŸ¨ ğ“ âŸ©-right (k x) â‰¡âŸ¨ (is-hom-right ğ“œ ğ“ k v x)â»Â¹ âŸ©
            k (right x)       âˆ

\end{code}

Notice that we didn't require binary systems to have underlying types
that are sets (in the sense of univalent mathematics) as their
underlying objects, but that the underlying type of the initial binary
system, having decidable equality, is a set. This is similar to what
happens with the unary system (â„• , zero, succ) of natural numbers.

In another file, we will define the midpoint operation

  _âŠ•_ : ğ•„ â†’ ğ•„ â†’ ğ•„

and show that it makes the initial binary system into the free
midpoint algebra over two generators (taken to be Left and Right, as
expected), where the midpoint algebra axioms are

   (idempotency)    x âŠ• x â‰¡ x,
   (commutativity)  x âŠ• y â‰¡ y âŠ• x,
   (transposition)  (u âŠ• v) âŠ• (x âŠ• y) â‰¡ (u âŠ• x) âŠ• (v âŠ• y).
