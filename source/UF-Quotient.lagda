Martin Escardo, August 2018.

Set quotients in univalent mathematics in Agda notation.

This took place during the Dagstuhl meeting "Formalization of
Mathematics in Type Theory", because Dan Grayson wanted to see how
universe levels work in Agda and I thought that this would be a nice
example to illustrate that.

We assume, in addition to Spartan Martin-LÃ¶f type theory,

 * function extensionality
   (any two pointwise equal functions are equal),

 * propositional extensionality
   (any two logically equivalent propositions are equal),

 * propositional truncation
   (any type can be universally mapped into a prop in the same
   universe),

and no resizing axioms.

The K axiom is not used (the without-K option below). We also make
sure pattern matching corresponds to Martin-LÃ¶f eliminators, using the
option exact-split. With the option safe we make sure that nothing
is postulated - any non-MLTT axiom has to be an explicit assumption
(argument to a function or a module).

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

open import SpartanMLTT
open import UF-FunExt
open import UF-PropTrunc
open import UF-Base
open import UF-Subsingletons
open import UF-Subsingletons-FunExt
open import UF-ImageAndSurjection

module UF-Quotient where

\end{code}

We define when a relation is subsingleton (or proposition) valued,
reflexive, transitive or an equivalence.

What is noteworthy, for the purpose of explaining universes in Agda to
Dan, is that X is in a universe ğ“¤, and the value of the relation is in
a universe ğ“¥, where ğ“¤ and ğ“¥ are arbitrary.

(NB. The Agda library uses the word "Level" for universes, and then
what we write "ğ“¤ Ì‡" here is written "Set ğ“¤". This is not good for
univalent mathematics, because the types in ğ“¤ Ì‡ need not be sets, and
also because it places emphasis on levels rather than universes
themselves.)

Then, for example, the function is-prop-valued defined below takes
values in the least upper bound of ğ“¤ and ğ“¥, which is denoted by ğ“¤ âŠ” ğ“¥.

We first define the type of five functions and then define them, where
_â‰ˆ_ is a variable:

\begin{code}

is-prop-valued
 reflexive
 symmetric
 transitive
 equivalence
   : {X : ğ“¤ Ì‡ } â†’ (X â†’ X â†’ ğ“¥ Ì‡ ) â†’ ğ“¤ âŠ” ğ“¥ Ì‡

is-prop-valued _â‰ˆ_ = âˆ€ x y â†’ is-prop (x â‰ˆ y)
reflexive      _â‰ˆ_ = âˆ€ x â†’ x â‰ˆ x
symmetric      _â‰ˆ_ = âˆ€ x y â†’ x â‰ˆ y â†’ y â‰ˆ x
transitive     _â‰ˆ_ = âˆ€ x y z â†’ x â‰ˆ y â†’ y â‰ˆ z â†’ x â‰ˆ z
equivalence    _â‰ˆ_ = is-prop-valued _â‰ˆ_ Ã— reflexive _â‰ˆ_ Ã— symmetric _â‰ˆ_ Ã— transitive _â‰ˆ_

\end{code}

Now, using an anonymous module with parameters (corresponding to a
section in Coq), we assume propositional truncations that stay in the
same universe, function extensionality for all universes, two
universes ğ“¤ and ğ“¥, propositional truncation for the universe ğ“¥, a type
X : ğ“¤ Ì‡, and an equivalence relation _â‰ˆ_ with values in ğ“¥ Ì‡.

\begin{code}

module _
       (pt  : propositional-truncations-exist)
       (fe  : FunExt)
       {ğ“¤ ğ“¥ : Universe}
       (pe  : propext ğ“¥)
       (X   : ğ“¤ Ì‡ )
       (_â‰ˆ_ : X â†’ X â†’ ğ“¥ Ì‡ )
       (â‰ˆp  : is-prop-valued _â‰ˆ_)
       (â‰ˆr  : reflexive _â‰ˆ_)
       (â‰ˆs  : symmetric _â‰ˆ_)
       (â‰ˆt  : transitive _â‰ˆ_)
      where

 open PropositionalTruncation pt
 open ImageAndSurjection pt

\end{code}

Now, Î© ğ“¥ is the type of subsingletons, or (univalent) propositions, or
h-propositions, or mere propositions, in the universe ğ“¥, which lives
in the next universe ğ“¥ âº.

From the relation _â‰ˆ_ : X â†’ (X â†’ ğ“¥ Ì‡ ) we define a relation
X â†’ (X â†’ Î© ğ“¥), which of course is formally a function. We then take
the quotient X/â‰ˆ to be the image of this function.

Of course, it is for constructing the image that we need propositional
truncations.

\begin{code}

 equiv-rel : X â†’ (X â†’ Î© ğ“¥)
 equiv-rel x y = x â‰ˆ y , â‰ˆp x y

\end{code}

Then the quotient lives in the least upper bound of ğ“¤ and ğ“¥ âº, where ğ“¥ âº
is the successor of the universe ğ“¥:

\begin{code}

 X/â‰ˆ : ğ“¤ âŠ” (ğ“¥ âº) Ì‡
 X/â‰ˆ = image equiv-rel

 X/â‰ˆ-is-set : is-set X/â‰ˆ
 X/â‰ˆ-is-set = subsets-of-sets-are-sets (X â†’ Î© ğ“¥) _
                (powersets-are-sets'' (fe ğ“¤ (ğ“¥ âº)) (fe ğ“¥ ğ“¥) pe)
                âˆ¥âˆ¥-is-prop

 Î· : X â†’ X/â‰ˆ
 Î· = corestriction equiv-rel

\end{code}

Then Î· is the universal solution to the problem of transforming
equivalence _â‰ˆ_ into equality _â‰¡_ (in Agda the notation for the
identity type is _â‰¡_ - we can't use _=_ because this is a reserved
symbol for definitional equality).

By construction, Î· is a surjection, of course:

\begin{code}

 Î·-surjection : is-surjection Î·
 Î·-surjection = corestriction-surjection equiv-rel

\end{code}

It is convenient to use the following induction principle for
reasoning about the image. Notice that the property we consider has
values in any universe ğ“¦ we please:

\begin{code}

 Î·-induction : âˆ€ {ğ“¦} (P : X/â‰ˆ â†’ ğ“¦ Ì‡ )
             â†’ ((x' : X/â‰ˆ) â†’ is-prop (P x'))
             â†’ ((x : X) â†’ P(Î· x))
             â†’ (x' : X/â‰ˆ) â†’ P x'
 Î·-induction = surjection-induction Î· Î·-surjection

\end{code}

The first part of the universal property of Î· says that equivalent
points are mapped to equal points:

\begin{code}

 Î·-equiv-equal : {x y : X} â†’ x â‰ˆ y â†’ Î· x â‰¡ Î· y
 Î·-equiv-equal {x} {y} e = to-Î£-â‰¡ (dfunext (fe ğ“¤ (ğ“¥ âº))
                                      (Î» z â†’ to-Î£-â‰¡ (pe (â‰ˆp x z) (â‰ˆp y z) (â‰ˆt y x z (â‰ˆs x y e)) (â‰ˆt x y z e) ,
                                                     being-prop-is-prop (fe ğ“¥ ğ“¥) _ _)) ,
                                   âˆ¥âˆ¥-is-prop _ _)

\end{code}

We also need the fact that Î· reflects equality into equivalence:

\begin{code}

 Î·-equal-equiv : {x y : X} â†’ Î· x â‰¡ Î· y â†’ x â‰ˆ y
 Î·-equal-equiv {x} {y} p = equiv-rel-reflect (ap prâ‚ p)
  where
   equiv-rel-reflect : equiv-rel x â‰¡ equiv-rel y â†’ x â‰ˆ y
   equiv-rel-reflect q = b (â‰ˆr y)
    where
     a : (y â‰ˆ y) â‰¡ (x â‰ˆ y)
     a = ap (Î» - â†’ prâ‚(- y)) (q â»Â¹)
     b : (y â‰ˆ y) â†’ (x â‰ˆ y)
     b = Idtofun a

\end{code}

We are now ready to formulate and prove the universal property of the
quotient. What is noteworthy here, regarding universes, is that the
universal property says that we can eliminate into any set A of any
universe ğ“¦.

                   Î·
              X ------> X/â‰ˆ
               \       .
                \     .
               f \   . f'
                  \ .
                   v
                   A

\begin{code}

 universal-property : âˆ€ {ğ“¦} (A : ğ“¦ Ì‡ )
                    â†’ is-set A
                    â†’ (f : X â†’ A)
                    â†’ ({x x' : X} â†’ x â‰ˆ x' â†’ f x â‰¡ f x')
                    â†’ âˆƒ! f' ê‰( X/â‰ˆ â†’ A), f' âˆ˜ Î· â‰¡ f
 universal-property {ğ“¦} A iss f pr = ic
  where
   Ï† : (x' : X/â‰ˆ) â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X ,  (Î· x â‰¡ x') Ã— (f x â‰¡ a))
   Ï† = Î·-induction _ Î³ induction-step
     where
      induction-step : (y : X) â†’ is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X ,  (Î· x â‰¡ Î· y) Ã— (f x â‰¡ a))
      induction-step x (a , d) (b , e) = to-Î£-â‰¡ (p , âˆ¥âˆ¥-is-prop _ _)
       where
        h : (Î£ x' ê‰ X , (Î· x' â‰¡ Î· x) Ã— (f x' â‰¡ a))
          â†’ (Î£ y' ê‰ X , (Î· y' â‰¡ Î· x) Ã— (f y' â‰¡ b))
          â†’ a â‰¡ b
        h (x' , r , s) (y' , t , u) = s â»Â¹ âˆ™ pr (Î·-equal-equiv (r âˆ™ t â»Â¹)) âˆ™ u

        p : a â‰¡ b
        p = âˆ¥âˆ¥-rec iss (Î» Ïƒ â†’ âˆ¥âˆ¥-rec iss (h Ïƒ) e) d

      Î³ : (x' : X/â‰ˆ) â†’ is-prop (is-prop (Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x â‰¡ x') Ã— (f x â‰¡ a)))
      Î³ x' = being-prop-is-prop (fe (ğ“¤ âŠ” (ğ“¥ âº) âŠ” ğ“¦) (ğ“¤ âŠ” (ğ“¥ âº) âŠ” ğ“¦))

   k : (x' : X/â‰ˆ) â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x â‰¡ x') Ã— (f x â‰¡ a)
   k = Î·-induction _ Ï† induction-step
    where
     induction-step : (y : X) â†’ Î£ a ê‰ A , âˆƒ x ê‰ X , (Î· x â‰¡ Î· y) Ã— (f x â‰¡ a)
     induction-step x = f x , âˆ£ x , refl , refl âˆ£

   f' : X/â‰ˆ â†’ A
   f' x' = prâ‚(k x')

   r : f' âˆ˜ Î· â‰¡ f
   r = dfunext (fe ğ“¤ ğ“¦) h
    where
     g : (y : X) â†’ âˆƒ x ê‰ X , (Î· x â‰¡ Î· y) Ã— (f x â‰¡ f' (Î· y))
     g y = prâ‚‚(k(Î· y))

     j : (y : X) â†’ (Î£ x ê‰ X , (Î· x â‰¡ Î· y) Ã— (f x â‰¡ f' (Î· y))) â†’ f'(Î· y) â‰¡ f y
     j y (x , p , q) = q â»Â¹ âˆ™ pr (Î·-equal-equiv p)

     h : (y : X) â†’ f'(Î· y) â‰¡ f y
     h y = âˆ¥âˆ¥-rec iss (j y) (g y)

   c : (Ïƒ : Î£ f'' ê‰ (X/â‰ˆ â†’ A), f'' âˆ˜ Î· â‰¡ f) â†’ (f' , r) â‰¡ Ïƒ
   c (f'' , s) = to-Î£-â‰¡ (t , v)
    where
     w : âˆ€ x â†’ f'(Î· x) â‰¡ f''(Î· x)
     w = happly (r âˆ™ s â»Â¹)

     t : f' â‰¡ f''
     t = dfunext (fe (ğ“¤ âŠ” ğ“¥ âº) ğ“¦) (Î·-induction _ (Î» _ â†’ iss) w)

     u : f'' âˆ˜ Î· â‰¡ f
     u = transport (Î» - â†’ - âˆ˜ Î· â‰¡ f) t r

     v : u â‰¡ s
     v = Î -is-set (fe ğ“¤ ğ“¦) (Î» _ â†’ iss) u s

   ic : âˆƒ! f' ê‰ (X/â‰ˆ â†’ A), f' âˆ˜ Î· â‰¡ f
   ic = (f' , r) , c

\end{code}
